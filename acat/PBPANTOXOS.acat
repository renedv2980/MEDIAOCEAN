*          DATA SET PBPANTOXOS AT LEVEL 005 AS OF 05/01/02                      
*CATALP PANTOXER                                                                
         PRINT GEN                                                              
         SPACE 1                                                                
PANTOXER CSECT                                                                  
         NBASE 0,**NTOX**,=V(REGSAVE),RA,R9                                     
         GOTO1 RDCTCRD,PARAP,P-1,=C'BL01'                                       
RDCTCRD  GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
         CLC   C(2),=C'/*'                                                      
         BNE   CTLLINE                                                          
         MVC   P(26),=C'** CONTROL LINE MISSING **'                             
         GOTO1 =V(PRINT),PARAP                                                  
         B     CLOSEPR             EOJ                                          
         SPACE 1                                                                
SETBKNME MVC   BOOKNAME(0),0(R4)                                                
         SPACE 1                                                                
CTLLINE  LA    R4,C                                                             
         TRT   0(11,R4),TERMTRT    BLANK OR ,                                   
         BZ    BADBKNM                                                          
         CLI   0(R1),C' '                                                       
         BE    BADBKNM                                                          
         LA    R2,0(R1)                                                         
         SR    R2,R4               L(BOOKNAME)                                  
         BNP   BADBKNM                                                          
         BCTR  R2,0                                                             
         EX    R2,SETBKNME                                                      
         LA    R4,1(R1)            BUMP PAST ,                                  
         CLC   0(6,R4),=C'START,'                                               
         BNE   SLNNO          NOT 1ST LINE - GET LINE NO                        
         LA    R4,6(R4)       SET C POINTER FOR ELNTEST                         
         MVI   SENOS+4,C'1'                                                     
         ZAP   PANSEQS,=P'1'                                                    
         B     ELNTEST                                                          
         SPACE 1                                                                
SLNNO    LA    R3,SENOS+4     UNITS DIGIT                                       
         BAS   R7,GETLNNO                                                       
         PACK  PANSEQS,SENOS(5)                                                 
         SPACE 1                                                                
ELNTEST  CLC   0(3,R4),=C'END'                                                  
         BNE   ELNNO                                                            
         MVC   SENOS+5(5),=5C'9'                                                
         ZAP   PANSEQE,=P'99999'                                                
         LA    R4,4(R4)       BUMP PAST END                                     
         B     MORECTLN                                                         
         SPACE 1                                                                
ELNNO    LA    R3,SENOS+9     UNITS DIGIT                                       
         BAS   R7,GETLNNO                                                       
         PACK  PANSEQE,SENOS+5(5)                                               
         CP    PANSEQS,PANSEQE                                                  
         BNH   MORECTLN                                                         
         MVC   P+24(23),=C'LINE NO. RANGE INVERTED'                             
         TITLE 'RETRIEVE FROM PANDD1 TO XEROX DISKETTE'                         
BADLNNO  MVC   P(22),=C'** BAD CONTROL LINE **'                                 
         MVC   P+24(29),=C'TOO LONG OR NOT NUM. LINE NO.'                       
         MVC   P+54(6),0(R4)                                                    
         GOTO1 =V(PRINT),PARAP                                                  
         MVC   P(80),C                                                          
         GOTO1 =V(PRINT),PARAP                                                  
         B     CLOSEPR             EOJ                                          
         SPACE 1                                                                
BADBKNM  MVC   P(24),=C'** INVALID BOOKNAME ** /'                               
         MVC   P+24(11),0(R4)                                                   
         MVI   P+35,C'/'                                                        
         B     BADLNNO+18                                                       
         SPACE 1                                                                
*                   GET AND VALIDATE LINE NUMBER                                
         SPACE 1                                                                
GETLNNO  TRT   0(6,R4),TERMTRT     BLANK OR ,                                   
         BZ    BADLNNO                                                          
         LA    R5,1(R1)            TERM + 1                                     
LNNOLP   BCTR  R1,0                                                             
         CR    R1,R4                                                            
         BL    LNNOGOT                                                          
         TM    0(R1),X'F0'                                                      
         BNO   BADLNNO                                                          
         MVC   0(1,R3),0(R1)                                                    
         BCT   R3,LNNOLP                                                        
         SPACE 1                                                                
LNNOGOT  LR    R4,R5                                                            
         BR    R7                                                               
         TITLE 'RETRIEVE FROM PANDD1 TO XEROX DISKETTE'                         
MORECTLN DS    0H                                                               
*&&US    START                                                                  
         CLI   0(R4),C'6'                                                       
         BNE   PARMTEST                                                         
         MVI   WPTOXWP+9,X'9D'     860 IX                                       
         MVI   WPTOXWP+12,X'94'    860 FF                                       
         MVI   WPTOXWP+47,X'8B'    860 HLT                                      
         MVI   WPTOXWP+56,X'9C'    860 RIX                                      
         MVI   WPTOXWP+58,X'95'    860 RPE                                      
         MVI   WPTOXWP+65,X'A0'    860 PSPC                                     
         MVI   WPTOXWP+74,X'60'    860 CENT SIGN                                
         MVI   WPTOXWP+96,X'2D'    860 RHY                                      
         MVI   WPTOXWP+106,X'BD'   860 1/2                                      
         MVI   WPTOXWP+202,X'A1'   860 SHYPH                                    
         MVI   WPTOXWP+224,X'BB'   860 1/4                                      
         MVI   XER6FLAG,C'Y'                                                    
         LA    R4,1(R4)                                                         
*&&US    END                                                                    
PARMTEST CLI   0(R4),C' '     NO EXTRA PARAMETER ?                              
         BE    TABRTEST         YES - SKIP REST                                 
         CLI   0(R4),C'A'       NO - RETRIEVE AS IS ?                           
         BNE   *+12                    NO                                       
         MVI   NAKED,C'Y'              YES                                      
         B     TABRTEST                                                         
         CLI   0(R4),C'W'          TAB WIDE-LINE PAGE                           
         BNE   *+12                    NO                                       
         MVI   TABWIDE,C'Y'                                                     
         B     TABRTEST                                                         
         CLI   0(R4),C'U'          UNCAP ?                                      
         BNE   *+12                    NO                                       
         MVI   UNCAP,C'Y'            YES                                        
         B     TABRTEST                                                         
         CLI   0(R4),C'R'          REPAGE - DEMOTE EJECT TO FF                  
         BNE   TABRTEST              NO                                         
         MVI   REPAGE,C'Y'                                                      
         TITLE 'RETRIEVE FROM PANDD1 TO XEROX DISKETTE'                         
TABRTEST TRT   0(20,R4),INTERTRT   EQUAL SIGN                                   
         BZ    BOOKTEST                                                         
         SPACE 1                                                                
*                  PICK UP TAB SETTINGS                                         
         SPACE 1                                                                
         LA    R8,0                TABRACK PTR                                  
         XC    TABRACK,TABRACK                                                  
         XC    FMTTAB,FMTTAB                                                    
NXTTABIN LA    R3,1(R1)            START OF TAB FIELD                           
         TRT   0(4,R3),TERMTRT     , OR BLANK                                   
         BZ    BADTBNO                                                          
         LA    R2,0(R1)                                                         
         BCTR  R2,0                UNITS DIGIT                                  
         LA    R4,TABFIELD+2                                                    
TABNOLP  TM    0(R2),X'F0'         NUMERIC                                      
         BNO   BADTBNO                                                          
         MVC   0(1,R4),0(R2)                                                    
         BCTR  R4,0                                                             
         BCTR  R2,0                                                             
         CR    R2,R3                                                            
         BNL   TABNOLP                                                          
         SPACE 1                                                                
         PACK  DUB,TABFIELD(3)                                                  
         CVB   R4,DUB                                                           
         CH    R4,TABRACK-2(R8)    ASCENDING                                    
         BNH   BADTBNO                                                          
         SPACE 1                                                                
         CH    R4,=H'144'                                                       
         BNL   BADTBNO                                                          
         SPACE 1                                                                
         STH   R4,TABRACK(R8)                                                   
         BAS   RE,DOFMTTAB                                                      
         SPACE 1                                                                
         CLI   0(R1),C' '          MORE TAB SETTINGS                            
         BE    WTABTEST              NO                                         
         LA    R8,2(R8)                                                         
         CH    R8,=H'36'           18 TABS                                      
         BL    NXTTABIN                                                         
         EJECT                                                                  
WTABTEST CLI   TABWIDE,C'Y'                                                     
         BNE   BOOKTEST                                                         
         SPACE 1                                                                
         MVC   WTABRACK,TABRACK    SAVE WIDE-LINE TABRACK                       
         B     BOOKTEST                                                         
         SPACE 1                                                                
BADTBNO  MVC   P(21),=C'** BAD TAB SETTING **'                                  
         MVC   P+22(4),0(R3)                                                    
         B     BADLNNO+18                                                       
         SPACE 1                                                                
BOOKTEST DS    0H                                                               
*&&US    START                                                                  
         CLI   XER6FLAG,C'Y'       *                                            
         BNE   *+8                 *                                            
         BAS   R8,CHTABS6                                                       
*&&US    END                                                                    
         GOTO1 =V(PANIC),PARA,=C'READ',=C'DIRECTORY',BOOKNAME,B                 
         TM    PARA+8,X'10'                                                     
         BZ    READSET                                                          
         SPACE 1                                                                
         MVC   P(2),=C'**'                                                      
         MVC   P+3(10),BOOKNAME                                                 
         MVC   P+13(16),=C' BOOK MISSING **'                                    
         GOTO1 =V(PRINT),PARAP                                                  
         MVC   P(80),C                                                          
         GOTO1 =V(PRINT),PARAP                                                  
         B     CLOSEPAN+4                                                       
         SPACE 1                                                                
READSET  GOTO1 READSET1,PARAC,P,=C'PE00'                                        
READSET1 GOTO1 READ,PARA,=C'READ',=C'PAN',BOOKNAME,C                            
         EJECT                                                                  
CHTABRAC CLC   C+1(2),=C'00'       XERTOPAN NULL TABRACK                        
         BE    READ                                                             
         CLC   C+1(2),C+3                                                       
         BE    READ                                                             
         MVI   TABINSRT,C'N'         NO - XERSTORE'D TEXT                       
         LA    R8,0                TABRACK PTR                                  
         XC    TABRACK,TABRACK                                                  
         XC    FMTTAB,FMTTAB                                                    
         LA    R1,C+1              PTR TO CHAR-HEX TAB LINE                     
CHTABLP  PACK  WORK(2),0(2,R1)                                                  
         LH    R4,WORK                                                          
         SRA   R4,4                                                             
         TM    1(R1),X'F0'                                                      
         BO    *+8                                                              
         AH    R4,=H'9'                                                         
         STH   R4,TABRACK(8)                                                    
         BAS   RE,DOFMTTAB                                                      
         LA    R1,2(R1)            NEXT INPUT PAIR                              
         CLC   0(2,R1),=H'0'                                                    
         BE    CHTABEND              NO                                         
         CLI   0(R1),X'2B'         ANY MORE                                     
         BE    CHTABEND              NO                                         
         LA    R8,2(R8)                                                         
         CH    R8,=H'36'           18 TABS                                      
         BL    CHTABLP                                                          
         SPACE 1                                                                
CHTABEND DS    0H                                                               
*&&US    START                                                                  
         CLI   XER6FLAG,C'Y'       *                                            
         BNE   *+8                 *                                            
         BAS   R8,CHTABS6                                                       
*&&US    END                                                                    
         TM    STOPSRCH,X'F0'                                                   
         BNO   READ                                                             
         BAS   R6,PUTFMT                                                        
         B     READ                                                             
         EJECT                                                                  
PUTFMT   DS    0H                                                               
*&&US    START                                                                  
         CLI   XER6FLAG,C'Y'                                                    
         BNE   PUTFMT5                                                          
         SPACE 1                                                                
         TR    P(2),WPTOXWP                                                     
         MVC   P+2(12),FORMAT61                                                 
         LA    R3,P+14                                                          
         LA    R4,TABS860                                                       
         ZIC   R1,LTABS860                                                      
         EX    R1,MOVTBMSK                                                      
         LA    R3,1(R1,R3)                                                      
         LA    R4,FORMAT62                                                      
         LA    R1,P+79                                                          
*                                                                               
*  7/6/83 FIX                                                                   
*                                                                               
         LA    R5,43               L(RORMAT62) - 1                              
*        LA    R5,45               L(FORMAT62) - 1                              
         SR    R1,R3               ROOM ON LNE 1 - 1                            
         CR    R5,R1                                                            
         BNH   FMTOUT                YES                                        
         SPACE 1                                                                
         EX    R1,MOVTBMSK           NO  - NEEDS 2ND LNE                        
         LA    R4,1(R1,R4)                                                      
         LA    R3,P                                                             
         SR    R5,R1                                                            
         BCTR  R5,0                L(REMAINDER) - 1                             
         BAS   R8,PUNCHOUT+6                                                    
         SP    LINE,=P'1'                                                       
         SPACE 1                                                                
FMTOUT   EX    R5,MOVTBMSK                                                      
         BAS   R8,PUNCHOUT+6                                                    
         SP    LINE,=P'1'                                                       
         MVI   P,X'00'                                                          
         BR    R6                                                               
         SPACE 1                                                                
MOVTBMSK MVC   0(0,R3),0(R4)                                                    
*&&US    END                                                                    
         SPACE 1                                                                
PUTFMT5  MVC   P+2(77),FORMAT                                                   
         BAS   R8,PUNCHOUT                                                      
         SP    LINE,=P'1'                                                       
         MVI   P,X'00'                                                          
         BR    R6                                                               
         EJECT                                                                  
*&&US    START                                                                  
*                   BUILD 860 TABS MASK FROM TABRACK                            
         SPACE 1                                                                
CHTABS6  LA    R0,18               TABRACK SIZE IN H'S                          
         LA    R1,3                MASK BASE + 3 FOR SIT-TAB BYTE               
         XC    TABS860+1(47),TABS860+1                                          
         LA    R2,TABRACK                                                       
         LA    R7,TABS860+1                                                     
         MVI   TABS860,X'41'       REG TAB AT 2                                 
         SPACE 1                                                                
TABLP860 LH    R5,0(R2)            GET TABRACK VALUE                            
         AH    R5,LEFTMARN                                                      
         SR    R5,R1                                                            
         SR    R4,R4                                                            
         LA    R3,3                                                             
         DR    R4,R3                                                            
         LTR   R5,R5               QUOTIENT = BLANK BYTES IN MASK               
BLNKLP6  BZ    NOBLANKS                                                         
         SPACE 1                                                                
         LA    R3,16               MAX BLANK-BYTE COUNT                         
         CR    R5,R3                                                            
         BL    *+22                                                             
         MVI   0(R7),X'3F'         SET MAX BLANK COUNT                          
         LA    R7,1(R7)            BUMP MASK PTR                                
         LA    R1,48(R1)           BUMP MASK BASE                               
         SR    R5,R3                                                            
         B     BLNKLP6             TRY AGAIN                                    
         SPACE 1                                                                
         BCTR  R5,0                SET L - 1 (BYTES)                            
         STC   R5,0(R7)                                                         
         OI    0(R7),X'30'         MARK BLANK-BYTES COUNT                       
         LA    R7,1(R7)                                                         
         LA    R5,1(R5)            RESTORE L (BYTES)                            
         MH    R5,=H'3'                                                         
         AR    R1,R5               BUMP MASK BASE                               
         SPACE 1                                                                
NOBLANKS LA    R3,1                                                             
         CR    R4,R3               REMAINDER = TAB DISP FROM MASK BASE          
         BL    BIT10                                                            
         BE    BIT04                                                            
         OI    0(R7),X'41'                                                      
         B     *+16                                                             
         SPACE 1                                                                
BIT10    OI    0(R7),X'50'                                                      
         B     *+8                                                              
         SPACE 1                                                                
BIT04    OI    0(R7),X'44'                                                      
         EJECT                                                                  
         BCT   R0,*+8                                                           
         B     WRAPUP              TABRACK COMPLETED                            
         LH    R3,2(R2)            NEXT TABRACK VALUE                           
         BZ    WRAPUP              NULL VALUE                                   
         AH    R3,LEFTMARN                                                      
         SR    R3,R1                                                            
         CH    R3,=H'2'                                                         
         BNH   TABLP860            IN SAME MASK BYTE                            
         SPACE 1                                                                
         LA    R7,1(R7)              NO                                         
         LA    R1,3(R1)                                                         
         LA    R2,2(R2)            DO NEXT TAB VALUE                            
         B     TABLP860                                                         
         SPACE 1                                                                
WRAPUP   MVC   1(4,R7),=X'3F3F3F3F'                                             
         LA    R7,4(R7)                                                         
         LA    R3,TABS860                                                       
         SR    R7,R3                                                            
         STC   R7,LTABS860         L - 1                                        
         SPACE 1                                                                
         BR    R8                                                               
*&&US    END                                                                    
         EJECT                                                                  
*                   TAB RACK CHANGE ROUTINE                                     
         SPACE 1                                                                
TABSHIFT LA    R8,0                INDEX REG                                    
         XC    FMTTAB,FMTTAB                                                    
         LH    R4,TABRACK(R8)                                                   
TABSHFLP BAS   RE,DOFMTTAB                                                      
         LA    R8,2(R8)                                                         
         LH    R4,TABRACK(R8)                                                   
         LTR   R4,R4                                                            
         BZR   R7                                                               
         CH    R8,=H'36'           18 TABS                                      
         BL    TABSHFLP                                                         
*&&US    START                                                                  
         CLI   XER6FLAG,C'Y'                                                    
         BNER  R7                                                               
         BAS   R8,CHTABS6                                                       
*&&US    END                                                                    
         BR    R7                                                               
         SPACE 1                                                                
DOFMTTAB AH    R4,LEFTMARN         TAB SHIFT FACTOR                             
         STC   R4,WORK                                                          
         NI    WORK,X'0F'          GET UNIT'S DIGIT                             
         CLI   WORK,X'09'                                                       
         BNH   *+8                                                              
         SH    R4,=H'9'                                                         
         SLA   R4,4                                                             
         STH   R4,WORK+2                                                        
         LA    R2,FMTTAB(R8)                                                    
         UNPK  0(2,R2),WORK+2(2)                                                
         CLI   WORK,X'09'                                                       
         BH    *+10                                                             
         OI    1(R2),X'F0'                                                      
         BR    RE                                                               
         SPACE 1                                                                
         OI    1(R2),X'C0'                                                      
         BR    RE                                                               
         EJECT                                                                  
READ     GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    FINISHED            END OF BOOK                                  
         SPACE 1                                                                
         NOP   ALLREADS                                                         
READ1SW  EQU   *-3                                                              
         SPACE 1                                                                
         OI    READ1SW,X'F0'       BYPASS 1ST READ TESTS                        
         CLC   C(16),=C'*           DATA '                                      
         BE    READ           SKIPS '*          DATA SET ... '                  
         SPACE 1                                                                
ALLREADS AP    PANSEQ,=P'1'                                                     
         CLI   C,X'2B'          *  FMT FROM XERSTORE OR XERTOPAN                
         BE    CHTABRAC         *  SKIP FMT CHECK - FIX 8/18/83                 
         NOP   NEXTLINE                                                         
STOPSRCH EQU   *-3                                                              
         SPACE 1                                                                
         CP    PANSEQ,PANSEQS                                                   
         BL    READ                FIND START LINE                              
         SPACE 1                                                                
         OI    STOPSRCH,X'F0'        FOUND - STOP SEARCH                        
         MVC   FMTBOOKN,BOOKNAME   ISSUE XEROX FORMAT BLOCK                     
         MVC   BOOKNAM6,BOOKNAME                                                
         TR    BOOKNAM6,WPTOXWP                                                 
         XC    P(80),P                                                          
         BAS   R6,PUTFMT                                                        
         EJECT                                                                  
*                   HANDLE LINES TO BE RETRIEVED                                
         SPACE 1                                                                
NEXTLINE CP    PANSEQ,PANSEQE                                                   
         BNH   NXTCD2                                                           
CDEND    SP    PANSEQ,=P'1'                                                     
         B     FINISHED                                                         
         SPACE 1                                                                
NXTCD2   CLI   DISKEND,X'FF'                                                    
         BE    CDEND                                                            
         SPACE 1                                                                
         CP    LINE,MAXLINE                                                     
         BL    DOLINE                                                           
         BAS   R7,ENDOLDPG                                                      
         B     NXTCD2                                                           
         SPACE 1                                                                
DOLINE   CLC   C(2),=C'*-'                                                      
         BNE   *+8                                                              
         MVI   C,C'-'                                                           
         SPACE 1                                                                
         CLI   NAKED,C'Y'                                                       
         BE    PUTASIS                                                          
         SPACE 1                                                                
         CLI   PAGEMODE,C'Y'                                                    
         BE    MOREPAGE                                                         
         SPACE 1                                                                
         CLC   C(10),=C'PAGE=START'                                             
         BE    PAGESTRT                                                         
         CLC   C(8),=C'PAGE=END'                                                
         BE    READ                                                             
         SPACE 1                                                                
         CLC   C(8),=C'CHAPNUM='                                                
         BNE   *+14                                                             
         MVC   P+1(9),C                                                         
         B     CTLNOUT                                                          
         SPACE 1                                                                
         CLC   C(4),=C'BIG='                                                    
         BNE   *+14                                                             
         MVC   P+1(12),C                                                        
         B     CTLNOUT                                                          
         SPACE 1                                                                
         CLC   C(17),=C'       ++INCLUDE '                                      
         BNE   COLHDTST                                                         
         EJECT                                                                  
INCLOUT  MVC   P+1(4),=X'094E3822' SUPR-+-SUBS-SUSC                             
         MVC   P+5(10),C+17                                                     
         LA    R3,P+14                                                          
         CLI   0(R3),C' '                                                       
         BNE   *+12                                                             
         MVI   0(R3),X'00'                                                      
         BCT   R3,*-12                                                          
         MVI   1(R3),X'23'         EUSC                                         
         CLC   C+30(20),SPACES                                                  
         BE    CTLNEND                                                          
         MVC   2(3,R3),SPACES                                                   
         LA    R3,5(R3)                                                         
         LA    R4,C+30                                                          
         CLI   0(R4),C' '                                                       
         BNE   *+12                                                             
         LA    R4,1(R4)                                                         
         B     *-12                                                             
         MVC   0(35,R3),0(R4)                                                   
         B     CTLNEND                                                          
         EJECT                                                                  
COLHDTST CLC   C(2),=X'6E6E'       COL HEADS                                    
         BNE   SCANTEST                                                         
         SPACE 1                                                                
         MVC   C(2),=X'0938'         YES - SPS,SBS                              
         CLC   C+2(78),SPACES      COLHEADS RESET                               
         BNE   COLHLIVE              NO                                         
         SPACE 1                                                                
         MVI   COLHEADS,C' '                                                    
         BAS   R6,UNKEEP                                                        
         BAS   R6,PUTFMT                                                        
         MVC   P+1(2),C                                                         
         MVC   P+3(2),=X'0909'                                                  
         SP    LINE,=P'1'                                                       
         BAS   R8,PUNCHOUT                                                      
         MVI   P,X'06'                                                          
         B     READ                                                             
         SPACE 1                                                                
COLHLIVE MVI   COLHEADS,C'Y'                                                    
         BAS   R6,KEEP                                                          
         BAS   R6,PUTFMT                                                        
         CLC   C+78(2),SPACES      FULL LINE                                    
         BNE   COLCD2                YES                                        
         MVC   P+1(78),C             NO                                         
         MVI   P+79,X'06'                                                       
         AP    LINE,=P'1'                                                       
         B     CTLNEND                                                          
         SPACE 1                                                                
COLCD2   MVC   P+1(79),C                                                        
         BAS   R8,PUNCHOUT                                                      
         MVC   P(1),C+79                                                        
         MVI   P+1,X'06'                                                        
         B     CTLNEND                                                          
         SPACE 1                                                                
UNKEEP   XC    KEEPFLD,KEEPFLD                                                  
         NI    SITTABSW,X'FE'                                                   
         NI    TABS860,X'F7'                                                    
         BR    R6                                                               
         SPACE 1                                                                
KEEP     MVC   KEEPFLD,=C'04'                                                   
         OI    SITTABSW,X'01'                                                   
         OI    TABS860,X'08'                                                    
         BR    R6                                                               
         SPACE 1                                                                
SCANTEST CLC   C(8),SPACES                                                      
         BE    SETSCAN                                                          
         B     XERTEST                                                          
         EJECT                                                                  
SETSCAN  LA    R2,C+8                                                           
         LA    R3,3                                                             
         CLI   0(R2),C' '                                                       
         BNE   *+16                                                             
         LA    R2,1(R2)                                                         
         BCT   R3,*-12                                                          
         B     XERTEST                                                          
         SPACE 1                                                                
         CLC   0(6,R2),=C'SPACE '                                               
         BE    SPCRTNE                                                          
         CLC   0(4,R2),=C'HEAD'                                                 
         BE    HEADRTNE                                                         
         CLC   0(7,R2),=C'INDEX '''                                             
         BE    INDEXRTN                                                         
         CLC   0(7,R2),=C'FIGURE '                                              
         BE    FIGRTNE                                                          
         CLC   0(6,R2),=C'EJECT '                                               
         BE    EJECTRTN                                                         
         CLC   0(6,R2),=C'PRINT '                                               
         BE    READ                                                             
         CLC   0(7,R2),=C'TITLE '''                                             
         BE    TTLRTNE                                                          
         CLC   0(7,R2),=C'SUPER '''                                             
         BE    SUPRTNE                                                          
         CLC   0(7,R2),=C'RFOOT '''                                             
         BE    INDEXRTN                                                         
         CLC   0(6,R2),=C'FRONT='                                               
         BE    FRONTOUT                                                         
         B     XERTEST                                                          
         SPACE 1                                                                
EJECTRTN CP    LINE,=P'0'                                                       
         BE    READ                                                             
         BAS   R7,ENDOLDPG                                                      
         B     READ                                                             
         SPACE 1                                                                
INDEXRTN LA    R3,P+1                                                           
         CLI   0(R2),C'R'          RFOOT                                        
         BE    *+32                  YES                                        
         LA    R4,2                  NO  - INDEX                                
         CLI   XER6FLAG,C'Y'                                                    
         BNE   *+8                                                              
         LA    R4,2(R4)                                                         
         MVI   0(R3),X'09'                                                      
         LA    R3,1(R3)                                                         
         BCT   R4,*-8                                                           
         SP    LINE,=P'1'                                                       
         MVC   0(1,R3),SUPR                                                     
         MVC   1(1,R3),0(R2)       I(NDEX) OR R(FOOT)                           
         MVC   2(1,R3),SUBS                                                     
         BAS   R7,SETHEAD                                                       
         CLI   0(R2),C'R'          RFOOT                                        
         BNE   CTLNOUT                                                          
         EJECT                                                                  
         MVI   P,X'06'                                                          
         BAS   R8,PUNCHOUT                                                      
         BAS   R7,ENDCURPG                                                      
         B     READ                                                             
         SPACE 1                                                                
*                   HANDLE SPACE N LINE                                         
         SPACE 1                                                                
SPCRTNE  CLI   6(R2),C'1'                                                       
         BL    PUTITOUT                                                         
         CLI   6(R2),C'9'                                                       
         BH    PUTITOUT                                                         
         MVI   CAPFLAG,C'Y'                                                     
         MVI   SCRNFLAG,C' '                                                    
         LA    R3,P+1                                                           
         CLC   10(3,R2),SPACES                                                  
         BE    SPCRTEND                                                         
         SPACE 1                                                                
         MVC   0(4,R3),=X'09D33822'                                             
         MVC   4(1,R3),6(R2)                                                    
         MVC   5(2,R3),=X'E723'                                                 
         LA    R3,7(R3)                                                         
         SPACE 1                                                                
SPCRTEND PACK  6(1,R2),6(1,R2)                                                  
         SP    LINE,=P'1'                                                       
         AP    LINE,6(1,R2)                                                     
         ZIC   R6,6(R2)                                                         
         SRA   R6,4                GET NUMBER OF LINES                          
         BCTR  R6,0                                                             
         LTR   R6,R6                                                            
         BNP   CTLNOUT                                                          
         MVI   0(R3),X'06'                                                      
         LA    R3,1(R3)                                                         
         BCT   R6,*-8                                                           
         B     CTLNOUT                                                          
         SPACE 1                                                                
*                   HANDLE FRONT= LINE                                          
         SPACE 1                                                                
FRONTOUT MVC   P+1(4),=X'09C33822' SUPR-C-SUBS-SUSC                             
         MVC   P+5(5),6(R2)        START OR END                                 
         CLI   P+5,C'S'            START                                        
         BNE   *+12                                                             
         MVI   P+10,X'23'          EUSC                                         
         B     *+8                                                              
         MVI   P+8,X'23'           END                                          
         MVC   P+12(35),C+39                                                    
         B     CTLNOUT                                                          
         SPACE 1                                                                
*                   HANDLE FIGURE LINE                                          
         SPACE 1                                                                
FIGRTNE  LA    R2,1(R2)            ALIGN AS IF FOR HEADN                        
         MVI   4(R2),C'F'          PUT 'F' INTO LEVEL NO. POS'N                 
         B     TRYHEAD                                                          
         EJECT                                                                  
*                   HANDLE HEADN (AND FIGURE) LINE                              
         SPACE 1                                                                
HEADRTNE TM    4(R2),X'F0'         NUMERIC LEVEL NO.                            
         BNO   PUTITOUT              NO                                         
         SPACE 1                                                                
TRYHEAD  BAS   R6,UNKEEP           RESET 'KEEP' SPECIAL TAB                     
         LA    R3,P+1                                                           
         CP    LINE,=P'0'                                                       
         BNH   DOHEADT                                                          
         ZAP   WORK(2),MAXLINE                                                  
         SP    WORK(2),LINE                                                     
         CP    WORK(2),=P'7'                                                    
         BNL   DOHEAD                                                           
         CLI   4(R2),C'F'          FIGURE LINE                                  
         BNE   NEXTPAGE              NO                                         
         MVI   LASTONPG,C'Y'                                                    
         CP    WORK(2),=P'3'         YES - ROOM ON PG FOR FIGURE                
         BNL   DOHEAD                        YES                                
         MVI   LASTONPG,C'N'                 NO                                 
         SPACE 1                                                                
NEXTPAGE BAS   R7,ENDOLDPG                                                      
         MVI   COLHEADS,C' '                                                    
         B     DOHEADT+4                                                        
         SPACE 1                                                                
DOHEAD   BAS   R7,ENDKEEP                                                       
         MVC   0(2,R3),=X'0606'    SKIP 2 LINES                                 
         LA    R3,2(R3)                                                         
         AP    LINE,=P'2'                                                       
         B     *+8                                                              
         SPACE 1                                                                
DOHEADT  BAS   R7,ENDKEEP                                                       
         MVC   0(1,R3),SUPR                                                     
         MVC   1(1,R3),4(R2)                                                    
         MVC   2(1,R3),SUBS                                                     
         BAS   R7,SETHEAD                                                       
         CLI   LASTONPG,C'Y'                                                    
         BE    FIGONBOT                                                         
         MVC   0(2,R3),=X'0606'                                                 
         AP    LINE,=P'2'                                                       
         B     CTLNOUT                                                          
         SPACE 1                                                                
FIGONBOT MVI   LASTONPG,C'N'                                                    
         MVI   P,X'06'                                                          
         BAS   R8,PUNCHOUT                                                      
         BAS   R7,ENDOLDPG                                                      
         B     READ                                                             
         SPACE 1                                                                
ENDKEEP  CLI   COLHEADS,C'Y'       IS KEEP ACTIVE BEFORE HEAD                   
         BNER  R7                    NO                                         
         MVI   COLHEADS,C' '                                                    
         BAS   R6,PUTFMT                                                        
         BR    R7                                                               
         EJECT                                                                  
*                   HANDLE SUPER LINE                                           
         SPACE 1                                                                
SUPRTNE  BAS   R6,UNKEEP                                                        
         MVI   COLHEADS,C' '                                                    
         BAS   R7,ENDCURPG                                                      
         MVC   0(3,R3),=X'09E238'  SUP - CAP S - SUBSCR                         
         BAS   R7,SETHEAD                                                       
         MVI   0(R3),X'06'                                                      
         MVC   1(8,R3),0(R3)                                                    
         AP    LINE,=P'9'                                                       
         B     CTLNOUT                                                          
         SPACE 1                                                                
*                   HANDLE TITLE LINE                                           
         SPACE 1                                                                
TTLRTNE  BAS   R6,UNKEEP                                                        
         MVI   COLHEADS,C' '                                                    
         BAS   R7,ENDCURPG                                                      
         MVC   0(3,R3),=X'09E338'  SUP - CAP T - SUBS                           
         BAS   R7,SETHEAD                                                       
         MVI   0(R3),X'06'                                                      
         MVC   1(3,R3),0(R3)                                                    
         AP    LINE,=P'4'                                                       
         B     CTLNOUT                                                          
         SPACE 1                                                                
SETHEAD  LA    R3,3(R3)                                                         
         MVI   0(R3),X'22'         SUSC                                         
         LA    R3,1(R3)                                                         
         MVI   CAPFLAG,C'Y'                                                     
         LA    R4,64(R2)           END OF MAX STRING IN C                       
         LA    R5,57               MAX STRING LENGTH                            
         SPACE 1                                                                
         CLI   0(R4),C' '                                                       
         BNE   *+14                                                             
         BCTR  R4,0                                                             
         BCT   R5,*-10                                                          
         B     PUTITOUT                                                         
         SPACE 1                                                                
         CLI   0(R4),X'7D'         APOSTROPHE                                   
         BE    *+12                  YES                                        
         LA    R5,1(R5)                                                         
         LA    R4,1(R4)              NO - INCLUDE IT                            
         MVI   0(R4),X'23'         EUS                                          
         EX    R5,MOVEHEAD                                                      
         LA    R3,1(R5,R3)         INCREMENT P POINTER                          
         BR    R7                                                               
         SPACE 1                                                                
MOVEHEAD MVC   0(0,R3),7(R2)                                                    
         EJECT                                                                  
CTLNOUT  CLI   P,X'0C'             FF                                           
         BE    CTLNEND                                                          
         CLI   P,X'3A'             RPE                                          
         BE    CTLNEND                                                          
         CLI   P,X'00'             NULL                                         
         BE    CTLNEND                                                          
         MVI   P,X'06'                                                          
CTLNEND  BAS   R8,PUNCHOUT                                                      
         MVI   P,X'06'                                                          
         B     READ                                                             
         SPACE 1                                                                
*                   HANDLE TEXT LINE                                            
         SPACE 1                                                                
XERTEST  CLI   TABINSRT,C'N'       XERSTORE TEXT                                
         BNE   CAPCHECK              NO                                         
         SPACE 1                                                                
         TRT   C,LINEEND                                                        
         BNZ   ONELINE                                                          
         LA    R4,D                                                             
         ST    R4,PARA+12                                                       
         GOTO1 =V(PANIC),PARA                                                   
         AP    PANSEQ,=P'1'                                                     
         LA    R4,C                                                             
         ST    R4,PARA+12                                                       
         TRT   D,LINEEND                                                        
         BNZ   ONELINE+4                                                        
         DC    H'0'                                                             
         SPACE 1                                                                
ONELINE  LA    R4,C                                                             
         LR    R5,R1                                                            
         SR    R5,R4                                                            
         STC   R5,LINELEN                                                       
         SPACE 1                                                                
CAPCHECK CLI   UNCAP,C'Y'                                                       
         BNE   PUTITOUT                                                         
         SPACE 1                                                                
         CLC   C,SPACES                                                         
         BNE   *+12                                                             
         MVI   CAPFLAG,C'Y'                                                     
         B     PUTASIS                                                          
         SPACE 1                                                                
         TRT   C,LCTEST       ANY SMALL LETTERS ?                               
         BZ    UNCAPPER         NO                                              
         MVI   UNCAP,C'N'       YES - DON'T TEST LATER LINES                    
         EJECT                                                                  
PUTITOUT CLI   C+9,C'*'            SCREEN                                       
         BNE   SPECTST             START                                        
         CLC   C+10(61),C+9         OR                                          
         BNE   SPECTST              END                                         
         SPACE 1                                                                
         CLI   SCRNFLAG,C'Y'       END                                          
         BNE   STRTSCRN              NO                                         
         BAS   R6,UNKEEP             YES                                        
         MVC   SCRNFLAG(3),=C' YY' SCRNFLAG,PROTECT,SCRNEND                     
         B     PUTASIS                                                          
         SPACE 1                                                                
STRTSCRN BAS   R6,KEEP                                                          
         BAS   R6,PUTFMT                                                        
         MVI   SCRNFLAG,C'Y'                                                    
         MVI   PROTECT,C'Y'                                                     
         B     PUTASIS                                                          
         SPACE 1                                                                
SPECTST  CLI   TABINSRT,C'N'       XERSTORE TEXT                                
         BE    PUTASIS+8             YES                                        
         TRT   C,SPECTEST                                                       
         BZ    NOTUSCOR                                                         
         CLI   0(R1),X'6D'         USCORE                                       
         BE    DOUSCORE              YES                                        
NOTUSCOR LA    R6,C                START ADDR                                   
         LA    R1,C+79             END ADDR                                     
         BAS   RE,TRYTABS                                                       
         B     PUTASIS1                                                         
         SPACE 1                                                                
TABSNOW  TRT   0(0,R4),TABCHECK                                                 
         SPACE 1                                                                
TRYTABS  NTR1                                                                   
         LR    R7,R1               END-OF-LINE PTR                              
         LR    R4,R6               SAVE START-OF-LINE PTR                       
         BCTR  R6,0                                                             
         LR    R5,R7                                                            
         SR    R5,R4                                                            
         EX    R5,TABSNOW                                                       
         BNZ   TABEXIT                                                          
         LA    R8,17               TABRACK COUNT - 1                            
         LA    R1,TABRACK                                                       
         CLI   0(R7),C' '          TRIM TRAILING BLANKS                         
         BNE   NXTTBRLP                                                         
         BCTR  R7,0                                                             
         CR    R4,R7                                                            
         BH    TABEXIT             BLANK LINE                                   
         B     *-16                                                             
         EJECT                                                                  
NEXTTAB  LA    R1,2(R1)            NEXT TAB                                     
         CLC   0(2,R1),=H'0'       NULL                                         
         BE    TABEXIT                                                          
         BCT   R8,NXTTBRLP                                                      
         B     TABEXIT             NO MORE TABS                                 
         SPACE 1                                                                
NXTTBRLP LA    R2,0(R4)            RESET TO LINE START                          
         BCTR  R2,0                                                             
         AH    R2,0(R1)                                                         
         CR    R2,R7               WITHIN END LIMIT                             
         BNL   TABEXIT               NO                                         
         LR    R5,R6               LEFT END OF THIS FIELD                       
         LR    R6,R2               LEFT END OF NEXT FIELD                       
         BCTR  R6,0                                                             
         LA    R3,3                                                             
FIRST3BL BCTR  R2,0                                                             
         CLI   0(R2),C' '                                                       
         BNE   NEXTTAB             NOT 3 SPACES                                 
         CR    R2,R5                                                            
         BNH   NEXTTAB                                                          
         BCT   R3,FIRST3BL                                                      
         SPACE 1                                                                
         LA    R3,2                                                             
         BCTR  R2,0                LOOK FOR MORE                                
BLNKFIND CLI   0(R2),C' '                                                       
         BNE   SETTAB              NO MORE                                      
         CR    R2,R5                                                            
         BNH   SETTAB                                                           
         LA    R3,1(R3)            MORE - COUNT 'EM                             
         BCT   R2,BLNKFIND                                                      
         SPACE 1                                                                
SETTAB   LA    R2,1(R2)                                                         
         EX    R3,NULLEM                                                        
         MVI   0(R2),X'39'         XEROX 'LOWER' TAB                            
         CLI   KEEPFLD,X'00'                                                    
         BNE   NEXTTAB                                                          
         CH    R8,=H'17'           1ST TAB OPPORTUNITY                          
         BE    *+8                                                              
         MVI   PROTECT,C'Y'                                                     
         B     NEXTTAB                                                          
         SPACE 1                                                                
NULLEM   XC    0(0,R2),0(R2)                                                    
         SPACE 1                                                                
TABEXIT  XIT1                                                                   
         EJECT                                                                  
DOUSCORE MVC   U,C                 SAVE USCORE LINE                             
         LA    R4,0(R1)                                                         
         TRT   C,SEPUSC                                                         
         BNZ   NOTUSCOR                                                         
         GOTO1 =V(PANIC),PARA      GET TEXT LINE                                
         TM    PARA+8,X'80'                                                     
         BO    FINISHED            END OF BOOK                                  
         AP    PANSEQ,=P'1'                                                     
         LA    R6,C                START ADDR                                   
         LA    R1,C+79             END ADDR                                     
         BAS   RE,TRYTABS                                                       
         MVC   T,C                 SAVE TEXT LINE                               
         MVC   D,SPACES            O'FLOW AREA                                  
         LA    R8,C                DEST PTR                                     
         SR    R4,R8               DISP TO 1ST USCORE                           
         LA    R3,U                U LINE PTR                                   
         LA    R5,T                SOURCE PTR                                   
         LA    R6,U+80             U LINE LIMIT                                 
         LA    R7,C+80             DEST LINE LIMIT                              
         B     SETPTRS                                                          
MOVEREST BCTR  R4,0                                                             
         EX    R4,MOVETTOC           YES - MOVE TEXT                            
         LA    R4,1(R4)                                                         
SETPTRS  AR    R8,R4                                                            
         AR    R3,R4                                                            
         AR    R5,R4                                                            
         CR    R3,R6               MORE IN USCORE LINE                          
         BNL   ENDUSCOR              NO                                         
         SPACE 1                                                                
         LA    R4,1(R3)                                                         
         MVI   0(R8),X'22'         MY XWP BUS CHAR                              
         LA    R8,1(R8)                                                         
ULENLP   CLI   0(R4),X'6D'         USCORE                                       
         BNE   SETUSCOR              NO - SET THOSE FOUND                       
         CR    R4,R6                 YES - LIMIT REACHED                        
         BNL   SETUSCOR                      YES                                
         LA    R4,1(R4)                      NO                                 
         B     ULENLP                                                           
         EJECT                                                                  
SETUSCOR BCTR  R4,0                                                             
         SR    R4,R3               ULEN - 1                                     
         EX    R4,MOVETTOC                                                      
         LA    R4,1(R4)                                                         
         AR    R8,R4                                                            
         AR    R3,R4                                                            
         AR    R5,R4                                                            
         MVI   0(R8),X'23'         USCORE WP CHAR                               
         LA    R8,1(R8)                                                         
         LR    R4,R6                                                            
         SR    R4,R3               MORE IN U                                    
         BNP   ENDUSCOR              NO                                         
         BCTR  R4,0                  YES                                        
         EX    R4,MOREUSC          MORE USCORES                                 
         BZ    MOVEREST+2            NO                                         
         LA    R4,0(R1)              YES                                        
         SR    R4,R3                                                            
         B     MOVEREST                                                         
         SPACE 1                                                                
ENDUSCOR BCTR  R8,0                                                             
         CLI   0(R8),C' '                                                       
         BNE   *+8                                                              
         BCT   R8,*-8                                                           
         CR    R8,R7                                                            
         BL    *+8                                                              
         MVI   OFLOWFLG,C'Y'                                                    
         B     PUTASIS1                                                         
         SPACE 1                                                                
MOVETTOC MVC   0(0,R8),0(R5)                                                    
         SPACE 1                                                                
MOREUSC  TRT   0(0,R3),SPECTEST                                                 
         EJECT                                                                  
PUTASIS  CLI   TABINSRT,C'N'       XERSTORE TEXT                                
         BNE   PUTASIS1              NO                                         
         SPACE 1                                                                
         LA    R4,C                                                             
         ZIC   R5,LINELEN                                                       
         CH    R5,=H'79'                                                        
         BNH   *+8                                                              
         MVI   OFLOWFLG,C'Y'                                                    
         LA    R5,0(R5,R4)                                                      
         B     LINEOUT                                                          
         SPACE 1                                                                
PUTASIS1 LA    R4,C                                                             
         CLI   OFLOWFLG,C'Y'       USCORED LINE IN C AND D                      
         BNE   *+10                  NO                                         
         LR    R5,R8                 YES                                        
         B     NORMLINE                                                         
         SPACE 1                                                                
         CLI   C+79,C' '           FULL LINE                                    
         BE    *+12                  NO                                         
         LA    R5,C+79               YES                                        
         B     NORMLINE                                                         
         SPACE 1                                                                
         LA    R5,C+78                                                          
TRAILLP  CLI   0(R5),C' '          TRAILING BLANK                               
         BNE   NORMLINE              NO                                         
         BCTR  R5,0                  YES                                        
         CR    R5,R4                                                            
         BH    TRAILLP                                                          
         SPACE 1                                                                
NORMLINE CLI   0(R5),X'DF'         VYDEC RCR                                    
         BNE   HYPHEND               NO                                         
         SPACE 1                                                                
         MVI   PROTECT,C'Y'          YES                                        
         MVI   0(R5),C' '                                                       
         BCTR  R5,0                                                             
         B     LINEOUT                                                          
         SPACE 1                                                                
HYPHEND  CLI   0(R5),C'-'          TRAILING HYPHEN                              
         BNE   LINEOUT               NO                                         
         BCTR  R5,0                                                             
         CLI   0(R5),C'-'          PRECEDING HYPHEN                             
         BE    *+8                   YES                                        
         MVI   1(R5),X'CA'         SHY INSTEAD OF REQ HYPHEN                    
         LA    R5,1(R5)                                                         
         EJECT                                                                  
LINEOUT  TRT   C,SPECTEST                                                       
         BZ    LINEOUT1                                                         
         CLI   0(R1),X'6D'                                                      
         BE    LINEOUT1                                                         
         BCTR  R1,0                                                             
         MVC   0(3,R1),=X'099638'  SUPR - LC O - SUBS                           
LINEOUT1 CLI   OFLOWFLG,C'Y'                                                    
         BE    LINESOUT                                                         
         CLI   C+79,C' '                                                        
         BNE   LINESOUT                                                         
         SR    R5,R4                                                            
         EX    R5,FIRSTLIN                                                      
         B     ENDLINE                                                          
         SPACE 1                                                                
FIRSTLIN MVC   P+1(0),C                                                         
         SPACE 1                                                                
SECONDLN MVC   P(0),C+79                                                        
         SPACE 1                                                                
LINESOUT MVI   OFLOWFLG,C' '                                                    
         MVC   P+1(79),C                                                        
         BAS   R8,PUNCHOUT                                                      
         SR    R5,R4                                                            
         SH    R5,=H'79'                                                        
         EX    R5,SECONDLN                                                      
         SP    LINE,=P'1'                                                       
         SPACE 1                                                                
ENDLINE  BAS   R8,PUNCHOUT                                                      
         CLI   TABINSRT,C'N'       XERSTORE TEXT                                
         BNE   *+12                  NO                                         
         SPACE 1                                                                
         MVI   P,X'00'               YES - DON'T SET EXTRA LINE END             
         B     NOPROT                                                           
         SPACE 1                                                                
         CLI   PROTECT,C'Y'                                                     
         BNE   READ                                                             
         MVI   P,X'06'             RCR                                          
NOPROT   MVI   PROTECT,C' '                                                     
         CLI   SCRNEND,C'Y'                                                     
         BNE   READ                                                             
         MVI   SCRNEND,C' '                                                     
         BAS   R6,PUTFMT                                                        
         B     READ                                                             
         EJECT                                                                  
ENDOLDPG CLI   REPAGE,C'Y'                                                      
         BNE   ENDCURPG                                                         
         MVI   P+1,X'0C'             FF                                         
         B     *+8                                                              
ENDCURPG MVI   P+1,X'3A'             RPE                                        
         MVI   P,X'06'             RCR                                          
         BAS   R6,PUTFMT                                                        
         LA    R3,P+1                                                           
         ZAP   LINE,=P'0'                                                       
         CLI   COLHEADS,C'Y'                                                    
         BNE   *+24                                                             
         MVC   P+1(14),=X'5E5E402283969340888581842306'                         
         BAS   R8,PUNCHOUT                                                      
         MVI   P,X'06'                                                          
         ZAP   LINE,=P'2'                                                       
         AP    PAGE,=P'1'                                                       
         CP    PAGE,MAXPAGE                                                     
         BLR   R7                                                               
         MVI   DISKEND,X'FF'                                                    
         BR    R7                                                               
         SPACE 3                                                                
PUNCHOUT TR    P(80),WPTOXWP                                                    
         GOTO1 =V(CARDS),PARAC                                                  
         XC    P+1(79),P+1                                                      
         AP    LINE,=P'1'                                                       
         MVI   P,X'1E'                                                          
         BR    R8                                                               
         EJECT                                                                  
*                   HANDLE REPORT PAGE                                          
         SPACE 2                                                                
PAGESTRT MVC   LEFTMAR,=C'0285'                                                 
         MVC   LEFTMARN,=H'1'                                                   
         MVC   MARGINS6,=X'40424245'                                            
         MVC   BOTMAR,=X'4060'                                                  
         MVC   LASTLN,=C'3C'                                                    
         BAS   R6,KEEP                                                          
         MVC   KEEPFLD,=C'01'                                                   
         CLI   TABWIDE,C'Y'                                                     
         BNE   *+22                                                             
         XC    TABRACK,WTABRACK                                                 
         XC    WTABRACK,TABRACK                                                 
         XC    TABRACK,WTABRACK                                                 
         BAS   R7,TABSHIFT                                                      
         MVC   COLHEADS(4),SPACES                                               
PAGEAGIN MVI   PAGEMODE,C'Y'                                                    
         BAS   R7,ENDCURPG                                                      
         L     R2,=A(PAGEBLOC)                                                  
         LH    R3,PAGELIM                                                       
PAGECLR  MVC   0(132,R2),SPACES                                                 
         LA    R2,132(R2)                                                       
         BCT   R3,PAGECLR                                                       
         MVC   P+1(69),C                                                        
         LA    R4,P+69                                                          
         CLI   0(R4),C' '                                                       
         BNE   *+12                                                             
         MVI   0(R4),X'00'                                                      
         B     *-12                                                             
         CLI   0(R4),X'06'                                                      
         BE    *+8                                                              
         MVI   1(R4),X'06'                                                      
         BAS   R8,PUNCHOUT                                                      
         B     READ                                                             
         SPACE 1                                                                
MOREPAGE CLC   C(8),=C'PAGE=END'                                                
         BE    PAGEEND                                                          
         CLC   C(10),=C'PAGE=START'                                             
         BNE   DOPGCARD         NO                                              
         OI    FINISW,X'F0'     YES - SET SWITCH                                
         BAS   R7,PAGEEND             END PRIOR REPORT PAGE                     
         NI    FINISW,X'0F'           RESET SWITCH                              
         B     PAGEAGIN               START NEW REPORT PAGE                     
         EJECT                                                                  
DOPGCARD MVC   WORK(2),=C'00'      LINE NUMBER                                  
         MVZ   WORK(2),C+1                                                      
         CLC   WORK(2),=C'00'                                                   
         BNE   PUTASIS                                                          
         PACK  DUB,C+1(2)                                                       
         CVB   R3,DUB                                                           
         CH    R3,PAGELIM                                                       
         BH    PUTASIS                                                          
         LTR   R3,R3                                                            
         BZ    PUTASIS                                                          
         BCTR  R3,0                                                             
         MH    R3,=H'132'                                                       
         L     R2,=A(PAGEBLOC)                                                  
         AR    R2,R3                                                            
         CLI   C,C'R'                                                           
         BNE   *+8                                                              
         LA    R2,66(R2)                                                        
         SPACE 1                                                                
         MVC   0(66,R2),C+3                                                     
         B     READ                                                             
         SPACE 1                                                                
PAGEEND  L     R3,=A(PAGEBLOC)                                                  
         LH    R4,PAGELIM                                                       
         LR    R5,R4                                                            
         BCTR  R5,0                                                             
         MH    R5,=H'132'                                                       
         AR    R5,R3               LAST LINE IN PAGEBLOC                        
         SPACE 1                                                                
TRIMBOT  CLC   0(132,R5),SPACES    TRAILING BLANK LINE                          
         BNE   *+16                  NO                                         
         SH    R5,=H'132'            YES                                        
         BCT   R4,TRIMBOT                                                       
         B     PENDLP2             NO LINES TO ISSUE                            
         EJECT                                                                  
STARTOUT CLC   0(132,R3),SPACES                                                 
         BNE   *+16                                                             
         MVI   P,X'06'                                                          
         BAS   R8,PUNCHOUT                                                      
         B     PGLNDONE                                                         
         SPACE 1                                                                
         MVC   P,0(R3)                                                          
         LA    R5,P+131            END-OF-LINE PTR                              
         LR    R6,R5               SAVE END-OF-LINE PTR                         
         LA    R2,P                START-OF-LINE PTR                            
FINDEND  CLI   0(R5),C' '          TRAILING BLANK                               
         BNE   TEST5F                NO                                         
MORETEST BCTR  R5,0                  YES                                        
         CR    R5,R2                                                            
         BH    FINDEND                                                          
         B     ENDFOUND                                                         
         SPACE 1                                                                
NULLOUT  MVC   3(0,R5),2(R5)                                                    
         SPACE 1                                                                
TEST5F   CLI   0(R5),X'5F'                                                      
         BE    MORETEST                                                         
         CLI   0(R5),X'DF'                                                      
         BE    MORETEST                                                         
         SPACE 1                                                                
ENDFOUND CLI   0(R5),X'06'                                                      
         BNE   *+6                                                              
         BCTR  R5,0                                                             
         MVC   1(2,R5),=X'0600'    RCR,NULL                                     
         SR    R6,R5                                                            
         BNP   *+8                                                              
         EX    R6,NULLOUT                                                       
         CLI   TABWIDE,C'Y'                                                     
         BNE   PGLINOUT                                                         
         SPACE 1                                                                
         LA    R6,P                START ADDR                                   
         LA    R1,1(R5)            END ADDR                                     
         BAS   RE,TRYTABS                                                       
         MVI   PROTECT,C' '                                                     
         SPACE 1                                                                
PGLINOUT SR    R5,R2                                                            
         BAS   R8,PUNCHOUT                                                      
         CH    R5,=H'79'                                                        
         BNH   PGLNDONE                                                         
         SPACE 1                                                                
         MVC   P(54),P+80                                                       
         BAS   R8,PUNCHOUT                                                      
PGLNDONE LA    R3,132(R3)          NEXT LINE IN PAGEBLOC                        
         BCT   R4,STARTOUT                                                      
         EJECT                                                                  
PENDLP2  MVI   P,X'06'             RCR                                          
         MVC   P+1(8),=C'PAGE=END'                                              
         BAS   R8,PUNCHOUT                                                      
         MVI   PAGEMODE,C'N'                                                    
         NOPR  R7                  FOR EOF FLUSH                                
FINISW   EQU   *-1                                                              
         GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    FINISHED            END OF BOOK                                  
         AP    PANSEQ,=P'1'                                                     
         CLC   C(10),=C'PAGE=START'                                             
         BE    PAGEAGIN                                                         
         SPACE 1                                                                
         BAS   R6,UNKEEP                                                        
         MVC   MARGINS6,=X'40454154'                                            
         MVC   BOTMAR,=X'4070'                                                  
         MVC   LEFTMAR,=C'0554'                                                 
         MVC   LEFTMARN,=H'4'                                                   
         MVC   LASTLN,=C'3A'                                                    
         CLI   TABWIDE,C'Y'                                                     
         BNE   *+22                                                             
         XC    TABRACK,WTABRACK                                                 
         XC    WTABRACK,TABRACK                                                 
         XC    TABRACK,WTABRACK                                                 
         BAS   R7,TABSHIFT                                                      
         BAS   R7,ENDCURPG                                                      
         B     NEXTLINE                                                         
         EJECT                                                                  
*                  UNCAP, THEN RESTORE CAPS WHERE NEEDED                        
         SPACE 2                                                                
UNCAPPER LA    R3,C                START OF LINE                                
         LA    R4,79               LENGTH - 1                                   
         MVI   TESTCHAR,C' '                                                    
         CLI   SCRNFLAG,C'Y'                                                    
         BE    PUTITOUT                                                         
         CLI   CAPFLAG,C'Y'        NEW PARA OR SENTENCE                         
         BNE   UNCAPEM               NO                                         
         SPACE 1                                                                
         MVI   CAPFLAG,C' '                                                     
         CLI   0(R3),C' '            YES - BLANK                                
         BNE   *+16                          NO                                 
         LA    R3,1(R3)                      YES                                
         BCT   R4,*-12                                                          
         B     PUTITOUT                      OOPS - BLANK LINE                  
         SPACE 1                                                                
         LA    R3,1(R3)            BUMP PAST 1ST CHARACTER                      
         BCTR  R4,0                                                             
         SPACE 1                                                                
UNCAPEM  EX    R4,LOWERCSE         UNCAP ALL (BUT 1ST) CHARACTERS               
         C     R3,=A(C)            WERE ANY CHARS SKIPPED                       
         BE    *+10                  NO                                         
         BCTR  R3,0                  YES - BACK UP FOR . * ' TESTING            
         LA    R4,1(R4)                                                         
         SR    R1,R1                                                            
         SPACE 1                                                                
TRYAGAIN EX    R4,FINDSTOP         LOOK FOR . * '                               
         BZ    PUTITOUT              NOT FOUND                                  
         STC   R2,WORK             FOUND - MOVE IT FOR CHECKING                 
         CLI   WORK,C'*'           ASTERISK                                     
         BE    ASTERTST              YES                                        
         SPACE 1                                                                
         CLI   WORK,X'7D'          APOSTROPHE                                   
         BNE   TRYSTOP               NO                                         
         CLC   1(2,R1),=X'A240'      YES - SMALL-S BLANK                        
         BNE   SETFORLP                      NO                                 
         SH    R4,=H'3'                      YES                                
         BNP   PUTITOUT                                                         
         LA    R3,3(R1)                                                         
         B     TRYAGAIN                                                         
         EJECT                                                                  
TRYSTOP  LA    R4,C+79                                                          
         SR    R4,R1               REMAINING LINE LENGTH                        
         BNP   STOPDONE              ZERO                                       
         BCTR  R4,0                  NOT ZERO                                   
         EX    R4,ALLBLANK                                                      
         BNE   TWOSPACE                                                         
STOPDONE MVI   CAPFLAG,C'Y'                                                     
         B     PUTITOUT                                                         
         SPACE 1                                                                
TWOSPACE CLC   1(2,R1),SPACES                                                   
         BNE   ONESPACE                                                         
         OI    3(R1),X'40'         RECAP NEXT CHAR                              
         LA    R5,3                                                             
         B     AFTERSTP                                                         
         SPACE 1                                                                
ONESPACE CLI   1(R1),C' '                                                       
         BNE   NOSPACES                                                         
         OI    2(R1),X'40'         RECAP NEXT CHAR                              
         LA    R5,2                                                             
         B     AFTERSTP                                                         
         SPACE 1                                                                
RECAPPER LR    R5,R1                                                            
         BCTR  R5,0                                                             
         SR    R5,R3               L(RECAP FIELD) - 1                           
         BM    NOSPACES                                                         
         EX    R5,RECAPEM                                                       
AFTERSTP SR    R4,R5                                                            
NOSPACES BCTR  R4,0                                                             
         LTR   R4,R4                                                            
         BNP   PUTITOUT                                                         
         LA    R3,1(R1)                                                         
         B     TRYAGAIN                                                         
         EJECT                                                                  
SETFORLP LA    R3,1(R1)            BUMP PAST '                                  
         LA    R4,C+79                                                          
         SR    R4,R3               LENGTH OF REMAINDER ON LINE                  
         BNP   PUTITOUT                                                         
         EX    R4,FINDSTOP                                                      
         BZ    PUTITOUT                                                         
         B     RECAPPER                                                         
         SPACE 1                                                                
LOWERCSE TR    0(0,R3),TRANTBLE    CHANGE TO SMALL LETTERS                      
         SPACE 1                                                                
FINDSTOP TRT   0(0,R3),RECAPTST    LOOK FOR . * '                               
         SPACE 1                                                                
ALLBLANK CLC   1(0,R1),SPACES                                                   
         SPACE 1                                                                
RECAPEM  OC    0(0,R3),SPACES                                                   
         SPACE 1                                                                
ASTERTST CLC   0(3,R1),=C'***'     1ST OR LAST SCREEN LINE                      
         BE    PUTITOUT                                                         
         LA    R5,C+9              CC 1-10                                      
         CR    R1,R5                                                            
         BH    PUTITOUT              NO                                         
         LR    R6,R1                 YES - SAVE START                           
         LA    R3,C+70                                                          
         LA    R4,9                CC 71-80                                     
         EX    R4,FINDSTOP                                                      
         BZ    PUTITOUT                                                         
         STC   R2,WORK                                                          
         CLI   WORK,C'*'                                                        
         BNE   PUTITOUT                                                         
         LR    R3,R6               SAVED START                                  
         SR    R1,R6                                                            
         BCTR  R1,0                                                             
         EX    R1,RECAPEM                                                       
         B     PUTITOUT                                                         
         EJECT                                                                  
*                  CLOSE OUT THIS RUN                                           
         SPACE 1                                                                
FINISHED CLI   PAGEMODE,C'Y'                                                    
         BNE   *+12                                                             
         OI    FINISW,X'F0'                                                     
         BAS   R7,PAGEEND                                                       
         MVI   P,X'3A'             RPE                                          
         MVC   P+1(12),=C'/***********'                                         
         MVI   P+13,X'06'                                                       
         LA    R3,P+14                                                          
         MVC   0(9,R3),=C'++UPDATE '                                            
         MVC   9(10,R3),BOOKNAME                                                
         LA    R3,18(R3)                                                        
FINLP    CLI   0(R3),C' '                                                       
         BNE   *+8                                                              
         BCT   R3,FINLP                                                         
         SPACE 1                                                                
         MVI   1(R3),C','                                                       
         TM    PARA+8,X'80'                                                     
         BZ    NOTALL                                                           
         SPACE 1                                                                
         CP    PANSEQS,=P'1'                                                    
         BNE   NOTALL                                                           
         SPACE 1                                                                
         MVC   2(5,R3),=C'0,ALL'                                                
         MVI   7(R3),X'06'                                                      
         B     CLOSEPAN                                                         
         SPACE 1                                                                
NOTALL   MVC   2(3,R3),B+10        LEVEL NUMBER                                 
         LA    R3,5(R3)                                                         
         MVI   0(R3),X'06'                                                      
         MVC   1(4,R3),=C'++D '                                                 
         MVC   5(5,R3),SENOS       START LINE NO.                               
         MVI   10(R3),C','                                                      
         UNPK  11(5,R3),PANSEQ     LAST LINE NO.                                
         OI    15(R3),X'F0'                                                     
         MVI   16(R3),X'06'                                                     
         SPACE 3                                                                
CLOSEPAN BAS   R8,PUNCHOUT                                                      
         GOTO1 =V(PANIC),PARA,=C'CLOSE'                                         
CLOSEPR  XBASE                                                                  
         EJECT                                                                  
*              WORK SPACE                                                       
         SPACE 3                                                                
PARA     DS    2D                                                               
PARAC    DS    2D                                                               
PARAP    DS    2D                                                               
DUB      DS    D                                                                
WORK     DS    CL32                                                             
*                                                                               
*  7/6/83 FIX                                                                   
*                                                                               
P        DC    132X'00'  OUTPUT AREA                                            
*P       DS    CL132     OUTPUT AREA                                            
         DS    H                                                                
         DS    H                   EXTRA H/W PUT IN RCY 4/14/81                 
SPACES   DC    CL132' '  PRESET TO SPACES                                       
LINE     DC    PL2'0'    CUR PAGE LINE COUNT                                    
MAXLINE  DC    PL2'54'   MAX PAGE LINE COUNT                                    
BOOKNAME DC    CL10' '                                                          
B        DS    CL80                                                             
C        DS    CL80                                                             
D        DS    CL80                                                             
T        DS    CL80                                                             
U        DS    CL80                                                             
         DC    H'0'                                                             
TABRACK  DS    0CL36                                                            
         DC    X'0006000A0019001E00230028004F'                                  
         DC    22X'00'                                                          
WTABRACK DS    CL36                                                             
LEFTMARN DC    H'4'                                                             
PAGELIM  DC    H'54'                                                            
SENOS    DC    10C'0'                                                           
PANSEQ   DC    PL3'0'                                                           
PANSEQS  DC    PL3'0'                                                           
PANSEQE  DC    PL3'0'                                                           
PAGE     DC    PL2'1'                                                           
MAXPAGE  DC    PL2'58'                                                          
TABFIELD DC    C'000'                                                           
LASTONPG DC    C' '                                                             
PAGEMODE DC    C'N'                                                             
REPAGE   DC    C' '                                                             
UNCAP    DC    C'N'                                                             
NAKED    DC    C'N'                                                             
CAPFLAG  DC    C'N'                                                             
TESTCHAR DC    C' '                                                             
SUPR     DC    X'09'          SUPERSCRIPT                                       
SUBS     DC    X'38'          SUBSCRIPT                                         
DISKEND  DS    CL1                                                              
OFLOWFLG DC    C' '                                                             
COLHEADS DC    C' '                                                             
SCRNFLAG DC    C' '                                                             
PROTECT  DC    C' '                                                             
SCRNEND  DC    C' '                                                             
TABINSRT DC    C'Y'                                                             
TABWIDE  DC    C' '                                                             
LINELEN  DS    X                                                                
         EJECT                                                                  
FORMAT   DS    0CL77                                                            
         DC    X'2BF0'             FMT                                          
LEFTMAR  DC    C'0554'       MARGINS 5 - 84                                     
         DC    X'1EF1'                                                          
FMTTAB   DS    0CL36                                                            
         DC    C'0A0E1D22272C53'   TABS OFFSET FOR LEFT MARGIN OF 5             
         DC    22X'00'                                                          
         DC    X'1EF2'                                                          
KEEPFLD  DS    CL2                                                              
         DC    X'1EF3F21EF6F0F5'   SINGLE SPACE,1ST LINE IS 5                   
LASTLN   DC    C'3A'                                                            
         DC    C'420000'                                                        
         DC    X'1EF9'                                                          
FMTBOOKN DC    CL10' '                                                          
         DC    X'1E2B'                                                          
         SPACE 1                                                                
FORMAT61 DS    0CL12                                                            
         DC    X'90306040FD'  FIELD 0                                           
SITTABSW EQU   *-2            SET ON X'01' FOR PROTECT                          
         DC    X'31'          FIELD 1                                           
MARGINS6 DC    X'40454154'    LEFT MAR 5 - RIGHT MAR 84                         
         DC    X'FD32'        START FIELD 2 - TABS                              
*                                                                               
*  7/6/83 FIX                                                                   
*                                                                               
FORMAT62 DS    0CL44                                                            
*FORMAT62 DS    0CL46                                                           
         DC    X'FD3348FD34584A404B404040FD38' FIELDS 3 - 4                     
TOPMAR   DC    X'4070'        6 LINES                                           
BOTMAR   DC    X'4070'        6 LINES                                           
*                                                                               
*  7/6/83 FIX                                                                   
*                                                                               
         DC    X'485040404040FD3940404040FCFC'                                  
*        DC    X'485040404040FD3940404040FBFCFBFC'                              
BOOKNAM6 DC    CL10' '                                                          
         DC    X'FD90'                                                          
TABS860  DS    XL48                                                             
LTABS860 DS    X                                                                
XER6FLAG DC    C' '                                                             
         EJECT                                                                  
         DS    0D                                                               
TRANTBLE DS    0CL256                                                           
         DC    74X'40'                                                          
         DC    X'4A4B4C4D4E4F50'                                                
         DC    9X'40'                                                           
         DC    X'5A5B5C5D5E5F6061'                                              
         DC    8X'40'                                                           
         DC    X'406B6C6D6E6F'                                                  
         DC    10X'40'                                                          
         DC    X'7A7B7C7D7E7F40'                                                
         DC    X'818283848586878889'                                            
         DC    X'408B'                                                          
         DC    5X'40'                                                           
         DC    X'919293949596979899'                                            
         DC    X'409B9C40409F40'                                                
         DC    X'A1A2A3A4A5A6A7A8A9'                                            
         DC    3X'40'                                                           
         DC    X'AD40AF'                                                        
         DC    10X'40'                                                          
         DC    X'404040BD404040'                                                
         DC    X'818283848586878889'                                            
         DC    7X'40'                                                           
         DC    X'919293949596979899'                                            
         DC    7X'40'                                                           
         DC    X'40A2A3A4A5A6A7A8A9'                                            
         DC    6X'40'                                                           
         DC    X'F0F1F2F3F4F5F6F7F8F9'                                          
         DC    6X'40'                                                           
         EJECT                                                                  
LCTEST   DS    0CL256                                                           
         DC    129X'00'                                                         
         DC    X'818283848586878889'                                            
         DC    7X'00'                                                           
         DC    X'919293949596979899'                                            
         DC    7X'00'                                                           
         DC    X'00A2A3A4A5A6A7A8A9'                                            
         DC    86X'00'                                                          
         SPACE 2                                                                
RECAPTST DS    0CL256                                                           
         DC    75X'00'                                                          
         DC    C'.'                PERIOD                                       
         DC    16X'00'                                                          
         DC    C'*'                ASTERISK                                     
         DC    32X'00'                                                          
         DC    X'7D'               APOSTROPHE                                   
         DC    130X'00'                                                         
         SPACE 2                                                                
SPECTEST DS    0CL256                                                           
         DC    109X'00'                                                         
         DC    X'6D'               UNDERSCORE                                   
         DC    65X'00'                                                          
         DC    X'01'               AF = BULLET                                  
         DC    80X'00'                                                          
         SPACE 1                                                                
WPTOXWP  DS    0CL256                                                           
         DC    X'00000000009A9700089C890B92000000'                              
         DC    X'000000000000000000009D0000009600'                              
         DC    X'00008485000000000000009000000000'                              
         DC    X'0000009500009E009B999F000000001A'                              
         DC    X'20E70000000000000000C02E3C282B7C'                              
         DC    X'2600000000000000000021242A293B5E'                              
         DC    X'EA2F00000000000000007C2C255F3E3F'                              
         DC    X'000000000000000000603A2340273D22'                              
         DC    X'00616263646566676869007B00000000'                              
         DC    X'006A6B6C6D6E6F707172007D00000000'                              
         DC    X'007E737475767778797A0000005B0000'                              
         DC    X'000000000000000000000000005D0000'                              
         DC    X'004142434445464748492D0000000000'                              
         DC    X'004A4B4C4D4E4F505152000000000000'                              
         DC    X'5CEF535455565758595A000000000000'                              
         DC    X'30313233343536373839000000000000'                              
         SPACE 1                                                                
*        DC    X'00008485000000000000009600000000'                              
TERMTRT  DS    0CL256                                                           
         DC    64X'00'                                                          
         DC    X'40'     SPACE                                                  
         DC    42X'00'                                                          
         DC    X'6B'     COMMA                                                  
         DC    148X'00'                                                         
         EJECT                                                                  
INTERTRT DS    0CL256                                                           
         DC    96X'00'                                                          
         DC    X'60'     DASH                                                   
         DC    29X'00'                                                          
         DC    X'7E'     EQUAL SIGN                                             
         DC    129X'00'                                                         
         SPACE 1                                                                
LINEEND  DS    0CL256                                                           
         DC    6X'00'                                                           
         DC    X'06'          RCR - 06                                          
         DC    5X'00'                                                           
         DC    X'0C'          FF  - 0C                                          
         DC    17X'00'                                                          
         DC    X'1E'          CRE - 1E                                          
         DC    12X'00'                                                          
         DC    X'2B'          FMT - 2B                                          
         DC    14X'00'                                                          
         DC    X'3A'          RPE - 3A                                          
         DC    197X'00'                                                         
         SPACE 1                                                                
TABCHECK DS    0CL256                                                           
         DC    5X'00'                                                           
         DC    X'05'          HT  - 05  XEROX PTB                               
         DC    51X'00'                                                          
         DC    X'39'          IT  - 39  XEROX TAB                               
         DC    198X'00'                                                         
         SPACE 1                                                                
SEPUSC   DS    0CL256                                                           
         DC    64X'01'                                                          
         DC    X'00'          40                                                
         DC    44X'01'                                                          
         DC    X'00'          6D                                                
         DC    146X'01'                                                         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
PAGEBLOC CSECT                                                                  
         DS    54CL132                                                          
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005PBPANTOXOS05/01/02'                                      
         END                                                                    
