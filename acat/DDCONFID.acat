*          DATA SET DDCONFID   AT LEVEL 012 AS OF 05/01/02                      
*CATALP CONFID                                                                  
         TITLE 'BUILD LIST OF CONTROL FILE - ID AND SE NUMBERS'                 
*              PARA 1    BYTE      SE NUMBER  DEFAULT = 10                      
*                        BYTE 1-3  A(TABLE)                                     
*              PARA 2    BYTE 0    MAXIMUM IN TABLE  0= NO MAX.                 
*                        BYTE 1-3  A(NUMBER IN TABLE)                           
         SPACE 2                                                                
CONFID   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKX-WRKD,*CONFID*                                               
         USING WRKD,RC                                                          
         LR    RA,R1                                                            
         USING PARS,RA                                                          
         L     R2,=V(DATAMGR)                                                   
         ST    R2,DATAMGR                                                       
         L     R2,=V(UTL)                                                       
         ST    R2,UTL                                                           
         SPACE 1                                                                
         MVC   SVUTL,4(R2)         SAVE OLD UTL                                 
         MVI   4(R2),10             SET UTL TO 10 TO OPEN CONTROL               
         CLI   PARS,0                                                           
         BE    *+10                                                             
         MVC   4(1,R2),PARS        OR USE SPECIFIED SYS NUM                     
         SPACE 1                                                                
         L     R4,ATABLE                                                        
         MVC   FILT,0(R4)          SAVE FIRST                                   
         SPACE 1                                                                
         ST    R4,ATHIS                                                         
         EJECT                                                                  
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'CONTROL',=C'NCTFILE X'                  
         SPACE 1                                                                
         BAS   R8,SETFILT          SET UP ANY SE FILTERS                        
         XR    R3,R3               COUNT NUMBER IN TABLE                        
         SPACE 1                                                                
         USING CTIREC,R5                                                        
         LA    R5,KEY              BUILD A KEY                                  
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         SPACE 1                                                                
         USING CND,R4                                                           
         LA    R4,FILT                                                          
         MVC   CTIKID+8(2),CNID    INPUT FILTER ID                              
         DROP  R5                                                               
         BAS   R9,HIGH                                                          
         SPACE 1                                                                
         CLI   IOAREA,C'I'                                                      
         BE    *+6                                                              
         DC    H'0'                CAN'T READ THE FIRST RECORD                  
         SPACE 1                                                                
CTNEXT   BAS   RE,ADDIT            ADD IT TO TABLE                              
         BAS   RE,FILTER           FILTER TO SEE IF I WANT IT                   
         CLI   ANUMB,0                                                          
         BE    CTNXT2              NO MAX SPECIFIED                             
         ZIC   R4,ANUMB                                                         
         CR    R3,R4               NUMBER IN TABLE VS. MAX.                     
         BE    CNFXIT              DON'T ADD ANY MORE                           
         SPACE 1                                                                
CTNXT2   LA    R4,FILT                                                          
         OC    CNID,CNID           IF FILTER ON ID CAN ONLY BE ONE              
         BNZ   CNFXIT              RECORD SO GET OUT                            
         SPACE 1                                                                
         BAS   R9,SEQ              GET NEXT ID RECORD                           
         CLI   IOAREA,C'I'                                                      
         BNE   CNFXIT                                                           
         OC    IOAREA+1(22),IOAREA+1                                            
         BZ    CTNEXT                                                           
         SPACE 1                                                                
CNFXIT   L     R2,UTL                                                           
         MVC   4(1,R2),SVUTL       RESTORE USER'S UTL                           
         L     R2,ANUMB                                                         
         ST    R3,0(R2)            RETURN NUMBER                                
         SPACE 1                                                                
CTXIT    XMOD1 1                                                                
         EJECT                                                                  
*              CHANGE USER'S INPUT NUMBER TO PROPER SE NUMBER                   
         SPACE 1                                                                
SETFILT  LA    R4,FILT                                                          
         LA    R6,SYSINDX          SYSTEM INDEX LIST                            
         SPACE 1                                                                
SETFIL2  CLI   0(R6),X'FF'         END OF SYSINDX LIST                          
         BER   R8                                                               
         ZIC   R7,1(R6)            INDEX TO SE FILT                             
         LA    R7,CNAGY(R7)                                                     
         CLI   0(R7),0             IS THERE A FILTER                            
         BNE   SETFIL4             YES                                          
         SPACE 1                                                                
SETFIL3  LA    R6,2(R6)            NEXT INDEX                                   
         B     SETFIL2                                                          
         SPACE 1                                                                
         USING CTWREC,R5                                                        
SETFIL4  LA    R5,KEY                                                           
         XC    CTWKEY,CTWKEY       BUILD SYSLIST KEY (SPECIFIC SYSTEM)          
         MVI   CTWKTYP,CTWKTYPQ    RECORD TYPE C'W' (SYSLIST)                   
         MVI   CTWKREC,CTWKRSYS    SUB-TYPE C'S' (SYSTEMS)                      
         MVC   CTWKSYSN,0(R6)      MAJOR SYSTEM NUMBER                          
         SPACE 1                                                                
         BAS   R9,HIGH                                                          
         CLC   KEY,KEYSAVE                                                      
         BE    *+6                                                              
         DC    H'0'                NO SPECIFIC SYSTEM LIST RECORD               
         SPACE 1                                                                
         LA    R5,IOAREA                                                        
         LA    R5,CTWDATA                                                       
         SPACE 1                                                                
SETFIL5  CLI   0(R5),X'A4'         SYSTEM LIST ELEMENT                          
         BE    SETFIL7                                                          
         CLI   0(R5),0             END OF RECORD                                
         BE    SETFIL3                                                          
         SPACE 1                                                                
SETFIL6  ZIC   R2,1(R5)                                                         
         AR    R5,R2                                                            
         B     SETFIL5                                                          
         DROP  R5                                                               
         SPACE 1                                                                
         USING CTLSTD,R5                                                        
SETFIL7  CLC   10(1,R5),0(R6)      MATCH MAJOR SYSNUM IN ELMNT TO TABLE         
         BE    *+6                                                              
         DC    H'0'                SYSLIST RECORD MAJOR SYSTEM MISMATCH         
         CLC   12(1,R5),0(R7)      MATCH ON SYSTEM FILSET NUMBER                
         BNE   SETFIL6                                                          
         MVC   0(1,R7),11(R5)      REPLACE FILSET NUMBER WITH SENUM             
         B     SETFIL3                                                          
         DROP  R5                                                               
         EJECT                                                                  
*              ADD THIS ID TO TABLE                                             
         SPACE 1                                                                
ADDIT    L     R4,ATHIS                                                         
         XC    CNLEN,CNLEN                                                      
         SPACE 1                                                                
         USING CTIREC,R5                                                        
         LA    R5,IOAREA                                                        
         MVC   CNID,CTIKID+8       ID NUMBER TO TABLE                           
         LA    R5,CTIDATA          R5 TO FIRST ELEMENT                          
         SPACE 1                                                                
ADDIT2   CLI   0(R5),0                                                          
         BER   RE                  END OF RECORD                                
         CLI   0(R5),X'06'                                                      
         BE    ADDIT6              AGENCY ALPHA ELEMENT                         
         CLI   0(R5),X'21'         SYSTEM ELEMENT                               
         BE    ADDIT21                                                          
ADDIT3   ZIC   R2,1(R5)                                                         
         AR    R5,R2               NEXT ELEMENT                                 
         B     ADDIT2                                                           
         DROP  R5                                                               
         SPACE 1                                                                
         USING CTAGYD,R5                                                        
ADDIT6   MVC   CNAGY,CTAGYID       ALPHA CODE TO TABLE                          
         B     ADDIT3                                                           
         DROP  R5                                                               
         SPACE 1                                                                
         USING CTSYSD,R5                                                        
ADDIT21  LA    R6,SYSINDX          MATCH SYSTEM NUMBER TO TABLE                 
         SPACE 1                                                                
ADDIT22  CLI   0(R6),X'FF'         END OF SELIST                                
         BE    ADDIT3              NEXT ELEMENT                                 
         CLC   CTSYSNUM,0(R6)                                                   
         BE    ADDIT23             SYSTEM NUMBER MATCHES TABLE                  
         LA    R6,2(R6)            NEXT SELIST ENTRY                            
         B     ADDIT22                                                          
         SPACE 1                                                                
ADDIT23  ZIC   R7,1(R6)            DISPLACEMENT TO SYS BYTES                    
         LA    R7,CNAGY(R7)        R7 TO SYSTEM BYTES IN TABLE ENTRY            
         MVC   0(1,R7),CTSYSSE     SE NUMBER TO TABLE                           
         MVC   1(1,R7),CTSYSAGB    BINARY AGENCY CODE                           
         B     ADDIT3              NEXT ELEMENT                                 
         DROP  R5                                                               
         EJECT                                                                  
*              FILTER ITEM ALREADY IN TABLE TO DETERMINE IF I KEEP IT           
         SPACE 1                                                                
FILTER   L     R4,ATHIS                                                         
         OC    CNLEN,CNLEN                                                      
         BZ    FILTNO              NO SIGNIFICANT DATA                          
         SPACE 1                                                                
         OC    FILT(2),FILT                                                     
         BZ    *+14                NO ALPHA AGENCY FILTER                       
         CLC   CNAGY,FILT          DOES IT MATCH                                
         BNE   FILTNO              NO, SO I DON'T WANT IT                       
         SPACE 1                                                                
         OC    FILT+2(2),FILT+2                                                 
         BZ    *+14                NO ID NUMBER FILTER                          
         CLC   CNID,FILT+2         DOES IT MATCH                                
         BNE   FILTNO              NO, SO I DON'T WANT IT                       
         SPACE 1                                                                
         LA    R2,5                NOW DO 5 SYSTEM FIELDS                       
         LA    R6,CNSSE            STARTING WITH SPOT                           
         LA    R7,FILT+4                                                        
         SPACE 1                                                                
FILTSE   CLI   0(R7),0                                                          
         BE    *+14                NO FILTER                                    
         CLC   0(1,R6),0(R7)       DOES SE NUMBER MATCH FILTER                  
         BNE   FILTNO              NO, SO GET OUT                               
         CLI   1(R7),0             DOES AGENCY CODE HAVE FILTER                 
         BE    *+14                BRANCH IF NO FILTER                          
         CLC   1(1,R6),1(R7)       DOES AGENCY CODE MATCH FILTER                
         BNE   FILTNO                                                           
         LA    R6,2(R6)            NEXT SE FIELD IN TABLE                       
         LA    R7,2(R7)            NEXT SE FIELD IN FILTER                      
         BCT   R2,FILTSE                                                        
         SPACE 1                                                                
FILTYES  LA    R4,L'CNLEN(R4)      TO NEXT ENTRY                                
         ST    R4,ATHIS                                                         
         LA    R3,1(R3)            ADD TO COUNT                                 
         BR    RE                  KEEP IT                                      
         SPACE 1                                                                
FILTNO   XC    CNLEN,CNLEN                                                      
         BR    RE                                                               
         EJECT                                                                  
*              READ CONTROL FILE                                                
         SPACE 1                                                                
HIGH     MVC   COMMAND,=C'DMRDHI'                                               
         MVC   KEYSAVE,KEY                                                      
         B     CTFLE                                                            
         SPACE 1                                                                
SEQ      MVC   COMMAND,=C'DMRSEQ'                                               
         MVC   KEYSAVE,KEY                                                      
         SPACE 1                                                                
CTFLE    EQU   *                                                                
         GOTO1 DATAMGR,DMCB,(0,COMMAND),=C'CTFILE  ',KEY,KEY,0                  
         BR    R9                                                               
         EJECT                                                                  
*  SYSINDX                                                                      
*        BYTE 0    SYSTEM NUMBER                                                
*        BYTE 1    INDEX TO SE FILE IN CND                                      
         SPACE 1                                                                
*&&US                                                                           
SYSINDX  DC    X'0204'             SPOT/BYTE 4                                  
         DC    X'030C'             NETPAK/BYTE 12                               
         DC    X'0406'             PRINT/BYTE 6                                 
         DC    X'050A'             PLANNING/BYTE 10                             
         DC    X'0608'             ACCOUNT/BYTE 8                               
         DC    X'080A'             REP/BYTE 10                                  
         DC    X'0E0A'             PERSON/BYTE 10                               
         DC    X'FF'                                                            
*&&                                                                             
         SPACE 1                                                                
*&&UK                                                                           
SYSINDX  DC    X'0406'             MEDLINE/BYTE 6                               
         DC    X'0608'             ACCOUNT/BYTE 8                               
         DC    X'0E0A'             PERSON/BYTE 10                               
         DC    X'FF'                                                            
*&&                                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
PARS     DSECT                                                                  
ATABLE   DS    A                   A(TABLE)                                     
ANUMB    DS    A                   A(NUMBER IN TABLE)                           
         SPACE 3                                                                
WRKD     DSECT                                                                  
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
UTL      DS    V                                                                
ATHIS    DS    A                                                                
SVUTL    DS    CL1                 OLD UTL                                      
         SPACE 1                                                                
FILT     DS    CL14                INPUT ENTRY                                  
COMMAND  DS    CL6                                                              
KEYSAVE  DS    CL25                                                             
KEY      DS    CL25                                                             
         ORG   KEY                                                              
IOAREA   DS    CL1008                                                           
WRKX     EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDCNTRL                                                        
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DDCONFID  05/01/02'                                      
         END                                                                    
