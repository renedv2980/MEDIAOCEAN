*          DATA SET DDSCANNER  AT LEVEL 003 AS OF 06/10/15                      
*CATALP SCANNER                                                                 
         TITLE 'VARIABLE SCAN MODULE'                                           
***********************************************************************         
* SEE DCSCANNER FOR PARAMETERS                                        *         
* 24 BIT VERSION USES P1 TO P3                                        *         
* 31 BIT VERSION USES P1 TO P5                                        *         
***********************************************************************         
SCANNER  CSECT                                                                  
         ENTRY SCAN31                                                           
         PRINT NOGEN                                                            
         MVI   72(RD),0            ORIGINAL CALL                                
         J     SCANIT                                                           
                                                                                
SCAN31   MVI   72(RD),1            NEW CALL                                     
                                                                                
***********************************************************************         
* WARNING DO NOT PUT CLEAR=Y ON NMOD1                                 *         
***********************************************************************         
SCANIT   NMOD1 WORKX-WORKD,**SCAN**                                             
         USING WORKD,RC                                                         
         SAM31 ,                   SET TO 31 BIT MODE                           
         LR    R9,R1               R9=A(PARAMETER LIST)                         
         CLI   0(RC),1             0(RC) WAS SET AT ENTRY                       
         BNE   SCANOLD             OLD STYLE CALL                               
                                                                                
SCANNEW  XC    WORKD(WORKX-WORKD),WORKD  CLEAR OUT ANY CRAP                     
         MVI   RGHTLEN+1,10        PRESET DEFAULT LENGTHS                       
         MVI   LEFTLEN+1,10                                                     
         MVC   P1(20),0(R9)                                                     
         OI    SCANIND,SCI31       31 BIT VERSION                               
         LM    R2,R3,P1            R2=A(DATA STRING), R3=A(BLOCK)               
         MVC   MAXLINES,P4         NUMBER OF LINES SUPPORTED (255)              
         MVC   SCANIND,P4+3        SAVE OFF SETTINGS                            
         CLI   P5+3,0              RIGHT HAND SIDE OVER-RIDE                    
         BE    SCANN10             NONE SET                                     
         OI    SCANIND,SCIORGHT    OVER-RIDE LENGTHS                            
         MVC   RGHTLEN+1(1),P5+3   RIGHT HAND SIDE OVER-RIDE VALUE              
                                                                                
SCANN10  CLI   P5,0                LEFT HAND SIDE OVER-RIDE                     
         BE    SCANSTR             NONE SET                                     
         MVC   LEFTLEN+1(1),P5     LEFT HAND SIDE OVER-RIDE VALUE               
         OI    SCANIND,SCIOLEFT                                                 
         B     SCANSTR                                                          
                                                                                
SCANOLD  SAM24 ,                   SET TO 24 BIT MODE                           
         XC    WORKD(WORKX-WORKD),WORKD CLEAR WORKING STORAGE                   
         MVI   RGHTLEN+1,10        PRESET DEFAULT LENGTHS                       
         MVI   LEFTLEN+1,10                                                     
         MVC   P1(12),0(R9)                                                     
         LM    R2,R3,P1            R2=A(DATA STRING) R3=A(BLOCK)                
         NILH  GR2,X'00FF'         CLEAR HIGH ORDER BYTE                        
         MVC   MAXLINES,P2                                                      
         NI    MAXLINES,X'7F'      NUM OF LINES SUPPORTED (127)                 
         TM    P2,X'80'            RETURN DISPLACEMENTS                         
         BZ    *+8                                                              
         OI    SCANIND,SCIRDISP                                                 
         CLI   P1,C'C'                                                          
         BNE   SCANO10                                                          
         OI    SCANIND,SCICARD     SET CARD INPUT                               
         B     SCANSTR                                                          
                                                                                
SCANO10  CLI   P1,0                RIGHT HAND SIDE OVER-RIDE?                   
         BE    SCANSTR             NO                                           
         LLC   R0,P1                                                            
         NILL  GR0,X'FFFF'-X'0080' FORCE 'CARD INPUT' BIT OFF                   
         CHI   R0,10               IGNORE OVERRIDE LENGTH IF <10                
         BL    SCANSTR                                                          
         CHI   R0,80               IGNORE OVERRIDE LENGTH IF >80                
         BH    SCANSTR                                                          
         OI    SCANIND,SCIORGHT    OVER-RIDE LENGTH                             
         STC   R0,RGHTLEN+1                                                     
         TM    P1,X'80'            CARD INPUT TOO?                              
         BZ    SCANSTR             NO                                           
         OI    SCANIND,SCICARD     SET CARD INPUT                               
*                                                                               
SCANSTR  CTRY  ,                   R0=X'OOAACCLL'                               
         STCM  R0,15,AGCOPT                                                     
         MVC   COMMA(2),COMAEQLS   SET DEFAULT TO ,=                            
         CLC   P3(2),COMAEQLS                                                   
         BNE   SCAN0                                                            
         MVC   COMMA(2),P3+2       OVERRIDE SPECIAL CHARACTERS                  
         CLC   COMMA(2),=C'  '     TEST IF CALLER WANTS LANG DEFAULTS           
         BNE   SCAN0               NO                                           
         SR    RF,RF               YES GET LANGUAGE CODE                        
         IC    RF,LANG                                                          
         CHI   RF,7                                                             
         BNH   *+6                                                              
         SR    RF,RF                                                            
         SLL   RF,1                                                             
         LA    RF,COMAEQLS(RF)     INDEX INTO COMMA EQUALS TABLE                
         MVC   COMMA(2),0(RF)      SET DEFAULTS FOR LANGUAGE                    
*                                                                               
SCAN0    MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         XC    DISP,DISP                                                        
         LLH   RF,RGHTLEN          GET NEW LENGTH                               
         STH   RF,LRIGHT                                                        
         AH    RF,LEFTLEN                                                       
         STH   RF,LBOTH                                                         
         LA    RF,SCONEFLD-SCANBLKD(,RF)                                        
         STH   RF,LROW                                                          
                                                                                
         LLC   R4,5(,R2)           L'DATA IF SCREEN FILED                       
         AHI   R2,8                IF SCREEN FIELD BUMP TO DATA                 
SCAN2    TM    SCANIND,SCICARD     DID CALLER OVER-RIDE AS CARD?                
         BZ    SCAN5               NO                                           
         EJECT                                                                  
***********************************************************************         
* FIND LENGTHS OF INPUT CARDS                                         *         
***********************************************************************         
SCAN3    SHI   R2,8                RE-ADJUST SINCE NOT A SCREEN FIELD           
         LA    R4,80               MAX LENGTH                                   
         CLC   0(80,R2),SPACES                                                  
         BE    ERROR2              NOTHING INPUT                                
         LA    R5,79(R2)           END OF INPUT                                 
*                                                                               
SCAN4    CLI   0(R5),C' '          FIND SIGNIFICAT DATA                         
         BH    SCAN5                                                            
         BCTR  R5,0                                                             
         BCT   R4,SCAN4                                                         
*                                                                               
SCAN5    LA    R5,0(R2,R4)         R4=LENGTH OF DATA, R5=END OF DATA            
         ST    R5,EOD              END OF DATA                                  
         SR    R6,R6               R6=NUMBER OF LINES USED                      
         EJECT                                                                  
***********************************************************************         
* PROCESS LEFT HAND SIDE                                              *         
***********************************************************************         
         USING SCANBLKD,R3                                                      
SCAN6    XC    SCLINE(12),SCLINE   PRESET A LINE                                
         LH    RF,LBOTH                                                         
         BCTR  RF,0                                                             
         EX    RF,CLRFLD                                                        
         MVI   SC1STVAL,SCNUMQ+SCALPHAQ+SCHEXQ                                  
         MVI   SC2NDVAL,SCNUMQ+SCALPHAQ+SCHEXQ                                  
         BAS   RE,GETL             PARSE DATA                                   
         CLI   SC1STLEN,0                                                       
         BNE   *+8                                                              
         MVI   SC1STVAL,0                                                       
         CLI   SC2NDLEN,0                                                       
         BNE   *+8                                                              
         MVI   SC2NDVAL,0                                                       
         CLC   SC1STLEN,LBOTH+1                                                 
         BH    ERROR                                                            
         CLC   SC2NDLEN,LRIGHT+1                                                
         BH    ERROR                                                            
         CLI   SC2NDLEN,0                                                       
         BE    SCAN8                                                            
         CLC   SC1STLEN,LEFTLEN+1                                               
         BH    ERROR                                                            
*                                                                               
SCAN8    SR    R7,R7                                                            
         ICM   R7,1,SC1STLEN       MOVE IN DATA                                 
         BZ    SCAN18                                                           
         BCTR  R7,0                                                             
         EX    R7,SETFLD1                                                       
*                                                                               
         TM    SC1STVAL,SCNUMQ+SCHEXQ                                           
         BZ    SCAN10              NO, SO PROCESS RIGHT HAND SIDE               
         TM    SCANIND,SCIHEXIN    CONVERT TO HEXIN?                            
         BO    SCAN8A              YES                                          
         TM    SC1STVAL,SCNUMQ     IS IT A NUMBER?                              
         BO    SCAN9               YES, TRANSLATE TO BINARY                     
         B     SCAN10              NO                                           
*                                                                               
SCAN8A   ICM   RF,15,=V(HEXIN)                                                  
         BZ    SCAN10                                                           
         MVC   WORK,=X'F0F0F0F0F0F0F0F0'                                        
         LA    R1,8                                                             
         SR    R1,R7                                                            
         LA    RE,WORK-1(R1)                                                    
         EX    R7,HEXFLD1                                                       
         GOTO1 (RF),DMCB,WORK,SC1STNUM,8,0                                      
         B     SCAN10                                                           
*                                                                               
SCAN9    CHI   R7,8                                                             
         BH    SCAN10                                                           
         EX    R7,VARPAK                                                        
         CVB   R8,DUB                                                           
         STCM  R8,7,SC1STNUM+1     STORE AT LEAST 3 BYTES BINARY                
         TM    SCANIND,SCIRDISP    THAT'S IF RETURNING DISPLACEMENTS            
         BO    SCAN10              YES                                          
         ST    R8,SC1STNUM         NO, SO STORE WHOLE THING                     
                                                                                
***********************************************************************         
* PROCESS RIGHT HAND SIDE                                             *         
***********************************************************************         
SCAN10   LA    R2,2(R2,R7)         NEXT                                         
         IC    R7,SC2NDLEN                                                      
         LTR   R7,R7                                                            
         BZ    SCAN20                                                           
         LA    R4,SCONEFLD         POINT TO SCAN FIELDS                         
         AH    R4,LEFTLEN          POINT TO START OF RIGHT HAND FIELD           
         BCTR  R7,0                                                             
         EX    R7,SETFLD2                                                       
*                                                                               
         TM    SC2NDVAL,SCNUMQ+SCHEXQ                                           
         BZ    SCAN12                                                           
         TM    SCANIND,SCIHEXIN    CONVERT USING HEXIN?                         
         BO    SCAN11A             YES                                          
         TM    SC2NDVAL,SCNUMQ     IS IT A NUMBER                               
         BO    SCAN11              YES, TRANSLATE TO BINARY                     
         B     SCAN12                                                           
*                                                                               
SCAN11A  ICM   RF,15,=V(HEXIN)                                                  
         BZ    SCAN12                                                           
         MVC   WORK,=X'F0F0F0F0F0F0F0F0'                                        
         LA    R1,8                                                             
         SR    R1,R7                                                            
         LA    RE,WORK-1(R1)                                                    
         EX    R7,HEXFLD2                                                       
         GOTO1 (RF),DMCB,WORK,SC2NDNUM,8,0                                      
         B     SCAN12                                                           
*                                                                               
SCAN11   CHI   R7,8                9 IS MAX FOR A NUMBER 999999999              
         BH    SCAN12                                                           
         EX    R7,VARPAK                                                        
         CVB   R8,DUB                                                           
         STCM  R8,7,SC2NDNUM+1     STORE AT LEAST 3 BYTES BINARY                
         TM    SCANIND,SCIRDISP    THAT'S ALL IF RETURNING DISPS.               
         BO    SCAN12                                                           
         ST    R8,SC2NDNUM                                                      
*                                                                               
SCAN12   LA    R2,2(R2,R7)                                                      
         B     SCAN20                                                           
*                                                                               
                                                                                
SCAN18   LA    R2,1(,R2)                                                        
         CLI   SC2NDLEN,0                                                       
         BNE   ERROR                                                            
*                                                                               
SCAN20   LA    R6,1(R6)            BUMP N'LINES                                 
         AH    R3,LROW             BUMP TO NEXT LINE IN BLOCK                   
         C     R2,EOD              ARE WE NOW PAST END OF DATA ?                
         BH    OK                                                               
         LLC   R7,MAXLINES                                                      
         LTR   R7,R7                                                            
         BZ    SCAN6                                                            
         CR    R6,R7               HAVE WE REACHED MAX N'LINES                  
         BNE   SCAN6                                                            
*                                                                               
OK       STC   R6,4(R9)            SET NUMBER OF LINES USED                     
         B     XIT                                                              
*                                                                               
ERROR    MVI   4(R9),0                                                          
         MVI   SC1STVAL,X'FF'                                                   
         MVI   SC2NDVAL,X'FF'                                                   
         B     XIT                                                              
*                                                                               
ERROR2   MVI   4(R9),0                                                          
         MVI   SC1STVAL,X'FF'                                                   
         MVI   SC2NDVAL,X'FF'                                                   
*                                                                               
XIT      XMOD1 1                                                                
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE AND GET LENGTHS                                            *         
* R2 = USER INPUT FIELD / CARD                                        *         
* R3 = BEGINING OF SCANBLOCK                                          *         
* R4 = CURRENT SCAN BLOCK ENTRY                                       *         
***********************************************************************         
GETL     NTR1                                                                   
         LR    R4,R3                                                            
         SR    R5,R5                                                            
         TM    SCANIND,SCIRDISP    RETURN DISPLACEMENTS                         
         BZ    GETL2               NO                                           
         MVC   4(1,R4),DISP+1      YES, DISPLACEMENT INTO FIELD                 
*                                                                               
GETL2    C     R2,EOD              TEST AT END                                  
         BNL   GETL12              YES DONE                                     
         CLC   0(1,R2),COMMA       TEST FIELD SEPERATOR                         
         BE    GETL12                                                           
         CLI   LANG,2                                                           
         BNH   *+12                                                             
         CLI   0(R2),X'5E'         TEST MULTI-NATIONAL STANDARD (SEMI)          
         BE    GETL12                                                           
         CLC   0(1,R2),EQUAL                                                    
         BE    GETL14                                                           
*                                                                               
GETL3    LA    R5,1(,R5)           COUNTER FOR LENGTH                           
         CLI   0(R2),C'9'                                                       
         BNH   *+8                                                              
         MVI   2(R4),0             ALL INVALID                                  
         CLI   0(R2),C'0'                                                       
         BL    GETL4                                                            
         NI    2(R4),255-SCALPHAQ  INVALID ALPHA                                
         B     GETL10                                                           
*                                                                               
GETL4    NI    2(R4),X'7F'         INVALID NUM                                  
         CLI   0(R2),C'Z'                                                       
         BNH   GETL6                                                            
         MVI   2(R4),0             Z-0 = ALL INVALID                            
         B     GETL10                                                           
*                                                                               
GETL6    CLI   0(R2),C'A'          LESS THAN A = ALL INVALID                    
         BNL   GETL8                                                            
         MVI   2(R4),0             ALL INVALID                                  
         B     GETL10                                                           
*                                                                               
GETL8    CLI   0(R2),C'F'          OK FOR ALPHA                                 
         BNH   GETL10                                                           
         NI    2(R4),255-SCHEXQ    G-Z = INVALID HEX                            
*                                                                               
GETL10   LA    R2,1(,R2)           NEXT CHARACTER IN USER FIELD                 
         B     GETL2                                                            
*                                                                               
GETL12   STC   R5,0(,R4)           COMMA FOUND OR END SAVE LENGTH               
         LA    R5,1(,R5)                                                        
         AH    R5,DISP                                                          
         STH   R5,DISP                                                          
         B     GETLX               DONE                                         
*                                                                               
GETL14   CR    R4,R3               EQUAL FOUND - IS THIS THE FIRST ONE?         
         BNE   GETL3               TREAT AS NORMAL CHARACTER IF NOT             
         STC   R5,0(,R4)           EQUAL FOUND, SAVE LENGTH                     
         LA    R5,1(,R5)                                                        
         AH    R5,DISP                                                          
         STH   R5,DISP                                                          
         TM    SCANIND,SCIRDISP    RETURN DISPLACEMENT?                         
         BZ    GETL16              NO                                           
         MVC   8(1,R4),DISP+1      YES, DISPLACEMENT INTO FIELD                 
*                                                                               
GETL16   LA    R4,1(,R4)           POINT TO FIELD2 DATA (SNEEKY)                
         SR    R5,R5               CLEAR L2                                     
         LA    R2,1(,R2)           POINT PAST EQUAL SIGN                        
         B     GETL2                                                            
*                                                                               
GETLX    XIT1  ,                                                                
         EJECT                                                                  
COMAEQLS DC    C',=,=,=#=;=;=;=;='                                              
         USING SCANBLKD,R3                                                      
CLRFLD   MVC   SCONEFLD(0),SPACES  CLEAR                                        
SETFLD1  MVC   SC1STFLD(0),0(R2)                                                
SETFLD2  MVC   0(0,R4),0(R2)       2ND IS SOFT SINCE LEFT CAN VARY              
HEXFLD1  MVC   0(0,RE),SC1STFLD                                                 
HEXFLD2  MVC   0(0,RE),0(R4)                                                    
VARPAK   PACK  DUB,0(0,R2)                                                      
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
EOD      DS    A                                                                
WORK     DS    CL32                                                             
SPACES   DS    CL80                                                             
*                                                                               
RGHTLEN  DS    H                   NEW RIGHT HAND SIDE OVER-RIDE LEN            
LEFTLEN  DS    H                   NEW LEFT  HAND SIDE OVER-RIDE LEN            
MAXLINES DS    CL1                                                              
COMMA    DS    CL1                                                              
EQUAL    DS    CL1                                                              
*                                                                               
SCANIND  DS    XL1                 SCANNER INDICATORS                           
*              X'80'               SCIORGHT                                     
*              X'40'               SCIOLEFT                                     
*              X'20'               SCICARD                                      
*              X'10'               SCIRDISP                                     
*              X'08'               SCIHEXIN                                     
SCI31    EQU   X'01'               31 BIT CALL                                  
*                                                                               
LROW     DS    H                                                                
LRIGHT   DS    H                                                                
LBOTH    DS    H                                                                
DISP     DS    H                                                                
*                                                                               
AGCOPT   DS    XL1                                                              
AGCTRY   DS    XL1                                                              
CTRY     DS    XL1                                                              
LANG     DS    XL1                                                              
*                                                                               
WORKX    DS    0C                                                               
         EJECT                                                                  
*SCANBLKD                                                                       
       ++INCLUDE DDSCANBLKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDSCANNER 06/10/15'                                      
         END                                                                    
