*          DATA SET DDLISTEM   AT LEVEL 093 AS OF 05/01/02                      
*CATALP LISTEM                                                                  
*                                                                               
         SPACE 2                                                                
*                                                                               
* THIS PROGRAM PRINTS A FORMATTED REPORT OF A RECORD. TABLES IN LISTERD         
* TELL WHAT IS TO BE PRINTED, FILTERS, TOTALS, HEADERS ETC.                     
* LISINIT MUST FIRST BE CALLED TO SET UP THESE TABLES                           
* PARAM1: ADDR OF DSECT LISTERD                                                 
* PARAM2: ADDR OF RECORD TO BE PRINTED                                          
* PARAM3: LENGTH OF RECORD TO BE PRINTED                                        
*                                                                               
         TITLE 'PRINT A RECORD'                                                 
LISTEM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**LISTEM                                             
         USING WORKD,RC                                                         
         USING LISTERD,R9                                                       
         L     R9,0(R1)                                                         
         L     RA,4(R1)                                                         
         USING INREC,RA                                                         
         MVC   IRECLEN(4),8(R1)    SAVE RECORD LENGTH                           
*                                                                               
         BAS   RE,READTAPE         READ FILE AND PRINT REPORT                   
*                                                                               
ENDMAIN  XBASE                                                                  
         EJECT                                                                  
* CALLS TO -  PRINTLIN -  FILLS IN INTERNAL PRINT LINE WITH CURRENT             
*                                                       FIELD.                  
*             PRINTIT -  BREAKS UP INTERNAL PRINT LINE AND PRINTS IT.           
*             HEXOUT - CONVERTS INTERNAL HEX TO PRINTABLE HEX.                  
*                                                                               
* LOCALS    -  R5 - CURRENT LOCATION IN INTERNAL PRINT LINE                     
*              R6 - CURRENT LOCATION ON THE RECORD READ FROM TAPE.              
*              R7 - CURRENT LOCATION IN TABLE1.                                 
*              R8 - ADDRESS OF END OF RECORD.                                   
**************************************************************                  
*                                                                               
READTAPE NTR1                                                                   
*                                                                               
******** GET INFO IN INREC                                                      
*                                                                               
         CLI   RECTYPE,X'00'       TEST IF TYPE CARD GIVEN                      
         BE    CKFILT                                                           
         CLC   INREC(1),RECTYPE    CK IF THIS RECORD IS PROPER TYPE             
         BNE   XITTAPE                                                          
CKFILT   BAS   RE,CKFILTER                                                      
         CLC   CFSTATUS(2),=C'OK'                                               
         BNE   XITTAPE                                                          
PROCIT   L     R2,LIMIT                                                         
         BCTR  R2,0                                                             
         LTR   R2,R2                                                            
         BZ    XITTAPE                                                          
         ST    R2,LIMIT                                                         
         BAS   RE,PREPRNT          PRINT BLANK LINE                             
*                                                                               
         LA    R5,0                INITIALIZE LOCALS                            
         BAS   RE,PRINTIT          INITIALIZE PRINT FIELDS                      
         BAS   RE,BUILDKEY         SET UP SUM RECORD                            
         LA    R6,INREC                                                         
         LA    R7,TBL1BEG                                                       
         SR    R8,R8                                                            
         L     R8,IRECLEN                                                       
         LA    R8,INREC(R8)                                                     
         BCTR  R8,0                ACCOUNTS FOR HELLO TYPE RECORDS              
*                                                                               
         MVC   RCRDNAM(10),HEAD1OFF(R7)                                         
RDTLOOP  CLC   HEAD1OFF(3,R7),=C'END'                                           
         BE    FLUSHIT                                                          
         TM    CTL1OFF(R7),X'01'  CK FOR ELEMENT                                
         BO    ELEMENT                                                          
         CLC   VAL1OFF(L'TBLVAL1,R7),=F'0'  CK FOR 0 LENGTH FIELD               
         BE    NEXTTAB                                                          
         CLI   CTL1OFF+1(R7),X'00' CK IF THIS FIELD IS SUMMED                   
         BE    RDT1                                                             
         BAS   RE,DOSUM                                                         
*                                                                               
RDT1     TM    CTL1OFF(R7),X'40'   CK IF TO BE PRINTED                          
         BO    NEXTFLD                                                          
         BAS   RE,PRINTLIN                                                      
         B     NEXTFLD                                                          
*                                                                               
ELEMENT  BAS   RE,PRINTIT          PRINT LAST ELEMENT OR RECORD                 
         LA    R3,TBL2BEG                                                       
LKUP     CLC   KEY2OFF(L'TBLKEY2,R3),0(R6)   FIND THE KEY FOR THIS              
         BE    FOUNDIT                             IN TABLE2                    
         CLC   KEY2OFF(L'TBLKEY2,R3),X'FF'                                      
         BE    NOTFOUND                      CK FOR END OF TABLE                
         LA    R3,TBL2RECL(R3)                                                  
         B     LKUP                                                             
*                                                                               
FOUNDIT  L     R7,ADDR2OFF(R3)                                                  
         MVC   RCRDNAM+8(L'TBLHEAD1),HEAD1OFF(R7)                               
RDT2     LA    R7,TBL1RECL(R7)               SAVE ELEMENT NAME AND              
         TM    CTL1OFF(R7),X'01'             GET 1ST NON-ELEMENT                
         BO    RDT2                                                             
         B     RDTLOOP                         GOTO PROCESS IT                  
*                                                                               
NEXTFLD  A     R6,VAL1OFF(R7)                NEXT FIELD ON RECORD               
NEXTTAB  LA    R7,TBL1RECL(R7)               NEXT ENTRY IN TABLE                
         CR    R6,R8                                                            
         BL    RDTLOOP             CK FOR END OF LOOP                           
FLUSHIT  BAS   RE,PRINTIT          FLUSH PRINT LINES                            
*                                  PERFORM SUMMING IN BUFFALO                   
         CLC   KNAMOFF+KTOTBEG(3),=C'END'                                       
         BE    XITTAPE             DONT SEND IF NO TOTALS                       
         GOTO1 BUFFALO,DMCB,=C'PUT',BUFFBUFF,SUMREC                             
         B     XITTAPE             GET NEXT RECORD                              
*                                                                               
*                                                                               
XITTAPE  XIT1                                                                   
         DROP  RA                                                               
*                                                                               
         USING DPRINT,RA                                                        
NOTFOUND LA    RA,CPRINT                                                        
         MVC   P+10(30),=CL30'BAD KEY IN  RECORD:'                              
         GOTO1 HEXOUT,DMCB,0(R6),P+35,L'TBLKEY2,0,0                             
         BAS   RE,PREPRNT                                                       
         B     XITTAPE                                                          
*****************************************************************               
         EJECT                                                                  
****************************************************************                
* SUBROUTINE BUILDKEY - INITIALIZES BUFFALO RECORD. BUILDS KEY BY               
*        READING KEYTABLE.                                                      
*                                                                               
* CALLED FROM: READTAPE                                                         
*                                                                               
* INPUTS : INREC - LOCATION OF THE CURRENT RECORD                               
*                                                                               
* OUTPUTS: SUMREC - FILLS IN KEY FIELDS. ZEROES SUM FIELDS.                     
*          SUMDATA - THE LOCATION IN SUMREC WHERE SUM FIELDS BEGIN              
*                                                                               
* LOCALS : R5 - THE CURRENT ENTRY IN THE KEYTABLE                               
*          R8 - THE BEGINNING OF THE NEXT KEY FIELD                             
*          R4 - LOCATION OF FIELD TO BE MOVED                                   
****************************************************************                
         SPACE 2                                                                
KEYMOVE  MVC   0(0,R8),0(R4)          **** DUMMY MOVE                           
         SPACE 2                                                                
BUILDKEY NTR1                                                                   
         XC    SUMREC,SUMREC       ZERO OUT SUMREC                              
         LA    R8,SUMREC                                                        
         LA    R5,KTOTBEG                                                       
*                                                                               
BKLOOP   CLC   KNAMOFF(3,R5),=C'END'    CK FOR END OF TABLE                     
         BE    XITBK                                                            
         L     R6,KCOLOFF(R5)      STARTING COLUMN OF KEY                       
         L     R7,KLENOFF(R5)      LENGTH OF THIS KEY                           
         BCTR  R7,0                LENGTH FOR EX                                
         LA    R4,INREC(R6)                                                     
         EX    R7,KEYMOVE          MOVE KEYFIELD INTO SUMREC                    
         LA    R8,1(R7,R8)         LOCN OF NEXT KEYFIELD IN SUMREC              
*                                   (1 IS TO USE ACTUAL LENGTH (1+R7))          
         LA    R5,KTOTRECL(R5)     GET NEXT ENTRY  IN KEY TABLE                 
         B     BKLOOP                                                           
*                                                                               
XITBK    ST    R8,SUMDATA          BEGINNING LOCN OF SUM FIELDS                 
         XIT1                                                                   
*****************************************************************               
         EJECT                                                                  
***************************************************************8                
* SUBROUTINE DOSUM - PERFORM SUMS ON CURRENT BUFFALO RECORD.                    
*            ADDS VALUES TO INTERNAL RECORD ONLY. CALL TO BUFFALO               
*            LATER WILL ADD THE INTERNAL RECORD TO CUMULATIVE                   
*            TOTALS.                                                            
*                                                                               
* CALLED FROM: READTAPE                                                         
*                                                                               
* INPUTS: R6 - THE LOCATION OF THE FIELD TO BE ADDED.                           
*         R7 - LOCN OF THIS FIELDS ENTRY IN TABLE1                              
*                                                                               
* OUTPUTS: SUMREC - ADDS FIELD VALUE TO SUM ALREADY THERE                       
****************************************************************                
         SPACE 2                                                                
DOSUM    NTR1                                                                   
         L     R8,SUMDATA          LOC IN SUMREC WHERE SUM FIELDS START         
         SR    R5,R5               CALCULATE OFFSET FROM R8 WHERE               
         IC    R5,CTL1OFF+1(R7)       THIS FIELD STARTS                         
         BCTR  R5,0                                                             
         SLL   R5,2                FIELDS ARE 1 FULLWORD LONG                   
         L     R4,0(R8,R5)         GET EXISTING SUM                             
         A     R4,0(R6)            ADD FIELD VALUE                              
         ST    R4,0(R8,R5)         SAVE THIS SUM IN SUMREC                      
*                                                                               
         XIT1                                                                   
***************************************************************                 
         EJECT                                                                  
****************************************************************                
*  SUBROUTINE CKFILTER                                                          
*        CHECKS IF THE CURRENT RECORD IN INREC MEETS ALL FILTER                 
*          CONDITIONS.                                                          
*                                                                               
*  INPUTS: INREC - THE LOCATION OF THE CURRENT RECORD.                          
*                                                                               
*  OUTPUTS: CFSTATUS: 'OK'- IF RECORD MEETS FILTER CONDITIONS                   
*                     'NO'- RECORD DOES NOT MEET FILTER CONDITIONS              
*                                                                               
*  LOCALS:  R3 - LOCATION OF CURRENT LIMIT VALUE                                
*           R4 - LENGTH OF FIELD BEING FILTERED                                 
*           R7 - LOCATION OF CURRENT FILTER INFO IN FILTER TABLE                
*           R8 - LOCATION OF FIELD BEING FILTERED                               
*                                                                               
*  CALLED FROM: READTAPE - PROCESSES A TAPE RECORD                              
*                                                                               
***********************************************************                     
***                                                                             
CHECKEM  CLC   0(0,R8),0(R3)       ***DUMMY                                     
***                                                                             
*                                                                               
CKFILTER NTR1                                                                   
         LA    R7,FTBLBEG          START W/ 1ST FILTER RECORD IN TABLE          
NXTFLTR  CLI   0(R7),X'FF'         CK FOR END OF TABLE                          
         BE    FLTROK                                                           
         L     R4,FLNGOFF(R7)      LOAD LENGTH INTO R4                          
         BCTR  R4,0                LENGTH FOR EX                                
         LA    R3,FLIMOFF(R7)      LOAD LOWER LIMIT ADDR INTO R3                
         L     R5,FCOLOFF(R7)      COLUMN TO FILTER                             
         LA    R8,INREC(R5)        LOAD LOCATION OF FIELD INTO R8               
*                                                                               
         EX    R4,CHECKEM          CHECK LOWER LIMIT                            
         BL    NOGOOD                                                           
         LA    R3,1(R3,R4)         LOAD UPPER LIMIT ADDR INTO R3                
         EX    R4,CHECKEM          CHECK UPPER LIMIT                            
         BH    NOGOOD                                                           
*                                                                               
         LA    R7,1(R3,R4)         ADDR OF NEXT FILTER RECORD                   
         B     NXTFLTR             PASSED. CHECK NEXT FILTER.                   
*                                                                               
NOGOOD   MVC   CFSTATUS,=C'NO'                                                  
         B     XITCF                                                            
*                                                                               
FLTROK   MVC   CFSTATUS,=C'OK'                                                  
*                                                                               
XITCF    XIT1                                                                   
***************************************************************                 
         EJECT                                                                  
         DROP  RA                                                               
*************************************************************8                  
* SUBROUTINE PRINTLIN - FILL IN THE INTERNAL PRINTLINE(S)                       
*                                                                               
* INPUTS : R5 - THE CURRENT OFFSET INTO THE PRINTLINE                           
*          R6 - THE LOCATION OF FIELD TO BE PRINTED                             
*          R7 - THE LOCATION OF TABLE ENTRY  DESCRIBING FIELD                   
*                                                                               
* OUTPUTS: R5 - UPDATED OFFSET INTO THE PRINTLINE                               
*                                                                               
* LOCALS: R2 - THE LENGTH OF FIELD AS IT IS TO BE PRINTED                       
*         R3 - MAX (FIELD HEADER LENGTH, R2)                                    
*                                                                               
* CALLS FROM: READTAPE - PROCESSES THE TAPE DATA                                
************************************************************                    
*                                                                               
PRINTLIN NTR1                                                                   
         L     RA,CPRINT                                                        
         USING DPRINT,RA                                                        
*                                                                               
         L     R2,VAL1OFF(R7)                                                   
         CLI   SUPPRESS,C'Y'       CHECK IF SUPPRESS OPTION IS WANTED           
         BNE   PL1                                                              
         XC    ZEROES,ZEROES       BINARY ZERO FIELD                            
         LR    R3,R2                                                            
         BCTR  R3,0                DECREMENT LENGTH BY 1 FOR EXCLC              
         EXCLC R3,0(R6),ZEROES                                                  
         BE    XITPLIN             IF ZEROES DONT PRINT                         
*                                                                               
PL1      TM    CTL1OFF(R7),X'10'   IF DATA IS HEX THEN                          
         BO    PL2                   PRINT LENGTH DOUBLES                       
         SLL   R2,1                                                             
PL2      LA    R3,L'TBLHEAD1                                                    
         CR    R2,R3                                                            
         BL    PL4                                                              
         LR    R3,R2                                                            
*                                                                               
PL4      LA    R4,HEADLONG(R5)                                                  
         MVC   0(L'TBLHEAD1,R4),HEAD1OFF(R7)                                    
         LA    R4,PLONG(R5)                                                     
         TM    CTL1OFF(R7),X'10'                                                
         BO    CHARMOVE                                                         
HEXMOVE  L     R2,VAL1OFF(R7)                                                   
         GOTO1 HEXOUT,DMCB,0(R6),0(R4),(R2),0,0                                 
         B     ENDPLIN                                                          
MOVECHAR MVC   0(0,R4),0(R6)       **********DUMMY                              
CHARMOVE BCTR  R2,0                DECREMENT R2                                 
         EX    R2,MOVECHAR                                                      
*                                                                               
ENDPLIN  LA    R5,4(R5,R3)                                                      
         LA    R4,L'PLONG                                                       
         CR    R5,R4                                                            
         BL    XITPLIN                                                          
         MVC   P+10(30),=CL30'PLONG TOO SHORT'                                  
         BAS   RE,PREPRNT                                                       
XITPLIN  XIT1  REGS=(R5)                                                        
         DROP  RA                                                               
*************************************************                               
         EJECT                                                                  
***********************************************************8                    
* SUBROUTINE PRINTIT - ACTUALLY PRINTS THE INTERNAL PRINT LINES                 
*                       IN A NICE FORMAT.                                       
*                                                                               
* INPUTS: R5 - OFFSET OF END OF PRINT LINE                                      
*         HEADLONG - THE INTEERNAL PRINT LINE OF HEADER INFORMATION             
*         PLONG -  THE INTERNAL PRINTLINE OF DATA                               
*                                                                               
* OUTPUTS: R5 - RESET BACK TO 0 (THE BEGINNING OF NEW PRINTLINES)               
*          HEADLONG - FILLED WITH DOTS                                          
*          PLONG  - FILLED WITH SPACES                                          
*                                                                               
* LOCALS: R3 - BEGINNING OFFSET   OF SUBLINE WITHIN INTERNAL PRINTLINE          
*         R4 - END OFFSET   OF SUBLINE WITHIN INTERNAL PRINTLINE                
*                                                                               
* CALLED FROM: READTAPE - PROCESSES AND PRINTS DATA FROM TAPE                   
*******************************************************************             
*                                                                               
PRINTIT  NTR1                                                                   
         USING DPRINT,RA                                                        
         L     RA,CPRINT                                                        
         LA    R3,0                INITIALIZE R3,R4                             
         LA    R4,110(R3)                                                       
         LR    R6,R5               MAY BE NEEDED FOR COMPACT FORMAT             
         LTR   R5,R5               IF PRINTLINE EMPTY THEN LEAVE                
         BZ    PCLEAR                                                           
*                                                                               
         LA    R2,110                                                           
         CR    R5,R2               CHECK IF COMPACT FORMAT POSSIBLE             
         BH    PMULTI                                                           
         CLC   RCRDNAM,LASTRNAM    SAVE OFF RECORDNAME AND CLEAR SO IT          
         MVC   LASTRNAM,RCRDNAM         ONLY PRINTS ONCE                        
         BNE   PMULTI              IF RECORD IS NOT SAME TYPE AS LAST           
*                                      RECORD THEN USE LONG FORM                
         CLI   SUPPRESS,C'Y'       USE LONG FORMAT ONLY W/SUPRESS OPTN          
L        BE    PMULTI                                                           
*                                                                               
         MVC   P+8(5),=C'---->'    GIVE SHORT FORM HEADER                       
         B     PDATA               GO USE SHORT FORM                            
*                                                                               
PMULTI   LA    R7,HEADLONG(R4)                                                  
         CLI   0(R7),C'.'          FIND END OF SUBFIELD                         
         BE    PHEAD                                                            
         BCT   R4,PMULTI                                                        
*                                                                               
         MVC   P+10(20),=CL20'FIELD LONGER THAN 110'                            
         BAS   RE,PREPRNT                 NO END FOUND                          
         B     XITPIT                                                           
*                                                                               
MOVESTUF MVC   P+22(0),0(R7)           ******** DUMMY                           
*                                                                               
PHEAD    LR    R6,R4               PUT LENGTH OF SUBLINE IN R6                  
         SR    R6,R3                                                            
         MVC   P(20),RCRDNAM       ONLY PRINT RECORDNAME ONCE                   
         MVC   RCRDNAM(20),=CL20' '                                             
         LA    R7,HEADLONG(R3)                                                  
         EX    R6,MOVESTUF         PRINT HEADERS                                
         BAS   RE,PREPRNT                                                       
PDATA    LA    R7,PLONG(R3)                                                     
         EX    R6,MOVESTUF         PRINT DATA LINE                              
         BAS   RE,PREPRNT                                                       
*                                                                               
         BAS   RE,PREPRNT          PRINT BLANK LINE                             
         LA    R3,1(R4)            PREPARE FOR NEXT SUBLINE                     
         LA    R4,110(R3)                                                       
         CR    R3,R5               CK FOR END OF LINE                           
         BL    PMULTI                                                           
*                                                                               
PCLEAR   LA    R5,L'HEADLONG       CLEAR FILEDS FOR NEXT CALL                   
PC2      LA    R1,HEADLONG-1(R5)                                                
         MVI   0(R1),C'.'                                                       
         BCT   R5,PC2                                                           
*                                                                               
         LA    R5,L'PLONG                                                       
PC3      LA    R1,PLONG-1(R5)                                                   
         MVI   0(R1),C' '                                                       
         BCT   R5,PC3                                                           
*                                                                               
         LA    R5,0                                                             
XITPIT   XIT1  REGS=(R5)                                                        
         DROP  RA                                                               
***************************************************                             
         EJECT                                                                  
****************************************************                            
* SUBROUTINE PREPRINT -MONITOR PRINTER CALLS                                    
***************************************************8                            
PREPRNT  NTR1                                                                   
         USING DPRINT,RA                                                        
         L     RA,CPRINT                                                        
         CP    PAGE,=PL2'100'                                                   
         BL    PRP2                                                             
         CLC   DIAGS,=C'YES'       IF DIAGS ON THEN DUMP                        
         BNE   XITPP                                                            
         GOTO1 PRINTER                                                          
         DC    H'0'                                                             
PRP2     GOTO1 PRINTER                                                          
XITPP    XIT1                                                                   
         DROP  RA                                                               
***********************************************                                 
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
* WORKING STORAGE                                                               
*                                                                               
WORKD    DSECT                                                                  
*                                                                               
         DS    F                                                                
SUMREC   DS    CL256               CURRENT BUFFALO RECORD                       
SUMDATA  DS    A                   ADDR WHERE DATA BEGINS                       
RCRDNAM  DS    CL20                NAME OF RECRD OR ELEMENT LAST PRNTED         
LASTRNAM DS    CL(L'RCRDNAM)       NAME OF PREVIOUS RECRD OR ELEMENT            
CFSTATUS DS    CL2                 STATUS OF CHECK FILTER ROUTINE               
IRECLEN  DS    F                   LENGTH OF RECORD TO PRINT                    
ZEROES   DS    CL100                                                            
*                                                                               
DUB      DS    D                   DOUBLEWORD WORK                              
DMCB     DS    8F                  BUFFER FOR PARAMETERS IN GOTO1S              
FULL     DS    F                                                                
HALF     DS    H                                                                
*                                                                               
         DS    F                                                                
INREC    DS    CL1000              RECORD FROM TAPE                             
         DS    F                                                                
PLONG    DS    CL2500              INTERNAL PRINT LINE                          
HEADLONG DS    CL(L'PLONG)         HEADER CORRESPONDING TO PRINT LINE           
WORKX    EQU   *                                                                
         EJECT                                                                  
         BUFF  LINES=400,ROWS=1,                                       X        
               COLUMNS=1,FLAVOR=BINARY,KEYLIST=(5,A)                            
         SPACE 5                                                                
* DDDPRINT                                                                      
*                                                                               
       ++INCLUDE DDDPRINT                                                       
*                                                                               
       ++INCLUDE DDBUFFALOD                                                     
         SPACE 2                                                                
LISTERD  DSECT                                                                  
       ++INCLUDE LISTERD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'093DDLISTEM  05/01/02'                                      
         END                                                                    
