*          DATA SET DDTRACE    AT LEVEL 001 AS OF 10/10/13                      
*CATALP DDTRACE                                                                 
*===================================================================*           
*    THIS IS FOR TEMPORARY LOGGING TO DATASPACE OF EVENTS           *           
*    FOR DUBUGGING PURPOSES.                                        *           
*    TRACE CALL                                                     *           
*    P1=XL4'CODE'                                                   *           
*    P2=AL2(LEN),DATA                                               *           
*===================================================================*           
DDTRACE  TITLE 'TEMPORARY EVENT LOGGING'                                        
         PRINT NOGEN                                                            
DDTRACE  CSECT                                                                  
*                                                                               
         NMOD1 TMPWORKX-TMPWORKD,**TRACE*,CLEAR=YES                             
         USING TMPWORKD,RC                                                      
         LR    R6,R1               R2=PARMS                                     
         USING PARMS,R6                                                         
         L     R4,4(R1)            R4=TRACE DATA                                
*                                                                               
         L     R1,PCODE            INDEX INTO TRACE TABLE                       
         SLL   R1,3                                                             
         LA    R1,TRCTABL(R1)                                                   
         CLI   7(R1),C'N'          TEST ENABLED                                 
         BE    TRACEX                                                           
         MVC   MODE,7(R1)          MODE Y OR F                                  
*                                                                               
         CLI   INITFLG,C'X'        TEST DISSABLED                               
         BE    TRACEX                                                           
         CLI   INITFLG,C'N'        DO INIT ONCE ONLY                            
         BNE   DDT000                                                           
*                                                                               
INITME   SAC   0                                                                
         BAS   RE,INIT             SET UP CONSTANTS ETC                         
         CLI   INITFLG,C'Y'                                                     
         BNE   TRACEX                                                           
*                                                                               
DDT000   TIME  TU                  GET TIME FOR TRACE ENTRY IN TUS              
         ST    R0,TIMENOW                                                       
*                                                                               
         MVI   TYPE,C'J'           DEFAULT TO JOB TYPE                          
         MVC   THISREF,MYREF       AND DEFAULT REF                              
         CLI   ONLINE,C'N'                                                      
         BE    DDT001                                                           
*                                                                               
         MVI   TYPE,C'A'           ADV TYPE                                     
         L     R1,=V(SSB)                                                       
         ICM   R1,15,SSBTKADR-SSBD(R1)                                          
         BZ    *+14                                                             
         MVC   THISREF+1(1),TCBTASK-TCBD(R1)                                    
         MVI   TYPE,C'T'           TASK TYPE                                    
*                                                                               
         CLI   THISREF+1,0         ANY TASK                                     
         BNE   *+8                                                              
         MVI   TYPE,C'A'                                                        
*                                                                               
DDT001   LAM   AR2,AR2,MYALET      DMGR DSPACE                                  
         SAC   512                                                              
         L     R2,ATRCTBL          DOES TABLE NEED UPDATE                       
         CLC   0(8,R2),TRCTABL                                                  
         BNE   INITME              YES SO INIT ME                               
*                                                                               
         L     R2,ATRCBUFF                                                      
         CLC   0(4,R2),=C'TRCB'    IS BUFFER ACTIVE                             
         BE    DDT010                                                           
         CLC   0(4,R2),=C'TRCF'    IS HIGH SPEED BUFFER ACTIVE                  
         BNE   TRACEX                                                           
         CLI   MODE,C'F'           ONLY DO FAST TRACE OBJECTS                   
         BNE   TRACEX                                                           
*                                                                               
DDT010   XR    R0,R0                                                            
         CLC   4(4,R2),8(R2)       IS READ POINT < ADD POINT                    
         BNH   DDT011              THEN OK                                      
         LA    R0,1                FLAG READ IS > ADD                           
*                                                                               
DDT011   L     RE,8(,R2)           RE = CURRENT BUFFER ADDRESS                  
         LR    RF,RE                                                            
         SR    R1,R1               ADD DATA LENGTH                              
         ICM   R1,3,0(R4)                                                       
         LA    R1,16(R1)           ADD 16 BYTES FOR TRACE HEADER                
         AR    RF,R1                                                            
*                                                                               
         LTR   R0,R0               IS READ > ADD                                
         BZ    DDT012                                                           
         C     RF,4(,R2)           WILL WE OVERTAKE READ POINT                  
         BNL   DDT025              THEN OVERFLOW                                
*                                                                               
DDT012   C     RF,12(,R2)          TEST WITH END OF BUFFER                      
         BL    DDT020                                                           
*                                                                               
         L     RF,ATRCBUFF                                                      
         LA    RF,16(RF)                                                        
         CS    RE,RF,8(R2)         SWAP IN NEW BUFFER ADDRESS                   
         B     DDT010              TRY AGAIN                                    
*                                                                               
DDT020   CS    RE,RF,8(R2)         SWAP IN NEW BUFFER ADDRESS                   
         BNE   DDT010              TRY AGAIN IF COLLISION WITH ANOTHER          
*                                                                               
         LR    R2,RE               SET R2 READY TO ADD TRACE DATA               
         USING TRACED,R2                                                        
*                                                                               
         MVC   TRCTYPE,TYPE        SET TYPE                                     
         SR    R1,R1                                                            
         ICM   R1,3,0(R4)                                                       
         LA    R1,16(R1)                                                        
         STCM  R1,3,TRCLEN         SET LENGTH                                   
         MVC   TRCREF,THISREF      SET REF                                      
         MVC   TRCTIME,TIMENOW     SET TIME                                     
         MVC   TRCTRAC,PCODE+2     SET CODE                                     
         MVC   TRCSTAT,=C'*TRC'    SET TRACE ENTRY ADDED                        
         SR    R3,R3                                                            
         ICM   R3,3,0(R4)                                                       
         LR    R5,R3                                                            
         LA    R2,TRCDATA                                                       
         MVCL  R2,R4               MOVE IN TRACE DATA                           
         B     TRACEX                                                           
*                                                                               
DDT025   L     R2,ATRCBUFF                                                      
         MVI   3(R2),C'X'          FLAG WE OVERFLOWED                           
         SAC   0                                                                
         B     TRACEX                                                           
*                                                                               
TRACEX   SAC   0                                                                
*                                                                               
         XMOD1                                                                  
*                                                                               
XIT1     XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
*        INIT ROUTINE                                                *          
**********************************************************************          
INIT     NTR1                                                                   
*                                                                               
         ICM   RF,15,=V(SSB)       MAKE SURE WE HAVE SSB                        
         USING SSBD,RF                                                          
         BZ    INITX2                                                           
*                                                                               
         CLC   SSBCNTL,=X'0000'    CHECK ONLINE OR OFFLINE                      
         BNE   INIT010                                                          
         MVI   ONLINE,C'N'                                                      
         CLI   SSODSPAC-SSOOFF(RF),0                                            
         BE    INITX3              DON'T INIT UNTIL WE HAVE A DATASPACE         
*                                                                               
INIT010  GOTO1 =V(LOCKSPC),DUB,X'00040000',FULL                                 
         MVC   MYREF(2),DUB+4                                                   
*                                                                               
         ICM   RF,15,=V(SSB)       MAKE SURE WE HAVE SSB AGAIN                  
         ICM   R1,15,SSBALET       MUST HAVE ALET                               
         BZ    INITX2                                                           
         ST    R1,MYALET                                                        
*                                                                               
         LAM   AR2,AR2,MYALET      ACCESS DMGR DATASPACE                        
         SAC   512                                                              
         SR    R2,R2                                                            
         USING DMDSHDR,R2                                                       
         ICM   R2,15,DHATRCB       GET A(TRACE BUFFER)                          
         BZ    INITX2                                                           
*                                                                               
         CLC   0(6,R2),TRCTABL     IS THERE A TRCTBL                            
         BE    *+10                INITIALISED TRCTBL                           
         MVC   0(256,R2),TRCTAREA  NO MOVE IN MY AREA TO INIT                   
*                                                                               
         MVC   TRCTAREA,0(R2)      MOVE AREA BACK TO MINE                       
*                                                                               
INIT020  ST    R2,ATRCTBL          A(TRACE TABLE COPY)                          
         AHI   R2,256              AREA FOR TRACE TABLE                         
         ST    R2,ATRCBUFF         AND SAVE IT                                  
         SAC   0                                                                
*                                                                               
         GOTO1 =V(LOCKSPC),DUB,X'00040000',FULL                                 
         MVC   MYREF(2),DUB+4                                                   
*                                                                               
         CLI   ONLINE,C'N'         ADVS DEFAULT TO ADV ONLY                     
         BE    *+8                                                              
         MVI   MYREF+1,0                                                        
*                                                                               
INITX1   MVI   INITFLG,C'Y'                                                     
         B     INITX3                                                           
*                                                                               
INITX2   SAC   0                                                                
         MVI   INITFLG,C'X'                                                     
*                                                                               
INITX3   B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS AND LTORG                                *                   
*************************************************************                   
         DC    C'TRCINIT='                                                      
INITFLG  DC    C'N'                                                             
ONLINE   DC    C'Y'                                                             
MYREF    DC    C'??'                                                            
*                                                                               
MYALET   DC    F'0'                                                             
ATRCBUFF DC    F'0'                                                             
ATRCTBL  DC    F'0'                                                             
         LTORG                                                                  
         EJECT                                                                  
*************************************************************                   
*        TRACE TABLE                                        *                   
*************************************************************                   
         DS    0F                                                               
TRCTAREA DS    XL256               AREA FOR HOLDING TABLE                       
         ORG   TRCTAREA            UP TO 32 TRACES                              
TRCTABL  DS    0CL8                                                             
         DC    C'TRCTBL',X'0001'                                                
         DC    C'DNEXT  ',C'N'     DNEXT TRACE CODE 0001                        
         DC    C'SCREEN ',C'N'     SCREEN TRACE     0002                        
         DC    C'ENQCTL ',C'N'     ENQCTL TRACE     0003                        
         DC    C'GETCI  ',C'N'     GETCI  TRACE     0004                        
         DC    C'IOTRACE',C'N'     IO     TRACE     0005                        
         DC    C'EOFTRAC',C'N'     EOF TRACE        0006                        
         DC    C'POST   ',C'N'     POST   TRACE     0007                        
         DC    C'WAIT   ',C'N'     WAIT   TRACE     0008                        
         DC    C'RESUME ',C'N'     RESUME TRACE     0009                        
         DC    C'WSSVR  ',C'N'     WSSVR CALLS      000A                        
         DC    XL2'FFFF'                                                        
         ORG                                                                    
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE                                    *                   
*************************************************************                   
TMPWORKD DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
THISREF  DS    H                                                                
BYTE     DS    X                                                                
TYPE     DS    X                                                                
MODE     DS    X                                                                
*                                                                               
TIMENOW  DS    F                                                                
LENGTH   DS    X                                                                
TMPWORKX EQU   *                                                                
         EJECT                                                                  
         SPACE 2                                                                
PARMS    DSECT                                                                  
PCODE    DS    F                                                                
PDATA    DS    A                                                                
         SPACE 2                                                                
       ++INCLUDE DDTRACED                                                       
       ++INCLUDE DMDSHDR                                                        
       ++INCLUDE FADSECTS                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDTRACE   10/10/13'                                      
         END                                                                    
