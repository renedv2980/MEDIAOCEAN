*          DATA SET PRPRINTIO  AT LEVEL 050 AS OF 05/01/02                      
*CATALP PRINTIO                                                                 
         TITLE 'NEW PRINT I/O MODULE'                                           
PRINTIO  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PRNTWRKX-PRNTWRK,**PRIO**,R9,R8,RR=RA                            
         USING PRNTIOD,RC                                                       
         MVC   USERRD,4(RD)                                                     
         ST    RA,RELO                                                          
         L     RA,0(R1)            USER PASSES A(PRNTBLOK)                      
         USING PRNTBLOK,RA                                                      
         MVC   PBCALLRD,4(RD)      SAVE W/S BACK LINK                           
*                                                                               
         MVI   SPACES,X'40'                                                     
         MVC   SPACES+1(131),SPACES                                             
*                                                                               
         MVI   PBERROR,PBGOOD      CHECK FOR INITIALIZATION                     
         CLI   PBINIT,0                                                         
         BNE   SPEC2                                                            
         BAS   RE,INIT                                                          
         SPACE 2                                                                
*              SPECIAL FUNCTIONS                                                
         SPACE 3                                                                
SPEC2    CLI   PBFUNCT,PBFGET      CHECK FOR SPECIAL FUNCTIONS                  
         BE    FUNCGET             TO GET A RECORD. ALREADY HAVE KEY            
         SPACE 1                                                                
         CLI   PBFUNCT,PBFVAL                                                   
         BE    FUNCVAL             TO VALUE THE RECORD IN PBAIO                 
         CLI   PBFUNCT,PBFRDHI     DO A READHI BEFORE CONTINUING                
         BNE   SPEC4                ALLOWS I/O IN BETWEEN NETIO CALLS           
         BAS   RE,HIGH                                                          
         SPACE 1                                                                
SPEC4    DS    0H                                                               
         B     PRBUY                                                            
         EJECT                                                                  
*                                                                               
*              INITIALIZATION ROUTINES                                          
         SPACE 3                                                                
INIT     NTR1                                                                   
         MVI   PBINIT,1            INITIALIZE NETBLOCK                          
         MVC   PBSELID-8(8),=C'**SELS**'                                        
         MVC   PBDATTYP-8(8),=C'**OPTS**'                                       
         MVC   PBAIO-8(8),=C'**PBIO**'                                          
         MVC   PBKEY-8(8),=C'**KEYS**'                                          
         MVC   PBMODE-8(8),=C'**MODE**'                                         
         MVC   PBERROR-8(8),=C'**ERRS**'                                        
         MVC   PBACTAGY-8(8),=C'**AKEY**'                                       
         MVC   PBGROSS-8(8),=C'**DOLS**'                                        
         MVC   PBBYOUT-8(8),=C'**BYOT**'                                        
*                                                                               
*        L     R2,=V(PRNTVAL)          LATER PRNTVAL IS CORE RES AND            
*        A     R2,RELO             WE WILL LOAD IT IN / FOR NOW                 
*        ST    R2,PBPRTVAL         PRINTIO/PRNTVAL ARE LINKED IN WITH           
*                                  PRMED00                                      
*                                                                               
         OC    PBACOM,PBACOM       IS A(COMFACS) AROUND                         
         BNZ   INIT2               NO - ASSUME WE'RE LINKED OFF LINE            
         MVC   PBXTRNS(XVTYPES-VTYPES),VTYPES     MOVE IN EXTRNS                
         OC    PBCONFID,PBCONFID                  CONFID LINKED                 
         BZ    INIT4                                                            
         LA    R2,WORK                                                          
         USING CND,R2                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNID,PBSELID                                                     
         OC    CNID,CNID           WAS AN ID PROVIDED                           
         BZ    INIT4                                                            
         GOTO1 PBCONFID,DMCB,(10,(R2)),DMCB+4                                   
         MVC   PBSELAGY,CNAGY      THEN WE CAN PROVIDE AGENCY CODE              
         CLI   PBOPNOPT,C'Y'       OPTION TO OPEN FILES                         
         BNE   INIT4                                                            
         L     R3,PBUTL                                                         
         MVC   4(1,R3),CNNSE                                                    
         GOTO1 PBDM,DMCB,=C'OPEN',=CL8'PRINT',FILLIST,PBAIO                     
         B     INIT4                                                            
         EJECT                                                                  
*              COMFACS AND CALLOV FUNCTIONS                                     
         SPACE 3                                                                
INIT2    L     R2,PBACOM           ON LINE - ADDRESSES FROM COMFACS             
         USING COMFACSD,R2                                                      
         MVC   PBDM,CDATAMGR                                                    
         MVC   PBCALLOV,CCALLOV                                                 
         MVC   PBDATCON,CDATCON                                                 
         MVC   PBGETDAY,CGETDAY                                                 
         MVC   PBADDAY,CADDAY                                                   
         MVC   PBHEXOUT,CHEXOUT                                                 
         MVC   PBHELLO,CHELLO                                                   
         MVC   PBGTPROF,CGETPROF                                                
*                                                                               
*        OC    PBPRTVAL,PBPRTVAL   IF NO ADDRESS FOR NETVALUE                   
*        BNZ   INIT4                                                            
*&       OC    PBCALLOV,PBCALLOV   IF CALL OVERLAY EXISTS                       
*        BZ    INIT3                                                            
******   GOTO1 PBCALLOV,DMCB,0,X'D9000A28'    GET PRTVALUE ADDRESS              
*        MVC   PBPRTVAL,DMCB                                                    
         B     INIT4                                                            
         SPACE 1                                                                
INIT3    GOTO1 PBLOADER,DMCB,=C'T00A28  ',0,0  OTHERWISE LOAD IT IN             
         MVC   PBPRTVAL,DMCB+4                                                  
         SPACE 1                                                                
*                                                                               
INIT4    DS    0H                                                               
*                                                                               
INIT6    B     EXIT                                                             
         EJECT                                                                  
*                                                                               
PRBUY    DS    0H                    SET UP KEY TO READ BUY RECORD              
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING PBUYREC,R2                                                       
         MVC   PBUYKAGY,PBSELAGY                                                
         MVC   PBUYKMED,PBSELMED                                                
         MVI   PBUYKRCD,X'20'                                                   
         CLI   PBSELCLT,C'*'                                                    
         BE    PR5                                                              
         CLC   PBSELCLT(3),=C'ALL'                                              
         BE    PR5                                                              
         MVC   PBUYKCLT,PBSELCLT                                                
PR5      CLC   PBSELPRD(3),=C'ALL'                                              
         BE    PR7                                                              
         MVC   PBUYKPRD,PBSELPRD                                                
PR7      CLC   PBSELPUB(3),=C'ALL'                                              
         BE    PR8                                                              
         MVC   PBUYKPUB(5),PBSELPUB                                             
PR8      CLC   PBSELEST,=C'ALL'                                                 
         BE    PR9                                                              
         MVC   PBUYKEST,PBSELEST                                                
PR9      MVC   PBUYKACT,PBSELAPR          ACTIVE PRODUCT                        
         MVC   PBUYKLIN,PBSELINE          LINE NUMBER                           
*                                                                               
         CLC   PBSELPUB(3),=C'ALL'                                              
         BE    RDHIGH                                                           
         OC    PBSELPUB,PBSELPUB          IF READING SINGLE PUB                 
         BZ    RDHIGH                                                           
         MVI   PBUYKRCD,X'21'             THEN READ PASSIVE POINTER             
         LA    R1,PBUYKPRD                                                      
         MVC   0(3,R1),PBSELPUB                                                 
         MVC   3(4,R1),PBSELPRD                                                 
*                                                                               
RDHIGH   BAS   RE,HIGH                                                          
         B     PR10                                                             
*                                                                               
RDSEQ    BAS   RE,SEQ                                                           
*                                                                               
PR10     CLI   KEY+25,X'FF'                                                     
         BE    RDSEQ                                                            
         CLC   KEY(4),KEYSAVE       AGY/MED/REC CODE                            
         BNE   LASTREQ                                                          
PR11     OC    PBSELCLT,PBSELCLT    TEST CLIENT                                 
         BZ    PR15                                                             
         CLC   PBSELCLT,=C'ALL'                                                 
         BE    PR15                                                             
         CLI   PBSELCLT,C'*'        TEST OFFICE                                 
         BNE   PR13                                                             
         CLC   PBUYKCLT,PBEFFCLT                                                
         BE    PR15                                                             
         BAS   RE,GETCLT                                                        
         BNE   EXIT                                                             
         IC    RF,KEY+6                                                         
         LA    RF,1(RF)                                                         
         STC   RF,KEY+6                                                         
         XC    KEY+7(18),KEY+7                                                  
         B     PR5                                                              
PR13     CLC   PBUYKCLT,PBSELCLT                                                
         BNE   LASTREQ                                                          
*                                                                               
PR15     DS    0H                                                               
         MVC   PBEFFAGY,PBUYKAGY    SET EFF AGY                                 
         MVC   PBEFFCLT,PBUYKCLT        EFF CLT                                 
*                                                                               
         LA    R1,KEY+7             TEST PRD=*XX                                
         CLI   KEY+3,X'20'          YES = OTHER AGY DATA/SO SKIP BUY            
         BE    *+8                                                              
         LA    R1,KEY+13                                                        
         CLI   0(R1),C'*'                                                       
         BE    RDSEQ                                                            
*                                                                               
         OC    PBSELPRD,PBSELPRD     TEST PRODUCT                               
         BZ    PR17                                                             
         CLC   PBSELPRD(3),=C'ALL'                                              
         BE    PR17                                                             
         LA    R1,KEY+7                                                         
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    R1,KEY+13                                                        
         CLC   PBSELPRD,0(R1)                                                   
         BNE   RDSEQ                                                            
*                                                                               
PR17     DS    0H                                                               
         LA    R1,KEY+7                                                         
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    R1,KEY+13                                                        
         MVC   PBEFFPRD,0(R1)                                                   
*                                                                               
         OC    PBUYKACT,PBUYKACT      TEST IF ACTIVE PROD POINTER               
         BNZ   RDSEQ                                                            
*                                                                               
         OC    PBSELDIV,PBSELDIV       TEST DIVISION FILTER                     
         BZ    PR20                                                             
         CLC   PBSELDIV(3),=C'ALL'                                              
         BE    PR20                                                             
         BAS   RE,GETPRD               READ PRDREC TO  GET  DIV                 
         CLC   PBSELDIV,PBEFFDIV                                                
         BNE   RDSEQ                                                            
*                                                                               
PR20     OC    PBSELPUB,PBSELPUB         PUBLICATION                            
         BZ    PR25                                                             
         CLC   PBSELPUB(3),=C'ALL'                                              
         BE    PR25                                                             
         LA    R1,KEY+10                                                        
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    R1,KEY+7                                                         
         CLC   PBSELPUB,0(R1)      PUB ONLY                                     
         B     *+14                                                             
PR22     CLC   PBSELPUB(6),0(R1)   PUB/ZONE/EDT                                 
         BNE   RDSEQ                                                            
*                                                                               
PR25     OC    PBSELREG,PBSELREG     REGION/DISTRICT FILTER                     
         BZ    PR27                                                             
         CLC   PBSELREG,=C'ALL'                                                 
         BE    PR27                                                             
         BAS   RE,GETLTL            GET LTLREC                                  
         BAS   RE,GETPRD            GET PROD REC TO GET DIVISION                
         BAS   RE,CHKRGDST         CHECK REG/DIST AGAINST LTLREC                
         BNE   RDSEQ                                                            
*                                                                               
PR27     OC    PBSELEST,PBSELEST       TEST ESTIMATE                            
         BZ    PR30                                                             
         CLC   PBSELEST,=C'ALL'                                                 
         BE    PR30                                                             
         CLC   PBUYKEST,PBSELEST                                                
         BE    PR30                                                             
         BL    RDSEQ                                                            
         OC    PBSELESE,PBSELESE            CHK RANGE                           
         BZ    RDSEQ                                                            
         CLC   PBUYKEST,PBSELESE                                                
         BH    RDSEQ                                                            
*                                                                               
PR30     OC    PBSELSTR,PBSELSTR         TEST INSERTION DATE                    
         BZ    PR32                                                             
         CLC   PBUYKDAT,PBSELSTR                                                
         BL    RDSEQ                                                            
PR32     OC    PBSELEND,PBSELEND                                                
         BZ    PRGETREC                                                         
         CLC   PBUYKDAT,PBSELEND                                                
         BH    RDSEQ                                                            
*                                                                               
PRGETREC DS    0H                                                               
         MVC   AREC,PBAIO                                                       
         MVI   PBFILE,C'P'        PRINT FILE                                    
         BAS   RE,GETREC           GET BUY REC                                  
         BAS   RE,SETSPLT          SET UP FOR R/D SPLIT                         
         PRINT GEN                                                              
PRVAL    GOTO1 PBPRTVAL,DMCB,PRNTBLOK                                           
         PRINT NOGEN                                                            
         MVI   PBMODE,PBPROCBU                                                  
         MVI   PBERROR,0                                                        
         BAS   RE,GO                                                            
         BAS   RE,NXTSPLT          IS THERE ANOTHER R/D SPLIT                   
         BE    PRVAL               YES/RETURN TO PRNTVAL                        
         BAS   RE,HIGH             NO/RESET FOR SEQUENTIAL                      
         B     RDSEQ                                                            
         EJECT                                                                  
*                                                                               
FUNCGET  DS    0H                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY,KEYSAVE                                                      
         BE    FGT5                                                             
         MVI   PBERROR,PBFILERR      I/O ERROR                                  
         B     *+12                                                             
FGT5     BAS   RE,GETREC                                                        
         GOTO1 PBPRTVAL,DMCB,PRNTBLOK                                           
         BAS   RE,GO                                                            
FGTX     B     EXIT                                                             
         SPACE 3                                                                
*                                                                               
FUNCVAL  DS    0H                                                               
         GOTO1 PBPRTVAL,DMCB,PRNTBLOK                                           
         BAS   RE,GO                                                            
         B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
LASTREQ  DS    0H                                                               
         MVI   PBERROR,0                                                        
         MVI   PBMODE,PBREQLST                                                  
         B     EXIT                                                             
         EJECT                                                                  
******************************************************************              
*  MATCHES PUBLICATION OF BUYREC WITH REG/DST ELEM(71) OF LTLREC *              
*          BEFORE EXITING R2 SET TO POINT TO 71 ELEM                            
*                                                                               
SETSPLT  NTR1                                                                   
         BAS   RE,GETLTL                                                        
         L     R6,PBAIO1                                                        
         LA    R6,33(R6)                                                        
         MVI   ELCODE,X'71'                                                     
         CLI   0(R2),X'71'                                                      
         BE    SET5                                                             
         B     SETSEQ                                                           
*                                                                               
NXTSPLT  NTR1                                                                   
         LR    R6,R2       EXPECTS R2 TO BE POINTING TO PREVIOUS '71'           
SETSEQ   BAS   RE,NEXTEL                                                        
         USING PUBDSTEL,R6                                                      
         BNE   SETX                                                             
*                                                                               
SET5     CLC   PBUYKCLT,PUBDCLT    TEST CLT                                     
         BNE   SETSEQ                                                           
         CLC   PBEFFDIV,PUBDDIV     TEST DIV                                    
         BNE   SETSEQ                                                           
         OC    PBSELREG,PBSELREG    IS THERE A REGION FILTER                    
         BZ    SET7                                                             
         CLC   PBSELREG,=C'ALL'                                                 
         BE    SET7                                                             
         CLC   PBSELREG,PUBDREG                                                 
         BNE   SETSEQ                                                           
SET7     CLC   PBSELDST,=C'ALL'        IS THERE A DISTRICT FILTER               
         BE    SET9                                                             
         OC    PBSELDST,PBSELDST                                                
         BZ    SET9                                                             
         CLC   PBSELDST,PUBDDST                                                 
         BNE   SETSEQ                                                           
SET9     MVC   PBVLRD,8(R6)        SET REG/DST FOR PRNTVAL                      
         LA    R2,R6                                                            
         XIT1  REGS=(R2)           RETURN R2                                    
SETX     LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP  R6                                                               
         SPACE                                                                  
***************************************************                             
*  CHECKS REGION/DISTRICT OF REQUEST AGAINST LTLREC                             
*                                                                               
*  IN : EXPECTS LTLREC IN PBAIO1                                                
*                                                                               
*  OUT: R1 NOT ZERO IF NO MATCH                                                 
*                                                                               
CHKRGDST NTR1                                                                   
         L     R6,PBAIO1                                                        
         LA    R6,33(R6)                                                        
         MVI   ELCODE,X'71'                                                     
         CLI   0(R6),X'71'                                                      
         BE    *+8                                                              
CRD10    BAS   RE,NEXTEL                                                        
         USING PUBDSTEL,R6                                                      
         BNE   CRDNO                                                            
         CLC   PBEFFCLT,PUBDCLT                                                 
         BNE   CRD10                                                            
         CLC   PBEFFDIV,PUBDDIV     TEST DIV AGAINST PRDREC                     
         BNE   CRD10                                                            
         CLC   PBSELREG,PUBDREG                                                 
         BNE   CRD10                                                            
CRD15    OC    PBSELDST,PBSELDST                                                
         BZ    CRD25                                                            
         CLC   PBSELDST,PUBDDST                                                 
         BNE   CRD10                                                            
CRD25    DS    0H                                                               
         SR    R1,R1                                                            
         B     *+8                                                              
CRDNO    LA    R1,1                                                             
CRDX     LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP R6                                                                
         EJECT                                                                  
*                                                                               
* GETS LTLREC AND PUTS IT INTO PBAIO1                                           
*                                                                               
*                                                                               
GETLTL   NTR1                                                                   
         DS    0H              TEST IF LTLREC ALREADY THERE                     
         MVC   SAVEKEY,KEY                                                      
         LA    R2,SAVEKEY                                                       
         USING PBUYKEY,R2                                                       
         LA    R1,PBUYKEY+10       X'20'KEY-PRD/PUB                             
         CLI   PBUYKRCD,X'20'                                                   
         BE    *+8                                                              
         LA    R1,PBUYKEY+7        X'21'KEY-PUB/PRD                             
         L     R5,PBAIO1                                                        
         USING LTLREC,R5                                                        
         CLC   LTLKPUB(5),0(R1)    PUB/Z/ED OF BUY= OF LTL                      
         BE    LTLX                                                             
         SPACE                                                                  
         XC    KEY,KEY          REQUIRED LTLREC NOT THERE/GET IT                
         LA    R5,KEY                                                           
         USING LTLKEY,R5                                                        
         MVC   LTLKMED,PBEFFMED                                                 
         MVC   LTLKPUB(6),0(R1)        PUB/Z/ED                                 
         MVC   LTLKAGY,PBEFFAGY                                                 
         MVI   LTLKCOD,X'85'                                                    
         MVI   PBFILE,C'U'         SET FOR PUB FILE                             
         BAS   RE,HIGH                                                          
         CLC   KEY(11),KEYSAVE     IF NO LTLREC                                 
         BE    LTL5                                                             
         L     R2,PBAIO1                                                        
         XC    0(40,R2),0(R2)      THEN CLEAR LTLREC X'71' AREA                 
         B     LTL10                                                            
LTL5     MVC   AREC,PBAIO1                                                      
         BAS   RE,GETREC                                                        
LTL10    MVC   KEY,SAVEKEY         RESET KEY                                    
         MVI   PBFILE,C'P'         RESET PRINT FILE                             
         BAS   RE,HIGH                RESTORE SEQUENTIAL READ                   
LTLX     B     EXIT                                                             
         DROP R2,R5                                                             
         EJECT                                                                  
* GET CLIENT RECORD                                                             
*                                                                               
GETCLT   NTR1                                                                   
         MVC   SAVEKEY,KEY                                                      
         LA    R2,SAVEKEY                                                       
         USING PBUYKEY,R2                                                       
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY                                                   
         MVI   KEY+3,2                                                          
         BAS   RE,READ                                                          
         MVC   AREC,IO1                                                         
         BAS   RE,GETREC                                                        
         MVC   KEY,SAVEKEY                                                      
         BAS   RE,HIGH                                                          
         B     EXIT                                                             
         EJECT                                                                  
* GET PRODUCT RECORD                                                            
*                                                                               
GETPRD   NTR1                                                                   
         MVC   SAVEKEY,KEY                                                      
         LA    R2,SAVEKEY                                                       
         USING PBUYKEY,R2                                                       
*                                                                               
         LA    R1,PBUYKEY+7        X'20'KEY-PRD/PUB/Z/ED                        
         CLI   PBUYKRCD,X'20'                                                   
         BE    *+8                                                              
         LA    R1,PBUYKEY+13       X'21'KEY-PUB/Z/ED/PRD                        
         CLC   PBEFFPRD,0(R1)                                                   
         BNE   IPRD10                                                           
         CLC   PRDKAGY(3),PBEFFAGY       AGY/MED                                
         BNE   IPRD10                                                           
         CLC   PRDKCLT,PBEFFCLT          CLT                                    
         BE    IPRDX                                                            
IPRD10   DS    0H                  PRDREC NOT THERE/GET IT                      
         XC    KEY,KEY                                                          
         MVC   KEY(3),PBUYKEY      AGY/MED                                      
         MVI   KEY+3,6                                                          
         MVC   KEY+4(3),PBUYKCLT                                                
         MVC   KEY+7(3),0(R1)      PRD                                          
         BAS   RE,READ                                                          
         MVC   AREC,IO1                                                         
         BAS   RE,GETREC                                                        
         LA    R3,AREC                                                          
         USING PPRDREC,R3                                                       
         MVC   PRDKAGY(3),PPRDKAGY                                              
         MVC   PRDKCLT,PPRDKCLT                                                 
         MVC   PBEFFDIV,PPRDDIV                                                 
*                                                                               
         MVC   KEY,SAVEKEY                                                      
         BAS   RE,HIGH                                                          
IPRDX    B     EXIT                                                             
         EJECT                                                                  
*                                                                               
GO       NTR1                                                                   
         L     RF,PBHOOK                                                        
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF                                                            
         XIT1                                                                   
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'PRTDIR  '         DIRECTORIES                         
         CLI   PBFILE,C'U'                                                      
         BNE   *+10                                                             
         MVC   FILE(8),=C'PUBDIR  '                                             
         ZIC   R4,PBDMGOPT         OPTIONAL DATAMGR BITS                        
         GOTO1 PBDM,DMCB,((R4),COMMAND),FILE,KEY,KEY,0                          
         BAS   RE,DMTRACE                                                       
         BAS   RE,DMCHECK                                                       
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         ZIC   R4,PBDMGOPT         OPTIONAL DATAMGR BITS                        
         MVC   FILE(8),=C'PRTFILE '     FILE                                    
         MVC   PBDTADSP,=H'33'                                                  
         LA    R3,KEY+27                                                        
         CLI   PBFILE,C'P'                                                      
         BE    GETREC3                                                          
         MVC   FILE(8),=C'PUBFILE '                                             
GETREC3  CLI   PBNOWRIT,C'N'       SEE IF WRITE INHIBIT IS SET                  
         BE    GETREC4             IF SO, DONT READ FOR UPDATE                  
         LA    R4,128(R4)          (TURN ON X'80' BIT)                          
         SPACE 1                                                                
GETREC4  GOTO1 PBDM,DMCB,((R4),=C'GETREC'),FILE,(R3),AREC,DMWORK                
         BAS   RE,DMCHECK                                                       
         B    EXIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0             OK                                           
         BER   RE                                                               
         CLI   8(R1),2             RECORD IS DELETED OK TOO                     
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
DMTRACE  CLI   PBTRCOPT,C'Y'                                                    
         BNER  RE                                                               
         NTR1                                                                   
         L     R2,AREC                                                          
         MVC   P,SPACES                                                         
         MVC   P(8),COMMAND                                                     
         MVC   P+8(8),FILE                                                      
         GOTO1 PBHEXOUT,PARAS,DMCB+8,P+16,1,=C'TOG'                             
         MVC   P+20(33),KEY                                                     
         GOTO1 PBHEXOUT,PARAS,KEY,P+55,33,=C'TOG'                               
         GOTO1 PBPRINT,PARAS,PFILL,=C'BL01'                                     
         MVC   P,SPACES                                                         
         MVC   P+20(33),0(R2)                                                   
         GOTO1 PBHEXOUT,PARAS,0(R2),P+55,33,=C'TOG'                             
         GOTO1 PBPRINT,PARAS,PFILL,=C'BL02'                                     
         MVC   P,SPACES                                                         
         B    EXIT                                                              
         SPACE 1                                                                
         GETEL (R6),PBDTADSP,ELCODE                                             
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
TRAPERR  DS    0H                                                               
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
*              CONSTANTS & LTORG                                                
         SPACE 3                                                                
VTYPES   DS    0F                                                               
         DC    V(ADDAY)                                                         
         DC    A(0)                                                             
         DC    V(CLPACK)                                                        
         DC    V(CLUNPK)                                                        
         DC    V(CONFID)                                                        
         DC    V(DATAMGR)                                                       
         DC    V(DATCON)                                                        
         DC    V(GETDAY)                                                        
         DC    V(HEXOUT)                                                        
         DC    V(HELLO)                                                         
         DC    V(GETPROF)                                                       
         DC    V(PRNTVAL)                                                       
XVTYPES  DS    0C                  END OF VTYPES LIST                           
         SPACE 1                                                                
FILLIST  DS    0F                                                               
         DC    CL8'NSPTFILE'                                                    
         DC    CL8'NSPTDIR'                                                     
         DC    CL8'NSTAFILE'                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL8'NDEMDIRA'                                                    
         DC    CL8'NDEMDIRN'                                                    
         DC    CL8'NDEMFILA'                                                    
         DC    CL8'NDEMFILN'                                                    
         DC    CL8'NHUTFL'                                                      
         DC    CL8'NPAVFLA'                                                     
         DC    CL8'NPAVFLN'                                                     
         DC    CL8'NPAVDIR'                                                     
         DC    CL8'NPAVFIL'                                                     
         DC    CL8'NUNTFIL'                                                     
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
PRNTIOD  DSECT                                                                  
PRNTWRK  DS    0C                                                               
RELO     DS    A                                                                
AREC     DS    A                   GETREC READS INTO AREC                       
DMCB     DS    6F                                                               
FULL     DS    F                                                                
USERRD   DS    F                                                                
DUB      DS    D                                                                
DOUBLE   DS    D                                                                
KEY      EQU   PBKEY                                                            
KEYSAVE  EQU   PBKEYSV                                                          
SAVEKEY  DS    CL33                                                             
SPACES   DS    CL132                                                            
WORK     DS    CL64                                                             
DMWORK   DS    CL96                                                             
BYTE     DS    CL1                                                              
PFILL    DS    CL1                                                              
FILE     DS    CL8                                                              
COMMAND  DS    CL8                                                              
PFIL     DS    C                                                                
P        DS    CL132                                                            
PARAS    DS    6F                                                               
ELCODE   DS    CL1                                                              
HOLDEFLT DS    CL1                 HOLD EST FILTER FOR ANDING                   
PRDKAGY  DS    CL3                                                              
PRDKCLT  DS    CL3                                                              
IO1      DS    CL1000                                                           
*                                                                               
PRNTWRKX EQU   *                                                                
         EJECT                                                                  
* THE FOLLOWING ARE INCLUDED                                                    
*                                                                               
*      PRNTBLOK                                                                 
*      PBUYREC                                                                  
*      PAGYREC                                                                  
*      PCLTREC                                                                  
*      PDIVREC                                                                  
*      PREGREC                                                                  
*      PDSTREC                                                                  
*      PPRDREC                                                                  
*      PESTREC                                                                  
*      PUBREC                                                                   
*      LTLREC                                                                   
*      DDCNTRL                                                                  
*      DDCOMFACS                                                                
         PRINT OFF                                                              
       ++INCLUDE PRNTBLOK                                                       
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PAGYREC                                                        
       ++INCLUDE PCLTREC                                                        
       ++INCLUDE PDIVREC                                                        
       ++INCLUDE PREGREC                                                        
       ++INCLUDE PDSTREC                                                        
       ++INCLUDE PPRDREC                                                        
       ++INCLUDE PESTREC                                                        
       ++INCLUDE PUBREC                                                         
       ++INCLUDE LTLREC                                                         
       ++INCLUDE PUBDSTEL                                                       
       ++INCLUDE DDCNTRL                                                        
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'050PRPRINTIO 05/01/02'                                      
         END                                                                    
