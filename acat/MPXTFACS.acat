*          DATA SET MPXTFACS   AT LEVEL 015 AS OF 05/01/02                      
*CATALP MPXTFAC                                                                 
         TITLE  'MPXTFAC - XTAB FILE EXTRACT FACILITY'                          
         SPACE 2                                                                
*        PARAMETER LIST                                                         
*                                                                               
*                                                                               
*  PARAM 1  BYTE 0-3   A(COMMAND)                                               
*                    ** BOOL -   EVALUATE REVERSE POLISH BOOLEAN                
*                                OPERATOR/OPERAND STRING                        
*                    ** COUNT -  COUNT BITS IN RESULT BIT STRING                
*                                AND GET WEIGHTED COUNTS                        
*                    ** SETVAR - ESTABLISH A USER VARIABLE                      
*                    ** RANGE -  CREATE SEPARATE BIT STRINGS IN                 
*                                STACK AREA FOR EACH RANGE PAIR                 
*                                GIVEN FOR A SPECIFIED VARIABLE                 
*                                                                               
*  PARAM 2  BYTE 0-3   A(MPXBLK)  CONTROL BLOCK                                 
*  PARAM 3  BYTE 0-3   A(INPUT)   STRING FOR BOOL COMMAND                       
*                                                                               
         PRINT NOGEN                                                            
MPXTFAC  CSECT                                                                  
         NMOD1 100,MPXTFAC                                                      
         USING WKD,RC                                                           
*                                                                               
         L     R9,4(R1)                                                         
         USING MPXBLKD,R9                                                       
         L     RA,MPXBFIL          A(FILE DESCRIPTOR)                           
         USING MPXFILD,RA                                                       
         ST    R1,SAVR1                                                         
         MVI   MPXBERR,0                                                        
         MVI   MPXBERR2,0                                                       
*                                                                               
         L     RF,0(R1)                                                         
         MVC   COMMAND,0(RF)                                                    
*                                                                               
         CLC   =C'BOOL',COMMAND                                                 
         BE    BOOLS                                                            
         CLC   =C'COUNT',COMMAND                                                
         BE    COUNT                                                            
         CLC   =C'SETVAR',COMMAND                                               
         BE    SETVAR                                                           
         CLC   =C'RANGE',COMMAND                                                
         BE    RANGE                                                            
         DC    H'0'                                                             
*                                                                               
EXIT     DS    0H                                                               
         L     R1,SAVR1                                                         
         CLI   4(R1),X'FF'         TEST ALWAYS TO DUMP                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   MPXBERR,0                                                        
         BE    EXITX                                                            
         CLI   4(R1),0             TEST TO DUMP ON ERROR                        
         BE    EXITX                                                            
         DC    H'0'                                                             
EXITX    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
*              ERROR EQUATES                                                    
*                                                                               
STRNGERR EQU   01                                                               
STKERR   EQU   02                                                               
NFNDERR  EQU   03                                                               
RECLERR  EQU   04                                                               
WGTRERR  EQU   05                                                               
WCNTERR  EQU   06                                                               
RCNTERR  EQU   07                                                               
CTLERR   EQU   08                                                               
         SPACE 2                                                                
*              OPERAND TYPE EQUATES                                             
*                                                                               
PCON     EQU   01                  PUNCH                                        
DBV      EQU   02                  DEFINED BINARY VARIABLE                      
DAV      EQU   03                  DEFINED ARITHMETIC VARIABLE                  
UBV      EQU   04                  USER BINARY VARIABLE                         
UAV      EQU   05                  USER ARITHMETIC VARIABLE                     
*                                                                               
         SPACE 2                                                                
*              EXECUTED INSTRUCTIONS                                            
*                                                                               
BSORX    OC    0(0,R5),0(R4)                                                    
BSANDX   NC    0(0,R5),0(R4)                                                    
BSEORX   XC    0(0,R5),0(R4)                                                    
BSPMVC   MVC   0(0,R3),2(R8)                                                    
BSPRMVC  MVC   1(0,R3),0(R3)                                                    
SDAVMVC  MVC   0(0,R4),2(R8)                                                    
SDAVRMVC MVC   0(0,R4),4(R8)                                                    
CTRICM   ICM   R1,0,0(RE)                                                       
CTRSTCM  STCM  R1,0,0(RE)                                                       
BSNICM1  ICM   RE,0,0(RF)                                                       
BSNICM2  ICM   R1,0,0(R2)                                                       
*                                                                               
*              DATA CONSTANT LISTS                                              
*                                                                               
BSXCTAB  DC    X'FF80C0E0F0F8FCFE'                                              
ICMSKS   DC    X'0103070F'         MASKS FOR EXECUTED ICM INST                  
RMSKS    DC    X'8040201008040201'                                              
         DS    0F                                                               
DVSRTAB  DC    AL4(1,10,100,1000,10000,100000)                                  
RNDRTAB  DC    AL4(0,5,50,500,5000,50000)                                       
*                                                                               
         EJECT                                                                  
*                                  BOOLEAN BIT STRING OPERATIONS                
         SPACE 2                                                                
BOOLS    DS    0H                                                               
         L     R2,8(R1)            A(INPUT STRING)                              
         LA    R2,0(R2)                                                         
*                                                                               
BOOLS2   DS    0H                                                               
         L     R3,MPXBSTK          A(STACK AREA)                                
         LA    R3,0(R3)                                                         
         ZICM  RF,MPXBSTMX         MAX LEVELS OF STACKING                       
         BNZ   *+8                                                              
         LA    RF,4                DEFAULT IS 4                                 
         MH    RF,MPXFBSLN                                                      
         AR    R3,RF                                                            
         ST    R3,LAPOS            POINT TO END OF STACK                        
         L     R3,MPXBSTK                                                       
*                                                                               
BS10     DS    0H                                                               
         CLI   0(R2),0             EOL                                          
         BE    BS12                                                             
*                                                                               
         ZIC   RF,0(R2)            INDEX INTO BRANCH TABLE                      
         N     RF,=F'15'                                                        
         SLL   RF,2                                                             
*                                                                               
         TM    0(R2),X'80'         80 = OPERATOR                                
         BNZ   BS11                                                             
*                                  HAVE OPERAND                                 
         C     R3,LAPOS            TEST ROOM FOR THIS OPERAND                   
         BL    BS10B                                                            
         MVI   MPXBERR,STKERR      TOO MANY STACK LEVELS                        
         B     BSX                                                              
*                                                                               
BS10B    DS    0H                                                               
         CLI   0(R2),MAXOPNDS                                                   
         BH    BSSTRERR                                                         
         LA    RF,OPANDBR-4(RF)                                                 
         OC    0(4,RF),0(RF)                                                    
         BZ    BSSTRERR                                                         
         BASR  RE,RF                                                            
         CLI   MPXBERR,0                                                        
         BNE   BSX                                                              
         AH    R3,MPXFBSLN         BUMP STACK POSITION                          
         AH    R2,OPNDLEN          NEXT ENTRY                                   
         B     BS10                                                             
*                                                                               
BS11     DS    0H                                                               
         CLI   0(R2),MAXOPTRS                                                   
         BH    BSSTRERR                                                         
         LA    RF,OPTORBR-4(RF)                                                 
         OC    0(4,RF),0(RF)                                                    
         BZ    BSSTRERR                                                         
         BASR  RE,RF                                                            
         B     BS10                                                             
*                                                                               
BS12     DS    0H                                                               
         SH    R3,MPXFBSLN                                                      
         C     R3,MPXBSTK          TEST STRING OK                               
         BNE   BSSTRERR                                                         
*                                                                               
         CLI   COMMAND,C'S'        TEST SETVAR COMMAND                          
         BNE   BS13                                                             
*                                                                               
         L     R5,SETVA            A(VAR DESC)                                  
         USING MPXVARD,R5                                                       
         CLI   MPXVDLEN,0          TEST BINARY VARIABLE                         
         BNE   BS12D                                                            
*                                  FOR BINARY VAR JSUT MOVE                     
         LH    R1,MPXFBSLN         RESULT STRING TO VAR STRING                  
         L     RF,MPXVSTR                                                       
         MOVE  ((RF),(R1)),(R3)                                                 
         B     BSX                                                              
*                                                                               
BS12D    DS    0H                  COMPLEX (BOOLEAN/ARITH) VARIABLE             
         B     BSX                 NOTHING TO DO HERE                           
*                                                                               
BS13     DS    0H                  'BOOL' COMMAND                               
         MVI   NSTRNGS,1           FOR FILTER ROUTINE                           
*                                                                               
BS14     DS    0H                  APPLY ANY FILTERS                            
         ICM   R6,15,MPXBFLST      TEST ANY FILTERS ACTIVE                      
         BZ    BS24                NO                                           
*                                                                               
BS15     DS    0H                                                               
         CLI   0(R6),X'FF'         EOL                                          
         BE    BS24                                                             
         CLI   0(R6),0             TEST THIS FILTER ACTIVE                      
         BE    BS20                NO - GET NEXT                                
*                                                                               
         L     R3,MPXBSTK          TOP OF STACK                                 
         ZIC   R0,NSTRNGS          NO. OF STRINGS BEING FILTERED                
*                                                                               
BS15B    DS    0H                                                               
         LR    R5,R3                                                            
         ICM   R4,7,0(R6)         ADDRESS OF FILTER                             
         BZ    BS20                                                             
*                                                                               
         LH    RF,MPXFBSLN                                                      
*                                                                               
BS16     DS    0H                                                               
         LR    R8,RF                                                            
         CH    R8,=H'256'                                                       
         BNH   *+8                                                              
         LA    R8,256                                                           
         BCTR  R8,R0                                                            
         EX    R8,BSANDX                                                        
         LA    R8,1(R8)                                                         
         AR    R4,R8                                                            
         AR    R5,R8                                                            
         SR    RF,R8                                                            
         BP    BS16                                                             
*                                                                               
         AH    R3,MPXFBSLN         NEXT STACK POSITION                          
         BCT   R0,BS15B                                                         
*                                                                               
BS20     DS    0H                                                               
         LA    R6,4(R6)            NEXT FILTER                                  
         B     BS15                                                             
*                                                                               
BS24     DS    0H                                                               
BSX      DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
BSSTRERR DS    0H                                                               
         MVI   MPXBERR,STRNGERR                                                 
         B     BSX                                                              
         SPACE 3                                                                
OPANDBR  DS    0F                  OPERAND BRANCH TABLE                         
         B     BSCP        01      PUNCH CONDITION                              
         B     BSCDBV      02      DEFINED BINARY VAR.                          
         B     BSCDAV      03      DEFINED ARITH. VAR.                          
         B     BSCUBV      04      USER BINARY VAR.                             
         B     BSCUAV      05      USER ARITH. VARIABLE                         
         DC    F'0'        06                                                   
         DC    F'0'        07                                                   
         DC    F'0'        08                                                   
         DC    F'0'        09                                                   
         DC    F'0'        10                                                   
         B     BSGIV       11      GIVEN STRING ADDRESS                         
         B     BSIMED      12      IMMEDIATE OPERAND (TESTING ONLY)             
*                                                                               
MAXOPNDS EQU   (*-OPANDBR)/4                                                    
*                                                                               
OPTORBR  DS    0F                  OPERATOR BRANCH TABLE                        
         B     BSOR        81      OR  OPERATOR                                 
         B     BSAND       82      AND                                          
         B     BSEOR       83      EOR                                          
         B     BSNOT       84      NOT                                          
         B     BSWGT       85      WEIGHT                                       
         B     BSCARD      86      SET CARD POINTER                             
         B     BSCOL       87      SET COLUMN POINTER                           
*                                                                               
MAXOPTRS EQU   (*-OPTORBR)/4+X'80'                                              
*                                                                               
         EJECT                                                                  
BSOR     DS    0H                                                               
         LA    R7,BSORX            OR EXEC INST                                 
         B     BSOPTR                                                           
         SPACE 2                                                                
BSAND    DS    0H                                                               
         LA    R7,BSANDX           AND EXEC INST                                
         B     BSOPTR                                                           
         SPACE 2                                                                
BSEOR    DS    0H                                                               
         LA    R7,BSEORX           EOR EXEC INST                                
         B     BSOPTR                                                           
         SPACE 2                                                                
BSOPTR   DS    0H                                                               
         LR    R4,R3                                                            
         SH    R4,MPXFBSLN         R4 = FIRST OPERAND                           
         LR    R5,R4                                                            
         SH    R5,MPXFBSLN         R5 = SECOND                                  
*                                                                               
         LH    RF,MPXFBSLN                                                      
BSOPTR2  DS    0H                                                               
         LR    R8,RF                                                            
         CH    R8,=H'256'                                                       
         BNH   *+8                                                              
         LA    R8,256                                                           
         BCTR  R8,R0                                                            
         EX    R8,0(R7)                                                         
         LA    R8,1(R8)                                                         
         AR    R4,R8                                                            
         AR    R5,R8                                                            
         SR    RF,R8                                                            
         BP    BSOPTR2                                                          
*                                                                               
         LA    R2,1(R2)            NEXT STRING ENTRY                            
         SH    R3,MPXFBSLN         DECREMENT STACK POINTER                      
         BR    RE                                                               
         SPACE 2                                                                
BSNOT    DS    0H                  LOGICAL NOT                                  
         LR    R4,R3                                                            
         SH    R4,MPXFBSLN                                                      
         LH    R0,MPXFBSLN         FOR BCT                                      
         BCTR  R0,R0               SAVE LAST BYTE FOR LATER                     
*                                                                               
         XI    0(R4),X'FF'                                                      
         LA    R4,1(R4)                                                         
         BCT   R0,*-8                                                           
*                                  LAST BYTE - DONT SET ON EXTRA BITS           
         L     R0,MPXFNRES                                                      
         SR    R1,R1                                                            
         SRDL  R0,3                                                             
         SRL   R1,29                                                            
         LA    R1,BSXCTAB(R1)      USE SPECIAL XC BYTE                          
         XC    0(1,R4),0(R1)                                                    
         LA    R2,1(R2)            NEXT STRING ENTRY                            
         BR    RE                                                               
         SPACE 2                                                                
BSWGT    DS    0H                  APPLY WEIGHT                                 
         SH    R3,MPXFBSLN                                                      
         C     R3,MPXBSTK          TEST STRING OK                               
         BNE   BSSTRERR                                                         
*                                                                               
         MVC   VARMULT,1(R2)       SET MULTIPLIER                               
         L     R5,SETVA                                                         
         USING MPXVARD,R5                                                       
         MVC   WDLEN+1(1),MPXVDLEN   SET DATA LENGTH                            
         DROP  R5                                                               
         LH    R8,WDLEN                                                         
         IC    R8,ICMSKS-1(R8)     SET MASK FOR ICM'S                           
         LA    RF,CTR70            SET BRANCH WITHIN CTR ROUTINE                
         ST    RF,CTRBRNCH                                                      
         LR    R0,RE                                                            
         BAS   RE,CTR                                                           
         LR    RE,R0                                                            
         LA    R2,5(R2)            NEXT STRING ENTRY                            
         CLI   0(R2),0             IF AT END                                    
         BNE   *+8                                                              
         AH    R3,MPXFBSLN         RESTORE STACK POINTER                        
         BR    RE                                                               
         SPACE 2                                                                
BSCARD   DS    0H                  SET CARD POINTER                             
         MVC   MPXBCRDN,1(R2)                                                   
         LA    R2,3(R2)                                                         
         BR    RE                                                               
         SPACE 2                                                                
BSCOL    DS    0H                  SET COLUMN POINTER                           
         MVC   MPXBCOLN,1(R2)                                                   
         LA    R2,3(R2)                                                         
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
BSCP     DS    0H                  PUNCH SPEC                                   
         MVC   OPNDLEN,=H'2'       TOTAL OPERAND LENGTH                         
         MVC   WORK(2),MPXBCRDN    CARD NO                                      
         MVC   WORK+2(2),MPXBCOLN  COL NO                                       
         MVC   WORK+4(1),1(R2)     PUNCH                                        
         B     BSCON                                                            
         SPACE 2                                                                
*                                                                               
BSCDBV   DS    0H                  DEFINED BINARY VARIABLE                      
         MVC   OPNDLEN,=H'3'       TOTAL OPERAND LENGTH                         
         XC    WORK,WORK                                                        
         MVC   WORK+2(2),1(R2)     VARIABLE NO.                                 
         MVI   WORK+4,X'10'        TYPE                                         
         B     BSCON                                                            
         SPACE 2                                                                
*                                                                               
BSCDAV   DS    0H                  DEFINED ARITHMETIC VARIABLE                  
         XC    WORK,WORK                                                        
         MVC   WORK+2(2),1(R2)     VARIABLE NO.                                 
         MVI   WORK+4,X'11'        TYPE                                         
         B     BSCON                                                            
         SPACE 2                                                                
*                                                                               
BSCUBV   DS    0H                  USER BINARY VARIABLE                         
         SR    R5,R5                                                            
         ICM   R5,3,1(R2)          VARIABLE NO.                                 
         MH    R5,=Y(MPXVARL)                                                   
         A     R5,MPXBVLST                                                      
*                                                                               
         LA    R5,MPXVSTR-MPXVARD(R5)      A(STRING)                            
         LA    RF,3                TOTAL OPERAND LENGTH                         
         B     BSGIV2              TREAT AS A 'GIVEN' STRING                    
         SPACE 2                                                                
*                                                                               
BSCUAV   NTR1                       USER ARITHMETIC VARIABLE                    
         SR    R5,R5                                                            
         ICM   R5,3,1(R2)           VARIABLE NO.                                
         MH    R5,=Y(MPXVARL)                                                   
         A     R5,MPXBVLST         POINT TO VARIABLE DESC                       
         USING MPXVARD,R5                                                       
         MVI   WDLEN,0                                                          
         MVC   WDLEN+1(1),MPXVDLEN    SET DATA LENGTH                           
         ZIC   RF,3(R2)            NO. OF RANGE PAIRS                           
         SLL   RF,1                X2                                           
         MH    RF,WDLEN            X DATA LEN                                   
         LA    RF,4(RF)            + CODE + VAR NO. + COUNT                     
         STH   RF,OPNDLEN          TOTAL OPERAND LENGTH                         
         ST    R2,SAVR2                                                         
*                                                                               
         LR    RE,R3               CLEAR STACK ENTRY                            
         LH    RF,MPXFBSLN                                                      
         XCEF                                                                   
*                                                                               
         L     RF,MPXVSTR          A(STRING)                                    
         L     R0,MPXFNRES         RESP COUNT (FOR BCT)                         
*                                                                               
BSCUAV4  DS    0H                                                               
         BAS   RE,BSNCHK           CHECK VALUE AGAINST RANGES                   
         AH    RF,WDLEN            NEXT VALUE                                   
         BCT   R0,BSCUAV4                                                       
*                                                                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
*                                  CONDITION SPEC                               
         SPACE 2                                                                
BSCON    NTR1                                                                   
         SPACE 2                                                                
         MVC   CONDTYP,0(R2)       SET CONDITION TYPE                           
         XC    WDLEN,WDLEN         CLEAR DATA LENGTH                            
*                                                                               
         XC    KEY,KEY                                                          
         LA    R7,KEY                                                           
         USING XVTRECD,R7                                                       
         MVC   XVTKRCD,=X'0301'    RECORD CODE                                  
         MVC   XVTKCRD(5),WORK     SPEC DESC                                    
*                                                                               
         SR    R6,R6               RESPONDENT/BYTE COUNTER                      
         XC    RSTOT,RSTOT                                                      
         BAS   RE,BSHIGH                                                        
         B     BSCON4B                                                          
BSCON4   DS    0H                                                               
         BAS   RE,BSSEQ                                                         
BSCON4B  DS    0H                                                               
         CLC   KEY(XVTKSEQ-XVTKEY),KEYSAVE                                      
         BNE   BSCON30                                                          
         BAS   RE,BSGET                                                         
         L     R7,MPXBAREC       A(REC)                                         
         LA    R8,XVTELEMS                                                      
         DROP  R7                                                               
*                                                                               
         MVI   ELCODLO,X'01'                                                    
         MVI   ELCODHI,X'03'                                                    
         BAS   RE,NEXTEL2                                                       
         B     BSCON6B                                                          
BSCON6   DS    0H                                                               
         BAS   RE,NEXTEL                                                        
BSCON6B  DS    0H                                                               
         BNE   BSCON4                                                           
*                                                                               
         CLI   0(R8),X'01'         CONTROL ELEM                                 
         BNE   BSCON6F                                                          
         USING XVTCNTEL,R8                                                      
         MVC   RSTOT,XVTCOUNT        SET COUNT                                  
         CLI   CONDTYP,DAV           OTHER CTL SETS ONLY FOR DAV                
         BNE   BSCON6                                                           
         MVC   WDLEN+1(1),XVTCDLEN   DATA LENGTH                                
*                                                                               
         ZIC   RF,3(R2)             NUMBER OF RANGE PAIRS                       
         SLA   RF,1                 X 2                                         
         BNZ   *+12                CANNOT BE ZERO                               
         MVI   MPXBERR,STRNGERR                                                 
         B     BSCONX                                                           
         MH    RF,WDLEN             X DATA LENGTH                               
         LA    RF,4(RF)             + 4 (OPND,VNO,COUNT)                        
         STH   RF,OPNDLEN           = OPERAND LENGTH                            
         LR    RE,R3               CLEAR STACK ENTRY                            
         LH    RF,MPXFBSLN                                                      
         XCEF                                                                   
*                                                                               
         B     BSCON6                                                           
*                                                                               
BSCON6F  DS    0H                                                               
         CLI   0(R8),X'02'         NORMAL DATA ELEM                             
         BNE   BSCON20                                                          
         USING XVTSTREL,R8                                                      
         CLI   CONDTYP,DAV         TEST DEFINED ARITHMETIC VAR                  
         BE    BSCON12                                                          
*                                  PUNCH SPEC                                   
         ZIC   RF,1(R8)                                                         
         SH    RF,=H'2'                                                         
         AR    R6,RF                                                            
         CH    R6,MPXFBSLN         TEST HAVE TOO MANY BYTES                     
         BNH   BSCON8                                                           
*                                                                               
BSCON7   DS    0H                                                               
         MVI   MPXBERR,RECLERR                                                  
         B     BSCONX                                                           
*                                                                               
*                                                                               
BSCON8   DS    0H                                                               
         BCTR  RF,R0                                                            
         EX    RF,BSPMVC           MOVE TO RECEIVING AREA                       
         LA    R3,1(R3,RF)         BUMP AREA POINTER                            
         B     BSCON6                                                           
*                                                                               
BSCON12  DS    0H                  NUMERIC FIELD - NORMAL ELEM                  
         OC    WDLEN,WDLEN         TEST HAD CONTROL ELEM                        
         BZ    BSCON27             NO - ERROR                                   
         ZIC   RF,1(R8)                                                         
         AR    RF,R8                                                            
         ST    RF,ENDEL            END OF ELEM                                  
*                                                                               
         LA    RF,2(R8)            DATA                                         
*                                                                               
BSCON13  DS    0H                                                               
         BAS   RE,BSNCHK                                                        
         AH    RF,WDLEN            NEXT ITEM                                    
         C     RF,ENDEL            TEST VS END                                  
         BL    BSCON13                                                          
*                                                                               
         B     BSCON6                                                           
*                                  REPEATER ELEMENT                             
BSCON20  DS    0H                                                               
         USING XVTRPTEL,R8                                                      
         CLI   CONDTYP,DAV         TEST DEFINED ARITH VARIABLE                  
         BE    BSCON26                                                          
*                                  PUNCH SPEC                                   
         ZICM  R1,XVTRPTNO,2       NO. OF REPETITIONS                           
         LR    R5,R1               SAVE FOR POINTER ADJUST                      
         AR    R6,R1                                                            
         CH    R6,MPXFBSLN         TEST TOO MANY BYTES                          
         BH    BSCON7                                                           
*                                                                               
         MVC   0(1,R3),XVTRPTND    REPETEND                                     
         BCTR  R1,R0                                                            
         LA    RF,1(R3)                                                         
         LR    RE,R3                                                            
         MOVE  ((RF),(R1)),(RE)                                                 
         AR    R3,R5                                                            
         B     BSCON6                                                           
*                                                                               
BSCON26  DS    0H                  NUMERIC FIELD-REPEATER ELEM                  
         OC    WDLEN,WDLEN         TEST HAD CONTROL ELEM                        
         BZ    BSCON27             NO - ERROR                                   
         ZICM  R0,2(R8),2          NO. OF REPETITIONS                           
         LA    RF,4(R8)            REPETEND                                     
         BAS   RE,BSNCHK                                                        
         BCT   R0,*-4                                                           
*                                                                               
         B     BSCON6                                                           
*                                                                               
BSCON27  DS    0H                                                               
         MVI   MPXBERR,CTLERR      CONTROL ELEM ERROR                           
         B     BSCONX                                                           
*                                                                               
BSCON30  DS    0H                                                               
         LTR   R6,R6                                                            
         BNZ   *+12                                                             
         MVI   MPXBERR,NFNDERR                                                  
         B     BSCONX                                                           
*                                                                               
         CLI   CONDTYP,DAV                                                      
         BE    BSCON32                                                          
*                                  PUNCH CONDITON                               
         CH    R6,MPXFBSLN         TEST NO. OF BYTES                            
         BNE   BSCON7                                                           
         B     BSCONX                                                           
*                                                                               
BSCON32  DS    0H                  ARITH VARIABLE                               
         C     R6,MPXFNRES         TEST NO. OF RESPONSES                        
         BNE   BSCON7                                                           
*                                                                               
BSCONX   DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
         DS    F                                                                
BSNCHK   DS    0H                                                               
         ST    RE,BSNCHK-4                                                      
         CLI   COMMAND,C'R'        FOR SEPARATE RANGES                          
         BNE   *+8                                                              
         L     R3,MPXBSTK          RESTORE R3 TO FIRST STRING                   
         LA    R6,1(R6)            BUMP RESPONDENT POINTER                      
         L     R2,SAVR2            START OF SPEC                                
         ZIC   R4,6(R2)            NO. OF PAIRS                                 
         LH    RE,WDLEN                                                         
         IC    R5,ICMSKS-1(RE)     MASK FOR EXEC ICM INSTRUCTIONS               
         LA    R2,7(R2)            FIRST PAIR                                   
*                                                                               
         SR    RE,RE                                                            
         EX    R5,BSNICM1          RE TO HAVE ARGUMENT                          
         SR    R1,R1                                                            
*                                                                               
BSNCHK3  DS    0H                                                               
         EX    R5,BSNICM2          R1 TO HAVE LOW OF PAIR                       
         CR    RE,R1                                                            
         BL    BSNCHK4                                                          
*                                                                               
         AH    R2,WDLEN                                                         
         EX    R5,BSNICM2          R1 TO HAVE HIGH OF PAIR                      
         CR    RE,R1                                                            
         BH    BSNCHK5                                                          
*                                  PASSES - GETS A YES                          
         LR    R1,R6                                                            
         BCTR  R6,R0                                                            
         SRDL  R6,3                COUNT/8                                      
         SRL   R7,29               REMAINDER                                    
*                                                                               
         LA    R7,RMSKS(R7)        GET MASK FOR 'OR' ING                        
         AR    R6,R3               POSITION OF BYTE                             
         OC    0(1,R6),0(R7)                                                    
         LR    R6,R1               RESTORE COUNTER                              
         CLI   COMMAND,C'R'        IF NOT SEPARATE RANGES                       
         BNE   BSNCHKX             THEN DONE                                    
         B     BSNCHK5                                                          
*                                  TRY NEXT PAIR                                
BSNCHK4  DS    0H                                                               
         AH    R2,WDLEN                                                         
BSNCHK5  DS    0H                                                               
         AH    R2,WDLEN                                                         
*                                                                               
         CLI   COMMAND,C'R'        IF DOING SEPARATE RANGES                     
         BNE   *+8                                                              
         AH    R3,MPXFBSLN         BUMP STACK POINTER                           
*                                                                               
         BCT   R4,BSNCHK3          NEXT PAIR                                    
*                                                                               
BSNCHKX  DS    0H                                                               
         L     RE,BSNCHK-4                                                      
         BR    RE                                                               
         DROP  R8                                                               
         SPACE 3                                                                
BSGIV    DS    0H                  'GIVEN' STRING                               
         LA    RF,4                OPERAND LENGTH                               
         LA    R5,1(R2)            POINT TO ADDRESS                             
         B     BSGIV2                                                           
         SPACE 2                                                                
         DS    F                                                                
BSGIV2   DS    0H                                                               
         ST    RE,BSGIV2-4                                                      
         STH   RF,OPNDLEN          OPERAND LENGTH                               
         SPACE 2                                                                
         LH    R1,MPXFBSLN                                                      
         ICM   R4,15,0(R5)         R4 = A(GIVEN STRING)                         
         MOVE  ((R3),(R1)),(R4)    MOVE TO STACK                                
*                                                                               
         L     RE,BSGIV2-4                                                      
         BR    RE                                                               
         SPACE 3                                                                
BSIMED   DS    0H     IMMEDIATE OPERAND (TEST ONLY)                             
         SPACE 2                                                                
         ZIC   R4,1(R2)                                                         
         STH   R4,OPNDLEN          OPERAND LENGTH                               
         SH    R4,=H'3'                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(R2)                                                    
*                                                                               
         BR    RE                                                               
         EJECT                                                                  
*                                  SET USER VARIABLE                            
         SPACE 2                                                                
SETVAR   DS    0H                                                               
         L     R2,8(R1)            A(VAR NO.)                                   
         LA    R2,0(R2)                                                         
         SR    R5,R5                                                            
         ICM   R5,3,0(R2)          VARIABLE NO.                                 
         MH    R5,=Y(MPXVARL)                                                   
         A     R5,MPXBVLST                                                      
         ST    R5,SETVA            SAVE A(VAR DESC)                             
         USING MPXVARD,R5                                                       
         MVC   AWGTAB,MPXVSTR      A(STRING)                                    
         L     R2,MPXVDEFI         A(INTERNAL DEFINITION SPEC)                  
*                                  TEST TYPE OF VARIABLE TO BE SET              
         CLI   MPXVDLEN,0                                                       
         BE    SV8                                                              
*                                  ARITHMETIC                                   
         CLI   0(R2),X'03'         ARITHMETIC SPEC                              
         BNE   SV4                                                              
         OC    3(2,R2),3(R2)       AND NO SPEC OTHER THAN VAR NO                
         BNZ   SV4                                                              
         BAS   RE,SDAV           SOURCE IS DEFINED ARITHMETIC VARIABLE          
         B     EXIT                                                             
*                                                                               
SV4      DS    0H                  COMPLEX (BOOLEAN/ARITH) SPEC                 
         XC    WDLEN,WDLEN                                                      
         MVC   WDLEN+1(1),MPXVDLEN SET DATA LENGTH                              
         ZIC   RF,MPXVPREC                                                      
         SLL   RF,2                                                             
         LR    RE,RF                                                            
         LA    RF,DVSRTAB(RF)                                                   
         MVC   VARDVSR,0(RF)       SET DIVISOR FOR COMPS                        
         LA    RE,RNDRTAB(RE)                                                   
         MVC   VARRNDR,0(RE)       SET ROUNDER                                  
*                                  INITIALIZE VARIABLE TO VARDVSR               
*                                  (UNIT VALUE FOR COMPS)                       
         LA    R4,VARDVSR+4                                                     
         ZIC   RF,MPXVDLEN                                                      
         SR    R4,RF                                                            
         L     R6,AWGTAB                                                        
         MVC   0(4,R6),0(R4)       SET FIRST                                    
*                                                                               
         LR    R1,RF                                                            
         M     R0,MPXFNRES         TOTAL STRING LENGTH                          
         SR    R1,RF               SET LENGTH FOR MOVE                          
         LA    RF,0(R6,RF)         TO ADDRESS (2ND POSITION)                    
         MOVE  ((RF),(R1)),(R6)    PROPAGATE                                    
         B     BOOLS2              BOOLEAN STRING PROCESSOR                     
*                                                                               
SV8      DS    0H                  BINARY VARIABLE SPEC                         
         B     BOOLS2              HANDLED BY NORMAL BOOLEAN STRING             
*                                  PROCESSOR (NO FILTERS, AND VARIABLE          
*                                  IS SET AT END)                               
*                                                                               
         SPACE 2                                                                
SDAV     NTR1                   FROM DEFINED ARITHMETIC VARIABLE                
         SPACE 2                                                                
         XC    KEY,KEY                                                          
         LA    R7,KEY                                                           
         USING XVTRECD,R7                                                       
         MVC   XVTKRCD,=X'0301'    RECORD CODE                                  
         MVC   XVTKCOL,1(R2)       VARIABLE NO. AND TYPE                        
         MVI   XVTKTYP,X'11'       ARITH TYPE                                   
*                                                                               
         L     R4,AWGTAB           R4 = RECEIVING AREA                          
         XC    WGTOT,WGTOT                                                      
         XC    RESPWC,RESPWC       CLEAR INPUT BYTE COUNT                       
         XC    RSTOT,RSTOT                                                      
         XC    WDLEN,WDLEN                                                      
*                                                                               
         BAS   RE,BSHIGH                                                        
         B     SDAV4B                                                           
SDAV4    DS    0H                                                               
         BAS   RE,BSSEQ                                                         
SDAV4B   DS    0H                                                               
         CLC   KEY(XVTKSEQ-XVTKEY),KEYSAVE                                      
         BNE   SDAV12                                                           
         BAS   RE,BSGET                                                         
         L     R7,MPXBAREC       A(REC)                                         
*                                                                               
         LA    R8,XVTELEMS                                                      
         MVI   ELCODLO,X'01'                                                    
         MVI   ELCODHI,X'03'                                                    
         BAS   RE,NEXTEL2                                                       
         B     SDAV6B                                                           
SDAV6    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
SDAV6B   DS    0H                                                               
         BNE   SDAV4                                                            
*                                                                               
         CLI   0(R8),X'01'         CONTROL ELEM                                 
         BNE   SDAV7                                                            
         USING XVTCNTEL,R8                                                      
         MVC   RSTOT,XVTCOUNT                                                   
         MVC   WDLEN+1(1),XVTCDLEN DATA LENGTH                                  
         L     RF,MPXFNRES         NO. OF RESPONDENTS                           
         MH    RF,WDLEN            X BYTES PER RESPONSE                         
         ST    RF,WSLEN            = EXPECTED LENGTH OF WEIGHT STRING           
*                                                                               
         B     SDAV6                                                            
*                                                                               
SDAV7    DS    0H                                                               
         CLI   0(R8),X'02'                                                      
         BNE   SDAV9                                                            
         USING XVTSTREL,R8                                                      
         ZIC   RF,1(R8)                                                         
         SH    RF,=H'2'                                                         
         LR    R0,RF                                                            
         A     R0,RESPWC                                                        
         C     R0,WSLEN            TEST HAVE TOO MANY BYTES                     
         BNH   SDAV8                                                            
SDAV7B   DS    0H                                                               
         MVI   MPXBERR,RECLERR                                                  
         B     SDAVX                                                            
*                                                                               
*                                                                               
SDAV8    DS    0H                                                               
         ST    R0,RESPWC           COUNT OF INPUT BYTES                         
         BCTR  RF,R0                                                            
         EX    RF,SDAVMVC          MOVE TO RECEIVING AREA                       
         LA    R4,1(R4,RF)         BUMP AREA POINTER                            
         B     SDAV6                                                            
*                                                                               
SDAV9    DS    0H                                                               
         CLI   0(R8),X'03'         REPEATER ELEMENT                             
         BNE   SDAV6                                                            
         USING XVTRPTEL,R8                                                      
         ZICM  RF,XVTRPTNO,2       NO. OF REPETITIONS                           
         LH    RE,WDLEN                                                         
         MR    RE,RE               X DATA FIELD LENGTH                          
         LR    R1,RF                                                            
         LR    R5,RF               SAVE TOTAL LEN FOR POINTER BUMP              
         A     RF,RESPWC                                                        
         C     RF,WSLEN            TEST TOO MANY BYTES                          
         BH    SDAV7B                                                           
         ST    RF,RESPWC                                                        
*                                                                               
         LH    RE,WDLEN                                                         
         SR    R1,RE               ADJUST LENGTH FOR 2ND MOVE                   
         BCTR  RE,R0                                                            
         EX    RE,SDAVRMVC                                                      
         LA    RF,1(R4,RE)         TO ADDRESS FOR MOVE                          
         MOVE  ((RF),(R1)),(R4)                                                 
         AR    R4,R5                  BUMP POINTER                              
         B     SDAV6                                                            
*                                                                               
SDAV12   DS    0H                                                               
         OC    RESPWC,RESPWC                                                    
         BNZ   *+12                                                             
         MVI   MPXBERR,NFNDERR                                                  
         B     SDAVX                                                            
*                                                                               
         CLC   RESPWC,WSLEN                                                     
         BNE   SDAV7B                                                           
*                                  CHECK TOTAL OF WEIGHTS                       
         LH    RE,WDLEN                                                         
         IC    R8,ICMSKS-1(RE)     MASK FOR EXEC ICM'S                          
         L     R0,MPXFNRES         FOR BCT                                      
         L     RE,AWGTAB           A(WEIGHT TABLE)                              
         SR    R3,R3                                                            
         SR    R1,R1                                                            
*                                                                               
SDAV14   DS    0H                                                               
         EX    R8,CTRICM           VALUE INTO R1                                
         AR    R3,R1                                                            
         AH    RE,WDLEN            NEXT VALUE                                   
         BCT   R0,SDAV14                                                        
*                                                                               
         OC    WGTOT,WGTOT         IF NOT GIVEN                                 
         BNZ   *+8                                                              
         ST    R3,WGTOT            SET TOTAL                                    
         C     R3,WGTOT            TEST VS EXPECTED VALUE                       
         BE    SDAV16                                                           
         MVI   MPXBERR,WCNTERR                                                  
*                                                                               
SDAV16   DS    0H                                                               
         C     R3,RSTOT            TEST VS CONTROL ELEM TOTAL                   
         BE    SDAVX                                                            
         MVI   MPXBERR,RCNTERR                                                  
*                                                                               
SDAVX    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
         DROP  R7                                                               
         DROP  R8                                                               
         EJECT                                                                  
*                                  GET RAW AND WEIGHTED COUNTS                  
         SPACE 2                                                                
COUNT    DS    0H                                                               
         OC    MPXBWV,MPXBWV      TEST ANY WEIGHTING                            
         BNZ   COUNT4                                                           
*                                  NO WEIGHTING                                 
         LA    RF,CTR14            SET BRANCH TO SKIP WEIGHTS                   
         ST    RF,CTRBRNCH                                                      
         BAS   RE,CTR                                                           
         B     EXIT                                                             
*                                                                               
COUNT4   DS    0H                                                               
         XC    WDLEN,WDLEN                                                      
*                                                                               
         CLI   MPXBWVT,C'U'        TEST WGT VARIABLE TYPE                       
         BNE   COUNT6                                                           
*                                  USER VARIABLE                                
         SR    R5,R5                                                            
         ICM   R5,3,MPXBWV                                                      
         MH    R5,=Y(MPXVARL)                                                   
         A     R5,MPXBVLST         POINT TO VARIABLE DESC                       
         USING MPXVARD,R5                                                       
*                                                                               
         MVC   WDLEN+1(1),MPXVDLEN DATE LENGTH                                  
         MVC   AWGTAB,MPXVSTR      A(STRING)                                    
         LA    RF,CTR40            SET BRANCH WITH CTR ROUTINE                  
         ST    RF,CTRBRNCH                                                      
         LH    RE,WDLEN                                                         
         IC    R8,ICMSKS-1(RE)     SET MASKS FOR ICM'S                          
*                                                                               
         B     COUNT8                                                           
*                                                                               
COUNT6   DS    0H                  DEFINED VARIABLE                             
         LA    RF,CTR50            SET BRANCH TO READ WEIGHTS                   
         ST    RF,CTRBRNCH                                                      
*                                                                               
COUNT8   DS    0H                                                               
         XC    WGTOT,WGTOT                                                      
         MVC   ELRHI,=F'-1'                                                     
         XC    ELAD,ELAD                                                        
         BAS   RE,CTR                                                           
         MVC   MPXBWTC,WGTOT       SET WEIGHTED TOTAL                           
*                                                                               
COUNTX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
CTR      NTR1                                                                   
         SPACE 2                                                                
         L     R3,MPXBSTK          POINT TO STRING                              
         SR    R7,R7               FOR RESPONDENT COUNT                         
         SR    R6,R6               COUNT OF YESES                               
         LH    R0,MPXFBSLN         FOR BCT                                      
         SR    R4,R4                                                            
*                                                                               
CTR10    DS    0H                                                               
         ICM   R4,1,0(R3)                                                       
         BZ    CTR30               WHOLE BYTE NULL                              
*                                                                               
         LA    R5,X'80'            SET BIT FINDER MASK                          
*                                                                               
CTR12    DS    0H                                                               
         LR    RE,R5                                                            
         NR    RE,R4                                                            
         BZ    CTR14                                                            
*                                                                               
         LA    R6,1(R6)            HAVE A YES                                   
         L     RF,CTRBRNCH         BRANCH TO PRE-SET ROUTINE                    
         BR    RF                                                               
*                                                                               
CTR14    DS    0H                                                               
         LA    R7,1(R7)            BUMP RSPONDENT COUNT                         
         SRA   R5,1                                                             
         BNZ   CTR12               ANOTHER BIT TO TEST                          
         B     CTR32                                                            
*                                                                               
CTR30    DS    0H                                                               
         LA    R7,8(R7)            NULL BYTE - SKIP COUNT BY 8                  
CTR32    DS    0H                                                               
         LA    R3,1(R3)            NEXT BYTE                                    
         BCT   R0,CTR10                                                         
*                                                                               
CTRX     DS    0H                                                               
         ST    R6,MPXBRAW          SET RAW COUNT                                
         XIT1                                                                   
         SPACE 3                                                                
CTR40    DS    0H                  WEIGHTS FROM TABLE                           
         LR    RE,R7                                                            
         MH    RE,WDLEN                                                         
         A     RE,AWGTAB           POINTS FO WEIGHT VALUE                       
*                                                                               
CTR42    DS    0H                                                               
         SR    R1,R1                                                            
         EX    R8,CTRICM           INSERT VALUE                                 
         A     R1,WGTOT                                                         
         ST    R1,WGTOT                                                         
         B     CTR14               'RETURN'                                     
         SPACE 2                                                                
*                             WEIGHTS FROM RECORDS                              
CTR50    DS    0H                                                               
         C     R7,ELRHI            TEST VS HIGHEST IN ELEM                      
         BH    CTR54               RESP NOT IN THIS ELEM                        
*                                                                               
         ICM   RE,15,ELAD          A(ELEM)                                      
         BZ    CTR54                                                            
         CLI   0(RE),X'03'         IS IT A REPEATER ELEM                        
         BNE   CTR52               NO                                           
         LA    RE,XVTRPTND-XVTRPTEL(RE)      YES-POINT TO VALUE                 
         B     CTR42                         AND ADD AS IF FROM TABLE           
*                                                                               
CTR52    DS    0H                                                               
         CLI   0(RE),X'02'         'NORMAL' DATA ELEM                           
         BNE   CTR56                                                            
         LR    R1,R7                                                            
         S     R1,ELRLO                                                         
         MH    R1,WDLEN            DISPLACEMENT INTO ELEM                       
         LA    RE,XVTSTR-XVTSTREL(RE,R1)     POINT TO VALUE                     
         B     CTR42                         ADD                                
*                                  TRY NEXT ELEM                                
CTR54    DS    0H                                                               
         L     RE,ELRHI            NEXT ELEM MUST BEGIN                         
         A     RE,=F'1'            WHERE LAST LEAVES OFF                        
         ST    RE,ELRLO                                                         
*                                                                               
         ICM   RE,15,ELAD          ADDRESS OF ELEM                              
         BZ    CTR60               NONE - GET 1ST RECORD                        
CTR56    DS    0H                                                               
         ZIC   R1,1(RE)                                                         
         AR    RE,R1               NEXT ELEM                                    
CTR56D   DS    0H                                                               
         CLI   0(RE),0                                                          
         BE    CTR60               EOR - GET ANOTHER RECORD                     
*                                                                               
         CLI   0(RE),X'02'                                                      
         BNE   CTR57                                                            
*                                  STRING ELEM                                  
         ZIC   R1,1(RE)            LENGTH                                       
         SH    R1,=H'2'                                                         
*                                  SET RESPONDENT COUNT FOR ELEM                
*                                  BASED ON RESPONSE LENGTH                     
         CLI   WDLEN+1,1           = IF 1 BYTE                                  
         BE    CTR58                                                            
         CLI   WDLEN+1,2                                                        
         BNE   *+12                                                             
         SRL   R1,1                /2 IF 2                                      
         B     CTR58                                                            
         CLI   WDLEN+1,4                                                        
         BNE   *+12                                                             
         SRL   R1,2                /4 IF 4                                      
         B     CTR58                                                            
*                                                                               
         LR    RF,R0               SAVE R0                                      
         SR    R0,R0                                                            
         D     R0,=F'3'            /3 IF 3                                      
         LR    R0,RF               RESTORE R0                                   
*                                                                               
         B     CTR58                                                            
*                                  REPEATER ELEM                                
CTR57    DS    0H                                                               
         CLI   0(RE),X'03'                                                      
         BNE   CTR59                                                            
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,XVTRPTNO-XVTRPTEL(RE)    NO. OF RESPONSES                   
*                                                                               
CTR58    DS    0H                                                               
         BCTR  R1,R0                                                            
         A     R1,ELRLO                                                         
         ST    R1,ELRHI                 SET HIGHEST IN ELEM                     
         ST    RE,ELAD             ELEM ADDRESS                                 
         B     CTR50                                                            
         SPACE 2                                                                
CTR59    DS    0H                                                               
         CLI   0(RE),X'01'         CONTROL ELEM                                 
         BNE   CTR56                                                            
*                                                                               
         USING XVTCNTEL,RE                                                      
         MVC   WDLEN+1(1),XVTCDLEN                                              
         DROP  RE                                                               
         LH    R8,WDLEN                                                         
         IC    R8,ICMSKS-1(R8)     SET MASKS FOR ICM'S                          
         B     CTR56                                                            
*                                  NEED TO GET WEIGHT RECORD                    
         SPACE 2                                                                
CTR60    DS    0H                                                               
         BAS   RE,CWR                                                           
         CLI   MPXBERR,0                                                        
         BNE   CTRX                                                             
         L     RE,ELAD                                                          
         B     CTR56D              RETURN                                       
         SPACE 2                                                                
CTR70    DS    0H                  APPLY MULTIPLIER                             
         LR    RE,R7                                                            
         MH    RE,WDLEN                                                         
         A     RE,AWGTAB           POINT TO CURRENT WEIGHT VALUE                
*                                                                               
         SR    R1,R1                                                            
         EX    R8,CTRICM                                                        
         LR    RF,R0               SAVE R0                                      
         M     R0,VARMULT                                                       
         A     R1,VARRNDR                                                       
         D     R0,VARDVSR                                                       
*                                                                               
         EX    R8,CTRSTCM          SET NEW VALUE                                
         LR    R0,RF               RESTORE R0                                   
         B     CTR14                                                            
         SPACE 2                                                                
CWR      NTR1                                                                   
         OC    ELAD,ELAD           FIRST TIME                                   
         BNZ   CWR4                                                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R7,KEY                                                           
         USING XVTRECD,R7                                                       
         MVC   XVTKRCD,=X'0301'     RECORD CODE                                 
         MVC   XVTKCOL,MPXBWV      VARIABLE NO.                                 
         MVI   XVTKTYP,X'11'       TYPE = ARITH                                 
         BAS   RE,BSHIGH                                                        
         B     CWR4B                                                            
*                                                                               
CWR4     DS    0H                                                               
         BAS   RE,BSSEQ                                                         
*                                                                               
CWR4B    DS    0H                                                               
         CLC   KEY(XVTKSEQ-XVTKEY),KEYSAVE                                      
         BNE   CWR7                ERROR TO READ PAST CONDITION                 
         BAS   RE,BSGET                                                         
         L     R7,MPXBAREC                                                      
*                                                                               
         LA    R8,XVTELEMS                                                      
         ST    R8,ELAD                                                          
         B     CWRX                                                             
*                                                                               
CWR7     DS    0H                                                               
         MVI   MPXBERR,WGTRERR                                                  
*                                                                               
CWRX     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*                                  SEPARATE RANGES FOR NUMERICS                 
         SPACE 2                                                                
RANGE    DS    0H                                                               
         L     R2,8(R1)            ADDRESS OF INPUT                             
         LA    R2,0(R2)                                                         
         ZICM  R4,MPXBSTMX,1       NO. OF STACK SLOTS                           
         BNZ   *+8                                                              
         LA    R4,4                DEFAULT IS 4                                 
*                                                                               
         ZICM  R5,5(R2),1          NO. OF RANGES                                
         CR    R5,R4                                                            
         BNH   RNG4                                                             
*                                                                               
         MVI   MPXBERR,STKERR      NOT ENOUGH STACK AREA PROVIDED               
         B     EXIT                                                             
*                                                                               
RNG4     DS    0H                                                               
         L     R3,MPXBSTK                                                       
*                                                                               
RNG6     DS    0H                                                               
         LR    RE,R3                                                            
         LH    RF,MPXFBSLN                                                      
         XCEF                                                                   
         AH    R3,MPXFBSLN                                                      
         BCT   R5,RNG6                                                          
*                                                                               
         L     R3,MPXBSTK                                                       
         MVC   NSTRNGS,5(R2)       FOR FILTER ROUTINE                           
*                                                                               
         LA    RF,BSCDAV           DEFINED ARITH VAR                            
         CLI   0(R2),DAV                                                        
         BE    *+8                                                              
         LA    RF,BSCUAV           OR USER ARITH VAR                            
         BASR  RE,RF                                                            
*                                                                               
         B     BS14                APPLY FILTERS                                
         EJECT                                                                  
BSHIGH   DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         LA    RF,=C'DMRDHI'                                                    
         B     BSDIR                                                            
         SPACE 2                                                                
BSSEQ    DS    0H                                                               
         LA    RF,=C'DMRSEQ'                                                    
         SPACE 2                                                                
BSDIR    DS    0H                                                               
         ST    RF,DMCB                                                          
         LR    R0,RE                                                            
         GOTO1 MPXBDMGR,DMCB,,MPXFDIR,KEY,KEY                                   
         B     BSDMCHK                                                          
         SPACE 3                                                                
BSGET    DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 MPXBDMGR,DMCB,=C'GETREC',MPXFFLE,KEY+27,MPXBAREC,DMWRK           
         SPACE 2                                                                
BSDMCHK  DS    0H                                                               
         TM    DMCB+8,X'FF'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R8)                                                         
         AR    R8,R0                                                            
NEXTEL2  DS    0H                                                               
         CLI   0(R8),0                                                          
         BNE   NEXTEL4                                                          
         LTR   RE,RE                                                            
         BR    RE                                                               
NEXTEL4  DS    0H                                                               
         CLC   0(1,R8),ELCODLO                                                  
         BL    NEXTEL                                                           
         CLC   0(1,R8),ELCODHI                                                  
         BH    NEXTEL                                                           
         SR    R0,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                  WORK AREA DSECT                              
WKD      DSECT                                                                  
*                                                                               
LAPOS    DS    F                                                                
SAVR1    DS    F                                                                
SAVR2    DS    F                                                                
DMCB     DS    6F                                                               
RSTOT    DS    F                                                                
RESPWC   DS    F                                                                
WSLEN    DS    F                                                                
WGTOT    DS    F                                                                
AWGTAB   DS    A                                                                
AWGTCOND DS    A                                                                
ELRLO    DS    F                                                                
ELRHI    DS    F                                                                
ELAD     DS    A                                                                
ENDEL    DS    A                                                                
WORK     DS    XL8                                                              
SETVA    DS    A                                                                
VARDVSR  DS    F                                                                
VARRNDR  DS    F                                                                
VARMULT  DS    F                                                                
CTRBRNCH DS    A                                                                
WDLEN    DS    H                                                                
OPNDLEN  DS    H                                                                
CONDTYP  DS    C                                                                
NSTRNGS  DS    X                                                                
COMMAND  DS    CL8                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
ELCODLO  DS    X                                                                
ELCODHI  DS    X                                                                
DMWRK    DS    12D                                                              
         SPACE 3                                                                
         SPACE 2                                                                
*                                                                               
       ++INCLUDE MPGENXVT                                                       
*                                                                               
       ++INCLUDE MPXFILD                                                        
*                                                                               
       ++INCLUDE MPXBLKD                                                        
*                                                                               
       ++INCLUDE MPXVARD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015MPXTFACS  05/01/02'                                      
         END                                                                    
