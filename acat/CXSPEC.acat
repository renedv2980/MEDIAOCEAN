*          DATA SET CXSPEC     AT LEVEL 029 AS OF 08/23/96                      
*CATALP CXSPEC                                                                  
         TITLE 'CXSPEC - CONTROL SECURITY EXTRACT - PERSON PASSWORD'            
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
* AL4 0 OR A(PASSWORD ENCRYPTION KEY)                                *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSPEC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSPEC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSPED,R2           R2=A(EXTRACT RECORD)                         
         USING SAPEREC,R3          R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSPELEN,CXSPELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSPELEN(2),=AL2(CXSPEDL)                                        
         MVC   CXSPETYP,CXSPEDQ                                                 
         MVI   CXSPEDX,MXTRTQ                                                   
         MVI   CXSPEDX+1,MXTRTQ                                                 
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   SAVEACTN,C' '                                                    
         MVI   CXSPEACT,C'A'       GOTO ADD PROCESS                             
         B     CXSAID                                                           
CXSACT1  CLI   CXSPEACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPEACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPEACT,C'A'       IF ADD DO 'K' DELETE ALL                     
         BE    CXSACT2                                                          
         B     CXSAID              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSPEACT   SAVE THIS ACTION                             
         MVI   CXSPEACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
*                                                                               
CXSAID   MVI   CXSPEAID-1,MXTRTQ   AGENCY                                       
         MVC   CXSPEAID,SAPEAGY                                                 
*                                                                               
CXSPID   MVI   CXSPEPID-1,MXTRTQ   PERSON ID                                    
         MVC   CXSPEPID,SAPEPID                                                 
*                                                                               
         LA    R4,SAPEDATA         R4=A(FIRST ELEMENT)                          
*                                                                               
CXSELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXSNXTX                                                          
         CLI   0(R4),SAPWDELQ      GET PASSWORD POINTER ELEMENT                 
         BE    CXSPWD                                                           
         CLI   0(R4),SANAMELQ      GET PERSON NAME ELEMENT                      
         BE    CXSNAM                                                           
         CLI   0(R4),SAPERELQ      GET PERSON DETAILS ELEMENT                   
         BE    CXSPER                                                           
         CLI   0(R4),SAADRELQ      GET PERSON ADDRESS ELEMENT                   
         BE    CXSADR                                                           
         CLI   0(R4),SAAGCELQ      GET ACCESS GROUP ELEMENT                     
         BE    CXSAGC                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXSELM                                                           
*                                                                               
         USING SAPWDD,R4                                                        
CXSPWD   MVI   CXSPEPWD-1,MXTRTQ   PASSWORD CODE                                
         MVC   CXSPEPWD,SAPWDCOD                                                
         ICM   RF,15,AENCKEY                                                    
         BZ    CXSPWD10                                                         
         LA    R5,CXSPEPWD                                                      
         GOTO1 VCRYPT,DMCB,L'CXSPEPWD,(R5),(RF)                                 
         CLI   0(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CXSPWD10 MVI   CXSPENUM-1,MXTRTQ   PERSON NUMBER                                
         SR    RF,RF                                                            
         ICM   RF,3,SAPWDNUM                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSPENUM,DUB+5(3)                                                
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SANAMD,R4                                                        
CXSNAM   MVI   CXSPEFNA-1,MXTRTQ   FIRST NAME                                   
         MVI   CXSPEMNA-1,MXTRTQ   MIDDLE NAME                                  
         MVI   CXSPELNA-1,MXTRTQ   LAST NAME                                    
         MVI   CXSPEFNA,C' '                                                    
         MVC   CXSPEFNA+1(L'CXSPEFNA-1),CXSPEFNA                                
         MVI   CXSPEMNA,C' '                                                    
         MVC   CXSPEMNA+1(L'CXSPEMNA-1),CXSPEMNA                                
         MVI   CXSPELNA,C' '                                                    
         MVC   CXSPELNA+1(L'CXSPELNA-1),CXSPELNA                                
         SR    RF,RF                                                            
         LA    RE,SANAMELN                                                      
CXSNAMF  TM    SANAMIND,SANAMIFN                                                
         BZ    CXSNAMM                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPEFNA(0),0(RE)                                                
         TR    CXSPEFNA,MXTRT                                                   
         LA    RF,1(RF)                                                         
         AR    RE,RF                                                            
CXSNAMM  TM    SANAMIND,SANAMIMN                                                
         BZ    CXSNAML                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPEMNA(0),0(RE)                                                
         TR    CXSPEMNA,MXTRT                                                   
         LA    RF,1(RF)                                                         
         AR    RE,RF                                                            
CXSNAML  TM    SANAMIND,SANAMILN                                                
         BZ    CXSNAMX                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPELNA(0),0(RE)                                                
         TR    CXSPELNA,MXTRT                                                   
CXSNAMX  B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAPERD,R4                                                        
CXSPER   MVI   CXSPEOFF-1,MXTRTQ   PERSON OFFICE CODE                           
         MVC   CXSPEOFF,SAPEROFF                                                
         MVI   CXSPEDEP-1,MXTRTQ   PERSON DEPARTMENT CODE                       
         MVC   CXSPEDEP,SAPERDID                                                
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAAGCD,R4                                                        
CXSAGC   MVI   CXSPEAGC-1,MXTRTQ   PERSON ACCESS GROUP CODE                     
* ??     MVC   CXSPEAGC,SAAGCCOD                                                
         GOTO1 VHEXOUT,DMCB,SAAGCNUM,CXSPEAGC,2,=C'TOG'                         
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAADRD,R4                                                        
CXSADR   EQU   *                   PERSON ADDRESS                               
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         SPACE 1                                                                
EXIT     CLI   SAVEACTN,C' '                                                    
         BE    EOFEXIT                                                          
         CLI   SAVEACTN,C'D'                                                    
         BE    EOFEXIT                                                          
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         XMOD1 1                                                                
*                                                                               
EOFEXIT  MVI   SAVEACTN,C' '                                                    
         XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    32C' '                                                           
SAVEACTN DC    CL1' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         DS    0D                                                               
         DC    C'CXSPECC '                                                      
CXSPECC  DS    0XL4                CLIENT SAVE DATA AREA                        
         DC    1000XL4'00'         A/M,CLI                                      
         DC    8X'FF'                                                           
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
AENCKEY  DS    A                   A(PASSWORD ENCRYPTION KEY, 10 BYTES)         
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSPED                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'029CXSPEC    08/23/96'                                      
         END                                                                    
