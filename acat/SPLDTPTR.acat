*          DATA SET SPLDTPTR   AT LEVEL 003 AS OF 06/06/11                      
*CATALP SPLDTPTR                                                                
         TITLE 'LDTPTR - SPOT - ACTIVE/PASSIVE POINTER TEST'                    
*        PARAMS VIA R1                                                          
*        XL1   X'00'     RETURN  X'00'=ACTIVE  OR  X'FF'=PASSIVE                
*        AL3   A(RECORD)                                                        
*        XL1   FILE NUM  RETURN  X'FF'=SKIP KEY REQUIRED                        
*        AL3   A(SKIP KEY)                                                      
*                                                                               
         PRINT NOGEN                                                            
LDTPTR   CSECT                                                                  
         NMOD1 0,SPLDTPTR                                                       
         L     R3,0(R1)            R3=A(DIRECTORY RECORD)                       
         CLI   4(R1),X'36'         TEST XSPDIR                                  
         BE    LDX                                                              
         CLI   4(R1),X'33'         TEST TRFDIR                                  
         BE    NGR3                                                             
*                                                                               
LDS      CLI   0(R3),2             TEST SPTDIR GOAL REC                         
         BNE   NGR                 NO                                           
         TM    11(R3),X'80'        TEST PASSIVE POINTER                         
         BO    DND                 YES - IGNORE                                 
         TM    11(R3),X'40'+X'20'+X'10'  TEST PACKAGE INFO + HISTORY            
         BNZ   XIT                       AND PLANNED                            
         CLI   11(R3),0            ADV GOAL                                     
         BNE   DND                                                              
         B     XIT                 ELSE DUMP IT                                 
DND      MVI   0(R1),X'FF'         SET NO DUMP                                  
         B     XIT                                                              
*                                                                               
* NOT A SPTDIR GOAL REC                                                         
*                                                                               
NGR      CLI   0(R3),X'0D'         NEW LFM REC                                  
         BNE   NGR2                                                             
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BO    DND                                                              
         CLI   1(R3),X'73'         TEST STATION LOCKIN RECORD                   
         BNE   XIT                                                              
         TM    12(R3),X'80'        YES-TEST PASSIVE POINTER                     
         BO    DND                                                              
         B     XIT                                                              
*                                                                               
NGR2     CLI   0(R3),X'03'         SYND REC                                     
         BNE   NGR3                                                             
         OC    8(5,R3),8(R3)       TEST PASSIVE                                 
         BNZ   DND                                                              
         B     XIT                                                              
*                                                                               
NGR3     CLI   0(R3),X'0A'         TRAFFIC                                      
         BNE   NGR4                                                             
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BO    DND                                                              
         B     XIT                                                              
*                                                                               
NGR4     CLI   0(R3),X'8A'         TEST ACTIVITY PASSIVE                        
         BE    DND                                                              
*                                                                               
NGR5     CLI   0(R3),X'0E'         WUNDERMAN                                    
         BNE   NGR6                                                             
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BO    DND                                                              
         B     XIT                                                              
*                                                                               
NGR6     CLI   0(R3),X'10'         DUMP ALL OTHER HDRS                          
         BL    XIT                                                              
         CLI   0(R3),255           EOF CONTROL                                  
         BC    8,XIT                                                            
*                                                                               
* MUST BE A BUYREC OF SORTS                                                     
*                                                                               
         CLI   10(R3),0            REG, ACT OR POOL MAS                         
         BC    7,DND               NO. DO NOT DUMP                              
         TM    0(R3),X'0C'         'MEDIA' C,D,E,F ARE SPECIAL                  
         BO    DND                 PASSIVE BUY POINTERS                         
         B     XIT                                                              
*                                                                               
* LOOKING AT XSPDIR RECORDS                                                     
*                                                                               
LDX      MVI   0(R1),X'FF'         SET XSPDIR PASSIVE                           
         CLC   0(2,R3),=X'0E83'    SPOTPAK INVOICE MATCH-MAKER PASSIVE          
         BE    XIT                                                              
         CLC   0(2,R3),=X'0E93'    NETPAK  INVOICE MATCH-MAKER PASSIVE          
         BE    XIT                                                              
         CLC   0(2,R3),=X'0E8C'    WB FLIGHT PASSIVE KEY 1                      
         BE    XIT                                                              
         CLC   0(2,R3),=X'0E9C'    WB FLIGHT PASSIVE KEY 2                      
         BE    XIT                                                              
         CLC   0(2,R3),=X'0DB9'    SUPERDESK AUTH DUE DATE PASSIVE              
         BE    XIT                                                              
         CLC   0(2,R3),=X'0DC9'    SUPERDESK AUTH START DATE PASSIVE            
         BE    XIT                                                              
         CLC   0(2,R3),=X'0DBC'    OM DESKTOP KEYS TO NEWLY ADDED ORDER         
         BE    XIT                                                              
         CLC   0(2,R3),=X'0E06'    NETPAK UNIT BILLING PASSIVE KEY              
         BE    XIT                                                              
         CLC   0(2,R3),=X'0AE1'    TRAFFIC                                      
         BE    XIT                                                              
         CLC   0(2,R3),=X'0E95'    SPOTPAK EMAIL ADDRESS PASSIVE                
         BE    XIT                                                              
*                                                                               
         CLC   0(2,R3),=X'0D73'    TEST STATION LOCKIN RECORD                   
         BNE   LDX2                                                             
         USING SLKRECD,R3                                                       
         TM    SLKKIND,SLKKIPSV                                                 
         BO    XIT                                                              
         DROP  R3                                                               
         B     LDX4                                                             
*                                                                               
LDX2     CLC   0(2,R3),=X'0D06'    CAN ORDERS DEMO RECORD                       
         BNE   LDX4                                                             
         USING DDMRECD,R3                                                       
         CLI   DDMKSPL,0           TEST SPILL POINTER                           
         BNE   XIT                 NON-ZERO IS PASSIVE                          
         TM    DDMKAGMD,X'03'      TEST NETWORK                                 
         BNO   LDX4                                                             
         OC    DDMKMKT,DDMKMKT     TEST MARKET 0                                
         BNZ   XIT                 IF NOT ZERO, IGNORE                          
*                                                                               
LDX4     MVI   0(R1),0             ACTIVE                                       
                                                                                
XIT      XMOD1 1                                                                
                                                                                
         PRINT OFF                                                              
       ++INCLUDE SPGENXLK                                                       
       ++INCLUDE SPGENCDORD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003SPLDTPTR  06/06/11'                                      
         END                                                                    
