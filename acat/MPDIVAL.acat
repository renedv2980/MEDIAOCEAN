*          DATA SET MPDIVAL    AT LEVEL 007 AS OF 05/01/02                      
*CATALP MPDIVAL                                                                 
         TITLE 'MPDIVAL - DATA SCANNER'                                         
         PRINT NOGEN                                                            
MPDIVAL  CSECT                                                                  
         SPACE 2                                                                
*                                                                               
*        INPUT IS FIVE FULLWORDS :-                                             
*         PROG ID, A(SCR HDR), A(PROC TAB), A(MPDITAB), SUFFIX(OPT)             
*                                                                               
*        OUTPUT IS ADDR OF NEXT SLOT IN PROCTAB IN PLIST+8                      
*                                                                               
*        THE INPUT STRING MAY BE IN SEVERAL FORMATS :-                          
*         BASE                                                                  
*         BASE(QUAL)                                                            
*         BASE(QUAL,QUAL...)                                                    
*         BASE/SCALING                                                          
*         BASE(QUAL)/SCALING                                                    
*         BASE/SCALING(QUAL)                                                    
*         BASE(QUAL,QUAL...)/SCALING                                            
*         BASE/SCALING(QUAL,QUAL...)                                            
*                                                                               
*        BASE AND QUALIFIER ARE FIRST CHECKED SYNTACTICALLY.                    
*        A TABLE IS CREATED IN THE CALLERS STORAGE, CONTAINING                  
*        AN INTERNAL CODE FOR BASE AND QUALIFIERS, ONE BLOCK                    
*        FOR EACH LOGICAL PAIRING, USER OVERRIDES ON SCALING                    
*        ARE INSERTED VALUE WHEN SPECIFIED.                                     
*                                                                               
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPDIVAL                                              
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         SH    RC,=H'04'                                                        
         L     RC,0(RC)                                                         
         USING GEND,RC                                                          
         EJECT                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R6,16(R1)           SUFFIX VALUE (FOR FREQ PROG)                 
         STCM  R6,3,SUFFIX         SAVE SUFFIX VALUE (IF ANY)                   
         L     R6,8(R1)            A(PROCESSING TABLE)                          
         XC    0(PCLEN,R6),0(R6)   INIT TABLE ENTRY                             
         MVC   PROCEND,20(R1)      ADDR END OF PROC TABLE                       
         L     R4,4(R1)            A(SCREEN HEADER)                             
         LA    R3,8(R4)            POINT TO DATA                                
         ZIC   R1,5(R4)            LEN OF DATA                                  
         LA    R4,0(R1,R3)         POINT TO END OF DATA LINE (+1)               
*                                                                               
*        SCAN BACK TO 1ST NON SPACE CHR                                         
*                                                                               
MPD0     DS    0H                                                               
         BCTR  R4,0                                                             
         CR    R3,R4               CHECK NOT BACK TO START OF DATA              
         BH    MPD90               EXIT IF NO DATA                              
         CLI   0(R4),C' '                                                       
         BE    MPD0                                                             
         ST    R4,AEOL             SAVE END OF LINE ADDR                        
         MVI   DSPSCF,X'FF'        INIT AS 'NO USER OVERRIDE SCALING'           
         MVI   DSPDEC,X'FF'        INIT AS 'NO USER OVERRIDE DECS'              
         ST    R6,BASEADDR         SAVE BASE ADDRESS FOR 1ST ENTRY              
         USING PCMDD,R6                                                         
         SPACE 2                                                                
MPD1     DS    0H                                                               
         SR    R4,R4                                                            
         LR    R2,R3                                                            
         CLC   0(2,R2),=C'C='      ABSOLUTE COLUMN OVERRIDE ?                   
         BE    MPD40                                                            
         CLC   0(2,R2),=C'C+'      RELATIVE COLUMN OVERRIDE ?                   
         BE    MPD40                                                            
MPD2     DS    0H                                                               
         C     R3,AEOL             END OF LINE REACHED ?                        
         BH    MPD80               YES EXIT                                     
         CLI   0(R3),C','          END OF BASE ?                                
         BE    MPD10                                                            
         CLI   0(R3),C'('          START OF QUAL ?                              
         BE    MPD20                                                            
         CLI   0(R3),C'/'          START OF SCALING   ?                         
         BE    MPD30                                                            
         CLI   0(R3),C' '          ENCOUNTERED SPACE CHR                        
         MVI   ERROR,NOSPACES                                                   
         BE    TRAPERR             NOT ALLOWED                                  
         LA    R3,1(R3)            NEXT CHAR                                    
         LA    R4,1(R4)            ADD 1 TO LENGTH                              
         B     MPD2                                                             
         SPACE 3                                                                
*                                                                               
*        REACHED ',' SO MUST HAVE A BASE WITH NO QUALIFIERS TO                  
*        SATISFY SYNTAX.                                                        
*                                                                               
MPD10    DS    0H                                                               
         BAS   R9,SCFINS           INSERT SCALING (IF DEFINED)                  
         BAS   R9,BASEVAL                                                       
         LA    R3,1(R3)                                                         
         BAS   R9,PCNEXT           INIT NEXT ENTRY                              
         B     MPD1                                                             
         EJECT                                                                  
*                                                                               
*        REACHED A '(' SO MUST HAVE A BASE BEFORE THE  '('                      
*        NOTE BASE/SCALING(Q,Q) IS HANDLED AT MPD35                             
*                                                                               
*        THIS ROUTINE WILL ONLY EXIT ON WHEN ALL QUALIFIERS CHECKED             
*        I.E. ENCOUNTER A ')'.                                                  
*                                                                               
MPD20    DS    0H                                                               
         BAS   R9,SCFINS           INSERT SCALING (IF DEFINED)                  
         BAS   R9,BASEVAL                                                       
MPD21    DS    0H                                                               
         LA    R3,1(R3)                                                         
         SR    R4,R4                                                            
         LR    R2,R3                                                            
MPD22    DS    0H                                                               
         CLI   0(R3),C','                                                       
         BE    MPD23                                                            
         CLI   0(R3),C')'                                                       
         BE    MPD24                                                            
         CLI   0(R3),C' '          ENCOUNTERED SPACE CHR                        
         MVI   ERROR,NOSPACES                                                   
         BE    TRAPERR             NOT ALLOWED                                  
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         B     MPD22                                                            
MPD23    DS    0H                                                               
         BAS   R9,QUALVAL                                                       
         BAS   R9,PCNEXT           INIT NEXT ENTRY                              
         B     MPD21                                                            
MPD24    DS    0H                                                               
         BAS   R9,QUALVAL                                                       
         LA    R3,1(R3)            BUMP TO NEXT CHAR                            
         CLI   0(R3),C','          IF COMMA FOLLOWS...                          
         BNE   MPD25                                                            
         LA    R3,1(R3)            .. BUMP PAST IT                              
MPD25    DS    0H                                                               
         BAS   R9,PCNEXT           INIT NEXT ENTRY                              
         B     MPD1                                                             
         EJECT                                                                  
*                                                                               
*        REACHED A '/' MAY HAVE A BASE TO VALIDATE IE BASE/SCALING              
*        OTHERWISE MUST BE BASE(QUAL)/SCALING                                   
*        MUST TERMINATE WITH '(' OR ',' OR EOL.                                 
*        /SCALING(QUAL  OR  /SCALING,BASE  OR /SCALING                          
*                                                                               
MPD30    DS    0H                                                               
         BCTR  R3,0                                                             
         CLI   0(R3),C')'                                                       
         BE    MPD31                                                            
         BAS   R9,SCFINS           INSERT SCALING   (IF DEFINED)                
         BAS   R9,BASEVAL                                                       
         BAS   R9,PCNEXT           INIT NEXT ENTRY                              
*                                  NB - KNOCKED BACK IF TERMINATOR              
*                                  IS '(' AS QUAL WILL DO THIS                  
MPD31    DS    0H                                                               
         LA    R3,2(R3)                                                         
         LR    R2,R3                                                            
         SR    R4,R4                                                            
MPD32    DS    0H                                                               
         C     R3,AEOL             END OF LINE ?                                
         BH    MPD35                                                            
         CLI   0(R3),C'('          START OF A QUALIFIER ?                       
         BE    MPD35                                                            
         CLI   0(R3),C','          END OF B(Q)/NNN ?                            
         BE    MPD35                                                            
         CLI   0(R3),C' '          ENCOUNTERED SPACE CHR                        
         MVI   ERROR,NOSPACES                                                   
         BE    TRAPERR             NOT ALLOWED                                  
         LA    R3,1(R3)            BUMP TO NEXT CHR IN I/P STRING               
         LA    R4,1(R4)            INCR LEN OF SUB-STRING                       
         B     MPD32               CHECK NEXT CHR                               
MPD35    DS    0H                                                               
         BAS   R9,SCFVAL           VALIDATE SCALING FACTOR                      
         CLI   0(R3),C'('          START OF A QUALIFIER ????                    
         BNE   MPD36               NO - SKIP (MUST BE ',' OR EOL)               
         SH    R6,=AL2(PCLEN)      ELSE - POINT TO CURRENT BASE                 
         B     MPD21               PROCESS QUALIFIER                            
MPD36    DS    0H                                                               
         LA    R3,1(R3)            ELSE - MUST BE EOL OR ','                    
         B     MPD1                                                             
         SPACE 2                                                                
*                                                                               
*        REACHED 'C=' (AT  START OF LINE OR AFTER A DELIM)                      
*        MUST BE DELIMITED BY A ',' ONLY (NOT EOL)                              
*                                                                               
MPD40    DS    0H                                                               
         MVC   RELABS,1(R2)       SAVE = OR + (ABSOLUTE OR RELATIVE)            
         LA    R2,2(R2)           SKIP 'C=', 'C+'                               
         LR    R3,R2              POINT TO START OF NUMBER                      
         SR    R4,R4                                                            
         MVI   ERROR,INVCOL       INVALID COLUMN SPECIFICATION (INCASE)         
MPD42    DS    0H                                                               
         C     R3,AEOL            BEYOND END ?                                  
         BH    TRAPERR                                                          
         CLI   0(R3),C','         END OF FIELD ?                                
         BE    MPD44                                                            
         CLI   0(R3),C'0'         CHECK CHR IS NUMERIC                          
         BL    TRAPERR                                                          
         LA    R4,1(R4)           INCR LENGTH                                   
         LA    R3,1(R3)           BUMP TO NEXT CHR                              
         B     MPD42              CHECK CHR                                     
MPD44    DS    0H                                                               
         LTR   R4,R4              ANY INPUT ?                                   
         BZ    TRAPERR                                                          
         BCTR  R4,0               SET EX LENGTH                                 
         EX    R4,*+8             PACK NUMBER                                   
         B     *+10                                                             
         PACK  DUB,0(0,R2)                                                      
         CVB   R1,DUB                                                           
         CH    R1,=H'255'        CHECK NOT > 1 BYTE                             
         BNL   TRAPERR                                                          
         LTR   R1,R1             CHECK NON ZERO                                 
         BZ    TRAPERR                                                          
         CLI   RELABS,C'='                                                      
         BE    MPD45             SKIP IF ABSOLUTE COLUMN POSN                   
         STC   R1,PCHPLOC        SAVE RELATIVE POSN FOR MPCHECK                 
         B     MPD46                                                            
*                                                                               
MPD45    BCTR  R1,0              SAVE AS OFFSET                                 
         STC   R1,PCDPLOCC       OVERRIDE COLUMN POSN (ABSOLUTE)                
*                                                                               
MPD46    LA    R3,1(R3)          BUMP PAST ','                                  
         B     MPD1                                                             
         SPACE 2                                                                
*                                                                               
*        REACHED END OF INPUT LINE. MAY BE A BASE WITH NO QUALIFIERS            
*        OR SCALING FACTOR OVERRIDE STILL OUTSTANDING                           
*                                                                               
MPD80    DS    0H                  PROCESS LAST FIELD ON LINE                   
         BAS   R9,SCFINS           INSERT SCALING (IF DEFINED)                  
         LTR   R4,R4               ANY DATA OUTSTANDING ?                       
         BZ    MPD90               NO - ALL DONE                                
         BAS   R9,BASEVAL          MUST BE A BASE WITH NO QUALIFIER             
         BAS   R9,PCNEXT           INIT NEXT ENTRY                              
MPD90    DS    0H                                                               
         XC    0(PCLEN,R6),0(R6)   SET END OF TABLE                             
         ST    R6,8(R8)            SET UP NEXT POSITION IN PROC TABLE           
XIT      DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         L     R2,4(R8)            RESTORE SCREEN HEADER ADDR                   
         GOTO1 ERREX                                                            
         EJECT                                                                  
*                                                                               
*        VALIDATE BASE                                                          
*                                                                               
BASEVAL  DS    0H                                                               
         LTR   R4,R4               ANY INPUT ?                                  
         MVI   ERROR,NOBASE        NO BASE SPECIFIED                            
         BZ    TRAPERR                                                          
         L     R5,12(R8)           ADDRESS MPDITAB                              
         A     R5,0(R5)            ADDRESS VALID BASES TABLE                    
         USING MPDIBD,R5                                                        
         CLC   0(3,R2),=C'RCH'     IS BASE 'REACH' ?                            
         BE    BV0                 NO - BRANCH TO VALIDATE                      
         CLC   0(3,R2),=C'COV'     DITTO                                        
         BNE   BV1                                                              
BV0      DS    0H                                                               
         CH    R4,=H'3'            HAS BASE GOT A SUFFIX ( EG RCH99 )           
         BE    BV1                 NO - SEE IF RCH IS VALID FOR REPORT          
         LA    R1,3(R2)                                                         
BV0A     DS    0H                                                               
         CLI   0(R1),C'0'                                                       
         BL    TRAPERR                                                          
         BCTR  R4,0                                                             
         LA    R1,1(R1)                                                         
         CH    R4,=H'3'                                                         
         BH    BV0A                                                             
         LR    R1,R3                                                            
         SR    R1,R2                                                            
         SH    R1,=H'4'            LESS 3 FOR 'RCH' LESS 1 FOR EX LEN           
         EX    R1,*+8              PACK NUMBER                                  
         B     *+10                                                             
         PACK  DUB,3(0,R2)                                                      
         CVB   R1,DUB                                                           
         STCM  R1,3,PCSFX          STORE SUFFIX                                 
BV1      DS    0H                                                               
         BCTR  R4,0                LESS 1 FOR EX LENGTH                         
BV2      DS    0H                                                               
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),MPBENAM     MATCH ?                                      
         BE    BV3                                                              
BV2A     DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,MPBLEN                                                        
         AR    R5,R0               NO - BUMP TO NEXT TABLE ENTRY                
         CLI   MPBTYP,C'B'         END OF TABLE ?                               
         MVI   ERROR,INVBASE       INVALID BASE NAME                            
         BNE   TRAPERR                                                          
         B     BV2                                                              
BV3      DS    0H                                                               
         LA    R1,1(R4)            RESTORE CORRECT LENGTH                       
         CLM   R1,1,MPBMINC        LONG ENOUGH TO IDENTIFY ?                    
         BL    BV2A                NO - TRY ANOTHER ENTRY                       
         MVI   PCLNTH,PCLEN        INSERT LENGTH IN TABLE                       
         MVC   PCBASE,MPBICOD      SAVE INTERNAL CODE                           
         DROP  R5                                                               
         MVI   PCQUAL,MPQ#         SET DEF QUAL                                 
         OC    SUFFIX,SUFFIX       ANY SUFFIX SUPPLIED ????                     
         BZ    BV4                 NO - SKIP                                    
         OC    PCSFX,PCSFX         USER SUPPLIED ONE ?????                      
         BNZ   TRAPERR             CAN'T HAVE USER & FORCED VALUE !!!           
         MVC   PCSFX,SUFFIX        INSERT FORCED SUFFIX                         
BV4      DS    0H                                                               
         MVI   PCDSPSCF,X'FF'      NO USER OVERRIDE SCALING FACTOR              
         MVI   PCDSPDEC,X'FF'      NO USER OVERRIDE DEC PLACES                  
         ST    R6,BASEADDR         SAVE ADDRESS OF CURRENT BASE                 
         BR    R9                                                               
         EJECT                                                                  
*                                                                               
*        VALIDATE QUALIFIER                                                     
*                                                                               
QUALVAL  DS    0H                                                               
         LTR   R4,R4               ANY INPUT ?                                  
         MVI   ERROR,NOQUAL        NO QUALIFIER                                 
         BZ    TRAPERR                                                          
         L     R5,12(R8)           ADDRESS MPDITAB                              
         A     R5,4(R5)            ADDRESS VALID QUAL TABLE                     
         USING MPDIQD,R5                                                        
QV1      DS    0H                                                               
         BCTR  R4,0                LESS 1 FOR EX LENGTH                         
QV2      DS    0H                                                               
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),MPQENAM     MATCH ?                                      
         BE    QV3                                                              
QV2A     DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,MPQLEN                                                        
         AR    R5,R0               NO - BUMP TO NEXT TABLE ENTRY                
         CLI   MPQTYP,C'Q'         END OF TABLE ?                               
         MVI   ERROR,INVQUAL       YES !!!!!                                    
         BNE   TRAPERR                                                          
         B     QV2                                                              
QV3      DS    0H                                                               
         LA    R1,1(R4)            RESTORE CORRECT LENGTH                       
         CLM   R1,1,MPQMINC        LONG ENOUGH TO IDENTIFY ?                    
         BL    QV2A                NO - TRY ANOTHER ENTRY                       
         CLI   PCBASE,0            BASE MISSING ?                               
         BNE   QV4                 NO - OKAY                                    
         SH    R6,=AL2(PCLEN)      ELSE POINT TO PREVIOUS                       
         MVC   PCLEN+1(1,R6),1(R6) EXTRACT BASE CODE                            
         MVC   PCLEN+4(2,R6),4(R6) SUFFIX (FOR REACH/FREQ)                      
         LA    R6,PCLEN(R6)        RESTORE PROCTAB INDEX                        
QV4      DS    0H                                                               
         MVI   PCLNTH,PCLEN        INSERT LENGTH IN TABLE                       
         MVC   PCQUAL,MPQCOD       SAVE INTERNAL CODE                           
         MVI   PCDSPSCF,X'FF'      NO USER OVERRIDE SCALING FACTOR              
         MVI   PCDSPDEC,X'FF'      NO USER OVERRIDE DECIMAL PLACES              
         DROP  R5                                                               
         DROP  R6                                                               
         BR    R9                                                               
         EJECT                                                                  
*                                                                               
*        VALIDATE SCALING FACTORS                                               
*                                                                               
SCFVAL   DS    0H                                                               
         LTR   R1,R4               LENGTH OF SCALING FACTOR FIELD               
         MVI   ERROR,INVSC         INVALID SCALING FACTOR                       
         BZ    TRAPERR                                                          
         CLI   DSPSCF,X'FF'        ALREADY USER DEFINED ???                     
         BNE   TRAPERR                                                          
         MVI   DSPSCF,0            INIT AS UNITY                                
         MVI   DSPDEC,0            INIT AS NO DECIMALS                          
*                                                                               
         LR    R5,R2               ADDR OF START OF SCALING FIELD               
*                                                                               
         CH    R1,=H'01'           ONLY 1 CHR SO CAN BE 'U'NITY                 
         BNE   SV1                                                              
         CLI   0(R5),C'U'                                                       
         BE    SVEX                VALID                                        
*                                                                               
SV1      DS    0H                                                               
         CLI   0(R5),C'.'                                                       
         BNE   SV2                                                              
         CLI   DSPSCF,0            ALREADY SET DECIMAL (I.E. '.' FOUND)         
         BNE   TRAPERR                                                          
         LR    R0,R5               CURRENT POSN                                 
         SR    R0,R2               START POSN                                   
         STC   R0,DSPSCF           SAVE NO CHRS TO LEFT OF '.'                  
         OI    DSPSCF,X'80'        INCASE /.00 (I.E SCALE = 0)                  
         LA    R5,1(R5)            BUMP TO NEXT                                 
         BCT   R1,SV2                                                           
         B     TRAPERR             MUST BE MIN OF 1 '0' TO RIGHT OF '.'         
*                                                                               
SV2      DS    0H                                                               
         CLI   0(R5),C'0'          MUST ALL BE '0'                              
         BNE   TRAPERR                                                          
         LA    R5,1(R5)            BUMP TO NEXT CHR IN SCALING                  
         BCT   R1,SV1                                                           
*                                                                               
SV3      DS    0H                                                               
*        AT THIS POINT R4 CONTAINS THE NUMBER OF CHRS IN STRING                 
*        IF  DSPSCF HAS A VALUE THEN A DECIMAL POINT WAS ENCOUNTERED            
*        IN WHICH CASE DSPDEC = R4 - DSPSCF - 1 = NUMBER TO RIGHT OF            
*        DECIMAL PLACE.                                                         
*                                                                               
*        E.G. FOR A NUMERIC VALUE OF 12345.6789 :-                              
*              USER DEFINES  DSPSCF  DSPDEC     DISPLAYS (NOT ROUNDED)          
*              000.00            03     02       12.34                          
*              000.000           03     03       12.345                         
*              .000              00     03       12345.678                      
*              .0                00     01       12345.6                        
*              00                02     00       123                            
*              U                 00     00       12345                          
*                                                                               
*                                                                               
         CLI   DSPSCF,0            ANY DEC PLACES ???                           
         BNE   SV4                 SKIP IF THERE ARE                            
         STC   R4,DSPSCF           ELSE SAVE USER DISPLAY SCALING               
         MVI   DSPDEC,0            WITH NO DEC PLACES                           
         B     SVEX                                                             
SV4      DS    0H                                                               
         NI    DSPSCF,X'7F'        TURN OFF X'80' TEMP FLAG                     
         ZIC   R1,DSPSCF           RETRIEVE CHRS TO LEFT OF DEC                 
         LA    R1,1(R1)                                                         
         SR    R4,R1                                                            
         STC   R4,DSPDEC                                                        
SVEX     DS    0H                                                               
         BR    R9                                                               
*                                                                               
*        INSERT USER DEFINED SCALING IN PROC TABLE. NOTE THAT THE               
*        VALUE MAY BE DEFINED FOR A GROUP OF B/Q PAIRS I.E.                     
*        B(Q1,Q2,Q3,Q4)/SCALING. BASEADDR POINTS TO 1ST OF GROUP, R6            
*        TO THE NEXT AVAILABLE SLOT IN THE TABLE.                               
*                                                                               
SCFINS   DS    0H                                                               
         CLI   DSPSCF,X'FF'        HAS USER DEFINED SCALING ???                 
         BE    SIEX                NO  - EXIT                                   
         L     R5,BASEADDR         SET TO START OF CURRENT B(Q,Q)               
         USING PCMDD,R5                                                         
SI10     DS    0H                                                               
         MVC   PCDSPSCF,DSPSCF     INSERT USER DEFINED SCALING FACTOR           
         MVC   PCDSPDEC,DSPDEC     INSERT USER DEFINED DECIMAL PLACES           
         LA    R5,PCLEN(R5)        BUMP TO NEXT ENTRY                           
         CR    R5,R6               ALL B/Q PAIRS DONE ?                         
         BL    SI10                NO - UPDATE NEXT                             
         MVI   DSPSCF,X'FF'        DEFAULT TO NO USER OVERRIDE                  
         MVI   DSPDEC,X'FF'        DEFAULT TO NO USER OVERRIDE                  
SIEX     DS    0H                                                               
         DROP  R5                                                               
         BR    R9                                                               
         EJECT                                                                  
PCNEXT   LA    R6,PCLEN(R6)                                                     
         LA    RF,PCLEN(R6)        POINT TO END OF THIS ENTRY                   
         C     RF,PROCEND          CHECK IF BEYOND END OF TABLE                 
         BNH   PC02                OK TO CONTINUE                               
         MVI   ERROR,CANTDO        CAN'T PROCESS - REQUEST TOO LARGE            
         CLI   OFFLINE,C'Y'        CHECK IF OFFLINE                             
         BNE   TRAPERR             ONLINE - LET USER KNOW (TEST MODE)           
         DC    H'0'                DIE - PROCESSING TABLE TOO SMALL             
PC02     XC    0(PCLEN,R6),0(R6)   INIT TABLE ENTRY                             
         BR    R9                                                               
         EJECT                                                                  
         LTORG                                                                  
WORKD    DSECT                                                                  
AEOL     DS    F                   ADDR OF END OF DATA LINE                     
BASEADDR DS    F                   ADDR START OF B(Q,Q,Q,Q)                     
PROCEND  DS    F                   ADDR OF END OF PROCESSING TABLE              
DSPSCF   DS    X                   USER DEFINED DISPLAY SCALING FACTOR          
DSPDEC   DS    X                   USER DEFINED DISPLAY DECIMAL PACES           
SUFFIX   DS    XL2                 CALLER SUPPLIED SUFFIX                       
RELABS   DS    X                   RELATIVE/ABSOLUTE USER DEFINED COL           
WORKX    DS    0H                                                               
         SPACE 3                                                                
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*DDSPLWORKD,MPDITABD,MPPCMDD,MPEQUATES                                          
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPDITABD                                                       
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPEQUATES                                                      
         PRINT ON                                                               
         SPACE                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007MPDIVAL   05/01/02'                                      
         END                                                                    
