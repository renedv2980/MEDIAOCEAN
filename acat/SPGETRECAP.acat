*          DATA SET SPGETRECAP AT LEVEL 027 AS OF 09/30/93                      
*CATALP GETRECAP                                                                
         TITLE 'READ INST. RECAP RECORDS FOR COMMERCIAL ALLOCATIONS'            
GETRECAP CSECT                                                                  
         NMOD1 800,GETRECAP,CLEAR=YES                                           
         USING RCWORKC,RC                                                       
         L     R2,0(R1)            INPUT PARAMETERS                             
         USING GRINP,R2                                                         
         L     R3,4(R1)            OUTPUT AREA                                  
         USING GROUT,R3                                                         
         ST    R3,OUTPTR           SET OUTPUT POINTER                           
         L     R4,8(R1)                                                         
         USING COMFACSD,R4                                                      
         LR    RE,R3               GET LENGTH OF OUTPUT                         
         ICM   RF,15,GRIOLEN                                                    
         XCEF                                                                   
         GOTO1 CDATCON,DMCB,(X'02',GRISDTE),(X'03',START)                       
         GOTO1 CDATCON,DMCB,(X'02',GRIEDTE),(X'03',END)                         
         LA    RE,PATTAB                                                        
         ST    RE,PATPTR                                                        
         XC    PATTAB,PATTAB                                                    
         XC    KEY,KEY             SET TO READ RECAPS                           
         LA    R5,KEY                                                           
         USING INSRECD,R5                                                       
         MVC   INSKID,=X'0A24'                                                  
         MVC   INSKAM,GRIAGYMD                                                  
         MVC   INSKCLT,GRICLT                                                   
         MVC   INSKPRD,GRIPRDA                                                  
         MVC   INSKMKT,GRIMKT                                                   
         MVC   INSKSTA,GRISTA                                                   
         MVC   INSKCOPY,GRICOPY                                                 
         MVC   KEYSAVE,KEY                                                      
*NSEL    GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                  
INSEL    GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'TRFDIR',KEYSAVE,KEY                  
         CLC   KEY(13),KEYSAVE                                                  
         BE    INSEL1                                                           
         LA    R5,KEYSAVE          HAVE WE DONE COPY 0                          
         CLI   INSKCOPY,0                                                       
         BE    EXIT                YES - FOUND NOTHING                          
         MVI   INSKCOPY,0          NO - TRY FOR IT                              
         MVC   KEY,KEYSAVE                                                      
         LA    R5,KEY                                                           
         B     INSEL                                                            
*                                                                               
*NSEL1   GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'SPTFIL',KEY+14,REC1,DMWORK           
INSEL1   GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'TRFFIL',KEY+14,REC1,DMWORK           
         LA    R5,REC1                                                          
         LA    R6,INSDTAEL         EXTRACT INST. RECAP FOR DATES                
         USING INSDTAEL,R6                                                      
IEL2     CLI   0(R6),X'10'                                                      
         BNE   IEL6                                                             
         CLC   INSPRD1,GRIPRDA     CHECK PRODUCT                                
         BNE   IEL6                                                             
         CLC   INSSLN1,GRISLNA     CHECK SPOT LENGTH                            
         BNE   IEL6                                                             
         ZIC   R8,1(R6)                                                         
         SH    R8,=H'11'                                                        
         LR    R7,R6                                                            
IEL4     L     RE,PATPTR           SAVE PATTERNS IN WORK AREA                   
         CLC   START,INSLTD        CHECK DATES                                  
         BH    IEL5                                                             
         CLC   END,INSFTD                                                       
         BL    IEL5                                                             
         MVC   9(1,RE),INSKCOPY                                                 
         MVC   0(9,RE),INSPTTN                                                  
         LA    RE,10(RE)                                                        
IEL5     ST    RE,PATPTR           SET TO NEXT SLOT                             
         SH    R8,=H'9'                                                         
         LA    R6,9(R6)                                                         
         C     R8,=F'8'                                                         
         BH    IEL4                                                             
         LR    R6,R7                                                            
IEL6     CLI   0(R6),0             END OF RECAP                                 
         BE    RDPAT                                                            
         ZIC   RE,1(R6)                                                         
         AR    R6,RE                                                            
         B     IEL2                                                             
         EJECT                                                                  
RDPAT    XC    KEY,KEY                                                          
         LA    RE,PATTAB           RESET PATTERN TABLE POINTER                  
         ST    RE,PATPTR                                                        
         LA    R5,KEY              SET UP TO READ PATTERNS                      
         USING PATRECD,R5                                                       
         MVC   PATKID,=X'0A22'                                                  
         MVC   PATKAM,GRIAGYMD                                                  
         MVC   PATKCLT,GRICLT                                                   
         MVC   PATKPRD,GRIPRDA                                                  
         MVC   PATKSLN,GRISLNA                                                  
RDPAT2   L     RE,PATPTR                                                        
         OC    0(9,RE),0(RE)                                                    
         BZ    EXIT                NO RECAP FOUND                               
         MVC   PATKCODE,9(RE)                                                   
         MVC   PATKREF,0(RE)                                                    
         MVC   KEYSAVE,KEY                                                      
         GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                  
         CLC   KEY(13),KEYSAVE                                                  
         BNE   EXIT                                                             
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'SPTFIL ',KEY+14,REC2,DMWORK          
         LA    R5,REC2                                                          
         LA    R6,PATDTAEL                                                      
FNDEL30  CLI   0(R6),X'30'         FIND CML ELEMENT                             
         BE    FNDEL32                                                          
         CLI   0(R6),0                                                          
         BE    NXTPAT                                                           
         ZIC   RE,1(R6)                                                         
         AR    R6,RE                                                            
         B     FNDEL30                                                          
FNDEL32  LR    R7,R6               FIND PATTERN ELEMENT                         
         USING PATPTNEL,R7                                                      
FNDEL32A CLI   0(R7),X'32'                                                      
         BE    PRPTN                                                            
         CLI   0(R7),0                                                          
         BE    NXTPAT                                                           
         ZIC   RE,1(R7)                                                         
         AR    R7,RE                                                            
         B     FNDEL32A                                                         
NXTPAT   L     RE,PATPTR                                                        
         LA    RE,10(RE)                                                        
         ST    RE,PATPTR                                                        
         B     RDPAT2                                                           
         EJECT                                                                  
PRPTN    L     RE,PATPTR           PROCESS THE PATTERNS                         
         L     R9,OUTPTR                                                        
         LA    RE,3(RE)            SET TO FTD                                   
         GOTO1 CDATCON,DMCB,(X'03',0(RE)),(X'02',0(R9))                         
         L     RE,PATPTR                                                        
         L     R9,OUTPTR                                                        
         LA    RE,6(RE)            SET TO LTD                                   
         CLI   0(RE),0                                                          
         BNE   *+8                                                              
         LA    RE,END                                                           
         LA    R9,2(R9)                                                         
         GOTO1 CDATCON,DMCB,(X'03',0(RE)),(X'02',0(R9))                         
         LA    RE,CMLWRK           CLEAR OUT CML WORK AREA                      
         L     RF,=F'340'                                                       
         XCEF                                                                   
         ZIC   R0,1(R7)                                                         
         SH    R0,=H'2'                                                         
CNTCML   NI    PATPTN,X'0F'        SET FOR INDEXING                             
         ZIC   R9,PATPTN                                                        
         BCTR  R9,0                                                             
         SLL   R9,4                / 16                                         
         LA    R9,2(R6,R9)         SET ALPHA CML ADDRESS                        
         LA    RE,CMLWRK                                                        
CNTCML2  CLI   0(RE),0                                                          
         BE    CNTCML6                                                          
         CLI   0(R9),C'*'          CHECK FOR DELETED CML                        
         BNE   *+14                                                             
         OC    1(4,R9),1(R9)                                                    
         BZ    CNTCML9                                                          
         CLC   0(16,RE),0(R9)      SAVE CML LIST                                
         BNE   CNTCML4                                                          
         ZIC   RF,16(RE)           COUNT CML OCCURENCES                         
         LA    RF,1(RF)                                                         
         STC   RF,16(RE)                                                        
         B     CNTCML8                                                          
CNTCML4  LA    RE,17(RE)                                                        
         B     CNTCML2                                                          
CNTCML6  MVC   0(16,RE),0(R9)                                                   
         MVI   16(RE),1                                                         
CNTCML8  LA    R7,1(R7)                                                         
CNTCML9  BCT   R0,CNTCML                                                        
         L     RE,OUTPTR                                                        
         LA    RF,CMLWRK                                                        
         LA    RE,4(RE)                                                         
CNTCML10 MVC   0(17,RE),0(RF)                                                   
         LA    RE,17(RE)                                                        
         LA    RF,17(RF)                                                        
         OC    0(17,RF),0(RF)                                                   
         BNZ   CNTCML10                                                         
         ST    RE,OUTPTR                                                        
         B     NXTPAT                                                           
         EJECT                                                                  
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
RCWORKC  DSECT                                                                  
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
LNKEY    EQU   13                                                               
DMCB     DS    6F                                                               
START    DS    CL3                                                              
END      DS    CL3                                                              
OUTPTR   DS    F                                                                
PATPTR   DS    F                   PATTERN TABLE POINTER                        
PATTAB   DS    CL250                                                            
DMWORK   DS    CL96                                                             
CMLWRK   DS    340C                                                             
REC1     DS    1600C                                                            
REC2     DS    1600C                                                            
         EJECT                                                                  
GRINP    DSECT                     INPUT PARAMETER BLOCK                        
GRIAGYMD DS    CL1                 AGENCY/MEDIA                                 
GRICLT   DS    CL2                                                              
GRIPRDA  DS    C                                                                
GRISLNA  DS    C                                                                
GRIPRDB  DS    C                                                                
GRISLNB  DS    C                                                                
GRICOPY  DS    C                                                                
GRISDTE  DS    CL2                                                              
GRIEDTE  DS    CL2                                                              
GRIMSTA  DS    0CL5                                                             
GRIMKT   DS    CL2                                                              
GRISTA   DS    CL3                                                              
GRIOLEN DS     CL4                                                              
*                                                                               
GROUT    DS    CL2                 OUTPUT DEFINITION                            
GROSDTE  DS    CL2                                                              
GROEDTE  DS    CL2                                                              
GROALPH1 DS    CL8                 CANNISTER 1                                  
GROALPH2 DS    CL8                 CANNISTER 2                                  
GROFREQ  DS    CL1                                                              
GROEN    DS    0C                                                               
GROLN    EQU   GROEN-GROALPH1                                                   
         EJECT                                                                  
       ++INCLUDE SPTRINST                                                       
       ++INCLUDE SPTRPAT                                                        
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027SPGETRECAP09/30/93'                                      
         END                                                                    
