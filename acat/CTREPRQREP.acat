*          DATA SET CTREPRQREP AT LEVEL 009 AS OF 05/01/02                      
*CATALP CTREQREP                                                                
         TITLE 'MODULE EDITS REQUESTS AND (OPTIONALLY) PRINTS REPORT'           
CTREQREP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,CTREQREP                                                      
         USING REQREPD,RC                                                       
         L     RA,=V(CTWORKC)      PARAMETER 1 RETURNED BY MODULE               
         USING CTWORKD,RA                      0=REQUEST OK                     
*                                              1=REQUEST ERROR                  
         LR    R2,R1                                                            
         XC    0(4,R2),0(R2)                                                    
         MVC   PRSWITCH,RCREQREP                                                
         CLI   RCREQREP,C'N'                                                    
         BE    REPX                                                             
         EJECT                                                                  
*                   PRINT SOME MINIMAL HEADLINES                                
         SPACE 3                                                                
HEADS    CLI   PRSWITCH,C'Y'                                                    
         BNE   VALIDATE                                                         
         MVC   P,SPACES                                                         
         MVC   P+24(6),=C'RUN ON'                                               
         GOTO1 DATCON,RQWORK,(4,RCDATE),(8,P+31)                                
         MVC   P+45(30),=C'DETAILS OF REQUEST      PAGE 1'                      
         GOTO1 PRINT,RQWORK,SPACES,=C'BC01'                                     
         BAS   RE,PRINT1                                                        
         MVC   P+45(30),=C'------------------      ------'                      
         MVC   P+28(2),=C'AT'                                                   
       ++INCLUDE DDGETIME                                                       
         XC    RQDUB,RQDUB                                                      
         ST    R1,RQWORK                                                        
         MVC   RQDUB+5(3),RQWORK                                                
         OI    RQDUB+7,X'0F'                                                    
         CVB   R3,RQDUB                                                         
         EDIT  (R3),(5,P+31),2,DUB=RQDUB,WRK=RQWORK                             
         BAS   RE,PRINT3                                                        
         SPACE 2                                                                
         MVC   P+24(25),=C'REQUEST FIELD        DATA'                           
         MVC   P+69(6),=C'ERRORS'                                               
         BAS   RE,PRINT1                                                        
         MVC   P+24(25),=C'-------------        ----'                           
         MVC   P+69(6),=C'------'                                               
         BAS   RE,PRINT3                                                        
         EJECT                                                                  
*                   LOOP THROUGH TABLE AND HANDLE REQUESTS                      
         SPACE 3                                                                
VALIDATE LA    R3,REQTAB                                                        
         SPACE 2                                                                
LOOP     CLC   0(3,R3),=C'END'                                                  
         BNE   LOOP2                                                            
         SPACE 2                                                                
REPX     DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
LOOP2    PACK  RQDUB,0(2,R3)       GET ADDRESS AND LENGTH OF FIELD              
         CVB   R4,RQDUB                                                         
         LA    R4,QRECORD-1(R4)                                                 
         PACK  RQDUB,2(2,R3)                                                    
         CVB   R5,RQDUB                                                         
         BCTR  R5,R0                                                            
         CLI   4(R3),C'N'          NUMERIC TESTS                                
         BNE   LOOP4                                                            
         EX    R5,SPACOMP          SPACES OK                                    
         BE    LOOP6                                                            
         CLC   0(3,R4),=C'ALL'     AND SO IS ALL                                
         BE    LOOP6                                                            
         MVC   RQWORK(8),=8X'F0'                                                
         EX    R5,MUVZONES                                                      
         CLC   RQWORK(8),=8X'F0'                                                
         BE    LOOP6                                                            
         CLI   PRSWITCH,C'Y'                                                    
         BNE   ERRET                                                            
         MVC   P+69(17),=C'FIELD NOT NUMERIC'                                   
         MVI   3(R2),1                                                          
         B     LOOP6                                                            
         SPACE 2                                                                
ERRET    MVI   PRSWITCH,C'Y'                                                    
         B     VALIDATE                                                         
         SPACE 2                                                                
SPACOMP  CLC   0(0,R4),SPACES                                                   
MUVZONES MVZ   RQWORK(0),0(R4)                                                  
MUVFIELD MVC   P+45(0),0(R4)                                                    
         SPACE 2                                                                
LOOP4    CLI   4(R3),C'D'          DATE TESTS                                   
         BNE   LOOP6                                                            
         CLC   0(2,R4),=C'ES'                                                   
         BE    LOOP6                                                            
         CLC   0(6,R4),SPACES                                                   
         BE    LOOP6                                                            
         MVC   DATE,0(R4)                                                       
         CLC   DATE+4(2),=C'  '                                                 
         BNE   *+10                                                             
         MVC   DATE+4(2),=C'01'                                                 
         GOTO1 GETDAY,RQWORK,DATE,RQDUB                                         
         CLC   RQDUB(3),SPACES                                                  
         BNE   LOOP6                                                            
         CLI   PRSWITCH,C'Y'                                                    
         BNE   ERRET                                                            
         MVC   P+69(16),=C'NOT A VALID DATE'                                    
         MVI   3(R2),1                                                          
         SPACE 2                                                                
LOOP6    CLI   PRSWITCH,C'Y'       ONLY PRINT IF OPTION ON                      
         BNE   LOOP8                                                            
         EX    R5,SPACOMP          AND FIELD IS NON-SPACES                      
         BE    LOOP8                                                            
         MVC   P+24(20),5(R3)                                                   
         EX    R5,MUVFIELD                                                      
         TM    P+45,X'40'          SPECIAL NEGATIVE FILTER FEATURE              
         BO    LOOP7                                                            
         MVC   P+56(1),P+45                                                     
         OI    P+56,X'40'                                                       
         MVC   P+45(10),=C'ALL EXCEPT'                                          
         SPACE 2                                                                
LOOP7    DS    0H                                                               
         BAS   RE,PRINT2                                                        
         SPACE 2                                                                
LOOP8    LA    R3,25(R3)                                                        
         B     LOOP                                                             
         EJECT                                                                  
*                   TABLE OF REQUEST FIELDS                                     
         SPACE 3                                                                
REQTAB   DS    0CL25                                                            
         DC    CL25'1021ASELECTED FIELDS'                                       
         DC    CL25'3401AFILTER 1'                                              
         DC    CL25'3501AFILTER 2'                                              
         DC    CL25'3601AFILTER 3'                                              
         DC    CL25'3706DSTART DATE'                                            
         DC    CL25'4306DEND DATE'                                              
         DC    CL25'4901ASORT OPTION'                                           
         DC    CL25'5001ASKIP OPTION'                                           
         DC    CL25'5109ASELECTIVE FIELD'                                       
         DC    CL25'6007AOPTIONS'                                               
         DC    CL25'6712AREQUESTOR NAME'                                        
         DC    C'END'                                                           
         EJECT                                                                  
*                   PRINTING AIDS                                               
         SPACE 2                                                                
PRINT1   MVC   RQCON,=C'BL01'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT2   MVC   RQCON,=C'BL02'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT3   MVC   RQCON,=C'BL03'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
GOPRINT  NTR                                                                    
         GOTO1 PRINT,RQWORK,P,RQCON                                             
         MVC   P,SPACES                                                         
         XIT                                                                    
         EJECT                                                                  
*                   DSECT FOR MODULE                                            
         SPACE 3                                                                
REQREPD  DSECT                                                                  
RQDUB    DS    D                                                                
RQWORK   DS    CL24                                                             
PRSWITCH DS    CL1                                                              
RQCON    DS    CL4                                                              
DATE     DS    CL6                                                              
         PRINT OFF                                                              
       ++INCLUDE CTREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009CTREPRQREP05/01/02'                                      
         END                                                                    
