*          DATA SET ACCATKIN   AT LEVEL 014 AS OF 05/01/02                      
*CATALP CATKIN                                                                  
CATKIN   TITLE ' DECODE EXPENSE CATEGORY KEY'                                   
CATKIN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CATK**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL WORKLNG STORAGE)                  
         LR    R9,R1                                                            
         USING CADCD,R9             R9=A(VATICAN CONTROL BLOCK)                 
         MVI   CADCRTN,C' '         CLEAR THE RETURN FIELDS                     
         MVC   CADCRTN+1(CADCRTNL-1),CADCRTN                                    
                                                                                
***********************************************************************         
*        MVC   DIR,ACCFIL                                             *         
*        GOTO1 CADCDMGR,DMCB,DMDTFA,DIR                               *         
*        L     RE,12(R1)           RE=A(ACCFIL DTF)                   *         
*        MVC   DATADISP,OLDDISP                                       *         
*        TM    DTFTYPE-DTFPHD(RE),DTFTEMU                             *         
*        BZ    CAT2                                                   *         
*        MVC   DIR,ACCDIR                                             *         
*        MVC   DATADISP,NEWDISP                                       *         
*        OI    INDS,INDSEMU        SET ACCFIL EMULATED FLAG           *         
*        BASR  RE,RF                                                  *         
*        L     RE,12(R1)           RE=A(ACCDIR DTF)                   *         
*                                                                     *         
*AT2     ST    RE,ADTF             SAVE A(DIRECTORY DTF)              *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   SAVKEY,0(RE)        SAVE LAST KEY                      *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
***********************************************************************         
                                                                                
         GOTO1 CADCDMGR,DMCB,DMKEY,ACCDIR,SAVKEY                                
         EJECT                                                                  
***********************************************************************         
* GET LEDGER RECORD AND ESTABLISH HEIRARCHY                           *         
***********************************************************************         
         SPACE 1                                                                
         ICM   R2,15,CADCL13       GET ADDRESS OF 13 LEDGER RECORD              
         BNZ   CAT7                                                             
         CLC   SAVCUL,CADCA13      SAVE C/U/L                                   
         BE    CAT8                                                             
         MVC   KEY,SPACES          OR GO FIND IT                                
         MVC   KEY(LDGKEND),CADCA13                                             
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   KEY(L'LDGKEY),IO                                                 
         BNE   ERR1                RECORD NOT FOUND                             
*        TM    INDS,INDSEMU        TEST NEW FILE                                
*        BZ    CAT7                                                             
         MVC   DA,IO+(LDGKDA-LDGRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
CAT7     LR    R4,R2                                                            
         MVI   ELCODE,ACLELQ       GET HEIRARCHY ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   ERR3                ELEMENT NOT FOUND                            
         LR    R5,R4                                                            
         USING ACLELD,R5                                                        
         SR    R1,R1               SAVE LENGTHS ELEMENT                         
         IC    R1,ACLLN                                                         
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   SAVACL(0),ACLELD                                                 
*                                                                               
CAT8     MVC   SAVCUL,CADCA13      SAVE C/U/L                                   
         LA    R5,SAVACL                                                        
         LA    R5,ACLVALS                                                       
         USING ACLVALS,R5                                                       
         SR    R1,R1                                                            
         SR    R2,R2                                                            
         CLI   ACLVLEN,2           MULI-LEVEL  - CATEGORY/OFF/DEPT              
         BH    CAT10                                                            
         IC    R1,ACLVLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+4              LEVEL A/CATEGORY/OFFICE/DEPT                 
         MVC   CADCLVA(0),CADCA13+3 FIRST LEVEL OF 13                           
         MVC   KEY,SPACES                                                       
         LA    R1,3(R1)                                                         
         EX    R1,*+4                                                           
         MVC   KEY(0),CADCA13      BUILD KEY FOR FIRST LEVEL OF 13              
         LA    R3,CADCLVAN         LEVEL A NAME                                 
         BAS   RE,GETNME           GET NAME                                     
         IC    R2,ACLVLEN                                                       
         LA    R5,L'ACLVALS(R5)                                                 
*                                                                               
CAT10    SR    R1,R1               CATEGORY/OFFICE/DEPT                         
         IC    R1,ACLVLEN                                                       
         SR    R1,R2               LENGTH OF THIS LEVEL                         
         BCTR  R1,0                                                             
         LA    RE,CADCA13+3(R2)                                                 
         EX    R1,*+4                                                           
         MVC   CADCCTG(0),0(RE)    GET THE CATEGORY                             
         CLC   CADCCTG,SPACES                                                   
         BE    XITMOD                                                           
         MVC   KEY,SPACES                                                       
         LA    R1,3(R2,R1)                                                      
         EX    R1,*+4                                                           
         MVC   KEY(0),CADCA13      BUILD KEY FOR CATEGORY LEVEL                 
         LA    R3,CADCCTGN         CATEGORY NAME                                
         BAS   RE,GETNME           GET NAME                                     
         IC    R2,ACLVLEN          SAVE NEW CUMULATIVE LENGTH                   
         CLI   ACLVLEN,12                                                       
         BE    XITMOD                                                           
         LA    R5,L'ACLVALS(R5)                                                 
*                                                                               
         SR    R1,R1               OFFICE                                       
         IC    R1,ACLVLEN                                                       
         SR    R1,R2               LENGTH OF THIS LEVEL                         
         BCTR  R1,0                                                             
         LA    RE,CADCA13+3(R2)                                                 
         EX    R1,*+4                                                           
         MVC   CADCOFF(0),0(RE)    GET THE OFFICE CODE                          
         CLC   CADCOFF,SPACES                                                   
         BE    XITMOD              NO OFFICE - ALL DONE                         
         MVC   KEY,SPACES                                                       
         MVC   KEY(1),CADCA13      COMPANY                                      
         MVC   KEY+1(2),=C'2D'                                                  
         MVC   KEY+3(2),CADCOFF    BUILD KEY FOR OFFICE                         
         LA    R3,CADCOFFN         OFFCIE NAME                                  
         BAS   RE,GETNME           GET NAME                                     
         IC    R2,ACLVLEN          SAVE NEW CUMULATIVE LENGTH                   
         CLI   ACLVLEN,12                                                       
         BE    XITMOD                                                           
         LA    R5,L'ACLVALS(R5)                                                 
*                                                                               
         SR    R1,R1               DEPARTMENT                                   
         IC    R1,ACLVLEN                                                       
         SR    R1,R2               LENGTH OF THIS LEVEL                         
         BCTR  R1,0                                                             
         LA    RE,CADCA13+3(R2)                                                 
         EX    R1,*+4                                                           
         MVC   CADCDPT(0),0(RE)    GET THE OFFICE CODE                          
         CLC   CADCDPT,SPACES                                                   
         BE    XITMOD              NO DEPARTMENT - GET OUT                      
         MVC   KEY+5(3),CADCDPT    BUILD KEY FOR OFFICE                         
         LA    R3,CADCDPTN         DEPARTMENT NAME                              
         BAS   RE,GETNME           GET NAME                                     
         B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* GET NAME FROM VARIOUS RECORDS                                       *         
***********************************************************************         
         SPACE 1                                                                
GETNME   NTR1  ,                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(L'ACTKEY),IO                                                 
         BNE   XIT                 RECORD NOT FOUND                             
*        TM    INDS,INDSEMU        TEST NEW FILE                                
*        BZ    GETNME3                                                          
         MVC   DA,IO+(ACTKDA-ACTRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
GETNME3  LA    R4,IO                                                            
         MVI   ELCODE,NAMELQ       GET NAME ELEMENT                             
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         USING NAMELD,R4                                                        
         SR    R1,R1                                                            
         IC    R1,NAMLN                                                         
         SH    R1,=H'3'                                                         
         EX    R1,*+4                                                           
         MVC   0(0,R3),NAMEREC                                                  
         MVC   CADCLOWN,SPACES                                                  
         EX    R1,*+4                                                           
         MVC   CADCLOWN(0),NAMEREC                                              
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER INTERFACE                                              *         
***********************************************************************         
         SPACE 1                                                                
HIGH     LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 CADCDMGR,DMCB,DMRDHI,ACCDIR,KEY,IO                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
GET      LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 CADCDMGR,DMCB,GETREC,ACCMST,DA,IO,WORK                           
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
* RESTORE LAST KEY READ FROM DIRECTORY FOR APPLICATION IF THIS        *         
* MODULE CHANGED THE READ SEQUENCE                                    *         
***********************************************************************         
         SPACE 1                                                                
***********************************************************************         
*                                                                     *         
*ITMOD   L     RE,ADTF                                                *         
*        USING ISDTF,RE                                               *         
*        LH    R1,ISKEYLN1                                            *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   WORK,0(RE)          EXTRACT CURRENT KEY                *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
*        EX    R1,*+8                                                 *         
*        BZ    XITMOD2                                                *         
*        OC    SAVKEY(0),SAVKEY    TEST ANY PREVIOUS DIRECTORY I/O    *         
*        EX    R1,*+8                                                 *         
*        BE    XITMOD2                                                *         
*        CLC   SAVKEY(0),WORK      TEST DIRECTORY I/O EXECUTED        *         
*        GOTO1 CADCDMGR,DMCB,DMREAD,DIR,SAVKEY,IO                     *         
*        BNE   ERR2                                                   *         
***********************************************************************         
                                                                                
XITMOD   OC    SAVKEY(L'ACCKEY),SAVKEY                                          
         BZ    XITMOD2                                                          
         GOTO1 CADCDMGR,DMCB,DMKEY,ACCDIR,WORK                                  
         CLC   SAVKEY(L'ACCKEY),WORK                                            
         BE    XITMOD2                                                          
         GOTO1 (RF),(R1),(X'08',DMREAD),ACCDIR,SAVKEY,SAVKEY                    
*                                                                               
XITMOD2  DS    0H                                                               
         SPACE 2                                                                
XIT      XIT1  ,                                                                
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS                                                      *         
***********************************************************************         
         SPACE 1                                                                
ERR1     DC    H'0',C'RECORD NOT FOUND'                                         
ERR2     DC    H'0',C'DISK ERROR'                                               
ERR3     DC    H'0',C'ELEMENT NOT FOUND'                                        
         SPACE 2                                                                
*MDTFA   DC    C'DTFAD  '                                                       
DMKEY    DC    C'DMKEY  '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
GETREC   DC    C'GETREC '                                                       
*CCFIL   DC    C'ACCOUNT'                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
EFFS     DC    4X'FF'                                                           
SPACES   DC    CL64' '                                                          
NINES    DC    CL9'999999999'                                                   
         SPACE 2                                                                
DATADISP DC    Y(ACTRFST-ACTRECD)  DISPLACEMENT TO FIRST ELEMENT                
*LDDISP  DC    Y(ACCORFST)                                                      
*EWDISP  DC    Y(ACTRFST-ACTRECD)                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR LOCAL WORKING STORAGE                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                     ** WORKING STORAGE DSECT **                  
*DTF     DS    A                   A(ACCFIL/ACCDIR DTF)                         
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
ELCODE   DS    XL1                                                              
*ATADISP DC    H                   DISPLACEMENT TO FIRST ELEMENT                
*IR      DS    CL(L'ACCFIL)        DIR NAME (ACCFIL/ACCDIR)                     
SAVKEY   DS    XL64                SAVED DIRECTORY KEY AREA                     
SAVCUL   DS    CL3                 SAVE C/U/L                                   
SAVACL   DS    CL100               SAVE ACCOUNT LENGTHS FOR 13                  
*NDS     DS    XL1                 INDICATOR BYTE                               
*NDSEMU  EQU   X'80'               EMULATED ACCFIL (NEW FILES)                  
*                                                                               
*                                                                               
DA       DS    XL4                 DISK ADDRESS                                 
KEY      DS    XL64                KEY AREA                                     
IO       DS    2000X               I/O AREA                                     
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE ACCATKIND                                                      
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
*********PRINT OFF                                                              
*********INCLUDE DMDTFIS                                                        
*********PRINT ON                                                               
         SPACE 1                                                                
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014ACCATKIN  05/01/02'                                      
         END                                                                    
