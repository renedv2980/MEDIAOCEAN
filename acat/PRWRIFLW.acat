*          DATA SET PRWRIFLW   AT LEVEL 010 AS OF 02/22/16                      
*CATALP PRWRIFLW                                                                
         TITLE 'PRWRIFLW - DATE FILTERING FOR FLOWCHARTING - ENTRY PTS'         
         ENTRY DOFLOW                                                           
PRWRIFLW CSECT                                                                  
         TITLE 'PRWRIFLW - CHANGE LOG'                                          
         TITLE 'PRWRIFLW - FILTER DATES FOR FLOWCHART COLUMN - DOFLOW'          
***********************************************************************         
*                                                                     *         
*        FLOWCHART EDITING                                            *         
*                                                                     *         
*        DATE MUST FALL WITHIN RANGE FOR COLUMN                       *         
*           START AND DATES FOR COLUMN AT GLARGS+9-14                 *         
*                                                                     *         
*NTRY                                                                 *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
DOFLOW   NMOD1 0,#DFL                                                           
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         L     R4,AIO1             ESTABLISH BUY RECORD                         
         USING PBUYRECD,R4                                                      
*                                                                               
         MVI   FLOWDATA,0          INIT SIGNIFICANT DATA FOUND SWITCH           
*                                                                               
         CLI   GLARGS+7,C'$'       IGNORE IF NOT A FLOWCHART FIELD              
         BNE   DOFLOWX                                                          
*                                                                               
         SR    RF,RF               INIT DATE POINTER                            
*                                                                               
*        DETERMINE DATE TO USE IN TEST                                          
*                                                                               
*        BUY RECORD                                                             
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ   IGNORE IF NO BUY RECORD                      
         BNE   DFLBUYN                                                          
*                                                                               
         CLI   GLARGS+8,C'I'       IF INSERT DATE FILTER                        
         BNE   *+8                                                              
         LA    RF,PBUYKDAT            USE BUY INSERT DATE                       
*                                                                               
         CLI   GLARGS+8,C'B'       IF  BILLABLE DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDBDATE             USE BILLABLE DATE                        
*                                                                               
         CLI   GLARGS+8,C'P'       IF  PAYABLE DATE FILTER                      
         BNE   *+8                                                              
         LA    RF,PBDPDATE             USE PAYABLE  DATE                        
*                                                                               
         CLI   GLARGS+8,C'S'       IF  ON-STAND DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDSDATE             USE ON-SALE  DATE                        
*                                                                               
         CLI   GLARGS+8,C'M'       IF  MATERIALS DATE FILTER                    
         BNE   *+8                                                              
         LA    RF,PBDMDATE             USE MATERIALS CLOSING DATE               
*                                                                               
         CLI   GLARGS+8,C'C'       IF  CLOSING  DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDCDATE             USE CLOSING DATE                         
*                                                                               
         CLI   GLARGS+8,C'L'       IF  BILLED DATE FILTER                       
         BNE   DFLBUYY                                                          
         ICM   RE,15,PBBLLELA      BILL ELEMENT ADDRESS                         
         BZ    DFLBUYY                                                          
         USING PBILELD,RE          ESTABLISH BILLING ELEMENT                    
         LA    RF,PBLDATE              USE BILLABLE DATE                        
         DROP  RE                  DROP BILL ELEMENT USING                      
*                                                                               
DFLBUYY  DS    0H                                                               
*                                                                               
         B     DFLTEST                                                          
*                                                                               
DFLBUYN  DS    0H                                                               
*                                                                               
*        ISSUE RECORD                                                           
*                                                                               
         CLI   PBUYKRCD,X'29'      IF ISSUE RECORD IN IOA1                      
         BNE   DFLISSN                                                          
*                                                                               
         L     RF,PBISSELA             POINT TO ISSUE ELEMENT                   
         LA    RF,PISSDAT-PISSEL29(RF) POINT TO ISSUE DATE                      
*                                                                               
         B     DFLTEST                                                          
*                                                                               
DFLISSN  DS    0H                                                               
*                                                                               
*        BILL RECORD                                                            
*                                                                               
         CLI   PBUYKRCD,X'08'      IF BILL RECORD IN IOA1                       
         BNE   DFLBILN                                                          
*                                                                               
         MVC   DUB(2),PBILKMOS-PBILLKEY(R4)   COPY MONTH OF SERVICE             
         MVI   DUB+2,15                       ASSUME MIDDLE OF MONTH            
*                                                                               
         LA    RF,DUB              POINT TO MONTH OF SERVICE                    
*                                                                               
         B     DFLTEST                                                          
*                                                                               
DFLBILN  DS    0H                  NON-DATE DEPENDENT DATA                      
*                                                                               
*        BUDGET RECORD                                                          
*                                                                               
         L     R4,AIO3             POINT TO AIO3                                
*                                                                               
         CLI   PBUYKRCD,X'18'      IF BUDGET RECORD IN IOA3                     
         BNE   DFLBDGN                                                          
*                                                                               
         LA    RF,PBBDGTST         POINT TO BUDGET CURRENT PERIOD               
*                                                                               
         B     DFLTEST                                                          
*                                                                               
DFLBDGN  DS    0H                  NON-DATE DEPENDENT DATA                      
*                                                                               
         MVI   FLOWDATA,C'Y'       INDICATE SIGNIFICANT DATA FOUND              
*                                                                               
         B     DOFLOWX                                                          
*                                                                               
DFLTEST  DS    0H                                                               
*                                                                               
         LTR   RF,RF               DONE IF NO DATE FOUND TO FILTER ON           
         BZ    DOFLOWX                                                          
*                                                                               
*        DATE MUST LIE WITHIN RANGE                                             
*                                                                               
         TM    PBQPTSW,PBQPTDYQ    IF GROUPING BY YEAR                          
         BNO   DFLTSTYN                                                         
*                                     CHECK ONLY MONTH                          
*                                                                               
         CLC   1(2,RF),GLARGS+10                                                
         BL    DFLDROP                                                          
         CLC   1(2,RF),GLARGS+13                                                
         BH    DFLDROP                                                          
*                                                                               
         MVI   FLOWDATA,C'Y'       INDICATE SIGNIFICANT DATA FOUND              
*                                                                               
         B     DOFLOWX                                                          
*                                                                               
DFLTSTYN DS    0H                                                               
*                                                                               
         CLC   0(3,RF),GLARGS+9                                                 
         BL    DFLDROP             REJECT - BEFORE START DATE                   
         CLC   0(3,RF),GLARGS+12                                                
         BH    DFLDROP             REJECT - AFTER  END   DATE                   
*                                                                               
         MVI   FLOWDATA,C'Y'       INDICATE SIGNIFICANT DATA FOUND              
*                                                                               
         B     DOFLOWX                                                          
*                                                                               
DFLDROP  DS    0H                                                               
*                                                                               
*        NEED TO CLEAR OUT DATA                                                 
*              CHARACTER TO SPACES                                              
*              PACKED TO ZERO                                                   
*              REST TO NULLS                                                    
*                                                                               
         L     R1,GLADTENT         ESTABLISH DRIVE TABLE ENTRY                  
         USING DRIND,R1            AS AN INPUT RECORD                           
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,DRINREP        GET REPETITION FACTOR                        
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,DRINFLEN       GET FIELD LENGTH                             
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
*                                                                               
         CLI   DRINTYPE,C'C'       IF CHARACTER FIELD                           
         BNE   DFLCLRCN                                                         
*                                                                               
         EX    RF,*+16                                                          
         LA    R2,1(RF,R2)                                                      
         BCT   R0,*-8                 GET ALL REPETITIONS                       
         B     DFLCLRX                                                          
         MVC   0(0,R2),SPACES         CLEAR TO SPACES                           
*                                                                               
DFLCLRCN DS    0H                                                               
*                                                                               
         CLI   DRINTYPE,C'P'       IF PACKED FIELD                              
         BE    *+8                                                              
         CLI   DRINTYPE,C'M'       OR MSKED  FIELD                              
         BNE   DFLCLRPN                                                         
*                                                                               
         SLL   RF,4                   LENGTH TO LEFT NYBBLE                     
*                                                                               
         EX    RF,*+16                                                          
         LA    R2,1(RF,R2)                                                      
         BCT   R0,*-8                 GET ALL REPETITIONS                       
         B     DFLCLRX                                                          
         ZAP   0(0,R2),=P'0'          CLEAR TO ZERO                             
*                                                                               
DFLCLRPN DS    0H                                                               
*                                                                               
         EX    RF,*+16                                                          
         LA    R2,1(RF,R2)                                                      
         BCT   R0,*-8                 GET ALL REPETITIONS                       
         B     DFLCLRX                                                          
         XC    0(0,R2),0(R2)       ELSE CLEAR TO NULLS                          
*                                                                               
DFLCLRX  DS   0H                                                                
*                                                                               
DOFLOWX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRIFLW DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         EJECT                                                                  
         PRINT   ON                                                             
* DRGLOBAL                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* DRIVETABLE                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010PRWRIFLW  02/22/16'                                      
         END                                                                    
