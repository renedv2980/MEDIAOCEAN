*          DATA SET ACPROCPT   AT LEVEL 006 AS OF 12/11/09                      
*CATALP ACPROCPT                                                                
*                                                                               
***********************************************************************         
*                                                                     *         
*  YOU MUST RE-LINK ACLD AND ACPROGEN WHEN YOU CHANGE THIS MODULE.    *         
*  YOU MUST ALSO RELINK SRUPD60       WHEN YOU CHANGE THIS MODULE.    *         
*                                                                     *         
*  IT IS ABSOLUTELY NECESSARY THAT BOTH ARE INSTALLED LIVE AT THE     *         
*                                                                     *         
*  SAME TIME.  BE CAREFUL, AND DON'T FORGET!!!!                       *         
*                                                                     *         
***********************************************************************         
*                                                                               
         TITLE 'ACPROCPT - PRODUCTION - CREATE DIRECTORY POINTERS'              
         PRINT NOGEN                                                            
*                                                                               
*        PARA1  A(RECORD)                                                       
*        PARA2  A(AREA TO CREATE PASSIVE POINTERS)                              
*               LAST POINTER FOLLOWED BY X'FF'                                  
*        PARA3  A(PARAMTER BLOCK - COVERED BY ACPROCPTRD)                       
*        PARA4  A(LDCPTR BLOCK) (IF PARAM1 BYTE0=X'FF')                         
*                                                                               
ACPROCPT CSECT                                                                  
         NMOD1 WRKX-WRKD,ACPROCPTR,RA,RR=R4                                     
         USING WRKD,RC                                                          
         ST    R4,RELO                                                          
         LM    R2,R4,0(R1)         R2 = A(RECORD)                               
         SR    R5,R5                                                            
         CLI   0(R1),X'FF'         TEST P4 PASSED                               
         BNE   *+8                                                              
         L     R5,12(R1)           R5=A(LDCPTR BLOCK)                           
         USING CPTRBLKD,R5                                                      
*                                  R3 = A(POINTER OUTPUT BLOCK)                 
*                                                                               
         USING CPTRD,R4            R4 = A(PARAMETER BLOCK)                      
         MVC   EMUFLAG,CPEMUFLG    COPY EMULATION FLAG                          
         DROP  R4                                                               
*                                  SET APPROPRIATE DATADISP                     
         MVC   DATADISP,=Y(ACCORFST)                                            
         CLI   EMUFLAG,CPEMUYES                                                 
         BE    *+10                                                             
         MVC   DATADISP,=Y(ACCRFST-ACCRECD)                                     
*                                                                               
         CLI   0(R3),X'FF'         IF DON'T ALREADY HAVE ACTIVE POINTER         
         BE    POINT2                                                           
         USING ACCRECD,R3          ADD ACTIVE POINTER TO BLOCK                  
         XC    0(ACCKLEN,R3),0(R3)                                              
         MVC   ACCKEY,ACCKEY-ACCRECD(R2)                                        
         MVC   ACCKSTA(1),ACCRSTA-ACCRECD(R2)                                   
         CLI   EMUFLAG,CPEMUYES    TEST IF EMULATED RECORD                      
         BE    *+10                YES                                          
         MVC   ACCKSTA,ACCRSTA-ACCRECD(R2)                                      
         LA    R3,ACCKLEN(R3)      R3 = A(NEXT PASSIVE POINTER RECORD)          
*                                                                               
*                                                                               
POINT2   MVI   BYTE,0              EXTRACT RECORD TYPE/COMPANY                  
         CLI   EMUFLAG,CPEMUYES                                                 
         BE    *+8                                                              
         MVI   BYTE,C'D'                                                        
*                                                                               
         LTR   R5,R5               TEST LDCPTR BLOCK PASSED                     
         BZ    POINT4                                                           
         MVC   RECTYPE,CPTREC      YES - EXTRACT RECORD TYPE                    
         CLI   CPTREC,0                                                         
         BE    EXIT                                                             
         MVC   COMPANY,CPTCPY      AND COMPANY                                  
         B     POINT6                                                           
*                                                                               
POINT4   GOTO1 =V(ACRECTYP),DMCB,(BYTE,(R2)),RR=RELO                            
         MVC   RECTYPE,0(R1)                                                    
         MVC   COMPANY,1(R1)                                                    
*                                                                               
POINT6   LR    R6,R2               SET R6 FOR GETEL CALLS                       
*                                                                               
         LA    R4,PPLIST           R4 = A(RECORD TYPE/ROUTINE ADDRESS)          
         USING PPTABD,R4                                                        
*                                                                               
POINT8   CLI   PPRTYP,0            LOOK FOR MATCH ON RECORD TYPE                
         BE    POINT10                (ALWAYS MATCH RECTYPE 0)                  
         CLC   RECTYPE,PPRTYP                                                   
         BNE   POINT12                                                          
         CLI   PPLEDG,C' '                                                      
         BE    POINT10                                                          
         USING LDGRECD,R2                                                       
         CLC   LDGKUNT(2),PPLEDG                                                
         BNE   POINT12                                                          
         DROP  R2                                                               
*                                  INITIALIZE PASSIVE POINTER                   
POINT10  XC    0(ACCKLEN,R3),0(R3)                                              
         MVC   ACCKEY(2),PPKTYP    SET TYPE, SUBTYPE, AND STATUS                
         MVC   ACCKSTA(1),ACCRSTA-ACCRECD(R2)                                   
         CLI   EMUFLAG,CPEMUYES    TEST IF EMULATED RECORD                      
         BE    *+10                YES                                          
         MVC   ACCKSTA,ACCRSTA-ACCRECD(R2)                                      
*                                                                               
         L     RF,PPARTN           PICK UP CREATE ROUTINE ADDRESS               
         A     RF,RELO                                                          
         BASR  RE,RF               AND EXECUTE                                  
         BNE   POINT12             (NO POINTER CREATED)                         
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT POINTER                         
*                                                                               
POINT12  LA    R4,PPTABL(R4)       BUMP TO NEXT PPLIST ENTRY                    
         CLI   PPRTYP,X'FF'                                                     
         BNE   POINT8                                                           
*                                                                               
POINT14  MVI   0(R3),X'FF'         MARK END OF POINTER LIST                     
         B     EXIT                AND EXIT                                     
*                                                                               
         DS    0D                                                               
PPLIST   DS    0CL(PPTABL)                                                      
         DC    AL1(ACRTACTL),C'SJ',AL1(STCKTYPQ,STCKSUBQ,0,0,0)                 
         DC    A(PASSSTC)                                                       
*                                                                               
         DC    AL1(ACRTACTL),C'SJ',AL1(SACKTYPQ,SACKSUBQ,0,0,0)                 
         DC    A(PASSSAC)                                                       
*                                                                               
         DC    AL1(ACRTORD),C'  ',AL1(STCKTYPQ,STCKSUBQ,0,0,0)                  
         DC    A(PASSSTC)                                                       
*                                                                               
         DC    AL1(ACRTORD),C'  ',AL1(SACKTYPQ,SACKSUBQ,0,0,0)                  
         DC    A(PASSSAC)                                                       
*                                                                               
         DC    AL1(0),C'  ',AL1(0,0,0,0,0)                                      
         DC    A(PASSRAP)                                                       
*                                                                               
         DC    X'FF'                                                            
         EJECT                                                                  
*              STUDIO TYPE CODE POINTERS                                        
*                                                                               
         USING STCRECD,R3                                                       
         USING ACTRECD,R2                                                       
         USING LNKELD,R6                                                        
*                                                                               
PASSSTC  NTR1                                                                   
         MVI   ELCODE,LNKELQ       FROM LINK ELEMENT...                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVC   STCKSTY,LNKSTUD     STUDIO TYPE                                  
         MVC   STCKSJB,LNKSTJB     STUDIO JOB                                   
         MVC   STCKAJB,LNKAGJB     AGENCY JOB                                   
*                                                                               
         CLI   PPRTYP,ACRTACTL     FROM ACCOUNT KEY...                          
         BNE   PASSSTC4                                                         
         MVC   STCKCPY,ACTKCPY     COMPANY                                      
         B     YES                                                              
*                                                                               
         USING ORDRECD,R2          FROM ORDER KEY...                            
PASSSTC4 MVC   STCKCPY,ORDKCPY     COMPANY                                      
         MVC   STCKPO,ORDKORD      ORDER NUMBER                                 
         B     YES                                                              
         EJECT                                                                  
*              STUDIO AGENCY JOB CODE POINTERS                                  
*                                                                               
         USING SACRECD,R3                                                       
         USING ACTRECD,R2                                                       
         USING LNKELD,R6                                                        
*                                                                               
PASSSAC  NTR1                                                                   
         MVI   ELCODE,LNKELQ       FROM LINK ELEMENT...                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVC   SACKAJB,LNKAGJB     AGENCY JOB                                   
         MVC   SACKSTY,LNKSTUD     STUDIO TYPE                                  
         MVC   SACKSJB,LNKSTJB     STUDIO JOB                                   
*                                                                               
         CLI   PPRTYP,ACRTACTL     FROM ACCOUNT KEY...                          
         BNE   PASSSAC4                                                         
         MVC   SACKCPY,ACTKCPY     COMPANY                                      
         B     YES                                                              
*                                                                               
         USING ORDRECD,R2          FROM ORDER KEY...                            
PASSSAC4 MVC   SACKCPY,ORDKCPY     COMPANY                                      
         MVC   SACKPO,ORDKORD      ORDER NUMBER                                 
         B     YES                                                              
         EJECT                                                                  
* THIS ROUTINE HANDLES BUILDING THE ACTIVITY PASSIVE POINTER FOR                
* ALL TYPES OF RECORDS.  IF THE RECORD HAS A POINTER ELEMENT OF TYPE            
* PTRTRAP, THE ROUTINE EXTRACTS THE PASSIVE POINTER FROM THE ELEMENT.           
*                                                                               
         USING RAPRECD,R3                                                       
         USING ACTRECD,R2                                                       
         USING PTRELD,R6                                                        
*                                                                               
PASSRAP  NTR1                                                                   
         MVI   ELCODE,PTRELQ       FROM POINTER ELEMENT...                      
         BAS   RE,GETEL                                                         
*                                                                               
PASSRAP2 BNE   NO                  KEEP LOOKING FOR POINTER ELEMENT             
         CLI   PTRTYPE,PTRTRAP         UNTIL ACTIVITY POINTER ELEMENT           
         BE    PASSRAP5                                                         
         BAS   RE,NEXTEL                                                        
         B     PASSRAP2                                                         
*                                                                               
PASSRAP5 MVC   RAPKEY,PTRCODE      GET ACTIVITY POINTER                         
         NI    RAPKSTA,X'FF'-ACTSDELT  TURN OFF DELETE BIT                      
         B     YES                                                              
         EJECT                                                                  
*              ODDMENTS, LTORG                                                  
*                                                                               
         GETEL (R6),DATADISP,ELCODE                                             
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
SPACES   DC    CL60' '                                                          
HEXFFS   DC    8X'FF'                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER LOCAL WORKING STORAGE                             
*                                                                               
WRKD     DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
RELO     DS    F                                                                
WORK     DS    CL48                                                             
BYTE     DS    X                                                                
HALF     DS    H                                                                
RECTYPE  DS    X                                                                
COMPANY  DS    X                                                                
ELCODE   DS    CL1                                                              
DATADISP DS    H                                                                
*                                                                               
EMUFLAG  DS    X                   EMULATION FLAG                               
WRKX     EQU   *                                                                
         EJECT                                                                  
*              DSECT TO COVER PASSIVE POINTER TABLE                             
*                                                                               
PPTABD   DSECT                                                                  
PPRTYP   DS    XL1                 RECORD TYPE (FROM ACRECEQUS)                 
PPLEDG   DS    CL2                 UNIT/LEDGER FILTER                           
PPKTYP   DS    XL1                 KEY RECORD TYPE BYTE                         
PPSUBTYP DS    XL1                 KEY SUB RECORD TYPE BYTE                     
         DS    XL3                 SPARE                                        
PPARTN   DS    A                   A(PASSIVE POINTER BUILD ROUTINE)             
PPTABL   EQU   *-PPTABD                                                         
         EJECT                                                                  
       ++INCLUDE ACRECEQUS                                                      
       ++INCLUDE ACPROCPTRD                                                     
         EJECT                                                                  
* ACGENFILE                                                                     
* ACGENBOTH                                                                     
* ACLDCPTRD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
CPTRBLKD DSECT                                                                  
       ++INCLUDE ACLDCPTRD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006ACPROCPT  12/11/09'                                      
         END                                                                    
