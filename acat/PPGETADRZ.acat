*          DATA SET PPGETADRZ  AT LEVEL 022 AS OF 04/09/97                      
*CATALP PPGETADR                                                                
         TITLE 'PPGETADR - MODULE TO HANDLE PUB ADDRESS READING'                
*                                                                               
*        NOTE THAT THIS MODULE READS THE PRINT FILE                             
*        AND WILL DESTROY SEQUENTIAL READING                                    
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*        INPUT                                                                  
*                                                                               
*   PARAM1   BYTE  0    KIND OF REC  - C'C' = CONTRACT ADDRESS                  
*                                      C'P' = PAYING   ADDRESS                  
*                                      C'S' = SHIPPING ADDRESS                  
*                                      C'T' = TRAFFIC  ADDRESS                  
*                                                                               
*            BYTES 1-3  A(PCLTREC DATA - 7 BYTES AS FOLLOWS)                    
*                                      CLIENT AGENCY - 2                        
*                                      CLIENT MEDIA  - 1                        
*                                      CLIENT CODE   - 3                        
*                                      CLIENT OFFICE - 1                        
*                                                                               
*   PARAM2              A(PUBREC)                                               
*                                                                               
*   PARAM3              A(DATAMGR)                                              
*                                                                               
*        OUTPUT                                                                 
*                                                                               
*   PARAM1   BYTE  0    X'FF' IF ERROR                                          
*                       X'00' IF NO ADDRESS FOUND                               
*                       C'A'  PUBREC ADDRESS IN OUTPUT AREA                     
*                       C'R'  REPREC ADDRESS IN OUTPUT AREA                     
*                                                                               
*            BYTES 1-3  3X'FF' = AGENCY                                         
*                       X'FF' + X + BLANK = OFFICE X                            
*                       ELSE CLIENT                                             
*                                                                               
*   PARAM2              A(200-BYTES OF ADDRESS DATA (MAY BE CLEAR))             
*                                                                               
*                                                                               
PPGETADR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPGETADR,CLEAR=YES                                   
         USING WORKD,RC                                                         
         OC    1(3,R1),1(R1)       PCLTREC DATA ADDRESS GIVEN ?                 
         BZ    PGAERR              NO - ERROR                                   
         OC    4(4,R1),4(R1)       PUBREC ADDRESS GIVEN ?                       
         BZ    PGAERR              NO - ERROR                                   
         OC    8(4,R1),8(R1)       DATAMGR ADDRESS GIVEN ?                      
         BZ    PGAERR              NO - ERROR                                   
*                                                                               
         LR    R8,R1               R8=A(PARAMETER LIST)                         
         L     R3,0(R8)            R3=A(PCLTREC DATA)                           
         L     R9,4(R8)            R9=A(PUBREC)                                 
*                                                                               
         MVC   DATAMGR,8(R8)       V(DATAMGR)                                   
         MVC   ADDRTYP(1),0(R8)    ADDRESS TYPE - C, P, S, OR T                 
*                                                                               
         XC    0(8,R8),0(R8)       AREA FOR RETURN INFO                         
         LA    R7,ADRAREA          R7=A(200-BYTE OUTPUT AREA)                   
         XC    ADRAREA,ADRAREA                                                  
*                                                                               
         USING PGETADRD,R7                                                      
         USING PUBRECD,R9                                                       
         USING PCLTRECD,R3                                                      
*                                                                               
*                                                                               
         XC    WORK,WORK                                                        
         MVI   WORK,X'08'        PAYING                                         
         CLI   ADDRTYP,C'P'                                                     
         BE    RREPADR           LOOK FOR PAYING ADDRESSES (PUBAREC)            
         MVI   WORK,X'09'        TRAFFIC                                        
         CLI   ADDRTYP,C'T'                                                     
         BE    RREPADR           LOOK FOR TRAFFIC ADDRESSES (PUBAREC)           
         MVI   WORK,X'0B'        SHIPPING ADDRESS                               
         CLI   ADDRTYP,C'S'                                                     
         BE    RREPADR           LOOK FOR SHIPPING ADDRESSES (PUBAREC)          
         MVI   WORK,X'0A'        CONTRACT                                       
         CLI   ADDRTYP,C'C'                                                     
         BE    RREPADR           LOOK FOR CONTRACT ADDRESSES (PUBAREC)          
         B     PGAERR            ERROR - INVALID OR NO REC TYPE                 
         SPACE 2                                                                
*                                                                               
RREPADR  DS    0H         **** LOOK FIRST FOR PUB ADDRESS RECORDS ****          
*                                                                               
         LA    R4,KEY                                                           
         USING PUBAREC,R4                                                       
         XC    KEY,KEY                                                          
         MVC   PUBAKEY(9),PUBKEY   MEDIA THRU AGENCY CODE                       
         MVI   PUBAKCOD,X'82'      RECORD CODE                                  
         MVC   PUBAKTYP(1),WORK    ADDRESS TYPE                                 
         MVC   PUBAKCLT,PCLTKCLT   CLIENT CODE                                  
*                                                                               
RADREAD  MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PUBDIR,KEY,KEY                           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PUBAKEY(10),KEYSAVE   ANY ADDRESS RECORDS ?                      
         BNE   RREPPUB             NO - GO TRY FOR ADDRESS ELEMENTS             
         CLC   PUBAKTYP(1),WORK    SAME ADDRESS TYPE ?                          
         BE    RADR2               YES                                          
         CLI   WORK,X'0B'          LOOKING FOR SHIPPING ?                       
         BNE   RREPPUB             NO - GO TRY FOR ADDRESS ELEMENTS             
         MVI   WORK,X'09'          YES AND NOT FOUND - GO TRY FOR               
         B     RREPADR             TRAFFIC                                      
*                                                                               
RADR2    DS    0H                  FOUND RECORD                                 
         CLC   PUBAKCLT,=3X'FF'    IS IT A DEFAULT ?                            
         BE    RADR6               YES - GET THE RECORD                         
         CLC   PUBAKCLT,PCLTKCLT   CLIENT SAME AS IN CLIENT HEADER ?            
         BE    RADR6               YES - GET THE RECORD                         
         CLI   PUBAKCLT,X'FF'      OFFICE OVERRIDE ?                            
         BNE   RADR4               NO - TRY FOR OFFICE                          
         CLC   PUBAKCLT+1(1),PCLTOFF   OFFICE MATCH CLIENT HEADER ?             
         BE    RADR6               YES - GET THE RECORD                         
         CLI   KEYSAVE+11,X'FF'    WAS I LOOKING FOR AN OFFICE ?                
         BNE   RADR4               NO - TRY FOR ONE                             
         MVC   PUBAKCLT,=3X'FF'    YES - AND NOT FOUND SO                       
         B     RADREAD             TRY FOR DEFAULT                              
*                                                                               
RADR4    DS    0H                       TRY FOR AN OFFICE                       
         MVI   PUBAKOFF,X'FF'                                                   
         MVC   PUBAKOFF+1(1),PCLTOFF                                            
         MVI   PUBAKOFF+2,X'40'                                                 
         B     RADREAD                                                          
*                                                                               
RADR6    DS    0H                  GET THE ADDRESS RECORD                       
******************************* TEMPORARY "FIX"                                 
         OC    KEY+27(4),KEY+27                                                 
         BZ    RREPPUB             NO FILE ADDRESS                              
******************************* TEMPORARY "FIX"                                 
         GOTO1 DATAMGR,DMCB,(0,GETREC),PUBFIL,KEY+27,IO,DMWORK                  
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   WORK+1(3),PUBAKOFF  SAVE OFFICE/CLIENT/DEFAULT                   
         LA    R5,IO+33                                                         
         ST    R5,FULL             SAVE A(ADDRESS OVERRIDE ELEM)                
*                                                                               
         CLI   WORK,X'0B'          SEE IF LOOKING FOR SHIPPING                  
         BE    RREP20              DON'T TRY FOR REP                            
         CLI   WORK+1,X'FF'        IF HAVE A CLIENT ADDR NO NEED                
         BL    RREP20                  TO LOOK FOR REP                          
         B     RREP6C              TRY FOR A REP                                
*                                                                               
         DROP  R4                                                               
*                                                                               
         EJECT                                                                  
RREPPUB  DS    0H             ** LOOK FOR ADDRESS ELEMENTS IN PUBREC **         
*                                  IF PUB ADDRESS RECORDS NOT FOUND             
         SPACE 2                                                                
         CLI   ADDRTYP,C'S'        LOOKING FOR SHIPPING ?                       
         BNE   RREP0               NO - WORK IS OK                              
         MVI   WORK,X'0B'          YES - MAKE SURE SHIP CODE                    
*                                  TRY FOR ADDRESS FIRST                        
RREP0    SR    R0,R0                                                            
         LA    R2,PUBREC+33                                                     
RREP1    DS    0H                                                               
         CLI   0(R2),0             END OF RECORD ?                              
         BE    RREP6               YES                                          
         CLC   0(1,R2),WORK        CHECK FOR ADDRESS OVERRIDE ELEMENT           
         BE    RREP3                                                            
RREP2    DS    0H                  BUMP TO NEXT ELEMENT                         
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RREP1                                                            
RREP3    DS    0H                  FOUND ELEMENT                                
         USING PUBAOVEL,R2         USE ADDRESS ELEMENT DSECT                    
         CLC   PUBAOFF,=3X'FF'     IS IT A DEFAULT ?                            
         BE    RREP4               YES                                          
         CLC   PUBAOFF,PCLTKCLT    CLIENT SAME AS IN CLIENT HEADER ?            
         BE    RREP4               YES                                          
         CLI   PUBAOFF,X'FF'       OFFICE OVERRIDE ?                            
         BNE   RREP2               NO - GET ANOTHER ELEMENT                     
         CLC   PUBAOFF+1(1),PCLTOFF    OFFICE MATCH CLIENT HEADER ?             
         BNE   RREP2               NO - GET ANOTHER ELEMENT                     
RREP4    DS    0H                                                               
         MVC   WORK+1(3),PUBAOFF   SAVE OFFICE/CLIENT/DEFAULT                   
         ST    R2,FULL             SAVE A(ELEM)                                 
         CLI   WORK,X'0B'          SEE IF LOOKING FOR SHIPPING                  
         BE    RREP20              DON'T TRY FOR REP                            
*                                                                               
         CLI   WORK+1,X'FF'        IF HAVE A CLIENT ADDR NO NEED                
         BL    RREP20                  TO LOOK FOR REP                          
         B     RREP6C                                                           
*                                                                               
RREP6    DS    0H                                                               
         CLI   WORK,X'0B'          SEE IF I WAS TRYING FOR SHIPPING             
         BNE   RREP6C                                                           
         MVI   WORK,X'09'          YES AND NOT FOUND - THEN TRY FOR             
         B     RREP0               TRAFFIC                                      
*                                                                               
         DROP  R2                                                               
*                                  NOW TRY FOR A REP                            
RREP6C   DS    0H                                                               
         LA    R2,PUBREC+33                                                     
         XC    DUB,DUB                                                          
         SR    R0,R0                                                            
RREP7    DS    0H                                                               
         CLI   0(R2),0                                                          
         BE    RREP11                                                           
         CLI   0(R2),X'14'                                                      
         BE    RREP9                                                            
RREP8    DS    0H                                                               
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RREP7                                                            
RREP9    DS    0H                                                               
         USING PUBREPEL,R2                                                      
         CLC   PUBRPOFF,=3X'FF'                                                 
         BE    RREP10                                                           
         CLC   PUBRPOFF,PCLTKCLT                                                
         BE    RREP10                                                           
         CLI   PUBRPOFF,X'FF'                                                   
         BNE   RREP8                                                            
         CLC   PUBRPOFF+1(1),PCLTOFF                                            
         BNE   RREP8                                                            
RREP10   DS    0H                                                               
         OC    PUBPAREP(12),PUBPAREP                                            
         BZ    RREP8               NO OVERIDES THIS ELEM                        
         LA    RF,PUBPAREP                                                      
         CLI   ADDRTYP,C'P'        PAY REP ?                                    
         BE    RREP10B             YES                                          
         LA    RF,PUBTRREP                                                      
         CLI   ADDRTYP,C'T'        TRAFFIC REP ?                                
         BE    RREP10B             YES                                          
         CLI   ADDRTYP,C'S'        OR SHIPPING - USE TRAFFIC REP                
         BE    RREP10B                                                          
         LA    RF,PUBCNREP         MUST BE CONTRACT                             
RREP10B  DS    0H                                                               
         MVC   DUB(4),0(RF)                                                     
         MVC   DUB+4(3),PUBRPOFF                                                
         MVC   1(3,R8),PUBRPOFF    "RETURN" OFFICE/CLIENT/DEFAULT               
RREP11   DS    0H                                                               
         OC    WORK+1(3),WORK+1                                                 
         BZ    RREP12                                                           
         CLC   WORK+1(3),DUB+4     TEST 'LEVEL'                                 
         BNH   RREP20              ADDR MORE SPECIFIC                           
RREP12   DS    0H                                                               
         OC    DUB(4),DUB                                                       
         BZ    RREP20              NO REP                                       
         DROP  R2                                                               
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),PUBKEY+7     AGENCY                                       
         MVC   KEY+2(1),PUBKEY     MEDIA                                        
         MVI   KEY+3,X'11'                                                      
         MVC   KEY+4(4),DUB        REP CODE                                     
         XC    PGADREC(200),PGADREC                                             
         MVC   KEYSAVE(10),KEY                                                  
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PRTDIR,KEY,KEY                           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   KEYSAVE(10),KEY                                                  
         BNE   PGAXIT       NO ADDRESS FOUND - EXIT WITH BYTE 0=X'00'           
         GOTO1 DATAMGR,DMCB,(0,GETREC),PRTFIL,KEY+27,PGADREC,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   0(R8),C'R'          ADDRESS FROM PREPREC                         
         B     PGAXIT              EXIT WITH BYTE 0=R                           
*                                                                               
         SPACE 3                                                                
RREP20   DS    0H                                                               
         XC    PGADREC(200),PGADREC                                             
         MVI   0(R8),0                                                          
         CLI   WORK+1,0                                                         
         BE    PGAXIT              NO ADDRESS FOUND                             
         L     R2,FULL             RESTORE POINTER TO ADDRESS                   
         USING PUBAOVEL,R2                                                      
         MVI   0(R8),C'A'          ADDRESS FROM PUBAOVEL                        
         MVC   1(3,R8),PUBAOFF                                                  
         MVC   PGADELEM(2),PUBAOVEL                                             
         MVC   PGADNAME(122),PUBAONAM   (NAME,LIN1+2,ATTN,TELE)                 
*                                                                               
         CLI   PUBAOVEL+1,165      SEE IF "SHORT" ELEMENT                       
         BL    *+16                                                             
*                                                                               
         MVC   PGADLIN3(L'PGADLIN3),PUBAOLN3                                    
         MVC   PGADFAX(L'PGADFAX),PUBAOFAX                                      
*                                                                               
         MVC   PGADKEY(3),PCLTKAGY        AGY/MED                               
         MVC   PGADKRCD(1),PUBAOVEL       RECORD CODE                           
*                                                                               
         B     PGAXIT              EXIT WITH BYTE 0=A                           
*                                                                               
         DROP  R2                                                               
*                                                                               
PGAERR   DS    0H                                                               
         MVI   0(R8),X'FF'         ERROR IN CALL                                
*                                                                               
PGAXIT   DS    0H                                                               
         ST    R7,FULL                                                          
         MVC   4(4,R8),FULL        ADDRESS OF ADDRESS DATA                      
         XIT1                                                                   
         LTORG                                                                  
         SPACE 1                                                                
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
PRTDIR   DC    C'PRTDIR  '                                                      
PRTFIL   DC    C'PRTFILE '                                                      
PUBDIR   DC    C'PUBDIR  '                                                      
PUBFIL   DC    C'PUBFILE '                                                      
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE **                        
WORK     DS    CL64                                                             
SPACES   DS    CL20                                                             
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
ELCODE   DS    CL1                                                              
ADDRTYP  DS    CL1                                                              
FULL     DS    F                                                                
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
ADRAREA  DS    200C                CREATE ADDRESS DATA HERE                     
IO       DS    4000C                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
PGETADRD DSECT                                                                  
       ++INCLUDE PPGETADRD                                                      
         EJECT                                                                  
PCLTRECD DSECT                                                                  
PCLTKAGY DS    CL2                                                              
PCLTKMED DS    CL1                                                              
PCLTKCLT DS    CL3                                                              
PCLTOFF  DS    CL3                                                              
         EJECT                                                                  
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBREPEL                                                       
         EJECT                                                                  
         ORG   PUBREC                                                           
PUBARECD DSECT                                                                  
       ++INCLUDE PPUBAREC                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022PPGETADRZ 04/09/97'                                      
         END                                                                    
