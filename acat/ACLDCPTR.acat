*          DATA SET ACLDCPTR   AT LEVEL 079 AS OF 01/10/21                      
*CATALP ACLDCPTR                                                                
LDCPTR   TITLE '- CREATE DIRECTORY POINTERS'                                    
*NSHE 121 08MAY09  APPROVER RECORD CHANGES                                      
*NSHE 122 15MAY09  EXPENSE RECORD CHANGES                                       
*NSHE 123 20MAY09  GROUP LIST CHANGES                                           
*NSHE 124 05JUN09  CHANGE TO PASSIVES FOR EXPENSES                              
*SMAN 127 20NOV09  <BR10288Y> FIX TO INTERNAL ESTIMATE PASSIVES                 
*JFOS 043 18FEB10  <LO01-9183> APPPASDS FOR ESTS, ORDS WITH UN/APP ESTS         
*YNGX 043 19JAN10  <OT58649L> FIX TO ESTIMATE BILLING PASSIVES                  
*MPEN 055 27JUL11  FIX FOR ABOVE TO ENSURE ONLY SAVE ON OFFLINE CALL            
*NSHE     03AUG11  <PR001318> CREATE NEW PASSIVES FOR EXPENSE APPROVERS         
*                  WHERE THE CLAIM SUBMITTER CHOSES APPROVER                    
*TKLU 149 24FEB14  <RD000869> NEW JOB DATE PASSIVES                             
*TKLU 150 16JUN14  <RD003030> OPEN UP PIDKJSSQ/PIDKJSAQ FOR 'ALL' JOBS          
*TKLU 151 26JUN14  <RD003081> JOB ADDED DATE (PIDKADAT) FOR PIDKJSA/SQ          
*TKLU 152 18JUL14  <RD003590> JDTPASD DATE SETTING BUG FIX                      
*TKLU 153 25JUL14  <RD003687> JDTPASD - CREATE FOR DRAFT JOBS, TOO              
*RGUP 064 22AUG14  <RD002956> COPY ACCOUNT STATUS BYTE FOR SAPRECD              
*MPEN 154 28JUL14  <RD003303> ERAPASD - RECENTLY ACTIONED PASSIVES              
*TKLU 160 03SEP15  <RD007727> ESTIMATE NAME SEARCH SUPPORT                      
*TKLU 161 02DEC15  <RD009622> ORDER NAME SEARCH SUPPORT                         
*NSHE 167 25MAY16  <DSRD-11237> CREATE POINTERS FOR ASSIGNED JOBS               
*MPEN 170 06FEB17  <DSRD-14837> FIX FOR ASSIGNED JOBS PASSIVES                  
*RGUP 077 21MAY18  <SPEC-20692> NEED ADDITIONAL MEDIA FOR DIGIAL AUDIO          
*MPEN 172 23OCT18  <DSRD-20447> NEW TIMEOFF APPROVER                            
*ASAX 079 04JAN21  <DSRD-28268> CREATE EXSPASD FOR DRAFT CLAIMS                 
**********************************************************************          
* PARA1 A(RECORD)                                                    *          
* PARA2 A(AREA TO CREATE PASSIVE POINTERS)                           *          
*       LAST POINTER FOLLOWED BY EOL (SEE EQUATE)                    *          
**********************************************************************          
         SPACE 1                                                                
LDCPTR   CSECT                                                                  
*        PRINT NOGEN                                                            
         NMOD1 WORKL,ACLDCPTR,R9,R8,R7,RR=R4                                    
         USING WORKD,RC                                                         
*        PROT  OFF                                                              
         ICM   RA,15,8(R1)         TEST DMLDMOD CALL                            
         BNZ   LDCP006             NO - SKIP                                    
*        PROT  OFF                                                              
         CLI   WINIT,C'Y'          EST FIRST TIME CALL                          
         MVI   WINIT,C'Y'                                                       
         BE    LDCP006             INITIALISE W/S ONCE ONLY                     
         LR    R2,RC                                                            
         LA    R3,WORKL                                                         
         SR    RF,RF                                                            
         SR    RE,RE                                                            
         MVCL  R2,RE                                                            
         MVC   CLIVALS(CLIVALSL+PROVALSL+JOBVALSL),SPACES                       
         MVC   ACCOFFC,SPACES                                                   
         MVI   PROAGR1,AGRKGTY1                                                 
         MVI   PROAGR2,AGRKGTY2                                                 
LDCP006  LM    R2,R3,0(R1)         R2=A(PRIME DATA RECORD)                      
         ST    R4,RELO                                                          
         ST    R3,ADPASS           R3=A(PASSIVE POINTERS)                       
         MVC   VCOMFACS,20(R1)                                                  
         XC    ASEARCHD,ASEARCHD                                                
         CLI   0(R1),X'FF'         TEST CALLER PASSING P7                       
         BNE   *+10                                                             
         MVC   ASEARCHD,24(R1)     YES - SET A(SEARCH BLOCK)                    
         MVI   OFFLINE,C'N'                                                     
         LTR   RA,RA                                                            
         BNZ   *+12                                                             
         LA    RA,CPTRBLK                                                       
         MVI   OFFLINE,C'Y'                                                     
         USING CPTRBLK,RA                                                       
         MVI   8(R1),EOL           SET END OF LIST INDICATOR                    
         L     R1,16(R1)                                                        
         MVC   DA,0(R1)            SAVE DISK ADDRESS OF RECORD                  
         LA    R0,VTYPESN          RELOCATE V-TYPES                             
         LA    R1,VTYPES                                                        
         LA    RE,VTYPESR                                                       
LDCP008  ICM   RF,15,0(R1)                                                      
         A     RF,RELO                                                          
         STCM  RF,15,0(RE)                                                      
         AHI   R1,L'VTYPES                                                      
         AHI   RE,L'VTYPES                                                      
         BCT   R0,LDCP008                                                       
*                                                                               
         ICM   RF,15,=V(ACRECTYP)  TEST/SET VRECTYP                             
         BZ    LDCP010             NOT RESOLVED                                 
         A     RF,RELO                                                          
         STCM  RF,15,VRECTYP                                                    
*                                                                               
         GOTO1 VRECTYP,DMCB,(C'D',(R2))                                         
         MVC   RECTYPE,0(R1)       EXTRACT RECORD TYPE & COMPANY CODE           
         MVC   COMPANY,1(R1)                                                    
         MVC   CPTREC,RECTYPE                                                   
         MVC   CPTCPY,COMPANY                                                   
         B     LDCP012                                                          
                                                                                
LDCP010  CLI   CPTREC,0            USER SUPPLIES RECTYPE + CPYCODE              
         BH    *+6                                                              
         DC    H'0'                                                             
         MVC   RECTYPE,CPTREC                                                   
         CLI   CPTCPY,0                                                         
         BH    *+6                                                              
         DC    H'0'                                                             
         MVC   COMPANY,CPTCPY                                                   
*                                                                               
         USING ACCRECD,R3          SET PRIMARY POINTER                          
LDCP012  MVC   ACCKEY,ACCKEY-ACCRECD(R2)                                        
         MVC   ACCKSTA,ACCRSTA-ACCRECD(R2)                                      
*                                                                               
*&&UK                                                                           
         CLI   RECTYPE,ACRTTRNA    IS RECORD ARCHIVED TRANSACTION               
         BNE   LDCP014             NO - TREAT AS NORMAL                         
         USING TRNRECD,R3                                                       
         CLI   ACLDPLDT,X'01'      HAS A PEEL DATE OR NULL BEEN SAVED           
         BE    LDCP014             NO - MUST HAVE COME FROM A97                 
         OC    ACLDPLDT,ACLDPLDT   WAS IT A PEEL DATE                           
         BZ    LDCP014             NO - LEAVE DIR STATUS ALONE                  
         OI    TRNKSTA2,TRNSPEEL   SET STATUS AS PEELED                         
         MVC   TRNKSUSE,ACLDPLDT   AND MOVE DATE INTO DIR STATUS AREA           
         MVI   ACLDPLDT,X'01'      SET NO DATE SAVED                            
         USING ACCRECD,R3                                                       
*&&                                                                             
                                                                                
LDCP014  XC    ACCKDA,ACCKDA                                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL           SET END OF POINTER LIST                      
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GENERATE INTERAGENCY PASSIVE POINTERS                               *         
***********************************************************************         
         SPACE 1                                                                
*&&US                                                                           
IDJREC   CLI   RECTYPE,ACRTINT     TEST FOR INTERAGENCY ESTIMATE                
         BNE   IDJRECX                                                          
         USING INTRECD,R2                                                       
*        OC    CUTOFF,CUTOFF       TEST CUTOFF INITIALIZED                      
*        BNZ   IDJREC02                                                         
*                                  CALCULATE TODAY MINUS SEVEN DAYS             
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)                                     
         GOTO1 VADDAY,(R1),TODAY,DUB,F'-7'                                      
         GOTO1 VDATCON,(R1),(0,DUB),(2,CUTOFF)                                  
*                                                                               
IDJREC02 LA    R6,INTRFST          R6=A(ESTIMATE ELEMENT)                       
         XC    NDATES,NDATES                                                    
         USING IESELD,R6                                                        
*                                                                               
IDJREC04 CLI   IESEL,0             TEST FOR EOR                                 
         BE    LDCPX                                                            
         CLI   IESEL,IESELQ        TEST FOR ESTIMATE ELEMENT                    
         BNE   IDJREC08                                                         
         OC    IESDAT,IESDAT       TEST POSTING DATE PRESENT                    
         BZ    IDJREC08                                                         
         TM    IESSTAT,IESSPOST    TEST IF MONTH POSTED                         
         BO    IDJREC08                                                         
*                                                                               
IDJREC06 BRAS  RE,TSTDATE          TEST ALREADY HAVE POINTER FOR DATE           
         BNE   IDJREC08                                                         
*                                                                               
         USING IDJRECD,R3                                                       
         MVC   IDJKEY,SPACES                                                    
         MVI   IDJKTYP,IDJKTYPQ    BUILD INTERAGENCY DAILY JOURNAL              
         MVI   IDJKSUB,IDJKSUBQ    PASSIVE POINTER                              
         GOTO1 VDATCON,DMCB,(2,IESDAT),(1,IDJKDATE)                             
         MVC   IDJKCULA,INTKCULA                                                
         MVC   IDJKCLT,INTKCLT                                                  
         MVC   IDJKPRD,INTKPRD                                                  
         MVC   IDJKMED,INTKMED                                                  
         MVC   IDJKEST,INTKEST                                                  
         XC    IDJKSTA,IDJKSTA     CLEAR STATUS AREA                            
         MVC   IDJKSTAT,INTRSTA    COPY FIRST STATUS BYTE FROM EST              
         XC    IDJKDA,IDJKDA                                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
IDJREC08 SR    R0,R0                                                            
         IC    R0,IESLN                                                         
         AR    R6,R0                                                            
         B     IDJREC04                                                         
         DROP  R2,R3,R6                                                         
*&&                                                                             
IDJRECX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* GENERATE ARTICLE PASSIVE POINTERS                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING ARTRECD,R2                                                       
ARTREC   CLI   RECTYPE,ACRTART                                                  
         BNE   ARTRECX                                                          
         LA    R6,ARTRFST                                                       
         XR    R0,R0                                                            
         B     ARTREC04                                                         
         USING AFDELD,R6                                                        
ARTREC02 IC    R0,AFDLN            LOCATE ART FILTER DET ELEMENT                
         AR    R6,R0                                                            
ARTREC04 CLI   AFDEL,0             TEST E-O-R                                   
         BE    ARTRECX                                                          
         CLI   AFDEL,AFDELQ        TEST GIN ELEMENT                             
         BNE   ARTREC02                                                         
*                                                                               
         USING PASRECD,R3          BUILD KEY OF PASSIVE POINTER                 
         XC    PASKEY,PASKEY                                                    
         MVI   PASKTYP,PASKTYPQ                                                 
         MVI   PASKSUB,PASKSQ                                                   
         MVC   PASKCPY,ARTKCPY                                                  
         MVC   PASKSEQ,AFDSEQ                                                   
         MVC   PASKSTA,ARTRSTA     SET STATUS                                   
         XC    PASKDA,PASKDA       SET DISK ADDRESS                             
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY                             
*                                                                               
         USING PAFRECD,R3          BUILD KEY OF PASSIVE POINTER                 
         XC    PAFKEY,PAFKEY                                                    
         MVI   PAFKTYP,PAFKTYPQ                                                 
         MVI   PAFKSUB,PAFKSQ                                                   
         MVC   PAFKCPY,ARTKCPY                                                  
         MVC   PAFKART,ARTKART                                                  
         MVC   PAFKSUP,ARTKSUP                                                  
         MVC   PAFKCLI,ARTKCLI                                                  
         MVC   PAFKOFF,ARTKOFF                                                  
         MVC   PAFKSTA,ARTRSTA     SET STATUS                                   
         XC    PAFKDA,PAFKDA       SET DISK ADDRESS                             
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
ARTRECX  DS    0H                                                               
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* COMPANY RECORD - EXTRACT PASSIVE POINTER RULES                      *         
***********************************************************************         
         SPACE 1                                                                
         USING CPYRECD,R2                                                       
CPYREC   CLI   RECTYPE,ACRTCPY     TEST COMPANY RECORD                          
         BNE   CPYRECX                                                          
         XC    CPYSTAC,CPYSTAC      INIT STATUS BYTE 12                         
         XC    CPYVALS(CPYVALSL),CPYVALS                                        
         XC    GLHIMOS,GLHIMOS                                                  
         XC    GLLOMOS,GLLOMOS                                                  
         GOTO1 VLDLANG,DMCB,CPYRECD                                             
         MVC   CPYLANG,0(R1)                                                    
         MVC   CPYCTRY,1(R1)                                                    
         LA    R6,CPYRFST                                                       
         SR    R0,R0                                                            
         USING CPYELD,R6                                                        
CPYREC02 CLI   CPYEL,0             LOCATE COMPANY ELEMENT                       
         BE    CPYREC04                                                         
         CLI   CPYEL,CPYELQ                                                     
         BE    *+14                                                             
         IC    R0,CPYLN                                                         
         AR    R6,R0                                                            
         B     CPYREC02                                                         
         CLI   CPYLN,CPYSTAT1-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA1,CPYSTAT1    SAVE COMPANY STATUS BYTE 1                   
         CLI   CPYLN,CPYSTAT2-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA2,CPYSTAT2    SAVE COMPANY STATUS BYTE 2                   
         CLI   CPYLN,CPYSTAT3-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA3,CPYSTAT3    SAVE COMPANY STATUS BYTE 3                   
         CLI   CPYLN,CPYSTAT4-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA4,CPYSTAT4    SAVE COMPANY STATUS BYTE 4                   
         CLI   CPYLN,CPYSTAT5-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA5,CPYSTAT5    SAVE COMPANY STATUS BYTE 5                   
         CLI   CPYLN,CPYSTAT6-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA6,CPYSTAT6    SAVE COMPANY STATUS BYTE 6                   
         CLI   CPYLN,CPYSTAT7-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA7,CPYSTAT7    SAVE COMPANY STATUS BYTE 7                   
         CLI   CPYLN,CPYSTAT9-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA9,CPYSTAT9    SAVE COMPANY STATUS BYTE 9                   
         CLI   CPYLN,CPYLN4Q                                                    
         BL    *+10                                                             
         MVC   CPYSTAC,CPYSTATC    SAVE COMPANY STATUS BYTE 12                  
CPYREC04 B     LDCPX                                                            
*                                                                               
CPYRECX  DS    0H                                                               
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* NEW BATCH PASSIVES                                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING TBAKEY,R2                                                        
TBAREC   CLI   RECTYPE,ACRTNBT     TEST NEW BATCH RECORD                        
         BNE   TBARECX                                                          
         OC    TBAKTSEQ,TBAKTSEQ   TEST HEADER                                  
         BNZ   TBARECX                                                          
         DROP  R2                                                               
         USING TBAKEY,R3           CREATE PASSIVE POINTER                       
         MVC   TBAPAS,TBAKEY-TBARECD(R2)                                        
         MVI   TBAPTYP,TBAPTYPQ                                                 
         MVC   TBAPEFDT,TBAHREDT-TBARECD(R2)                                    
         MVC   TBAPTSEQ,TBAHRADT-TBARECD(R2)  SET ADDED DATE HERE               
         MVC   TBAKSTA,TBARSTA-TBARECD(R2)                                      
         XC    TBAKDA,TBAKDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
*                                                                               
TBARECX  DS    0H                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* OFFICE/ACCOUNT PASSIVES                                             *         
***********************************************************************         
         SPACE 1                                                                
         USING OFAKEY,R2                                                        
OFAREC   CLI   RECTYPE,ACRTOFA     TEST FOR ACCOUNT RECORD(S)                   
         BNE   OFARECX                                                          
*                                                                               
         USING OAPKEY,R3                                                        
         XC    OAPKEY,OAPKEY       BUILD OFFICE/ACCOUNT PASSIVE POINTER         
         MVI   OAPKTYP,OAPKTYPQ                                                 
         MVC   OAPKCUL,OFAKCPY                                                  
         MVC   OAPKOFF,OFAKOFF                                                  
         MVC   OAPKACT,OFAKACT                                                  
         MVC   OAPKSTA,OFARSTA                                                  
         XC    OAPKDA,OAPKDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
*                                                                               
OFARECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ORDER # ALLOCATION (PASSIVES)                                       *         
***********************************************************************         
         SPACE 1                                                                
         USING ONARECD,R2                                                       
         USING ONAPASD,R3                                                       
ONAREC   CLI   RECTYPE,ACRTONAR    TEST FOR OFFICE # ALLOCATION REC'S           
         BNE   ONARECX                                                          
         LA    R1,ONARFST                                                       
         USING ONCELD,R1                                                        
         XR    R0,R0                                                            
ONAREC2  CLI   ONCEL,0                                                          
         BE    LDCPX                                                            
         CLI   ONCEL,ONCELQ                                                     
         BE    ONAREC4                                                          
         IC    R0,ONCLN                                                         
         AR    R1,R0                                                            
         B     ONAREC2                                                          
ONAREC4  DS    0H                                                               
         XC    ONAPKEY,ONAPKEY     BUILD PASSIVE                                
         MVI   ONAPTYP,ONAPTYPQ                                                 
         MVI   ONAPSUB,ONAPSUBQ                                                 
         MVC   ONAPCPY,ONAKCPY                                                  
         MVC   ONAPEND#,ONCEND#                                                 
         MVC   ONAPSTA#,ONCSTA#                                                 
         XC    ONAPSTA,ONAPSTA                                                  
         MVC   ONAPSTAT,ONARSTAT                                                
         MVC   ONAPSAPP,ONAKAPP                                                 
         MVC   ONAPSCLS,ONAKCLS                                                 
         MVC   ONAPSOFF,ONAKOFF                                                 
         XC    ONAPDA,ONAPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
*                                                                               
ONARECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ORDER REQUISITION # AND SJ PASSIVES                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING ORDRECD,R2                                                       
ORNREC   CLI   RECTYPE,ACRTORD     TEST FOR ORDER RECORD(S)                     
         BNE   ORNRECX                                                          
         CLI   ORDKSEQ,0           ONLY REAL ORDERS                             
         BNE   LDCPX                                                            
*                                                                               
         LA    R6,ORDRFST                                                       
         XR    R0,R0                                                            
         USING FFTELD,R6                                                        
ORNREC02 CLI   FFTEL,0             LOCATE FFT/REQ. NUMBER ELEMENT               
         BE    ORNREC20                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   ORNREC04                                                         
         CLI   FFTTYPE,FFTTORNO                                                 
         BE    ORNREC06                                                         
ORNREC04 IC    R0,FFTLN                                                         
         AR    R6,R0                                                            
         B     ORNREC02                                                         
*                                                                               
         USING ORNPASD,R3                                                       
ORNREC06 XC    ORNPKEY,ORNPKEY     BUILD ORDER REQ. # PASSIVE POINTER           
         MVI   ORNPTYP,ORNPTYPQ                                                 
         MVI   ORNPSUB,ORNPSUBQ                                                 
         MVC   ORNPCPY,ORDKCPY                                                  
         MVC   ORNPRNUM,FFTDATA                                                 
         MVC   ORNPSTA,ORDRSTA                                                  
         XC    ORNPDA,ORNPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
ORNREC20 XC    WORK,WORK                                                        
         LA    R6,ORDRFST                                                       
*        TM    ORDRSTAT,ORDSLDEL+ORDSDEL                                        
*        BNZ   ORNRECX                                                          
*        TM    ORDRSTA2,ORDSOREJ                                                
*        BNZ   ORNRECX                                                          
*        TM    ORDRSTA2,ORDSEXEX   BRANDOCEAN ONLY (IF FOR ALL CHECK            
*        BZ    ORNRECX             WITH OPS FOR FILE SIZES)                     
         XR    R0,R0                                                            
         USING ORDELD,R6                                                        
ORNREC22 CLI   ORDEL,0             LOCATE ORDER AND SOURCE ELEMENTS             
         BE    ORNREC30                                                         
         CLI   ORDEL,ORDELQ                                                     
         BE    ORNREC26                                                         
         CLI   ORDEL,SORELQ                                                     
         BE    ORNREC28                                                         
         CLI   ORDEL,GDAELQ                                                     
         BE    ORNREC29                                                         
ORNREC24 IC    R0,ORDLN                                                         
         AR    R6,R0                                                            
         B     ORNREC22                                                         
ORNREC26 CLI   ORDLN,ORDLN2Q       ELEMENT MUST BE LONG ENOUGH                  
         JL    *+10                                                             
         MVC   WORK+36(L'ORDGSTAT),ORDGSTAT STORE GAP STATUS                    
         CLC   ORDACCU(2),=C'SJ'                                                
         BNE   ORNREC27                                                         
         MVI   WORK,0                                                           
         MVC   WORK+1(12),ORDACCA                                               
         MVC   WORK+20(L'ORDSUP-L'ORDSUPC),ORDSUP+L'ORDSUPC                     
         B     ORNREC24                                                         
*                                                                               
ORNREC27 MVC   WORK+20(L'ORDSUP-L'ORDSUPC),ORDSUP+L'ORDSUPC                     
         B     ORNREC24                                                         
*                                                                               
         USING SORELD,R6                                                        
ORNREC28 CLC   SORAULA(2),=C'SJ'                                                
         BNE   ORNREC24                                                         
         MVI   WORK,X'FF'                                                       
         MVC   WORK+1(12),SORAACT                                               
         B     ORNREC24                                                         
*                                                                               
         USING GDAELD,R6                                                        
ORNREC29 CLI   GDATYPE,GDAGAPEX    GAP EXPIRY DATE?                             
         BNE   *+10                                                             
         MVC   WORK+37(L'GDADATE),GDADATE                                       
         CLI   GDATYPE,GDAGAPST    GAP SENT DATE?                               
         BNE   *+10                                                             
         MVC   WORK+40(L'GDADATE),GDADATE                                       
         B     ORNREC24                                                         
*                                                                               
ORNREC30 OC    WORK(13),WORK       ANY SJ ACCOUNT?                              
         BZ    ORNREC32                                                         
*                                                                               
         USING OSJPASD,R3                                                       
         XC    OSJPAS,OSJPAS       BUILD ORDER SJ PASSIVE                       
         MVI   OSJPTYP,OSJPTYPQ                                                 
         MVI   OSJPSUB,OSJPSUBQ                                                 
         MVC   OSJPCPY,ORDKCPY                                                  
         MVC   OSJPACT,WORK+1                                                   
         MVC   OSJPMEM,WORK                                                     
         MVC   OSJPORD,ORDKORD                                                  
         MVC   OSJPSTA,ORDRSTA                                                  
         TM    ORDRSTA2,ORDSEXEX   IF NON BRANDOCEAN                            
         BNZ   *+8                 SET ORDER AS FULLY APPROVED                  
         OI    OSJPSTA2,ORDSAPPR                                                
         XC    OSJPDA,OSJPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
         USING OSUPASD,R3          BUILD ORDER SUPPLIER PASSIVE                 
ORNREC32 CLC   WORK+22(L'ORDSUPA),SPACES                                        
         BNH   ORNREC34                                                         
*                                                                               
         XC    OSUPAS,OSUPAS                                                    
         MVI   OSUPTYP,OSUPTYPQ                                                 
         MVI   OSUPSUB,OSUPSUBQ                                                 
         MVC   OSUPCPY,ORDKCPY                                                  
         MVC   OSUSULA,WORK+20                                                  
         MVC   OSUPMEM,WORK                                                     
         MVC   OSUPORD,ORDKORD                                                  
         MVC   OSUPSTA,ORDRSTA                                                  
         MVC   OSUPSTA2,ORDRSTA2                                                
         TM    ORDRSTA2,ORDSEXEX   IF NON BRANDOCEAN                            
         BNZ   *+12                SET ORDER AS FULLY APPROVED                  
         NI    OSUPSTA2,X'FF'-ORDSDRFT+ORDSPAPP+ORDSOREJ+ORDSSUBM               
         OI    OSUPSTA2,ORDSAPPR                                                
         MVC   OSUPOFF,ORDROFF                                                  
         MVC   OSUPEXTY,ORDREXTY                                                
         XC    OSUPDA,OSUPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
         USING OGAPASD,R3                                                       
ORNREC34 OC    WORK+36(L'ORDGSTAT),WORK+36  ANY GAP STATUS?                     
         BZ    ORNREC40                                                         
         CLC   WORK+22(L'ORDSUPA),SPACES   ANY SUPPLIER?                        
         BNH   ORNREC40                                                         
         TM    ORDRSTA2,ORDSEXEX   ONLY FOR BO ORDERS                           
         BZ    ORNREC40                                                         
         CLI   WORK+36,ORDGNSNT    IF NOT SENT                                  
         BNE   *+12                                                             
         TM    ORDRSTA2,ORDSAPPR   ONLY FOR APPROVED ORDERS                     
         BZ    ORNRECX                                                          
*                                                                               
         XC    OGAPAS,OGAPAS       BUILD GAP PASSIVE                            
         MVI   OGAPTYP,OGAPTYPQ                                                 
         MVI   OGAPSUB,OGAPSUBQ                                                 
         MVC   OGAPCPY,ORDKCPY                                                  
         MVC   OGAPGAP,WORK+36     GAP STATUS                                   
         ICM   RF,7,WORK+37                                                     
         LNR   RF,RF                                                            
         STCM  RF,7,OGAPEXD                                                     
         MVC   OGAPSND,WORK+40                                                  
         MVC   OGAPORD,ORDKORD                                                  
         MVC   OGAPSUP,WORK+20                                                  
         OC    OGAPSUP,SPACES                                                   
         MVC   OGAPSTA,ORDRSTA                                                  
         MVC   OGAPSTA2,ORDRSTA2                                                
         TM    ORDRSTA2,ORDSEXEX   IF NON BRANDOCEAN                            
         BNZ   *+12                SET ORDER AS FULLY APPROVED                  
         NI    OGAPSTA2,X'FF'-ORDSDRFT+ORDSPAPP+ORDSOREJ+ORDSSUBM               
         OI    OGAPSTA2,ORDSAPPR                                                
         MVC   OGAPOFF,ORDROFF                                                  
         MVC   OGAPEXTY,ORDREXTY                                                
         XC    OGAPDA,OGAPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
ORNREC40 TM    ORDRSTA2,ORDSEXEX   ORDER STATUS PASIVES                         
         JZ    ORNRECX             BRANDOCEAN/AURA ORDERS ONLY                  
         TM    ORDRSTAT,ORDSFMCH+ORDCLOSE+ORDSLDEL                              
         JNZ   ORNRECX             SKIP FULY MATCHED/CLOSED/DELETED             
         TM    ORDRSTA2,ORDSDRFT+ORDSPAPP+ORDSOREJ+ORDSSUBM                     
         JZ    ORNRECX             SKIP IF NOT ANY OF TEHSE STATUS'             
*                                                                               
         USING OSTPASD,R3                                                       
         XC    OSTPAS,OSTPAS       BUILD PASSIVE                                
         MVI   OSTPTYP,OSTPTYPQ                                                 
         MVI   OSTPSUB,OSTPSUBQ                                                 
         MVC   OSTPCPY,ORDKCPY                                                  
         MVC   OSTPOST,ORDRSTA2                                                 
         NI    OSTPOST,ORDSDRFT+ORDSPAPP+ORDSOREJ+ORDSSUBM                      
         MVC   OSTPORD,ORDKORD                                                  
         MVC   OSTPSTA,ORDRSTA                                                  
         MVC   OSTPSTA2,ORDRSTA2                                                
         MVC   OSTPOFF,ORDROFF                                                  
         MVC   OSTPEXTY,ORDREXTY                                                
         XC    OSTPDA,OSTPDA                                                    
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
ORNRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* REB/INV PASSIVES                                                    *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
         USING REBRECD,R2                                                       
         USING REBPASD,R3                                                       
REBREC   CLI   RECTYPE,ACRTREBR    TEST FOR REB/INV RECORD                      
         BNE   REBRECX                                                          
         OC    REBKNUM,REBKNUM     SUPPRESS CONTROL                             
         BZ    LDCPX                                                            
*                                                                               
         LA    R6,REBRFST                                                       
         USING RBDELD,R6                                                        
REBREC02 CLI   RBDEL,0             LOCATE FFT/REQ. NUMBER ELEMENT               
         BE    REBRECX                                                          
         CLI   RBDEL,RBDELQ                                                     
         BE    REBREC06                                                         
REBREC04 LLC   R1,RBDLN                                                         
         AR    R6,R1                                                            
         B     REBREC02                                                         
                                                                                
REBREC06 CLI   RBDTYP,RBDTHDR                                                   
         BNE   REBREC08                                                         
         XC    REBPAS,REBPAS                                                    
         MVI   REBPTYP,REBPTYPQ                                                 
         MVI   REBPSUB,REBPSUBQ                                                 
         MVC   REBPCPY,REBKCPY                                                  
         MVI   REBPMOD,REBPMREF                                                 
         MVC   REBPNUM,REBKNUM                                                  
         MVC   REBPREF,RBDREF                                                   
         MVC   REBPSTA,REBRSTA                                                  
         XC    REBPDA,REBPDA                                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         XC    REBPAS,REBPAS                                                    
         MVI   REBPTYP,REBPTYPQ                                                 
         MVI   REBPSUB,REBPSUBQ                                                 
         MVC   REBPCPY,REBKCPY                                                  
         MVI   REBPMOD,REBPMSUP                                                 
         MVC   REBPNUM,REBKNUM                                                  
         MVC   REBPSUP,RBDSUPP                                                  
         MVC   REBPSTA,REBRSTA                                                  
         XC    REBPDA,REBPDA                                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         B     REBREC04                                                         
*                                                                               
REBREC08 CLI   RBDTYP,RBDTAPP                                                   
         BNE   REBREC04                                                         
                                                                                
         LA    RF,RBDAPTRY                                                      
EN       USING RBDAPTRY,RF                                                      
         USING RSTPASD,R3                                                       
         LLC   R4,RBDAPNUM                                                      
REBREC10 XC    RSTPAS,RSTPAS                                                    
         MVI   RSTPTYP,RSTPTYPQ                                                 
         MVI   RSTPSUB,RSTPSUBQ                                                 
         MVC   RSTPCPY,REBKCPY                                                  
         TM    REBRSTA2,REBSBREG                                                
         JZ    REBREC11                                                         
         MVI   RSTPIST,RSTSREG                                                  
         B     REBREC16                                                         
                                                                                
REBREC11 TM    REBRSTA2,REBSBLDE                                                
         JZ    REBREC12                                                         
         MVI   RSTPIST,RSTSDELT                                                 
         B     REBREC16                                                         
                                                                                
REBREC12 TM    REBRSTA2,REBSBREJ                                                
         JZ    REBREC14                                                         
         MVI   RSTPIST,RSTSREJT                                                 
         B     REBREC16                                                         
                                                                                
REBREC14 MVI   RSTPIST,RSTSAWAT                                                 
         TM    EN.RBDAPSTA,RBDAAPP                                              
         BZ    REBREC16                                                         
         MVI   RSTPIST,RSTSAPRD                                                 
         B     REBREC16                                                         
                                                                                
REBREC16 MVC   RSTPDAT,REBRSDTA                                                 
         MVC   RSTPPID,EN.RBDAPPID                                              
         XC    RSTPDAT,=XL2'FFFF'                                               
         MVC   RSTPNUM,REBKNUM                                                  
         MVC   RSTPSEQ,REBKSEQ                                                  
         MVC   RSTPSTA,REBRSTA                                                  
         XC    RSTPDA,RSTPDA                                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         LA    RF,RBDAPLNQ(RF)                                                  
         BCT   R4,REBREC10                                                      
         B     REBREC04                                                         
*                                                                               
REBRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*&&                                                                             
***********************************************************************         
* CREATE TRANSACTION SERIAL PASSIVE POINTER                           *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2          BUILD SER PASSIVE POINTER                    
TRSREC   CLI   RECTYPE,ACRTTRN                                                  
         BE    *+12                                                             
         CLI   RECTYPE,ACRTTRNA                                                 
         BNE   TRSRECX                                                          
         TM    CPYSTA9,CPYSSRNM    IS COMPANY USING SERIAL PASSIVES             
         BZ    TRSRECX             NO, THEN DON'T BOTHER BUILDING THEM          
         LA    R6,TRNRFST                                                       
         SR    R0,R0                                                            
         USING SERELD,R6                                                        
TRSREC02 IC    R0,SERLN            LOCATE MRH ELEMENT                           
         AR    R6,R0                                                            
         CLI   SEREL,0             TEST E-O-R                                   
         BE    RNSREC                                                           
         CLI   SEREL,SERELQ        TEST MRH ELEMENT                             
         BNE   TRSREC02                                                         
*                                                                               
         USING TRSPASD,R3          BUILD KEY OF PASSIVE POINTER                 
         XC    TRSPKEY,TRSPKEY                                                  
         MVI   TRSPTYP,TRSPTYPQ                                                 
         MVI   TRSPSUB,TRSPSUBQ                                                 
         MVC   TRSPCPY,TRNKCPY                                                  
         SR    RF,RF                                                            
         ICM   RF,15,SERNM         GET TX SERIAL NUMBER                         
         LNR   RF,RF                                                            
         STCM  RF,15,TRSPSER                                                    
*                                                                               
         MVC   TRSPSTA,TRNRSTA     SET STATUS                                   
         XC    TRSPDA,TRSPDA       CLEAR DISK ADDRESS                           
TRSREC06 LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
TRSRECX  DS    0H                                                               
         DROP  R3,R6                                                            
***********************************************************************         
* REFERENCE NUMBER SEARCH PASSIVES                                    *         
***********************************************************************         
*                                                                               
         SPACE 1                                                                
         USING TRNRECD,R2          BUILD SER PASSIVE POINTER                    
RNSREC   CLI   RECTYPE,ACRTTRN                                                  
         BE    *+12                                                             
         CLI   RECTYPE,ACRTTRNA                                                 
         BNE   RNSRECX                                                          
         CLI   TRNKUNT,C'S'        ONLY FOR UNIT S                              
         BNE   RNSRECX                                                          
*                                                                               
RNSREC02 MVC   CURINT,SPACES       PRESET FOR INTERNAL                          
         MVC   CURORD,SPACES       PRESET FOR ORDER                             
         MVC   CURREF,TRNKREF      PRESET FOR REAL REFERENCE NUMBER             
*&&DO                              CALCULATE CUT OFF DATE                       
         MVC   TODAY,=X'FAF6F0F7F0F1'                                           
         CLI   ISITFQA,1                                                        
         BE    RNSREC03                                                         
*&&                                CALCULATE CUT OFF DATE                       
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)                                     
RNSREC03 GOTO1 VADDAY,DMCB,(C'Y',TODAY),DUB,F'-7'                               
         GOTO1 VDATCON,DMCB,(0,DUB),(2,CUTOFF)                                  
*                                                                               
         MVI   FLAG,C'N'                                                        
         USING TRSELD,R6                                                        
         LA    R6,TRNRFST                                                       
RNSREC04 CLI   TRSEL,TRNELQ                                                     
         BNE   *+10                                                             
         MVC   OFFICE,TRNOFFC-TRNELD(R6)  SAVE OFFICE CODE                      
         LLC   R0,TRSLN                                                         
         AR    R6,R0                                                            
         CLI   TRSEL,0                                                          
         BE    RNSREC06            EOR                                          
         CLI   TRSEL,TRSELQ                                                     
         BNE   RNSREC04                                                         
         CLC   TRSDATE,CUTOFF                                                   
         BL    RNSRECX                                                          
         TM    TRSSTAT5,TRSSINVQ                                                
         BZ    *+8                                                              
         MVI   FLAG,C'Y'           SET BRANDOCEAN INVOICES POSTING              
         B     *+12                                                             
RNSREC06 CLI   TRNRSTYP,12         TEST UNMATCHED ORDER TRANS                   
         BNE   RNSRECX             NO SKIP                                      
*                                  ELSE IGNORE LACK OF A TRSEL                  
         USING FFTELD,R6                                                        
         LA    R6,TRNRFST                                                       
RNSREC10 IC    R0,FFTLN                                                         
         AR    R6,R0                                                            
         CLI   FFTEL,0                                                          
         BE    RNSREC22                                                         
         CLI   FLAG,C'Y'           TEST BRANDOCEAN INVOICES POSTING             
         BNE   RNSREC14                                                         
         CLI   FFTEL,GPXELQ                                                     
         BNE   RNSREC14                                                         
         CLI   GPXTYP-GPXELD(R6),GPXNVLQ                                        
         BNE   RNSREC10                                                         
         MVC   CURINT,GPXDATA-GPXELD(R6)  EXTRACT INVOICE NUMBER                
         B     RNSREC10                                                         
RNSREC14 CLI   FFTEL,FFTELQ                                                     
         BNE   RNSREC16                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         BNE   RNSREC10                                                         
         MVC   CURREF,FFTDATA                                                   
         B     RNSREC10                                                         
         USING FFNELD,R6                                                        
RNSREC16 CLI   FFNEL,FFNELQ                                                     
         BNE   RNSREC10                                                         
         CLC   FFNONUM,AC@NOORD                                                 
         BE    RNSREC10                                                         
         MVC   CURORD,FFNONUM                                                   
         B     RNSREC10                                                         
*                                                                               
         USING RNSPASD,R3          BUILD KEY OF PASSIVE POINTER                 
RNSREC22 XC    RNSPKEY,RNSPKEY                                                  
         MVI   RNSPTYP,RNSPTYPQ                                                 
         MVI   RNSPSUB,RNSPSUBQ                                                 
         MVC   RNSPCPY,TRNKCPY                                                  
         MVC   RNSPBTY,TRNRSTYP                                                 
         CLC   =C'**',OFFICE       IF SJ AND TYPE 12 REFERENCE WILL             
         JNE   *+8                 HAVE ORDER NO.                               
         CLI   TRNRSTYP,12                                                      
         JNE   *+10                                                             
         MVC   CURORD,TRNKREF                                                   
         CLC   CURREF,SPACES       TEST REF SET                                 
         BNH   RNSREC24                                                         
         MVI   RNSPIND,RNSPRFQ     BUILD REFERENCE NUMBER PASSIVE               
         MVC   RNSPREF,CURREF                                                   
         MVC   CURREF,SPACES                                                    
         B     RNSREC30                                                         
RNSREC24 CLC   CURINT,SPACES       TEST INVLOG SET                              
         BNH   RNSREC26                                                         
         MVI   RNSPIND,RNSPINQ     BUILD INVOICE LOG TRANS PASSIVE              
         MVC   RNSPINT,CURINT                                                   
         MVC   CURINT,SPACES                                                    
         B     RNSREC30                                                         
RNSREC26 CLC   CURORD,SPACES       TEST ORDER SET                               
         BNH   RNSREC34                                                         
         MVI   RNSPIND,RNSPORQ     BUILD ORDER TRANS PASSIVE                    
         MVC   RNSPREF,CURORD                                                   
         MVC   CURORD,SPACES                                                    
RNSREC30 MVI   RNSPFIL,RNSPARQ                                                  
         TM    TRNRSTAT,TRNSARCH                                                
         BNZ   RNSREC32                                                         
         MVI   RNSPFIL,RNSPMSQ                                                  
RNSREC32 MVC   RNSPDA,DA                                                        
         MVC   RNSPSTA,TRNRSTA     SET STATUS                                   
         XC    RNSPKDA,RNSPKDA     CLEAR DISK ADDRESS                           
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         CLC   CURINT,SPACES       TEST INVLOG SET                              
         BH    RNSREC22            ADD ANOTHER PASSIVE                          
         CLC   CURORD,SPACES       TEST ORDER SET                               
         BH    RNSREC22            ADD ANOTHER PASSIVE                          
*                                                                               
RNSREC34 MVI   0(R3),EOL           MAKE SURE IT SETS E-O-L                      
*                                                                               
RNSRECX  DS    0H                                                               
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* GENERATE GROUP INVOICE PASSIVE POINTERS                             *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
GINREC   LA    R6,TRNRFST                                                       
         SR    R0,R0                                                            
         USING GINELD,R6                                                        
GINREC02 IC    R0,GINLN            LOCATE GIN ELEMENT                           
         AR    R6,R0                                                            
         CLI   GINEL,0             TEST E-O-R                                   
         BE    GINRECX                                                          
         CLI   GINEL,GINELQ        TEST GIN ELEMENT                             
         BNE   GINREC02                                                         
*                                                                               
         USING GINPASD,R3          BUILD KEY OF PASSIVE POINTER                 
         XC    GINPKEY,GINPKEY                                                  
         MVI   GINPTYP,GINPTYPQ                                                 
         MVC   GINPCPY,TRNKCPY                                                  
         MVC   GINPINV,GININV                                                   
         CLI   GINLN,GINLN2Q                                                    
         BL    *+16                                                             
         MVC   GINPISN,GINISN                                                   
         MVC   GINPPTYP,GINTYP                                                  
         MVC   GINPWORK,TRNRFST+(TRNANAL-TRNELD)                                
         MVC   GINPULA,TRNKUNT                                                  
         MVC   GINPDA,DA                                                        
         MVC   GINPKSTA,TRNRSTA    SET STATUS                                   
         XC    GINPKDA,GINPKDA     CLEAR DISK ADDRESS                           
*                                                                               
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
GINRECX  DS    0H                                                               
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* TRANSACTION RECORD - INVOICE NUMBER PASSIVES                        *         
***********************************************************************         
         SPACE 1                                                                
INVREC   LA    R6,TRNRFST                                                       
         CLC   =C'SV',TRNKUNT                                                   
         BE    INVREC01                                                         
         CLC   =C'SX',TRNKUNT                                                   
         BE    INVREC01                                                         
         CLC   =C'SW',TRNKUNT                                                   
         BE    INVREC01                                                         
         CLC   =C'SY',TRNKUNT                                                   
         BNE   INVRECX                                                          
INVREC01 SR    R0,R0                                                            
         USING FFTELD,R6                                                        
INVREC02 IC    R0,FFTLN            FIRST LOOK FOR INVOICE# FFTEL                
         AR    R6,R0                                                            
         CLI   FFTEL,0                                                          
         BE    INVREC04                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   INVREC02                                                         
         CLI   FFTTYPE,FFTTINVN                                                 
         BNE   INVREC02                                                         
         MVC   WORK,SPACES                                                      
         SR    RE,RE                                                            
         IC    RE,FFTLN                                                         
         SHI   RE,FFTLN1Q+2       DATA LENGTH + 1 FOR EX MVC                    
         CHI   RE,L'INVPINV-1                                                   
         BNH   *+8                                                              
         LHI   RE,L'INVPINV-1                                                   
         MVC   WORK(0),FFTDATA                                                  
         EX    RE,*-6                                                           
         B     INVREC06                                                         
*                                                                               
INVREC04 MVC   WORK,SPACES                                                      
         MVC   WORK(L'TRNKREF),TRNKREF                                          
*                                                                               
         USING INVPASD,R3                                                       
INVREC06 XC    INVPKEY,INVPKEY                                                  
         MVI   INVPTYP,INVPTYPQ                                                 
         MVC   INVPCPY,TRNKCPY                                                  
         MVC   INVPLDG,TRNKLDG                                                  
         MVC   INVPACT,TRNKACT                                                  
         MVC   INVPINV,WORK                                                     
         MVC   INVPDAT,TRNKDATE                                                 
         MVC   INVPDA,DA                                                        
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
INVRECX  DS    0H                                                               
         DROP  R3,R6                                                            
*                                                                               
         EJECT                                                                  
***********************************************************************         
* TRANSACTION RECORD - MOS PASSIVE                                    *         
***********************************************************************         
         SPACE 1                                                                
MOSREC   LA    R6,TRNRFST                                                       
         USING TRNELD,R6                                                        
         CLC   =C'GB',TRNKUNT                                                   
         BE    *+14                                                             
         CLC   =C'GP',TRNKUNT                                                   
         BNE   MOSREC10                                                         
         CLI   TRNTYPE,25          TEST GENERAL LEDGER POSTING                  
         BNE   MOSRECX                                                          
         CLC   TRNRSMOS,GLHIMOS    TEST HIGH MOS                                
         BNH   MOSRECX                                                          
         MVC   GLHIMOS,TRNRSMOS    SAVE HIGH MOS                                
         B     MOSRECX                                                          
*                                                                               
MOSREC10 CLI   TRNKUNT,C'S'        TEST UNIT S                                  
         BL    MOSRECX                                                          
         BH    MOSREC70                                                         
         CLC   GLHIMOS,NULLS       TEST ANY G/L POSTINGS                        
         BE    MOSRECX                                                          
         CLC   GLLOMOS,NULLS       TEST LOW MOS ALREADY SET                     
         BNE   MOSREC20                                                         
         GOTO1 VDATCON,DMCB,(5,0),(0,WORK)     TODAY'S DATE                     
         GOTO1 VADDAY,(R1),(C'M',WORK),WORK+6,F'-3'                             
         GOTO1 VDATCON,(R1),(0,WORK+6),(1,WORK+15)                              
         MVC   GLLOMOS,WORK+15                                                  
*                                                                               
         USING MOSPASD,R3                                                       
MOSREC20 XC    MOSPKEY(ACCKLEN),MOSPKEY                                         
         MVI   MOSPTYP,MOSPTYPQ    BUILD KEY FOR PASSIVE POINTER                
         MVC   MOSPCPY,TRNKCPY                                                  
         MVC   MOSPMOS,TRNRSMOS                                                 
         MVC   MOSPULA,TRNKULA                                                  
         MVC   MOSPOFF,TRNKOFF                                                  
         MVC   MOSPCULC,TRNKCULC                                                
         MVC   MOSPDATE,TRNKDATE                                                
         MVC   MOSPDA,DA                                                        
         MVC   MOSPKST1,TRNRSTAT                                                
         MVC   MOSPKST2,TRNRSTA2                                                
         NI    FLAG,X'FF'-FLG60                                                 
*                                                                               
         LA    R5,TRNRFST                                                       
MOSREC30 CLI   0(R5),0                                                          
         BE    MOSREC80                                                         
         CLI   0(R5),TRSELQ        IS IT X'60' ELEMENT                          
         BE    MOSREC50                                                         
         CLI   0(R5),GLDELQ        IS IT X'63' ELEMENT                          
         BE    MOSREC60                                                         
MOSREC40 LLC   R0,1(R5)            GET LENGTH                                   
         AR    R5,R0                                                            
         B     MOSREC30                                                         
*                                                                               
         USING TRSELD,R5                                                        
MOSREC50 OI    FLAG,FLG60          FOUND X'60' ELEMENT                          
         CLC   TRNRSMOS,GLLOMOS    TEST TRANSACTION MOS                         
         BNL   MOSREC55            BUILD MOS POINTER                            
         TM    TRSSTAT,TRSSGLUP    IS IT UPDATED TO GL ?                        
         BNO   MOSREC85            NO EXIT                                      
         GOTO1 VDATCON,(R1),(2,TRSUPDT),(1,WORK)                                
         CLC   WORK(2),GLLOMOS                                                  
         BL    MOSREC85                                                         
*                                                                               
MOSREC55 OC    TRSUPDT,TRSUPDT     DO WE HAVE UPDATED TO GL DATE                
         BZ    MOSREC90                                                         
         TM    TRSSTAT,TRSSGLUP    UPDATED TO GL ?                              
         BNO   MOSREC90                                                         
         MVC   MOSPUPDT,TRSUPDT    UPDATED TO GL DATE TO PASSIVE                
         B     MOSREC40            GET X'63' TO FIND TIME ?                     
         DROP  R5                                                               
*                                                                               
         USING GLDELD,R5                                                        
MOSREC60 OC    MOSPUPDT,MOSPUPDT   DO WE HAVE UPDATED TO GL DATE.               
         BZ    MOSREC80            SOMETHING WRONG BUT NOT WORTH DYING          
*        GOTO1 VDATCON,(R1),(2,MOSPUPDT),(1,WORK)                               
         CLC   GLDDATE,WORK                                                     
         BNE   MOSREC40                                                         
         MVC   MOSPUPTM,GLDUPTM    TIME OF DAY                                  
         B     MOSREC90                                                         
         DROP  R5                                                               
*                                                                               
MOSREC70 CLC   GLLOMOS,NULLS       TEST LOW MOS ALREADY POSTED                  
         BE    MOSRECX                                                          
         XC    MOSPKEY(ACCKLEN),MOSPKEY                                         
         MVI   MOSPTYP,MOSPTYPQ    BUILD KEY FOR PASSIVE POINTER                
         MVC   MOSPCPY,TRNKCPY                                                  
         MVC   MOSPLMOS,GLLOMOS    SET LOWEST MONTH                             
         XC    GLLOMOS,GLLOMOS                                                  
         B     MOSREC90                                                         
*                                                                               
MOSREC80 TM    FLAG,FLG60                                                       
         BO    MOSREC90            FOUND 60 ELEMENT                             
         CLC   TRNRSMOS,GLLOMOS    MOS LOWER THEN LOWEST MOS ?                  
         BNL   MOSREC90            BUILD POINTER IF NOT LOW                     
MOSREC85 MVI   0(R3),EOL           DO NOT BUILD POINTER                         
         B     MOSRECX                                                          
*                                                                               
MOSREC90 XC    MOSPKDA,MOSPKDA                                                  
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
*                                                                               
MOSRECX  DS    0H                                                               
         DROP  R3,R6                                                            
*                                                                               
TRNRECX  DS    0H                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* LEDGER RECORD - EXTRACT SEARCH PASSIVE RULE AND ACCOUNT LENGTHS     *         
***********************************************************************         
         SPACE 1                                                                
         USING LDGRECD,R2                                                       
LDGREC   CLI   RECTYPE,ACRTLDG     TEST FOR LEDGER RECORD                       
         BNE   LDGRECX                                                          
         XC    LDGVALS(LDGVALSL),LDGVALS                                        
*                                                                               
         LA    R6,LDGRFST                                                       
         SR    R0,R0                                                            
         USING LDGELD,R6           R6=A(FIRST ELEMENT)                          
LDGREC02 CLI   LDGEL,0             TEST END OF RECORD                           
         BE    LDGRECX                                                          
*                                                                               
         CLI   LDGEL,LDGELQ        TEST LEDGER ELEMENT                          
         BNE   LDGREC04                                                         
         CLI   LDGLN,LDGLNQ                                                     
         BL    LDGREC06                                                         
         MVC   LDGSTA2,LDGSTAT2                                                 
         B     LDGREC06                                                         
*                                                                               
         USING ACLELD,R6                                                        
LDGREC04 CLI   ACLEL,ACLELQ        TEST HEIRARCHY ELEMENT                       
         BNE   LDGREC06                                                         
         MVC   LDGLVALN,ACLVALS                                                 
         MVC   LDGLVBLN,ACLVALS+L'ACLVALS                                       
         MVC   LDGLVCLN,ACLVALS+L'ACLVALS+L'ACLVALS                             
         MVC   LDGLVDLN,ACLVALS+L'ACLVALS+L'ACLVALS+L'ACLVALS                   
*                                                                               
LDGREC06 IC    R0,ACLLN                                                         
         AR    R6,R0                                                            
         B     LDGREC02                                                         
*                                                                               
LDGRECX  DS    0H                                                               
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* DRAFT ACCOUNT PASSIVE POINTERS                                      *         
* <<< FOR SJ JOBS ONLY >>>                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
DRAREC   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         BNE   DRARECX                                                          
         CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         BNE   DRARECX                                                          
         TM    ACTRSTAT,ACTSDRFT   DRAFT ACCOUNTS ONLY                          
         BZ    DRARECX                                                          
         XC    DUB,DUB                                                          
         XR    R0,R0                                                            
         USING RACELD,RE                                                        
         LA    RE,ACTRFST                                                       
*                                                                               
DRAREC2  CLI   RACEL,0                                                          
         BE    DRAREC6                                                          
         CLI   RACEL,RACELQ                                                     
         BNE   DRAREC4                                                          
         CLI   RACTYPE,RACTADD                                                  
         BNE   DRAREC4                                                          
         MVC   DUB(2),RACPERS                                                   
         B     DRAREC6                                                          
*                                                                               
DRAREC4  IC    R0,RACLN                                                         
         AR    RE,R0                                                            
         B     DRAREC2                                                          
         DROP  RE                                                               
*                                                                               
         USING DRAPASD,R3                                                       
DRAREC6  XC    DRAPASD(ACCKLEN),DRAPASD                                         
         MVI   DRAPTYP,DRAPTYPQ                                                 
         MVI   DRAPSUB,DRAPSUBQ                                                 
         MVC   DRAPCPY,ACTKCPY                                                  
         MVI   DRAPAPS,DRAPDRA     DRAFT INDICATOR                              
         MVC   DRAPULA,ACTKULA     ACCOUNT                                      
         MVC   DRAPPID,DUB         PERSON ADDED                                 
         XC    DRAPDA,DRAPDA                                                    
         MVC   DRAPSTA,ACTRSTA     COPY STATUS                                  
         MVC   DRAPSOFF,ACCOFFC    ASSUME JOB HAS NO OFFICE SET                 
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
DRARECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* JOB DATE PASSIVE POINTERS                                           *         
* <<< FOR SJ JOBS ONLY >>>                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
JDTREC   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         JNE   JDTRECX                                                          
         CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         JNE   JDTRECX                                                          
*        TM    ACTRSTAT,ACTSDRFT   SKIP DRAFTS                                  
*        JNZ   JDTRECX                                                          
         XC    DUB,DUB                                                          
         USING JOBELD,RE                                                        
         LA    RE,ACTRFST                                                       
*                                                                               
JDTREC1  CLI   JOBEL,0                                                          
         JE    JDTREC3                                                          
         CLI   JOBEL,JOBELQ                                                     
         JNE   JDTREC2                                                          
         LR    R0,RE                                                            
         GOTO1 VDATCON,DMCB,(1,JOBCDATE),(2,DUB+0)                              
         LR    RE,R0                                                            
         CLI   JOBLN,JOBLN1Q                                                    
         JNH   JDTREC3                                                          
         LR    R0,RE                                                            
         GOTO1 VDATCON,DMCB,(1,JOBODATE),(2,DUB+2)                              
         LR    RE,R0                                                            
         J     JDTREC3                                                          
*                                                                               
JDTREC2  LLC   R0,JOBLN                                                         
         AR    RE,R0                                                            
         J     JDTREC1                                                          
         DROP  RE                                                               
*                                                                               
         USING JDTPASD,R3                                                       
JDTREC3  OC    DUB+0(2),DUB+0      ANY CLOSE DATE?                              
         JZ    JDTREC4                                                          
         XC    JDTPASD(ACCKLEN),JDTPASD                                         
         MVI   JDTPTYP,JDTPTYPQ                                                 
         MVI   JDTPSUB,JDTPSUBQ                                                 
         MVC   JDTPCPY,ACTKCPY                                                  
         MVI   JDTPDTYP,JDTPDTCQ                                                
         MVC   JDTPDATE,DUB+0                                                   
         MVC   JDTPOFFC,ACCOFFC                                                 
         MVC   JDTPJACT,ACTKACT                                                 
         MVC   JDTPSTA,ACTRSTA                                                  
         XC    JDTPDA,JDTPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
JDTREC4  OC    DUB+2(2),DUB+2      ANY OPEN DATE?                               
         JZ    JDTRECX                                                          
         XC    JDTPASD(ACCKLEN),JDTPASD                                         
         MVI   JDTPTYP,JDTPTYPQ                                                 
         MVI   JDTPSUB,JDTPSUBQ                                                 
         MVC   JDTPCPY,ACTKCPY                                                  
         MVI   JDTPDTYP,JDTPDTOQ                                                
         MVC   JDTPDATE,DUB+2                                                   
         MVC   JDTPOFFC,ACCOFFC                                                 
         MVC   JDTPJACT,ACTKACT                                                 
         MVC   JDTPSTA,ACTRSTA                                                  
         XC    JDTPDA,JDTPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
JDTRECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* MASTER JOB PASSIVE POINTERS                                         *         
* <<< FOR SJ JOBS ONLY >>>                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
MJBREC   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         BNE   MJBRECX                                                          
         CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         BNE   MJBRECX                                                          
         XR    R0,R0                                                            
         USING SPAELD,RE                                                        
         LA    RE,ACTRFST                                                       
*                                                                               
MJBREC2  CLI   SPAEL,0                                                          
         BE    MJBRECX                                                          
         CLI   SPAEL,SPAELQ                                                     
         BNE   MJBREC4                                                          
         CLI   SPATYPE,SPATMJOB                                                 
         BE    MJBREC6                                                          
*                                                                               
MJBREC4  IC    R0,SPALN                                                         
         AR    RE,R0                                                            
         B     MJBREC2                                                          
*                                                                               
         USING MJBPASD,R3                                                       
MJBREC6  XC    MJBPASD(ACCKLEN),MJBPASD                                         
         MVI   MJBPTYP,MJBPTYPQ                                                 
         MVI   MJBPSUB,MJBPSUBQ                                                 
         MVC   MJBPCPY,ACTKCPY                                                  
         MVC   MJBPACT,SPAAACT                                                  
         MVC   MJBPSUBJ,ACTKACT                                                 
         XC    MJBPDA,MJBPDA                                                    
         MVC   MJBPSTA,ACTRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
MJBRECX  DS    0H                                                               
         DROP  R2,R3,RE                                                         
         EJECT                                                                  
***********************************************************************         
* JOB APPROVALS PASSIVE POINTERS                                      *         
* <<< FOR SJ JOBS ONLY >>>                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
JOBAPP   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         JNE   JOBAPPX                                                          
         CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         JNE   JOBAPPX                                                          
         XC    DUB,DUB                                                          
         USING RACELD,RE                                                        
         LA    RE,ACTRFST                                                       
*                                                                               
JOBAPP1  CLI   RACEL,0             GET CREATOR AND APPROVER                     
         JE    JOBAPP5                                                          
         CLI   RACEL,RACELQ                                                     
         JNE   JOBAPP3                                                          
         LA    RF,DUB                                                           
         CLI   RACTYPE,RACTADD                                                  
         JE    JOBAPP2                                                          
         LA    RF,DUB+2                                                         
         CLI   RACTYPE,RACTAPP                                                  
         JNE   JOBAPP4                                                          
*                                                                               
JOBAPP2  MVC   0(2,RF),RACPERS                                                  
         CLI   RACTYPE,RACTADD                                                  
         JNE   JOBAPP4                                                          
         MVC   DUB+4(L'RACDATE),RACDATE                                         
         J     JOBAPP4                                                          
*                                                                               
JOBAPP3  CLI   RACEL,JOBELQ                                                     
         JNE   JOBAPP4                                                          
         MVC   DUB+4(L'JOBADATE),RACEL+JOBADATE-JOBELD                          
*                                                                               
JOBAPP4  LLC   R0,RACLN                                                         
         AR    RE,R0                                                            
         J     JOBAPP1                                                          
         DROP  RE                                                               
*                                                                               
JOBAPP5  TM    ACTRSTAT,ACTSDRFT   APPROVED ACCOUNTS ONLY                       
         JNZ   JOBAPPX                                                          
         OC    DUB+2(2),DUB+2                                                   
         JNZ   JOBAPP6                                                          
         MVC   DUB+2(2),DUB        SET APPROVER FROM SUBMITTER                  
*                                                                               
         USING PIDRECD,R3                                                       
JOBAPP6  OC    DUB+2(2),DUB+2                                                   
         JZ    JOBAPP8                                                          
         XC    PIDRECD(ACCKLEN),PIDRECD                                         
         MVI   PIDKTYP,PIDKTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,ACTKCPY                                                  
         MVI   PIDKSTYP,PIDKJOBQ                                                
         MVI   PIDKJST,PIDKJSAQ                                                 
         MVC   PIDKACC,ACTKACT                                                  
         MVC   PIDKPID,DUB+2       FOR PERSON APPROVED                          
         XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,ACTRSTA                                                  
         OC    DUB+4(L'RACDATE),DUB+4                                           
         JZ    JOBAPP7                                                          
         GOTO1 VDATCON,DMCB,(1,DUB+4),(2,PIDKADAT)                              
JOBAPP7  DS    0H                                                               
*&&UK*&& LA    RE,PIDRECD+ACTKSUFA-ACTRECD                                      
*&&US*&& LA    RE,PIDRECD+ACTKSOFF-ACTRECD                                      
         MVC   0(2,RE),ACCOFFC     ASSUME JOB HAS NO OFFICE SET                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
JOBAPP8  OC    DUB(2),DUB                                                       
         JZ    JOBAPPX                                                          
         XC    PIDRECD(ACCKLEN),PIDRECD                                         
         MVI   PIDKTYP,PIDKTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,ACTKCPY                                                  
         MVI   PIDKSTYP,PIDKJOBQ                                                
         MVI   PIDKJST,PIDKJSSQ                                                 
         MVC   PIDKACC,ACTKACT                                                  
         MVC   PIDKPID,DUB         FOR PERSON ADDED/(CREATED                    
         XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,ACTRSTA                                                  
         OC    DUB+4(L'RACDATE),DUB+4                                           
         JZ    JOBAPP9                                                          
         GOTO1 VDATCON,DMCB,(1,DUB+4),(2,PIDKADAT)                              
JOBAPP9  DS    0H                                                               
*&&UK*&& LA    RE,PIDRECD+ACTKSUFA-ACTRECD                                      
*&&US*&& LA    RE,PIDRECD+ACTKSOFF-ACTRECD                                      
         MVC   0(2,RE),ACCOFFC     ASSUME JOB HAS NO OFFICE SET                 
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
JOBAPPX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* JOB ASSIGNEES PASSIVE POINTERS                                      *         
* <<< FOR SJ JOBS ONLY >>>                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
JOBASS   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         JNE   JOBASSX                                                          
         CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         JNE   JOBASSX                                                          
         USING LIDELD,RE                                                        
         LA    RE,ACTRFST                                                       
*                                                                               
JOBASS02 CLI   LIDEL,0                                                          
         JE    JOBASSX                                                          
         CLI   LIDEL,LIDELQ                                                     
         JE    JOBASS06                                                         
*                                                                               
JOBASS04 LLC   R0,LIDLN                                                         
         AR    RE,R0                                                            
         J     JOBASS02                                                         
*                                                                               
JOBASS06 CLI   LIDTYPE,LIDTTEAJ    TEST IF TEAM LIST ELEMENT                    
         JNE   JOBASS04            NO - GET NEXT ELEMENT                        
         LLC   R5,LIDLN                                                         
         SHI   R5,LIDLNDQ                                                       
         SRL   R5,2                R5=NUMBER OF PEOPLE ON JOB                   
PID      USING LIDROLN,R4                                                       
         LA    R4,LIDROLN          R4=A(FIRST ENTRY)                            
         ST    RE,SAVERE                                                        
                                                                                
         USING PIDRECD,R3                                                       
JOBASS08 OC    PID.LIDBPID,PID.LIDBPID                                          
         BZ    JOBASS11                                                         
         XC    PIDRECD(ACCKLEN),PIDRECD                                         
         MVI   PIDKTYP,PIDKTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,ACTKCPY                                                  
         MVI   PIDKSTYP,PIDKJOBQ                                                
         MVI   PIDKJST,PIDKJSTQ    ASSIGNED PASSIVE                             
         MVC   PIDKACC,ACTKACT                                                  
         MVC   PIDKPID,PID.LIDBPID                                              
         XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,ACTRSTA                                                  
         OC    DUB+4(L'RACDATE),DUB+4                                           
         JZ    JOBASS10                                                         
         GOTO1 VDATCON,DMCB,(1,DUB+4),(2,PIDKADAT)                              
JOBASS10 DS    0H                                                               
*&&UK*&& LA    RE,PIDRECD+ACTKSUFA-ACTRECD                                      
*&&US*&& LA    RE,PIDRECD+ACTKSOFF-ACTRECD                                      
         MVC   0(2,RE),ACCOFFC     ASSUME JOB HAS NO OFFICE SET                 
*                                                                               
JOBASS11 LA    R3,ACCKLEN(R3)                                                   
         LA    R4,LIDTMLNQ(R4)                                                  
         JCT   R5,JOBASS08                                                      
*                               ** ASSIGNED JOBS PASSIVES **                    
         USING ASJPASD,R3                                                       
         L     RE,SAVERE                                                        
         LLC   R5,LIDLN                                                         
         SHI   R5,LIDLNDQ                                                       
         SRL   R5,2                R5=NUMBER OF PEOPLE ON JOB                   
         LA    R4,LIDROLN          R4=A(FIRST ENTRY)                            
*                                                                               
JOBASS12 OC    PID.LIDBPID,PID.LIDBPID                                          
         BZ    JOBASS14                                                         
         XC    ASJPASD(ACCKLEN),ASJPASD                                         
         MVI   ASJPTYP,ASJPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   ASJPSUB,ASJPSUBQ                                                 
         MVC   ASJPCPY,ACTKCPY                                                  
         MVC   ASJPULA,ACTKULA                                                  
         MVC   ASJPPID,PID.LIDBPID                                              
         XC    ASJPDA,ASJPDA                                                    
         MVC   ASJPSTA,ACTRSTA                                                  
*                                                                               
JOBASS14 LA    R3,ACCKLEN(R3)                                                   
         LA    R4,LIDTMLNQ(R4)                                                  
         JCT   R5,JOBASS12                                                      
*                                                                               
         MVI   0(R3),EOL                                                        
*                                                                               
JOBASSX  DS    0H                                                               
         DROP  R2,R3,RE,PID                                                     
         EJECT                                                                  
***********************************************************************         
* SEARCH PASSIVES                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
SRCREC   CLI   RECTYPE,ACRTACTL    TEST FOR LOW-LEVEL ACCOUNT RECORD            
         JE    *+12                                                             
         CLI   RECTYPE,ACRTACTH    TEST FOR HIGH-LEVEL ACCOUNT RECORD           
         JNE   SRCREC10                                                         
         TM    LDGSTA2,LDGSSRCH    TEST SEARCH PASSIVES WANTED                  
         JZ    SRCRECX                                                          
         J     SRCREC20                                                         
*                                                                               
SRCREC10 CLI   RECTYPE,ACRTWCO     TEST FOR WORK-CODE RECORD                    
         JE    SRCREC20                                                         
*        CLI   RECTYPE,ACRTPMD     TEST FOR MEDIA CODE RECORD                   
*        BE    SRCREC20                                                         
*        CLI   RECTYPE,ACRTETYR    TEST FOR EXPENDITURE TYPE RECORDS            
*        BE    SRCREC20                                                         
*        CLI   RECTYPE,ACRTDEPT    TEST FOR DEPARTMENT RECORDS                  
*        BE    SRCREC20                                                         
         CLI   RECTYPE,ACRTORD     TEST FOR ORDER RECORD                        
         JE    SRCREC12                                                         
         CLI   RECTYPE,ACRTESTR    TEST FOR ESTIMATE RECORD                     
         JNE   SRCRECX                                                          
         USING ESTRECD,R2                                                       
         CLI   ESTKSEQ,ESTKSMQ     (MAIN RECORD ONLY)                           
         JNE   SRCRECX                                                          
         J     SRCREC20                                                         
*                                                                               
         USING ORDRECD,R2                                                       
SRCREC12 CLC   ORDKORD,=C'00000'   (MAIN BO/AURA ORDERS ONLY)                   
         JE    SRCRECX                                                          
         CLI   ORDKSEQ,0                                                        
         JNE   SRCRECX                                                          
         TM    ORDRSTA2,ORDSEXEX                                                
         JZ    SRCRECX                                                          
*                                                                               
         USING ACTRECD,R2                                                       
SRCREC20 ICM   R6,15,ASEARCHD      SET/TEST A(SEARCH BLOCK)                     
         JNZ   SRCREC30                                                         
*                                                                               
         LA    R6,SRCHBLKO         OFFLINE SEARCH BLOCK IN PROGRAM              
         CLI   OFFLINE,C'Y'                                                     
         JE    SRCREC30                                                         
         LA    R6,SRCHBLK          IF RUNNING ONLINE                            
         LA    RE,SRCHBLK                                                       
         LA    RF,SEARCHDL         LENGTH TO CLEAR                              
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR SRCHBLK                                
*                                                                               
         USING SEARCHD,R6          R6=A(SEARCH BLOCK)                           
SRCREC30 MVC   SBID,SPACES                                                      
         MVC   SBSYSTEM,ACC                                                     
         MVC   SBLANG,CPYLANG                                                   
         MVC   SBCTRY,CPYCTRY                                                   
         MVC   SBCOMFAC,VCOMFACS                                                
         ST    R2,SBAIN                                                         
         MVI   SBSTYLI,SBSTREQ     INPUT IS A RECORD                            
         ST    R3,SBAOUT                                                        
         MVI   SBSTYLO,SBSTPASQ    OUTPUT IS PASSIVE LIST                       
         MVI   SBMODE,1            MODE IS CREATE PASSIVES                      
*                                                                               
         CLI   RECTYPE,ACRTACTL    TEST FOR ACCOUNT RECORD                      
         JE    SRCREC40                                                         
         CLI   RECTYPE,ACRTACTH                                                 
         JE    SRCREC40                                                         
*                                                                               
         GOTO1 VSRCHXEC,DMCB,SEARCHD                                            
         JNE   SRCREC70            IGNORE IF ANY ERRORS                         
         L     R3,SBAOUT           GET NEXT AVAILABLE SLOT                      
         B     SRCREC70                                                         
*                                                                               
SRCREC40 DS    0H                                                               
         MVI   ACTKXHDR,ACTKXHDQ   PASS EXTRA DATA FOR ACSRCHDIR                
         MVC   ACTKXLV1,LDGLVALN   PASS ACCOUNT LEVEL LENGTHS                   
         MVC   ACTKXLV2,LDGLVBLN                                                
         MVC   ACTKXLV3,LDGLVCLN                                                
         MVC   ACTKXLV4,LDGLVDLN                                                
         MVC   ACTKXCPY,CPYSTAC                                                 
         CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         JNE   *+10                                                             
         MVC   ACTKXOFF,ACCOFFC    YES - PASS ACCOUNT OFFICE                    
         GOTO1 VSRCHXEC,DMCB,SEARCHD                                            
         JNE   SRCREC70            IGNORE IF ANY ERRORS                         
         MVC   ACTKXTRA,SPACES     ENSURE KEY BACK TO NORMAL                    
         L     R3,SBAOUT           GET NEXT AVAILABLE SLOT                      
*                                                                               
         USING SAPRECD,R3          ADD SEARCH ACCOUNT CODE POINTER              
         XC    SAPRECD(ACCKLEN),SAPRECD                                         
         MVI   SAPKTYP,SAPKTYPQ                                                 
         MVC   SAPKCUL,ACTKCPY                                                  
         MVI   SAPKSUB,SAPKSUBQ                                                 
         XR    RF,RF               FIND LEVEL OF ACCOUNT                        
         LA    R0,4                                                             
SRCREC50 XR    RE,RE                                                            
         IC    RE,LDGLVALN(RF)                                                  
         LA    RF,1(RF)                                                         
         LA    RE,ACTKACT(RE)                                                   
         CLC   0(L'ACTKACT,RE),SPACES                                           
         JE    *+12                                                             
         JCT   R0,SRCREC50                                                      
         J     SRCREC70                                                         
         STC   RF,ACTLVL                                                        
         STC   RF,SAPKLVL                                                       
         MVC   SAPKACT,ACTKACT                                                  
         MVC   SAPKSTAT,ACTRSTAT                                                
         L     RE,SBSEQNCE         GET SEQUENCE NUMBER                          
         LCR   RE,RE                                                            
         STCM  RE,7,ACTSEQ                                                      
         STCM  RE,7,SAPKSEQ                                                     
         CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         JNE   *+10                                                             
         MVC   SAPKSOFF,ACCOFFC                                                 
         MVC   SAPKDA,SBDA                                                      
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         TM    LDGSTA2,LDGSSNAM    TEST NAME PASSIVES WANTED                    
         JZ    SRCREC70                                                         
*                                                                               
         USING SANRECD,R3                                                       
         XC    SANRECD(ACCKLEN),SANRECD                                         
         MVI   SANKTYP,SANKTYPQ                                                 
         MVC   SANKCUL,ACTKCPY                                                  
         MVI   SANKSUB,SANKSUBQ                                                 
         LA    R4,ACTRFST          FIND NAME OF RECORD                          
         USING NAMELD,R4                                                        
         XR    RF,RF                                                            
*                                                                               
SRCREC60 CLI   NAMEL,0                                                          
         JE    SRCREC70                                                         
         IC    RF,NAMLN                                                         
         CLI   NAMEL,NAMELQ                                                     
         JE    *+8                                                              
         JXH   R4,RF,SRCREC60                                                   
         SH    RF,=Y(NAMLN1Q+1)                                                 
         JM    SRCREC70                                                         
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   SANKNAME(0),NAMEREC                                              
         DROP  R4                                                               
         MVC   SANKSEQ,ACTSEQ                                                   
         MVC   SANKLVL,ACTLVL                                                   
         MVC   SANKDA,SBDA                                                      
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
SRCREC70 MVI   0(R3),EOL                                                        
*                                                                               
SRCRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* PRODUCTION LEDGER PASSIVE POINTERS                                  *         
*                                                                     *         
* IF COMPANY IS USING ACCOUNT GROUPS (CPYSTAT7 CPYSAGRP BIT ON), THE  *         
* PRODUCTION CLIENT RECORD DEBTORS AND COSTING ACCOUNT CODES WILL BE  *         
* COPIED INTO THE PRODUCT PROFILE IF THOSE FIELDS ARE NOT ALREADY SET *         
* (THIS IS SO THAT WE CAN GENERATE THE ACCOUNT GROUP PASSIVES FROM    *         
* PRODUCT RECORDS ONLY)                                               *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
PROREC   CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         BNE   PRORECXX                                                         
         CLI   RECTYPE,ACRTACTH    TEST CLIENT/PRODUCT LEVEL ACCOUNT            
         BNE   PROREC08                                                         
         SR    R1,R1                                                            
         ICM   R1,1,LDGLVALN                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,ACTKACT(R1)                                                   
         CLI   0(R1),C' '          TEST CLIENT OR PRODUCT RECORD                
         BNE   PROREC04                                                         
*                                                                               
         LA    R1,ACTRFST          DEAL WITH CLIENT RECORD                      
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    CLIVALS(CLIVALSL),CLIVALS                                        
PROREC02 CLI   PPREL,0             TEST END OF RECORD                           
         BE    PRORECX                                                          
         CLI   PPREL,PPRELQ        LOCATE CLIENT PROFILE ELEMENT                
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     PROREC02                                                         
         MVC   CLIDEBAC,PPRRECVU                                                
         MVC   CLICSTAC,PPRCOSTU                                                
         MVC   CLIOFFC,PPRGAOFF                                                 
         MVC   ACCOFFC,CLIOFFC     SAVE OFF CLIENT OFFICE                       
         B     PRORECX                                                          
*                                                                               
PROREC04 LA    R1,ACTRFST          DEAL WITH PRODUCT RECORD                     
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    PROVALS(PROVALSL),PROVALS                                        
         MVI   PROAGR1,AGRKGTY1                                                 
         MVI   PROAGR2,AGRKGTY2                                                 
*                                                                               
PROREC06 CLI   PPREL,0             TEST END OF RECORD                           
         BE    PRORECX                                                          
         CLI   PPREL,PPRELQ        LOCATE PRODUCT PROFILE ELEMENT               
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     PROREC06                                                         
         MVC   PRODEBAC,PPRRECVU   SET PRODUCT ACCOUNT CODES                    
         MVC   PROCSTAC,PPRCOSTU                                                
         MVC   PROOFFC,PPRGAOFF                                                 
*                                                                               
         CLI   PROOFFC,C' '        FOUND ANY OFFICE AT PRODUCT LEVEL ?          
         BH    *+10                YES                                          
         MVC   PROOFFC,CLIOFFC     MERGE CLIENT OFFICE AT PRODUCT LVL ?         
         MVC   ACCOFFC,PROOFFC                                                  
         B     PRORECX                                                          
*                                                                               
PROREC08 CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         BNE   PRORECXX                                                         
         LA    R1,ACTRFST          DEAL WITH PRODUCT RECORD                     
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    JOBVALS(JOBVALSL),JOBVALS                                        
*                                                                               
PROREC10 CLI   PPREL,0             TEST END OF RECORD                           
         BE    PRORECXX                                                         
         CLI   PPREL,PPRELQ        LOCATE JOB PROFILE ELEMENT                   
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     PROREC10                                                         
         MVC   JOBOFFC,PPRGAOFF    SET JOB VALUES                               
*                                                                               
         CLI   JOBOFFC,C' '        MERGE WITH PRODUCT OFFICE                    
         BH    *+10                                                             
         MVC   JOBOFFC,PROOFFC                                                  
         MVC   ACCOFFC,JOBOFFC                                                  
         B     PRORECXX                                                         
*                                                                               
PRORECX  DS    0H                                                               
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ADVERTISER PASSIVES                                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
ACPREC   TM    CPYSTA6,CPYSADVP    TEST ADVERTISER POINTERS REQUIRED            
         BZ    ACPRECX                                                          
         SR    R1,R1                                                            
         IC    R1,LDGLVALN         TEST CLIENT LENGTH SET                       
         LA    R1,ACTKACT(R1)                                                   
         CLI   0(R1),C' '          TEST CLIENT RECORD                           
         BNE   ACPRECX                                                          
*                                                                               
         MVC   CLIADVC,ACTKACT     SET DEFAULT ADVERTISER CODE                  
         LA    R6,ACTRFST          PROCESS CLIENT RECORD                        
         SR    R0,R0                                                            
         USING PPRELD,R6                                                        
*                                                                               
         USING PTRELD,R6                                                        
ACPREC02 CLI   PTREL,0             TEST END OF RECORD                           
         BE    ACPREC06                                                         
         CLI   PTREL,PTRELQ        PROCESS ADVERTISER POINTER ELEMENT           
         BNE   ACPREC04                                                         
         CLI   PTRTYPE,PTRTADV     TEST ADVERTISER POINTER                      
         BNE   ACPREC04                                                         
         SR    RE,RE                                                            
         IC    RE,PTRLN                                                         
         SH    RE,=Y(PTRLN1Q+1)                                                 
         MVC   CLIADVC,SPACES      EXTRACT ADVERTISER CODE                      
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CLIADVC(0),PTRCODE                                               
*                                                                               
ACPREC04 IC    R0,PTRLN                                                         
         AR    R6,R0                                                            
         B     ACPREC02                                                         
*                                                                               
         USING ACPRECD,R3                                                       
ACPREC06 XC    ACPKEY,ACPKEY       BUILD ADVERTISER PASSIVE POINTERS            
         MVI   ACPKTYP,ACPKTYPQ                                                 
         MVC   ACPKCPY,COMPANY                                                  
         MVC   ACPKADV,CLIADVC                                                  
         MVC   ACPKCLI,ACTKACT                                                  
*                                                                               
         MVC   ACPKULA,ACTKUNT     PRODUCTION POINTER                           
         MVC   ACPKSTA,ACTRSTA                                                  
         XC    ACPKDA,ACPKDA                                                    
         MVC   ACPRECD+ACCKLEN(ACCKLEN),ACPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         OC    CLIDEBAC,CLIDEBAC                                                
         BZ    ACPREC08                                                         
         MVC   ACPKULA,CLIDEBAC    DEBTORS ACCOUNT POINTER                      
         MVC   ACPKSTA,ACTRSTA                                                  
         XC    ACPKDA,ACPKDA                                                    
         MVC   ACPRECD+ACCKLEN(ACCKLEN),ACPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
ACPREC08 OC    CLICSTAC,CLICSTAC                                                
         BZ    ACPREC10                                                         
         MVC   ACPKULA,CLICSTAC    COSTING ACCOUNT POINTER                      
         MVC   ACPKSTA,ACTRSTA                                                  
         XC    ACPKDA,ACPKDA                                                    
         MVC   ACPRECD+ACCKLEN(ACCKLEN),ACPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
ACPREC10 MVI   0(R3),EOL                                                        
*                                                                               
ACPRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* ACCOUNT GROUP PASSIVES                                              *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
AGPREC   TM    CPYSTA7,CPYSAGRP    TEST ACCOUNT GROUP PASSIVES REQUIRED         
         BZ    AGPRECX                                                          
         SR    R1,R1                                                            
         ICM   R1,1,LDGLVALN                                                    
         LA    R1,ACTKACT(R1)                                                   
         CLI   0(R1),C' '          TEST PRODUCT RECORD                          
         BE    AGPRECX                                                          
*                                                                               
         LA    R6,ACTRFST          PROCESS PRODUCT RECORD                       
         SR    R0,R0                                                            
         USING AGRELD,R6                                                        
AGPREC02 CLI   AGRELD,0            TEST END OF RECORD                           
         BE    AGPREC06                                                         
         CLI   AGREL,AGRELQ        TEST ACCOUNT GROUP ELEMENT                   
         BNE   AGPREC04                                                         
         MVC   PROAGR1C,AGRAGR1                                                 
         CLI   AGRLN,AGRLN2Q                                                    
         BL    AGPREC04                                                         
         MVC   PROAGR2C,AGRAGR2                                                 
*                                                                               
AGPREC04 IC    R0,AGRLN                                                         
         AR    R6,R0                                                            
         B     AGPREC02                                                         
*                                                                               
         USING AGPRECD,R3                                                       
AGPREC06 LA    R1,PROAGR1          POINT TO FIRST CODE                          
         LA    R0,2                R0=NUMBER OF GROUP CODES                     
*                                                                               
AGPREC08 XC    AGPKEY,AGPKEY       BUILD ACCOUNT GROUP PASSIVES                 
         MVI   AGPKTYP,AGPKTYPQ                                                 
         MVC   AGPKCPY,COMPANY                                                  
         MVC   AGPKCLPR,ACTKACT                                                 
         MVC   AGPKGTYP,0(R1)                                                   
         OC    AGPKAGR,L'AGPKGTYP(R1)                                           
         BZ    AGPREC12                                                         
*                                                                               
         MVC   AGPKULA,ACTKUNT                                                  
         MVC   AGPKSTA,ACTRSTA                                                  
         XC    AGPKDA,AGPKDA                                                    
         MVC   AGPRECD+ACCKLEN(ACCKLEN),AGPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         OC    PRODEBAC,PRODEBAC                                                
         BZ    AGPREC10                                                         
         MVC   AGPKULA,PRODEBAC    DEBTORS ACCOUNT POINTER                      
         MVC   AGPKSTA,ACTRSTA                                                  
         XC    AGPKDA,AGPKDA                                                    
         MVC   AGPRECD+ACCKLEN(ACCKLEN),AGPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
AGPREC10 OC    PROCSTAC,PROCSTAC                                                
         BZ    AGPREC12                                                         
         MVC   AGPKULA,PROCSTAC    COSTING ACCOUNT POINTER                      
         MVC   AGPKSTA,ACTRSTA                                                  
         XC    AGPKDA,AGPKDA                                                    
         MVC   AGPRECD+ACCKLEN(ACCKLEN),AGPRECD                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         MVI   AGPKUNT,C'2'        29 ACCOUNT (SAME AS COSTING)                 
         MVI   AGPKLDG,C'9'                                                     
         MVC   AGPKSTA,ACTRSTA                                                  
         XC    AGPKDA,AGPKDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
AGPREC12 MVI   0(R3),EOL                                                        
         LA    R1,L'AGRKGTYP+L'AGRKAGR(R1)                                      
         BCT   R0,AGPREC08                                                      
*                                                                               
AGPRECX  DS    0H                                                               
*                                                                               
PRORECXX DS    0H                  END OF PRODUCTION RECORD TYPES               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* CONTRACT PASSIVE POINTER                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING CNTRECD,R2                                                       
CNTREC   CLI   CNTKTYP,CNTKTYPQ    CONTRACT RECORD -X'3D01'                     
         BNE   CNTRECX                                                          
         CLI   CNTKSUB,CNTKSUBQ                                                 
         BNE   CNTRECX                                                          
         DROP  R2                                                               
*                                                                               
         USING CNTRECD,R3                                                       
         XC    CNTRECD(ACCKLEN),CNTRECD                                         
         MVI   CNTPTYP,CNTPTYPQ                 PASSIVE KEY - X'3D02'           
         MVI   CNTPSUB,CNTPSUBQ                                                 
         MVC   CNTPCPY,CNTKCPY-CNTRECD(R2)      COMPANY CODE                    
         MVC   CNTPSM,CNTKSM-CNTRECD(R2)        SYSTEM-MEDIA                    
         MVC   CNTPCNTR,CNTKCNTR-CNTRECD(R2)    CONTRACTOR                      
         MVC   CNTPCON,CNTKCON-CNTRECD(R2)      CONTRACT                        
         XC    CNTPDA,CNTPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
*                                                                               
CNTRECX  DS    0H                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* CALENDAR PASSIVE POINTER                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING CASRECD,R2                                                       
CASREC   CLI   CASKTYP,CASKTYPQ    CALENDAR RECORD                              
         BNE   CASRECX                                                          
         CLI   CASKSUB,CASKSUBQ                                                 
         BNE   CASRECX                                                          
*                                                                               
         LA    RF,CASRFST                                                       
         SR    R0,R0                                                            
         USING TMRELD,RF                                                        
CASREC02 CLI   TMREL,0             TEST END OF RECORD                           
         BE    CASRECX                                                          
         CLI   TMREL,TMRELQ        RULES RECORD                                 
         BE    CASREC06                                                         
CASREC04 IC    R0,TMRLN                                                         
         AR    RF,R0                                                            
         B     CASREC02                                                         
         DROP  R2                                                               
*                                                                               
         USING CASRECD,R3                                                       
CASREC06 XC    CASRECD(ACCKLEN),CASRECD                                         
         MVI   CASPTYP,CASPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   CASPSUB,CASPSUBQ                                                 
         MVC   CASPCPY,CASKCPY-CASRECD(R2)      COMPANY CODE                    
         MVC   CASPEDTE,TMREND     END OF PERIOD                                
         MVC   CASPSDTE,TMRSTART   START                                        
         MVC   CASPOFC,CASKOFC-CASRECD(R2)                                      
         MVC   CASPSTA,CASRSTA-CASRECD(R2)                                      
         XC    CASPDA,CASPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
*                                                                               
CASRECX  DS    0H                                                               
         DROP  R3,RF                                                            
         EJECT                                                                  
***********************************************************************         
* OFFICE - OFFICE LIST PASSIVE POINTER                                *         
***********************************************************************         
         SPACE 1                                                                
         USING OFFRECD,R2                                                       
OFFREC   CLI   RECTYPE,ACRTOFF     OFFICE RECORD                                
         BNE   OFFRECX                                                          
         TM    OFFRSTAT,OFFSLIST   CHECK IT'S A LIST RECORD                     
         BZ    OFFRECX             NO - NOT INTERESTED                          
         LA    RF,OFFRFST                                                       
         SR    R0,R0                                                            
         USING OFLELD,RF                                                        
OFFREC02 CLI   OFLEL,0             LOCATE OFFICE LIST                           
         BE    OFFRECX                                                          
         CLI   OFLEL,OFLELQ                                                     
         BE    OFFREC06                                                         
OFFREC04 IC    R0,OFLLN                                                         
         AR    RF,R0                                                            
         B     OFFREC02                                                         
*                                                                               
OFFREC06 XR    RE,RE                                                            
         IC    RE,OFLLN                                                         
         SHI   RE,OFLLN1Q                                                       
         SRL   RE,1                RF=NUMBER OF ENTRIES IN LIST                 
         LA    R1,OFLNTRY                                                       
         USING OFLNTRY,R1                                                       
         USING OFLPASD,R3                                                       
OFFREC08 XC    OFLPASD(ACCKLEN),OFLPASD                                         
         MVI   OFLPTYP,OFLPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   OFLPSUB,OFLPSUBQ                                                 
         MVC   OFLPCPY,COMPANY     COMPANY CODE                                 
         MVC   OFLPOFF,OFLNTRY     OFFICE CODE                                  
         MVC   OFLPOFL,OFFKOFF     OFFICE LIST CODE                             
         XC    OFLPDA,OFLPDA                                                    
         XC    OFLPSTA,OFLPSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         LA    R1,L'OFLNTRY(R1)                                                 
         BCT   RE,OFFREC08                                                      
         B     OFFREC04                                                         
*                                                                               
OFFRECX  DS    0H                                                               
         DROP  R3,RF                                                            
         EJECT                                                                  
***********************************************************************         
* PERSON - PID PASSIVE POINTER                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING PERRECD,R2                                                       
PIDREC   CLI   PERKTYP,PERKTYPQ    PERSON RECORD                                
         BE    PIDREC30                                                         
         CLI   RECTYPE,ACRTTEAM    TEAM RECORD                                  
         BE    PIDREC05                                                         
         CLI   RECTYPE,ACRTAPPR    APPROVER RECORD                              
         BE    PIDREC05                                                         
         CLI   RECTYPE,ACRTGRPL    GROUP LIST RECORD                            
         BE    PIDREC05                                                         
         CLI   RECTYPE,ACRTROLE    ROLE RECORD                                  
         BE    PIDREC05                                                         
*                                                                               
         CLI   RECTYPE,ACRTACTL    TEST FOR LOW-LEVEL ACCOUNT RECORD            
         BNE   PIDREC01                                                         
         USING ACTRECD,R2                                                       
         CLC   =C'SV',ACTKUNT      TEST SV/SX LOW-LEVEL ACCOUNT?                
         BE    PIDREC30                                                         
         CLC   =C'SX',ACTKUNT                                                   
         BE    PIDREC30                                                         
         CLC   =C'2P',ACTKUNT                                                   
         BE    PIDREC30                                                         
         CLC   =C'SW',ACTKUNT                                                   
         BE    PIDREC30                                                         
         CLC   =C'SY',ACTKUNT                                                   
         BE    PIDREC30                                                         
         CLC   =C'SJ',ACTKUNT                                                   
         BE    PIDREC05                                                         
         B     PIDRECX                                                          
*                                                                               
         USING ORDRECD,R2                                                       
PIDREC01 CLI   RECTYPE,ACRTORD     TEST FOR ORDER RECORD(S)                     
         BNE   PIDRECX                                                          
         CLI   ORDKSEQ,0           ONLY REAL ORDERS                             
         BNE   PIDRECX                                                          
         TM    ORDRSTA2,ORDSEXEX                                                
         BZ    PIDRECX                                                          
*                                                                               
         LA    RF,ORDRFST                                                       
         SR    R0,R0                                                            
         USING ORDELD,RF                                                        
PIDREC02 CLI   ORDEL,0             LOCATE FFT/REQ. NUMBER ELEMENT               
         BE    LDCPX                                                            
         CLI   ORDEL,ORDELQ                                                     
         BNE   PIDREC04                                                         
         OC    ORDCPID,ORDCPID                                                  
         BNZ   PIDREC34                                                         
PIDREC04 IC    R0,ORDLN                                                         
         AR    RF,R0                                                            
         B     PIDREC02                                                         
*                                                                               
PIDREC05 LA    RF,ORDRFST                                                       
         USING LIDELD,RF                                                        
PIDREC06 CLI   LIDEL,0             RECORD END?                                  
         BE    PIDRECX             YES                                          
         CLI   LIDEL,LIDELQ        LIDEL?                                       
         BNE   PIDREC08            YES                                          
         CLI   LIDTYPE,LIDTPID     IS IT PID LIST                               
         BE    PIDREC10                                                         
         CLI   LIDTYPE,LIDTTEAJ    IS IT TEAM ROLE/PID LIST                     
         BE    PIDREC10                                                         
         CLI   LIDTYPE,LIDTBACK    IS IT BACK UP APPROVER LIST                  
         BE    PIDREC10                                                         
*                                                                               
PIDREC08 SR    R0,R0                                                            
         IC    R0,LIDLN            GET NEXT ELEMENT                             
         AR    RF,R0                                                            
         B     PIDREC06                                                         
*                                                                               
PIDREC10 LA    R4,LIDDATA                                                       
         SR    R4,RF               MINUS ELEMENT START ADDRESS                  
         STH   R4,CURDISP          CURRENT DISPLACEMENT INTO ELEMENT            
         XR    R4,R4                                                            
         IC    R4,LIDITLN          GET LENGTH OF DATA                           
         STH   R4,DATALEN          SAVE THIS LENGTH                             
         IC    R4,LIDLN                                                         
         STH   R4,TOTELLN          SAVE TOTAL ELEMENT LENGTH                    
         LA    R4,LIDDATA                                                       
         B     PIDREC34                                                         
*                                                                               
PIDREC12 LH    R4,DATALEN          LENGTH OF DATA                               
         AH    R4,CURDISP          CURRENT DISPLACEMENT TO DATA                 
         STH   R4,CURDISP          SAVE NEXT DISPLACEMENT TO DATA               
         CH    R4,TOTELLN          HAVE WE REACHED END OF ELEMENT               
         BNL   PIDREC08            YES                                          
         AR    R4,RF                                                            
         B     PIDREC34            NO PUT OUT NEXT PID PASSIVE                  
*                                                                               
         USING PERRECD,R2                                                       
PIDREC30 LA    RF,PERRFST                                                       
         SR    R0,R0                                                            
         USING PIDELD,RF                                                        
PIDREC32 CLI   PIDEL,0             TEST END OF RECORD                           
         BE    PIDRECX                                                          
         CLI   PIDEL,PIDELQ        PID ELEM                                     
         BE    PIDREC34                                                         
         IC    R0,PIDLN                                                         
         AR    RF,R0                                                            
         B     PIDREC32                                                         
*                                                                               
         USING PIDRECD,R3                                                       
PIDREC34 XC    PIDRECD(ACCKLEN),PIDRECD                                         
         CLI   RECTYPE,ACRTGRPL    GROUP LIST RECORD                            
         BE    PIDREC52                                                         
         MVI   PIDKTYP,PIDKTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,COMPANY     COMPANY CODE                                 
*                                                                               
         CLI   PERKTYP,PERKTYPQ    PERSON RECORD                                
         BNE   PIDREC36                                                         
         MVI   PIDKSTYP,PIDKPERQ   SET PERSON PASSIVE                           
         MVC   PIDKPER,PERKCODE    PERSON CODE                                  
         MVC   PIDKPID,PIDNO       PID NUMBER                                   
         MVC   PIDKSTA,PERRSTA     PERSON STATUS                                
*                                                                               
         USING APPRECD,R2                                                       
PIDREC36 CLI   RECTYPE,ACRTAPPR    APPROVER RECORD                              
         BNE   PIDREC40                                                         
         MVI   PIDKSTYP,PIDKBAPQ   SET APPROVER PASSIVE                         
         MVC   PIDKPID,2(R4)                                                    
         MVC   PIDKAPPL(L'PIDKAPPL+L'PIDKAPP2),0(R4)                            
         MVC   PIDKPIDO,APPKPIDB   PID OF ORIGINAL APPROVER                     
*                                                                               
         USING ORDRECD,R2                                                       
         USING ORDELD,RF                                                        
PIDREC40 CLI   RECTYPE,ACRTORD     ORDER RECORD                                 
         BNE   PIDREC42                                                         
         MVI   PIDKSTYP,PIDKORDQ   SET ORDER PASSIVE                            
         MVC   PIDKPID,ORDCPID     PID OF PERSON WHO CREATED ORDER              
         MVC   PIDKORD,ORDKORD     ORDER NUMBER                                 
*                                                                               
         USING TEARECD,R2                                                       
PIDREC42 CLI   RECTYPE,ACRTTEAM    TEAM RECORD                                  
         BNE   PIDREC44                                                         
         MVI   PIDKSTYP,PIDKTEAQ   SET TEAM PASSIVE                             
         MVC   PIDKPID,0(R4)                                                    
         MVC   PIDKNUM,TEAKNUM                                                  
         MVC   PIDKOFF,TEAKOFF                                                  
*                                                                               
         USING ROLRECD,R2                                                       
PIDREC44 CLI   RECTYPE,ACRTROLE    ROLE RECORD                                  
         BNE   PIDREC46                                                         
         MVI   PIDKSTYP,PIDKROLQ   SET ROLE PASSIVE                             
         MVC   PIDKPID,0(R4)                                                    
         MVC   PIDKNUM,ROLKNUM                                                  
         MVC   PIDKOFF,ROLKOFF                                                  
*                                                                               
         USING ACTRECD,R2                                                       
         USING PIDELD,RF                                                        
PIDREC46 CLI   RECTYPE,ACRTACTL    LOW-LEVEL ACCOUNT RECORD                     
         BNE   PIDREC50                                                         
         CLC   =C'SJ',ACTKULA      PRODUCTION ACCOUNT?                          
         BE    PIDREC48                                                         
         MVI   PIDKSTYP,PIDKCRDQ   SET CREDITOR PASSIVE                         
         CLC   =C'2P',ACTKULA      PERSONNEL ACCOUNT?                           
         BNE   *+8                                                              
         MVI   PIDKSTYP,PIDKPSNQ   SET PERSONNEL PASSIVE                        
         MVC   PIDKULA,ACTKULA     ACCOUNT CODE                                 
         MVC   PIDKPID,PIDNO                                                    
         B     PIDREC50                                                         
PIDREC48 MVI   PIDKSTYP,PIDKCLRQ   SET ROLE PASSIVE                             
         MVC   PIDKPID,2(R4)                                                    
         MVC   PIDKSJAC,ACTKACT                                                 
         MVC   PIDKROLE,0(R4)                                                   
*                                                                               
         USING PERRECD,R2                                                       
PIDREC50 XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,PERRSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CLI   RECTYPE,ACRTTEAM    TEAM RECORD                                  
         BE    PIDREC12                                                         
         CLI   RECTYPE,ACRTROLE    ROLE RECORD                                  
         BE    PIDREC12                                                         
         CLI   RECTYPE,ACRTAPPR    APPROVER RECORD                              
         BE    PIDREC12                                                         
         CLI   RECTYPE,ACRTACTL    ACCOUNT RECORD                               
         BNE   PIDRECX                                                          
         USING ACTRECD,R2                                                       
         CLC   =C'SJ',ACTKULA      PRODUCTION ACCOUNT?                          
         BE    PIDREC12                                                         
         B     PIDRECX                                                          
         USING GLSRECD,R2                                                       
         USING LLSRECD,R3                                                       
PIDREC52 XC    LLSKEY(ACCKLEN),LLSKEY                                           
         MVI   LLSKTYP,LLSKTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   LLSKSUB,LLSKSUBQ                                                 
         MVC   LLSKCPY,COMPANY     COMPANY CODE                                 
         MVC   LLSKPIDB,0(R4)      PERSONAL ID                                  
         MVC   LLSKGRP,GLSKGRP                                                  
         MVC   LLSKOFF,GLSKOFF                                                  
         XC    LLSKDA,LLSKDA                                                    
         MVC   LLSKSTA,GLSRSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     PIDREC12                                                         
*                                                                               
PIDRECX  DS    0H                                                               
         DROP  R2,R3,RF                                                         
         EJECT                                                                  
***********************************************************************         
* APPROVER - DEFAULT APPROVER POINTERS                                *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R2                                                       
APPREC   CLI   RECTYPE,ACRTAPPR    APPROVER RECORD                              
         BNE   APPRECX                                                          
                                                                                
         TM    APPRSTAT,APPSFIND+APPSFINA                                       
         BZ    APPRECX                                                          
         USING DPAPASD,R3                                                       
         XC    DPAPASD(ACCKLEN),DPAPASD                                         
         MVI   DPAPTYP,DPAPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   DPAPSUB,DPAPSUBQ                                                 
         MVC   DPAPCPY,APPKCPY     COMPANY CODE                                 
         MVI   DPAPAPPL,DPAPAEXF   FINANCE APPROVER                             
         MVC   DPAPPIDB,APPKPIDB   2BYTE BINARY PID                             
         MVC   DPAPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         MVC   DPAP1RAC,XFFS                                                    
         ZAP   DPAPXVAL,PZERO                                                   
         XC    DPAPDA,DPAPDA                                                    
         TM    APPRSTAT,APPSFIND                                                
         BZ    *+8                                                              
         OI    DPAPSTAT,DPAPDFLT                                                
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     APPRECX                                                          
*                                                                               
APPRECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* APPROVER - JOB PID, MANAGER PID, PURCHASING PID, FINANCE PID AND    *         
*            JOB APPROVER                                             *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R2                                                       
ASJREC   CLI   RECTYPE,ACRTAPPR    APPROVER RECORD                              
         BNE   ASJRECX                                                          
                                                                                
         MVI   0(R3),EOL                                                        
*                                                                               
         MVI   MYBYTE2,0                                                        
         MVI   MYBYTE3,0                                                        
ASJREC18 LA    RF,APPRFST                                                       
         USING LIDELD,RF                                                        
ASJREC20 CLI   LIDEL,0             TEST END OF RECORD                           
         BE    ASJREC64                                                         
         CLI   LIDEL,LIDELQ        LIST ELEMENT                                 
         BNE   ASJREC22                                                         
         CLI   LIDTYPE,LIDTAPSJ    CLIENT PRODUCT JOB APPLICATION LIST          
         BE    ASJREC24                                                         
         CLI   LIDTYPE,LIDTAP1R    OFFICE/DEPT/SUB-DEPT/PERS                    
         BE    ASJREC24                                                         
         CLI   LIDTYPE,LIDTAP1N    NON CLIENT TYPE                              
         BE    ASJREC24                                                         
         CLI   LIDTYPE,LIDTINOR    APPROVER INVOICE/ORDER                       
         BE    ASJREC24                                                         
ASJREC22 SR    R0,R0                                                            
         IC    R0,LIDLN                                                         
         AR    RF,R0                                                            
         B     ASJREC20                                                         
                                                                                
ASJREC24 LA    R4,LIDDATA                                                       
         SR    R4,RF               MINUS ELEMENT START ADDRESS                  
         STH   R4,CURDISP          CURRENT DISPLACEMENT INTO ELEMENT            
         XR    R4,R4                                                            
         IC    R4,LIDITLN          GET LENGTH OF DATA                           
         STH   R4,DATALEN          SAVE THIS LENGTH                             
         IC    R4,LIDLN                                                         
         STH   R4,TOTELLN          SAVE TOTAL ELEMENT LENGTH                    
         LA    R4,LIDDATA                                                       
AP       USING LIDDATA,R4                                                       
         B     ASJREC28                                                         
*                                                                               
ASJREC26 LH    R4,DATALEN          LENGTH OF DATA                               
         AH    R4,CURDISP          CURRENT DISPLACEMENT TO DATA                 
         STH   R4,CURDISP          SAVE NEXT DISPLACEMENT TO DATA               
         CH    R4,TOTELLN          HAVE WE REACHED END OF ELEMENT               
         BNL   ASJREC22            YES                                          
         AR    R4,RF                                                            
*                                                                               
         USING JOBPASD,R3                                                       
ASJREC28 CLI   LIDTYPE,LIDTAP1N    NON CLIENT TYPE                              
         BE    ASJREC36                                                         
         CLI   LIDTYPE,LIDTAPSJ    CLIENT PRODUCT JOB APPLICATION LIST          
         BE    ASJREC42                                                         
         CLI   LIDTYPE,LIDTINOR    APPROVER INVOICE/ORDER                       
         BE    ASJREC44                                                         
*                                                                               
ASJREC30 SR    RE,RE                                                            
         LH    RE,DATALEN                                                       
         SHI   RE,2                                                             
         BM    ASJREC22                                                         
         TM    0(R4),LIDAPDTI      TIME APPLICATION ?                           
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPATIM                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR TIME APPL                 
         TM    0(R4),LIDAPDEX      EXPENSE LINE MANAGER APPROVER?               
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEXP                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE APPL              
         TM    0(R4),LIDAPDEF+LIDAPDED EXPENSE FINANCE APPROVER                 
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEXF                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE APPL FIN          
         TM    0(R4),LIDAPDE1                                                   
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEX1                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE LVL 1             
         TM    0(R4),LIDAPDE2                   NON BILLABLE                    
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEX2                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE LEVEL 2           
         TM    0(R4),LIDAPDB1                        NON BILLABLE               
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEB1                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE LEVEL 1           
         TM    0(R4),LIDAPDB2                       BILLABLE                    
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPAEB2                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR EXPENSE LEVEL 2           
         TM    AP.LIDAPDT2,LIDAPDTO                                             
         BZ    *+12                                                             
         MVI   MYBYTE,DPAPATIO                                                  
         BRAS  RE,ADD1RPAS         ADD 1R PASSIVE FOR TIMEOFF                   
         B     ASJREC26                                                         
*                                                                               
         USING NCTPASD,R3                                                       
ASJREC36 SR    RE,RE                                                            
         LH    RE,DATALEN                                                       
         SHI   RE,3                                                             
         BM    ASJREC22                                                         
         TM    0(R4),LIDATIME      TIME APPLICATION ?                           
         BZ    *+12                                                             
         MVI   MYBYTE,NCTPATIM                                                  
         BRAS  RE,ADD1NPAS         ADD 1R PASSIVE FOR TIME APPL                 
         B     ASJREC26                                                         
*                                                                               
ASJREC42 SR    RE,RE                                                            
         LH    RE,DATALEN                                                       
         SHI   RE,3                                                             
         BM    ASJREC22                                                         
         TM    0(R4),LIDATIME      TIME APPLICATION ?                           
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPATIM                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR TIME APPL                 
         TM    0(R4),LIDAEXPN      EXPENSE APPLICATION ?                        
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAEXP                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR EXPENSE APPL              
         TM    0(R4),LIDAESTY+LIDAESTD  ESTIMATE APPLICATION ?                  
         BZ    *+16                                                             
         MVI   MYBYTE,JOBPAEST                                                  
         MVI   MYBYTE2,1           SET ESTIMATE APPROVAL FOUND                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR ESTIMATE APPL             
         TM    0(R4),LIDAORDY+LIDAORDD ORDER APPLICATION ?                      
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAORD                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR ORDER APPL                
         TM    0(R4),LIDAJOBY+LIDAJOBD    JOB APPLICATION ?                     
         BZ    *+16                                                             
         MVI   MYBYTE,JOBPAJOB                                                  
         MVI   MYBYTE3,1           SET JOB APPROVAL FOUND                       
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR JOB APPL                  
         TM    1(R4),LIDAESI+LIDAESID   ESTIMATE INTERNAL APPROVAL              
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAESI                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR ESTIMATE APPL             
         TM    1(R4),LIDAEL1B      EXPENSE LEVEL 1 BILLABLE                     
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAX1B                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR EXPENSE APPL              
         TM    1(R4),LIDAEL1N      EXPENSE LEVEL 1 NON BILLABLE                 
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAX1N                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR EXPENSE APPL              
         TM    1(R4),LIDAEL2B      EXPENSE LEVEL 2 BILLABLE                     
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAX2B                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR EXPENSE APPL              
         TM    1(R4),LIDAEL2N      EXPENSE LEVEL 2 NON BILLABLE                 
         BZ    *+12                                                             
         MVI   MYBYTE,JOBPAX2N                                                  
         BRAS  RE,ADDSJPAS         ADD SJ PASSIVE FOR EXPENSE APPL              
         B     ASJREC26                                                         
*                                                                               
         USING APPPASD,R3                                                       
ASJREC44 SR    RE,RE                                                            
         LH    RE,DATALEN                                                       
         LTR   RE,RE                                                            
         BZ    ASJREC22                                                         
                                                                                
         TM    AP.LIDAPTYP,LIDAPOR+LIDAPORD                                     
         BZ    ASJREC46                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPORD     APPROVAL TYPE                                
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF SELF APPROVAL                    
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPORD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC46            NO                                           
         CP    AP.LIDAPSEL,PZERO   HAVE WE GOT A SELF APPROVAL VALUE            
         BE    ASJREC46            NO                                           
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPORD     APPROVAL TYPE                                
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPORD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC46 TM    AP.LIDAPTYP,LIDAPOF+LIDAPOFD                                     
         BZ    ASJREC48                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPORDF                                                 
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,PZERO                                                    
         ZAP   APPPSEL,PZERO                                                    
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPOFD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC48 TM    AP.LIDAPTYP,LIDAPIN+LIDAPIND                                     
         BZ    ASJREC50                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPINV                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPIND                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC50 TM    AP.LIDAPTYP,LIDAPES+LIDAPESD  ESTIMATES                          
         BZ    ASJREC52                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPEST                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPESD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC52            NO                                           
         CP    AP.LIDAPSEL,PZERO      HAVE WE GOT A SELF APPROVAL VALUE         
         BE    ASJREC52                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPEST                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTYP,LIDAPESD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC52 TM    AP.LIDAPTY2,LIDAPOA+LIDAPOAD  ORDS WITH APPROVED ESTS            
         BZ    ASJREC54                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPAEO                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY2,LIDAPOAD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC54            NO                                           
         CP    AP.LIDAPSEL,PZERO      HAVE WE GOT A SELF APPROVAL VALUE         
         BE    ASJREC54                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPAEO                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY2,LIDAPOAD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC54 TM    AP.LIDAPTY2,LIDAPOU+LIDAPOUD  ORDS WITH UNAPPROVED ESTS          
         BZ    ASJREC56                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPUEO                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY2,LIDAPOUD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC56            NO                                           
         CP    AP.LIDAPSEL,PZERO      HAVE WE GOT A SELF APPROVAL VALUE         
         BE    ASJREC56                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPUEO                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY2,LIDAPOUD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC56 TM    AP.LIDAPTY3,LIDAINEA+LIDAINED INTERNAL ESTIMATE APPRS            
         BZ    ASJREC58                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPIEA                                                  
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY3,LIDAINED                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC58            NO                                           
         CP    AP.LIDAPSEL,PZERO      HAVE WE GOT A SELF APPROVAL VALUE         
         BE    ASJREC58                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPIEA     INTERNAL ESTIMATE APPROVER                   
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER CRED OR CLI/PRO/JOB              
         TM    AP.LIDAPTY3,LIDAINED                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ASJREC58 TM    AP.LIDAPTY3,LIDAEXCD+LIDAEXC EXPENSE CLAIM APPRS                 
         BZ    ASJREC26                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPEXPS                                                 
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPVAL    VALUE OF APPROVER                         
         ZAP   APPPSEL,AP.LIDAPSEL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER 1R                               
         TM    AP.LIDAPTY3,LIDAEXCD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         CP    AP.LIDAPSEL,AP.LIDAPVAL IS SELF APPROVAL VALUE DIFFERENT         
         BE    ASJREC26            NO                                           
         CP    AP.LIDAPSEL,PZERO      HAVE WE GOT A SELF APPROVAL VALUE         
         BE    ASJREC26                                                         
         XC    APPPASD(ACCKLEN),APPPASD                                         
         MVI   APPPTYP,APPPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,APPKCPY     COMPANY CODE                                 
         MVI   APPPCAT,APPPEXPS    EXPENSE CLAIM APPROVER                       
         MVC   APPPSCAT,AP.LIDAPSCT   SUB CATEGORY                              
         ZAP   APPPVAL,AP.LIDAPSEL    VALUE OF SELF APPROVER                    
         ZAP   APPPSEL,AP.LIDAPVAL    VALUE OF APPROVER                         
         MVC   APPPPIDB,APPKPIDB   2 BYTE BINARY PID                            
         MVC   APPPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    APPPDA,APPPDA                                                    
         MVC   APPPSTA,APPRSTA                                                  
         MVC   APPPMED,AP.LIDAPMED    APPROVER MEDIA                            
         MVC   APPPOFFC,AP.LIDAPOFF   APPROVER OFFICE                           
         MVC   APPPDEPT,AP.LIDAPDPT   APPROVER DEPT                             
         MVC   APPPETYP,AP.LIDAPETY   APPROVER EXPENDITURE TYPE                 
         MVC   APPPACLA,AP.LIDAPAC    APPROVER 1R ACCOUNT                       
         TM    AP.LIDAPTY3,LIDAEXCD                                             
         BZ    *+8                                                              
         OI    APPPSTAT,APPPDFLT   SET DEFAULT APPROVER                         
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     ASJREC26                                                         
         DROP  AP                                                               
*                                                                               
ASJREC64 CLI   MYBYTE3,0           DID WE FIND JOB APPROVER                     
         BNE   ASJREC66            YES - NOT AT HIGH LEVEL                      
         TM    APPRSTA2,APPSRJAQ+APPSDJAQ     ARE THEY A JOB APPROVER           
         BZ    ASJREC66            NO - CHECK FOR OTHER PASSIVES                
         USING JOBPASD,R3                                                       
         XC    JOBPASD(ACCKLEN),JOBPASD                                         
         MVI   JOBPTYP,JOBPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   JOBPSUB,JOBPSUBQ                                                 
         MVC   JOBPCPY,APPKCPY     COMPANY CODE                                 
         MVC   JOBPPIDB,APPKPIDB                                                
         MVC   JOBPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         MVI   JOBPAPPL,JOBPAJOB                                                
         MVI   JOBPVIEW,JOBPVOFF                                                
         MVC   JOBPOFFC,XFFS                                                    
         MVC   JOBPMED,XFFS                                                     
         MVC   JOBPJOB,XFFS                                                     
         XC    JOBPDA,JOBPDA                                                    
         XC    JOBPSTA,JOBPSTA                                                  
         TM    APPRSTA2,APPSDJAQ   ARE THEY A DEFAULT JOB APPROVER              
         BZ    *+8                 NO - CHECK FOR OTHER PASSIVES                
         OI    JOBPSTAT,JOBPDFLT                                                
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
ASJREC66 CLI   MYBYTE2,0           ESTIMATE APROVAL FOUND                       
         BNE   ASJREC68            YES - NO HIGH LEVEL PASSIVE                  
         TM    APPRSTA2,APPSESTQ   ESTIMATE APPROVER?                           
         BZ    ASJREC68            NO - CHECK FOR OTHER PASSIVES                
         USING JOBPASD,R3                                                       
         XC    JOBPASD(ACCKLEN),JOBPASD                                         
         MVI   JOBPTYP,JOBPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   JOBPSUB,JOBPSUBQ                                                 
         MVC   JOBPCPY,APPKCPY     COMPANY CODE                                 
         MVC   JOBPPIDB,APPKPIDB                                                
         MVC   JOBPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         MVI   JOBPAPPL,JOBPAEST   CLIENT APPROVAL                              
         MVI   JOBPVIEW,JOBPVOFF                                                
         MVC   JOBPJOB,XFFS                                                     
         MVC   JOBPMED,XFFS                                                     
         MVC   JOBPOFFC,XFFS                                                    
         XC    JOBPDA,JOBPDA                                                    
         XC    JOBPSTA,JOBPSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
ASJREC68 DS    0H                                                               
         TM    APPRSTA2,APPSESIQ                                                
         BZ    ASJRECX                                                          
         USING JOBPASD,R3                                                       
         XC    JOBPASD(ACCKLEN),JOBPASD                                         
         MVI   JOBPTYP,JOBPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   JOBPSUB,JOBPSUBQ                                                 
         MVC   JOBPCPY,APPKCPY     COMPANY CODE                                 
         MVC   JOBPPIDB,APPKPIDB                                                
         MVC   JOBPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         MVI   JOBPAPPL,JOBPAESI   INTERNAL APPROVAL                            
         MVI   JOBPVIEW,JOBPVOFF                                                
         MVC   JOBPJOB,XFFS                                                     
         MVC   JOBPMED,XFFS                                                     
         MVC   JOBPOFFC,XFFS                                                    
         XC    JOBPDA,JOBPDA                                                    
         XC    JOBPSTA,JOBPSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
ASJREC70 DS    0H                                                               
ASJRECX  DS    0H                                                               
         DROP  R2,R3,RF                                                         
         EJECT                                                                  
***********************************************************************         
* EXPENDITURE CATEGORY - PASSIVE POINTER FROM ETYPE RECORD            *         
***********************************************************************         
         SPACE 1                                                                
         USING ETYRECD,R2                                                       
EXPCAT   CLI   RECTYPE,ACRTETYR    EXPENDITURE TYPE RECORD                      
         BNE   EXPCATX                                                          
                                                                                
         USING ECTPASD,R3                                                       
         MVI   MYBYTE,0                                                         
ECAT02   LA    RF,ETYRFST                                                       
         SR    R0,R0                                                            
         USING XNMELD,RF                                                        
ECAT04   CLI   XNMEL,0             TEST END OF RECORD                           
         BE    ECAT16                                                           
         CLI   XNMEL,LIDELQ        LIST ELEMENT                                 
         BE    ECAT10                                                           
         CLI   XNMEL,XNMELQ        EXTRA NAME ELEMENT                           
         BNE   ECAT06                                                           
         CLI   XNMLN,XNMLN1Q                                                    
         BH    ECAT08                                                           
*                                                                               
ECAT06   LLC   R0,XNMLN                                                         
         AR    RF,R0                                                            
         B     ECAT04                                                           
*                                                                               
ECAT08   XC    ECTPASD(ACCKLEN),ECTPASD                                         
         MVC   ECTPCAT,SPACES                                                   
         SR    R4,R4                                                            
         IC    R4,XNMSUBL                                                       
         SHI   R4,1                                                             
         MVC   ECTPCAT(0),XNMSUBN                                               
         EX    R4,*-6                                                           
         MVI   ECTPTYP,ECTPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   ECTPSUB,ECTPSUBQ                                                 
         MVC   ECTPCPY,ETYKCPY     COMPANY CODE                                 
         MVC   ECTPCODE,ETYKCODE   EXPENDITURE CODE                             
         MVC   ECTPSEQ,ETYKSEQ     SEQUENCE NUMBER                              
         XC    ECTPDA,ECTPDA                                                    
         MVC   ECTPSTA,ETYRSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     ECAT06                                                           
                                                                                
         USING LIDELD,RF                                                        
ECAT10   CLI   LIDTYPE,LIDTSUPP                                                 
         BNE   ECAT06                                                           
         LA    R4,LIDDATA                                                       
         SR    R4,RF               MINUS ELEMENT START ADDRESS                  
         STH   R4,CURDISP          CURRENT DISPLACEMENT INTO ELEMENT            
         XR    R4,R4                                                            
         IC    R4,LIDITLN          GET LENGTH OF DATA                           
         STH   R4,DATALEN          SAVE THIS LENGTH                             
         IC    R4,LIDLN                                                         
         STH   R4,TOTELLN          SAVE TOTAL ELEMENT LENGTH                    
         LA    R4,LIDDATA                                                       
AP       USING LIDDATA,R4                                                       
         B     ECAT14                                                           
*                                                                               
ECAT12   LH    R4,DATALEN          LENGTH OF DATA                               
         AH    R4,CURDISP          CURRENT DISPLACEMENT TO DATA                 
         STH   R4,CURDISP          SAVE NEXT DISPLACEMENT TO DATA               
         CH    R4,TOTELLN          HAVE WE REACHED END OF ELEMENT               
         BNL   ECAT06              YES                                          
         AR    R4,RF                                                            
*                                                                               
         USING CETPASD,R3                                                       
ECAT14   SR    RE,RE                                                            
         LH    RE,DATALEN                                                       
         SHI   RE,3                                                             
         BM    ECAT06                                                           
         MVI   MYBYTE,1                                                         
         XC    CETPASD(ACCKLEN),CETPASD                                         
         MVI   CETPTYP,CETPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   CETPSUB,CETPSUBQ                                                 
         MVC   CETPCPY,ETYKCPY     COMPANY CODE                                 
         MVC   CETPCRED,AP.LIDDATA CREDITOR ACCOUNT                             
         OC    CETPCRED,SPACES     MAKE UPPER CASE                              
         MVC   CETPETYP,ETYKCODE   EXPENDITURE CODE                             
         MVC   CETPOFFC,ETYKOFFC   OFFICE                                       
         MVC   CETPSEQ,ETYKSEQ     SEQUENCE NUMBER                              
         XC    CETPDA,CETPDA                                                    
         MVC   CETPSTA,ETYRSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     ECAT12                                                           
*                                                                               
ECAT16   CLI   MYBYTE,1                                                         
         BE    EXPCATX                                                          
         XC    CETPASD(ACCKLEN),CETPASD                                         
         MVI   CETPTYP,CETPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   CETPSUB,CETPSUBQ                                                 
         MVC   CETPCPY,ETYKCPY     COMPANY CODE                                 
         OC    CETPCRED,SPACES     MAKE UPPER CASE                              
         MVC   CETPETYP,ETYKCODE   EXPENDITURE CODE                             
         MVC   CETPOFFC,ETYKOFFC   OFFICE                                       
         MVC   CETPSEQ,ETYKSEQ     SEQUENCE NUMBER                              
         XC    CETPDA,CETPDA                                                    
         MVC   CETPSTA,ETYRSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
EXPCATX  DS    0H                                                               
         DROP  AP,R2,R3,RF                                                      
         EJECT                                                                  
***********************************************************************         
* EXPENSE CLAIM - CREATE STATUS AND NUMBER PASSIVE POINTERS           *         
***********************************************************************         
         SPACE 1                                                                
         USING EXCRECD,R2                                                       
EXPREC   CLI   RECTYPE,ACRTEXPC    EXPENSE CLAIM RECORD                         
         BNE   EXPRECX                                                          
                                                                                
                                                                                
*        CLI   EXCKTYPE,EXCPTLQ    ONLY FOR 'LIVE' EXPENSE CLAIMS               
*        BNE   EXPREC96                                                         
                                                                                
         XC    SAVLMSTA,SAVLMSTA                                                
         LA    R0,CLIPTAB                                                       
         LHI   R1,CLIPLN                                                        
         XR    RF,RF                                                            
         XR    RE,RE                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    RE,EXCRFST                                                       
         USING CLDELD,RE                                                        
EXPREC02 CLI   CLDEL,0                                                          
         BE    EXPREC56                                                         
         CLI   EXCKTYPE,EXCPTLQ    'LIVE' EXPENSE CLAIMS                        
         BNE   *+12                                                             
         CLI   CLDEL,CLDELQ                                                     
         BE    EXPREC06                                                         
         CLI   CLDEL,CIDELQ                                                     
         BE    EXPREC16                                                         
EXPREC04 XR    R0,R0               LOOP THROUGH CLAIM RECORDS                   
         IC    R0,CLDLN                                                         
         AR    RE,R0                                                            
         B     EXPREC02                                                         
                                                                                
EXPREC06 CLI   CLDTYPE,CLDLMAPQ                                                 
         BNE   EXPREC10                                                         
         CLI   CLDLN,CLDL1LNQ                                                   
         BNH   EXPREC04                                                         
         XR    R4,R4                                                            
         ICM   R4,1,CLDLNUM                                                     
         BZ    EXPREC04                                                         
         LA    RF,CLDLNTRY                                                      
EN       USING CLDLNTRY,RF                                                      
         LHI   R0,EXSPLMN1                                                      
         USING EXSPASD,R3                                                       
EXPREC08 TM    EXCRSTA2,EXCKCLAQ   SKIP THIS PASSIVE IF APPROVER CHOSEN         
         BNZ   EXPREC9A                                                         
         XC    EXSPASD(ACCKLEN),EXSPASD                                         
         MVI   EXSPTYP,EXSPTYPQ                                                 
         MVI   EXSPSUB,EXSPSUBQ                                                 
         MVC   EXSPCPY,EXCKCPY                                                  
         STC   R0,EXSPCAT                                                       
         MVI   EXSPKYST,EXCSCOMP                                                
         TM    EXCRSTAT,EXCSCOMP   IS IT POSTED                                 
         BNZ   EXPREC09            YES                                          
         MVI   EXSPKYST,EXCSFNTA   SET APPROVED IF APPROVED                     
         TM    EN.CLDLSTAT,CIDAAPP                                              
         BNZ   EXPREC09                                                         
         MVI   EXSPKYST,EXCSSUBM   NOT APPROVED - SET AS AWAITING               
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   *+10                                                             
         MVC   EXSPKYST,EXCRSTAT                                                
EXPREC09 MVC   SAVLMSTA,EXSPKYST                                                
         MVC   EXSPDATE,EXCKDATE                                                
         MVC   EXSP1RAC,EXCK1RAC                                                
         MVC   EXSPREF,EXCKREF                                                  
         MVC   EXSPSEQ,EXCKSEQ                                                  
         XC    EXSPDA,EXSPDA                                                    
         MVC   EXSPSTA,EXCRSTA     COPY STATUS                                  
         B     EXPREC9B                                                         
                                                                                
         USING PIDRECD,R3                                                       
EXPREC9A XC    PIDKEY,PIDKEY                                                    
         MVI   PIDKTYP,PIDKTYPQ                                                 
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,EXCKCPY                                                  
         MVC   PIDKPID,EN.CLDLAPP                                               
         MVI   PIDKSTYP,PIDKXAPQ                                                
         MVC   PIDKXCLM,EXCKREF                                                 
         MVC   PIDKXDTE,EXCKDATE                                                
         MVC   PIDKSEQ,EXCKSEQ                                                  
         XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,EXCRSTA     COPY STATUS                                  
         MVC   PIDKXOFF,SPACES                                                  
         LA    R1,0                                                             
         TM    CPYSTA4,CPYSOFF2    2 CHARACTER OFFICES                          
         JZ    *+8                                                              
         LA    R1,1                                                             
         MVC   PIDKXOFF(0),EXCK1RAC                                             
         EX    R1,*-6                                                           
         MVI   PIDKXSTA,EXCSCOMP                                                
         TM    EXCRSTAT,EXCSCOMP   IS IT POSTED                                 
         BNZ   EXPREC9B            YES                                          
         MVI   PIDKXSTA,EXCSFNTA   SET APPROVED IF APPROVED                     
         TM    EN.CLDLSTAT,CIDAAPP                                              
         BNZ   EXPREC9B                                                         
         MVI   PIDKXSTA,EXCSSUBM   NOT APPROVED - SET AS AWAITING               
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   *+10                                                             
         MVC   PIDKXSTA,EXCRSTAT                                                
EXPREC9B LA    R3,ACCKLEN(R3)                                                   
         LA    RF,CLDLLNQ(RF)                                                   
         MVI   0(R3),EOL                                                        
         AHI   R0,1                                                             
         BCT   R4,EXPREC08                                                      
         B     EXPREC04                                                         
         DROP  EN                                                               
                                                                                
EXPREC10 CLI   CLDTYPE,CLDFNAPQ                                                 
         BNE   EXPREC04                                                         
         CLI   CLDLN,CLDL1LNQ                                                   
         BNH   EXPREC04                                                         
         LLC   R4,CLDFNUM                                                       
         LTR   R4,R4                                                            
         BZ    EXPREC04                                                         
         LA    RF,CLDFNTRY                                                      
EN       USING CLDFNTRY,RF                                                      
         LHI   R0,EXSPLFN1                                                      
         USING EXSPASD,R3                                                       
EXPREC12 XC    EXSPASD(ACCKLEN),EXSPASD                                         
         MVI   EXSPTYP,EXSPTYPQ                                                 
         MVI   EXSPSUB,EXSPSUBQ                                                 
         MVC   EXSPCPY,EXCKCPY                                                  
         STC   R0,EXSPCAT                                                       
         MVI   EXSPKYST,EXCSCOMP                                                
         TM    EN.CLDFSTAT,CIDAAPP                                              
         BNZ   EXPREC14                                                         
         MVI   EXSPKYST,EXCSFNTA                                                
         TM    EXCRSTAT,EXCSFNTA                                                
         BNZ   EXPREC14                                                         
         MVI   EXSPKYST,EXCSSUBM                                                
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   *+10                                                             
         MVC   EXSPKYST,EXCRSTAT                                                
EXPREC14 MVC   EXSPDATE,EXCKDATE                                                
         MVC   EXSP1RAC,EXCK1RAC                                                
         MVC   EXSPREF,EXCKREF                                                  
         MVC   EXSPSEQ,EXCKSEQ                                                  
         XC    EXSPDA,EXSPDA                                                    
         MVC   EXSPSTA,EXCRSTA     COPY STATUS                                  
         AHI   R0,1                                                             
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         LA    RF,L'CLDFNTRY(RF)                                                
         BCT   R4,EXPREC12                                                      
         B     EXPREC04                                                         
                                                                                
         USING CIDELD,RE                                                        
EXPREC16 CLI   CIDTYPE,CIDTYMQ     MAIN ELEMENT?                                
         BNE   EXPREC04                                                         
         CLI   CIDMTYP,C'A'        SKIP ADVANCES                                
         BE    EXPREC04                                                         
         TM    CIDMSTA,CIDMSID     OR ANY DELETED LINES                         
         BNZ   EXPREC04                                                         
         MVC   SAVSEQ,CIDSEQ                                                    
         MVC   SAVETYPE,CIDMTYP    SAVE WHETHER BILL OR NON BILLABLE            
         LA    R4,CIDEL                                                         
ACT      USING CIDEL,R4                                                         
EXPREC18 CLI   ACT.CIDEL,0                                                      
         BE    EXPREC04                                                         
         CLI   ACT.CIDEL,CIDELQ                                                 
         BNE   EXPREC20                                                         
         CLI   ACT.CIDTYPE,CIDTYCQ                                              
         BNE   EXPREC20                                                         
         CLC   SAVSEQ,ACT.CIDSEQ   ARE WE WITHIN SAME BLOCK OF CIDELS           
         BE    EXPREC22            YES                                          
EXPREC20 SR    R0,R0                                                            
         IC    R0,ACT.CIDLN                                                     
         AR    R4,R0                                                            
         B     EXPREC18                                                         
                                                                                
EXPREC22 CLC   ACT.CIDCCPJ,SPACES      ANY CLI/PRO/JOB SET?                     
         BH    EXPREC24                                                         
         TM    EXCRSTA2,EXCKWPPQ       WPP WORKFLOW                             
         BZ    EXPREC04                                                         
         MVC   SAVEACC,EXCK1RAC                                                 
         XC    SAVEACT,SAVEACT         IF NO SJ ACCOUNT SAVE 1R ACC             
         B     EXPREC26                                                         
EXPREC24 MVC   SAVEACC,ACT.CIDCCPJ     SAVE SJ ACCOUNT                          
         MVC   SAVEACT,ACT.CIDCCPJ     SAVE SJ ACCOUNT                          
         MVC   SAVEMED,ACT.CIDCMED     SAVE MEDIA CODE                          
         MVC   SAVEOFFC,ACT.CIDCOFF    SAVE OFFICE                              
EXPREC26 LA    R4,CIDELD                                                        
         DROP  ACT                                                              
                                                                                
         MVI   MYBYTE,0                                                         
APP      USING CIDEL,R4                                                         
EXPREC28 CLI   APP.CIDEL,0                                                      
         BNE   EXPREC30                                                         
         CLI   MYBYTE,0            DID WE FIND AN APPROVER                      
         BNE   EXPREC04            YES                                          
         B     EXPREC46            NO - NEED TO ADD ACCOUNT TO TABLE            
EXPREC30 CLI   APP.CIDEL,CIDELQ                                                 
         BNE   EXPREC32                                                         
         CLI   APP.CIDTYPE,CIDTYAQ                                              
         BNE   EXPREC32                                                         
         CLC   SAVSEQ,APP.CIDSEQ   ARE WE WITHIN SAME BLOCK OF CIDELS           
         BE    EXPREC33            YES                                          
EXPREC32 SR    R0,R0                                                            
         IC    R0,APP.CIDLN                                                     
         AR    R4,R0                                                            
         B     EXPREC28                                                         
                                                                                
EXPREC33 LLC   R5,APP.CIDANUM                                                   
         LTR   R5,R5                                                            
         BNZ   EXPREC34                                                         
         CLI   APP.CIDLN,CIDALN1Q+CIDALNQ                                       
         BNE   EXPREC46                                                         
         LHI   R5,1                                                             
EXPREC34 MVI   MYBYTE,1            SET FOUND APPROVERS                          
         LA    RF,APP.CIDANTRY                                                  
         DROP  APP                                                              
                                                                                
EN       USING CIDANTRY,RF                                                      
         LHI   R6,1                SET INITIAL LEVEL R6=LEVEL                   
EXPREC35 LA    R1,CLIPTAB                                                       
         LHI   R0,MAXCLIS                                                       
         USING CLIPTABD,R1                                                      
         TM    EN.CIDASTAT,CIDA1RL IS THE APPROVER FROM 1R                      
         BZ    EXPREC36                                                         
         MVC   SAVEACC,EXCK1RAC    CHANGE SJ ACCOUNT TO 1R ACC                  
EXPREC36 OC    CLIPACC,CLIPACC                                                  
         BZ    EXPREC42                                                         
         CLC   CLIPACC+2(L'SAVEACC),SAVEACC IS THE ACCOUNT THE SAME             
         BNE   EXPREC38            NO                                           
         CLC   CLIPACT,SAVEACT     IS THE ACCOUNT THE SAME                      
         BNE   EXPREC38            NO                                           
         CLC   CLIPOFF,SAVEOFFC    IS THE OFFICE THE SAME                       
         BNE   EXPREC38            NO                                           
         CLC   CLIPMED,SAVEMED     IS THE MEDIA THE SAME                        
         BNE   EXPREC38            NO                                           
         TM    EXCRSTA2,EXCKWPPQ   ONLY CHECK TYPE FOR WPP WORKFLOW             
         BZ    *+14                                                             
         CLC   CLIPTYP,SAVETYPE    IS THE TYPE THE SAME                         
         BNE   EXPREC38            NO                                           
         LLC   R4,CLIPLVL                                                       
         CR    R4,R6               IS THE LEVEL THE SAME                        
         BNE   EXPREC38            NO                                           
         CLC   CLIPST,EN.CIDASTAT  IS THE STATUS THE SAME                       
         BE    EXPREC44            YES                                          
         TM    CLIPST,CIDAREJ      WHAT STATUS DO WE HAVE PREVIOUSLY            
         BNZ   EXPREC44            REJECTED - LOWEST SO DON'T CHANGE            
         TM    EN.CIDASTAT,CIDAREJ THIS ONE IS NOW REJECTED                     
         BNZ   EXPREC40            SO REPLACE PREVIOUS STATUS                   
         OC    CLIPST,CLIPST       DID WE HAVE PREVIOUS STATUS APPROVED         
         BNZ   EXPREC40            YES  - SO REPLACE WITH NOTHING               
         B     EXPREC44            NO - KEEP AS NOTHING                         
EXPREC38 LA    R1,CLIPTABL(R1)                                                  
         BCT   R0,EXPREC36                                                      
         DC    H'0'                                                             
*                                                                               
EXPREC40 MVC   CLIPST,EN.CIDASTAT                                               
         B     EXPREC44                                                         
*                                                                               
EXPREC42 MVC   CLIPACC+2(L'SAVEACC),SAVEACC                                     
         MVC   CLIPACT,SAVEACT                                                  
         MVC   CLIPOFF,SAVEOFFC                                                 
         MVC   CLIPMED,SAVEMED                                                  
         TM    EXCRSTA2,EXCKWPPQ   ONLY SAVE TYPE FOR WPP WORKFLOW              
         BZ    *+10                                                             
         MVC   CLIPTYP,SAVETYPE                                                 
         STC   R6,CLIPLVL                                                       
         MVC   CLIPPID,EN.CIDAPID                                               
         MVC   CLIPST,EN.CIDASTAT                                               
*                                                                               
EXPREC44 LA    RF,L'CIDANTRY(RF)                                                
         AHI   R6,1                                                             
         BCT   R5,EXPREC35                                                      
         B     EXPREC04                                                         
         DROP  EN                                                               
                                                                                
EXPREC46 LA    R1,CLIPTAB                                                       
         LHI   R0,MAXCLIS                                                       
         USING CLIPTABD,R1                                                      
EXPREC48 OC    CLIPACC,CLIPACC                                                  
         BZ    EXPREC52                                                         
         CLC   CLIPACC+2(L'SAVEACC),SAVEACC IS THE ACCOUNT THE SAME             
         BNE   EXPREC50            NO                                           
         CLC   CLIPACT,SAVEACT     IS THE ACCOUNT THE SAME                      
         BNE   EXPREC50            NO                                           
         CLC   CLIPOFF,SAVEOFFC    IS THE OFFICE THE SAME                       
         BNE   EXPREC50            NO                                           
         CLC   CLIPMED,SAVEMED     IS THE MEDIA THE SAME                        
         BE    EXPREC04            NO                                           
EXPREC50 LA    R1,CLIPTABL(R1)                                                  
         BCT   R0,EXPREC48                                                      
         DC    H'0'                                                             
*                                                                               
EXPREC52 MVC   CLIPACC+2(L'SAVEACC),SAVEACC                                     
         MVC   CLIPACT,SAVEACT                                                  
         MVC   CLIPOFF,SAVEOFFC                                                 
         MVC   CLIPMED,SAVEMED                                                  
         MVI   CLIPLVL,1                                                        
         B     EXPREC04                                                         
*                                                                               
EXPREC56 LA    R1,CLIPTAB                                                       
         MVI   MYBYTE2,0                                                        
         MVI   MYBYTE1,0                                                        
*                                                                               
EXPREC58 OC    CLIPACC,CLIPACC     END OF TABLE                                 
         BZ    EXPREC96            YES                                          
         LA    R4,OFFTYPE                                                       
         CLC   CLIPACT,CLIPACC+2  1R APPROVER FOR CLIENT LINE                   
         BE    EXPREC60           NO                                            
         LA    R4,ONERTYPE        YES                                           
         USING CATTYPD,R4                                                       
EXPREC60 CLI   CATLEVL,X'FF'                                                    
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   CATLEVL,CLIPLVL                                                  
         BE    EXPREC62                                                         
         LA    R4,CATTYPL(R4)                                                   
         B     EXPREC60                                                         
EXPREC62 OC    CLIPPID,CLIPPID     DO WE HAVE AN APPROVER                       
         BZ    EXPREC75            NO                                           
         USING EXSPASD,R3                                                       
         XC    EXSPASD(ACCKLEN),EXSPASD BUILD APPROVAL STATUS PASSIVE           
         MVI   EXSPTYP,EXSPTYPQ                                                 
         MVI   EXSPSUB,EXSPSUBQ                                                 
         MVC   EXSPCPY,EXCKCPY                                                  
         MVC   EXSPCAT,CATCAT                                                   
         TM    EXCRSTA2,EXCKWPPQ                                                
         BZ    EXPREC66                                                         
         MVC   EXSPCAT,CATCATNB                                                 
         CLI   CLIPTYP,C'B'                                                     
         BNE   EXPREC66                                                         
         MVC   EXSPCAT,CATCATBL                                                 
EXPREC66 MVI   EXSPKYST,EXCSCOMP   COPY STATUS FROM PRIMARY KEY                 
         TM    EXCRSTAT,EXCSCOMP   IS IT POSTED                                 
         BNZ   EXPREC68            YES - NOTHIGN ELSE TO DO                     
         MVI   EXSPKYST,EXCSFNTA   SET APPROVED                                 
         TM    CLIPST,CIDAAPP      IF IT IS APPROVED                            
         BNZ   EXPREC68                                                         
         MVI   EXSPKYST,EXSSSUBM   OTHERWISE AWAITING APPROVAL                  
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP  IF IT'S SUBMITTED OR PART AP         
         BNZ   *+10                                                             
         MVC   EXSPKYST,EXCRSTAT   TAKE REJECTED                                
EXPREC68 MVC   EXSPDATE,EXCKDATE                                                
         CLC   CLIPACT,CLIPACC+2  1R APPROVER FOR CLIENT LINE                   
         BE    EXPREC69           NO                                            
         MVC   SAVLMSTA,EXSPKYST  YES - SAVE 1R APPROVAL STATUS                 
EXPREC69 MVC   EXSP1RAC,EXCK1RAC                                                
         CLC   CLIPACT,CLIPACC+2  1R APPROVER FOR CLIENT LINE                   
         BE    EXPREC72           NO                                            
         CLI   CLIPLVL,1                                                        
         BNE   EXPREC70                                                         
         CLI   MYBYTE1,1          CHECKING IF WE HAVE PUT PASSIVE               
         BE    EXPREC94                                     ALREADY             
         MVI   MYBYTE1,1                                                        
         B     EXPREC73                                                         
EXPREC70 CLI   MYBYTE2,1          CHECKING IF WE HAVE PUT PASSIVE               
         BE    EXPREC94                                     ALREADY             
         MVI   MYBYTE2,1                                                        
         B     EXPREC73                                                         
                                                                                
EXPREC72 MVC   EXSPSJAC,CLIPACT                                                 
         MVC   EXSPCOFF,CLIPOFF                                                 
         MVC   EXSPMED,CLIPMED                                                  
                                                                                
EXPREC73 MVC   EXSPREF,EXCKREF                                                  
         MVC   EXSPSEQ,EXCKSEQ                                                  
         XC    EXSPDA,EXSPDA                                                    
         MVC   EXSPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         CLC   CLIPACT,CLIPACC+2  1R APPROVER FOR CLIENT LINE                   
         BE    EXPREC75           NO                                            
         USING EXDPASD,R3                                                       
         XC    EXDPASD(ACCKLEN),EXDPASD                                         
         MVI   EXDPTYP,EXDPTYPQ                                                 
         MVI   EXDPSUB,EXDPSUBQ                                                 
         MVC   EXDPCPY,EXCKCPY                                                  
         MVI   EXDPCLS,EXDPCODQ   OFFICE/DATE PASSIVE                           
         MVC   EXDPCAT,CATCAD                                                   
         TM    EXCRSTA2,EXCKWPPQ                                                
         BZ    EXPREC74                                                         
         MVC   EXDPCAT,CATCADNB                                                 
         CLI   CLIPTYP,C'B'                                                     
         BNE   EXPREC74                                                         
         MVC   EXDPCAT,CATCADBL                                                 
                                                                                
EXPREC74 MVC   EXDP1RAC,EXCK1RAC                                                
         MVC   EXDPDATE,EXCKDATE                                                
         MVC   EXDPNUM,EXCKREF                                                  
         MVC   EXDPSEQ,EXCKSEQ                                                  
         MVC   EXDPKSTA,SAVLMSTA                                                
         XC    EXDPDA,EXDPDA                                                    
         MVC   EXDPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         USING EXJPASD,R3                                                       
EXPREC75 OC    CLIPACT,CLIPACT     DO WE HAVE AN ACCOUNT                        
         BZ    EXPREC94                                                         
         XC    EXJPASD(ACCKLEN),EXJPASD                                         
         MVI   EXJPTYP,EXJPTYPQ                                                 
         MVI   EXJPSUB,EXJPSUBQ                                                 
         MVC   EXJPCPY,EXCKCPY                                                  
                                                                                
EXPREC7A MVC   EXJPVIEW,CATCAD                                                  
         TM    EXCRSTA2,EXCKWPPQ                                                
         BZ    EXPREC77                                                         
         MVC   EXJPVIEW,CATCADNB                                                
         CLI   CLIPTYP,C'B'                                                     
         BNE   EXPREC77                                                         
         MVC   EXJPVIEW,CATCADBL                                                
                                                                                
EXPREC77 MVC   EXJPCPJ,CLIPACT                                                  
         MVC   EXJPCOFF,CLIPOFF                                                 
         MVC   EXJPMED,CLIPMED                                                  
         MVC   EXJPDATE,EXCKDATE                                                
         MVC   EXJPPID,EXCKPIDB                                                 
         MVC   EXJPNUM,EXCKREF                                                  
         MVC   EXJPSEQ,EXCKSEQ                                                  
         MVI   EXJPKSTA,EXCSCOMP                                                
         TM    EXCRSTAT,EXCSCOMP   IS EXPENSE CLAIM POSTED                      
         BNZ   EXPREC78            YES                                          
         MVI   EXJPKSTA,EXCSFNTA   SET AS APPROVED                              
         TM    CLIPST,CIDAAPP      IF APPROVED                                  
         BNZ   EXPREC78                                                         
         MVI   EXJPKSTA,EXSSSUBM   OTHERWISE AWAITING APPROVAL                  
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   *+10                                                             
         MVC   EXJPKSTA,EXCRSTAT                                                
EXPREC78 OC    CLIPPID,CLIPPID     DO WE HAVE AN APPROVER                       
         BZ    EXPREC79            NO                                           
         CLC   CLIPACT,CLIPACC+2   1R APPROVER FOR CLIENT LINE                  
         BE    EXPREC80            NO                                           
EXPREC79 MVI   EXJPSRC,C'Y'        USE FOR SEARCH ONLY                          
EXPREC80 XC    EXJPDA,EXJPDA                                                    
         MVC   EXJPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         OC    CLIPPID,CLIPPID     DO WE HAVE AN APPROVER                       
         BZ    EXPREC94            NO                                           
         CLC   CLIPACT,CLIPACC+2   1R APPROVER FOR CLIENT LINE                  
         BNE   EXPREC94            YES                                          
         CLI   CLIPMED,X'40'       IF NO MEDIA PRESENT DON'T BUILD              
         BNH   EXPREC94                            MEDIA VIEW                   
                                                                                
         USING EXSPASD,R3                                                       
         XC    EXSPASD(ACCKLEN),EXSPASD BUILD APPROVAL STATUS PASSIVE           
         MVI   EXSPTYP,EXSPTYPQ                                                 
         MVI   EXSPSUB,EXSPSUBQ                                                 
         MVC   EXSPCPY,EXCKCPY                                                  
         LA    R4,MEDTYPE                                                       
         USING CATTYPD,R4                                                       
EXPREC82 CLI   CATLEVL,X'FF'                                                    
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   CATLEVL,CLIPLVL                                                  
         BE    EXPREC84                                                         
         LA    R4,CATTYPL(R4)                                                   
         B     EXPREC82                                                         
                                                                                
EXPREC84 MVC   EXSPCAT,CATCAT                                                   
         TM    EXCRSTA2,EXCKWPPQ                                                
         BZ    EXPREC86                                                         
         MVC   EXSPCAT,CATCATNB                                                 
         CLI   CLIPTYP,C'B'                                                     
         BNE   EXPREC86                                                         
         MVC   EXSPCAT,CATCATBL                                                 
EXPREC86 MVI   EXSPKYST,EXCSCOMP                                                
         TM    EXCRSTAT,EXCSCOMP                                                
         BNZ   EXPREC88                                                         
         MVI   EXSPKYST,EXSSFNTA                                                
         TM    CLIPST,CIDAAPP                                                   
         BNZ   EXPREC88                                                         
         MVI   EXSPKYST,EXSSSUBM                                                
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   *+10                                                             
         MVC   EXSPKYST,EXCRSTAT                                                
EXPREC88 MVC   EXSPDATE,EXCKDATE                                                
                                                                                
         MVC   EXSPMSJA,CLIPACT                                                 
         MVC   EXSPMOFF,CLIPOFF                                                 
         MVC   EXSPMMED,CLIPMED                                                 
         MVC   EXSPREF,EXCKREF                                                  
         MVC   EXSPSEQ,EXCKSEQ                                                  
         XC    EXSPDA,EXSPDA                                                    
         MVC   EXSPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         USING EXJPASD,R3                                                       
         XC    EXJPASD(ACCKLEN),EXJPASD                                         
         MVI   EXJPTYP,EXJPTYPQ                                                 
         MVI   EXJPSUB,EXJPSUBQ                                                 
         MVC   EXJPCPY,EXCKCPY                                                  
         MVC   EXJPVIEW,CATCAD                                                  
         TM    EXCRSTA2,EXCKWPPQ                                                
         BZ    EXPREC90                                                         
         MVC   EXJPVIEW,CATCADNB                                                
         CLI   CLIPTYP,C'B'                                                     
         BNE   EXPREC90                                                         
         MVC   EXJPVIEW,CATCADBL                                                
                                                                                
EXPREC90 MVC   EXJPMCPJ,CLIPACT                                                 
         MVC   EXJPMMED,CLIPMED                                                 
         MVC   EXJPMOFF,CLIPOFF                                                 
         MVC   EXJPDATE,EXCKDATE                                                
         MVC   EXJPPID,EXCKPIDB                                                 
         MVC   EXJPNUM,EXCKREF                                                  
         MVC   EXJPSEQ,EXCKSEQ                                                  
         MVI   EXJPKSTA,EXCSCOMP                                                
         TM    EXCRSTAT,EXCSCOMP   IS EXPENSE CLAIM POSTED                      
         BNZ   EXPREC92            YES                                          
         MVI   EXJPKSTA,EXCSFNTA   SET AS APPROVED                              
         TM    CLIPST,CIDAAPP      IF APPROVED                                  
         BNZ   EXPREC92                                                         
         MVI   EXJPKSTA,EXSSSUBM   OTHERWISE AWAITING APPROVAL                  
         TM    EXCRSTAT,EXCSSUBM+EXCSPAPP                                       
         BNZ   EXPREC92                                                         
         MVC   EXJPKSTA,EXCRSTAT                                                
EXPREC92 XC    EXJPDA,EXJPDA                                                    
         XC    EXJPDA,EXJPDA                                                    
         MVC   EXJPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
                                                                                
EXPREC94 MVI   0(R3),EOL                                                        
         LA    R1,CLIPTABL(R1)                                                  
         B     EXPREC58                                                         
*                                                                               
         USING EXDPASD,R3                                                       
*XPREC96 CLI   EXCKTYPE,EXCPTLQ    ONLY FOR 'LIVE' EXPENSE CLAIMS               
*        BNE   EXPREC98                                                         
EXPREC96 XC    EXDPASD(ACCKLEN),EXDPASD                                         
         MVI   EXDPTYP,EXDPTYPQ                                                 
         MVI   EXDPSUB,EXDPSUBQ                                                 
         MVC   EXDPCPY,EXCKCPY                                                  
         MVI   EXDPCLS,EXDPCODQ    OFFICE/DATE PASSIVE                          
         MVC   EXDP1RAC,EXCK1RAC                                                
         MVC   EXDPDATE,EXCKDATE                                                
         MVC   EXDPNUM,EXCKREF                                                  
         MVC   EXDPSEQ,EXCKSEQ                                                  
         MVC   EXDPKSTA,SAVLMSTA                                                
         XC    EXDPDA,EXDPDA                                                    
         MVC   EXDPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         XC    EXDPASD(ACCKLEN),EXDPASD                                         
         MVI   EXDPTYP,EXDPTYPQ                                                 
         MVI   EXDPSUB,EXDPSUBQ                                                 
         MVC   EXDPCPY,EXCKCPY                                                  
         MVI   EXDPCLS,EXDPCDOQ    DATE/OFFICE PASSIVE                          
         MVC   EXDPFDAT,EXCKDATE                                                
         MVC   EXDPF1RA,EXCK1RAC                                                
         MVC   EXDPNUM,EXCKREF                                                  
         MVC   EXDPSEQ,EXCKSEQ                                                  
         MVC   EXDPKSTA,SAVLMSTA                                                
         XC    EXDPDA,EXDPDA                                                    
         MVC   EXDPSTA,EXCRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         USING EXNPASD,R3                                                       
EXPREC98 XC    EXNPASD(ACCKLEN),EXNPASD                                         
         MVI   EXNPTYP,EXNPTYPQ                                                 
         MVI   EXNPSUB,EXNPSUBQ                                                 
         MVC   EXNPCPY,EXCKCPY                                                  
         MVC   EXNPTYPE,EXCKTYPE                                                
         MVC   EXNPNUM,EXCKREF                                                  
         MVC   EXNPSEQ,EXCKSEQ                                                  
         XC    EXNPDA,EXNPDA                                                    
         XC    EXNPSTA,EXNPSTA     BUILD OWN STATUS                             
         MVC   EXNPSTAT,EXCRSTAT                                                
         MVC   EXNPOFFC,EXCK1RAC                                                
         MVC   EXNPDATE,EXCKDATE                                                
         MVC   EXNPXPID,EXCKPIDB                                                
         MVC   EXNPSTA2,EXCRSTA2                                                
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
EXPRECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* MCS ESTIMATE RECORD - CREATE GLOBAL NOMBER AND PID PASSIVES         *         
***********************************************************************         
         SPACE 1                                                                
         USING ESTRECD,R2                                                       
ESTREC   CLI   RECTYPE,ACRTESTR    MCS ESTIMATE RECORD                          
         BNE   ESTRECX                                                          
                                                                                
         CLI   ESTKSEQ,ESTKSMQ     TEST MAIN RECORD                             
         BNE   ESTREC10            NO - BUILD BILL PASSIVE POINTERS             
                                                                                
         XC    SVESTSTA,SVESTSTA                                                
         TM    ESTRSTA2,ESTKBILA   TEST IF IT HAS BILL ACTIVITIES               
         BZ    ESTREC1                                                          
         MVC   SVESTSTA,ESTRSTA          AND STATUS                             
                                                                                
         USING EMDELD,R6                                                        
ESTREC1  LA    R6,ESTRFST          GET MAIN DATA ELEMENT                        
         XR    R0,R0                                                            
                                                                                
ESTREC1A CLI   EMDEL,0                                                          
         BE    ESTRECX                                                          
         CLI   EMDEL,EMDELQ                                                     
         BE    ESTREC3                                                          
         CLI   EMDEL,PIDELQ                                                     
         BE    ESTREC6                                                          
                                                                                
ESTREC2  IC    R0,EMDLN                                                         
         AR    R6,R0                                                            
         B     ESTREC1A                                                         
                                                                                
         USING EGNPASD,R3                                                       
ESTREC3  XC    EGNPASD(ACCKLEN),EGNPASD                                         
         MVI   EGNPTYP,EGNPTYPQ                                                 
         MVI   EGNPSUB,EGNPSUBQ                                                 
         MVC   EGNPCPY,ESTKCPY                                                  
         MVC   EGNPNUM,EMDGNO                                                   
         MVC   EGNPCLI,ESTKCLI                                                  
         MVC   EGNPPRO,ESTKPRO                                                  
         MVC   EGNPJOB,ESTKJOB                                                  
         MVC   EGNPLNO,ESTKLNO                                                  
         XC    EGNPDA,EGNPDA                                                    
         MVC   EGNPSTA,ESTRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
*        OC    CUTOF2,CUTOF2      INITIALISED?                                  
*        BNZ   ESTREC4                                                          
*&&UK                                                                           
         MVC   TODAY,=X'FAF6F0F7F0F1'                                           
         CLI   ISITFQA,1                                                        
         BE    ESTREC3A                                                         
*&&                                                                             
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)                                     
ESTREC3A GOTO1 VADDAY,(R1),(C'Y',TODAY),DUB,F'-1'                               
         GOTO1 VDATCON,(R1),(0,DUB),(1,CUTOF2)                                  
                                                                                
ESTREC4  CLC   EMDLDT,CUTOF2                                                    
         BL    ESTREC2                                                          
                                                                                
         USING EADPASD,R3                                                       
         XC    EADPASD(ACCKLEN),EADPASD                                         
         MVI   EADPTYP,EADPTYPQ                                                 
         MVI   EADPSUB,EADPSUBQ                                                 
         MVC   EADPCPY,ESTKCPY                                                  
         MVC   EADPOFF,ESTRSOFF                                                 
         XR    R1,R1                                                            
         ICM   R1,7,EMDLDT                                                      
         LNR   R1,R1                                                            
         STCM  R1,7,EADPDAT                                                     
         XR    R1,R1                                                            
         ICM   R1,7,EMDLTI                                                      
         LNR   R1,R1                                                            
         STCM  R1,7,EADPTIM                                                     
         MVC   EADPNUM,EMDGNO                                                   
         XC    EADPDA,EADPDA                                                    
         MVC   EADPSTA,ESTRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         CLC   ESTRSOFF,SPACES                                                  
         BNH   ESTREC2                                                          
                                                                                
         XC    EADPASD(ACCKLEN),EADPASD                                         
         MVI   EADPTYP,EADPTYPQ                                                 
         MVI   EADPSUB,EADPSUBQ                                                 
         MVC   EADPCPY,ESTKCPY                                                  
         MVC   EADPOFF,SPACES                                                   
         XR    R1,R1                                                            
         ICM   R1,7,EMDLDT                                                      
         LNR   R1,R1                                                            
         STCM  R1,7,EADPDAT                                                     
         XR    R1,R1                                                            
         ICM   R1,7,EMDLTI                                                      
         LNR   R1,R1                                                            
         STCM  R1,7,EADPTIM                                                     
         MVC   EADPNUM,EMDGNO                                                   
         XC    EADPDA,EADPDA                                                    
         MVC   EADPSTA,ESTRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     ESTREC2                                                          
                                                                                
         USING PIDELD,R6                                                        
ESTREC6  CLI   PIDTYPE,PIDTOWN     LOOK FOR OWNER'S ELEMENTS                    
         BNE   ESTREC2                                                          
         XR    R1,R1                                                            
         IC    R1,PIDNTR#          GET NUMBER OF ENTRIES                        
         LA    RE,PIDOWNR          AND POINT TO FIRST OWNER                     
                                                                                
         USING PIDRECD,R3                                                       
ESTREC7  XC    PIDKEY(ACCKLEN),PIDKEY                                           
         MVI   PIDKTYP,PIDKTYPQ                                                 
         MVI   PIDKSUB,PIDKSUBQ                                                 
         MVC   PIDKCPY,ESTKCPY                                                  
         MVC   PIDKPID,0(RE)                                                    
         MVI   PIDKSTYP,PIDKESTQ                                                
         MVC   PIDKESTA,ESTRSTA1                                                
         MVC   PIDKECLI,ESTKCLI                                                 
         MVC   PIDKEPRO,ESTKPRO                                                 
         MVC   PIDKEJOB,ESTKJOB                                                 
         MVC   PIDKELNO,ESTKLNO                                                 
         XC    PIDKDA,PIDKDA                                                    
         MVC   PIDKSTA,ESTRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         AHI   RE,2                                                             
         BCT   R1,ESTREC7                                                       
                                                                                
         B     ESTREC2                                                          
                                                                                
***********************************************************************         
* GENERATE BILL PASSIVE POINTERS FOR ESTIMATE RECORD                  *         
***********************************************************************         
         SPACE 1                                                                
ESTREC10 DS   0H                                                                
*&&UK                                                                           
         TM    SVESTST2,ESTKBILA   TEST IF IT HAS BILL ACTIVITIES               
         BZ    ESTRECX             NO - OK                                      
         ST    R3,SVESTPAS         SAVE 1ST BILL PASSIVE POINTERS               
*                                                                               
         MVC   SVESTCPJ,SPACES                                                  
         MVC   SVESTCPJ(L'ESTKCLI),ESTKCLI                                      
         LA    R6,SVESTCPJ+3                                                    
         LHI   R1,L'ESTKJOB-1                                                   
         CLC   ESTKCLI+3(2),SPACES                                              
         BE    *+12                                                             
         LA    R6,2(,R6)           SJ STRUCTURE IS 5,2,5                        
         LHI   R1,4                                                             
*                                                                               
         MVC   0(L'ESTKPRO,R6),ESTKPRO                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   L'ESTKPRO(0,R6),ESTKJOB                                          
*                                                                               
         LA    R6,ESTRFST          GET PRODUCTION DATA ELEMENT                  
         XR    R0,R0                                                            
         USING PTAELD,R6                                                        
         B     *+10                                                             
ESTREC12 IC    R0,PTALN                                                         
         AR    R6,R0                                                            
*                                                                               
         CLI   PTAEL,0                                                          
         BE    ESTRECX                                                          
         CLI   PTAEL,PTAELQ                                                     
         BNE   ESTREC12                                                         
         CLI   PTATYPE,PTATRAL     TEST ALLOCATION                              
         BNE   ESTREC12                                                         
         OC    PTADATE,PTADATE     TEST EMPTY PTA ELEMENT                       
         BZ    ESTREC12            YES - GET NEXT                               
         TM    PTASTAT1,PTASPEND   TEST UPDATED BILLING                         
         BO    ESTREC12                                                         
*        TM    PTASTAT1,PTASREVS+PTASREVU                                       
*        BNZ   ESTREC12                                                         
         OC    PTARBLNO,PTARBLNO   IGNORE IF NO BILL NUMBER                     
         BZ    ESTREC12                                                         
*                                                                               
         L     RF,SVESTPAS         1ST BILL PASSIVE POINTERS                    
S        USING BDPPASD,RF                                                       
ESTREC14 CLI   0(RF),EOL                                                        
         BE    ESTREC18                                                         
         CLI   S.BDPPTYP,BDPPTYPQ  TEST BILL PASSIVE POINTERS                   
         BNE   ESTREC16                                                         
         CLC   S.BDPPBIL#,PTARBLNO                                              
         BNE   ESTREC16                                                         
         CLC   S.BDPPBILD,PTARBLDT                                              
         BE    ESTREC12            GET NEXT IF BILL PASSIVE ADDED               
*                                                                               
ESTREC16 LA    RF,ACCKLEN(RF)                                                   
         B     ESTREC14                                                         
         DROP  S                                                                
*                                                                               
         USING BDPPASD,R3                                                       
ESTREC18 XC    BDPPKEY,BDPPKEY                                                  
         MVI   BDPPTYP,BDPPTYPQ                                                 
         MVC   BDPPCPY,ESTKCPY                                                  
         MVI   BDPPSYS,BDPPACCQ                                                 
         MVC   BDPPOFF,SVESTOFF                                                 
*                                                                               
         MVC   BDPPJOB,SVESTCPJ    JOB CODE                                     
         MVC   BDPPBILM,PTAMOA                                                  
         MVC   BDPPBIL#,PTARBLNO                                                
         MVC   BDPPBILD,PTARBLDT                                                
         MVI   BDPPRTYP,BDPPRWDR                                                
         MVC   BDPPDA,DA           SET CURRENT DA TO AVOID DUP KEYS             
         MVI   BDPPEST,BDPPESTQ    RECORD IS ESTIMATE BILL                      
         MVC   BDPPKSTA(L'SVESTSTA),SVESTSTA                                    
         LA    R3,ACCKLEN(R3)      BUMP TO NEXT KEY AND SET E-O-L               
         MVI   0(R3),EOL                                                        
         B     ESTREC12            BUMP TO NEXT ELEMENT                         
*&&                                                                             
ESTRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
         EJECT                                                                  
***********************************************************************         
* BRANDOCEAN FORMAT CODE RECORD - CREATE PASSIVES                     *         
***********************************************************************         
         SPACE 1                                                                
         USING ERFRECD,R2                                                       
FORREC   CLI   RECTYPE,ACRTERFR    BRANDOCEAN FORMAT CODE RECORD                
         BNE   FORRECX                                                          
         CLI   ERFKAPPL,ERFKORDQ                                                
         BNE   LDCPX                                                            
                                                                                
         LA    R0,XSUPPLS          CLEAR AND INI TABLES                         
         LHI   R1,XSUPPLQ                                                       
         XR    RF,RF                                                            
         XR    RE,RE                                                            
         MVCL  R0,RE                                                            
         XC    XOTYPES(8),XOTYPES                                               
         XC    XETYPES(128),XETYPES                                             
         MVC   XOTYPES,XFFS                                                     
         MVC   XETYPES,XFFS                                                     
         MVC   XSUPPLS,XFFS                                                     
         USING LIDELD,R6                                                        
         LA    R6,ERFRFST          GET LID ELEMENTS AND BUILD TABLE             
         XR    R0,R0                                                            
         LA    RE,XOTYPES                                                       
         LA    RF,XETYPES                                                       
         LA    R1,XSUPPLS                                                       
                                                                                
FORREC05 CLI   LIDEL,0                                                          
         BE    FORREC40                                                         
         CLI   LIDEL,LIDELQ                                                     
         BE    FORREC15                                                         
                                                                                
FORREC10 IC    R0,LIDLN                                                         
         AR    R6,R0                                                            
         B     FORREC05                                                         
                                                                                
FORREC15 XR    R4,R4                                                            
         IC    R4,LIDLN                                                         
         SHI   R4,LIDDATA-LIDELD                                                
         LTR   R4,R4                                                            
         BNP   FORREC10                                                         
         SHI   R4,1                                                             
         CLI   LIDTYPE,LIDTSUPP                                                 
         BE    FORREC25                                                         
         CLI   LIDTYPE,LIDTEXPD                                                 
         BE    FORREC20                                                         
         CLI   LIDTYPE,LIDTOTYP                                                 
         BNE   FORREC10                                                         
                                                                                
         MVC   0(0,RE),LIDDATA     SAVE ORDER TYPES                             
         EX    R4,*-6                                                           
         LA    RE,1(R4,RE)                                                      
         B     FORREC10                                                         
                                                                                
FORREC20 MVC   0(0,RF),LIDDATA     SAVE EXPENDITURE TYPES                       
         EX    R4,*-6                                                           
         LA    RF,1(R4,RF)                                                      
         B     FORREC10                                                         
                                                                                
FORREC25 MVC   0(0,R1),LIDDATA     SAVE SUPPLIERS                               
         EX    R4,*-6                                                           
         LA    R1,1(R4,R1)                                                      
         B     FORREC10                                                         
         DROP  R6                                                               
                                                                                
FORREC40 LA    RE,XOTYPES          BUILD PASSIVES OUT OF TABLE(S)               
                                                                                
FORREC45 DS    0H                  ORDER TYPE LOOP                              
         OC    0(L'XOTYPES,RE),0(RE)                                            
         BZ    FORRECX                                                          
                                                                                
         LA    RF,XETYPES                                                       
FORREC50 DS    0H                  EXPENDITURE TYPE LOOP                        
         OC    0(L'XETYPES,RF),0(RF)                                            
         BZ    FORREC65                                                         
                                                                                
         LA    R1,XSUPPLS                                                       
FORREC55 DS    0H                  SUPPLIER LOOP                                
         OC    0(L'XSUPPLS,R1),0(R1)                                            
         BZ    FORREC60                                                         
                                                                                
         USING ERFPASD,R3                                                       
         XC    ERFPASD(ACCKLEN),ERFPASD                                         
         MVI   ERFPTYP,ERFPTYPQ                                                 
         MVI   ERFPSUB,ERFPSUBQ                                                 
         MVC   ERFPCPY,ERFKCPY                                                  
         MVC   ERFPAPPL,ERFKAPPL                                                
         MVC   ERFPOTYP,0(RE)                                                   
         MVC   ERFPETYP,0(RF)                                                   
         MVC   ERFPSACC,0(R1)                                                   
         MVC   ERFPFRMT,ERFKFRMT                                                
         XC    ERFPDA,ERFPDA                                                    
         MVC   ERFPSTA,ERFRSTA     COPY STATUS                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
         AHI   R1,L'XSUPPLS                                                     
         B     FORREC55                                                         
                                                                                
FORREC60 AHI   RF,L'XETYPES                                                     
         B     FORREC50                                                         
                                                                                
FORREC65 AHI   RE,L'XOTYPES                                                     
         B     FORREC45                                                         
                                                                                
FORRECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* BRO RESOURCE WORK RECORD - CREATE PERSON KEY STAGE PASSIVES         *         
***********************************************************************         
         SPACE 1                                                                
         USING RWKRECD,R2                                                       
CAMREC   CLI   RECTYPE,ACRTRWK     BRO RESOURCE WORK RECORD                     
         BNE   CAMRECX                                                          
                                                                                
         OC    RWKKACT,RWKKACT     IS IT CAMPAIGN RECORD                        
         BZ    CAMREC8             YES                                          
         USING KSTELD,R6                                                        
         LA    R6,RWKRFST          GET MAIN DATA ELEMENT                        
         XR    R0,R0                                                            
                                                                                
CAMREC1  CLI   KSTEL,0                                                          
         BE    CAMRECX                                                          
         CLI   KSTEL,KSTELQ                                                     
         BE    CAMREC3                                                          
                                                                                
CAMREC2  IC    R0,KSTLN                                                         
         AR    R6,R0                                                            
         B     CAMREC1                                                          
                                                                                
         USING PKSRECD,R3                                                       
CAMREC3  LA    R4,KSTNTRY                                                       
         SR    RF,RF                                                            
         IC    RF,KSTLN            RF=ELEMENT LENGTH                            
         SHI   RF,KSTLN2Q          RF=LENGTH OF PIDS ATTACHED                   
CAMREC4  CHI   RF,0                HAVE WE REACH END OF ELEMENT                 
         BE    CAMREC2             YES - LOOK FOR NEXT ELEMENT                  
         XC    PKSKEY(ACCKLEN),PKSKEY                                           
         MVI   PKSKTYP,PKSKTYPQ                                                 
         MVI   PKSKSUB,PKSKSUBQ                                                 
         MVC   PKSKCPY,RWKKCPY                                                  
         MVC   PKSKCODE,RWKKCCDE                                                
         MVC   PKSKKYST,KSTCODE                                                 
         MVC   PKSKUNT(L'PKSKUNT+L'PKSKLDG),=C'SJ'                              
         MVC   PKSKACT,RWKKACT                                                  
         MVC   PKSKPIDB,0(R4)                                                   
         SR    R1,R1                                                            
         ICM   R1,7,KSTENDT                                                     
         LNR   R1,R1                                                            
         STCM  R1,7,PKSKEDT                                                     
         SR    R1,R1                                                            
         ICM   R1,7,KSTSTDT                                                     
         LNR   R1,R1                                                            
         STCM  R1,7,PKSKSDT                                                     
         XC    PKSKDA,PKSKDA                                                    
         MVC   PKSKSTA,RWKRSTA     COPY STATUS                                  
         MVC   PKSKAMPM,KSTSTAT                                                 
         MVC   PKSKPID,RWKRPID                                                  
         MVC   PKSKOFF,RWKKOFF                                                  
         MVC   PKSKDTE,RWKRDTE                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         SHI   RF,L'KSTPID         REMOVE LENGTH OF ONE PID                     
         LA    R4,L'KSTPID(R4)     R4=A(PID)                                    
         B     CAMREC4             NO - ADD NEXT PASSIVE                        
*                                                                               
         USING CPJELD,R6                                                        
CAMREC8  LA    R6,RWKRFST          GET MAIN DATA ELEMENT                        
         XR    R0,R0                                                            
                                                                                
CAMREC10 CLI   CPJEL,0                                                          
         BE    CAMRECX                                                          
         CLI   CPJEL,CPJELQ                                                     
         BE    CAMREC14                                                         
                                                                                
CAMREC12 IC    R0,CPJLN                                                         
         AR    R6,R0                                                            
         B     CAMREC10                                                         
                                                                                
         USING CPNPASD,R3                                                       
CAMREC14 XC    CPNPAS(ACCKLEN),CPNPAS                                           
         MVI   CPNPTYP,CPNPTYPQ                                                 
         MVI   CPNPSUB,CPNPSUBQ                                                 
         MVC   CPNPCPY,RWKKCPY                                                  
         MVC   CPNPCODE,RWKKCCDE                                                
         XC    CPNPCODE,=X'FFFFFFFF'                                            
         MVC   CPNPACT,SPACES                                                   
         MVC   CPNPACT(L'CPJCLI),CPJCLI                                         
         XC    CPNPDA,CPNPDA                                                    
         MVC   CPNPSTA,RWKRSTA     COPY STATUS                                  
         MVC   CPNPPID,RWKRPID                                                  
         MVC   CPNPOFF,RWKKOFF                                                  
         MVC   CPNPDTE,RWKRDTE                                                  
         LA    R3,ACCKLEN(R3)                                                   
         USING CPCPASD,R3                                                       
CAMREC16 XC    CPCPAS(ACCKLEN),CPCPAS                                           
         MVI   CPCPTYP,CPCPTYPQ                                                 
         MVI   CPCPSUB,CPCPSUBQ                                                 
         MVC   CPCPCPY,RWKKCPY                                                  
         MVC   CPCPCAMP,RWKKCCDE                                                
         MVC   CPCPACT,SPACES                                                   
         MVC   CPCPACT(L'CPJCLI),CPJCLI                                         
         XC    CPCPDA,CPCPDA                                                    
         MVC   CPCPSTA,RWKRSTA     COPY STATUS                                  
         MVC   CPCPPID,RWKRPID                                                  
         MVC   CPCPOFF,RWKKOFF                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
CAMRECX  DS    0H                                                               
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* EXTRA DATA PASSIVE POINTER                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING XDFRECD,R2                                                       
XDFREC   CLI   RECTYPE,ACRTXDAT  EXTRA DATA RECORD                              
         BNE   XDFRECX                                                          
         LA    R6,XDFRFST                                                       
         USING XDFELD,R6                                                        
XDFR02   CLI   XDFEL,0                                                          
         BE    XDFRECX                                                          
         CLI   XDFEL,XDFELQ                                                     
         BE    XDFR06                                                           
XDFR04   XR    RF,RF                                                            
         IC    RF,XDFLN                                                         
         AR    R6,RF                                                            
         B     XDFR02                                                           
*                                                                               
         USING XDFPASD,R3                                                       
XDFR06   XC    XDFPAS,XDFPAS                                                    
         MVI   XDFPTYP,XDFPTYPQ                                                 
         MVI   XDFPSUB,XDFPSUBQ                                                 
         MVC   XDFPCPY,XDFKCPY                                                  
         MVC   XDFPPTR,XDFRPTR                                                  
         MVC   XDFPSEQ,XDFSEQ                                                   
         MVC   XDFPSTAT,XDFRSTAT   COPY STATUS                                  
         XC    XDFPDA,XDFPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     XDFR04                                                           
*                                                                               
XDFRECX  DS    0H                                                               
         DROP  R2,R3                                                            
***********************************************************************         
* TIME SAVE PASSIVE POINTER                                         *           
***********************************************************************         
         SPACE 1                                                                
         USING TSSRECD,R2                                                       
TSSREC   CLI   RECTYPE,ACRTSSAV    TIME SAVED RECORD                            
         BNE   TSSRECX                                                          
*                                                                               
         OC    TSSKSBR,TSSKSBR                                                  
         BNZ   TSSREC99                                                         
         USING TDTPASD,R3                                                       
         XC    TDTPAS,TDTPAS                                                    
         MVI   TDTPTYP,TDTPTYPQ                                                 
         MVI   TDTPSUB,TDTPSUBQ                                                 
         MVC   TDTPCPY,TSSKCPY                                                  
         MVC   TDTPPEDT,TSSKEND                                                 
         MVC   TDTPODSP,SPACES                                                  
         MVC   TDTPODSP(L'TSSKODS),TSSKODS                                      
         LA    RE,TDTPODSP+L'TSSKODS-1                                          
         LA    RF,L'TSSKODS-1                                                   
TSSREC02 CLI   0(RE),C' '                                                       
         BH    TSSREC04                                                         
         BCTR  RE,0                                                             
         BCT   RF,TSSREC02                                                      
TSSREC04 LA    RE,1(RE)                                                         
         MVC   0(L'TSSKPER,RE),TSSKPER                                          
         MVC   TDTPCULA,SPACES                                                  
         MVC   TDTPKSTA,TSSRRI1                                                 
         MVC   TDTPSBR,TSSKSBR                                                  
*        XC    TDTPSTA,TDTPSTA                                                  
         MVC   TDTPSTA,TSSRRI1                                                  
         ZAP   TDTPHRS,PZERO                                                    
         ZAP   TDTPMTS,PZERO                                                    
         XC    TDTPDA,TDTPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
TSSREC99 MVI   0(R3),EOL                                                        
         B     LDCPX                                                            
TSSRECX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* TIME RECORD PASSIVE POINTER                                         *         
***********************************************************************         
         SPACE 1                                                                
         USING TIMRECD,R2                                                       
TIMREC   CLI   RECTYPE,ACRTTIM     TIME RECORD                                  
         BNE   TIMRECX                                                          
         GOTOR TIMPAS                                                           
         B     LDCPX                                                            
TIMRECX  DS    0H                                                               
                                                                                
         EJECT                                                                  
***********************************************************************         
* PRODUCTION ACTIVITY PASSIVE POINTERS                                *         
***********************************************************************         
         SPACE 1                                                                
RAPREC   LA    RF,PROCOPTW                                                      
         USING CPTRD,RF                                                         
         MVI   CPEMUFLG,CPEMUNO                                                 
         GOTO1 VPROCPT,DMCB,(X'FF',(R2)),(R3),PROCOPTW,CPTRBLK                  
*                                                                               
RAPRECX  DS    0H                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* PRODUCTION BILL PASSIVES                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING PBRRECD,R3                                                       
PBRREC   CLI   RECTYPE,ACRTPBA                                                  
         BNE   PBRRECX                                                          
         OC    PBRKPARA-PBRRECD(2,R2),PBRKPARA-PBRRECD(R2)                      
         BNZ   PBRRECX                                                          
         XC    PBRKEY,PBRKEY                                                    
         MVI   PBRKTYP,PBRKTYPQ                                                 
         MVC   PBRPCPY,PBRKCPY-PBRRECD(R2)                                      
         MVI   PBRPSUB,PBRPPASQ                                                 
         MVC   PBRPJOB,PBRKJOB-PBRRECD(R2)                                      
         MVC   PBRKSTA,PBRRSTA-PBRRECD(R2)                                      
         XC    PBRKDA,PBRKDA                                                    
*                                                                               
         LA    R6,PBRRFST-PBRRECD(R2)                                           
         SR    R0,R0                                                            
         USING BLHELD,R6                                                        
PBRREC02 CLI   BLHEL,0             TEST END OF RECORD                           
         BE    PBRREC04                                                         
         CLI   BLHEL,BLHELQ                                                     
         BE    *+14                                                             
         IC    R0,BLHLN                                                         
         AR    R6,R0                                                            
         B     PBRREC02                                                         
         MVC   PBRPBLNO,BLHBLNO                                                 
         MVI   PBRPIND,PBRPIDFT                                                 
         OC    BLHBILD,BLHBILD                                                  
         BZ    *+8                                                              
         MVI   PBRPIND,PBRPILVE                                                 
         MVC   PBRPUSER,BLHUSER                                                 
         MVC   PBRPCRED,BLHCRED                                                 
         MVC   PBRPBILD,BLHBILD                                                 
         MVC   PBRPFORM,BLHFORM                                                 
         MVC   PBRPPERS,BLHPERS                                                 
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
PBRREC04 MVI   0(R3),EOL                                                        
*                                                                               
PBRRECX  DS    0H                                                               
         DROP  R3,R6                                                            
         SPACE 2                                                                
***********************************************************************         
* AUDIT RECORD PASSIVES                                               *         
***********************************************************************         
*                                                                               
R        USING AUDRECD,R2                                                       
         USING ERAPASD,R3                                                       
         USING AUDPIDD,R5                                                       
AUDREC   CLI   RECTYPE,ACRTAUDT                                                 
         BNE   AUDRECX                                                          
         CLI   R.AUDKAUDT,AUDKEST                                               
         BNE   AUDRECX                                                          
*                                  IGNORE ANY OLDER THAN 3 MONTHS AGO           
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)                                     
         GOTO1 VADDAY,DMCB,(C'M',TODAY),DUB,F'-3'                               
         GOTO1 VDATCON,DMCB,(0,DUB),(1,CUTOF3)                                  
*                                                                               
AUDREC00 LA    R6,R.AUDRFST                                                     
*                                  CLEAR AUDIT PID TABLE FOR EACH               
         LA    R0,AUDPIDT          AUDIT RECORD                                 
         LHI   R1,L'AUDPIDT                                                     
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         USING STCELD,R6                                                        
AUDREC02 CLI   STCEL,0                                                          
         BE    AUDREC08                                                         
         CLI   STCEL,STCELQ                                                     
         BE    AUDREC04                                                         
AUDREC03 LLC   R0,STCLN                                                         
         AR    R6,R0                                                            
         B     AUDREC02                                                         
*                                                                               
AUDREC04 LA    R5,AUDPIDT                                                       
         LHI   R0,AUDPIDTL                                                      
*                                                                               
AUDRECAA OC    AUDPIDP(AUDPIDTL),AUDPIDP BLANK ENTRY?                           
         BZ    AUDREC05                                                         
         CLC   STCPERS,AUDPIDP     SAME PID?                                    
         BNE   AUDREC06                                                         
         CLC   STCDATE,AUDPDAT     IS DATE MORE RECENT?                         
         BNH   AUDREC03                                                         
*                                                                               
AUDREC05 CLC   STCDATE,CUTOF3      IS IT EARLIER THAN 3 MONTHS AGO?             
         BL    AUDREC03                                                         
         MVC   AUDPIDP,STCPERS     THEN REPLACE ENTRY                           
         MVC   AUDPDAT,STCDATE                                                  
         B     AUDREC03                                                         
*                                                                               
AUDREC06 LA    R5,AUDPIDTL(R5)                                                  
         BCT   R0,AUDRECAA                                                      
         DC    H'0'                BAD TABLE IS FULL!                           
*                                                                               
AUDREC08 LA    R5,AUDPIDT          NOW BUILD PASSIVES                           
*                                                                               
AUDREC10 OC    AUDPIDP(AUDPIDTL),AUDPIDP BLANK ENTRY?                           
         BZ    AUDRECX                                                          
         XC    ERAPAS,ERAPAS                                                    
         MVI   ERAPTYP,ERAPTYPQ                                                 
         MVI   ERAPSUB,ERAPSUBQ                                                 
         MVC   ERAPCPY,R.AUDKCPY                                                
         MVC   ERAPPID,AUDPIDP                                                  
         XR    RF,RF                                                            
         ICM   RF,7,AUDPDAT                                                     
         LNR   RF,RF                                                            
         STCM  RF,7,ERAPCDA                                                     
         MVC   ERAPCPJ,R.AUDKECPJ                                               
         MVC   ERAPLNO,R.AUDKELNO                                               
         MVC   ERAPSEQ,R.AUDKSEQ                                                
         MVC   ERAPSTA,R.AUDRSTA                                                
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         LHI   R0,AUDPIDTL                                                      
         AR    R5,R0                                                            
         B     AUDREC10                                                         
*                                                                               
AUDRECX  DS    0H                                                               
         DROP  R3,R6,R                                                          
         EJECT                                                                  
***********************************************************************         
* VENDOR PASSIVE POINTERS FOR AUTO APPROVE                            *         
***********************************************************************         
         SPACE 1                                                                
AAVREC   BRAS  RE,VAAPTR                                                        
         BNE   AAVRECX                                                          
         B     LDCPX                                                            
*                                                                               
AAVRECX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* RECEIVABLE PASSIVE POINTERS FOR AUTO APPROVE                                  
***********************************************************************         
         SPACE 1                                                                
AARREC   BRAS  RE,RAAPTR                                                        
         BNE   AARRECX                                                          
         B     LDCPX                                                            
*                                                                               
AARRECX  DS    0H                                                               
                                                                                
LDCPX    XIT1  ,                                                                
         EJECT                                                                  
         SPACE 2                                                                
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
***********************************************************************         
* CONSTANTS                                                           *         
***********************************************************************         
         SPACE 1                                                                
VTYPES   DS    0V                                                               
         DC    V(LDLANG)                                                        
         DC    V(ACPROCPT)                                                      
         DC    V(ADDAY)                                                         
         DC    V(DATCON)                                                        
         DC    V(SRCHEXEC)                                                      
         DC    V(GETDAY)                                                        
         DC    V(GETBROAD)                                                      
VTYPESN  EQU   (*-VTYPES)/L'VTYPES                                              
*                                                                               
ACCMST   DC    C'ACCMST'                                                        
PZERO    DC    PL6'0'                                                           
XFFS     DC    13X'FF'                                                          
*                                                                               
*CUTOFF   DC    XL2'0000'          CUTOFF DATE FOR INTERAGENCY POINTERS         
*CUTOF2   DC    XL3'000000'        CUTOFF DATE FOR ESTIMATES                    
TWOYR    DC    XL3'000000'         CUTOFF DATE FOR PAYABLE AA POINTERS          
THREEYR  DC    XL3'000000'         CUTOFF DATE FOR RECV AA POINTERS             
GLHIMOS  DC    XL2'00'             G/L HIGH MONTH OF SERVICE                    
GLLOMOS  DC    XL2'00'             G/L LOW MONTH OF SERVICE                     
ACC      DC    C'ACC   '                                                        
SPACES   DC    CL(L'ACCKEY)' '                                                  
NULLS    DC    XL6'00'                                                          
AC@NOORD DC    C'DUMMY '                                                        
*                                                                               
OFFTYPE  DS    0X                                                               
         DC    AL1(1,EXSPCLI1,EXSPCNB1,EXSPCBL1)                                
         DC    AL1(EXJPCLI1,EXJPCNB1,EXJPCBL1)                                  
         DC    AL1(2,EXSPCLI2,EXSPCNB2,EXSPCBL2)                                
         DC    AL1(EXJPCLI2,EXJPCNB2,EXJPCBL2)                                  
         DC    X'FF'                                                            
                                                                                
MEDTYPE  DS    0X                                                               
         DC    AL1(1,EXSPMED1,EXSPMNB1,EXSPMBL1)                                
         DC    AL1(EXJPMED1,EXJPMNB1,EXJPMBL1)                                  
         DC    AL1(2,EXSPMED2,EXSPMNB2,EXSPMBL2)                                
         DC    AL1(EXJPMED2,EXJPMNB2,EXJPMBL2)                                  
         DC    X'FF'                                                            
                                                                                
ONERTYPE DS    0X                                                               
         DC    AL1(1,0,EXSPRNB1,EXSPRBL1,0,EXDPNBL1,EXDPBIL1)                   
         DC    AL1(2,0,EXSPRNB2,EXSPRBL2,0,EXDPNBL2,EXDPBIL2)                   
         DC    X'FF'                                                            
*                                                                               
         DS    0D                                                               
         ENTRY SRCHBLKO                                                         
SRCHBLKO DC    (SEARCHDL)X'00'     SEARCH CONTROL BLOCK FOR OFFLINE             
*                                  CALL TO CPTR                                 
WINIT    DC    C'N'                W/S INITIALISE FLAG - DMLDMOD ONLY           
         ENTRY ACLDPLDT                                                         
ACLDPLDT DC    XL2'0101'           PEEL DATE SAVE (SET BY ACLDPRGE)             
*                                                                               
SVDISP   DS    H                   SAVED DISPL INTO SR TABLE                    
SVTRKEY  DS    CL(TRNKSBR-TRNKEY)  SAVED TRANSACTION KEY                        
SVDA     DS    XL4                 SAVED DISK ADDRESS                           
SRTAB    DS    256CL(SRTBLNQ)      TABLE OF SR INFO                             
SRTEND   DS    C                                                                
         SPACE 1                                                                
***********************************************************************         
* TIME PASSIVE POINTERS FOR BRANDOCEAN                                          
***********************************************************************         
TIMPAS   NTR1  BASE=*,LABEL=NO                                                  
         J     *+12                                                             
         DC    C'*TIMPAS*'                                                      
         USING TIMRECD,R2                                                       
         USING TSWRECD,R3                                                       
         XC    TSWKEY,TSWKEY                                                    
         MVI   TSWKTYP,TSWKTYPQ                                                 
         MVI   TSWKSUB,TSWKSUBQ                                                 
         MVC   TSWKCPY,TIMKCPY                                                  
         MVC   TSWKPER,SPACES                                                   
         MVC   TSWKODS,SPACES                                                   
                                                                                
         LA    RF,LDGLVALN                                                      
         CLI   0(RF),12            TEST ONE LEVEL                               
         BE    TIMPASX             SKIP POINTERS                                
         CLI   0(RF),0             TEST WHETHER LEDGER READ                     
         BE    TIMPASX             SKIP POINTERS                                
         LA    R0,3                                                             
*                                                                               
         LA    RF,1(RF)            FIND LAST LEVEL                              
         CLI   0(RF),12                                                         
         BE    *+12                                                             
         BCT   R0,*-12                                                          
         B     TIMPASX             SOMETHING WRONG BUT NOT WORTH DYING          
*                                                                               
         MVC   PERCDE,SPACES                                                    
         MVC   PERODS,SPACES                                                    
         MVC   PERDEPT,SPACES                                                   
         MVC   PERSUB,SPACES                                                    
         MVC   PEROFF,SPACES                                                    
         LR    RE,RF                                                            
         BCTR  RE,0                                                             
         SR    R4,R4                                                            
         IC    R4,0(RF)            R4=LENGTH OF LAST LEVEL                      
         SR    R5,R5                                                            
         IC    R5,0(RE)            R5=LENGTH OF LAST-1 LEVEL                    
         LA    R1,TIMKACT(R5)      R1=A(LAST LEVEL)                             
         SR    R4,R5                                                            
         BCTR  R4,0                                                             
         MVC   PERCDE(0),0(R1)     PERSON TO PASSIVE KEY                        
         EX    R4,*-6                                                           
         MVC   PERPODS(0),0(R1)    PERSON TO PASSIVE KEY                        
         EX    R4,*-6                                                           
         AHI   R4,1                                                             
         BCTR  R5,0                                                             
         MVC   PERODS(0),TIMKACT   OFFICE/DEPT/SUB                              
         EX    R5,*-6                                                           
         LA    R1,PERPODS                                                       
         AR    R1,R4                                                            
         MVC   0(0,R1),TIMKACT     OFFICE/DEPT/SUB                              
         EX    R5,*-6                                                           
*                                                                               
         SHI   RF,2                                                             
         SR    R4,R4                                                            
         IC    R4,0(RE)            ISOLATE SUB DEPT CODE                        
         SR    R5,R5                                                            
         IC    R5,0(RF)            LEVEL B                                      
         SR    R4,R5               R4 = EX MOVE COUNT                           
         LA    R1,TIMKACT(R5)                                                   
         SHI   R4,1                                                             
         BNM   *+6                                                              
         DC    H'0'                                                             
         MVC   PERSUB(0),0(R1)                                                  
         EX    R4,*-6                                                           
*                                                                               
         SHI   RE,2                                                             
         SR    R4,R4                                                            
         IC    R4,0(RF)            ISOLATE SUB DEPT CODE                        
         SR    R5,R5                                                            
         IC    R5,0(RE)            LEVEL A                                      
         SR    R4,R5               R4 = EX MOVE COUNT                           
         LA    R1,TIMKACT(R5)                                                   
         SHI   R4,1                                                             
         BNM   *+6                                                              
         DC    H'0'                                                             
         MVC   PERDEPT(0),0(R1)                                                 
         EX    R4,*-6                                                           
*                                                                               
         SR    R5,R5                                                            
         IC    R5,0(RE)            LEVEL A                                      
         SHI   R5,1                                                             
         BNM   *+6                                                              
         DC    H'0'                                                             
         MVC   PEROFF(0),TIMKACT                                                
         EX    R5,*-6                                                           
*                                                                               
         MVC   TSWKODS,PERODS                                                   
         MVC   TSWKPER,PERCDE                                                   
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TSWKEND                                                     
         MVC   TSWKULC,TIMKCUNT    COST CODE                                    
         MVC   TSWKCOFC,TIMKOFF    OFFICE                                       
         MVC   TSWKSBR,TIMKSBR     SUB-REF                                      
         MVC   TSWKSTA,TIMRSTA                                                  
         XC    TSWKDA,TSWKDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         CLI   OFFLINE,C'Y'        IF IT'S ONLINE THEN ALWAYS ADD PNTRS         
         BNE   TIMREC01                                                         
         TM    CPYSTAC,CPYCOBOT    IS THIS COMPANY ON BRAND OCEAN TIME?         
         BNO   TIMPASX                                                          
*                                                                               
                                                                                
TIMREC01 ZAP   HRSBU,PZERO                                                      
         ZAP   MTSBU,PZERO                                                      
         LA    R0,CLIPTAB                                                       
         LHI   R1,CLIPLN                                                        
         XR    RF,RF                                                            
         XR    RE,RE                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R5,TIMRFST                                                       
         USING TIMELD,R5                                                        
TIMREC08 CLI   TIMEL,0                                                          
         BE    TIMREC42                                                         
         CLI   TIMEL,TIMELQ                                                     
         BNE   TIMREC10                                                         
         CLI   TIMETYP,TIMEINP                                                  
         BE    TIMREC12                                                         
TIMREC10 SR    R0,R0                                                            
         IC    R0,TIMLN                                                         
         AR    R5,R0                                                            
         B     TIMREC08                                                         
*                                                                               
TIMREC12 CLI   TIMLN,X'23'                                                      
         BE    TIMRECX                                                          
         CLI   TIMLN,X'4E'                                                      
         BE    TIMRECX                                                          
         CLI   TIMLN,X'56'                                                      
         BE    TIMRECX                                                          
         CLI   TIMLN,X'46'                                                      
         BE    TIMRECX                                                          
         AP    HRSBU,TIMHRS                                                     
         LA    R4,TIMEL                                                         
         MVC   SAVSEQ,TIMSEQ                                                    
PID      USING TIMEL,R4                                                         
TIMREC14 CLI   PID.TIMEL,0                                                      
         BE    TIMREC20                                                         
         CLI   PID.TIMEL,TIMELQ                                                 
         BNE   TIMREC18                                                         
         CLI   PID.TIMETYP,TIMETIME                                             
         BNE   TIMREC18                                                         
         CLC   SAVSEQ,PID.TIMSEQ   ARE WE WITHIN SAME BLOCK OF TIMELS           
         BE    TIMREC20            YES                                          
TIMREC18 SR    R0,R0                                                            
         IC    R0,PID.TIMLN                                                     
         AR    R4,R0                                                            
         B     TIMREC14                                                         
                                                                                
TIMREC20 LA    R1,CLIPTAB                                                       
         LHI   R0,MAXCLIS                                                       
         USING CLIPTABD,R1                                                      
TIMREC22 OC    CLIPACC,CLIPACC                                                  
         BZ    TIMREC28                                                         
         CLC   CLIPACC,TIMACC      IS THE ACCOUNT THE SAME                      
         BNE   TIMREC24            NO                                           
         CLC   =C'1N',TIMACC                                                    
         BE    TIMREC23                                                         
         TM    CPYSTA1,CPYSOROE                                                 
         BZ    TIMREC23                                                         
         CLC   CLIPOFF,TIMOFF      IS THE OFFICE THE SAME                       
         BNE   TIMREC24            NO                                           
TIMREC23 CLI   PID.TIMEL,0         YES - ARE WE AN MCS TIME USER                
         BE    TIMREC26            NO                                           
         CLC   CLIPST,PID.TIMEPST1 IS THE STATUS THE SAME                       
         BE    TIMREC26            YES                                          
         TM    CLIPST,TIMESREJ     WHAT STATUS DO WE HAVE PREVIOUSLY            
         BNZ   TIMREC26            REJECTED - LOWEST SO DON'T CHANGE            
         TM    PID.TIMEPST1,TIMESREJ  THIS ONE IS NOW REJECTED                  
         BNZ   TIMREC25                   SO REPLACE PREVIOUS STATUS            
         OC    CLIPST,CLIPST       DID WE HAVE PREVIOUS STATUS APPROVED         
         BNZ   TIMREC25            YES  - SO REPLACE WITH NOTHING               
         B     TIMREC26            NO - KEEP AS NOTHING                         
TIMREC24 LA    R1,CLIPTABL(R1)                                                  
         BCT   R0,TIMREC22                                                      
         DC    H'0'                                                             
*                                                                               
TIMREC25 MVC   CLIPST,PID.TIMEPST1                                              
TIMREC26 AP    CLIPHRS,TIMHRS                                                   
         B     TIMREC30                                                         
*                                                                               
TIMREC28 MVC   CLIPACC,TIMACC                                                   
         MVC   CLIPMED,TIMMED                                                   
         MVC   CLIPOFF,TIMOFF                                                   
         TM    CPYSTA1,CPYSOROE                                                 
         BNZ   *+10                                                             
         MVC   CLIPOFF,SPACES                                                   
         ZAP   CLIPHRS,TIMHRS                                                   
         ZAP   CLIPMTS,PZERO                                                    
         CLI   PID.TIMEL,0                                                      
         BE    TIMREC10                                                         
         MVC   CLIPPID,PID.TIMEPIDC                                             
         MVC   CLIPST,PID.TIMEPST1                                              
*                                                                               
TIMREC30 CLI   PID.TIMEL,0                                                      
         BE    TIMREC10                                                         
         CLI   PID.TIMEL,TIMELQ                                                 
         BNE   TIMREC32                                                         
         CLI   PID.TIMETYP,TIMEITMS                                             
         BNE   TIMREC32                                                         
TIMREC31 CLC   SAVSEQ,PID.TIMSEQ   ARE WE WITHIN SAME BLOCK OF TIMELS           
         BE    TIMREC34            YES                                          
TIMREC32 SR    R0,R0                                                            
         IC    R0,PID.TIMLN                                                     
         AR    R4,R0                                                            
         B     TIMREC30                                                         
*                                                                               
TIMREC34 AP    MTSBU,PID.TIMITOT                                                
         AP    CLIPMTS,PID.TIMITOT                                              
         B     TIMREC32                                                         
         DROP  PID,R1                                                           
*                                                                               
TIMREC42 LA    R4,CLIPTAB                                                       
         USING CLIPTABD,R4                                                      
TIMREC44 OC    CLIPACC,CLIPACC                                                  
         BZ    TIMREC80                                                         
         OC    CLIPPID,CLIPPID                                                  
         BZ    TIMREC70                                                         
         USING TAPPASD,R3                                                       
         XC    TAPPAS,TAPPAS       BUILD STATUS PASSIVE FOR CLIENT              
         MVI   TAPPTYP,TAPPTYPQ                                                 
         MVI   TAPPSUB,TAPPSUBQ                                                 
         MVI   TAPPCAT,TAPPCLI                                                  
         CLC   =C'1N',CLIPACC                                                   
         BNE   *+8                                                              
         MVI   TAPPCAT,TAPP1NA                                                  
         XC    SAVSTAT,SAVSTAT                                                  
         TM    TIMRSTAT,TIMSFAPP                                                
         BNZ   TIMREC48                                                         
         TM    TIMRSTAT,TIMSSUBM   TIME HAS BEEN SUBMITTED                      
         BNZ   TIMREC46            YES                                          
         TM    TIMRSTAT,TIMSMAAP   IS TIME LINE MANAGER APPROVED                
         BNZ   TIMREC46            YES                                          
         TM    TIMRSTAT,TIMSAWAP   IS TIME AWAITING MANAGER APPROVAL            
         BNZ   TIMREC46            YES - MUST BE ALL CLIENT APPROVED            
         TM    TIMRSTAT,TIMSREJE   IS TIME REJECTED                             
         BNZ   TIMREC46            YES                                          
         TM    TIMRSTAT,TIMSPAPP   TIME HAS BEEN APPROVED                       
         BZ    TIMREC52            NO                                           
TIMREC46 TM    CLIPST,CLIAPR       TIME APPROVED                                
         BNZ   TIMREC48            YES - TIME IS PART APPROVED                  
         TM    CLIPST,TIMESREJ     TIME REJECTED                                
         BNZ   TIMREC50            YES - TIME IS REJECTED                       
         MVI   TAPPKYST,TAPSAWPQ   NO -TIME IS AWAITING APPROVAL                
         B     TIMREC54                                                         
*                                                                               
TIMREC48 MVI   TAPPKYST,TAPSAPPQ                                                
         B     TIMREC54                                                         
*                                                                               
TIMREC50 MVI   TAPPKYST,TAPSREJQ                                                
         B     TIMREC54                                                         
*                                                                               
TIMREC52 MVI   TAPPKYST,TAPSSAVQ   SET RECORD AS SAVED TIME                     
*                                                                               
TIMREC54 MVC   TAPPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TAPPPEDT                                                    
         MVC   TAPCPODS,PERPODS                                                 
         MVC   TAPCODE,SPACES                                                   
         MVC   TAPC1NAC,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
         CLI   TAPPCAT,TAPP1NA                                                  
         BE    TIMREC56                                                         
         MVC   TAPCOFF,CLIPOFF                                                  
         MVC   TAPCACT,CLIPACC+L'ACTKUNT+L'ACTKLDG                              
         MVC   TAPCMED,CLIPMED                                                  
TIMREC56 MVC   TAPCDA,DA                                                        
         MVC   TAPPSBR,TIMKSBR                                                  
         MVC   TAPPSTAT,TIMRSTAT                                                
         MVC   SAVSTAT,TAPPKYST                                                 
         ZAP   TAPPHRS,CLIPHRS     TOTAL HOURS FOR CLIENT/PRODUCT/JOB           
         ZAP   TAPPMTS,CLIPMTS     TOTAL MATERIALS FOR CLIENT/PROD/JOB          
         XC    TAPPDA,TAPPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         CLC   CLIPMED,SPACES      HAVE WE GOT A MEDIA CODE                     
         BNH   TIMREC70            NO                                           
                                                                                
         XC    TAPPAS,TAPPAS       BUILD STATUS PASSIVE FOR MEDIA               
         MVI   TAPPTYP,TAPPTYPQ                                                 
         MVI   TAPPSUB,TAPPSUBQ                                                 
         MVI   TAPPCAT,TAPPMED     SET VIEW AS BY MEDIA                         
         MVC   TAPPKYST,SAVSTAT                                                 
         MVC   TAPPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TAPPPEDT                                                    
         MVC   TAPCPODS,PERPODS                                                 
         MVC   TAPCMMED,CLIPMED                                                 
         MVC   TAPCOFFC,CLIPOFF                                                 
         MVC   TAPCSJAC,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
         MVC   TAPCDA,DA                                                        
         MVC   TAPPSBR,TIMKSBR                                                  
         MVC   TAPPSTAT,TIMRSTAT                                                
         ZAP   TAPPHRS,CLIPHRS     TOTAL HOURS FOR CLIENT/PRODUCT/JOB           
         ZAP   TAPPMTS,CLIPMTS     TOTAL MATERIALS FOR CLIENT/PROD/JOB          
         XC    TAPPDA,TAPPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
                                                                                
         USING TACPASD,R3                                                       
TIMREC70 XC    TACPAS,TACPAS                                                    
         MVI   TACPSUB,TACPSUBQ    CLIENT PASSIVE FOR EVERYONE                  
         MVI   TACPTYP,TACPTYPQ                                                 
         MVC   TACPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TACPPEDT                                                    
         MVC   TACPPODS,PERPODS                                                 
         MVC   TACPCODE,SPACES                                                  
         CLC   =C'1N',CLIPACC                                                   
         BNE   TIMREC72                                                         
         MVI   TACPVIEW,TACP1NAQ                                                
         MVC   TACP1NAC,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
         B     TIMREC74                                                         
                                                                                
TIMREC72 MVI   TACPVIEW,TACPSJAQ                                                
         MVC   TACPCOFF,CLIPOFF                                                 
         MVC   TACPACT,CLIPACC+L'ACTKUNT+L'ACTKLDG                              
         MVC   TACPMED,CLIPMED                                                  
                                                                                
TIMREC74 MVC   TACPKDA,DA                                                       
         MVC   TACPSBR,TIMKSBR                                                  
         MVC   TACPKSTA,SAVSTAT                                                 
         NI    TACPKSTA,X'FF'-TIMSDELT                                          
         OC    CLIPPID,CLIPPID                                                  
         BNZ   *+8                                                              
         MVI   TACPKSTA,X'FF'      TO DENOTE NOT NEEDING CLIENT APPRVL          
         MVC   TACPSTAT,TIMRSTAT                                                
         ZAP   TACPHRS,CLIPHRS     TOTAL HOURS FOR ACCOUNT                      
         ZAP   TACPMTS,CLIPMTS     TOTAL MATERIALS FOR ACCOUNT                  
         XC    TACPDA,TACPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
                                                                                
         CLC   CLIPMED,SPACES      HAVE WE GOT A MEDIA CODE                     
         BNH   TIMREC78            NO                                           
                                                                                
         XC    TACPAS,TACPAS                                                    
         MVI   TACPSUB,TACPSUBQ    CLIENT PASSIVE FOR EVERYONE                  
         MVI   TACPTYP,TACPTYPQ                                                 
         MVC   TACPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TACPPEDT                                                    
         MVC   TACPPODS,PERPODS                                                 
         MVI   TACPVIEW,TACPMEDQ                                                
         MVC   TACPMMED,CLIPMED                                                 
         MVC   TACPMOFF,CLIPOFF                                                 
         MVC   TACPMACT,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
                                                                                
         MVC   TACPKDA,DA                                                       
         MVC   TACPSBR,TIMKSBR                                                  
         MVC   TACPKSTA,SAVSTAT                                                 
         NI    TACPKSTA,X'FF'-TIMSDELT                                          
         OC    CLIPPID,CLIPPID                                                  
         BNZ   *+8                                                              
         MVI   TACPKSTA,X'FF'      TO DENOTE NOT NEEDING CLIENT APPRVL          
         MVC   TACPSTAT,TIMRSTAT                                                
         ZAP   TACPHRS,CLIPHRS     TOTAL HOURS FOR ACCOUNT                      
         ZAP   TACPMTS,CLIPMTS     TOTAL MATERIALS FOR ACCOUNT                  
         XC    TACPDA,TACPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
                                                                                
         USING TSJPASD,R3                                                       
TIMREC78 XC    TSJPAS,TSJPAS                                                    
         MVI   TSJPSUB,TSJPSUBQ    CLIENT PASSIVE FOR EVERYONE                  
         MVI   TSJPTYP,TSJPTYPQ                                                 
         MVC   TSJPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TSJPPEDT                                                    
         MVC   TSJPCODE,SPACES                                                  
         CLC   =C'1N',CLIPACC                                                   
         BNE   TIMREC7A                                                         
         MVI   TSJPVIEW,TSJP1NAQ                                                
         MVC   TSJP1NAC,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
         B     TIMREC7B                                                         
                                                                                
TIMREC7A MVI   TSJPVIEW,TSJPSJAQ                                                
         MVC   TSJPCOFF,CLIPOFF                                                 
         MVC   TSJPACT,CLIPACC+L'ACTKUNT+L'ACTKLDG                              
         MVC   TSJPMED,CLIPMED                                                  
                                                                                
TIMREC7B MVC   TSJPPODS,PERPODS                                                 
         MVC   TSJPKDA,DA                                                       
         MVC   TSJPSBR,TIMKSBR                                                  
         MVC   TSJPKSTA,SAVSTAT                                                 
         NI    TSJPKSTA,X'FF'-TIMSDELT                                          
         OC    CLIPPID,CLIPPID                                                  
         BNZ   *+8                                                              
         MVI   TSJPKSTA,X'FF'      TO DENOTE NOT NEEDING CLIENT APPRVL          
         MVC   TSJPSTAT,TIMRSTAT                                                
         ZAP   TSJPHRS,CLIPHRS     TOTAL HOURS FOR ACCOUNT                      
         ZAP   TSJPMTS,CLIPMTS     TOTAL MATERIALS FOR ACCOUNT                  
         XC    TSJPDA,TSJPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
                                                                                
         CLC   CLIPMED,SPACES      HAVE WE GOT A MEDIA CODE                     
         BNH   TIMREC7C            NO                                           
                                                                                
         XC    TSJPAS,TSJPAS                                                    
         MVI   TSJPSUB,TSJPSUBQ    CLIENT PASSIVE FOR EVERYONE                  
         MVI   TSJPTYP,TSJPTYPQ                                                 
         MVC   TSJPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TSJPPEDT                                                    
         MVC   TSJPPODS,PERPODS                                                 
         MVI   TSJPVIEW,TSJPMEDQ                                                
         MVC   TSJPMMED,CLIPMED                                                 
         MVC   TSJPMOFF,CLIPOFF                                                 
         MVC   TSJPMACT,CLIPACC+L'ACTKUNT+L'ACTKLDG                             
         MVC   TSJPKDA,DA                                                       
         MVC   TSJPSBR,TIMKSBR                                                  
         MVC   TSJPKSTA,SAVSTAT                                                 
         NI    TSJPKSTA,X'FF'-TIMSDELT                                          
         OC    CLIPPID,CLIPPID                                                  
         BNZ   *+8                                                              
         MVI   TSJPKSTA,X'FF'      TO DENOTE NOT NEEDING CLIENT APPRVL          
         MVC   TSJPSTAT,TIMRSTAT                                                
         ZAP   TSJPHRS,CLIPHRS     TOTAL HOURS FOR ACCOUNT                      
         ZAP   TSJPMTS,CLIPMTS     TOTAL MATERIALS FOR ACCOUNT                  
         XC    TSJPDA,TSJPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
TIMREC7C LA    R4,CLIPTABL(R4)                                                  
         B     TIMREC44                                                         
*                                                                               
         USING TAPPASD,R3                                                       
TIMREC80 XC    TAPPAS,TAPPAS       STATUS PASSIVE FOR MANAGER                   
         MVI   TAPPTYP,TAPPTYPQ                                                 
         MVI   TAPPSUB,TAPPSUBQ                                                 
         MVI   TAPPCAT,TAPPMAN                                                  
         MVC   TAPPCPY,TIMKCPY                                                  
         TM    TIMRSTAT,TIMSFAPP   TIME HAS BEEN FULLY APPROVED                 
         BNZ   TIMREC82            YES                                          
         TM    TIMRSTAT,TIMSMAAP   IS TIME LINE MANAGER APPROVED                
         BNZ   TIMREC82            YES -                                        
         TM    TIMRSTAT,TIMSAWAP   IS TIME AWAITING MANAGER APPROVAL            
         BNZ   TIMREC84            YES - MUST BE ALL CLIENT APPROVED            
         TM    TIMRSTAT,TIMSSUBM   TIME HAS BEEN SUBMITTED                      
         BNZ   TIMREC86                                                         
         TM    TIMRSTAT,TIMSPAPP   TIME HAS BEEN PART APPROVED                  
         BNZ   TIMREC86            YES                                          
         B     TIMREC88                                                         
*                                                                               
TIMREC82 MVI   TAPPKYST,TAPSAPPQ                                                
         B     TIMREC90                                                         
TIMREC84 MVI   TAPPKYST,TAPSAWPQ                                                
         B     TIMREC90                                                         
TIMREC86 MVI   TAPPKYST,TAPSSUBQ                                                
         B     TIMREC90                                                         
TIMREC88 MVC   TAPPKYST,TIMRSTAT                                                
         NI    TAPPKYST,X'FF'-TIMSDELT                                          
TIMREC90 MVC   SAVSTAT,TAPPKYST                                                 
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TAPPPEDT                                                    
         MVC   TAPMODSP,TIMKACT                                                 
         MVC   TAPMCULA,TIMKULC                                                 
         MVC   TAPMCOFF,TIMKOFF                                                 
         MVC   TAPPSBR,TIMKSBR                                                  
         MVC   TAPPSTAT,TIMRSTAT                                                
         ZAP   TAPPHRS,HRSBU       TOTAL HOURS OF TIME RECORD                   
         ZAP   TAPPMTS,MTSBU       TOTAL MATERIALS OF TIME RECORD               
         XC    TAPPDA,TAPPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
*                                                                               
         USING TDTPASD,R3                                                       
         XC    TDTPAS,TDTPAS       TIME DATE PASSIVE                            
         MVI   TDTPTYP,TDTPTYPQ                                                 
         MVI   TDTPSUB,TDTPSUBQ                                                 
         MVC   TDTPCPY,TIMKCPY                                                  
         SR    R1,R1                                                            
         ICM   R1,7,TIMKPEDT       PERIOD END                                   
         LNR   R1,R1                                                            
         STCM  R1,7,TDTPPEDT                                                    
         MVC   TDTPODSP,TIMKACT                                                 
         MVC   TDTPCULA,TIMKULC                                                 
         MVC   TDTPCOFF,TIMKOFF                                                 
         MVC   TDTPSBR,TIMKSBR                                                  
         MVC   TDTPKSTA,SAVSTAT                                                 
         MVC   TDTPSTAT,TIMRSTAT                                                
         ZAP   TDTPHRS,HRSBU                                                    
         ZAP   TDTPMTS,MTSBU                                                    
         XC    TDTPDA,TDTPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
                                                                                
TIMPASX  XIT1  REGS=(R3)                                                        
         DROP  R3,R2                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* RECEIVABLE PASSIVE POINTERS FOR AUTO APPROVE                        *         
* R3 POINTS TO NEXT AVAILABLE SPACE IN BUFFER FOR POINTERS            *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING AARPASD,R3                                                       
RAAPTR   NTR1  BASE=*,LABEL=*                                                   
         CLI   RECTYPE,ACRTTRN     TRANSACTION?                                 
         BNE   RAAPL                                                            
         CLC   =C'SR',TRNKUNT      ONLY FOR SR/SB                               
         BE    *+14                                                             
         CLC   =C'SB',TRNKUNT                                                   
         BNE   RAAPL                                                            
*                                                                               
         OC    THREEYR,THREEYR     DID WE ALREADY GET THE DATE?                 
         BNZ   RAAP05              YES                                          
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)        TAKE TODAY'S DATE            
         GOTO1 VADDAY,DMCB,(C'Y',TODAY),DUB,F'-3'  AND SUBTRACT 3 YEARS         
         GOTO1 VDATCON,DMCB,(0,DUB),(1,THREEYR)    FOR RECEIVABLES              
*                                                                               
         USING TRNELD,R6                                                        
RAAP05   LA    R6,TRNRFST                                                       
RAAP10   CLI   TRNEL,0                                                          
         BE    RAAPL                                                            
         CLI   TRNEL,TRNELQ        44 ELEMENT                                   
         BE    RAAP20                                                           
         ZIC   R0,TRNLN                                                         
         AR    R6,R0                                                            
         B     RAAP10                                                           
*                                                                               
RAAP20   DS    0H                                                               
         TM    TRNSTAT,TRNSDR      IS THIS A DEBIT? NO SO SKIP SINCE            
         BZ    RAAPL               I JUST WANT DEBITS.                          
         CLI   TRNRSTYP,X'06'      IGNORE TYPE 6                                
         BE    RAAPL                                                            
         MVC   SVOFF,TRNOFFC       SAVE OFFICE                                  
         XC    SVMOS,SVMOS                                                      
         MVC   SVEST,SPACES                                                     
         NI    BYTE,X'FF'-(MOSF+FLGBRD+NOPTR)                                   
         XC    SVSYS,SVSYS                                                      
         BRAS  RE,RD1A             READ 1A/6A ELEM FOR MOS/ESTIMATE             
         BE    RAAP30              FOUND IT HERE                                
         TM    BYTE,NOPTR          DO WE REALLY NEED A POINTER?                 
         BO    RAAPL               NO                                           
         BRAS  RE,RDF3             READ F3 ELEMENT FOR MOS/EST                  
RAAP30   CLC   SVEST,SPACES        IS THIS A ZERO ESTIMATE?                     
         BE    RAAPL               YES-DON'T BOTHER WITH THE POINTER            
*                                                                               
         CLC   SVMOS,THREEYR       ONLY GOING BACK 3 YEARS (YYMM)               
         BL    RAAPL                                                            
*                                                                               
         CLC   TRNKEY(TRNKSBR-TRNKEY),SVTRKEY   SAME TRANSACTION KEY?           
         BE    RAAP40              YES SO CHECK SOME INFO                       
         MVC   SVTRKEY,TRNKEY      SAVE TRANSACTION KEY                         
         MVC   SVDA,DA             SAVE THE DISK ADDRESS                        
         XC    SVDISP,SVDISP       CLEAR DISPL INTO SR TABLE                    
         LA    R0,SRTAB            CLEAR SR TABLE FOR NEW SR ACCT               
         L     R1,=A(SRTEND-SRTAB)                                              
         LR    RE,R0                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         BRAS  RE,ADDSR            ADD SR ENTRY TO TABLE                        
         B     RAAP50              AND GO ADD POINTER                           
*                                                                               
RAAP40   DS    0H                                                               
         BRAS  RE,CHKSR            CHK IF POINTER ALREADY CREATED               
         BNE   RAAPL               NO POINTER NEEDED                            
         BRAS  RE,ADDSR            POINTER NEEDED-SO GO ADD TO TABLE            
*                                                                               
RAAP50   MVC   AARPKEY,SPACES                                                   
         MVI   AARPTYP,AARPTYPQ    X'24'                                        
         MVI   AARPSUB,AARPSUBQ    X'02'                                        
         MVC   AARPCPY,TRNKCPY                                                  
         MVI   AARPIND,AARPIEST    ESTIMATE INDICATOR                           
         MVC   AARPCLT,SVCLI       CLIENT                                       
         MVC   AARPPRD,SVPRD       PRODUCT                                      
         MVC   AARPEST,SVEST       ESTIMATE                                     
         MVC   AARPSYS,SVSYS       SYSTEM                                       
         MVC   AARPOFF,SVOFF       OFFICE                                       
         MVC   AARPACCT,TRNKLDG    RECEIVABLE (SR OR SB ACCOUNT)                
         MVC   AARPKDA,SVDA        SAVE DISK ADDRESS AS PART OF KEY             
         XC    AARPSTA,AARPSTA                                                  
         XC    AARPDA,AARPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     RAAPE                                                            
*                                                                               
RAAPL    CLI   *,FF                                                             
         B     RAAPX                                                            
RAAPE    CR    RB,RB                                                            
RAAPX    XIT1                                                                   
         DROP  R2,R3,R6                                                         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ADD INFO TO SR TABLE                                                *         
***********************************************************************         
         SPACE 1                                                                
         USING SRTABD,RF                                                        
ADDSR    NTR1  BASE=*,LABEL=*                                                   
         LA    RF,SRTAB                                                         
         AH    RF,SVDISP                                                        
         L     RE,=A(SRTEND)                                                    
         CR    RF,RE                                                            
         BL    *+6                                                              
         DC    H'0'                MUST INCREASE TABLE                          
         MVC   SRCLI,SVCLI                                                      
         MVC   SRPRD,SVPRD                                                      
         MVC   SREST,SVEST                                                      
         MVC   SRSYS,SVSYS                                                      
         LA    RF,SRTBLNQ(RF)      BUMP TO NEXT ENTRY TO SAVE DISP              
         LA    RE,SRTAB                                                         
         SR    RF,RE                                                            
         STH   RF,SVDISP                                                        
         XIT1                                                                   
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CHECK SR TABLE TO SEE IF WE ALREADY HAVE A MATCH                    *         
* ON EXIT - CC=LOW IF ALREADY FOUND IN TABLE (NO POINTER NEEDED)      *         
*           CC=EQUAL IF ENTRY NOT FOUND IN TABLE (POINTER NEEDED)     *         
***********************************************************************         
         SPACE 1                                                                
         USING SRTABD,RF                                                        
CHKSR    NTR1  BASE=*,LABEL=*                                                   
         LA    RF,SRTAB                                                         
CHKSR10  L     RE,=A(SRTEND)       END OF TABLE                                 
         CR    RF,RE                                                            
         BNL   CHKSRE              ENTRY NOT FOUND-ADD POINTER                  
         CLC   SRCLI,SVCLI         SAME CLIENT?                                 
         BNE   CHKSR20             NO CHK NEXT ENTRY                            
         CLC   SRPRD,SVPRD         SAME PRODUCT?                                
         BNE   CHKSR20                                                          
         CLC   SREST,SVEST         SAME ESTIMATE?                               
         BNE   CHKSR20                                                          
         CLC   SRSYS,SVSYS         SAME SYSTEM?                                 
         BNE   CHKSR20                                                          
         B     CHKSRL              ENTRY FOUND-DO NOT ADD POINTER               
*                                                                               
CHKSR20  LA    RF,SRTBLNQ(RF)      BUMP TO NEXT ENTRY                           
         B     CHKSR10                                                          
*                                                                               
CHKSRL   CLI   *,FF                                                             
         B     CHKSRX                                                           
CHKSRE   CR    RB,RB                                                            
CHKSRX   XIT1                                                                   
         DROP  RF                                                               
***********************************************************************         
* VENDOR PASSIVE POINTERS FOR AUTO APPROVE                            *         
* R3 POINTS TO NEXT AVAILABLE SPACE IN BUFFER FOR POINTERS            *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING AAVPASD,R3                                                       
VAAPTR   NTR1  BASE=*,LABEL=*                                                   
         XC    SVSYS,SVSYS                                                      
         CLI   RECTYPE,ACRTTRN     TRANSACTION?                                 
         BNE   VAAPL               NO                                           
         CLI   TRNKUNT,C'S'        UNIT S TRANSACTION?                          
         BNE   VAAPL               NO                                           
         LA    RF,LDSYTAB          CHECK TABLE OF LEDGER/SYSTEM B/C             
VAAP02   CLI   0(RF),X'FF'         ONLY WANT CERTAIN LEDGERS                    
         BE    VAAPL                                                            
         CLC   TRNKLDG,0(RF)                                                    
         BE    VAAP05                                                           
         LA    RF,2(RF)                                                         
         B     VAAP02                                                           
*                                                                               
VAAP05   MVC   SVSYS,1(RF)         SAVE SYSTEM FROM TABLE                       
         CLI   SVSYS,C'S'          IF IT'S SPOT CHECK TO SEE IF IT'S            
         BNE   *+16                REALLY NET BY CHECKING THE 1ST CHAR          
         CLI   TRNKACT,C'N'        OF THE ACCOUNT FOR 'N' (SSN)                 
         BNE   *+8                                                              
         MVI   SVSYS,C'N'                                                       
         TM    TRNRSTAT,TRNSDRFT   DON'T CREATE POINTERS FOR DRAFT TRN          
         BO    VAAPL               DSSUP-6862                                   
         OC    TWOYR,TWOYR         DID WE ALREADY GET THE DATE?                 
         BNZ   VAAP07              YES                                          
         GOTO1 VDATCON,DMCB,(5,0),(0,TODAY)        TAKE TODAY'S DATE            
         GOTO1 VADDAY,DMCB,(C'Y',TODAY),DUB,F'-2'  AND SUBTRACT 2 YEARS         
         GOTO1 VDATCON,DMCB,(0,DUB),(1,TWOYR)      FOR PAYABLES                 
*                                                                               
         USING TRNELD,R6                                                        
VAAP07   LA    R6,TRNRFST                                                       
VAAP10   CLI   TRNEL,0                                                          
         BE    VAAPL                                                            
         CLI   TRNEL,TRNELQ        44 ELEMENT                                   
         BE    VAAP20                                                           
         ZIC   R0,TRNLN                                                         
         AR    R6,R0                                                            
         B     VAAP10                                                           
*                                                                               
VAAP20   DS    0H                                                               
         TM    TRNSTAT,TRNSDR      IS THIS A DEBIT? YES SO SKIP SINCE           
         BO    VAAPL               I JUST WANT CREDITS.                         
         MVC   SVOFF,TRNOFFC       SAVE OFFICE                                  
         XC    SVMOS,SVMOS                                                      
         MVC   SVEST,SPACES                                                     
         NI    BYTE,X'FF'-(MOSF+FLGBRD+NOPTR)                                   
         BRAS  RE,CHK60            SAVE USED DATE                               
         BRAS  RE,RDE5             READ E5 ELEMENT FOR MOS                      
         BE    VAAP30              FOUND IT HERE                                
         BRAS  RE,RDF3             READ F3 FOR MOS                              
         BE    VAAP30              FOUND IT HERE                                
         BRAS  RE,RD23             READ 23 FOR MOS                              
VAAP30   OC    SVMOS,SVMOS         DID WE GET THE MOS ALREADY?                  
         BZ    VAAP40              NO                                           
         CLC   SVMOS,TWOYR         ONLY GOING BACK 1 YEAR (YYMM)                
         BNL   *+14                                                             
         OC    SVUSED,SVUSED       LONGER THAN A YEAR SO CHECK IF IT'S          
         BNZ   VAAPL               BEEN DISBURSED.  IF NOT CREATE A PTR         
VAAP40   BRAS  RE,RD46             READ 46 ELEMENT FOR MOS AND ESTIMATE         
         BE    *+12                                                             
         BRAS  RE,RD1A             LOOK FOR ESTIMATE HERE                       
         BE    *+14                                                             
         OC    SVMOS,SVMOS         ANY MOS                                      
         BZ    VAAP50              PUT OUT POINTER ANYWAY                       
         CLC   SVMOS,TWOYR         ONLY GOING BACK 1 YEAR (YYMM)                
         BNL   *+14                                                             
         OC    SVUSED,SVUSED       LONGER THAN A YEAR SO CHECK IF IT'S          
         BNZ   VAAPL               BEEN DISBURSED.  IF NOT CREATE A PTR         
*                                                                               
VAAP50   MVC   AAVPKEY,SPACES                                                   
         MVI   AAVPTYP,AAVPTYPQ    X'24'                                        
         MVI   AAVPSUB,AAVPSUBQ    X'01'                                        
         MVC   AAVPCPY,TRNKCPY                                                  
         MVC   AAVPCLT,TRNKCACT+9  LAST 3 CHAR OF CONTRA IS CLIENT              
         MVC   AAVPPRD,TRNKREF     1ST 3 CHAR OF REFERENCE IS PRODUCT           
         MVC   AAVPEST,SVEST       ESTIMATE                                     
         MVC   AAVPMOS,SVMOS       MOS                                          
         MVC   AAVPSYS,SVSYS       SYSTEM                                       
         MVC   AAVPOFF,SVOFF       OFFICE                                       
         MVC   AAVPACCT,TRNKLDG    STATION                                      
         BRAS  RE,CHKEST           CHECK ESTIMATE                               
         MVI   AAVPFLG1,0          DEFAULT TO A GOOD ESTIMATE                   
         CLI   ESTIND,C'Z'         IS THIS A ZERO ESTIMATE                      
         BNE   *+12                NO                                           
         OI    AAVPFLG1,AAVPNEST   YES                                          
         B     AAVP50                                                           
         CLI   ESTIND,C'B'         IS THIS A BAD (NON-NUMERIC) EST?             
         BNE   *+8                 NO                                           
         OI    AAVPFLG1,AAVPBEST   YES                                          
AAVP50   MVC   AAVPKDA,DA          SAVE DISK ADDRESS AS PART OF KEY             
         XC    AAVPSTA,AAVPSTA                                                  
         TM    TRNSTAT,TRNSAPPR    APPROVED?                                    
         BZ    *+8                                                              
         OI    AAVPSTAT,AAVPAPP    SET APPROVED IN STATUS                       
         TM    TRNSTAT,TRNSHOLD    ON HOLD?                                     
         BZ    *+8                                                              
         OI    AAVPSTAT,AAVPHLD                                                 
         MVC   AAVPUSED,SVUSED     USED DATE                                    
         XC    AAVPDA,AAVPDA                                                    
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
         B     VAAPE                                                            
*                                                                               
VAAPL    CLI   *,FF                                                             
         B     VAAPX                                                            
VAAPE    CR    RB,RB                                                            
VAAPX    XIT1                                                                   
         DROP  R2,R3,R6                                                         
         SPACE 1                                                                
*                                  TABLE OF LEDGER AND SYSTEM                   
LDSYTAB  DC    C'SS'               LEDGER S - SYS SPOT (BUT MAYBE NET)          
         DC    C'TS'               LEDGER T - SYS SPOT                          
         DC    C'PP'               LEDGER P - SYS PRINT                         
         DC    C'QP'               LEDGER Q - SYS PRINT                         
         DC    C'UN'               LEDGER U - SYS NET                           
         DC    X'FF'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CHECK ESTIMATE TO SEE IF IT'S NUMERIC                               *         
* EXIT - ESTIND CONTAINS 'Z' FOR ZERO ESTIMATE, 'B' FOR BAD ESTIMATE  *         
***********************************************************************         
         SPACE 1                                                                
CHKEST   NTR1  BASE=*,LABEL=*                                                   
         XC    ESTIND,ESTIND                                                    
         CLC   SVEST,SPACES                                                     
         BNE   *+12                                                             
         MVI   ESTIND,C'Z'                                                      
         B     CESTX                                                            
         CLC   SVEST+3(3),SPACES                                                
         BNE   CEST20                                                           
*                                                                               
         LA    RF,SVEST                                                         
         LHI   RE,3                                                             
CEST10   CLI   0(RF),C'0'                                                       
         BL    CEST20                                                           
         CLI   0(RF),C'9'                                                       
         BH    CEST20                                                           
         BCT   RE,CEST10                                                        
         B     CESTX                                                            
*                                                                               
CEST20   MVI   ESTIND,C'B'                                                      
CESTX    XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 60 ELEMENT LOOKING FOR OFFSETS                                 *         
* AND SAVE THE USED DATE TOO FOR VENDOR PASSIVES                                
* EXIT - LOW IF TRANSACTION IS AN OFFSET (NO POINTER)                 *         
*        EQUAL IF TRANSACTION NOT AN OFFSET (POINTER)                 *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING TRSELD,R6                                                        
CHK60    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVUSED,SVUSED                                                    
         LA    R6,TRNRFST                                                       
RD6010   CLI   TRSEL,0                                                          
         BE    RD60L                                                            
         CLI   TRSEL,TRSELQ        60 ELEMENT                                   
         BE    RD6020                                                           
         ZIC   R0,TRSLN                                                         
         AR    R6,R0                                                            
         B     RD6010                                                           
*                                                                               
RD6020   MVC   SVUSED,TRSUDAT      SAVE THE USED DATE                           
         TM    TRSSTAT,TRSSOFFS    OFFSET?                                      
         BZ    RD60E               NO                                           
*                                                                               
RD60L    CLI   *,FF                                                             
         B     RD60X                                                            
RD60E    CR    RB,RB                                                            
RD60X    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 1A/6A ELEMENT LOOKING FOR MOS AND ESTIMATE FOR RECEIVABLES     *         
* AND CLIENT PRODUCT FOR RCV                                                    
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING MDTELD,R6                                                        
RD1A     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R6,TRNRFST                                                       
RD1A10   CLI   MDTEL,0                                                          
         BE    RD1AL                                                            
         CLI   MDTEL,MDTELQ        1A ELEMENT                                   
         BE    RD1A20                                                           
         CLI   MDTEL,MDPELQ        6A ELEMENT                                   
         BE    RD1A20                                                           
         ZIC   R0,MDTLN                                                         
         AR    R6,R0                                                            
         B     RD1A10                                                           
*                                                                               
RD1A20   CLI   MDTSYS,C'J'         IS SYSTEM PRODUCTION?                        
         BNE   *+12                                                             
         OI    BYTE,NOPTR          YES NO POINTER NEEDED                        
         B     RD1AL                                                            
*                                                                               
         OC    SVSYS,SVSYS         DID WE ALREADY FIGURE OUT THE                
         BNZ   RD1A30              SYSTEM? (FOR PAYABLES)                       
         MVC   SVSYS,MDTSYS        SYSTEM                                       
         LA    RF,PSYSTAB          TABLE OF EQUIVALENT SYSTEMS-PRINT            
RD1A22   CLI   0(RF),X'FF'                                                      
         BE    RD1A24                                                           
         CLC   MDTSYS,0(RF)        IF MATCH ON SYSTEM THAN THE ACTUAL           
         BNE   *+12                SYSTEM MUST BE PRINT                         
         MVI   SVSYS,C'P'                                                       
         B     RD1A30                                                           
         LA    RF,1(RF)                                                         
         B     RD1A22                                                           
RD1A24   CLI   SVSYS,C'P'          IF PRINT DON'T NEED TO CHK SUB-MEDIA         
         BE    RD1A30                                                           
*                                                                               
         LA    RF,SMEDTAB          TABLE OF SUB-MEDIA'S FOR NET                 
RD1A25   CLI   0(RF),X'FF'                                                      
         BE    RD1A30                                                           
         CLC   MDTMED,0(RF)        IF MATCH ON SUB-MED THAN THE SYSTEM          
         BNE   *+12                MUST BE NET                                  
         MVI   SVSYS,C'N'                                                       
         B     RD1A30                                                           
         LA    RF,1(RF)                                                         
         B     RD1A25                                                           
*                                                                               
RD1A30   MVC   SVMOS,MDTMOS        MOVE IN MONTH OF SERVICE                     
         OI    BYTE,MOSF           FOUND MOS                                    
         MVC   SVCLI,MDTCLI        CLIENT                                       
         MVC   SVPRD,MDTPRD        PRODUCT                                      
         MVC   SVEST,MDTEST        SAVE CHARACTER ESTIMATE                      
         OC    SVEST,SPACES                                                     
         OI    BYTE,ESTF                                                        
         B     RD1AE                                                            
*                                                                               
RD1AL    CLI   *,FF                                                             
         B     RD1AX                                                            
RD1AE    CR    RB,RB                                                            
RD1AX    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
SMEDTAB  DC    C'NCDSOHV'          TABLE OF SUB-MEDIA'S                         
         DC    X'FF'                                                            
PSYSTAB  DC    C'I'                 INTERACTIVE                                 
         DC    C'L'                 SOCIAL                                      
         DC    C'B'                 MOBILE                                      
         DC    C'V'                 NATIONAL VIDEO                              
         DC    C'W'                 LOCAL VIDEO                                 
         DC    C'D'                 DIGITAL PRINT SYSTEM MEDIA                  
         DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ E5 ELEMENT LOOKING FOR MOS FOR PAYABLES                                  
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING GDAELD,R6                                                        
RDE5     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R6,TRNRFST                                                       
RDE510   CLI   GDAEL,0                                                          
         BE    RDE5L                                                            
         CLI   GDAEL,GDAELQ        E5 ELEMENT                                   
         BNE   *+12                                                             
         CLI   GDATYPE,GDAMMOS     TYPE MUST BE MEDIA MOS                       
         BE    RDE520                                                           
         ZIC   R0,GDALN                                                         
         AR    R6,R0                                                            
         B     RDE510                                                           
*                                                                               
RDE520   MVC   SVMOS,GDAYYMM                                                    
         OI    BYTE,MOSF                                                        
         B     RDE5E                                                            
*                                                                               
RDE5L    CLI   *,FF                                                             
         B     RDE5X                                                            
RDE5E    CR    RB,RB                                                            
RDE5X    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ F3 ELEMENT LOOKING FOR MOS FOR PAYABLES AND RECEIVABLE                   
* AND CLIENT/PRODUCT FOR RCV                                                    
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING MBIELD,R6                                                        
RDF3     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R6,TRNRFST                                                       
RDF310   CLI   MBIEL,0                                                          
         BE    RDF3L                                                            
         CLI   MBIEL,MBIELQ        F3 ELEMENT                                   
         BE    RDF320                                                           
         ZIC   R0,MBILN                                                         
         AR    R6,R0                                                            
         B     RDF310                                                           
*                                                                               
RDF320   MVC   SVMOS,MBIMOS                                                     
         MVC   SVCLI,MBICLI                                                     
         MVC   SVPRD,MBIPRD                                                     
         MVC   SVEST,MBIEST                                                     
         OI    BYTE,MOSF                                                        
         B     RDF3E                                                            
*                                                                               
RDF3L    CLI   *,FF                                                             
         B     RDF3X                                                            
RDF3E    CR    RB,RB                                                            
RDF3X    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 23 ELEMENT LOOKING FOR MOS FOR PAYABLES AND EST FOR PAY/RCV              
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING OTHELD,R6                                                        
RD23     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R6,TRNRFST                                                       
RD2310   CLI   OTHEL,0                                                          
         BE    RD23L                                                            
         CLI   OTHEL,OTHELQ        23 ELEMENT                                   
         BE    RD2320                                                           
         ZIC   R0,OTHLN                                                         
         AR    R6,R0                                                            
         B     RD2310                                                           
*                                                                               
RD2320   CLI   OTHPROF-3,C' '      IF SOMETHING IN THIS FIELD THAN NO           
         BH    RD23L               MOS IN THIS ELEMENT                          
         MVC   SVMOS,OTHDATE                                                    
         OI    BYTE,MOSF                                                        
*                                                                               
RD23L    CLI   *,FF                                                             
         B     RD23X                                                            
RD23E    CR    RB,RB                                                            
RD23X    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 46 ELEMENT LOOKING FOR MOS AND ESTIMATE FOR PAYABLES                     
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING XPYELD,R6                                                        
RD46     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R6,TRNRFST                                                       
RD4610   CLI   XPYEL,0                                                          
         BE    RD46L                                                            
         CLI   XPYEL,XPYELQ                                                     
         BE    RD4620                                                           
         ZIC   R0,XPYLN                                                         
         AR    R6,R0                                                            
         B     RD4610                                                           
*                                                                               
RD4620   TM    BYTE,MOSF           DID WE FIND THE MOS ALREADY?                 
         BO    RD4640              YES                                          
         CLI   TRNKCULA+3,C'N'       IF NETWORK                                 
         BE    *+8                                                              
         OI    BYTE,FLGBRD           GET DATE FROM BROADCAST CAL                
         MVC   WORK(L'XPYPER),XPYPER                                            
         LA    R2,WORK                                                          
         CLI   WORK+6,C' '                                                      
         BNH   *+8                                                              
         LA    R2,WORK+6                                                        
         CLI   WORK+6,C'-'                                                      
         BNE   *+8                                                              
         LA    R2,WORK+7                                                        
         TM    BYTE,FLGBRD                                                      
         BNO   RD4630                                                           
         GOTO1 VGETBRD,DMCB,(1,(R2)),DTE1,VGETDAY,VADDAY                        
         LA    R2,DTE2                                                          
RD4630   GOTO1 VDATCON,DMCB,(0,(R2)),(1,WORK+20)                                
         MVC   SVMOS,WORK+20       MOVE IN YYMM                                 
*                                                                               
RD4640   DS    0H                                                               
         CLC   XPYEST,SPACES       IS THERE AN ESTIMATE?                        
         BE    RD46E                                                            
         OC    XPYEST,XPYEST                                                    
         BZ    RD46E                                                            
         LH    RE,XPYEST                                                        
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SVEST(3),DUB           UNPACK THE ESTIMATE                       
         B     RD46E                                                            
*                                                                               
RD46L    CLI   *,FF                                                             
         B     RD46X                                                            
RD46E    CR    RB,RB                                                            
RD46X    XIT1                                                                   
         DROP  R2,R6                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO TEST WHETHER AN INTERAGENCY PASSIVE POINTER          *         
* HAS BEEN CREATED FOR A DATE                                         *         
*                                                                     *         
* AT ENTRY, R6=A(ESTIMATE ELEMENT)                                    *         
***********************************************************************         
         SPACE 1                                                                
*&&US                                                                           
         USING IESELD,R6                                                        
TSTDATE  NTR1  BASE=*,LABEL=*                                                   
         LA    RE,DATETAB          RE=A(DATE TABLE)                             
         L     RF,NDATES                                                        
         LTR   R1,RF                                                            
         BZ    TSTDATE4            NO ENTRIES IN TABLE                          
*                                                                               
TSTDATE2 CLC   IESDAT,0(RE)        TEST FOR MATCH ON DATE                       
         BE    TSTDATEN            YES-DUPLICATE DATE                           
         LA    RE,L'DATETAB(RE)                                                 
         BCT   R1,TSTDATE2                                                      
*                                                                               
TSTDATE4 MVC   0(L'DATETAB,RE),IESDAT ADD DATE TO TABLE                         
         LA    RF,1(RF)            INCREMENT ENTRY COUNT                        
         CH    RF,=Y(MAXDATES)     TEST FOR TABLE OVERFLOW                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    RF,NDATES                                                        
*                                                                               
TSTDATEY CLI   *+1,0               CC=EQ                                        
         B     TSTDATEX                                                         
TSTDATEN CLI   *+0,0               CC=NEQ                                       
TSTDATEX XIT1                                                                   
         DROP  R6                                                               
         LTORG                                                                  
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO CREATE NON CLIENT PASSIVE POINTER FOR EACH APPL      *         
*                                                                     *         
* AT ENTRY, MYBYTE - APPLICATION (SEE NCTPAPPL)                       *         
*           R2     - APPROVER RECORD                                  *         
*           R3     - BUILDING BLOCK                                   *         
*           R4     - LIDTAP1N LIST DATA                               *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R2                                                       
         USING NCTPASD,R3                                                       
ADD1NPAS NTR1  BASE=*,LABEL=*                                                   
         XC    NCTPASD(ACCKLEN),NCTPASD                                         
         MVI   NCTPTYP,NCTPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   NCTPSUB,NCTPSUBQ                                                 
         MVC   NCTPCPY,APPKCPY     COMPANY CODE                                 
         MVC   NCTPAPPL,MYBYTE     APPLICATION                                  
         SR    RE,RE                                                            
         LH    RE,DATALEN          LENGTH OF EACH ITEM IN LIST                  
         SHI   RE,3                LENGTH OF 1N ACCOUNT - 3                     
         MVC   NCTPNCC(0),2(R4)                                                 
         EX    RE,*-6                                                           
         OC    NCTPNCC,SPACES                                                   
         MVC   NCTPPIDB,APPKPIDB                                                
         MVC   NCTPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    NCTPDA,NCTPDA                                                    
         XC    NCTPSTA,NCTPSTA                                                  
         LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         XIT1  REGS=(R3)                                                        
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO CREATE DEPT PASSIVE POINTER FOR EACH APPLICATION     *         
*                                                                     *         
* AT ENTRY, MYBYTE - APPLICATION (SEE DPAPAPPL)                       *         
*           R2     - APPROVER RECORD                                  *         
*           R3     - BUILDING BLOCK                                   *         
*           R4     - LIDTAP1R LIST DATA                               *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R2                                                       
         USING DPAPASD,R3                                                       
         USING LIDDATA,R4                                                       
ADD1RPAS NTR1  BASE=*,LABEL=*                                                   
         XC    DPAPASD(ACCKLEN),DPAPASD                                         
         MVI   DPAPTYP,DPAPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   DPAPSUB,DPAPSUBQ                                                 
         MVC   DPAPCPY,APPKCPY     COMPANY CODE                                 
         MVC   DPAPAPPL,MYBYTE     APPLICATION                                  
         SR    RE,RE                                                            
         LH    RE,DATALEN          LENGTH OF EACH ITEM IN LIST                  
         SHI   RE,2                LENGTH OF 1R ACCOUNT - 2                     
         MVC   DPAP1RAC(0),LIDAPACC                                             
         EX    RE,*-6                                                           
         OC    DPAP1RAC,SPACES                                                  
         ZAP   DPAPXVAL,LIDAPEXV                                                
         MVC   DPAPPIDB,APPKPIDB                                                
         MVC   DPAPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    DPAPDA,DPAPDA                                                    
         XC    DPAPSTA,DPAPSTA                                                  
         CLI   MYBYTE,DPAPAEXF     FINANCE APPROVER?                            
         BNE   ADD1RP02                                                         
         TM    0(R4),LIDAPDED      YES - IS IT DEFAULT APPROVER                 
         BZ    *+8                                                              
         OI    DPAPSTAT,DPAPDFLT                                                
ADD1RP02 LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         XIT1  REGS=(R3)                                                        
         DROP  R2,R3,R4                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO CREATE SJ PASSIVE POINTER FOR EACH APPLICATION       *         
*                                                                     *         
* AT ENTRY, MYBYTE - APPLICATION (SEE JOBPAPPL)                       *         
*           R2     - APPROVER RECORD                                  *         
*           R3     - BUILDING BLOCK                                   *         
*           R4     - LIDTAPSJ LIST DATA                               *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R2                                                       
         USING JOBPASD,R3                                                       
         USING LIDDATA,R4                                                       
ADDSJPAS NTR1  BASE=*,LABEL=*                                                   
         XC    JOBPASD(ACCKLEN),JOBPASD                                         
         MVI   JOBPTYP,JOBPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   JOBPSUB,JOBPSUBQ                                                 
         MVC   JOBPCPY,APPKCPY     COMPANY CODE                                 
         MVC   JOBPAPPL,MYBYTE     APPLICATION                                  
         MVI   JOBPVIEW,JOBPVOFF   TYPE OF VIEW                                 
         MVC   JOBPCOFF,LIDASJOF                                                
         MVC   JOBPCPJ,LIDASJAC                                                 
         MVC   JOBPCMED,LIDASJME                                                
         OC    JOBPCODE,SPACES                                                  
         MVC   JOBPPIDB,APPKPIDB                                                
         MVC   JOBPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    JOBPDA,JOBPDA                                                    
         CLI   MYBYTE,JOBPAORD                                                  
         BNE   ADDSJP02                                                         
         TM    0(R4),LIDAORDD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP02 CLI   MYBYTE,JOBPAJOB                                                  
         BNE   ADDSJP04                                                         
         TM    0(R4),LIDAJOBD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP04 CLI   MYBYTE,JOBPAEST                                                  
         BNE   ADDSJP06                                                         
         TM    0(R4),LIDAESTD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP06 CLI   MYBYTE,JOBPAESI                                                  
         BNE   ADDSJP08                                                         
         TM    1(R4),LIDAESID                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP08 LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
*                                                                               
         CLC   LIDASJME,SPACES                                                  
         BNH   ADDSJP20                                                         
         XC    JOBPASD(ACCKLEN),JOBPASD                                         
         MVI   JOBPTYP,JOBPTYPQ    CREATE THE PASSIVE POINTER                   
         MVI   JOBPSUB,JOBPSUBQ                                                 
         MVC   JOBPCPY,APPKCPY     COMPANY CODE                                 
         MVC   JOBPAPPL,MYBYTE     APPLICATION                                  
         MVI   JOBPVIEW,JOBPVMED   TYPE OF VIEW                                 
         MVC   JOBPMED,LIDASJME                                                 
         MVC   JOBPOFFC,LIDASJOF                                                
         MVC   JOBPJOB,LIDASJAC                                                 
         OC    JOBPCODE,SPACES                                                  
         MVC   JOBPPIDB,APPKPIDB                                                
         MVC   JOBPSEQ,APPKSEQ     SEQUENCE NUMBER                              
         XC    JOBPDA,JOBPDA                                                    
         CLI   MYBYTE,JOBPAORD                                                  
         BNE   ADDSJP12                                                         
         TM    0(R4),LIDAORDD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP12 CLI   MYBYTE,JOBPAJOB                                                  
         BNE   ADDSJP14                                                         
         TM    0(R4),LIDAJOBD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP14 CLI   MYBYTE,JOBPAEST                                                  
         BNE   ADDSJP16                                                         
         TM    0(R4),LIDAESTD                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP16 CLI   MYBYTE,JOBPAESI                                                  
         BNE   ADDSJP18                                                         
         TM    1(R4),LIDAESID                                                   
         BZ    *+8                                                              
         OI    JOBPSTAT,JOBPDFLT                                                
ADDSJP18 LA    R3,ACCKLEN(R3)                                                   
         MVI   0(R3),EOL                                                        
ADDSJP20 XIT1  REGS=(R3)                                                        
*                                                                               
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
EOL      EQU   X'FF'                                                            
MAXDATES EQU   50                                                               
         SPACE 1                                                                
***********************************************************************         
* DSECT TO COVER WORKING STORAGE                                      *         
***********************************************************************         
         SPACE 1                                                                
FF       EQU   X'FF'                                                            
*                                                                               
WORKD    DSECT                                                                  
OFFICE   DS    CL2                                                              
CURREF   DS    CL6                                                              
CURINT   DS    CL7                                                              
CURORD   DS    CL6                                                              
CUTOFF   DS    XL2                CUTOFF DATE FOR INTERAGENCY POINTERS          
CUTOF2   DS    XL3                CUTOFF DATE FOR ESTIMATES                     
CUTOF3   DS    PL3                CUTOFF DATE FOR ESTIMATES                     
*TWOYR    DS    XL3                CUTOFF DATE FOR PAYABLE AA POINTERS          
*THREEYR  DS    XL3                CUTOFF DATE FOR RECV AA POINTERS             
DUB      DS    D                                                                
VTYPESR  DS    0V                                                               
VLDLANG  DS    V                                                                
VPROCPT  DS    V                                                                
VADDAY   DS    V                                                                
VDATCON  DS    V                                                                
VSRCHXEC DS    V                                                                
VGETDAY  DS    V                                                                
VGETBRD  DS    V                                                                
VRECTYP  DS    V                                                                
VCOMFACS DS    A                                                                
*                                                                               
RELO     DS    F                                                                
ASEARCHD DS    A                   A(CALLER'S SEARCH BLOCK)                     
XETYPES  DS    0XL3                                                             
WORK     DS    XL128                                                            
DA       DS    XL4                 DISK ADDRESS                                 
SAVERE   DS    A                                                                
MYBYTE   DS    X                                                                
MYBYTE1  DS    X                                                                
MYBYTE2  DS    X                                                                
MYBYTE3  DS    X                                                                
ADPASS   DS    F                                                                
DMCB     DS    6F                                                               
NDATES   DS    F                                                                
DATALEN  DS    H                                                                
CURDISP  DS    H                                                                
TOTELLN  DS    H                                                                
RECTYPE  DS    XL1                                                              
COMPANY  DS    XL1                                                              
ACTLVL   DS    XL1                                                              
ACTSEQ   DS    XL3                                                              
XOTYPES  DS    0XL1                                                             
PERODS   DS    CL8                                                              
PERPODS  DS    CL12                PERSON/OFFICE/DPT/SUB-DEPT                   
PERCDE   DS    CL(L'PERKCODE)                                                   
PEROFF   DS    CL(L'LOCOFF)                                                     
PERDEPT  DS    CL(L'LOCDEPT)                                                    
PERSUB   DS    CL(L'LOCSUB)                                                     
SAVSEQ   DS    XL(L'TIMSEQ)        SAVED TIMEL SEQUENCE                         
SAVSTAT  DS    XL(L'TIMKSTAT)      SAVE STATUS                                  
MANPID   DS    CL(L'PIDNO)         MANAGER PID                                  
HRSBU    DS    PL6                 HOURS BUCKET                                 
MTSBU    DS    PL6                 MATERIALS BUCKET                             
SAVEACC  DS    CL12                SAVE ACCOUNT FOR EXPENSE ITEM                
SAVEACT  DS    CL12                SAVE ACCOUNT FOR EXPENSE ITEM                
SAVEMED  DS    CL1                 MEDIA CODE                                   
SAVEOFFC DS    CL2                 OFFICE CODE                                  
SAVETYPE DS    CL1                 TYPE OF CLAIM                                
SAVLMSTA DS    XL1                 LINE MANAGER/1R STATUS                       
SVOFF    DS    CL2                 SAVED OFFICE                                 
SVUSED   DS    XL2                 SAVE USED DATE (COMPRESSED FROM 60)          
SVEST    DS    CL6                 SAVED ESTIMATE                               
SVMOS    DS    XL2                 SAVED MOS                                    
SVCLI    DS    CL3                 SAVED CLIENT                                 
SVPRD    DS    CL3                 SAVED PRODUCT                                
SVSYS    DS    XL1                 SAVED SYSTEM                                 
TMPEST   DS    CL6                 TEMP ESTIMATE FIELD (EBCDIC)                 
DTE1     DS    CL6                 YYMMDD (EBCDIC)                              
DTE2     DS    CL6                 YYMMDD (EBCDIC)                              
DATETAB  DS    (MAXDATES)XL(L'IESDAT)                                           
ESTIND   DS    CL1                 Z(ZERO),B(BAD)                               
BYTE     DS    XL1                                                              
MOSF     EQU   X'80'               FOUND MOS                                    
ESTF     EQU   X'40'               FOUND EST                                    
FLGBRD   EQU   X'20'                                                            
NOPTR    EQU   X'10'               NO POINTER NEEDED                            
TRNHLD   EQU   X'08'               ON HOLD                                      
*                                                                               
FLAG     DS    XL1                                                              
FLG60    EQU   X'80'               60 ELEMENT FOUND                             
*                                                                               
PROCOPTW DS    XL(L'CPBLOCK)                                                    
OFFLINE  DS    CL1                 Y = OFFLINE, N = ONLINE                      
SVESTCPJ DS    CL(L'ACTKACT)       ESTIMATE CLI/PRO/JOB CODE                    
         DS    0D                                                               
XSUPPLS  DS    0XL14                                                            
CLIPTAB  DS   (MAXCLIS)XL(CLIPTABL)                                             
MAXCLIS  EQU   40                                                               
SJTABLE  DS    51XL12              EXPENSE SJ TABLE                             
XSUPPLQ  EQU   *-XSUPPLS                                                        
SRCHBLK  DC    (SEARCHDL)X'00'     SEARCH CONTROL BLOCK FOR ONLINE CALL         
*                                                                               
SVESTPAS DS    A                   SAVED 1ST BILL PASSIVE POINTERS              
SVESTDA  DS    XL4                 SAVED MAIN ESTIMATE RECORD DA                
SVESTSTA DS    0XL4                SAVED MAIN ESTIMATE STATUS                   
SVESTST1 DS    XL1                                                              
SVESTST2 DS    XL1                                                              
SVESTOFF DS    CL2                                                              
*                                                                               
WORKX    EQU   *                                                                
         SPACE 1                                                                
       ++INCLUDE ACLDCPTRD                                                      
*                                                                               
AUDPIDT  DS    XL(30*AUDPIDTL)     STORES PIDS AND DATE FOR EST AUDIT           
*                                                                               
         SPACE 1                                                                
WORKL    EQU   *-WORKD                                                          
         SPACE 1                                                                
*          DATA SET ACLDCPTR   AT LEVEL 125 AS OF 09/18/09                      
CATTYPD  DSECT                                                                  
CATLEVL  DS    XL1                 LEVEL OF APPROVER                            
CATCAT   DS    XL1                 CATEGORY FOR STATUS PASSIVE NON WPP          
CATCATNB DS    XL1                 CATEGORY FOR STATUS PASSIVE WPP              
CATCATBL DS    XL1                 CATEGORY FOR STATUS PASSIVE WPP              
CATCAD   DS    XL1                 CATEGORY FOR DATE PASSIVE NON WPP            
CATCADNB DS    XL1                 CATEGORY FOR DATE PASSIVE WPP                
CATCADBL DS    XL1                 CATEGORY FOR DATE PASSIVE WPP                
CATTYPL  EQU   *-CATTYPD                                                        
                                                                                
CLIPTABD DSECT                                                                  
CLIPHRS  DS    PL6                 HOURS FOR THIS CLIENT  (TIME ONLY)           
CLIPMTS  DS    PL6                 MATERIALS FOR THIS CLIENT(TIME ONLY)         
CLIPPID  DS    XL2                 BINARY PID OF APPROVER LEVEL 1               
CLIPST   DS    XL1                 CLIENT LEVEL 1 STATUS                        
CLIPTYP  DS    XL1                 TYPE OF ITEM BILLABLE OR NON BILL            
CLIAPR   EQU   TIMESAPR                                                         
CLIPLVL  DS    XL1                 LEVEL OF APPROVER                            
CLIPACT  DS    CL(L'ACTKACT)       SJ ACCOUNT                                   
CLIPACC  DS    CL(L'ACTKULA)       SJ ACCOUNT                                   
CLIPOFF  DS    CL(L'TRNOFFC)       OFFICE - COULD BE SJ OR 1R                   
CLIPMED  DS    CL1                 MEDIA CODE  - ONLY IF JOB PRESENT            
CLIPTABL EQU   *-CLIPTABD                                                       
CLIPLN   EQU   (MAXCLIS*CLIPTABL)                                               
*                                                                               
AUDPIDD  DSECT                                                                  
AUDPIDP  DS    XL(L'STCPERS)       2 BYTE PID                                   
AUDPDAT  DS    XL(L'STCDATE)       DATE OF CHANGE                               
AUDPIDTL EQU   *-AUDPIDD                                                        
* ACRECEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACRECEQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACPROCPTRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACPROCPTRD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENRAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENRAC                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACSRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACSRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
***********************************************************************         
* DSECTS                                                                        
***********************************************************************         
         SPACE 1                                                                
SRTABD   DSECT                                                                  
SRCLI    DS    CL3                 SR CLIENT                                    
SRPRD    DS    CL3                 SR PRODUCT                                   
SREST    DS    CL6                 SR ESTIMATE                                  
SRSYS    DS    CL1                 SRSYS                                        
SRTBLNQ  EQU   *-SRTABD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'079ACLDCPTR  01/10/21'                                      
         END                                                                    
