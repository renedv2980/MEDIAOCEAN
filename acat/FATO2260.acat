*          DATA SET FATO2260   AT LEVEL 004 AS OF 05/01/02                      
*CATALP FATO2260                                                                
         TITLE 'TO2260 - 2260 TERMINAL OUTPUT TRANSLATOR'                       
TO2260   CSECT                                                                  
         NMOD1 260,**TO2260                                                     
         USING TWA,2                                                            
         USING WORK,12                                                          
         EJECT                                                                  
*                                                                               
*                                                                               
*              REGISTER USAGE -                                                 
*                                                                               
*        1     CURRENT MOVE ADDRESS                                             
*        2     NEXT TWA ADDRESS                                                 
*        3     NEXT CWA MOVE ADDRESS                                            
*        4     REMAINING MESSAGE LENGTH                                         
*        5     REAMINING COUNT TO START MANUAL INPUT SYMBOL                     
*        6     BYTE COUNT TO CURRENT MOVE ADDRESS                               
*                                                                               
*                                                                               
         EJECT                                                                  
         L     R2,0(R1)            A(TWA)                                       
         ST    R2,VTWA                                                          
         L     R3,4(R1)            A(OUTPUT BUFFER)                             
         ST    R3,VCWA                                                          
         MVI   0(R3),STX                                                        
         MVI   1(R3),ESC           SET FORMAT OFF                               
         MVI   2(R3),C'B'                                                       
         LA    R3,3(R3)                                                         
         LH    4,TWA+4             SET MESSAGE LENGTH                           
         LH    5,TWA+10            SET SMI COUNT                                
         SLL   5,20                                                             
         SRL   5,20                REMOVE LINE NUMBER                           
         LA    2,63(2)             SET FIRST CHARACTER ADDRESS - 1              
         BAS   9,CLEAR             CLEAR COUNTERS AND OUTPUT AREAS              
         MVI   LASTSW,0                                                         
         MVI   FMTSW,0             ASSUME FIRST INPUT TO BE FMT OFF             
         LA    1,OFFAREA           SET POINTER FOR MOVE                         
         LTR   5,5                 SMI COUNT INITIALLY 0 ?                      
         BC    7,NEXTCHAR          BRANCH ONNO                                  
         MVI   0(3),X'4A'          YES. MOVE SMI SYMBOL DIRECTLY TO CWA         
         LA    3,1(3)              BUMP POINTER                                 
         LH    5,=H'8192'          SET COUNTER TO INFINITY                      
NEXTCHAR LA    2,1(2)                                                           
         BCTR  4,0                 DECREMENT MESSAGE LENGTH                     
         BCTR  5,0                 DECREMENT SMI COUNT                          
         LTR   5,5                 SMI COUNT = 0 ?                              
         BC    10,A100             BRANCH IF GE 0                               
*                                  MOVE START MANUAL INPUT SYMBOL TO            
*                                  CURRENT MOVE ADDRESS                         
         BCTR  1,0                 BACK UP 1 BYTE                               
         MVC   0(2,R1),=X'BD7A'    MOVE SMI/COLON                               
         LA    6,1(6)                                                           
         LA    1,1(1)                                                           
         LH    5,=H'8192'          SET COUNTER TO INFINITY                      
         B     B100                GO MOVE OUTPUT                               
A100     LTR   4,4                 MESSAGE LENGTH LESS THAN 0 ?                 
         BC    10,A200             BRANCH ON NO                                 
         MVI   LASTSW,255          YES, SET LAST FIELD SWITCH                   
         B     B100                GO MOVE LAST FIELDS                          
A200     CLI   0(2),X'22'          CHARACTER EOL ?                              
         BC    8,B100              YES, GO TO MOVE ROUTINE                      
         CLI   0(2),X'FE'                                                       
         BC    7,A300                                                           
         MVI   0(2),0              REPLACE X'FE' WITH BINARY ZERO               
*                                                                               
A300     CLI   FMTSW,0             NO, FMTSW = OFF ?                            
         BC    8,A400              BRANCH ON YES                                
         CLI   0(2),X'14'          CHARACTER = X'14' (FORMAT OFF) ?             
         BC    7,A310              BRANCH ON NO                                 
         MVI   OFFIND,X'FF'        SET RECEIVED INDICATOR                       
         MVI   FMTSW,0             SET SWITCH OFF                               
         STH   6,ONCNT1            STORE LENGTH                                 
*                                  (MUST BE FIRST FMT ON FIELD)                 
         SR    6,6                 CLEAR COUNTER                                
         LA    1,OFFAREA           SET NEXT MOVE ADDRESS                        
         OC    OFFCNT,OFFCNT       FIRST FMT OFF FIELD ?                        
         BC    8,NEXTCHAR          YES                                          
         LH    6,OFFCNT            NO                                           
         AR    1,6                 SET NEXT MOVE ADDRESS                        
         B     NEXTCHAR                                                         
A310     CLI   0(2),X'24'          TEST FOR DUPLICATE FMT ON SYMBOL             
         BC    8,NEXTCHAR          YES, IGNORE                                  
A320     MVC   0(1,1),0(2)         MOVE CHARACTER                               
         LA    1,1(1)              BUMP ADDRESS                                 
         LA    6,1(6)              BUMP COUNTER                                 
         B     NEXTCHAR            GET NEXT CHARACTER                           
*                                                                               
*                                  FMTSW = OFF                                  
A400     CLI   0(2),X'24'          CHARACTER = X'24' (FORMAT ON) ?              
         BC    7,A410              BRANCH ON NO                                 
         MVI   FMTSW,255           SET FMTSW = ON                               
         STH   6,OFFCNT            STORE LENGTH IN OFFCNT                       
         SR    6,6                 CLEAR COUNTER                                
*                                  RESET NEXT OUTPUT ADDRESS                    
         LA    1,ONAREA1                                                        
         OC    ONCNT1,ONCNT1                                                    
         BC    8,NEXTCHAR                                                       
         LH    6,ONCNT1                                                         
         AR    1,6                                                              
         B     NEXTCHAR                                                         
A410     CLI   0(2),X'14'          TEST FOR DUPLICATE FMT OFF SYMBOL            
         BC    8,NEXTCHAR                                                       
A420     MVC   0(1,1),0(2)         MOVE CHARACTER                               
         LA    1,1(1)              BUMP ADDRESS                                 
         LA    6,1(6)              BUMP COUNTER                                 
         B     NEXTCHAR            GET NEXT CHARACTER                           
         EJECT                                                                  
*                                  END OF LINE SYMBOL ENCOUNTERED               
*                                  STORE CURRENT COUNTS                         
B100     CLI   FMTSW,0             FMTSW = OFF ?                                
         BC    7,B110              BRANCH ON NO                                 
         STH   6,OFFCNT                                                         
         B     B200                                                             
B110     STH   6,ONCNT1                                                         
*                                                                               
B200     LH    6,OFFCNT                                                         
         LA    1,OFFAREA                                                        
         LA    0,80                SET COUNTER                                  
         LTR   6,6                 ZERO FMT OFF BYTES TO MOVE ?                 
         BC    7,B205              BRANCH ON NO                                 
         CLI   OFFIND,255          RECEIVE FMT OFF SYMBOL ?                     
         BC    8,B202              BRANCH IF YES                                
         SR    0,0                 CLEQR COUNTER                                
         B     B300                                                             
B202     EQU   *                                                                
         BAS   9,CWA1A             SEND 24 X'00'                                
         B     B210                                                             
B205     BAS   9,MVCWA             MOVE OUTPUT TO CWA                           
         XC    OFFCNT,OFFCNT       ZERO COUNTER                                 
B210     OC    ONCNT1(2),ONCNT1    ZERO ?                                       
         BC    8,B400              BRANCH ON YES                                
         MVI   0(3),X'7B'          MOVE POUND SIGN                              
         LA    3,1(3)              BUMP ADDRESS                                 
         BCTR  0,0                 ADJUST COUNTER                               
*                                  TRANSFER FORMATTED FIELDS                    
B300     LH    6,ONCNT1                                                         
         LA    1,ONAREA1                                                        
         BAS   9,MVCWA             MOVE TO CWA                                  
*                                  TEST IF EOL TO BE SENT                       
B400     SH    0,ONCNT1            COUNT IS IN R0 (OFFCNT ALREADY SUBT)         
B420     LTR   0,0                 C6UNT LESS THAN 0 ?                          
         BC    8,B450              BRANCH ON EQUAL                              
         BC    2,B440              BRANCH ON HIGH                               
         AH    0,=H'80'                                                         
         B     B420                                                             
B440     CLI   LASTSW,0                                                         
         BNZ   B450                                                             
         MVI   0(3),X'15'          SEND NEW LINE SYMBOL                         
         LA    3,1(3)                                                           
B450     BAS   9,CLEAR             CLEAR C6UNTERS, ETC                          
         CLI   LASTSW,0            TEST LASTSW                                  
         BC    8,NEXTCHAR          BRANCH IF NOT LAST                           
*                                  COMPUTE MESSAGE LENGTH                       
         L     2,VCWA              BUFFER ADDRESS                               
         MVI   0(R3),HT                                                         
         MVI   1(R3),HT                                                         
         MVI   2(R3),ETX                                                        
         LA    R3,3(R3)            BUMP FOR 3 CNTRL CHARS                       
         SR    R3,R2               GET OUTPUT LENGTH                            
         SH    R2,=H'8'            BACK UP TO HEADER                            
         STH   R3,6(R2)            STORE LENGTH                                 
EXIT     XMOD  1                                                                
         EJECT                                                                  
MVCWA    LTR   6,6                 0 BYTES LEFT ?                               
         BCR   12,9                RETURN ON YES                                
         CH    6,=H'80'            LESS THAN 80 BYTES LEFT ?                    
         BC    4,CWA1              BRANCH ON YES                                
         MVC   0(80,3),0(1)        MOVE 80 BYTES TO CWA                         
         SH    6,=H'80'            DECREMENT COUNTER                            
         LA    1,80(1)             BUMP POINTER                                 
         LA    3,80(3)             BUMP POINTER                                 
         B     MVCWA                                                            
CWA1     OC    OFFCNT(2),OFFCNT    DOING FMT OFF CHARACTERS ?                   
         BC    8,CWA3              BRANCH ON NO                                 
CWA1A    CH    6,=H'24'            AT LEAST 24 BYTES FOR THIS LINE ?            
         BC    10,CWA2             BRANCH ON YES                                
         LA    6,24                SET TO MOVE 24 BYTES                         
*                                  NOTE TRAILING BYTES ARE X'00'                
CWA2     SR    0,6                 DECREMENT LINE END COUNTER                   
CWA3     BCTR  6,0                 SET FOR EXECUTE                              
         EX    6,MOVE                                                           
         LA    6,1(6)              RESTORE COUNTER                              
         AR    3,6                 BUMP ADDRESS                                 
         BR    9                   RETURN                                       
MOVE     MVC   0(0,3),0(1)                                                      
*                                                                               
*                                                                               
CLEAR    SR    6,6                 CURRENT BYTE COUNT                           
         CLI   FMTSW,0             FORMAT OFF ?                                 
         BC    7,CLEAR1            BRANCH ON NO                                 
         LA    1,OFFAREA           RESET POINTER TO START OF AREA               
         B     CLEAR2                                                           
CLEAR1   LA    1,ONAREA1           RESET POINTER TO START OF AREA               
CLEAR2   MVI   OFFIND,0            RESET SWITCH                                 
         XC    ONCNT1(6),ONCNT1    COUNTERS                                     
         LA    0,10                CLEAR OUTPUT AREAS - 2000 BYTES              
         LA    7,ONAREA1                                                        
CLEAR3   XC    0(200,7),0(7)                                                    
         LA    7,200(7)                                                         
         BCT   0,CLEAR3                                                         
         BR    9                   RETURN                                       
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*FASCREQUS                                                                      
       ++INCLUDE FASCREQUS                                                      
         EJECT                                                                  
WORK     DSECT                                                                  
VTWA     DS    A                                                                
VCWA     DS    A                                                                
ONCNT1   DS    H                                                                
ONCNT2   DS    H                                                                
OFFCNT   DS    H                                                                
FMTSW    DS    C                   0 MEANS FORMAT OFF, FF MEANS ON              
OFFIND   DS    C                                                                
LASTSW   DS    C                                                                
         DS    C                   ALIGNMENT                                    
ONAREA1  DS    1000C                                                            
OFFAREA  DS    1000C                                                            
TWA      DSECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004FATO2260  05/01/02'                                      
         END                                                                    
