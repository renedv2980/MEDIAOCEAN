*          DATA SET PPAOREAD   AT LEVEL 014 AS OF 05/01/02                      
         TITLE 'MODULE TO READ AOR CONTRACCTS'                                  
*CATALP MODAOR                                                                  
*   PARAMETERS PASSED TO THIS MODULE                                            
*                                                                               
* WORD                                                                          
*=======                                                                        
*  1  *  ADDRESS OF PRTDIR KEY -- NEEDED TO RESTORE D/M POINTERS                
*     *  -----> BYTE 0 MUST BE                                                  
*     *  -----> C'Y' SET BY CALLER SIGNIFYFING IF CALL IS ON-LINE               
*     *  ...OTHERWISE PGM ASSUMES OFF-LINE                                      
*=======                                                                        
*  2  *  ADDRESS OF AOR CONTRACT KEY (25). THE INSERT DATE MUST                 
*     *  BE IN POSTION +25 YYMMDD (HEX) FORMAT                                  
*     *  -----> BYTE 0 ON RETURN X'80' = NO REC FOUND                           
*=======                                                                        
*  3  *  ADDRESS OF CALLING PGM I/O AREA                                        
*=======                                                                        
*  4  *  ADDRESS OF DATA MANAGER                                                
*=======                                                                        
*  5  *  ADDRESS OF AOR WORK AREA 16 BTYES.                                     
*     *  -----> ON LINE CALLS - THIS AREA M/B IN TWA                            
*     *  -----> OFF LINE MUST BE WITHIN CALLING PGM.                            
*     *           MUST BE CLEARED TO BIN 0 THE FIRST TIME                       
*=======                                                                        
*  6  *  ADDRESS OF 'FASWITCH' IF ON-LINE                                       
*     *             'UTL' IF OFF-LINE                                           
*=======                                                                        
*==================================================================             
         EJECT                                                                  
         PRINT NOGEN                                                            
*                                                                               
MODAOR   CSECT                                                                  
*                                                                               
         NMOD1 0,AORMOD,R7                                                      
         MVC   SAVEPTRS(L'SAVEPTRS*6),0(R1)                                     
         ST    R1,R1PTR            SAVE R1                                      
*                                                                               
* DETERMINE IF SE NUMBERS HAVE BEEN LOOKED UP FOR AOR AND CALLING               
*        AGENCY                                                                 
*                                                                               
**       ===================== **                                               
         L     RF,SAAORWK           SAVED WORK AREA                             
         USING CALLWORK,RF                                                      
         L     RE,SACONKEY         CONTRACT KEY                                 
         USING PCONRECD,RE                                                      
**       ===================== **                                               
         CLC   CALAORPC,PCONKAGY   SAME POWER CODE                              
         BNE   RDFORAS             READ FOR AOR SE NUMBER                       
         CLI   CALAPSEN,0                                                       
         BE    *-2                 SHOULD NOT HAPPEN                            
         B     AORGETCO            GET CONTRACT/ CONTROL READ                   
         SPACE 2                                                                
RDFORAS  DS    0H                  SE# NOT FOUND YET.                           
*              POWER CODES FOR AOR AND BRAND AGENCY PROVIDED.                   
*              NOW MUST LOOK UP SIGN ON CODE IN TABLE                           
*              BEFORE GETTING SE# FOR EACH                                      
*                                                                               
         L     R3,SACONKEY         POINT TO AOR CONTRACT KEY                    
         BAS   RE,FINDSION         FIND SIGN ON CODE                            
**       ===================== **                                               
         L     RF,SAAORWK                                                       
         USING CALLWORK,RF                                                      
**       ===================== **                                               
         MVC   CALAORPC(8),PPOWRCOD SAVE AOR PC, S/O AND SE#                    
         SPACE 3                                                                
         L     R3,SARESTPT         POINT TO CALLING POWER CODE                  
         BAS   RE,FINDSION         FIND SIGN ON CODE                            
**       ===================== **                                               
         L     RF,SAAORWK                                                       
         USING CALLWORK,RF                                                      
**       ===================== **                                               
         MVC   CALPC(8),PPOWRCOD   SAVE AOR PC, S/O AND SE#                     
*                                                                               
*-------> AT THIS POINT CONTROL FILE HAS BEEN READ IN AND SE #                  
*----      HAS BEEN FOUND FOR AOR AND CALLING AGENCY AND SAVED                  
*----       IN THE CALLING PGM SAVE AREA                                        
*                                                                               
AORGETCO DS    0H                  PROCEEDE WITH READING CONTRACT               
**       ===================== **                                               
         L     RF,SAAORWK          CHECK TO SEE IF AOR AND CALLING              
         USING CALLWORK,RF         AGENCY ARE ON THE SAME PRINT FILE            
**       ===================== **                                               
         CLC   CALSE,CALAPSEN                                                   
         BE    NOSWITCH                                                         
         LA    R3,CALAPSEN         AOR SE NUMBER                                
         BAS   RE,SWIT2SE                                                       
*                                                                               
         CH    R1,=H'255'          REMOVE AFTER TESTING                         
         BE    *-2                 FOR TESTING IF ERROR PASS MESSAGE            
*                                                                               
NOSWITCH BAS   RE,FINDCONT         FIND THE CONTRACT                            
**       ===================== **                                               
         L     RF,SAAORWK          CHECK TO SEE IF AOR AND CALLING              
         USING CALLWORK,RF         AGENCY ARE ON THE SAME PRINT FILE            
**       ===================== **                                               
         CLC   CALSE,CALAPSEN                                                   
         BE    XIT                 ******** DONE **********                     
         LA    R3,CALSE            CALLING SE NUMBER                            
         BAS   RE,SWIT2SE                                                       
*                                                                               
         CH    R1,=H'255'                                                       
         BE    *-2                 FOR TESTING IF ERROR PASS MESSAGE            
*                                                                               
*        =======  *                                                             
         DROP RF                                                                
*        =======  *                                                             
         L     RF,SADATAM                                                       
         L     R3,SARESTPT                                                      
         MVC   KEYSAVE,0(R3)                                                    
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'PRTDIR',KEYSAVE,KEY                      
         CLC   KEY,KEYSAVE                                                      
         BNE   *-2                                                              
         B     XIT                                                              
         TITLE 'SUB ROUTINES     '                                              
*******************************************************************             
*             SWITCH TO REQUESTED PRINT FILE                      *             
*******************************************************************             
*                                                                               
SWIT2SE  NTR1                      R3 POINTS TO SE NUMBER                       
         L     RF,R1PTR                                                         
         CLI   0(RF),C'Y'          ON-LINE                                      
         BNE   SOFFLINE                                                         
*                                                                               
*------------> ON-LINE SWITCHING                                                
*                                                                               
         MVC   DMCB,=X'FFFFFFFF'                                                
         MVC   DMCB(1),0(R3)       SE NUMBER                                    
         L     RF,SAAFASW          ADDRESS OF FASWITCH                          
         GOTO1 (RF),DMCB,,0                                                     
         CLI   4(R1),0                                                          
         BE    XIT                                                              
*============================*                                                  
         L     RF,SAAORWK                                                       
         USING CALLWORK,RF                                                      
*============================*                                                  
         CLC   0(1,R1),CALSE       IF PROBLEM WITH CALLING SE                   
         BE    *-2                                                              
         LA    R1,255              ERROR                                        
*        ====  ==            *                                                  
         DROP  RF                                                               
*        ====  ==            *                                                  
         XIT1  REGS=(R1)                                                        
*                                                                               
*================> OFF-LINE SWITCHING                                           
*                                                                               
SOFFLINE DS    0H                                                               
         L     RF,SADATAM                                                       
         GOTO1 (RF),DMCB,=C'DMCLSE',=C'PRNT'                                    
         L     RE,SAAUTL                                                        
         MVC   4(1,RE),0(R3)       MOVE SE NUMBER                               
         GOTO1 (RF),DMCB,=C'DMOPEN',=C'PRNT',FLIST  ??,ADBUY???                 
         B     XIT                                                              
         EJECT                                                                  
*******************************************************************             
*             GET APPROPRIATE CONTRACT                            *             
*******************************************************************             
*                                                                               
FINDCONT NTR1                                                                   
         L     RF,SACONKEY                                                      
         MVC   KEYSAVE(25),0(RF)                                                
         MVC   IOAREA,SACONREC     IOAREA                                       
         L     RF,SADATAM                                                       
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'PRTDIR',KEYSAVE,KEY                      
         B     FCERRCK                                                          
*                                                                               
FCNEXT   GOTO1  (RF),DMCB,=C'DMRSEQ',=C'PRTDIR',KEYSAVE,KEY                     
*                                                                               
FCERRCK  CLC   KEY(13),KEYSAVE                                                  
         BE    FIDATES                                                          
FCERR    L     RF,R1PTR                                                         
         MVI   8(RF),X'80'         NO REC FOUND                                 
         B     XIT                                                              
*                                                                               
*  DETERMINE IF WITHIN START & END DATES                                        
*                                                                               
FIDATES  DS    0H                                                               
         MVC   IOAREA,SACONREC                                                  
         GOTO1 (RF),DMCB,=C'GETREC',=C'PRTFILE',KEY+27                          
         CLI   DMCB+8,0                                                         
         BNE   FCERR                                                            
**       ===================== **                                               
         L     R3,IOAREA                                                        
         USING PCONRECD,R3                                                      
**       ===================== **                                               
         LA    R3,33(R3)                                                        
         CLI   0(R3),X'10'                                                      
         BE    FCGOTIT                                                          
         MVI   ELCODE,X'10'                                                     
         BAS   RE,FIRSTEL                                                       
         BNE   *-2                                                              
FCGOTIT  L     RE,SACONKEY                                                      
         LA    RE,25(RE)           POINT TO CALLER'S START DATE                 
         CLC   0(3,RE),2(R3)                                                    
         BL    FCNEXT              GET NEXT CONTRACT                            
         CLC   0(3,RE),5(R3)                                                    
         BNH   XIT                                                              
         MVC   KEYSAVE,KEY                                                      
         L     RF,SADATAM                                                       
         GOTO1 (RF),=C'DMRSEQ',=C'PRTDIR',KEYSAVE,KEY                           
         B     FCERRCK                                                          
         SPACE 3                                                                
         EJECT                                                                  
*                                                                               
*******************************************************************             
*             FIND SIGN ON FROM POWER CODE                        *             
*******************************************************************             
*                                                                               
FINDSION NTR1                                                                   
         LA    RF,PPAORTAB                                                      
AORTL    CLC   0(2,R3),0(RF)                                                    
         BE    AORPCFND                                                         
         LA    RF,L'PPOWRCOD+L'PPSIGNON+L'PPSSEE(RF)                            
         CLI   0(RF),255                                                        
         BNE   AORTL                                                            
         DC    H'0'                                                             
AORPCFND DS    0H                                                               
         MVC   PPOWRCOD(7),0(RF) MOVE POWER CODE AND SIGN ON                    
         BAS   RE,RDFORSE1         READ CONTROL FILE                            
         B     XIT                                                              
         SPACE 3                                                                
         EJECT                                                                  
*******************************************************************             
*             READ CONTROL FILE TO FIND SE #                      *             
*******************************************************************             
RDFORSE1 NTR1                                                                   
         DS    0H                                                               
         XC    KEY,KEY                                                          
*========================*                                                      
         LA    R3,KEY                                                           
         USING CTIKEY,R3                                                        
*========================*                                                      
         MVI   CTIKTYP,C'I'        BUILD KEY FOR ID RECORD                      
         MVC   CTIKID(L'PPSIGNON),PPSIGNON                                      
         OC    CTIKID,=CL10'       '                                            
         L     R2,SACONREC                                                      
         L     RF,SADATAM                                                       
*                                                                               
*------------->      READ ID RECORD ON CONTROL FILE                             
*                                                                               
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'CTFILE',KEY,(R2) CONTROL FILE            
*                                                                               
         CLC   CTIKEY(23),0(R2)        DID WE GET THE RECORD                    
         BE    CONFOUND                                                         
         SPACE 1                                                                
         L     R1,R1PTR            ADDRESS CALLING LIST                         
         MVI   4(R1),X'80'                                                      
         B     XIT                                                              
*                                                                               
*                                                                               
CONFOUND LR    R3,R2               FOR GETEL                                    
         LA    R3,28(R3)           POINT TO FIRST ELEMENT                       
         SPACE 1                                                                
         MVI   ELCODE,X'21'        GET SYSTEM ELEMENT FOR ACC                   
         BAS   RE,FIRSTEL                                                       
         B     *+8                                                              
USERV6   BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
**       ===================== **                                               
         USING CTSYSD,R3                                                        
**       ===================== **                                               
         CLI   CTSYSNUM,4          NEED PRINT SYSTEM                            
         BNE   USERV6                                                           
         MVC   PPSSEE,CTSYSSE     SAVE ACCPAK SE NUMBER                         
XIT      XIT1                                                                   
*                                                                               
         TITLE 'WORKING STORAGE'                                                
*                                                                               
R1PTR    DS    F                                                                
DMCB     DC    8F'0'                                                            
         ORG   DMCB                                                             
COMMAND  DS    F                                                                
FILE     DS    F                                                                
AOFKEY   DS    F                                                                
IOAREA   DS    F                                                                
         ORG   *                                                                
         SPACE 2                                                                
*                                                                               
SAVEPTRS DS    0F                                                               
SARESTPT DS    F                   POINT TO PRTDIR RESTORE KEY                  
SACONKEY DS    F                   POINT TO CONTRACT KEY                        
SACONREC DS    F                   CALLING I/O AREA                             
SADATAM  DS    F                   DATA MANAGER                                 
SAAORWK  DS    F                   CALLING PGM WORK AREA FOR AOR MODULE         
SAAFASW  DS    F                   POINT TO FASWITCH                            
SAAUTL   EQU   SAAFASW                                                          
*************************                                                       
FLIST    DC    CL8'PRTFILE'                                                     
         DC    CL8'PRTDIR'                                                      
         DC    CL8'X'                                                           
PPAORTAB DS    0CL800                                                           
PPOWRCOD DS    CL2                 POWER CODE                                   
PPSIGNON DS    CL5                 SIGN ON CODE                                 
PPSSEE   DS    XL1                 SE CODE                                      
         ORG   PPAORTAB+800                                                     
       ++INCLUDE PPAORTAB                                                       
KEY      DS    XL40                                                             
KEYSAVE  DS    XL32                                                             
ELCODE   DS    CL1                                                              
         SPACE 3                                                                
         GETEL (R3),DISP,ELCODE                                                 
DISP     DC    H'0'                                                             
         LTORG                                                                  
CALLWORK DSECT                                                                  
CALAORPC DS    CL2                 AOR POWER CODE                               
CALAORSO DS    CL5                 AOR SIGN ON                                  
CALAPSEN DS    XL1                 SE NUMBER                                    
*                                                                               
CALPC    DS    CL2                 CALLING SYSTEM PC                            
CALSO    DS    CL5                 CALLING SIGN ONJ                             
CALSE    DS    XL1                 CALLING SE NUMBER                            
******************                                                              
         SPACE 3                                                                
PCONRECD DSECT                                                                  
       ++INCLUDE PCONREC                                                        
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
