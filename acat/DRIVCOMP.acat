*          DATA SET DRIVCOMP   AT LEVEL 022 AS OF 08/13/92                      
*CATALP DRIVCOMP                                                                
         TITLE 'DRIVCOMP - COMPUTATIONAL HANDLER FOR DRIVER'                    
DRIVCOMP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 100,**DCMP**                                                     
         USING DCMD,RC                                                          
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         EJECT                                                                  
*              CONTROL OF OPERANDS                                              
         SPACE 3                                                                
         L     R2,4(R1)            PARA2=A(COMP ELEMENT)                        
         USING DRCMD,R2                                                         
         L     R5,8(R1)            PARA3=A(RECORD)                              
*                                  RESULT WILL BE RETURNED IN DUB               
         ZAP   CDUB,=P'0'                                                       
         ZAP   DUB,=P'0'                                                        
         ZIC   R4,DRCMNEXP         R4=NUMBER OF EXPRESSIONS                     
         LA    R2,DRCMEXP          NOW R2 WILL ADDRESS AN EXPRESS.              
         USING DRCMEXP,R2                                                       
         SPACE 1                                                                
COMP2    CLI   DRCMTYPE,1          OPERAND IS IN RECORD                         
         BE    COMPLIT                                                          
         L     R3,DRCMAIN          PICK UP ASSOCIATED IN                        
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         USING DRIND,R3                                                         
         LH    RE,DRINDISP                                                      
         AR    RE,R5               AND DISPLACE TO INPUT FIELD                  
         CLI   DRCMOP,C'V'         FOR V WE NEED TO FIND SPECIFIC LEVEL         
         BNE   COMP4                                                            
         CLC   1(1,R5),DRCMSUB     IF THE LEVEL OF TOTALS IS HIGHER             
         BL    BADCOMP             (LOWER NUMBER) THAN V% LEVEL, IGNORE         
         ZIC   R1,0(R5)                                                         
         BCTR  R1,0                                                             
         SLL   R1,2                                                             
         LA    R1,GLAINTD(R1)                                                   
         L     R1,0(R1)                                                         
         USING GLINTD,R1                                                        
         ZIC   R0,DRCMSUB          (LEVEL NUMBER)                               
         SLL   R0,2                                                             
         LA    R1,GLARECL0                                                      
         DROP  R1                                                               
         AR    R1,R0                                                            
         L     R6,0(R1)            NOW HAVE A(RECORD AT THIS LEVEL)             
         LTR   R6,R6               BUT IF WE DON'T                              
         BZ    BADCOMP             IT'S BETTER TO GET OUT NOW                   
         CLI   0(R6),0                                                          
         BE    BADCOMP                                                          
         CLC   0(1,R6),0(R5)       AND BOTH S/B FOR SAME REC                    
         BNE   BADCOMP                                                          
         CLC   1(1,R5),DRCMSUB     DON'T DO INT COMPS IF TOTALLING              
         BE    COMP4                                                            
         LR    R7,R5                                                            
         L     R5,=A(COMPTEMP)     MAY BE INTERNAL COMPUTES                     
         MOVE  ((R5),4000),(R6)    ON THE TOTAL RECORD                          
         GOTO1 =V(DRIVICMP),DMCB,(RA)   THAT WE HAVE NOT YET DONE               
         LH    RE,DRINDISP                                                      
         AR    RE,R5                                                            
         LR    R5,R7                                                            
         SPACE 1                                                                
COMP4    ZIC   R1,DRINFLEN                                         C02          
         BCTR  R1,0                                                             
         CLI   DRINTYPE,C'B'                                                    
         BE    COMPB                                                            
         CLI   DRINTYPE,C'P'                                                    
         BE    COMPP                                                            
         CLI   DRINTYPE,C'E'                                                    
         BE    COMPE                                                            
         CLI   DRINTYPE,C'M'                                                    
         BE    COMPM                                                            
         CLI   DRINTYPE,C'S'                                                    
         BE    COMPS                                                            
         B     BADCOMP                                                          
         EJECT                                                                  
*              ROUTINES TO CONVERT TO DUB                                       
         SPACE 3                                                                
COMPB    ZIC   R1,0(RE)            BINARY                                       
         CLI   DRINLEN,1                                                        
         BE    COMPBEND                                                         
         LH    R1,0(RE)                                                         
         CLI   DRINLEN,2                                                        
         BE    COMPBEND                                                         
         L     R1,0(RE)                                                         
         CLI   DRINLEN,4                                                        
         BE    COMPBEND                                                         
         CLI   DRINLEN,3                                                        
         BNE   COMPBEND                                                         
         SRA   R1,8                                                             
         SPACE 1                                                                
COMPBEND CVD   R1,DUB                                                           
         B     COMPUTE                                                          
         SPACE 1                                                                
COMPM    BCTR  R1,0                MASKED - GET PAST MASK BYTE                  
         LA    RE,1(RE)                                                         
         SPACE 1                                                                
COMPP    DS    0H                  PACKED                                       
         LA    RF,0(R1,RE)         POINT TO BYTE CONTAINING SIGN   C01          
         OI    0(RF),12            SLAP IN SIGN                    C01          
         EX    R1,*+8                                                           
         B     COMPUTE                                                          
         ZAP   DUB,0(0,RE)                                                      
         SPACE 1                                                                
COMPS    ZIC   RF,0(RE)            SCALED - FIRST BYTE IS SCALE                 
         TM    0(RE),X'80'         GET SCALE INTO RF                            
         BNO   COMPS2                                                           
         SLL   RF,25                                                            
         SRL   RF,25                                                            
         LCR   RF,RF                                                            
         SPACE 1                                                                
COMPS2   BCTR  R1,0                GET PAST SCALE BYTE                          
         LA    RE,1(RE)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         ZAP   DUB,0(0,RE)                                                      
         SRP   DUB,0(RF),0         ADJUST FOR SCALE                             
         B     COMPUTE                                                          
         SPACE 1                                                                
COMPE    EX    R1,*+8              NUMERIC EBCDIC                               
         B     COMPUTE                                                          
         PACK  DUB,0(0,RE)                                                      
         SPACE 1                                                                
COMPLIT  L     R1,DRCMLIT          SELF CONTAINED (BINARY) LITERAL              
         CVD   R1,DUB                                                           
         EJECT                                                                  
*              NOW CARRY OUT THIS PIECE OF THE COMPUTATION                      
         SPACE 3                                                                
COMPUTE  CLI   DRCMOP,C'='                                                      
         BE    COMPEQU                                                          
         CLI   DRCMOP,0                                                         
         BE    COMPEQU                                                          
         CLI   DRCMOP,C'+'                                                      
         BE    COMPADD                                                          
         CLI   DRCMOP,C'-'                                                      
         BE    COMPSUB                                                          
         CLI   DRCMOP,C'X'                                                      
         BE    COMPMULT                                                         
         CLI   DRCMOP,C'/'                                                      
         BE    COMPDIV                                                          
         CLI   DRCMOP,C'I'                                                      
         BE    COMPIX                                                           
         CLI   DRCMOP,C'%'                                                      
         BE    COMPPCT                                                          
         CLI   DRCMOP,C'V'                                                      
         BE    COMPPCT                                                          
         DC    H'0'                                                             
         SPACE 1                                                                
COMPEQU  ZAP   CDUB,DUB            EQUALS                                       
         B     COMPNEXT                                                         
         SPACE 1                                                                
COMPADD  AP    CDUB,DUB            ADD                                          
         B     COMPNEXT                                                         
         SPACE 1                                                                
COMPSUB  SP    CDUB,DUB            SUBTRACT                                     
         B     COMPNEXT                                                         
         SPACE 1                                                                
COMPMULT MP    CDUB,DUB            MULTIPLY                                     
         B     COMPNEXT                                                         
         SPACE 1                                                                
COMPDIV  ZAP   FACTOR,=P'2'        DIVIDE                                       
         B     COMPDIV2                                                         
         SPACE 1                                                                
COMPIX   ZAP   FACTOR,=P'200'      INDEX                                        
         B     COMPDIV2                                                         
         SPACE 1                                                                
COMPPCT  ZAP   FACTOR,=P'20000'    PERCENT                                      
         SPACE 1                                                                
COMPDIV2 CP    DUB,=P'0'                                                        
         BNE   COMPDIV4                                                         
         ZAP   CDUB,=P'0'                                                       
         B     COMPNEXT                                                         
         SPACE 1                                                                
COMPDIV4 MP    CDUB,FACTOR                                                      
         ZAP   BIG,CDUB                                                         
         DP    BIG,DUB                                                          
         ZAP   CDUB,BIG(8)         ROUND                                        
         BM    *+10                                                             
         AP    CDUB,=P'1'                                                       
         ZAP   BIG,CDUB                                                         
         DP    BIG,=PL8'2'                                                      
         ZAP   CDUB,BIG(8)                                                      
         SPACE 1                                                                
COMPNEXT LA    R2,L'DRCMEXP(R2)                                                 
         BCT   R4,COMP2                                                         
         ZAP   DUB,CDUB                                                         
         B     XIT                                                              
         SPACE 1                                                                
BADCOMP  ZAP   DUB,=P'0'                                                        
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG ETC.                                                       
         SPACE 1                                                                
         LTORG                                                                  
COMPTEMP DS    4000C                                                            
         EJECT                                                                  
*              DSECT FOR COMPUTATION                                            
         SPACE 3                                                                
DCMD     DSECT                                                                  
CDUB     DS    CL16                                                             
BIG      DS    CL16                                                             
FACTOR   DS    PL6                                                              
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022DRIVCOMP  08/13/92'                                      
         END                                                                    
