*          DATA SET SPGETSNV   AT LEVEL 059 AS OF 03/04/21                      
*CATALP SPGETSNV                                                                
GETSNV   TITLE 'SPGETSNV- MODULE TO READ SNV RECORDS FOR SI2 AND $MAT'          
***********************************************************************         
* NOTE: SEE SPSNVBLK FOR DSECTS                                                 
*                                                                               
* ON ENTRY:    PARAM1   BYTES 1-3  A(CONTROL BLOCK)                             
***********************************************************************         
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-46696  02/22/21 CARRY AND DISPLAY 4 BYTE DEMO VALUES      *         
***********************************************************************         
         SPACE 2                                                                
GETSNV   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,GETSNV,CLEAR=YES                                          
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     RA,0(R1)                                                         
         ST    RA,AINBLCK                                                       
         USING SBLD,RA                                                          
         LA    R5,MINBLOCK                                                      
         USING MINBLKD,R5                                                       
         ST    RD,SAVERD                                                        
*                                                                               
         BAS   RE,PROGINIT         PROGRAM INITIALIZATION                       
*                                                                               
         BAS   RE,BUILDBUF         BUILD THE BUFFER                             
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE INITIALIZES SYSTEM VARIABLES                                     
***********************************************************************         
PROGINIT NTR1                                                                   
         OC    SBLIEND,SBLIEND     ANY DATA IN INOVICE TABLE?                   
         BNZ   PINIT10                                                          
         MVC   SBLIEND,SBLAITB     NO, SET END TO BE THE BEGINNING              
*                                                                               
PINIT10  MVI   SBLSTAT,0           NO OVERFLOW ERRORS YET                       
         MVC   COMFACS,SBLACOM     SET UP COMMONLY USED ADDRESSES               
         L     R1,COMFACS                                                       
         USING COMFACSD,R1                                                      
         MVC   ADDAY,CADDAY                                                     
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         MVC   GETDAY,CGETDAY                                                   
         MVC   HELLO,CHELLO                                                     
         MVC   VMINIO,CMINIO                                                    
         OC    VMINIO,VMINIO                                                    
         BNZ   PINIT12                                                          
         MVC   VMINIO,=V(MINIO)   IF NO COMFACS MINIO, SEE IF LINKED            
         OC    VMINIO,VMINIO                                                    
         BNZ   PINIT12                                                          
         DC    H'0'               NO MINIO                                      
         DROP  R1                                                               
*                                                                               
PINIT12  DS    0H                                                               
*                                  DETERMINE CABLE/NET STATUS                   
         MVI   CBLSW,C'N'                                                       
         MVI   BCBLNET,0                                                        
         CLI   SBLSTA,X'E8'        CABLE HEADS START WITH X'E8'                 
         BL    PINIT14                                                          
         MVI   CBLSW,C'Y'                                                       
         MVC   BCBLNET,SBLSTA+2    NETWORK                                      
         NI    BCBLNET,X'7F'       SEVEN BITS ONLY                              
*                                                                               
PINIT14  LA    RF,INOLSTL          LENGTH WITHOUT DIGITAL INFO                  
         CLI   SBLILEN+1,IELI3LEN  DIGITAL REQUEST?                             
         BL    *+8                 NO                                           
         LA    RF,INOLSTL2         LENGTH WITH DIGITAL INFO                     
         STH   RF,INOLSTQ          LENGTH OF INVOICE TABLE                      
*                                                                               
         LA    RF,1                GET STARTING INTIDNUM                        
         L     R2,SBLAINOL                                                      
*                                                                               
PINIT15  DS    0H                  COUNT INVS ALREADY IN LIST                   
         CLI   0(R2),C' '          (FROM OLD $INV INVOICES)                     
         BNH   PINIT16                                                          
         LA    RF,1(RF)                                                         
         AH    R2,INOLSTQ                                                       
         B     PINIT15                                                          
*                                                                               
PINIT16  STCM  RF,3,NEWSTART                                                    
*                                                                               
PINITX   B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE BUILDS THE INVOICE DETAIL BUFFER                                 
***********************************************************************         
BUILDBUF NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING SNVKEYD,R3                                                       
         MVI   SNVKTYPE,SNVKTYPQ                                                
         MVI   SNVKSUB,SNVKSUBQ                                                 
         MVC   SNVKAM,SBLAGYMD                                                  
         MVC   SNVKCLT,SBLCLT                                                   
         MVC   SNVKSTA,SBLSTA                                                   
         CLI   SBLSTA,X'E8'        IF CABLE HEAD                                
         BL    BLDB04                                                           
         CLI   SBLCMOD,C'Y'        AND NEW MODE                                 
         BNE   BLDB04                                                           
         NI    SNVKSTA+2,X'80'     STRIP NET BECAUSE NEED TO LOOK AT            
*                                  NNNN/ALL REC                                 
BLDB04   DS    0H                                                               
         BAS   RE,XSPDHIGH                                                      
         CLI   DMCB+8,0                                                         
         BNE   BBUFX               END-OF-FILE THEN                             
*                                                                               
BBUFLOOP CLI   SNVKTYPE,SNVKTYPQ   STILL AN SNV RECORD?                         
         BNE   BBUFX                                                            
         CLI   SNVKSUB,SNVKSUBQ                                                 
         BNE   BBUFX                                                            
         CLI   CBLSW,C'Y'          DOING CABLE HEAD?                            
         BNE   BBUFL4                                                           
         CLI   SBLCMOD,C'Y'        NEW CABLE MODE?                              
         BNE   BBUFL4                                                           
*                                                                               
         CLC   SNVKAM(SBLSTA-SBLAGYMD),SBLAGYMD   TEST THRU CLT                 
         BNE   BBUFX                                                            
*                                  TEST STATION(3)                              
         MVC   DUB(3),SBLSTA       TEST RIGHT CABLE HEAD                        
         NI    DUB+2,X'80'                                                      
         MVC   DUB+3(3),SNVKSTA                                                 
         NI    DUB+5,X'80'                                                      
         CLC   DUB(3),DUB+3                                                     
         BE    BBUFL5                                                           
         B     BBUFNXRC                                                         
*                                                                               
BBUFL4   DS    0H                                                               
*                                  YES, FOR THE SAME AGY/MED/CLT/STA?           
         CLC   SNVKAM(SBLPRD-SBLAGYMD),SBLAGYMD                                 
         BNE   BBUFX                    NO                                      
*                                                                               
BBUFL5   DS    0H                                                               
         MVC   HALF,SNVKMOS        COMPLEMENT THE MONTH OF SERVICE              
         XC    HALF,=X'FFFF'                                                    
         CLC   HALF,SBLEDAT        IS MOS HIGHER THAN END?                      
         BH    BBUFNXRC                                                         
         CLC   HALF,SBLMOS         OR LOWER THAN START?                         
         BNL   BBUFL6                                                           
         CLI   CBLSW,C'Y'          EXCEPT FOR CABLE HEAD                        
         BNE   BBUFX                                                            
         CLI   BCBLNET,0           WITH NO SPECIFIC NET REQ'D                   
         BNE   BBUFX               CAN'T HAVE ANYMORE AT THIS POINT             
         B     BBUFNXRC                                                         
*                                                                               
BBUFL6   DS    0H                                                               
         MVI   GLOBLCBN,0          SET GLOBAL CABLE NET                         
         CLI   CBLSW,C'Y'                                                       
         BNE   *+14                                                             
         MVC   GLOBLCBN,SNVKSTA+2                                               
         NI    GLOBLCBN,X'7F'      7 BITS ONLY                                  
*                                                                               
         MVC   SVSNVKEY,SNVKEY     YES                                          
         MVC   BSTA,SNVKSTA        SAVE INFO FOR MINIO                          
         MVC   BMOSS,SNVKMOS                                                    
         MVC   QINVOICE,SNVKINV                                                 
*                                                                               
         BRAS  RE,MNIOINIT                                                      
*                                                                               
         MVI   INVMGR,C'N'                                                      
         XC    MINEKEY,MINEKEY     SEE IF WE HAVE INV MANAGER ELEM              
         MVI   MINEKEY,SNVIMELQ    X'15'                                        
         BAS   RE,MINIOHI          HAVE ONE?                                    
         BNE   BBUFHD00            NO                                           
*                                                                               
         L     R6,MINELEM                                                       
         CLI   0(R6),SNVIMELQ      DID WE GET A X'15' ELEM                      
         BNE   BBUFHD00            NO                                           
         MVI   INVMGR,C'Y'                                                      
*                                                                               
***************                                                                 
* GOING TO GET THE HEADER ELEMENT                                               
***************                                                                 
BBUFHD00 DS    0H                                                               
         XC    MINEKEY,MINEKEY     READ HEADER ELEMENT                          
         MVI   MINEKEY,SNVHDELQ                                                 
         BAS   RE,MINIOHI                                                       
         BE    *+6                                                              
         DC    H'0'                DIE, EVERY ONE SHOULD HAVE HEADER            
*                                                                               
         L     R6,MINELEM                                                       
         USING SNVHDELD,R6                                                      
*                                                                               
         MVC   HALF,SNVHDSDT       PERIOD START DATE                            
         OC    SNVHDSDT,SNVHDSDT   IF NOT PRESENT                               
         BNZ   *+16                                                             
         MVC   HALF,SNVKMOS        USE MOS (YYMM01) FROM KEY                    
         XC    HALF,=X'FFFF'                                                    
*                                                                               
         GOTO1 DATCON,DMCB,(2,HALF),(0,HDRSTDAT)                                
*                                                                               
         OC    SNVHDSDT,SNVHDSDT                                                
         BNZ   BBUFHD02                                                         
*                                                                               
         GOTO1 GETDAY,DMCB,HDRSTDAT,DUB                                         
         CLI   DMCB,0                                                           
         BH    *+6                                                              
         DC    H'0'                                                             
         LLC   R2,DMCB                                                          
         BCTR  R2,0                                                             
         LCR   R2,R2                                                            
         GOTO1 ADDAY,DMCB,HDRSTDAT,HDRSTDAT,(R2)                                
         DROP  R3                                                               
*                                                                               
BBUFHD02 DS    0H                                                               
         GOTO1 DATCON,DMCB,(2,SNVHDIDT),(0,HDRINDAT)    INVOICE DATE            
         XC    HDRBDATC,HDRBDATC                                                
         CLI   SNVHDLEN,SNVHDLN2    ONLY NEW LENGTH ELEM CAN                    
         BL    BBUFHD03             HAVE BATCH DATE                             
         GOTO1 DATCON,DMCB,(3,SNVHDEZB),(2,HDRBDATC)    BATCH DATE              
*                                                                               
BBUFHD03 DS    0H                                                               
         MVC   HDRCTLBT,SNVHDCTL                                                
         MVC   HDRPKG,SNVHDPKG     SAVE PACKAGE                                 
         MVC   HDRREP,SNVHDREP     SAVE REP CODE                                
         MVC   HDREASI,SNVHDEZS                                                 
         MVC   HDRDEMO,SNVHDDEM    DEMO CATEGORY                                
*                                                                               
         MVC   GLOBLPRD,SNVHDPRD   COPY SOME HEADER VALUES                      
         MVC   GLOBLPR2,SNVHDPR2                                                
         XC    GLOBNPRD,GLOBNPRD                                                
         XC    GLOBNPR2,GLOBNPR2                                                
*                                                                               
         CLI   SNVHDLEN,SNVHDLN3   HAVE 3 CHAR PRODUCT?                         
         BL    BBUFHD3A            NO                                           
         MVC   GLOBNPRD,SNVHDAP1   SET 3 CHAR PRODUCT                           
         MVC   GLOBNPR2,SNVHDAP2   SET 3 CHAR PIGGY                             
         CLC   GLOBNPRD,=C'   '    SPACES?                                      
         BNE   *+10                NO                                           
         XC    GLOBNPRD,GLOBNPRD   YES - MAKE BINARY ZEROS                      
         CLC   GLOBNPR2,=C'   '    SPACES?                                      
         BNE   *+10                NO                                           
         XC    GLOBNPR2,GLOBNPR2   YES - MAKE BINARY ZEROS                      
*                                                                               
BBUFHD3A MVC   GLOBLEST,SNVHDEST                                                
*                                                                               
*********                                                                       
* CHECKING EASI/NOEASI                                                          
*********                                                                       
         CLI   SBLEASI,C'B'        BOTH OK?                                     
         BE    BBUFHD04                                                         
         MVI   BYTE,C'Y'                                                        
         CLI   SNVHDEZS,C' '       DO WE HAVE EASI SOURCE?                      
         BH    *+8                                                              
         MVI   BYTE,C'N'                                                        
         CLC   SBLEASI,BYTE                                                     
         BNE   BBUFDTX             REJECT                                       
*********                                                                       
* CHECKING MCT                                                                  
*********                                                                       
BBUFHD04 DS    0H                                                               
         CLI   SBLMCT,C'B'         BOTH MCT AND NON-MCT OK?                     
         BE    BBUFHD06                                                         
         MVI   BYTE,C'Y'                                                        
         TM    SNVHDCTL,SNVHDMCQ                                                
         BO    *+8                                                              
         MVI   BYTE,C'N'                                                        
         CLC   SBLMCT,BYTE                                                      
         BNE   BBUFDTX             REJECT                                       
*********                                                                       
* CHECKING RESPONSE                                                             
*********                                                                       
BBUFHD06 DS    0H                                                               
         CLI   SBLRESP,C'B'        BOTH RESPONSE AND NON-RESP OK?               
         BE    BBUFHD08                                                         
         MVI   BYTE,C'Y'                                                        
         TM    SNVHDCTL,SNVHDRSQ                                                
         BO    *+8                                                              
         MVI   BYTE,C'N'                                                        
         CLC   SBLRESP,BYTE                                                     
         BNE   BBUFDTX             REJECT                                       
*********                                                                       
* CHECKING PRODUCT                                                              
*********                                                                       
BBUFHD08 DS    0H                                                               
         CLI   SNVHDLEN,SNVHDLN3   HAVE 3 CHAR PRODUCT?                         
         BL    BBUFHD8A            NO                                           
         CLC   SNVHDAP1,=C'   '    3 CHAR PRODUCT SPECIFIED IN HEADER?          
         BNH   BBUFHD10            NO, OK                                       
         B     *+12                                                             
*                                                                               
BBUFHD8A CLI   SNVHDPRD,0          PRODUCT SPECIFIED IN HEADER?                 
         BE    BBUFHD10            NO, OK                                       
*                                                                               
* NEED TO CHECK IF PRD IN THIS PRDGRP                                           
         ICM   R8,15,SBLPRDGR      POINT TO CURRENT PRD                         
         BZ    BBUFHD8D            NOT PRD GROUPS                               
*                                                                               
BBUFHD8B CLI   SNVHDLEN,SNVHDLN3   HAVE 3 CHAR PRODUCT?                         
         BL    *+14                NO                                           
         CLC   2(3,R8),SNVHDAP1    MATCH ON 3 CHAR PRODUCT CODE                 
         B     *+10                                                             
         CLC   5(1,R8),SNVHDPRD    MATCH PRODUCT CODE                           
         BE    BBUFHD10            OK                                           
         CLC   0(2,R8),6(R8)       NEXT PRD SAME PRDGRP                         
         BNE   BBUFDTX             NO, NEXT REC                                 
         LA    R8,6(R8)            ELSE NEXT ENTRY                              
         B     BBUFHD8B                                                         
*                                                                               
BBUFHD8D DS    0H                                                               
         CLI   SBLPRD,X'FF'        YES, ACCEPT ANY PRODUCT?                     
         BE    BBUFHD8P            YES, OK - STILL CHECK PIGGY                  
         CLI   SBLPRD,0            (TREAT 00 AS FF)                             
         BNE   BBUFHD8E                                                         
         CLC   SBLNPRD,=C'   '     HAVE 3 CHAR PRODUCT?                         
         BNH   BBUFHD8P            NO, OK - STILL CHECK PIGGY                   
         CLC   SBLNPRD,SNVHDAP1    HAVE A MATCH ON THIS PRODUCT?                
         BE    BBUFHD8P            YES                                          
         B     BBUFDTX           **NOOP OTHER SEQUENCE TEST                     
*                                                                               
BBUFHD8E CLC   SBLPRD,SNVHDPRD     HAVE A MATCH ON THIS PRODUCT?                
         BE    BBUFHD8P            YES                                          
         B     BBUFDTX           **NOOP OTHER SEQUENCE TEST                     
*                                                                               
*        CLC   SBLPRD,SNVHDPR2     NO, SEE IF PIGGYPACK MATCHES                 
*        BNE   BBUFDTX                 NO, CHECK NEXT RECORD                    
*        CLC   SBLPRD2,SNVHDPRD        YES, THEN OTHER CASE HAS TO              
*        BE    BBUFHD10                     YES                                 
*        B     BBUFDTX                      NO, CHECK NEXT RECORD               
*********                                                                       
* CHECKING PIGGYBACK PRODUCT                                                    
*********                                                                       
BBUFHD8P DS    0H                                                               
         CLI   SBLPIGCT,C'1'       SPECIFIC PIGGY REQUEST                       
         BE    BBUFHD8S            THEN MUST MATCH                              
*                                                                               
         CLI   SBLPRD2,0           ACCEPT NO PIGGYS?                            
         BNE   BBUFHD8R                                                         
         CLC   SBLNPRD2,=C'   '    3 CHAR PIGGY?                                
         BH    BBUFHD8R            YES                                          
*                                                                               
         CLI   SNVHDLEN,SNVHDLN3   HAVE 3 CHAR PRODUCT?                         
         BL    BBUFHD8Q            NO                                           
         CLC   SNVHDAP2,=C'   '    3 CHAR PIGGY SPECIFIED IN HEADER?            
         BNH   BBUFHD10            NO, OK                                       
         B     BBUFDTX             PIGGY = NEXT REC                             
*                                                                               
BBUFHD8Q CLI   SNVHDPR2,0          PIGGY SPECIFIED IN HEADER?                   
         BE    BBUFHD10            NO, OK                                       
         B     BBUFDTX             PIGGY = NEXT REC                             
*                                                                               
BBUFHD8R CLI   SBLPRD2,X'FF'       YES, ACCEPT BOTH                             
         BE    BBUFHD10            YES, OK                                      
*                                                                               
BBUFHD8S OC    SBLNPRD2,SBLNPRD2   3 CHAR PRD PASSED IN?                        
         BZ    BBUFHD8T            NO, MUST BE $MATCH!                          
         CLI   SNVHDLEN,SNVHDLN3   HAVE 3 CHAR PRODUCT?                         
         BL    BBUFHD8T            NO                                           
         CLC   SBLNPRD2,SNVHDAP2   HAVE A MATCH ON 3 CHAR PIGGY PROD?           
         B     *+10                                                             
BBUFHD8T CLC   SBLPRD2,SNVHDPR2    HAVE A MATCH ON THIS PIGGY PRODUCT?          
         BNE   BBUFDTX             NO, CHECK NEXT RECORD                        
*********                                                                       
* CHECKING ESTIMATE                                                             
*********                                                                       
BBUFHD10 DS    0H                                                               
         CLI   SNVHDEST,0          ESTIMATE SPECIFIED IN HEADER?                
         BE    BBUFHD11            NO, OK                                       
*                                                                               
         CLI   SBLEST,0            YES, ACCEPT ANY ESTIMATE?                    
         BE    BBUFHD11            YES, OK                                      
*                                                                               
         CLC   SBLEST,SNVHDEST     HAVE A MATCH ON THIS ESTIMATE?               
         BNE   BBUFDTX             NO, CHECK NEXT RECORD                        
*                                                                               
BBUFHD11 DS    0H                                                               
BBUFHD30 MVC   IDTOT,SNVHDTAX      START INV $TOT WITH TAX FROM HEADER          
         CLI   SBLNETP,C'Y'        NETPAK?                                      
         BE    *+10                YES                                          
         MVC   SVHDCT2,SNVHDCT2    SAVED CONTROL BYTE 2                         
*                                                                               
         XC    ISTOT,ISTOT         AND CLEAR SPOT TOTAL                         
*********                                                                       
* PUT CONTRACT/ID IN THE CONTRACT/ID LIST                                       
*********                                                                       
BBUFHD33 DS    0H                                                               
         MVI   CONIDNUM,0          CLEAER ID NUMBER                             
         OC    SNVHDCON,SNVHDCON                                                
         BZ    BBUFHD60                                                         
         MVI   CONIDNUM,X'FF'      ANY NON-ZERO OK OF IID                       
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,15,SBLAIDL       ANY ID LIST?                                 
         BZ    BBUFHD60            NO, IGNORE (NOT USED FOR $MAT)               
         USING IDLISTD,R2                                                       
*                                                                               
         LLC   RF,SBLMXIDS         RF = A(1ST BYTE AFTER CONTRACT LIST)         
         MHI   RF,IDLSTL                USED TO TEST LIST OVERFLOW              
         LA    RF,0(R2,RF)                                                      
         LA    R1,1                R1 = LOOKING AT FIRST CONTRACT               
*                                                                               
BBUFHD40 CLI   IDLID,X'FF'         EOF                                          
         BE    BBUFHD50                                                         
         CLI   IDLID,0             EOF?                                         
         BE    BBUFHD50                                                         
         CLC   SNVHDCON,IDLID      THE CONTRACT IS IN HERE ALREADY?             
         BNE   BBUFHD45            NO                                           
         STC   R1,CONIDNUM                                                      
         B     BBUFHD60            YES                                          
*                                                                               
BBUFHD45 LA    R1,1(R1)            BUMP THE CONTRACT ID NUMBER                  
         LA    R2,IDLSTL(R2)                                                    
         CR    R2,RF               BEYOND CONTRACT ID LIST?                     
         BNE   BBUFHD40                                                         
         OI    SBLSTAT,SBLIDOVQ    YES, CONTRACT ID LIST OVERFLOW               
         B     GSNVEXIT              -> EXIT GETSNV                             
*                                                                               
BBUFHD50 STC   R1,CONIDNUM         CURRENT CONTRACT ID NUMBER                   
         MVC   IDLID,SNVHDCON                                                   
         MVI   IDLSTL(R2),X'FF'    SET NEW EOF                                  
*                                                                               
*********                                                                       
* CHECKING CONTRACT/ID                                                          
*********                                                                       
BBUFHD60 DS    0H                                                               
         CLI   SBLBUYID,C' '       ANY ID FILTER?                               
         BNH   BBUFHD64            NO, OK                                       
         MVC   WORK(12),SNVHDCON                                                
         OC    WORK(12),=12C' '                                                 
         CLC   WORK(12),SBLBUYID   YES, IS A MATCH?                             
         BE    BBUFHD64            YES, OK                                      
         CLC   =C'NONE ',SBLBUYID  YES, ARE WE LOOKING FOR 'NONE'?              
         BE    *+14                                                             
         CLC   =C'**UNKNOWN*',SBLBUYID                                          
         BNE   BBUFDTX                                                          
         CLI   SNVHDCON,C' '       ANY ID PRESENT?                              
         BH    BBUFDTX             YES, REJECT                                  
         MVI   CONIDNUM,0          CLEAR ID NUMBER                              
*                                                                               
BBUFHD64 NI    MISCFLG1,X'FF'-MF1NETAM                                          
         TM    SNVHDCTL,SNVHDNTQ+SNVHDDNQ    NET AMOUNTS?                       
         BZ    *+8                                                              
         OI    MISCFLG1,MF1NETAM   YES                                          
*                                                                               
         MVI   SETINVSW,C'N'       SET HAVE TO DO SETINV                        
*                                                                               
BBUFHDX  DS    0H                                                               
         DROP  R2,R6                                                            
***************                                                                 
* GOING TO GET THE DETAIL ORDER ELEMENT - INTEGRATION OPTIONS                   
***************                                                                 
BBUFIN00 DS    0H                  SEE IF INTEGRATION IS A COLUMN               
         NI    MISCFLG1,X'FF'-MF1INTEG      NO INTEG COLUMN                     
         XC    MINEKEY,MINEKEY     READ DETAIL ORDER ELEM                       
         MVI   MINEKEY,SNVDTELQ    X'20'                                        
         BAS   RE,MINIOHI                                                       
         BNE   BBUFDT00                                                         
*                                                                               
         USING SNVDTELD,R6                                                      
         L     R6,MINELEM                                                       
         CLI   0(R6),X'20'         DID WE GET A X'20' ELEM                      
         BE    BBUFIN08            YES - CHECK COLUMNS FOR INTEG                
         CLI   SBLINT,C'O'         DID THEY ASK FOR INTEGRATION ONLY            
         BE    BBUFDTX             YES THEN SKIP - NO INTEG IF NO ELEM          
         B     BBUFDT00            NO, PROCEED WITH DETAILS                     
*                                                                               
BBUFIN08 LLC   R2,SNVDTLEN         ELEM LEN                                     
         AHI   R2,-7               SUB HEADER LEN - R2=# OF FIELDS              
         LA    R3,SNVDTFLD         START OF LIST                                
BBUFIN10 CLI   0(R3),FLDNINTG      IS INTEGRATION IN THE LIST                   
         BE    BBUFIN20            YES, SET FLAG AND DO DETAILS                 
         LA    R3,1(R3)                                                         
         BCT   R2,BBUFIN10                                                      
*                                                                               
         CLI   SBLINT,C'O'         DID THEY ASK FOR INTEGRATION ONLY            
         BE    BBUFDTX             YES, THEN SKIP - NO INTEG                    
         B     *+8                 CONTINUE TO DETAILS                          
BBUFIN20 OI    MISCFLG1,MF1INTEG   YES INTEGRATION IS A COLUMN                  
         DROP  R6                                                               
*                                                                               
***************                                                                 
* GOING INTO THE DETAILS                                                        
***************                                                                 
BBUFDT00 MVI   SVBPRD,0            CLEAR SAVED BINARY PRODUCT                   
         XC    SVPRD,SVPRD         CLEAR SAVED 3 CHAR PRODUCT                   
         MVI   SVEST,0             CLEAR SAVED ESTIMATE                         
         XC    MINEKEY,MINEKEY     GO THROUGH THE DETAIL ELEMENTS               
         MVI   MINEKEY,SNVIDELQ                                                 
         BAS   RE,MINIOHI                                                       
BBUFDTLP BNE   BBUFDTX                                                          
*                                                                               
         L     R6,MINELEM          MAKE SURE ITS A DETAIL ELEMENT               
         USING SNVIDELD,R6                                                      
         CLI   SNVIDEL,SNVIDELQ                                                 
         BNE   BBUFDTX                                                          
*                                                                               
         LLC   RE,SNVIDLEN         ELEMENT LENGTH                               
         BCTR  RE,0                -1 FOR EXECUTED MVC                          
         EX    RE,*+8               EXECUTE MVC                                 
         B     *+10                SO IDF DOESN'T COMPLAIN                      
         MVC   SVINVCE(0),SNVIDEL  SAVE THE INVOICE DETAIL                      
*                                                                               
         CLI   GLOBNPRD,0          3 CHAR PRODUCT IN HEADER?                    
         BNE   BBUFDT20            YES                                          
         CLI   GLOBLPRD,0          PRODUCT IN HEADER?                           
         BNE   BBUFDT20            YES, OK - SKIP PIGGY CHECK TOO!              
*                                                                               
* NEED TO CHECK IF PRD IN THIS PRDGRP                                           
         ICM   R8,15,SBLPRDGR      POINT TO CURRENT PRD                         
         BZ    BBUFDT08            NOT PRD GROUPS                               
*                                                                               
BBUFDT06 CLI   SNVIDLEN,SNVIDL3Q   HAVE 3 CHAR PRODUCT?                         
         BL    *+14                NO                                           
         CLC   2(3,R8),SNVIDAP1    MATCH ON 3 CHAR PRODUCT CODE                 
         B     *+10                                                             
         CLC   5(1,R8),SNVIDPRD    MATCH PRODUCT CODE                           
         BE    BBUFDT20            OK                                           
         CLC   0(2,R8),6(R8)       NEXT PRD SAME PRDGRP                         
         BNE   BBUFNXDT            NO, NEXT DETAIL                              
         LA    R8,6(R8)            ELSE NEXT ENTRY                              
         B     BBUFDT06                                                         
*                                                                               
BBUFDT08 CLI   SBLPRD,X'FF'        NO, ACCEPT ANY PRODUCT?                      
         BE    BBUFDT10            YES, OK - CHECK PIGGYS                       
         CLI   SBLPRD,0            (TREAT 00 AS FF)                             
         BNE   BBUFDT09                                                         
         CLC   SBLNPRD,=C'   '     HAVE 3 CHAR PRODUCT?                         
         BNH   BBUFDT10            NO, OK - STILL CHECK PIGGY                   
*                                                                               
         TM    SBLFLAG,SBLPRDAL    ALL PRODUCT REQUEST?                         
         BZ    BBUFDT8A            NO                                           
         OC    SVPRD,SVPRD         HAVE A PRD FROM PREVIOUS DETAIL?             
         BNZ   *+10                YES                                          
         MVC   SVPRD,SNVIDAP1      NO - SET THE SAVED PRODUCT                   
         CLC   SVPRD,SNVIDAP1      DOES THIS PRD MATCH THE PREVIOUS?            
         BE    BBUFDT8A            YES                                          
         OI    SBLSTAT,SBLUDERR    NO - FLAG TO PREVENT SRUPD00 DUMPS           
*                                                                               
BBUFDT8A CLC   SBLNPRD,SNVIDAP1    HAVE A MATCH ON THIS PRODUCT?                
         BE    BBUFDT10            YES                                          
         B     BBUFNXDT                                                         
*                                                                               
BBUFDT09 TM    SBLFLAG,SBLPRDAL    ALL PRODUCT REQUEST?                         
         BZ    BBUFDT9A            NO                                           
         CLI   SVBPRD,0            HAVE A PRD FROM PREVIOUS DETAIL?             
         BNE   *+10                YES                                          
         MVC   SVBPRD,SNVIDPRD     NO - SET THE SAVED PRODUCT                   
         CLC   SVBPRD,SNVIDPRD     DOES THIS PRD MATCH THE PREVIOUS?            
         BE    BBUFDT9A            YES                                          
         OI    SBLSTAT,SBLUDERR    NO - FLAG TO PREVENT SRUPD00 DUMPS           
*                                                                               
BBUFDT9A CLC   SBLPRD,SNVIDPRD     NO, MAKE SURE PRODUCTS MATCH                 
         BNE   BBUFNXDT                                                         
*                                                                               
BBUFDT10 DS    0H                                                               
         CLI   SBLPIGCT,C'1'       SPECIFIC PIGGY REQUEST                       
         BE    BBUFDT14            THEN MUST MATCH                              
*                                                                               
         CLI   SBLPRD2,0           ACCEPT NO PIGGIES?                           
         BNE   BBUFDT12                                                         
         CLC   SBLNPRD2,=C'   '    3 CHAR PIGGY?                                
         BH    BBUFDT12            YES                                          
*                                                                               
         CLI   SNVIDLEN,SNVIDL3Q   HAVE 3 CHAR PRODUCT?                         
         BL    BBUFDT11            NO                                           
         CLC   SNVIDAP2,=C'   '    3 CHAR PIGGY SPECIFIED IN HEADER?            
         BNH   BBUFDT20            NO, OK                                       
         B     BBUFNXDT            PIGGY = NEXT REC                             
*                                                                               
BBUFDT11 CLI   SNVIDPR2,0         YES, ONLY ACCEPT IF NOT PIGGY                 
         BE    BBUFDT20                                                         
         B     BBUFNXDT                                                         
*                                                                               
BBUFDT12 DS    0H                                                               
         CLI   SBLPRD2,X'FF'       ACCEPT BOTH?                                 
         BE    BBUFDT20            OK                                           
*                                                                               
BBUFDT14 CLI   SNVIDLEN,SNVIDL3Q   HAVE 3 CHAR PRODUCT?                         
         BL    *+14                NO                                           
         CLC   SBLNPRD2,SNVIDAP2   HAVE A MATCH ON 3 CHAR PIGGY PROD?           
         B     *+10                NO, CHECK NEXT RECORD                        
         CLC   SBLPRD2,SNVIDPR2     ELSE MAKE SURE PIGGYS MATCH                 
         BNE   BBUFNXDT                                                         
*                                                                               
BBUFDT20 CLI   GLOBLEST,0          ESTIMATE IN HEADER?                          
         BNE   BBUFDT22            YES                                          
         CLI   SNVIDEST,0          NO, ANY DETAIL ESTIMATE?                     
         BE    BBUFDT22            NO, OK                                       
*                                                                               
         TM    SBLFLAG,SBLESTAL    ALL ESTIMATE REQUEST?                        
         BZ    BBUFDT21            NO                                           
         CLI   SVEST,0             HAVE SAVED EST FROM PREV DETAIL?             
         BNE   *+10                YES                                          
         MVC   SVEST,SNVIDEST      NO - SAVE THE FIRST DETAIL'S EST             
         CLC   SVEST,SNVIDEST      YES - DO ESTIMATES MATCH?                    
         BE    BBUFDT21            YES                                          
         OI    SBLSTAT,SBLUDERR    NO - FLAG TO PREVENT SRUPD00 DUMPS           
*                                                                               
BBUFDT21 CLI   SBLEST,0            YES, ACCEPT ANY ESTIMATE?                    
         BE    BBUFDT22            YES, OK                                      
         CLC   SBLEST,SNVIDEST     NO, MAKE SURE ESTIMATES MATCH                
         BNE   BBUFNXDT                                                         
*                                                                               
BBUFDT22 DS    0H                  TEST INTEGRATION                             
         CLI   SBLINT,C'O'         DID THEY ASK TO MATCH INTEG ONLY             
         BE    BBUFDT24            YES, CONTINUE                                
         CLI   SBLINT,C'Y'         DID THEY ASK TO MATCH INTEG/COST             
         BE    BBUFDT24            YES, CONTINUE                                
*                                                                               
         OC    SNVIDCST,SNVIDCST   REG REQUEST - CHECK COST                     
         BNZ   BBUFDT24            CONTINUE IF THERE'S A COST                   
         TM    MISCFLG1,MF1INTEG   IS INTEGRATION A COLUMN                      
         BZ    BBUFDT24            CONTINUE IF THERE'S NO INT COLUMN            
         TM    SNVIDCT2,SNVIDBON   BONUS SPOT INT AND COST=0                    
         BO    BBUFNXDT            SKIP ON REG REQ IF BONUS INT SPOT            
         OC    SNVIDINT,SNVIDINT   REG REQUEST - OK TO INCLUDE INT=0            
         BNZ   BBUFNXDT            SKIP IF INTEG IS NOT ZERO                    
*                                                                               
BBUFDT24 DS    0H                  TEST CABLE NETWORK                           
         CLC   BCBLNET,GLOBLCBN    MUST EQUAL GLOBAL                            
         BE    BBUFDT30                                                         
         CLC   BCBLNET,SNVIDNWK    OR DETAIL                                    
         BNE   BBUFNXDT                                                         
*                                                                               
BBUFDT30 LA    R4,TMPELEM          BUILD INVOICE ELEMENT                        
         USING IELEMD,R4                                                        
         XC    TMPELEM,TMPELEM                                                  
*                                                                               
         XC    DMCB+8(3),DMCB+8                                                 
         MVC   DMCB+11(1),SNVIDDAY                                              
         GOTO1 ADDAY,DMCB,HDRSTDAT,WORK                                         
         GOTO1 DATCON,DMCB,(0,WORK),(2,IDAT)                                    
*                                                                               
         CLC   IDAT,SBLSDAT        IS SPOT WITHIN START-END DATES               
         BL    BBUFNXDT                                                         
         CLC   IDAT,SBLEDAT                                                     
         BH    BBUFNXDT                                                         
*                                                                               
         GOTO1 GETDAY,DMCB,WORK,WORK+7                                          
         CLC   =C'   ',WORK+7                                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R0,X'80'            SHIFT TO GET APPROPRIATE DAY BIT ON          
         LLC   R1,DMCB                                                          
         SRL   R0,1                                                             
         BCT   R1,*-4                                                           
         STC   R0,IDAY                                                          
*                                                                               
         ZICM  R1,SNVIDTIM,2       ADD 6 HOURS TO OUR TIME BECAUSE IT           
         AHI   R1,360                  IS AN OFFSET FROM 6:00A                  
         STCM  R1,3,ITIM                                                        
*                                                                               
         MVC   ILEN,SNVIDSLN                                                    
         MVC   ICOST,SNVIDCST                                                   
         MVC   IMNSEQ,SNVIDSEQ     MINIO SPOT SEQ #                             
         MVC   IINTEG,SNVIDINT     INTEGRATION COST                             
         MVC   IPKG,HDRPKG         PACKAGE                                      
         CLI   HDRPKG,0            NO PACKAGE IN HEADER                         
         BNE   *+10                                                             
         MVC   IPKG,SNVIDPKG       PACKAGE FROM DETAIL                          
*                                                                               
         CLI   SBLINT,C'O'         DID THEY ASK TO MATCH INTEG ONLY             
         BNE   BBUFDT33                                                         
         MVC   ICOST,IINTEG        THEN REPLACE COST WITH INTEG                 
         B     BBUFDT34                                                         
BBUFDT33 CLI   SBLINT,C'Y'         DID THEY ASK TO MATCH INTEG/COST             
         BNE   BBUFDT34                                                         
         ICM   R1,15,IINTEG        THEN ADD COST + INTEG                        
         ICM   R0,15,ICOST                                                      
         AR    R1,R0                                                            
         STCM  R1,15,ICOST                                                      
*                                                                               
BBUFDT34 TM    HDRCTLBT,SNVHDRSQ   IF A RESPONSE INVOICE                        
         BZ    *+14                                                             
         MVI   ICOST,0                                                          
         MVC   ICOST+1(3),SNVIDRSP  SET COST=REPSONSE                           
*                                                                               
         TM    SNVIDCTL,SNVIDICQ   IGNORE COST?                                 
         BZ    *+8                                                              
         OI    ISTAT,ISTNCOST                                                   
*                                                                               
         TM    SNVIDCTL,SNVIDIFQ   IGNORE FILM?                                 
         BZ    *+8                                                              
         OI    ISTAT2,X'02'                                                     
*                                                                               
         TM    SNVIDCTL,SNVIDSIQ   SKIP INTERVAL CHECK?                         
         BZ    *+8                                                              
         OI    ISTAT,ISTNINTV                                                   
*                                                                               
         TM    SNVIDCTL,SNVIDMGQ   MAKEGOOD?                                    
         BZ    *+8                                                              
         OI    ISTAT,ISTMG                                                      
*                                                                               
         TM    SNVIDCTL,SNVIDITQ   IGNORE TIME?                                 
         BZ    *+8                                                              
         OI    ISTAT,ISTNTIME                                                   
*                                                                               
         TM    SNVIDCTL,SNVIDNGQ   NEGATIVE ITEM?                               
         BZ    *+8                                                              
         OI    ISTAT,ISTNEG                                                     
*                                                                               
         TM    SNVIDCTL,SNVIDBLQ   BILLBOARD?                                   
         BZ    *+8                                                              
         OI    ISTAT,ISTBLBQ                                                    
*                                                                               
         TM    SNVIDCT2,SNVIDCTQ   CONTRACT TRADE?                              
         BZ    *+8                                                              
         OI    ISTAT2,ISTCTRQ                                                   
*                                                                               
         TM    SNVIDCT2,SNVIDISQ   IGNORE SPOT LENGTH?                          
         BZ    *+8                                                              
         OI    ISTAT3,IST3ISPT                                                  
*                                                                               
         TM    SVHDCT2,SNVHDTXQ    IS TAX FIELD NET+TAX? (CANADA ONLY)          
         BO    BUFDT34A            YES, LEAVE IDTOT AS IS                       
*                                                                               
         ICM   R0,15,ICOST         RE-CALCULATE INVOICE TOTAL                   
         TM    ISTAT,ISTNEG             NEGATIVE AMOUNT?                        
         BZ    *+6                                                              
         LCR   R0,R0                    YES, NEGATE AMOUNT BEFORE ADD           
         A     R0,IDTOT                                                         
         ST    R0,IDTOT                                                         
*                                                                               
BUFDT34A L     R1,ISTOT            BUMP SPOT COUNT                              
         LA    R1,1(R1)                                                         
         ST    R1,ISTOT                                                         
*                                                                               
         MVC   IPRD,GLOBLPRD                                                    
         CLI   GLOBLPRD,0                                                       
         BNE   *+10                                                             
         MVC   IPRD,SNVIDPRD                                                    
         CLI   IPRD,0              IS BINARY PRODUCT SET?                       
         BE    BUFDT34B            NO, SET THE 3 CHAR OVERFLOW PRODUCT!         
*                                                                               
         MVC   HALF,BMOSS          MOS                                          
         XC    HALF,=X'FFFF'       X'FF' COMPLIMENT                             
         CLC   HALF,=X'D421'       MOS ON/BEFORE JAN/06?                        
         BNH   BBUFDT35            YES - DO NOT SET 3 CHAR PRD                  
*                                                                               
BUFDT34B MVC   IPRDNT,GLOBNPRD     SET 3 CHARACTER PRODUCT FROM HEADER          
         MVC   IPRD2NT,GLOBNPR2    SET 3 CHAR PIGGY FROM HEADER                 
         CLI   SNVIDLEN,SNVIDL3Q   HAVE NEW ELEMENT LENGTH?                     
         BL    BBUFDT35            NO - NO 3 CHAR PRDODUCTS AVAILIBLE           
         CLI   GLOBNPRD,0          HAVE SAVED PRODUCT FROM HEADER?              
         BNE   *+10                YES                                          
         MVC   IPRDNT,SNVIDAP1     NO - GET IT FROM DETAIL ELEM                 
         CLI   GLOBNPR2,0          HAVE SAVED PRODUCT 2 FROM HEADER?            
         BNE   *+10                YES                                          
         MVC   IPRD2NT,SNVIDAP2    NO - GET IT FROM DETAIL ELEM                 
         CLC   IPRDNT,=C'   '      SPACES?                                      
         BNE   *+10                NO                                           
         XC    IPRDNT,IPRDNT       YES - MAKE BINARY ZEROS                      
         CLC   IPRD2NT,=C'   '     SPACES?                                      
         BNE   *+10                NO                                           
         XC    IPRD2NT,IPRD2NT     YES - MAKE BINARY ZEROS                      
         LH    R1,SBLILEN          IELEM LENGTH                                 
         CHI   R1,IELI3LEN         LENGTH WITH DIGITAL INFO?                    
         BL    BBUFDT35            NO                                           
         CLI   SNVIDLEN,SNVIDL4Q   HAVE NEW ELEMENT LENGTH?                     
         BL    BBUFDT35            NO - NO DIGITAL INFO AVAILABLE               
         MVC   IIDIMP,SNVIDIMP     IMPRESSIONS                                  
         MVC   IIDRAT,SNVIDRAT     RATINGS                                      
         MVC   IIDCLK,SNVIDCLK     CLICKS                                       
*                                                                               
BBUFDT35 MVC   IPRD2,GLOBLPR2                                                   
         CLI   GLOBLPR2,0                                                       
         BNE   BBUFDT36                                                         
         MVC   IPRD2,SNVIDPR2                                                   
         SR    R1,R1                                                            
         ICM   R1,15,SBLAPGL       THIS LITTLE PIGGY GOES TO MARKET             
         BZ    BBUFDT36            NO PIG LIST                                  
         LLC   R0,IPRD2                                                         
         AR    R1,R0                                                            
         MVC   0(1,R1),IPRD2                                                    
*                                                                               
BBUFDT36 MVC   IEST,GLOBLEST                                                    
         CLI   GLOBLEST,0                                                       
         BNE   *+10                                                             
         MVC   IEST,SNVIDEST                                                    
         MVC   ICBLNET,GLOBLCBN    GLOBAL CABLE NET                             
         CLI   GLOBLCBN,0                                                       
         BNE   *+10                                                             
         MVC   ICBLNET,SNVIDNWK    OR DETAIL                                    
*                                                                               
         MVC   IID,CONIDNUM                                                     
*                                                                               
         TM    MISCFLG1,MF1NETAM   NET AMOUNT?                                  
         BZ    *+12                                                             
         OI    ISTAT2,ISTNET       NET INDICATOR                                
         OI    SBLSTAT,SBLNETQ                                                  
*                                                                               
         CLI   SBLNETP,C'Y'        NETPAK?                                      
         BNE   BBUFDT37                                                         
*                                                                               
         OC    SNVIDUDT,SNVIDUDT   IS THERE MATCHED DATA                        
         BZ    BBUFDT38                                                         
         MVC   IMATDATE,SNVIDUDT   DATE                                         
         MVC   IMATSQH,SNVIDUTM    TIME (1/4 HOUR)                              
         MVC   IMATEST,SNVIDUES    EST                                          
         MVC   IMATSPT,SNVIDUSB    UNIT SUB LINE                                
         MVC   IMATDP,SNVIDUDP     DAYPART                                      
         MVC   IMATPRG,SNVIDUPG    PROGRAM                                      
         B     BBUFDT38                                                         
*                                                                               
BBUFDT37 OC    SNVIDBDT,SNVIDBDT   IS THERE A MATCHED DATE?                     
         BZ    BBUFDT38            NO                                           
*                                                                               
         MVC   IMATSEST,SNVIDBES   ESTIMATE                                     
         MVC   IMATSLIN,SNVIDTBL   2-BYTE BUYLINE                               
         OC    SNVIDTBL,SNVIDTBL   HAVE 2-BYTE BUYLINE?                         
         BNZ   *+10                YES                                          
         MVC   IMATSLIN+1(1),SNVIDBLN  NO - USE 1-BYTE BUYLINE                  
         MVC   IMATSDAT,SNVIDBDT   SPOT DATE                                    
         MVC   IMATSNUM,SNVIDBNO   SPOT NUMBER WITHIN DATE                      
         MVC   IMATSORB,SNVIDBOR   ORBIT LINE NUMBER                            
***      MVC   IMATSDEM,SNVIDDEM   FIRST DEMO VALUE FOR MM                      
*                                                                               
BBUFDT38 CLI   SETINVSW,C'Y'       ALREADY DONE SETINV?                         
         BE    *+12                                                             
         BRAS  RE,SETINV           ADD TO INV LIST AND GET INTERNAL             
         MVI   SETINVSW,C'Y'                                                    
         MVC   IINVID,INTIDNUM     INTERNAL ID NUMBER                           
*                                                                               
         CLI   SNVIDCML,0                                                       
         BE    BBUFDT40                                                         
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVCMELQ                                                 
         MVC   MINEKEY+1(L'SNVCMICD),SNVIDCML                                   
         BAS   RE,TOFLMTBL                                                      
         MVC   IFILM,FLMIDNUM                                                   
         CLI   ELEMADID,C'Y'       IS FILM AN ADID                              
         BNE   *+8                                                              
         OI    ISTAT3,IST3ADID                                                  
         CLI   ELEMADID,C'H'       IS FILM HDEF?                                
         BNE   *+8                                                              
         OI    ISTAT3,IST3HDEF                                                  
*                                                                               
         LA    R6,SVINVCE          RESET X'40' ELEMENT!                         
         CLI   SNVIDCM2,0                                                       
         BE    BBUFDT40                                                         
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVCMELQ                                                 
         MVC   MINEKEY+1(L'SNVCMICD),SNVIDCM2                                   
         BAS   RE,TOFLMTBL                                                      
         MVC   IFILM2,FLMIDNUM                                                  
*                                                                               
BBUFDT40 L     R2,SBLIEND          R2 = A(WHERE TO PUT NEXT DETAIL)             
         LR    RE,R2                                                            
*                                                                               
         LHI   R1,IEL$MLEN         SET LENGTH OF INSERTION ELEMENT              
         CLI   SBLMODE,C'$'        $MAT?                                        
         BE    *+8                 YES                                          
         LH    R1,SBLILEN          NO, SI2                                      
*                                                                               
         AR    RE,R1                                                            
         C     RE,SBLAITBX         PASSED THE END OF TABLE?                     
         BL    *+12                                                             
         OI    SBLSTAT,SBLITOVQ    YES, INVOICE ITEM TABLE OVERFLOW             
         B     GSNVEXIT              -> EXIT GETSNV                             
*                                                                               
         BCTR  R1,0                NO, THEN WE CAN PUT ELEMENT IN               
         EX    R1,*+8                  INVOICE ITEM TABLE                       
         B     *+10                                                             
         MVC   0(0,R2),TMPELEM                                                  
*                                                                               
         ST    RE,SBLIEND          1 BYTE AFTER END OF INV TABLE                
         OI    SBLSTAT,SBLINVQ     SET HAVE SOME INVOICE ITEM                   
*                                                                               
         DROP  R4,R6                                                            
*                                                                               
BBUFNXDT LA    R1,SVINVCE                                                       
         USING SNVIDELD,R1                                                      
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVIDELQ                                                 
         MVC   MINEKEY+1(SNVIDSLN-SNVIDDAY),SNVIDDAY                            
         DROP  R1                                                               
         BAS   RE,MINIOHI                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,MINIOSEQ                                                      
         B     BBUFDTLP                                                         
*                                                                               
BBUFDTX  DS    0H                                                               
         BAS   RE,TOTINVS          SET INVOICE TOTALS IN LIST                   
         XC    KEY,KEY                                                          
         MVC   KEY(SNVKMINK-SNVKEY),MINMKEY                                     
         BAS   RE,XSPDHIGH                                                      
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BBUFNXRC BAS   RE,XSPDSEQ                                                       
         CLI   DMCB+8,0                                                         
         BNE   BBUFX               END-OF-FILE THEN                             
*                                                                               
         LA    R3,KEY                                                           
         USING SNVKEY,R3                                                        
         CLC   KEY(SNVKMINK-SNVKEY),KEYSAVE                                     
         BE    BBUFNXRC                                                         
         B     BBUFLOOP                                                         
         DROP  R3                                                               
*                                                                               
BBUFX    B     XIT                                                              
         SPACE 2                                                                
***********************************************************************         
* TOTINVS - SET INVOICE TOTALS IN LIST                                          
***********************************************************************         
         SPACE 2                                                                
TOTINVS  NTR1                                                                   
         OC    ISTOT,ISTOT         ANY SPOTS FOR INVOICE?                       
         BZ    TIXIT               NO, SKIP                                     
*                                                                               
         GOTO1 DATCON,DMCB,(0,HDRINDAT),(3,WORK)                                
*                                                                               
         USING INOLSTD,R2                                                       
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,3,INTIDNUM           INTERNAL NUMBER                          
         BCTR  R2,0                                                             
         MH    R2,INOLSTQ                                                       
         A     R2,SBLAINOL                                                      
*                                                                               
         ICM   R0,15,INOLAMT       BUMP INVOICE $                               
         A     R0,IDTOT                                                         
         STCM  R0,15,INOLAMT                                                    
*                                                                               
         XC    IDTOT,IDTOT         CLEAR $ TOT FOR INVOICE                      
         XC    ISTOT,ISTOT         AND SPOT TOTAL                               
         DROP  R2                                                               
*                                                                               
TIXIT    DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE ADDS TO AND GETS FROM FLMTAB                                     
***********************************************************************         
TOFLMTBL NTR1                                                                   
         BAS   RE,MINIOHI          THE FILM ELEMENT BETTER BE HERE              
         BE    *+6                                                              
TFLMDIE  DC    H'0'                                                             
*                                                                               
         MVI   ELEMADID,C'N'                                                    
         L     R6,MINELEM                                                       
         USING SNVCMELD,R6                                                      
         CLI   SNVCMEL,SNVCMELQ                                                 
         BNE   TFLMDIE                                                          
         CLC   SNVCMICD,MINEKEY+1                                               
         BNE   TFLMDIE                                                          
*                                                                               
         XC    FLMELEM,FLMELEM     BUILD THE FILM TABLE ELEMENT                 
         LA    R2,FLMELEM                                                       
         USING FLMTABD,R2                                                       
         MVC   FLMSEQ,SNVCMSEQ                                                  
         MVC   FLMIDNO,INTIDNUM                                                 
         MVC   FLMICOD,SNVCMICD                                                 
         CLC   SNVCMSUF,=C'    '                                                
         BH    TFLM10              PROCESS AS AD-ID                             
         MVC   FLMCOD,SNVCMCD      REGULAR 8 CHAR ASCII FILM                    
         B     TFLM20                                                           
*                                  AD-ID - PACK INTO TABLE                      
TFLM10   ICM   RF,15,SBLATRPK      A(TRPACK)                                    
         BNZ   TFLM15                                                           
         OI    SBLSTAT,SBLADID     NO AD-ID FOR MATCH                           
         B     GSNVEXIT                                                         
*                                                                               
TFLM15   LA    R3,FLMADID          POINT TO AD-ID                               
         MVI   ELEMADID,C'Y'       ELEM HAS AD-ID                               
         TM    SNVCMFLG,SNVCMHDQ   IS THIS HIGHDEF?                             
         BZ    *+12                NO                                           
         LA    R3,FLMHDEF          YES - POINT TO HIGHDEF FIELD                 
         MVI   ELEMADID,C'H'       ELEM HAS HIGHDEF                             
         GOTO1 (RF),DMCB,(C'P',SNVCMCD),0(R3)                                   
         DROP  R2,R6                                                            
*                                                                               
TFLM20   L     R1,SBLAFTBP                                                      
         L     RF,SBLABSRC                                                      
         GOTO1 (RF),(R1),(X'01',FLMELEM)                                        
         CLI   0(R1),X'01'         FILM ELEMENT WAS IN TABLE ALREADY?           
         BNE   TFLMFND             YES                                          
*                                                                               
         OC    1(3,R1),1(R1)       FILM TABLE FULL?                             
         BNZ   *+12                                                             
         OI    SBLSTAT,SBLFTOVQ    YES, FILM TABLE OVERFLOW                     
         B     GSNVEXIT              -> EXIT GETSNV                             
*                                                                               
TFLMFND  L     R6,0(R1)            A(WHERE ELEMENT WAS FOUND)                   
         USING FLMTABD,R6                                                       
         MVC   FLMIDNUM,FLMICOD                                                 
         DROP  R6                                                               
*                                                                               
TFLMX    B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE READS A MINIO ELEMENT.  MINEKEY MUST BE SET BY CALLER            
***********************************************************************         
MINIORD  NTR1                                                                   
         GOTO1 VMINIO,DMCB,('MINRD',(R5))                                       
         CLI   MINERR,0                                                         
         BE    XIT                                                              
         DC    H'0'                DIE ON ANY ERROR                             
         SPACE 2                                                                
***********************************************************************         
* THIS ROUTINE READ HIGH A MINIO ELEMENT.  MINEKEY MUST BE SET BY               
* THE CALLER.                                                                   
***********************************************************************         
MINIOHI  NTR1                                                                   
         GOTO1 VMINIO,DMCB,('MINHI',(R5))                                       
         CLI   MINERR,0            RETURN 'YES' IF NO ERRORS                    
         BE    YES                                                              
         CLI   MINERR,MINEEOF      RETURN 'NO' IF END-OF-FILE                   
         BE    NO                                                               
         DC    H'0'                DIE ON ANY OTHER ERROR                       
         SPACE 2                                                                
***********************************************************************         
* THIS ROUTINE READ SEQUENTIAL FOR A MINIO ELEMENT.                             
***********************************************************************         
MINIOSEQ NTR1                                                                   
         GOTO1 VMINIO,DMCB,('MINSEQ',(R5))                                      
         CLI   MINERR,0            RETURN 'YES' IF NO ERRORS                    
         BE    YES                                                              
         CLI   MINERR,MINEEOF      RETURN 'NO' IF END-OF-FILE                   
         BE    NO                                                               
         DC    H'0'                DIE ON ANY OTHER ERROR                       
         EJECT                                                                  
***********************************************************************         
* DATAMGR CALLS FOR XSPDIR AND XSPFIL                                           
***********************************************************************         
XSPDHIGH DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'                                             
         B     XSPDRCT                                                          
*                                                                               
XSPDSEQ  DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
*                                                                               
XSPDRCT  NTR1                                                                   
         GOTO1 DATAMGR,DMCB,COMMAND,=CL8'XSPDIR',KEY,IOAREA                     
*                                                                               
         LA    R6,IOAREA                                                        
         USING SNVKEYD,R6                                                       
         MVC   KEY(L'SNVKEY),SNVKEY                                             
         DROP  R6                                                               
*                                                                               
         CLI   DMCB+8,0            NO ERRORS?                                   
         BE    XSPD10                                                           
         TM    DMCB+8,X'80'        EOF OR RECORD DELETED?                       
         BNZ   XSPD10                                                           
         DC    H'0'                NO, THEN DIE                                 
*                                                                               
XSPD10   DS    0H                                                               
*                                                                               
XSPDX    B     XIT                                                              
         EJECT                                                                  
GSNVEXIT L     RD,SAVERD           EXIT FROM THIS MODULE                        
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
***********************************************************************         
* SETINV - SET INVOICE IN LIST AND SET INTIDNUM                                 
***********************************************************************         
         SPACE 2                                                                
SETINV   NTR1  BASE=*,LABEL=*                                                   
         GOTO1 DATCON,DMCB,(0,HDRINDAT),(3,WORK)                                
*                                                                               
         L     R2,SBLAINOL                                                      
         USING INOLSTD,R2                                                       
*                                                                               
         OC    SBLMXINB,SBLMXINB   CALLER USING 2 BYTE MAX                      
         BNZ   SI10                                                             
         LLC   RF,SBLMXINS         RF = START OF LAST POSSIBLE ENTRY            
         BCTR  RF,0                                                             
         MH    RF,INOLSTQ              USED TO TEST LIST OVERFLOW               
         LA    RF,0(R2,RF)                                                      
         LA    R3,1                    FIRST AVAILABLE NUMBER                   
         B     SI12                                                             
*                                                                               
SI10     SR    RF,RF               RF = A(LAST VALID ENTRY)                     
         ICM   RF,3,SBLMXINB                                                    
         AHI   RF,-1                                                            
         MH    RF,INOLSTQ              USED TO TEST LIST OVERFLOW               
         LA    RF,0(R2,RF)                                                      
         LA    R3,1                    FIRST AVAILABLE NUMBER                   
*                                                                               
SI12     DS    0H                                                               
         CLI   INOLINV,0           NO NUMBER HERE?                              
         BE    SI20                                                             
         TM    SBLSTAT,SBLINOVQ    TABLE ALREADY OVERFLOWED                     
         BZ    *+6                 NO                                           
         DC    H'0'                YES - 650 INV IN INOLST                      
         CLC   QINVOICE,INOLINV    IS THE INVOICE HERE ALREADY?                 
         BNE   SI14                                                             
         CLC   INOLDAT,WORK                                                     
         BNE   SI14                                                             
         CLC   INOLMOS,MINMKEY+SNVKMOS-SNVKEY                                   
         BNE   SI14                                                             
         STCM  R3,3,INTIDNUM       ENTRY FOUND                                  
         B     SI30                                                             
*                                  TRY NEXT ENTRY                               
SI14     LA    R3,1(R3)            BUMP THE INVOICE NUMBER                      
         AH    R2,INOLSTQ                                                       
         CR    R2,RF               BEYOND INVOICE NUMBER LIST?                  
         BL    SI12                                                             
         OI    SBLSTAT,SBLINOVQ    YES, INVOICE NUMBER LIST OVERFLOW            
*                                                                               
SI20     DS    0H                                                               
         STCM  R3,3,INTIDNUM       CURRENT INTERNAL INVOICE NUMBER              
         MVC   INOLINV,QINVOICE                                                 
         MVC   INOLDAT,WORK                                                     
         MVC   INOLADAT,HDRBDATC   BATCH DATE                                   
         MVC   INOLMOS,MINMKEY+SNVKMOS-SNVKEY                                   
***      MVC   INOLCTL,HDRCTLBT    INVOICE STATUS                               
         OC    HDREASI,HDREASI     ELECTRONIC INVOICE?                          
         BZ    *+8                 NO                                           
         OI    INOFLAG,INOEASI     YES - FLAG ELECTRONIC INVOICE                
*                                                                               
         CLI   INVMGR,C'Y'         CAME FROM INVOICE MANAGER?                   
         BNE   *+8                 NO                                           
         OI    INOFLAG,INOINVM     YES - FLAG INV MANAGER                       
*                                                                               
         MVC   INOLNUM,INTIDNUM    INTERNAL INV ID NUM                          
*                                                                               
         OC    HDRREP,HDRREP       ANY REP CODE IN HEADER                       
         BZ    SI23                                                             
         ICM   RF,15,SBLARCPK      A(RCPACK)                                    
         GOTO1 (RF),DMCB,(C'P',HDRREP),INOLREP                                  
*                                                                               
SI23     MVC   INOLPDEM,HDRDEMO    DEMO CATEGORY                                
         OC    SBLAIKL,SBLAIKL     IF DOING KEY LIST                            
         BZ    SI30                                                             
         OC    SBLMXINB,SBLMXINB   CALLER USING 2 BYTE MAX INV                  
         BNZ   SI24                                                             
         CLM   R3,1,SBLMXINS       AND NOT TOO MANY INVOICES                    
         BH    SI30                                                             
         B     SI28                                                             
SI24     CLM   R3,3,SBLMXINB       AND NOT TOO MANY INVOICES                    
         BH    SI30                                                             
*                                                                               
SI28     AHI   R3,-1               SET KEY IN INVOICE KEY LIST                  
         MHI   R3,32               KEY LENGTH                                   
         A     R3,SBLAIKL                                                       
         MVC   0(32,R3),MINMKEY                                                 
*                                                                               
SI30     DS    0H                                                               
         MVC   SBLINVC,INTIDNUM    RETURN INVOICE COUNT                         
         DROP  R2                                                               
*                                                                               
SIXIT    DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
***********************************************************************         
* THIS ROUTINE INITSILIZES MINIO VARIABLES                                      
***********************************************************************         
MNIOINIT NTR1  BASE=*,LABEL=*                                                   
         LA    R0,MINBLOCK         CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINCOMF,SBLACOM     A(COMFACS)                                   
         MVC   MINRECUP,SBLARCUP   A(RECUP)                                     
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,=CL8'XSPFIL'     FILE NAME                                
         MVC   MINDIR,=CL8'XSPDIR'     DIR NAME                                 
         MVI   MINFKLEN,L'SNVKEY   KEY LENGTH                                   
         MVI   MINEKLEN,L'SNVKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'SNVKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'SNVDSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(3975) MAXIMUM RECORD LENGTH                        
         MVC   MINBUFF,SBLAIO      A(BUFFER FOR MINIO)                          
         MVI   MINNBUF,1           USE ONE BUFFER                               
*                                                                               
         LA    R1,MRECTAB          A(AREA FOR RECORD TABLE)                     
         ST    R1,MINRTAB                                                       
         MVC   MINRTABL,=Y(MRECTABL)  LENGTH OF RECORD TABLE                    
*                                                                               
         LA    RE,MELEM            A(AREA FOR ELEM OR CLUSTER)                  
         ST    RE,MINELEM                                                       
         MVC   MINMAXEL,=Y(L'MELEM)   MAX LENGTH OF ELEM OF CLUSTER             
         XC    0(L'MELEM,RE),0(RE)   CLEAR MINELEM AREA                         
*                                                                               
         XC    MINMKEY,MINMKEY     BUILD MASTER KEY                             
         LA    R4,MINMKEY                                                       
         USING SNVKEY,R4                                                        
         MVI   SNVKTYPE,SNVKTYPQ                                                
         MVI   SNVKSUB,SNVKSUBQ                                                 
         MVC   SNVKAM,SBLAGYMD                                                  
         MVC   SNVKCLT,SBLCLT                                                   
         MVC   SNVKSTA,BSTA                                                     
         MVC   SNVKMOS,BMOSS                                                    
         MVC   SNVKINV,QINVOICE                                                 
         DROP  R4                                                               
*                                                                               
MINITX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
         SPACE 2                                                                
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
       ++INCLUDE DDMINBLK                                                       
         EJECT                                                                  
       ++INCLUDE SPGENSNV                                                       
         EJECT                                                                  
       ++INCLUDE SPSNVBLK                                                       
         EJECT                                                                  
       ++INCLUDE SPSNVFEQTB                                                     
         EJECT                                                                  
BINSRCHD DSECT                                                                  
BSRCOPTN DS    XL1                 OPTION                                       
BSRCOEXA EQU   X'00'                  SEARCH FOR EXACT MATCH ONLY               
BSRCOINS EQU   X'01'                  INSERT IF NOT FOUND                       
BSRCORHI EQU   X'02'                  READ HIGH                                 
BSRCODEL EQU   X'80'                  DELETE                                    
         ORG   BSRCOPTN                                                         
BSRCAREC DS    A                   A(REC) FOR THE ACTION                        
*                                                                               
BSRCATBL DS    A                   A(BINSRCH TABLE)                             
BSRCNUMR DS    F                   NUMBER OF RECORDS IN TABLE SO FAR            
BSRCLREC DS    F                   LENGTH OF RECORD (MAX=255)                   
*                                                                               
BSRCDSPK DS    XL1                 DISPLACEMENT OF KEY INTO RECORD              
         ORG   BSRCDSPK                                                         
BSRCLKEY DS    F                   LENGTH OF KEY (MAX=255)                      
*                                                                               
BSRCMAXR DS    F                   MAXIMUM NUMBER OF RECORDS IN TABLE           
*                                                                               
BSRCRETN EQU   BSRCOPTN            X'01' = RECORD NOT FOUND                     
         EJECT                                                                  
WORKD    DSECT                                                                  
COMFACS  DS    A                                                                
ADDAY    DS    A                                                                
DATCON   DS    A                                                                
DATAMGR  DS    A                                                                
GETDAY   DS    A                                                                
HELLO    DS    A                                                                
VMINIO   DS    V                                                                
*                                                                               
AINBLCK  DS    A                   A(INPUT BLOCK)                               
*                                                                               
DUB      DS    D                                                                
SAVR1    DS    F                   CONTENTS OF R1 ON ENTRY                      
SAVERD   DS    F                   CONTENTS OF RD ON ENTRY                      
DMCB     DS    6F                  PARAMETER BLOCK                              
WORK     DS    CL60                                                             
WORD     DS    F                                                                
IDTOT    DS    F                   TOTAL $ FOR INVOICE                          
ISTOT    DS    F                   TOTAL SPOTS                                  
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
*                                                                               
MISCFLG1 DS    CL1                 MISCELLANEOUS FLAGS                          
MF1NETAM EQU   X'80'               NET AMOUNTS                                  
MF1INTEG EQU   X'40'               INTEGRATION IS A COLUMN ON INVOICE           
*                                                                               
INTIDNUM DS    XL2                 INTERNAL ID NUMBER                           
CONIDNUM DS    XL1                 CONTRACT ID NUMBER                           
FLMIDNUM DS    XL1                 FILM ID NUMBER                               
ELEMADID DS    CL1                 INDICATE FILM IS ADID                        
NEWSTART DS    XL2                 START FOR NEW STYLE INVOICES                 
SETINVSW DS    CL1                 Y= HAVE DONE SETINV FOR INV                  
*                                                                               
BSTA     DS    XL3                 BINARY STATION                               
BMOSS    DS    XL2                 MONTH OF SERVICE (YM) (X'FF' COMPL)          
QINVOICE DS    CL10                EBCDIC INVOICE NUMBER                        
BCBLNET  DS    XL1                 CABLE NETWORK                                
CBLSW    DS    CL1                 Y=DOING CABLE HEAD                           
*                                                                               
HDRSTDAT DS    CL6                 INVOICE HEADER START DATE                    
HDRINDAT DS    CL6                 INVOICE HEADER INVOICE DATE                  
HDRCTLBT DS    XL1                 INVOICE HEADER CONTROL BYTE                  
*                                  X'80' = AMOUNTS ARE NET, NOT GROSS           
HDRBDATC DS    XL2                 EASI BATCH DATE OR DATE ADDED                
HDRPKG   DS    XL1                 NETPAK PACKAGE FROM INV HEADER               
HDRREP   DS    CL3                 REP FROM INV HEADER                          
HDREASI  DS    CL4                 EASI SOURCE                                  
HDRDEMO  DS    XL3                 HEADER DEMO CATEGORY FOR DIGITAL INV         
*                                                                               
GLOBLPRD DS    XL1                 PRODUCT IN THE INVOICE HEADER                
GLOBLPR2 DS    XL1                 PIGGY IN THE INVOICE HEADER                  
GLOBLEST DS    XL1                 ESTIMATE IN THE INVOICE HEADER               
GLOBLCBN DS    XL1                 CABLE NETWORK IN INVOICE HEADER              
GLOBNPRD DS    CL3                 3 CHAR PRODUCT IN THE INVOICE HEADER         
GLOBNPR2 DS    CL3                 3 CHAR PIGGY IN THE INVOICE HEADER           
*                                                                               
SVHDCT2  DS    XL1                 SAVED HEADER CONTROL BYTE 2 (CANADA)         
SVBPRD   DS    XL1                 SAVED BINARY PRODUCT                         
SVPRD    DS    XL3                 SAVED 3 CHAR PRODUCT                         
SVEST    DS    XL1                 SAVED ESTIMATE                               
INVMGR   DS    XL1                 INVOICE MANAGER FLAG                         
INOLSTQ  DS    H                   INVOICE TABLE LENGTH                         
*                                                                               
COMMAND  DS    CL8                                                              
KEY      DS    CL40                                                             
KEYSAVE  DS    CL40                                                             
SVSNVKEY DS    CL32                SAVED KEY                                    
*                                                                               
TMPELEM  DS    XL(IELI3LEN)        INVOICE TABLE ELEMENT                        
FLMELEM  DS    XL(FLMTABEL)        FILM TABLE ELEMENT                           
*                                                                               
SVINVCE  DS    XL(SNVIDL4Q)        SAVED INVOICE ELEMENT                        
*                                                                               
MELEM    DS    XL128               MINIO ELEMENT                                
MINBLOCK DS    CL(MINBLKL)                                                      
*                                                                               
MRECTABL EQU   2400                                                             
MRECTAB  DS    XL(MRECTABL)                                                     
*                                                                               
IOAREA   DS    XL4096                                                           
*                                                                               
WORKDL   EQU   *-WORKD                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'059SPGETSNV  03/04/21'                                      
         END                                                                    
