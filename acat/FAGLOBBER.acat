*          DATA SET FAGLOBBER  AT LEVEL 003 AS OF 05/01/02                      
*CATALP GLOBBER                                                                 
         TITLE 'GLOBBER - GLOBAL VARIABLE MAINTENANCE ROUTINE'                  
         PRINT NOGEN                                                            
GLOBBER  CSECT                                                                  
         NMOD1 WRKX-WRKD,**GLOB**,CLEAR=YES                                     
         USING WRKD,RC             RC=A(LOCAL W/S)                              
         LA    RE,*+6                                                           
         BSM   0,RE                SET IN 24-BIT MODE                           
*                                                                               
         MVC   GPARMS(GPARML),0(R1)                                             
         ST    R1,SAVEDR1                                                       
         MVI   ERRCODE,0           CLEAR ERROR FLAG                             
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,RECLEN         SET TEMPSTR REC LEN ON FIRST CALL            
         BNZ   GLOB005                                                          
         PROT  OFF                                                              
         L     RE,=V(SSB)                                                       
         MVC   RECLEN,SSBTWAL-SSBD(RE)                                          
         LH    RF,RECLEN                                                        
         LA    RF,7(RF)            ADJUST TO MODULO 8                           
         SRL   RF,3                                                             
         SLL   RF,3                                                             
         STH   RF,READTWA+20       SET LEN OF W/S REQUIRED BY NMOD1             
         PROT  ON                                                               
*                                                                               
GLOB005  L     RE,=V(SSB)          RE=A(SSB)                                    
         L     RF,SSBTKADR-SSBD(RE)                                             
         USING TCBD,RF             RF=A(TCB)                                    
         SR    RA,RA                                                            
         TM    GCOMMAND,GFLAGP5    DO WE HAVE A P5                              
         BZ    GLOB010                                                          
         TM    GFLAGS,GFLAGP5      IS A(GLOBAREA) IN P5                         
         BZ    GLOB010                                                          
         ICM   RA,7,GPARMADR       YES IT IS                                    
         BNZ   GLOB015                                                          
GLOB010  L     RA,TCBWRKA          FIND GLOBAL STORAGE IN MNTR'S W/S            
         LA    R0,8                                                             
GLOB011  C     RA,SSBLOADR-SSBD(RE)                                             
         BNH   ERR0                CHECK RA STAYS WITHIN BOUNDS                 
         C     RA,SSBHIADR-SSBD(RE)                                             
         BNL   ERR0                                                             
         CLC   0(4,RA),=C'MNTR'    FIND MONITORS REGISTERS                      
         BE    GLOB012                                                          
         L     RA,8(RA)                                                         
         BCT   R0,GLOB011                                                       
         B     ERR0                EXIT WITH ERROR IF CANT FIND IT              
GLOB012  ICM   RA,15,104(RA)       PICK UP SPECIAL STORAGE LOCATION             
         BZ    ERR0                                                             
GLOB015  ST    RA,ATSKGLOB                                                      
         USING GLOBD,RA            RA=A(GLOBAREA)                               
         L     R9,TCBUTL                                                        
         USING UTLD,R9             R9=A(UTL)                                    
         EJECT                                                                  
***********************************************************************         
*SCAN GLOBAL AREA FOR ELEMENT CODE                                    *         
***********************************************************************         
         SPACE 1                                                                
SCANGLOB TM    GLOBSWS,GLOBREAD    CHECK TWA CONTAINING GLOBALS READ            
         BO    SG1                                                              
         MVC   DMCB+20(2),=C'L='   NO SET ACTUAL RECORD LENGTH                  
         MVC   DMCB+22(2),RECLEN                                                
         GOTO1 READTWA,DMCB        READ IT IN                                   
*                                                                               
SG1      LA    R2,GLOBELS          POINT TO FIRST ELEMENT                       
         SR    R3,R3               R3 BECOMES LENGTH OF GLOBAL AREA             
         SR    R4,R4                                                            
         LA    R0,GLOBELEN         R0=MAXIMUM LENGTH OF GLOBAL AREA             
*                                                                               
SG2      CLI   0(R2),0             TEST END OF GLOBAL AREA                      
         BE    SGX                                                              
         IC    R4,1(R2)            R4 HOLDS LENGTH OF ELEMENT                   
         CH    R4,=H'2'                                                         
         BNL   *+12                                                             
SG3      MVI   0(R2),0             TRUNCATE AT START OF BAD ELEMENT             
         B     SGX                                                              
         LA    R1,0(R4,R3)         TEST ELEMENT WITHIN AREA                     
         CR    R1,R0                                                            
         BNL   SG3                                                              
*                                                                               
SG4      CLC   0(1,R2),GVARCODE+3  TEST ELEMENT FOUND                           
         BNE   *+12                                                             
         ST    R2,ELEMPTR          STORE POINTER TO THIS ELEMENT                
         ST    R4,ELEMLEN                                                       
         CLI   0(R2),XFRCTRLQ      TEST XFR CONTROL ELEMENT                     
         BNE   *+8                                                              
         ST    R2,ELEMXFR          STORE POINTER TO XFR CONTROL ELEMENT         
         AR    R2,R4               BUMP ELEMENT POINTER                         
         AR    R3,R4               BUMP GLOBAL AREA LENGTH                      
         B     SG2                                                              
*                                                                               
SGX      ST    R3,GLOBLEN          STORE LENGTH OF GLOBAL AREA                  
         EJECT                                                                  
***********************************************************************         
*FIND REQUESTED COMMAND ROUTINE                                       *         
***********************************************************************         
         SPACE 1                                                                
         L     R3,GCOMMAND         DONT MATTER IF CLEAR CALL                    
         CLC   0(4,R3),=C'CLEAR'                                                
         BE    CLEAR                                                            
         CLC   GVARCODE,=F'255'    CHECK VALID GLOBAL CODE PASSED               
         BH    ERR1                                                             
         CLI   GVARCODE+3,0                                                     
         BE    ERR1                                                             
*                                                                               
         CLC   0(4,R3),=C'PUTD'                                                 
         BE    PUTD                                                             
         CLC   0(4,R3),=C'PUTF'                                                 
         BE    PUTF                                                             
         CLC   0(4,R3),=C'GETD'                                                 
         BE    GETD                                                             
         CLC   0(4,R3),=C'GETF'                                                 
         BE    GETF                                                             
         CLC   0(4,R3),=C'DELE'                                                 
         BE    DELE                                                             
         B     ERR2                                                             
         EJECT                                                                  
***********************************************************************         
*PUT GLOBAL ELEMENT FROM SCREEN FIELD                                 *         
***********************************************************************         
         SPACE 1                                                                
PUTF     L     R2,GAREA            SET UP PARMS FOR PUTD                        
         ZIC   R1,5(R2)                                                         
         ST    R1,GLENGTH                                                       
         LA    R2,8(R2)                                                         
         ST    R2,GAREA                                                         
         B     PUTD                                                             
         SPACE 2                                                                
***********************************************************************         
*PUT GLOBAL ELEMENT FROM DATA AREA                                    *         
***********************************************************************         
         SPACE 1                                                                
PUTD     CLC   GLENGTH,=F'0'       CHECK VALID LENGTH                           
         BNH   ERR3                                                             
         CLC   GLENGTH,=F'254'                                                  
         BNL   ERR3                                                             
         LA    R3,ELDATA           SET UP ELEMENT DATA                          
*                                                                               
         CLI   GVARCODE+3,GLVXCTL  TEST TRANSFER CONTROL ELEMENT                
         BNE   *+10                NO                                           
         XC    0(24-2,R3),0(R3)                                                 
*                                                                               
         L     R4,GAREA                                                         
         L     R1,GLENGTH                                                       
         BCTR  R1,0                                                             
         EX    R1,EXMOVE           MOVE IN ELEMENT DATA                         
         AHI   R1,3                                                             
         CLI   GVARCODE+3,GLVXCTL  TEST TRANSFER CONTROL ELEMENT                
         BNE   *+8                                                              
         LHI   R1,24               GUARANTEE LENGTH                             
         STC   R1,ELLEN            MOVE IN ELEMENT LENGTH                       
         MVC   ELCODE,GVARCODE+3   MOVE IN ELEMENT CODE                         
*                                                                               
PD1      L     R1,GLOBLEN          CHECK FOR ROOM FOR ANOTHER GLOBAL            
         S     R1,ELEMLEN                                                       
         ZIC   R0,ELLEN                                                         
         AR    R1,R0                                                            
         C     R1,=A(L'GLOBELS)                                                 
         BNL   ERR4                                                             
*                                                                               
PD2      OC    ELEMPTR,ELEMPTR     TEST ELEMENT FOUND                           
         BNZ   PD3                                                              
         BAS   RE,ADDEND           ADD IT TO END OF GLOBAL AREA                 
         B     PDX                                                              
*                                                                               
PD3      C     R0,ELEMLEN          CHECK ELEMENT LENGTH SAME AS GLOBAL          
         BNE   PD4                                                              
         LA    R3,ELEMENT          CHECK ELEMENT CONTENTS AREA SAME             
         L     R4,ELEMPTR                                                       
         L     R1,ELEMLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,EXCLC            COMPARE CONTENTS                             
         BE    EXIT                GLOBAL AREA WILL NOT CHANGE                  
         BAS   RE,OVERLAY          OVERWRITE WITH NEW ELEMENT                   
         B     PDX                                                              
*                                                                               
PD4      BAS   RE,DELETE           DELETE OLD ELEMENT                           
         L     R0,GLOBLEN          ADJUST GLOBAL LENGTH                         
         S     R0,ELEMLEN                                                       
         ST    R0,GLOBLEN                                                       
         BAS   RE,ADDEND           THEN ADD NEW ONE AT END                      
*                                                                               
PDX      OI    GLOBSWS,GLOBUPDT    INDICATE GLOBAL AREA UPDATED                 
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*GET GLOBAL ELEMENT INTO SCREEN FIELD                                 *         
***********************************************************************         
         SPACE 1                                                                
GETF     OC    ELEMPTR,ELEMPTR     TEST ELEMENT EXISTS                          
         BZ    ERR5                                                             
         L     R2,GAREA            POINT R2 TO FIELD HEADER                     
         ZIC   R1,0(R2)            CALCULATE LENGTH OF FIELD DATA               
         SH    R1,=H'8'                                                         
         TM    1(R2),X'02'         TEST EXTENDED FIELD                          
         BZ    GF1                                                              
         SH    R1,=H'8'                                                         
*                                                                               
GF1      LA    R3,8(R2)            POINT R3 TO FIELD DATA                       
         MVI   5(R2),0             CLEAR INPUT LENGTH                           
         BCTR  R1,0                                                             
         EX    R1,EXCLEAR          CLEAR FIELD DATA                             
         LA    R1,1(R1)            RESTORE FIELD LENGTH                         
         L     R0,ELEMLEN          INPUT LENGTH EQUALS                          
         SH    R0,=H'2'            GLOBAL ELEMENT LENGTH MINUS TWO              
         CR    R1,R0               UNLESS THIS EXCEEDS FIELD LENGTH             
         BNH   GF2                                                              
         LR    R1,R0                                                            
GF2      STC   R1,5(R2)            FAKE INPUT LENGTH                            
         L     R4,ELEMPTR          POINT R4 TO GLOBAL ELEMENT DATA              
         LA    R4,2(R4)                                                         
         BCTR  R1,0                                                             
         EX    R1,EXMOVE           MOVE DATA INTO FIELD                         
         LA    R1,1(R1)            RESTORE FIELD LENGTH                         
         MVI   STAT,X'8E'          INITIALIZE ALL STATUS BITS TO BE SET         
*                                                                               
GF3      CLI   0(R3),C'0'          IF CHAR IS A DIGIT                           
         BL    GF4                                                              
         CLI   0(R3),C'9'                                                       
         BH    GF4                                                              
         NI    STAT,X'FB'          THEN CLEAR ALPHA BIT                         
         B     GF7                                                              
*                                                                               
GF4      CLI   0(R3),C'A'          IF CHAR IS BETWEEN C'A' AND C'F'             
         BL    GF5                                                              
         CLI   0(R3),C'F'                                                       
         BH    GF5                                                              
         NI    STAT,X'F7'          THEN CLEAR NUMERIC BIT                       
         B     GF7                                                              
*                                                                               
GF5      CLI   0(R3),C'G'          IF CHAR IS BETWEEN C'G' AND C'Z'             
         BL    GF6                                                              
         CLI   0(R3),C'Z'                                                       
         BH    GF6                                                              
         NI    STAT,X'F5'          THEN CLEAR NUMERIC AND HEX BITS              
         B     GF7                                                              
*                                                                               
GF6      NI    STAT,X'F1'          ELSE CLEAR ALL BITS EXCEPT INPUT BIT         
         B     GF8                                                              
*                                                                               
GF7      LA    R3,1(R3)            NEXT CHAR                                    
         BCT   R1,GF3                                                           
*                                                                               
GF8      MVC   4(1,R2),STAT        SAVE STATUS BYTE IN FIELD HEADER             
         OI    6(R2),X'80'         TRANSMIT FIELD                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*GET GLOBAL ELEMENT INTO DATA FIELD                                   *         
***********************************************************************         
         SPACE 1                                                                
GETD     OC    ELEMPTR,ELEMPTR     CHECK ELEMENT EXISTS                         
         BZ    ERR5                                                             
         L     R3,GAREA            POINT R3 TO DATA FIELD                       
         L     R1,GLENGTH                                                       
*                                                                               
         BCTR  R1,0                CLEAR DATA FIELD                             
         EX    R1,EXCLEAR                                                       
         LA    R1,1(R1)            RESTORE FIELD LENGTH                         
*                                                                               
         L     R0,ELEMLEN          CHECK THAT ELEMENT FITS                      
         SH    R0,=H'2'                                                         
         CR    R0,R1                                                            
         BNH   GETD2                                                            
         L     R4,ELEMPTR                                                       
         CLI   0(R4),GLVXCTL       TEST XFR CTL ELEMENT                         
         BNE   ERR6                NO - THEN AREA MUST BE BIG ENOUGH            
         LA    R4,2(R4)            POINT TO ELEMENT DATA                        
         CH    R1,=H'14'           DID USER GIVE AT LEAST 14 BYTES              
         BL    ERR6                NO - NEED AT LEAST THAT MANY                 
         BCTR  R1,0                                                             
         EX    R1,EXMOVE           GIVE USER BACK AS MUCH AS WE CAN             
         B     EXIT                                                             
*                                                                               
GETD2    L     R4,ELEMPTR          POINT R4 TO GLOBAL ELEMENT DATA              
         LA    R4,2(R4)                                                         
         L     R1,ELEMLEN          SET R1 TO LENGTH OF ELEMENT DATA             
         SH    R1,=H'3'            SUBTRACT ONE EXTRA FOR EXECUTE               
         EX    R1,EXMOVE           MOVE DATA INTO AREA                          
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------*         
*DELETE GLOBAL ELEMENT FROM GLOBAL AREA                               *         
*---------------------------------------------------------------------*         
         SPACE 1                                                                
DELE     OC    ELEMPTR,ELEMPTR     ERROR IF NOT IN GBLOBAL AREAB                
         BZ    ERR5                                                             
         OI    GLOBSWS,GLOBUPDT    INDICATE GLOBAL AREA UPDATED                 
         BAS   RE,DELETE                                                        
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------*         
*CLEAR ALL GLOBAL DATA                                                          
*---------------------------------------------------------------------*         
         SPACE 1                                                                
CLEAR    LA    R0,GLOBELS                                                       
         LA    R1,GLOBELEN                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         XC    ELEMXFR,ELEMXFR     CLEAR XFR CONTROL ELEM ADDR                  
         OI    GLOBSWS,GLOBUPDT    INDICATE GLOBAL AREA UPDATED                 
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------*         
*ROUTINES TO UPDATE GLOBAL VARIABLES                                  *         
*---------------------------------------------------------------------*         
         SPACE 1                                                                
ADDEND   LA    R3,GLOBELS          ADD ELEMENT TO END OF GLOBALS                
         A     R3,GLOBLEN                                                       
         LA    R4,ELEMENT                                                       
         ZIC   R1,ELLEN                                                         
         BCTR  R1,0                                                             
         EX    R1,EXMOVE                                                        
         CLI   0(R3),XFRCTRLQ      TEST IF ADDING XFR CTRL ELEMENT              
         BNE   *+8                                                              
         ST    R3,ELEMXFR                                                       
         LA    R3,1(R3,R1)         FIND NEW END OF AREA                         
         MVI   0(R3),0             MARK END OF GLOBAL AREA                      
         BR    RE                                                               
         SPACE 1                                                                
DELETE   L     R2,ELEMPTR          DELETE ELEMENT POINTED TO BY ELEMPTR         
         C     R2,ELEMXFR                                                       
         BH    DELETE1             OK IF HIGHER THAN XFR CTRL EL                
         BL    *+14                                                             
         XC    ELEMXFR,ELEMXFR     CLEAR IF DELETING XFR CTRL EL                
         B     DELETE1                                                          
         L     R1,ELEMXFR          ADJUST IF LOWER THAN XFR CTRL EL             
         S     R1,ELEMLEN                                                       
         BP    *+6                                                              
         SR    R1,R1                                                            
         ST    R1,ELEMXFR                                                       
DELETE1  LR    R0,R2                                                            
         A     R0,ELEMLEN          R0 POINTS TO NEXT ELEMEMT                    
         LA    R1,GLOBELS                                                       
         A     R1,GLOBLEN                                                       
         LA    R1,1(R1)            ALLOW FOR ZERO ELEMENT AT END                
         SR    R1,R0               R1=NUMBER OF BYTES TO MOVE LEFT              
         BNPR  RE                                                               
         LR    R3,R1               SET LENGTH OF MOVE                           
         MVCL  R2,R0                                                            
         L     R3,ELEMPTR          POINT TO DELETION POINT                      
         BR    RE                                                               
         SPACE 1                                                                
OVERLAY  L     R3,ELEMPTR          OVERWRITE ELEMENT WITH SAME LENGTH           
         LA    R4,ELEMENT                                                       
         L     R1,ELEMLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,EXMOVE                                                        
         BR    RE                                                               
         SPACE 1                                                                
EXMOVE   MVC   0(0,R3),0(R4)       EXECUTED MOVE                                
EXCLEAR  XC    0(0,R3),0(R3)       EXECUTED CLEAR AREA                          
EXCLC    CLC   0(0,R3),0(R4)       EXECUTED COMPARE CHARACTERS                  
         EJECT                                                                  
***********************************************************************         
*ERROR ROUTINES                                                       *         
***********************************************************************         
         SPACE 1                                                                
ERR0     MVI   ERRCODE,GLEBSA      BAD STORAGE AREA                             
         B     ERRX                                                             
ERR1     MVI   ERRCODE,GLEBVC      BAD VARIABLE CODE                            
         B     ERRX                                                             
ERR2     MVI   ERRCODE,GLEBCN      BAD COMMAND NAME                             
         B     ERRX                                                             
ERR3     MVI   ERRCODE,GLEBFL      BAD FIELD LENGTH                             
         B     ERRX                                                             
ERR4     MVI   ERRCODE,GLEGAO      GLOBAL AREA OVERFLOW                         
         B     ERRX                                                             
ERR5     MVI   ERRCODE,GLEGNF      GLOBAL NOT FOUND                             
         B     ERRX                                                             
ERR6     MVI   ERRCODE,GLEGTB      GLOBAL TOO BIG TO FIT IN FIELD               
         B     ERRX                                                             
ERRX     B     EXIT                                                             
*                                                                               
EXIT     L     R1,SAVEDR1          RETURN ERROR CODE IN PARAM LIST              
         MVC   8(1,R1),ERRCODE                                                  
         CLI   ERRCODE,GLEBSA      BAD STORAGE AREA - RA CRAP SO EXIT           
         BE    EXITX                                                            
         L     R0,ELEMXFR          SET DISP TO XFR GLOBAL EL                    
         SR    R0,RA                                                            
         BP    *+6                                                              
         SR    R0,R0                                                            
         STH   R0,GLOBXFR                                                       
EXITX    XMOD1 1                                                                
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLER'S ADDRESSING MODE             
         EJECT                                                                  
***********************************************************************         
*READ IN SAVED STORAGE                                                *         
***********************************************************************         
         SPACE 1                                                                
READTWA  NMOD1 1800,**RTWA**                                                    
*                                                                               
READTWA0 SR    R2,R2               GLOBAL AREA IN TWA#0                         
         ICM   R2,3,TNUM                                                        
         GOTO1 CDATAMGR,(R1),(X'00',=C'DMREAD'),=C'TEMPSTR',0,(RC)              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LR    R0,RA               POINT TO GLOBAL AREA IN TASK                 
         LA    R1,GLOBALEN                                                      
         LH    R2,=Y(CHKPTGLD)     POINT TO GLOBAL DATA IN TWA#0                
         AR    R2,RC                                                            
         LR    R3,R1                                                            
         MVCL  R0,R2               MOVE TWA0 GLOBALS INTO TASK AREA             
         MVI   GLOBSWS,GLOBREAD    SET GLOBALS READ                             
*                                                                               
READTWA1 L     RE,=V(SSB)          CHECK VALIDITY OF DATE/TIME STAMP            
         CLC   GLOBDATE(8),SSBSDATE-SSBD(RE)                                    
         BNL   READTWAX                                                         
         LR    R0,RA               INITIALISE GLOBAL INFO FOR THIS TRM          
         LA    R1,GLOBALEN                                                      
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         TIME  BIN                 R0=TIME(1/100 SEC),R1=DATE(JULIAN)           
         ST    R0,GLOBTIME                                                      
         ST    R1,GLOBDATE                                                      
         MVI   GLOBSWS,GLOBREAD+GLOBUPDT                                        
*                                                                               
READTWAX XIT1                                                                   
         EJECT                                                                  
XFRCTRLQ EQU   4                   TRANSFER CONTROL ELEMENT                     
CDATAMGR DC    V(DATAMGR)                                                       
RECLEN   DC    H'0'                SET TO LEN OF TWA#0 REC ON 1ST CALL          
         LTORG                                                                  
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
GPARMS   DS    0X                  PARAMETERS PASSED TO GLOBBER                 
GCOMMAND DS    A                   A(COMMAND)                                   
GAREA    DS    A                   A(DATA AREA OR FIELD HEADER)                 
GLENGTH  DS    F                   LENGTH OF DATA OR ZERO(FOR FIELDS)           
GVARCODE DS    F                   GLOBAL VARIABLE CODE                         
GFLAGS   DS    X                   FLAGS                                        
GFLAGP5  EQU   X'80'               ADDRESS OF GLOBALS IS IN P5                  
GPARMADR DS    XL3                 A(GLOBAL AREA) IF GFLAGP5 SET                
GPARML   EQU   *-GPARMS                                                         
*                                                                               
SAVEDR1  DS    F                                                                
ATSKGLOB DS    F                                                                
*                                                                               
GLOBLEN  DS    F                   LENGTH OF GLOBAL VARIABLE FROM POOL          
ELEMPTR  DS    A                   POINT TO GLOBAL VARIABLE                     
ELEMLEN  DS    F                   LENGTH OF GLOBAL VARIABLE                    
ELEMXFR  DS    A                   ADDRESS OF XFR CONTROL VARIABLE              
*                                                                               
ELEMENT  DS    0CL256              AREA FOR BUILDING NEW GLOBAL VAR             
ELCODE   DS    X                                                                
ELLEN    DS    X                                                                
ELDATA   DS    CL254                                                            
*                                                                               
ERRCODE  DS    X                   ERROR RETURN CODE                            
STAT     DS    X                   USED TO FORMULATE VALIDITY BITS              
*                                                                               
WRKX     EQU   *                                                                
         EJECT                                                                  
GLOBD    DSECT                     GLOBAL AREA DSECT                            
GLOBHDR  DS    0XL12               GLOBAL HEADER DATA                           
GLOBDATE DS    PL4                 DATE OF LAST SAVE OF GLOBALS                 
GLOBTIME DS    XL4                 TIME OF LAST SAVE OF GLOBALS                 
GLOBXFR  DS    XL2                 DISPLACEMENT TO XFR CONTROL ELEMENT          
         DS    XL1                 N/D                                          
GLOBSWS  DS    XL1                 GLOBBER SWITCHES                             
GLOBREAD EQU   X'80'               SET IF TWA READ                              
GLOBUPDT EQU   X'40'               SET IF GLOBALS UPDATED                       
GLOBCLR  EQU   X'20'               SET TO CLEAR GLOBALS                         
*                                                                               
GLOBELS  DS    XL500               GLOBAL DATA AREA                             
*                                                                               
GLOBELEN EQU   *-GLOBELS           LENGTH OF GLOBAL ELEMENTS                    
GLOBALEN EQU   *-GLOBD             LENGTH OF GLOBAL DATA AREA                   
         SPACE 2                                                                
* DDGLOBEQUS                                                                    
       ++INCLUDE DDGLOBEQUS                                                     
         EJECT                                                                  
* FACHKPT                                                                       
       ++INCLUDE FACHKPT                                                        
         SPACE 2                                                                
* FADSECTS                                                                      
       ++INCLUDE FADSECTS                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FAGLOBBER 05/01/02'                                      
         END                                                                    
