*          DATA SET BULDDCNT   AT LEVEL 002 AS OF 11/21/85                      
*CATALP BULDDCNT                                                                
         TITLE 'LDCOUNT - BUDGET SYSTEM - DIR REC TYPE COUNT/PRINT'             
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'81'=COUNT DELETED PASSIVE            
*                           X'FF'=PRINT                                         
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 WRKX-WRKD,BULDDCNT                                               
         USING WRKD,RC                                                          
*                                                                               
         MVC   BYTE,0(R1)                                                       
         CLI   BYTE,X'FF'                                                       
         BE    LDCOUNTP                                                         
         CLI   BYTE,X'81'                                                       
         BE    LDCOUNTX                                                         
         L     R2,0(R1)                                                         
         USING BURECD,R2                                                        
*                                                                               
         LA    RE,FHD                                                           
         OC    BUKEY(L'BUKEY-1),BUKEY                                           
         BZ    LDCOUNT8                                                         
         LA    RE,FTR                                                           
         CLI   BUKEY,X'FF'                                                      
         BE    LDCOUNT8                                                         
*                                                                               
         CLI   BUKSYS,C'B'         TEST BUDGET SYSTEM RECORD                    
         BNE   LDCOUNT6                                                         
         CLI   BUKRTYP,BUKRTYPQ                                                 
         BNE   LDCOUNT2                                                         
         LA    RE,BMS                                                           
         OC    BUKNODE(L'BUKNODE+L'BUKCODE+L'BUSUBKEY),BUKNODE                  
         BZ    LDCOUNT8                                                         
         LA    RE,BCL                                                           
         TM    BUKCTYP,BUKCCLT                                                  
         BNZ   LDCOUNT8                                                         
         LA    RE,BPR                                                           
         TM    BUKCTYP,BUKCPRO                                                  
         BNZ   LDCOUNT8                                                         
         LA    RE,BPL                                                           
         TM    BUKCTYP,BUKCPLN                                                  
         BNZ   LDCOUNT8                                                         
         LA    RE,BOU                                                           
         TM    BUKCTYP,BUKCOUT                                                  
         BNZ   LDCOUNT8                                                         
         CLI   BUKCTYP,BUKCSUB                                                  
         BNE   LDCOUNT4                                                         
         LA    RE,BDD                                                           
         CLI   BUDSUB,BUDSUBQ                                                   
         BE    LDCOUNT8                                                         
         LA    RE,BTX                                                           
         CLI   BUTSUB,BUTSUBQ                                                   
         BE    LDCOUNT8                                                         
         LA    RE,BDV                                                           
         CLI   BUVSUB,BUVSUBQ                                                   
         BE    LDCOUNT8                                                         
         B     LDCOUNT4                                                         
*                                                                               
         USING BUCRECD,R2                                                       
LDCOUNT2 LA    RE,BOUP                                                          
         CLI   BUCRTYP,BUCRTYPQ                                                 
         BE    LDCOUNT8                                                         
*                                                                               
LDCOUNT4 LA    RE,BXX                                                           
         B     LDCOUNT8                                                         
*                                                                               
LDCOUNT6 LA    RE,XXX                                                           
         B     LDCOUNT8                                                         
*                                                                               
LDCOUNT8 TM    BUKCTL-BURECD(R2),X'80'                                          
         BZ    *+8                                                              
         LA    RE,4(RE)                                                         
         ICM   RF,15,0(RE)                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,15,0(RE)                                                      
         LA    RE,FTS                                                           
         TM    BUKCTL-BURECD(R2),X'80'                                          
         BZ    *+8                                                              
         LA    RE,4(RE)                                                         
         ICM   RF,15,0(RE)                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,15,0(RE)                                                      
         B     LDCOUNTX                                                         
         EJECT                                                                  
LDCOUNTP L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   MID1(L'MYMID1+L'MYMID1A),MYMID1                                  
         MVC   MID2(L'MYMID2+L'MYMID2A),MYMID2                                  
*                                                                               
         LA    R2,COUNTS                                                        
LDCOUNTQ CLI   0(R2),X'FF'                                                      
         BE    LDCOUNTX                                                         
         OC    0(8,R2),0(R2)                                                    
         BZ    LDCOUNTT                                                         
         MVC   P(40),8(R2)                                                      
         ICM   R0,15,0(R2)                                                      
         BNZ   *+14                                                             
         MVC   P+44(7),=7C'.'                                                   
         B     LDCOUNTR                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+44(7),DUB                                                      
LDCOUNTR ICM   R1,15,4(R2)                                                      
         BNZ   *+14                                                             
         MVC   P+54(7),=7C'.'                                                   
         B     LDCOUNTS                                                         
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+54(7),DUB                                                      
LDCOUNTS AR    R1,R0                                                            
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+64(7),DUB                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
LDCOUNTT LA    R2,L'COUNTS(R2)                                                  
         B     LDCOUNTQ                                                         
*                                                                               
LDCOUNTX XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
MYMID1   DC    CL44'SYSTEM NAME     RECORD TYPE'                                
MYMID1A  DC    C' ACTIVE   DELETED    TOTAL '                                   
MYMID2   DC    CL44'-----------     -----------'                                
MYMID2A  DC    C'RECORDS   RECORDS   RECORDS'                                   
         SPACE 1                                                                
COUNTS   DS    0XL48                                                            
FHD      DC    8X'0',CL40'                FILE HEADER RECORDS'                  
FTR      DC    8X'0',CL40'                FILE TRAILER RECORDS'                 
XXX      DC    8X'0',CL40'                UNKNOWN RECORD TYPES'                 
BMS      DC    8X'0',CL40'BUDGET SYSTEM - MASTER RECORDS (AGENCY)'              
BCL      DC    8X'0',CL40'BUDGET SYSTEM - CLIENT RECORDS'                       
BPR      DC    8X'0',CL40'BUDGET SYSTEM - PRODUCT RECORDS'                      
BPL      DC    8X'0',CL40'BUDGET SYSTEM - PLAN RECORDS'                         
BOU      DC    8X'0',CL40'BUDGET SYSTEM - OUTLINE RECORDS'                      
BOUP     DC    8X'0',CL40'BUDGET SYSTEM - OUTLINE PASSIVE PTRS'                 
BDD      DC    8X'0',CL40'BUDGET SYSTEM - DATA TYPE RECORDS'                    
BTX      DC    8X'0',CL40'BUDGET SYSTEM - TEXT RECORDS'                         
BDV      DC    8X'0',CL40'BUDGET SYSTEM - DATA VALUE RECORDS'                   
BXX      DC    8X'0',CL40'BUDGET SYSTEM - UNKNOWN RECORD TYPES'                 
FTS      DC    8X'0',CL40'                ** FILE TOTALS **'                    
COUNTSX  DC    X'FF'                                                            
         EJECT                                                                  
*DDDPRINT                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* BUGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE BUGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
WRKD     DSECT                                                                  
BYTE     DS    X                                                                
DUB      DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002BULDDCNT  11/21/85'                                      
         END                                                                    
