*          DATA SET GEFILACV   AT LEVEL 002 AS OF 06/11/99                      
*CATALP GEFILACV                                                                
         TITLE 'GENERALISED ACTIVITY OVERLAY'                                   
* TCLE 002 11JUN99 - DON'T DUMP IF CAN'T FIND TERMINAL #                        
FILACV   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILACV,RR=RE                                                 
         USING TWAD,RA                                                          
         USING WORKD,R9                                                         
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         L     RC,AOVERWRK                                                      
         USING OVERWRKD,RC                                                      
         L     R6,ASVEBLK                                                       
         USING TWSAVE,R6                                                        
         ST    RE,BORELO                                                        
         MVC   SVPARMS,0(R1)                                                    
         ST    R1,CALLR1                                                        
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
         SPACE 1                                                                
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/4                                                      
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         SPACE 1                                                                
EXIT     L     R1,CALLR1           RETURN PARAMETERS                            
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
         XIT1  ,                   EXIT WITH CC SET                             
         EJECT                                                                  
***********************************************************************         
* TABLE ITERATION ROUTINE  - EXPECTS RF TO HOLD A(TABLE)              *         
*                          - EXPECTS R1 TO HOLD VERB                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK                                                           
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATED VERB                        
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE THIS TABLE                           
*                                                                               
ITER02   ICM   RF,15,OBJADR        ROUTINE TO HANDLE THE VERB                   
         LA    R1,SVPARMS                                                       
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   L     R1,SVPARMS                                                       
         LA    RF,TABLEOO          KNOWN OBJECTS                                
         B     ITER                                                             
*                                                                               
TABLEOO  DS    0X                                                               
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(EXITH)                               
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     MVI   CSNEXTPF,PFK12                                                   
         OI    BCINDS2,BCIACTCP                                                 
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1=DATA IDENTIFIER                           
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         L     R2,SVPARMS4                                                      
         USING FDRRECD,R2                                                       
         XR    R1,R1                                                            
         IC    R1,SVPARMS3         GET GLOBAL VERB                              
         LA    RF,DTATABL          TABLE OF GLOBAL VERBS                        
         B     ITER                ITERATE TABLE                                
*                                                                               
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
         USING KNOWTABD,RF                                                      
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
*                                                                               
         LM    R1,R2,SVPARMS3      R1 HOLDS VERB                                
         USING FDRRECD,R2          R2 HOLDS A(RECORD)                           
         BR    RF                                                               
         SPACE 1                                                                
*                                                                               
DTATABL  DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
         SPACE 1                                                                
KNOWTAB  DC    AL2(00085),AL4(ADTEDTA)   RECORD ADDED ON                        
         DC    AL2(00086),AL4(ATIMDTA)   AT                                     
         DC    AL2(00087),AL4(APERDTA)   BY                                     
         DC    AL2(00088),AL4(ATERDTA)   AT TERMINAL                            
         DC    AL2(00089),AL4(AUSRDTA)   CONNECTED TO                           
         DC    AL2(00090),AL4(CDTEDTA)   RECORD CHANGED ON                      
         DC    AL2(00091),AL4(CTIMDTA)   AT                                     
         DC    AL2(00092),AL4(CPERDTA)   BY                                     
         DC    AL2(00093),AL4(CTERDTA)   AT TERMINAL                            
         DC    AL2(00094),AL4(CUSRDTA)   CONNECTED TO                           
         DC    AL2(EOT)                                                         
*                                                                               
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FILACV   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ADD USER                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
AUSRDTA  LA    RF,AUSRTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
AUSRTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISAUSR)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD USER                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISAUSR  LA    R3,GCRACADD                                                      
         MVC   FVIFLD(L'CTDSC),QUERY                                            
         OC    RACUSER,RACUSER                                                  
         BZ    EXITOK                                                           
*                                                                               
         GOTOX ('GETUID',AGROUTS),RACUSER                                       
         MVC   FVIFLD(L'CTDSC),BCWORK                                           
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR CHANGE USER                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
CUSRDTA  LA    RF,CUSRTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
CUSRTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISCUSR)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY CHANGE USER                                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISCUSR  LA    R3,GCRACCHA                                                      
         MVC   FVIFLD(L'CTDSC),QUERY                                            
         OC    RACUSER,RACUSER                                                  
         BZ    EXITOK                                                           
         GOTOX ('GETUID',AGROUTS),RACUSER                                       
         MVC   FVIFLD(L'CTDSC),BCWORK                                           
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ADD TERMINAL                                        *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
ATERDTA  LA    RF,ATERTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
ATERTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISATER)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD TERMINAL                                                *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISATER  LA    R3,GCRACADD                                                      
         MVC   FVIFLD(L'QUERY),QUERY                                            
         OC    RACTERM,RACTERM                                                  
         BZ    EXITOK                                                           
         GOTOX ('GETTERM',AGROUTS),BOPARM,RACTERM                               
         BL    *+14                                                             
         MVC   FVIFLD(L'BCWORK),BCWORK                                          
         B     EXITOK                                                           
         MVI   FVIFLD+1,C'#'       GETTERM COULDN'T MATCH THIS NUMBER           
         XR    R0,R0               EDIT AS ?#999999                             
         ICM   R0,3,RACTERM                                                     
         CVD   R0,BCDUB                                                         
         OI    BCDUB+7,X'0F'                                                    
         UNPK  FVIFLD+2(6),BCDUB                                                
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR CHANGE TERMINAL                                     *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
CTERDTA  LA    RF,CTERTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
CTERTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISCTER)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY CHANGE TERMINAL                                             *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISCTER  LA    R3,GCRACCHA                                                      
         MVC   FVIFLD(L'QUERY),QUERY                                            
         OC    RACTERM,RACTERM                                                  
         BZ    EXITOK                                                           
         GOTOX ('GETTERM',AGROUTS),BOPARM,RACTERM                               
         BL    *+14                                                             
         MVC   FVIFLD(L'BCWORK),BCWORK                                          
         B     EXITOK                                                           
         MVI   FVIFLD+1,C'#'       GETTERM COULDN'T MATCH THIS NUMBER           
         XR    R0,R0               EDIT AS ?#999999                             
         ICM   R0,3,RACTERM                                                     
         CVD   R0,BCDUB                                                         
         OI    BCDUB+7,X'0F'                                                    
         UNPK  FVIFLD+2(6),BCDUB                                                
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ADD PERSON                                          *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
APERDTA  LA    RF,APERTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
APERTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISAPER)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD PERSON                                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISAPER  LA    R3,GCRACADD                                                      
         MVC   FVIFLD(8),QUERY                                                  
         OC    RACPERS,RACPERS                                                  
         BZ    EXITOK                                                           
         GOTOX ('GETPID',AGROUTS),RACPERS                                       
         MVC   FVIFLD(8),BCWORK                                                 
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR CHANGE PERSON                                       *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
CPERDTA  LA    RF,CPERTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
CPERTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISCPER)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD CHANGE PERSON                                           *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISCPER  LA    R3,GCRACCHA                                                      
         MVC   FVIFLD(8),QUERY                                                  
         OC    RACPERS,RACPERS                                                  
         BZ    EXITOK                                                           
         GOTOX ('GETPID',AGROUTS),RACPERS                                       
         MVC   FVIFLD(8),BCWORK                                                 
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ADD TIME                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
ATIMDTA  LA    RF,ATIMTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
ATIMTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISATIM)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD TIME                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISATIM  LA    R3,GCRACADD                                                      
         MVC   FVIFLD(8),QUERY                                                  
         OC    RACTIME,RACTIME                                                  
         BZ    EXITOK                                                           
         UNPK  BCDUB,RACTIME                                                    
         OI    BCDUB+7,C'0'                                                     
         MVC   FVIFLD(2),BCDUB+2                                                
         MVI   FVIFLD+2,C'.'                                                    
         MVC   FVIFLD+3(2),BCDUB+4                                              
         MVI   FVIFLD+5,C'.'                                                    
         MVC   FVIFLD+6(2),BCDUB+6                                              
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR CHANGE TIME                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
CTIMDTA  LA    RF,CTIMTBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
CTIMTBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISCTIM)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY CHANGE TIME                                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISCTIM  LA    R3,GCRACCHA                                                      
         MVC   FVIFLD(8),QUERY                                                  
         OC    RACTIME,RACTIME                                                  
         BZ    EXITOK                                                           
         UNPK  BCDUB,RACTIME                                                    
         OI    BCDUB+7,C'0'                                                     
         MVC   FVIFLD(2),BCDUB+2                                                
         MVI   FVIFLD+2,C'.'                                                    
         MVC   FVIFLD+3(2),BCDUB+4                                              
         MVI   FVIFLD+5,C'.'                                                    
         MVC   FVIFLD+6(2),BCDUB+6                                              
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ADD DATE                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
ADTEDTA  LA    RF,ADTETBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
ADTETBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISADTE)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ADD DATE                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISADTE  LA    R3,GCRACADD                                                      
         OC    RACDATE,RACDATE                                                  
         BNZ   *+14                                                             
         MVC   FVIFLD(8),QUERY                                                  
         B     EXITOK                                                           
*                                                                               
         GOTOX VDATCON,BOPARM,(1,RACDATE),(17,FVIFLD)                           
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR CHANGE DATE                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
CDTEDTA  LA    RF,CDTETBL          TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
CDTETBL  DC    AL1(DDIS),AL1(0,0,0),AL4(DISCDTE)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY CHANGE DATE                                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING RACELD,R3                                                        
DISCDTE  LA    R3,GCRACCHA                                                      
         OC    RACDATE,RACDATE                                                  
         BNZ   *+14                                                             
         MVC   FVIFLD(8),QUERY                                                  
         B     EXITOK                                                           
*                                                                               
         GOTOX VDATCON,BOPARM,(1,RACDATE),(17,FVIFLD)                           
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* NTRSES OBJECT                                                       *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(INITIAL LIST KEY)                                        *         
* P4 HOLDS A(MEMORY COVERED BY FESD)                                  *         
***********************************************************************         
         SPACE 1                                                                
NTRSES   LM    R0,R3,SVPARMS                                                    
         USING SSAVD,R2                                                         
         USING FESD,R3                                                          
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DC    AL1(SNTRIN),AL1(0,0,0),AL4(NTRIN)                                
         DC    AL1(EOT)                                                         
         SPACE 1                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
NTRIN    OI    SNINDS1,SNIUSECR                                                 
         B     EXITOK                                                           
         SPACE 1                                                                
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
FF       EQU   X'FF'                                                            
QUERY    DC    40C'?'                                                           
         SPACE 1                                                                
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
*        GEFILWORK                                                              
         PRINT OFF                                                              
       ++INCLUDE GEFILWORK                                                      
         PRINT ON                                                               
*        CTGENFILE                                                              
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
*        DDDDEQUS                                                               
         PRINT OFF                                                              
       ++INCLUDE DDDDEQUS                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002GEFILACV  06/11/99'                                      
         END                                                                    
