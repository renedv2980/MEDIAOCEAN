*          DATA SET SPFILCON   AT LEVEL 138 AS OF 07/20/20                      
*CATALP SPFILCON  <=====                                                        
                                                                                
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-45362  07/06/20 HONOR NEW STW ESTIMATES ON I2 FIELD ON I2N*         
* AKAT SPEC-17069  10/13/17 LEV 132 CHANGES DSSUP-2974 ONLY FOR I2    *         
* MHER SPEC-8436   06/26/17 UPDATES FOR COMSCORE                      *         
* AKAT MOSTK-62    03/21/16 NEW -CM BAND SUPPORT FOR IHEART RADIO     *         
***********************************************************************         
* 22FEB90 STUPID  DEMO ADJ FACTORS                                              
* 02JUL91 STUPID  CLIENT GROUPS                                                 
* 01NOV91 STUPID  CLIENT GROUPS FOR NETPAK                                      
* 04DEC92 SUPPORT FOR LOCAL CABLE                                               
* 02SEP94 SUPPORT FOR STAPACK (INTERCEPT MSPACK/MSUNPK CALLS)                   
* 08SEP95 SET RQFLAG IF THERE IS PW ADJ BILLING FOR ANY MARKET                  
*         AND UNFORTUNATELY MOVE NBSLVTST FOR ADDRESSABILITY !                  
* 29MAR96 FOR AGENCY CK, SKIP MASTER CLIENTS ON ALL CLT REQUESTS                
* 02APR98 CHANGES FOR SOFT CABLE SEQUENCE NUMBERS                               
* 17SEP01 SUPPRESS CANADIAN CABLE NETWORK ON MSPACK CALLS                       
* 09OCT01 PROCESS CABLE STATIONS IN ALPHA SEQUENCE                              
* 06DEC01 ALLOW INVALID MEDIA CODE FOR SP74 REQUEST                             
* 15FEB02 PUT LABEL=* ON NTR1 FOR EYE-CATCHER                                   
* 09JUL02 ETYPE FILTERS                                                         
* 07JUL03 FIX MISSING STALAST FOR NETWORKS IN ALPHA SEQ WHEN ONLY               
*         BUY ON LAST NETWORK IS DELETED                                        
* 09JUL03 CHANGE MGRP LOGIC SO CODES < C'A' ARE 'ALL' CLIENT                    
* 23MAR04 FIX FOR CABLE >= X'E8'                                                
* 09AUG05 CHANGE FOR 2 CHAR OFFICES                                             
* 09AUG05 CHANGE FOR 2 CHAR OFFICES AGAIN                                       
* 31JAN07 SUPPORT FOR MEDIA * REQUESTS                                          
* 01APR08 SUPPORT FOR EXTENDED BUYLINES                                         
* 22FEB11 FIX CABLE NETWORK FILTER BUG                                          
* 08AUG11 2-BYTE BUYLINE SUPPORT                                                
* 19FEB13 DETACH TO SHARED MEMORY IN RUNLAST                                    
* 20FEB13 FIX LHI FOR BUYLISTL MAGNITUDE                                        
* 12SEP14 FILTER ON ETYPE FOR SINGLE ESTIMATE REQUEST                           
*======================================================================         
                                                                                
*=================================================================              
* IMPORTANT NOTE ABOUT ID SEQUENCE PROCESSING                                   
* THERE ARE TWO UNRELATED USES OF THE WORDS 'ID PROCESSING'                     
* 1. FCRDBUYS IS SET TO A C'I' IF THE REQUEST IS BY MKTGRP AND                  
*    THE CHARACTER MATCHES THE CLIENT PROFILE MKTGRP CHARACTER.                 
*    THIS IS NOW HANDLED IN THE BUILDING OF THE ACTMKT LIST                     
*    WHERE BUYS ARE READ UNTIL ONE MATCHING THE CORRECT MKTGRP                  
*    IS FOUND AND THE ACTIVITY FLAG IS SET ACCORDINGLY.                         
* 2. QBYID IS SET TO A C'Y'. THIS IS HANDLED IN THE FRSTBUY/NEXTBUY             
*    CODE, WHERE THE ENTIRE STATION IS READ, A LIST OF ID'S BUILT               
*    AND THEN SORTED.                                                           
* I HOPE THIS HELPS!  MHER 16APR08                                              
*                                                                               
* IT TURNS OUT THAT AFTER SCANNING EVERY SPTFILE, THERE ARE NO REAL             
* CLIENT RECORDS THAT HAVE A VALUE G-K IN CXTRA+2. SO FCRDBUYS IS               
* NEVER SET TO AN I. SO THERE IS NO WAY TO TEST THE NEW AMLID CODE.             
* NEVERTHELESS, IT'S GOT TO BE BETTER THAN CODE THAT COULD NOT WORK.            
* IF YOU END UP IN THAT CODE, YOU SHOULDN'T BE THERE. MHER 28MAY08              
*=================================================================              
         TITLE 'SPOTPAK MAIN FILE CONTROLLER'                                   
SPFILCON CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPFILCON                                                       
*                                                                               
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         LA    RC,2048(R9)                                                      
         LA    RC,2048(RC)                                                      
         USING SPFILCON+4096,R9,RC                                              
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R8,2048(RA)                                                      
         LA    R8,2048(R8)                                                      
         USING SPWORKD+4096,R8                                                  
*                                                                               
         STM   R8,RC,FCREGS                                                     
         ST    RD,BASERD                                                        
                                                                                
*=============================================================*                 
* OVERRIDE MSPACK/MSUNPK ADDRESSES                            *                 
* SO CAN REDIRECT TO STAPACK                                  *                 
*=============================================================*                 
         MVC   MSPACK,=A(FCMSPACK)                                              
         MVC   MSUNPK,=A(FCMSUNPK)                                              
         EJECT                                                                  
* SET UP BRANCH TABLE VALUES                                                    
         LA    R0,FCTABLEN         NUMBER OF ENTRIES                            
         SR    R1,R1               ROUTINE NUMBER                               
         LA    RE,FCNXTREQ         FIRST ROUTINE                                
         LA    RF,FCNEXT           NTR STMT ADDRESS                             
*                                                                               
FC10     ST    RF,0(RE)            STORE COMMON ENTRY                           
         STC   R1,0(RE)            ROUTINE NUMBER                               
         LA    R1,4(R1)                                                         
         LA    RE,4(RE)                                                         
         BCT   R0,FC10                                                          
*                                                                               
         L     R0,=A(ENDREQ)                                                    
         ST    R0,AENDREQ                                                       
* FIRST TIME PROCESSING                                                         
         L     R1,=V(MASTC)        SET UP FOR STXIT TO COME BACK HERE           
         USING MASTD,R1                                                         
         LA    RE,FC32                                                          
         STM   R0,RF,MCRUNLST                                                   
         LA    RE,FC42                                                          
         STM   R0,RF,MCREQLST                                                   
* SET UP PARAMS FOR MULTIPLE REQUESTS                                           
         LA    RF,QCONTREQ                                                      
         LA    RE,QAREA                                                         
         SR    RF,RE                                                            
         LA    RF,1(RF)            ADD 1 TO GET COL NUM                         
         STC   RF,MCRQCOL          SET CONT REQ COL NUM                         
         MVI   MCRQCHAR,C'*'       SET CONT REQ CHAR                            
         DROP  R1                                                               
*                                                                               
FC20     MVI   MODE,RUNFRST                                                     
         GOTO1 GO                                                               
*                                                                               
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   COMSCORE,MCCSMODE   SAVE COMSCORE PASS NUM                       
         CLI   COMSCORE,C'1'                                                    
         JL    FC30                                                             
         GOTOR =V(JOBREG)                                                       
         DROP  R1                                                               
*                                                                               
FC30     GOTO1 FCNXTREQ                                                         
         BE    FC40                                                             
*                                                                               
FC32     MVI   MODE,RUNLAST                                                     
         GOTOR =V(JOBDONE)                                                      
         GOTO1 GO                                                               
         GOTO1 =V(DMSHMUSS),DMCB,(0,=CL8'DETACH'),(0,=CL8'WRKF')                
         GOTO1 =V(DMSHMUSS),DMCB,(0,=CL8'DETACH'),(0,=CL8'PRTQ')                
         GOTO1 =V(DMSHMUSS),DMCB,(0,=CL8'DETACH'),(0,=CL8'STAPACK')             
         B     FCEXT                                                            
*                                                                               
FC40     L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   COMSCORE,MCCSMODE   SAVE COMSCORE PASS NUM                       
         DROP  R1                                                               
*                                                                               
         CLI   COMSCORE,C'1'                                                    
         JL    FC40X                                                            
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    RF,=C'ALLOC'        ALLOC FOR PASS 1                             
         CLI   COMSCORE,C'1'                                                    
         JE    *+8                                                              
         LA    RF,=C'START'        START FOR PASS 2                             
         ST    RF,DMCB+0                                                        
*                                                                               
         L     RE,ADBLOCK                                                       
         ST    RE,DMCB+4                                                        
         MVC   DBCOMFCS-DBLOCK(4,RE),ACOMFACS                                   
*                                                                               
         L     RF,VCOMINTER                                                     
         GOTO1 (RF),DMCB                                                        
*                                                                               
FC40X    L     R1,=A(STAWORK)                                                   
         XC    0(L'STAWORK,R1),0(R1)                                            
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'V'        REQUEST STAPACK VERSION                      
         MVC   STAPAGY,RCAGENCY                                                 
         GOTO1 VSTAPACK,(R1)                                                    
         MVC   PACKVRSN,STAPVRSN                                                
         DROP  R1                                                               
*                                                                               
         XC    WORK,WORK           CLEAR WORK                                   
         LA    R2,WORK             R2=WORK                                      
         USING GETBUYD,R2          GETBUY DSECT                                 
         XC    0(GETBUYL,R2),0(R2) CLEAR GETBUY BLOCK                           
         MVC   GBYCOMF,ACOMFACS    A(COMFACS)                                   
         MVI   GBYACT,GBYINIT      INIT                                         
         MVC   GBYAGY,QAGY         AGENCY                                       
*                                                                               
         GOTO1 VGETBUY,0(R2)       INIT CALL TO SET 1 OR 2 BYTE BUYLINE         
         MVC   VGETBUY(1),GBY1OR2  1 OR 2 BYTE BUYLINE                          
         DROP  R2                                                               
*                                                                               
FC41     MVI   RQFLAG,0            RESET REQUEST FLAGS                          
         MVI   MODE,REQFRST                                                     
         GOTO1 GO                                                               
*                                                                               
         GOTO1 SUBCON,DMCB,WORKC                                                
*                                                                               
FC42     MVI   MODE,REQLAST                                                     
         GOTO1 GO                                                               
*                                                                               
         CLI   COMSCORE,0                                                       
         BE    FC42X               NO COMSCORE WORK                             
         XC    DMCB,DMCB                                                        
         LA    RF,=C'CLOSE'        PASS 1                                       
         CLI   COMSCORE,C'1'                                                    
         JE    *+8                                                              
         LA    RF,=C'END'          PASS 2                                       
         ST    RF,DMCB+0           P1                                           
*                                                                               
         L     RE,ADBLOCK                                                       
         ST    RE,DMCB+4                                                        
         MVC   DBCOMFCS-DBLOCK(4,RE),ACOMFACS                                   
*                                                                               
         L     RF,VCOMINTER                                                     
         GOTO1 (RF),DMCB                                                        
*                                                                               
         CLI   COMSCORE,C'2'       IF JUST FINISHED PASS 2                      
         JNE   FC42A                                                            
*                                                                               
         GOTO1 =V(JCREQEND)                                                     
         J     FC42X               GO ON TO NEXT REQUEST                        
*                                                                               
FC42A    CLI   COMSCORE,C'1'       TEST COMSCORE PASS 1                         
         JNE   FC42X                                                            
         GOTOR =V(JOBWAIT)                                                      
*                                                                               
FC42X    L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   COMSCORE,MCCSMODE   SAVE COMSCORE PASS NUM                       
*                                                                               
         OC    MCRHACCS,MCRHACCS   TEST ANY LIMIT ACCESS                        
         BZ    FC44                NO                                           
         DROP  R1                                                               
         LARL  RE,CLTSKIP                                                       
         CLI   0(RE),C'Y'          TEST ANY CLIENTS EXCLUDED                    
         BNE   FC44                                                             
*****========================================                                   
         B     FC44                NOP MESSAGE PER ABEA 24AUG12                 
*                                  TO BE SURE DOWNLOADS NOT MESSED UP           
*****========================================                                   
         L     R0,HEADHOOK         SAVE HEADHOOK ADDRESS                        
         XC    HEADHOOK,HEADHOOK   THANKS CURTIS!                               
         MVC   P(45),=C'REPORT DOES NOT INCLUDE UNAUTHORIZED CLIENTS.'          
         GOTO1 REPORT                                                           
         ST    R0,HEADHOOK         RESTORE ADDRESS                              
         MVI   FORCEHED,C'Y'       MAKE SURE NEXT REQ IS NEW PAGE               
*                                                                               
FC44     B     FC30                                                             
*                                                                               
FCEXT    XIT1                                                                   
         EJECT                                                                  
*                                                                               
* THE LABEL=* IS SAFE (NOT HAVING IT IS POTENTIALLY DANGEROUS). DONT            
* EVEN THINK ABOUT REMOVING THE RF USING OR CHANGING THE BASE=FCRB              
*                                                                               
         USING *,RF                                                             
FCNEXT   NTR1  BASE=FCRB,LABEL=*                                                
         DROP  RF                                                               
*                                                                               
         LM    R8,RC,FCREGS                                                     
         MVC   FCDMBTS(2),DMINBTS  SAVE DMINBTS/DMOUTBTS                        
         MVI   DMINBTS,0                                                        
*                                                                               
         SRL   RF,24                                                            
         B     FCJUMP(RF)                                                       
*                                                                               
EQXIT    CR    RB,RB               SET CC =                                     
         J     *+6                                                              
NEQXIT   LTR   RB,RB               SET CC NOT =                                 
*                                                                               
         MVC   DMINBTS(2),FCDMBTS  RESTORE DMINBTS/DMOUTBTS                     
         XIT1                                                                   
*                                                                               
FCREGS   DC    3A(0)               SPFILCON R8-RA                               
FCRB     DC    A(0)                                                             
FCRC     DC    A(0)                                                             
FCDMBTS  DC    2X'00'                                                           
         SPACE 2                                                                
FCJUMP   B     NEXTREQ             00                                           
         B     NEXTCLT             04                                           
         B     NEXTPGR             08                                           
         B     NEXTPRD             12                                           
         B     NEXTEST             16                                           
         B     NEXTMGR             20                                           
         B     NEXTMKT             24                                           
         B     NEXTSTA             28                                           
         B     FRSTBUY             32                                           
         B     NEXTBUY             36                                           
         B     FRSTGOAL            40                                           
         B     NEXTGOAL            44                                           
         B     FCGO                                                             
         EJECT                                                                  
*                                                                               
NEXTREQ  DS    0H                                                               
         CLI   RCMULTIQ,C'Y'       PROCESSING MULTI-MEDIA REQ?                  
         BNE   NEXTRQ10                                                         
NEXTRQ1  LA    RF,MEDLIST                                                       
*                                                                               
NEXTRQ2  CLC   QMED,0(RF)                                                       
         BE    NEXTRQ4                                                          
         LA    RF,1(RF)                                                         
         B     NEXTRQ2                                                          
NEXTRQ4  DS    0H                                                               
         CLI   1(RF),X'FF'        NEXT IS LAST MEDIA                            
         BE    NEXTRQ6            MEANS I'M DONE                                
*                                                                               
*                                 SEE IF MEDIA APPLIES TO THIS AGENCY           
*                                                                               
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   QAREA(80),MCREQREC  RESTORE ORIGINAL REQUEST                     
*                                                                               
         DROP  R1                                                               
*                                                                               
         MVC   QMED,1(RF)         THEN ALTER MEDIA (DATES WERE LOST)            
         B     NEXTRQ10                                                         
*                                                                               
NEXTRQ6  DS    0H                 JUST GO PROCESS NEXT REQUEST                  
         MVI   RCMULTIQ,C'N'      MUST SET THIS OFF                             
*                                 SO THAT NREQ WILL READ THE NEXT REQ           
*                                                                               
NEXTRQ10 BRAS  RE,NREQ                                                          
         BNE   NEQXIT                                                           
         MVI   CLTSKIP,C'N'       RESET FLAG BYTE                               
         B     EQXIT                                                            
*                                                                               
         EJECT                                                                  
NEXTCLT  DS    0H                                                               
         CLC   =C'**',QAGY                                                      
         BE    EQXIT                                                            
*                                                                               
         L     R0,=V(VMDPRDRD)     POINT TO OLD MEDPRDRD                        
         CLI   RQPOLFLT,C'Y'       TEST TO USE NEW VERSION                      
         BNE   *+8                                                              
         L     R0,=V(VMDPRDRN)                                                  
         ST    R0,MEDPRDRD                                                      
*                                                                               
         CLI   QFILTER,C'F'        TEST FILTER ON FILM TYPE                     
         BNE   NCLT1A                                                           
         OC    CMLPTR,CMLPTR       IN CMML TYPE PROCESSING                      
         BZ    NCLT1A              NO                                           
         L     RE,CMLPTR           GET LIST POINTER                             
         LA    RE,4(RE)            POINT BEYOND CLASS                           
         OC    0(2,RE),0(RE)       TEST E-O-L                                   
         BZ    *+12                                                             
         LA    RE,2(RE)            NEXT SEQ NUMBER                              
         B     *-14                                                             
*                                                                               
         LA    RE,2(RE)            POINT TO START OF NEXT ENTRY                 
         CLI   0(RE),0             TEST NO MORE ENTRIES                         
         BE    NCLT1A              DONE                                         
         ST    RE,CMLPTR           SET NEW POINTER                              
         XC    KEY,KEY                                                          
         L     R6,ADCLT                                                         
         MVC   KEY(13),0(R6)       SET KEY                                      
         B     HAVCLT              AND PROCESS AS NEW CLT                       
*                                                                               
NCLT1A   CLI   QCLGID,C' '         TEST CLIENT GROUP REQ                        
         BNH   NCLT1B                                                           
         GOTO1 =A(NEXTCGR)                                                      
         BNZ   NEQXIT                                                           
         B     NCLT11                                                           
*                                                                               
NCLT1B   CLC   =C'ALL',QCLT                                                     
         BE    NCLT10                                                           
         CLI   QCLT,C'*'           TEST OFFICE REQUEST                          
         BE    NCLT10                                                           
         CLI   QCLT,C'$'           TEST OFFICE LIST                             
         BE    NCLT20                                                           
         OC    SVCLT,SVCLT         TEST FIRST TIME                              
         BNZ   NEQXIT                                                           
*                                                                               
         GOTO1 CLPACK,DMCB,QCLT,SVCLT                                           
*                                                                               
         CLI   0(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAGYMD                                                 
         MVC   KEY+2(2),SVCLT                                                   
*                                                                               
         GOTO1 READ                                                             
         CLI   MODE,DISKERR                                                     
         BNE   NCLT2                                                            
         GOTO1 AENDREQ                                                          
NCLT2    GOTO1 GETCLT                                                           
*                                                                               
         B     HAVCLT                                                           
         SPACE 1                                                                
* ALL CLIENT PROCESSING                                                         
*                                                                               
NCLT10   LH    RE,SVCLT                                                         
         LA    RE,1(RE)                                                         
         STH   RE,SVCLT                                                         
*                                                                               
NCLT11   XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAGYMD                                                 
         MVC   KEY+2(2),SVCLT                                                   
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(2),KEYSAVE      SAME A-M                                     
         BNE   NEQXIT                                                           
         MVC   SVCLT,KEY+2                                                      
*                                                                               
         GOTO1 GETCLT                                                           
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
*===>                                                                           
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         OC    MCRHACCS,MCRHACCS   TEST ANY LIMIT ACCESS                        
         BZ    NCLT12                                                           
         DROP  R1                                                               
*                                                                               
         CLI   QCLGID,C' '         TEST CLIENT GROUP REQ                        
         BNE   *+14                                                             
         CLC   =C'ALL',QCLT                                                     
         BNE   NCLT12                                                           
         BRAS  RE,CALLOFCR                                                      
         BE    NCLT12                                                           
         MVI   CLTSKIP,C'Y'        SET FLAG THAT CLIENT WAS EXCLUDED            
         B     NCLT1A                                                           
*                                                                               
NCLT12   CLI   QCLT,C'$'           TEST OFFICE LIST                             
         BE    HAVCLT                                                           
*                                                                               
         CLI   QCLT,C'*'           TEST OFFICE REQUEST (NEW STYLE)              
         BNE   NCLT16                                                           
*                                                                               
         CLI   QCLT+1,C'-'         TEST 'ALL BUT'                               
         BE    NCLT14                                                           
*                                                                               
         CLC   COFFICE,QCLT+1                                                   
         BE    HAVCLT              OK                                           
         BL    NCLT10                                                           
*                                                                               
         CLI   QCLT+2,C' '         TEST RANGE                                   
         BE    NCLT10              NO                                           
*                                                                               
         CLC   COFFICE,QCLT+2                                                   
         BH    NCLT10                                                           
         B     HAVCLT              OK                                           
         SPACE 1                                                                
NCLT14   DS    0H                  'ALL BUT'                                    
         CLC   COFFICE,QCLT+2                                                   
         BE    NCLT10                                                           
         B     HAVCLT                                                           
         SPACE 1                                                                
NCLT16   DS    0H                                                               
         CLI   QCLOFFC,C' '        TEST OFFICE FILTER (OLD STYLE)               
         BE    HAVCLT              NO                                           
* FILTER ON OFFICE                                                              
         CLC   QCLOFFC,COFFICE                                                  
         BNE   NCLT10                                                           
         B     HAVCLT                                                           
         EJECT                                                                  
*===========================================================                    
* OFFICE LIST PROCESSING *                                                      
*===========================================================                    
                                                                                
NCLT20   DS    0H                                                               
         MVI   SVNEWOFC,C'N'                                                    
         OC    SVCLT,SVCLT         TEST FIRST TIME                              
         BNZ   NCLT22                                                           
         GOTO1 =A(BLDOFC)                                                       
         L     RE,=V(OFCLIST)                                                   
         OC    0(4,RE),0(RE)       TEST FOR NO DATA                             
         BZ    NEQXIT                                                           
         MVI   SVNEWOFC,C'Y'       SET NEW OFFICE FLAG                          
         B     NCLT28                                                           
*                                                                               
NCLT22   L     RE,=V(OFCLIST)      FIND ENTRY IN PROCESS                        
*                                                                               
NCLT24   CLC   SVCLT,2(RE)         MATCH CLIENT CODE                            
         BE    NCLT26                                                           
         AHI   RE,4                                                             
         OC    0(2,RE),0(RE)                                                    
         BNZ   NCLT24                                                           
         DC    H'0'                                                             
NCLT26   CLC   0(2,RE),4(RE)       TEST NEXT IS SAME OFFICE                     
         BE    *+8                                                              
         MVI   SVNEWOFC,C'Y'                                                    
*                                                                               
         AHI   RE,4                                                             
         OC    0(2,RE),0(RE)                                                    
         BZ    NEQXIT                                                           
*                                                                               
NCLT28   MVC   SVCLT,2(RE)         SET NEXT CLIENT CODE                         
         B     NCLT11                                                           
         EJECT                                                                  
HAVCLT   MVC   SVCLT,KEY+2                                                      
         MVC   BCLT,SVCLT                                                       
         XC    SVPGRKEY(SVDATAX-SVPGRKEY),SVPGRKEY                              
         MVI   SVMKGPGA,0                                                       
         MVI   SVONEPRD,0                                                       
* GET THE EQUIVALENCY TABLE IF NEEDED                                           
         CLI   RQEQUIV,C'Y'                                                     
         BNE   HAVCLT2                                                          
*                                                                               
         MVC   DUB(3),QAGY         MOVE AGENCY/MEDIA                            
         MVC   DUB+3(2),BCLT                                                    
* READ EQTAB INTO BUYREC, THEN MOVE 120 BYTES ONLY                              
         GOTO1 EQVRD,DMCB,DUB,DPEQTAB,ADBUY,DATAMGR                             
         L     RE,ADBUY                                                         
         MVC   EQTAB,0(RE)                                                      
*                                                                               
HAVCLT2  DS    0H                                                               
         MVC   RQCRRNCY,QCRRNCY    SET CURRENCY OVERRIDE                        
         CLI   RQCRRNCY,C' '       TEST PRESENT                                 
         BH    HAVCLT2A            YES                                          
         MVI   RQCRRNCY,C'U'       ASSUME DEFAULT TO US                         
         CLI   COUNTRY,C'C'        TEST CANADIAN AGENCY                         
         BNE   *+8                                                              
         MVI   RQCRRNCY,C'C'       SET FOR CANADIAN CURRENCY                    
*                                                                               
HAVCLT2A DS    0H                                                               
         MVC   WORK(12),=CL12'S000'                                             
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),CLIENT                                                 
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         GOTO1 GETPROF,DMCB,WORK,SPOTPROF,DATAMGR                               
*                                                                               
         XC    SPDFPROF,SPDFPROF                                                
*                                                                               
HAVCLT4  L     RE,MEDBUFF                                                       
         USING MEDBLOCK,RE                                                      
         MVC   MEDDFORM,SPOTPROF+2                                              
         NI    MEDDFORM,X'0F'                                                   
         DROP  RE                                                               
         MVC   WORK+2(2),RCPROG                                                 
         GOTO1 (RF),(R1),,PROGPROF                                              
         CLC   =C'ALL',QPRD                                                     
         BE    HAVCLT10                                                         
         CLC   =C'NO ',QPRD                                                     
         BE    HAVCLT10                                                         
         TM    QPGR,X'40'          TEST SINGLE POL PRDGRP REQ                   
         BZ    *+12                YES - TREAT AS SINGLE POL REQ                
         CLI   QPRD,C'0'           NUMERIC IS PRDGRP                            
         BNL   HAVCLT10                                                         
         MVI   SVONEPRD,C'Y'       INDICATE SINGLE PRD REQUEST                  
         EJECT                                                                  
* READ PGRDEF AND MGRDEF IF REQUIRED                                            
*                                                                               
HAVCLT10 DS    0H                                                               
         CLI   QPGR,C'N'                                                        
         BNE   *+8                                                              
         MVI   QPGR,C' '                                                        
         CLI   QPGR,C' '                                                        
         BE    HAVCLT12                                                         
* READ PGRDEF                                                                   
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D01'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         MVC   KEY+3(2),SVCLT                                                   
         MVC   KEY+5(1),QPGR       PRDGRP ID                                    
         OI    KEY+5,X'40'         INSURE UPPER CASE                            
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                ***** TEST ONLY *****                        
         MVC   SVPGRKEY,KEY                                                     
         GOTO1 GETPRDGR                                                         
*                                                                               
HAVCLT12 CLI   QMGR,C'N'           THIS TEST IS FOR COMPATIBILITY               
         BNE   *+8                 WITH OLD PROGRAMS WHICH SUPPRESS             
         MVI   QMGR,C' '           MKTGRPS                                      
         CLI   QMGR,C' '                                                        
         BE    HAVCLT22                                                         
         CLI   QMGR,C'9'           FA-FF ARE MKTGRPS                            
         BH    *+12                                                             
         CLI   QMGR,C'0'           IF NUMERIC NO MKT GROUPS                     
         BNL   HAVCLT20                                                         
         EJECT                                                                  
* READ MGRDEF                                                                   
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D02'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         CLI   QMGR,C' '           01-3F ARE ALL CLIENT GRPS                    
         BL    HAVCLT14                                                         
         CLI   QMGR,C'F'                                                        
         BH    HAVCLT14                                                         
         MVC   KEY+3(2),SVCLT      A-F REQUIRE CLT                              
*                                                                               
HAVCLT14 MVC   KEY+8(1),QMGR       MKTGRP ID                                    
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                ***** TEST ONLY *****                        
         MVC   SVMGRKEY,KEY                                                     
         GOTO1 GETMKTGR                                                         
*                                                                               
         L     R6,ADMKTGRP                                                      
         USING MKGRECD,R6                                                       
         LA    R6,MKGEL                                                         
         USING MKGEL01,R6                                                       
         CLI   MKGPGA,0            TEST ASSGNS BY PRDGRP                        
         BZ    HAVCLT22                                                         
         CLI   QPGR,C' '                                                        
         BE    HAVCLT22                                                         
         MVC   SVMKGPGA,PGR1LEN                                                 
         B     HAVCLT22                                                         
*                                                                               
HAVCLT20 MVI   MGR1LEN,4           SET LENGTH IF RANKING                        
         MVI   MGR1,C'9'           OTHER MARKET FIELDS TO '9'                   
         MVI   MGR1NM,C'9'                                                      
         MVI   MGR1BK,C'9'                                                      
*                                  IF QMGR = ID POINTER SCHEME                  
*                                  THEN FORCE READ BY ID'S                      
HAVCLT22 CLI   FCRDBUYS,C'N'       NOT IF NOT READING BUYS                      
         BE    HAVCLT30                                                         
         MVI   FCRDBUYS,C'Y'       SET FOR NORMAL READ                          
*                                                                               
         CLC   AGY,=C'MC'        **TEMPORARILY NOT FOR MCCANN**                 
         BE    HAVCLT30                                                         
         CLI   QMGR,C'G'           AND ONLY IF ID POINTER SCHEME                
         BL    HAVCLT30            IS G                                         
         CLI   QMGR,C'K'           THRU K                                       
         BH    HAVCLT30                                                         
         L     RF,ADCLT                                                         
         LA    RF,CEXTRA+2-CLTHDR(RF)   ID SCHEME                               
         CLC   QMGR,0(RF)                                                       
         BNE   HAVCLT30                                                         
*                                                                               
         MVI   FCRDBUYS,C'I'                                                    
*                                                                               
         MVC   QMKT,=C'ALL  '      MARKET MUST BE 'ALL'                         
         CLI   QSTA,C'0'           SO MUST STATION,UNLESS                       
         BNL   *+10                IS ONE MGR REQUEST                           
         MVC   QSTA,=C'ALL  '                                                   
*                                                                               
HAVCLT30 L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         TM    COPT2,COP2DIY       TEST DIY TRADE CLIENT                        
         BO    HAVCLT32            YES                                          
         MVI   Q2TRADE,C' '        SUPPRESS COMBINE TRADE                       
         B     HAVCLT40                                                         
*                                                                               
HAVCLT32 CLI   Q2TRADE,C' '        COMBINE TRADE OPTION SET                     
         BNE   HAVCLT40            YES - USER WINS                              
         CLI   QPRD+2,C'#'         TEST TRADE PRODUCT REQUEST                   
         BE    HAVCLT40                                                         
         CLC   =C'ALL',QPRD        ALL PRODUCT REQUEST                          
         BE    HAVCLT40            NEVER COMBINES CASH/TRADE                    
         MVI   Q2TRADE,C'Y'        ELSE DEFAULT TO COMBINE                      
*                                                                               
HAVCLT40 L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         TM    CINDS1,CIN1OVP      MORE THAN 255 PRODUCTS ON THIS CLT           
         BNO   HAVCLT50                                                         
         DROP  R6                                                               
*                                                                               
         L     R0,VCLIST           CLEAR CLIST                                  
         LH    R1,=H'3004'                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R2,VCLIST           R2 = START OF CLIST                          
         LH    R1,=H'3000'                                                      
         AR    R1,R2                                                            
         LR    R3,R1               R3 = END OF CLIST                            
*                                                                               
         LA    R6,KEY                                                           
         USING PLSTPSSV,R6                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0DF1'     PASSIVES FOR ALL PRDS                        
         MVC   PLSTAM,SVAGYMD                                                   
         MVC   PLSTCLT,SVCLT                                                    
         GOTO1 HIGH                                                             
         B     HAVCLT44                                                         
*                                                                               
HAVCLT42 GOTO1 SEQ                                                              
*                                                                               
HAVCLT44 CLC   KEY(5),KEYSAVE      TEST SAME CLIENT                             
         BNE   HAVCLT50                                                         
*                                                                               
         MVC   0(3,R2),PLSTPRD                                                  
         MVC   3(1,R2),PLSTBPRD+1                                               
         LA    R2,4(R2)                                                         
         CR    R2,R3                                                            
         BNH   *+6                                                              
         DC    H'0'                                                             
         B     HAVCLT42                                                         
*                                                                               
HAVCLT50 B     EQXIT                                                            
*                                                                               
CLTSKIP  DC    C'N'                                                             
         DROP  R6                                                               
         EJECT                                                                  
NEXTPGR  CLI   QPGR,C' '           TEST PRDGRPS REQUESTED                       
         BE    NEQXIT              NO                                           
*                                                                               
         OC    SVPGRKEY+6(2),SVPGRKEY+6  TEST FIRST TIME  (GRP 0000)            
         BNZ   NPGR40              NO                                           
* BUILD PRDGRP ASSIGNMENT TABLE                                                 
         L     R0,PRDLIST          CLEAR PRDLIST                                
         LH    R1,=H'4500'                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
**>      LA    RE,CLIST                                                         
         L     RE,VCLIST           COMBINED CLIST+CLIST2                        
         L     RF,PRDLIST                                                       
         CLI   0(RE),0             TEST NO PRODUCTS                             
         BE    NEQXIT                                                           
*                                                                               
NPGR4    CLI   0(RE),0             TEST E-O-L                                   
         BE    NPGR6                                                            
         CLI   3(RE),X'FF'         SKIP POL                                     
         BE    NPGR5                                                            
         CLI   RQPRDAAA,C'Y'                                                    
         BE    *+14                                                             
         CLC   0(3,RE),=C'AAA'     SKIP AAA                                     
         BE    NPGR5                                                            
         MVC   0(2,RF),=X'9999'    SET DEFAULT GRP                              
         MVC   2(4,RF),0(RE)       MOVE PRD MNEMONIC AND CODE                   
         LA    RF,6(RF)                                                         
NPGR5    LA    RE,4(RE)                                                         
         B     NPGR4                                                            
*                                                                               
* NOW READ PRDGRP POINTERS (PASSIVES)                                           
*                                                                               
NPGR6    C     RF,PRDLIST          TEST NO PRDS                                 
         BE    NEQXIT                                                           
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(13),SVPGRKEY                                                 
         MVI   KEY+1,X'81'                                                      
*                                                                               
         GOTO1 HIGH                                                             
         B     NPGR9                                                            
*                                                                               
NPGR8    GOTO1 SEQ                                                              
*                                                                               
NPGR9    CLC   KEY(6),KEYSAVE      TEST SAME SCHEME                             
         BNE   NPGR20                                                           
* FIND PRD IN TABLE                                                             
         L     RE,PRDLIST                                                       
NPGR10   CLC   2(3,RE),KEY+8                                                    
         BE    NPGR12                                                           
         LA    RE,6(RE)                                                         
         CLI   2(RE),0                                                          
         BNE   NPGR10                                                           
         B     NPGR8                                                            
NPGR12   MVC   0(2,RE),KEY+6       MOVE PRDGRP                                  
         B     NPGR8                                                            
         SPACE 2                                                                
NPGR20   DS    0H                                                               
         CLC   =C'ALL',QPRD                                                     
         BE    NPGR30                                                           
         CLC   =C'NO ',QPRD                                                     
         BE    NPGR30                                                           
         CLC   =C'POL',QPRD                                                     
         BE    NPGR30                                                           
*                                                                               
         CLI   QPRD,C'0'           TEST NUMERIC PRO                             
         BL    NPGR27              NO                                           
* FIND NUMBER OF DIGITS FOR COMPARE                                             
         LA    R1,QPRD+2                                                        
         LA    R2,3                                                             
         CLI   0(R1),C' '                                                       
         BNE   *+12                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10                                                          
         DC    H'0'                                                             
*                                                                               
         CLI   0(R1),C'*'                                                       
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10             ON EXIT R2 HAS COMPARE LEN                   
*                                                                               
         BCTR  R2,0                SET FOR EX                                   
         L     RE,PRDLIST                                                       
*                                                                               
NPGR24   OC    0(2,RE),0(RE)                                                    
         BE    NPGR24X                                                          
         UNPK  DUB,0(3,RE)                                                      
         EX    R2,*+8                                                           
         B     *+10                                                             
         CLC   DUB+3(0),QPRD                                                    
         BE    *+10                                                             
         MVC   0(2,RE),=X'9999'                                                 
         LA    RE,6(RE)                                                         
         B     NPGR24                                                           
*                                                                               
NPGR24X  TM    QPGR,X'40'          TEST UPPER CASE                              
         BO    *+10                                                             
         MVC   QPRD,=C'POL'        LOWER CASE IS FOR POL REQUEST                
         B     NPGR30                                                           
         EJECT                                                                  
* ONE PRODUCT REQUESTED                                                         
*                                                                               
NPGR27   L     RE,PRDLIST                                                       
*                                                                               
NPGR29   OC    0(2,RE),0(RE)                                                    
         BE    NPGR30                                                           
         CLC   2(3,RE),QPRD                                                     
         BE    *+10                                                             
         MVC   0(2,RE),=X'9999'                                                 
         LA    RE,6(RE)                                                         
         B     NPGR29                                                           
         SPACE 2                                                                
NPGR30   SR    R0,R0               COUNT PRDLIST ENTRIES                        
         L     RE,PRDLIST                                                       
*                                                                               
         CLI   2(RE),0                                                          
         BE    *+12                                                             
         LA    RE,6(RE)                                                         
         BCT   R0,*-12                                                          
*                                                                               
         LPR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 XSORT,DMCB,PRDLIST,(R0),6,6,0                                    
*                                                                               
         L     RE,PRDLIST                                                       
         B     NPGR44                                                           
         EJECT                                                                  
* FIND NEXT PRDGRP                                                              
*                                                                               
NPGR40   L     RE,SVPRDADR         GET LAST PRD PROCESSED                       
         LA    RE,6(RE)            BUMP TO NEXT                                 
         OC    0(2,RE),0(RE)       TEST EOL                                     
         BE    NEQXIT                                                           
         TM    QPGR,X'40'          TEST SINGLE POL PRDGRP REQ                   
         BZ    NEQXIT              YES - EXIT                                   
         CLC   =C'POL',QPRD                                                     
         BE    NPOLPGR                                                          
*                                                                               
NPGR44   ST    RE,SVPRDADR                                                      
         C     RE,PRDLIST          TEST FIRST TIME (START OF LIST)              
         BE    *+16                                                             
         UNPK  DUB,SVPGRKEY+6(3)                                                
         MVC   SVOLDPGR,DUB+3      ELSE SAVE LAST PRDGRP                        
         CLC   =X'9999',0(RE)      TEST FOR UNASSIGNED PRDGRP                   
         BE    NPGR50              BUILD DUMMY PRDGRP                           
         MVC   SVPGRKEY+6(2),0(RE)                                              
         MVC   KEY(13),SVPGRKEY                                                 
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 GETPRDGR                                                         
*                                                                               
HAVPGR   MVC   BPGR,SVPGRKEY+5     MOVE GRPID AND NUMBER                        
         XC    SVPRD(SVDATAX-SVPRD),SVPRD                                       
         B     EQXIT                                                            
         EJECT                                                                  
* BUILD DUMMY PRDGRP                                                            
*                                                                               
NPGR50   CLI   QPRD,C'0'           TEST ONE PRDGRP REQUEST                      
         BNL   NEQXIT              YES-DONE                                     
         CLC   =C'POL',QPRD                                                     
         BE    NPGR52                                                           
         CLI   SVONEPRD,0          TEST MULTI-PRD REQUEST                       
         BNE   NEQXIT              NO                                           
*                                                                               
NPGR52   MVC   PGR1+1(4),=C'999 '                                               
         MVC   PGR1NM,=CL24'*** UNDEFINED ***'                                  
         CLI   QLANG,C'F'                                                       
         BNE   *+10                                                             
         MVC   PGR1NM,=CL24'*** INCONNU ***'                                    
         MVC   PGR2+1(4),=C'999 '                                               
         MVC   PGR2NM,PGR1NM                                                    
         MVC   PGR3+1(4),=C'999 '                                               
         MVC   PGR3NM,PGR1NM                                                    
*                                                                               
         MVC   SVPGRKEY+6(2),=X'9999'                                           
         B     HAVPGR                                                           
*                                                                               
NPOLPGR  L     RE,SVPRDADR                                                      
*                                                                               
NPOLPGR2 OC    6(2,RE),6(RE)       TEST NO MORE ENTRIES                         
         BZ    NEQXIT                                                           
         CLC   0(2,RE),6(RE)       TEST NEXT PRD IS SAME PRDGRP                 
         LA    RE,6(RE)            BUMP TO NEXT                                 
         BE    NPOLPGR2            IF EQUAL, TRY AGAIN                          
         B     NPGR44              ELSE PROCESS                                 
         EJECT                                                                  
NEXTPRD  DS    0H                                                               
         CLI   SVONEPRD,0          TEST MULTI-PRD REQUEST                       
         BE    NPRD2               YES                                          
* SINGLE PRD                                                                    
         OC    SVPRD,SVPRD         TEST FIRST TIME                              
         BNZ   NEQXIT              NO-DONE                                      
*                                                                               
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVC   KEY(13),CKEY                                                     
         MVC   KEY+4(3),QPRD                                                    
         OC    KEY+4(3),=C'   '    YOU FIND THE U9 PROBLEM !                    
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    NPRD1                                                            
         CLC   =C'ALL',QCLT                                                     
         BE    NEQXIT                                                           
         CLI   QCLT,C'*'           TEST OFFICE REQUEST                          
         BE    NEQXIT                                                           
         CLI   QCLT,C'$'           TEST OFFICE LIST                             
         BE    NEQXIT                                                           
         GOTO1 AENDREQ                                                          
*                                                                               
NPRD1    GOTO1 GETPRD                                                           
*                                                                               
         B     HAVPRD                                                           
         SPACE 2                                                                
NPRD2    CLI   RQRDPOL,C'Y'        TEST READ POL IF CLT IS ALL POL              
         BE    NPRD20                                                           
*                                                                               
NPRD3    CLI   QPGR,C' '           TEST PRDGRPS                                 
         BNE   NPRD10                                                           
         EJECT                                                                  
* ALL PRDS - NO PRDGRPS                                                         
*                                                                               
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
**>      LA    RE,CLIST                                                         
         L     RE,VCLIST           COMBINED CLIST+CLIST2                        
*                                                                               
         OC    SVPRDADR,SVPRDADR   TEST FIRST TIME                              
         BZ    NPRD6               YES                                          
*                                                                               
NPRD4    L     RE,SVPRDADR                                                      
         LA    RE,4(RE)                                                         
         CLI   0(RE),0             TEST DONE                                    
         BE    NEQXIT                                                           
*                                                                               
NPRD6    ST    RE,SVPRDADR                                                      
         CLI   RQPRDAAA,C'Y'                                                    
         BE    *+14                                                             
         CLC   0(3,RE),=C'AAA'     SKIP AAA                                     
         BE    NPRD4                                                            
         CLI   3(RE),X'FF'         TEST POL                                     
         BNE   NPRD8                                                            
         CLI   RQALLPOL,C'Y'       TEST INCLUDE POL                             
         BNE   NPRD4                                                            
*                                                                               
NPRD8    L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVC   KEY(13),CKEY        A-M/CLT                                      
         MVC   KEY+4(3),0(RE)      PRD                                          
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   NPRD4                                                            
*                                                                               
         GOTO1 GETPRD                                                           
*                                                                               
         B     HAVPRD                                                           
         EJECT                                                                  
* ALL PRDS - PRDGRPS                                                            
*                                                                               
NPRD10   L     RE,SVPRDADR                                                      
         OC    SVPRD,SVPRD         TEST FIRST TIME THIS PRDGRP                  
         BZ    NPRD14              YES                                          
NPRD12   L     RE,SVPRDADR         GET LAST PRD PROCESSED                       
*                                                                               
         CLC   0(2,RE),6(RE)       TEST NEXT PRD IN SAME PRDGRP                 
         BNE   NEQXIT              NO                                           
         LA    RE,6(RE)            POINT TO NEXT PRD                            
*                                                                               
NPRD14   ST    RE,SVPRDADR                                                      
         UNPK  DUB,6(3,RE)         UNPACK NEXT PRDGRP                           
         MVC   SVNXTPGR,DUB+3                                                   
         OC    6(2,RE),6(RE)       TEST E-O-L                                   
         BNZ   *+10                NO                                           
         XC    SVNXTPGR,SVNXTPGR                                                
*                                                                               
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVC   KEY(13),CKEY        A-M/CLT                                      
         MVC   KEY+4(3),2(RE)      PRD                                          
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(13),KEYSAVE     SKIP PRD IF NOT FOUND                        
         BNE   NPRD12                                                           
*                                                                               
         GOTO1 GETPRD                                                           
*                                                                               
         B     HAVPRD                                                           
         EJECT                                                                  
* THESE ROUTINES DETERMINE IF ANY NON-POL BRAND ESTIMATES ARE OPEN.             
* IF NOT, ONLY POL DATA WILL BE READ FOR AN 'ALL' PRD REQUEST.                  
*                                                                               
NPRD20   DS    0H                                                               
* READ POL ESTIMATES                                                            
         XC    ESTLST,ESTLST                                                    
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVC   KEY(13),CKEY                                                     
         MVC   KEY+4(3),=C'POL'                                                 
         MVI   KEY+7,1                                                          
*                                                                               
         MVI   BEST,1                                                           
         MVI   BESTEND,255         SIMULATE SERIES REQUEST                      
         CLC   =C'ALL',QEST                                                     
         BE    NPRD22                                                           
         CLC   =C'NO ',QEST                                                     
         BE    NPRD22                                                           
         CLC   QEST,SPACES                                                      
         BE    NPRD22                                                           
*                                                                               
         PACK  DUB,QEST                                                         
         CVB   R0,DUB                                                           
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         STC   R0,BEST                                                          
         STC   R0,BESTEND                                                       
         STC   R0,KEY+7                                                         
         CLC   QESTEND,SPACES                                                   
         BE    NPRD22                                                           
         PACK  DUB,QESTEND                                                      
         CVB   R0,DUB                                                           
         STC   R0,BESTEND                                                       
         CLC   BEST,BESTEND                                                     
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
NPRD22   GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   NPRD24                                                           
         OC    KEY+8(5),KEY+8      TEST FOR BILL                                
         BNZ   NPRD23                                                           
         CLC   KEY+7(1),BESTEND    TEST REACHED HI LIMIT                        
         BH    NPRD24                                                           
         EJECT                                                                  
* POST EST TO LIST                                                              
         SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         MVI   0(RE),C'P'                                                       
NPRD23   DS    0H                                                               
*                                                                               
         CLC   KEY+7(1),BESTEND    TEST REACHED HI LIMIT                        
         BNL   NPRD24              IF HI OR EQ, DONE                            
         MVC   KEY+8(5),=5X'FF'    ELSE NEXT EST                                
         B     NPRD22                                                           
*                                                                               
NPRD24   OC    ESTLST,ESTLST       TEST ANY POL ESTS FOUND                      
         BNZ   NPRD30                                                           
* POL CANNOT BE USED                                                            
NPRD26   MVI   RQRDPOL,C'N'                                                     
         B     NPRD3                                                            
         EJECT                                                                  
* NOW READ BRAND ESTIMATES                                                      
*                                                                               
NPRD30   L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
***>     LA    R4,CLIST-4                                                       
         L     R4,VCLIST           COMBINED CLIST+CLIST2                        
         AHI   R4,-4                                                            
         B     NPRD36                                                           
*                                                                               
NPRD32   GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE      SAME A-M/CLT/PRD                             
         BNE   NPRD36                                                           
         OC    KEY+8(5),KEY+8      TEST FOR BILL                                
         BNZ   NPRD34              YES - NEXT EST                               
* WE HAVE AN ESTIMATE                                                           
         CLC   KEY+7(1),BESTEND    TEST REACHED END OF SERIES                   
         BH    NPRD36                                                           
* TEST POL EST OPEN                                                             
         SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0             TEST POL OPEN                                
         BE    NPRD26              TOO BAD                                      
NPRD34   CLC   KEY+7(1),BESTEND    TEST REACHED HI LIMIT                        
         BNL   NPRD36              IF HI OR EQ, DONE                            
         MVC   KEY+8(5),=5X'FF'    ELSE NEXT EST                                
         B     NPRD32                                                           
* NEXT PRD                                                                      
NPRD36   LA    R4,4(R4)            NEXT CLIST ENTRY                             
         CLI   3(R4),X'FF'         TEST POL                                     
         BE    NPRD38              YES-DONE                                     
         CLI   0(R4),0                                                          
         BE    NPRD38                                                           
*                                                                               
         MVC   KEY(13),CKEY                                                     
         MVC   KEY+4(3),0(R4)                                                   
         MVC   KEY+7(1),BEST                                                    
         B     NPRD32                                                           
* ALL BRANDS CHECKED                                                            
NPRD38   MVC   KEY(13),CKEY                                                     
         MVC   KEY+4(3),=C'POL'                                                 
         MVI   SVONEPRD,C'Y'                                                    
         GOTO1 READ                                                             
         GOTO1 GETPRD                                                           
         B     HAVPRD                                                           
         SPACE 2                                                                
HAVPRD   L     R6,ADPRD                                                         
         USING PRDHDR,R6                                                        
         MVC   SVPRD,PKEY+4                                                     
         MVC   SVPRDCD,PCODE+1                                                  
         MVC   BPRD,PCODE+1                                                     
         XC    SVEST(SVDATAX-SVEST),SVEST                                       
         XC    ESTLST,ESTLST                                                    
         B     EQXIT                                                            
         EJECT                                                                  
NEXTEST  DS    0H                                                               
         CLC   =C'ALL',QEST                                                     
         BE    NEST30                                                           
* SINGLE OR GROUP REQUEST                                                       
         CLI   SVEST,0             TEST FIRST TIME                              
         BNE   NEQXIT              NO                                           
         MVI   BEST,0                                                           
         MVI   BESTEND,0                                                        
*                                                                               
         CLC   =C'NO ',QEST        TEST 'NO'                                    
         BE    NEST10              GO BUILD LIST                                
         CLC   QEST,SPACES                                                      
         BE    NEST10                                                           
* SINGLE OR SERIES                                                              
         PACK  DUB,QEST                                                         
         CVB   R0,DUB                                                           
         STC   R0,BEST                                                          
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   QESTEND(3),SPACES                                                
         BE    NEST2                                                            
         PACK  DUB,QESTEND                                                      
         CVB   R0,DUB                                                           
         STC   R0,BESTEND                                                       
         CLC   BEST,BESTEND                                                     
         BNH   *+6                                                              
         DC    H'0'                                                             
NEST2    L     R6,ADPRD                                                         
         USING PRDHDR,R6                                                        
         MVC   KEY(13),PKEY                                                     
         MVC   KEY+7(1),BEST                                                    
*                                                                               
NEST3    GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   NEQXIT                                                           
         CLC   KEY+7(1),BEST                                                    
         BE    NEST4                                                            
         CLC   KEY+7(1),BESTEND    TEST REACHED HI LIMIT                        
         BH    NEQXIT                                                           
*                                                                               
NEST4    OC    KEY+8(5),KEY+8      TEST BILL                                    
         BZ    NEST6                                                            
         MVC   KEY+8(5),=5X'FF'                                                 
         B     NEST3                                                            
NEST6    DS    0H                                                               
         GOTO1 GETEST                                                           
*==============================================================                 
* TEST FOR REG/STW/BAR ESTIMATE FILTERING  (LCI)                                
*==============================================================                 
         L     R6,ADEST                                                         
         USING ESTHDR,R6                                                        
         CLC   QPROG,=C'I2'        I2 REPORT?                                   
         BNE   HAVEST              NO - THIS IS ONLY FOR I2 DSSUP-2974          
         CLI   QETYPE,C' '         ANY REG FILTER SPECIFIED                     
         BNE   NEST7               YES                                          
         CLI   ETYPE,0             IS THIS A REGULAR EST                        
         BE    HAVEST              YES                                          
         CLI   ETYPE,C'R'          IS THIS A REGULAR EST                        
         BE    HAVEST              YES                                          
         CLI   ETYPE,C'U'          IS THIS AN UNWIRED EST                       
         BE    HAVEST              YES - TREAT AS REGULAR                       
         CLI   PROFSTW,C'Y'        PROCESS STW ESTIMATES ON I2?                 
         BNE   *+12                NO                                           
         CLI   ETYPE,C'S'          STEWARDSHIP ESTIMATE?                        
         BE    HAVEST              YES - PROCESS                                
         B     NEQXIT              NO - SKIP                                    
*                                                                               
NEST7    CLC   QETYPE,ETYPE        IF SPECIFIED, MUST MATCH                     
         BE    HAVEST                                                           
         CLI   QETYPE,C'*'         UNLESS 'ALL' TYPES REQUESTED                 
         BE    HAVEST                                                           
         B     NEQXIT                                                           
         DROP  R6                                                               
*                                                                               
         EJECT                                                                  
* 'NO' ESTIMATE REQUEST - BUILD LIST OF ESTS IN PERIOD                          
*                                                                               
NEST10   DS    0H                                                               
         XC    ESTLST,ESTLST                                                    
         L     R6,ADPRD                                                         
         USING PRDHDR,R6                                                        
         MVC   KEY(13),PKEY                                                     
         MVI   KEY+7,1                                                          
*                                                                               
         CLI   RQESTFLT,C'Y'       TEST LIST REQUIRED                           
         BE    NEST12              YES                                          
         CLC   QESTEND,SPACES      LIST ALWAYS REQUIRED IF FILTERING            
         BNE   NEST12                                                           
* FLAG ALL ESTIMATES ACTIVE                                                     
         LA    R0,255              SET EST NUMBERS IN SLOTS                     
         LA    RE,ESTLST+255                                                    
         STC   R0,0(RE)                                                         
         BCTR  RE,0                                                             
         BCT   R0,*-6                                                           
* FIND ONE                                                                      
NEST11   GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   NEQXIT                                                           
         OC    KEY+8(5),KEY+8                                                   
         BZ    NEST4                                                            
         MVC   KEY+8(5),=5X'FF'                                                 
         B     NEST11                                                           
*                                                                               
NEST12   MVC   WORK(12),QSTART     MOVE DATES                                   
         CLC   QSTART+4(2),SPACES  CHECK FOUR BYTES OF DATE PRESENT             
         BNE   NEST12B                                                          
         CLI   RQSTDTLN,6          ** WHEN 6 ARE REQUIRED                       
         BNE   NEST12B                                                          
         MVC   QEND,QSTART         TREAT QSTART AS MONTH OF SERVICE             
         B     NEST13                                                           
*                                                                               
NEST12B  CLI   RQSTDTLN,6          TEST YYMMDD REQUIRED                         
         BE    NEST20              YES                                          
         CLI   RQNDDTLN,0          TEST END DATE REQUIRED                       
         BE    NEST14              NO                                           
*                                                                               
* HAVE YYMM FOR START AND END DATES                                             
* FIRST GET START MONTH START DATE                                              
*                                                                               
NEST13   MVC   WORK,SPACES                                                      
         MVC   WORK(4),QSTART                                                   
         MVC   WORK+6(4),WORK                                                   
         LA    R0,1                BROADCAST MONTH START                        
         CLI   RQBRDMON,C'Y'                                                    
         BE    *+8                                                              
         LA    R0,3                CALENDAR MONTH START                         
         GOTO1 MOBILE,DMCB,(1,WORK),((R0),WORK+12)                              
* GET END MONTH END DATE                                                        
         MVC   WORK(12),SPACES                                                  
         MVC   WORK(4),QEND                                                     
         MVC   WORK+6(4),WORK                                                   
         GOTO1 (RF),(R1),,((R0),WORK+24)                                        
* GET 6 BYTE START/END IN WORK/WORK+6                                           
         GOTO1 DATCON,DMCB,(2,WORK+12),WORK                                     
*                                                                               
         GOTO1 (RF),(R1),(2,WORK+26),WORK+6                                     
         B     NEST20                                                           
*                                                                               
* HAVE YYMM FOR START DATE, NO END DATE.                                        
* TREAT IT AS A MONTH OF SERVICE END DATE.                                      
*                                                                               
NEST14   MVC   WORK,SPACES                                                      
         MVC   WORK(4),QSTART                                                   
         MVC   WORK+6(4),WORK                                                   
         LA    R0,1                                                             
         CLI   RQBRDMON,C'Y'                                                    
         BE    *+8                                                              
         LA    R0,3                                                             
         GOTO1 MOBILE,DMCB,(1,WORK),((R0),WORK+24)                              
*                                                                               
         GOTO1 DATCON,(R1),(2,WORK+24),WORK+6                                   
         MVC   WORK(6),=6C'0'                                                   
         B     NEST20                                                           
         EJECT                                                                  
* FILTER ESTIMATES ON DATES IN WORK                                             
*                                                                               
NEST20   MVC   SVSTART(12),WORK                SAVE DATES                       
         CLC   =C'ALL',QEST                                                     
         BE    NEST31                                                           
*                                                                               
NEST21   GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE                                                   
         BNE   NEST24                                                           
         OC    KEY+8(5),KEY+8      TEST FOR BILL DATA                           
         BNZ   NEST22                                                           
*                                                                               
         MVC   AREC,ADEST                                                       
         GOTO1 GET                                                              
*                                                                               
         L     R6,AREC                                                          
         USING ESTHDR,R6                                                        
* MAKE ESTIMATE DATES Y2K COMPATIBLE                                            
         GOTO1 DATCON,DMCB,ESTART,ESTART                                        
         GOTO1 (RF),(R1),EEND,EEND                                              
*                                                                               
         CLC   SVSTART,EEND        REQ START AFTER EST END                      
         BH    NEST22                                                           
         CLC   SVEND,ESTART        REQ END BEFORE EST START                     
         BL    NEST22                                                           
*                                                                               
         BAS   RE,FLTREST                                                       
         BNE   NEST22                                                           
         CLI   RQESTTST,C'Y'       DOES USER WANT A LOOK?                       
         BNE   NEST21D                                                          
         MVI   MODE,ESTTEST                                                     
         GOTO1 GO                                                               
         CLI   MODE,ESTTEST        IF USER CHANGES MODE (TO ESTLAST?)           
         BNE   NEST22              REJECT ESTIMATE                              
*                                                                               
* INCLUDE THIS EST                                                              
NEST21D  DS    0H                                                               
         SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         MVC   0(1,RE),KEY+7       SAVE EST NO                                  
*                                                                               
NEST22   MVC   KEY+8(5),=5X'FF'                                                 
         B     NEST21                                                           
* END OF PRD                                                                    
NEST24   OC    ESTLST,ESTLST       ANY ESTIMATES FOUND                          
         BZ    NEQXIT                                                           
* REREAD LOWEST EST                                                             
         LA    R1,ESTLST                                                        
         CLI   0(R1),0                                                          
         BNE   *+12                                                             
         LA    R1,1(R1)                                                         
         B     *-12                                                             
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(7),KEYSAVE      RESTORE A-M/CLT/PRD                          
         MVC   KEY+7(1),0(R1)      MOVE EST NUM                                 
*                                                                               
         GOTO1 READ                                                             
         GOTO1 GETEST                                                           
*                                                                               
         B     HAVEST                                                           
         EJECT                                                                  
* 'ALL' ESTIMATES                                                               
*                                                                               
NEST30   DS    0H                                                               
         CLI   SVEST,0             TEST FIRST TIME                              
         BNE   NEST31              NO                                           
         CLC   =C'ES',QSTAUTO                                                   
         BNE   NEST12              SET ESTDATES FOR DATE CHECKING               
NEST31   DS    0H                                                               
         MVI   BEST,0                                                           
         MVI   BESTEND,0                                                        
         L     R6,ADPRD                                                         
         USING PRDHDR,R6                                                        
         MVC   KEY(13),PKEY                                                     
         DROP  R6                                                               
         MVC   KEY+7(1),SVEST                                                   
NEST32   MVC   KEY+8(5),=5X'FF'                                                 
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   NEQXIT                                                           
         OC    KEY+8(5),KEY+8                                                   
         BNZ   NEST32                                                           
         MVC   BEST,KEY+7          SET ACTUAL EST                               
*                                                                               
         GOTO1 GETEST                                                           
*                                                                               
         BAS   RE,FLTREST                                                       
         BNE   NEST32                                                           
*                                                                               
         CLC   =C'ES',QSTAUTO                                                   
         BE    HAVEST                                                           
* FILTER ON DATES IF SPECIFIED                                                  
         L     R6,ADEST                                                         
         USING ESTHDR,R6                                                        
* MAKE ESTIMATE DATES Y2K COMPATIBLE                                            
         GOTO1 DATCON,DMCB,ESTART,ESTART                                        
         GOTO1 (RF),(R1),EEND,EEND                                              
*                                                                               
         CLC   SVSTART,EEND          REQ START AFTER EST END                    
         BH    NEST32                                                           
         CLC   SVEND,ESTART        REQ END BEFORE EST START                     
         BL    NEST32                                                           
         B     HAVEST                                                           
         EJECT                                                                  
*                                                                               
FLTREST  NTR1                                                                   
*                                                                               
         L     R6,ADEST                                                         
         USING ESTHDR,R6                                                        
*                                                                               
         CLC   QESTEND,SPACES                                                   
         BE    FLTRES10                                                         
*                                                                               
         LA    R0,3                                                             
         LA    RE,QESTEND                                                       
         LA    RF,EPROF                                                         
*                                                                               
FLTRES2  CLI   0(RE),C'*'                                                       
         BE    FLTRES6                                                          
         CLI   0(RE),C' '                                                       
         BE    FLTRES6                                                          
         TM    0(RE),X'40'         TEST NEGATIVE FILTER                         
         BZ    FLTRES4             YES                                          
*                                                                               
         CLC   0(1,RE),0(RF)       POSITIVE FILTER MUST MATCH                   
         BNE   NEQXIT                                                           
         B     FLTRES6                                                          
*                                                                               
FLTRES4  MVC   BYTE,0(RE)                                                       
         OI    BYTE,C' '           MAKE CHAR UPPER CASE FOR COMPARE             
         CLC   BYTE,0(RF)                                                       
         BE    NEQXIT                                                           
*                                                                               
FLTRES6  LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,FLTRES2                                                       
*                                                                               
         B     EQXIT                                                            
         SPACE 1                                                                
*==============================================================                 
* TEST FOR REG/STW/BAR ESTIMATE FILTERING  (LCI)                                
*==============================================================                 
         SPACE 1                                                                
FLTRES10 CLI   QETYPE,C' '         ANY REG FILTER SPECIFIED                     
         BNE   FLTRES20            YES                                          
         CLI   ETYPE,0             IS THIS A REGULAR EST                        
         BE    EQXIT               YES                                          
         CLI   ETYPE,C'R'          IS THIS A REGULAR EST                        
         BE    EQXIT               YES                                          
         CLI   ETYPE,C'U'          IS THIS AN UNWIRED EST                       
         BE    EQXIT               YES - TREAT AS REGULAR                       
         CLC   QPROG,=C'I2'        I2 REPORT?                                   
         BNE   FLTRES15            NO                                           
         CLI   PROFSTW,C'Y'        PROCESS STW ESTIMATES ON I2?                 
         BNE   *+12                NO                                           
         CLI   ETYPE,C'S'          STEWARDSHIP ESTIMATE?                        
         BE    EQXIT               YES - PROCESS                                
FLTRES15 B     NEQXIT              NO - SKIP                                    
*                                                                               
FLTRES20 CLC   QETYPE,ETYPE        IF SPECIFIED, MUST MATCH                     
         BE    EQXIT                                                            
         CLI   QETYPE,C'*'         UNLESS 'ALL' TYPES REQUESTED                 
         BE    EQXIT                                                            
         B     NEQXIT                                                           
         EJECT                                                                  
HAVEST   MVC   SVEST,KEY+7         SAVE EST NUMBER                              
*                                                                               
         L     R6,ADEST                                                         
         USING ESTHDR,R6                                                        
         CLC   =C'ES',QSTAUTO                                                   
         BNE   *+10                                                             
         MVC   QSTART(12),ESTART                                                
*                                                                               
         OC    RQBILLDT,RQBILLDT   TEST FOR BILLING DATE FILTER                 
         BZ    HAVEST1             NO                                           
         GOTO1 =A(ESTBILL)                                                      
         BNE   NEXTEST             ESTIMATE NOT BILLED ON FILTER DATE           
*                                                                               
HAVEST1  XC    SVMGRKEY(SVDATAX-SVMGRKEY),SVMGRKEY                              
* READ THE DAYPART MENU IF NEEDED                                               
         CLI   RQDAYPT,C'Y'                                                     
         BNE   HAVEST2                                                          
*                                                                               
         XC    DMCB(12),DMCB                                                    
         MVC   DMCB(3),QAGY        A-M                                          
         MVC   DMCB+3(1),EDAYMENU                                               
         CLI   RQDPOVRD,C' '                                                    
         BE    *+10                                                             
         MVC   DMCB+3(1),RQDPOVRD                                               
*                     GET NORMAL AND PLANNING DAYPARTS                          
         GOTO1 DPTRD,DMCB,,(C'P',ADDPTTAB)                                      
*                                                                               
HAVEST2  DS    0H                                                               
         CLI   QFILTER,C'F'        TEST FILM TYPE FILTER                        
         BNE   HAVEST2X                                                         
         OC    CMLPTR,CMLPTR       TEST FIRST PASS THIS REQ                     
         BNZ   HAVEST2X                                                         
         GOTO1 =A(BLDFLMS)         GO BUILD FILM LIST                           
         OC    CMLPTR,CMLPTR       TEST ANY FILMS FOUND                         
         BZ    NEXTEST                                                          
*                                                                               
HAVEST2X CLI   QPWCV,C'Y'           TEST PW CLIENT VERSION                      
         BNE   HAVEST2Y                                                         
         TM    RCOPTS,RCOPT_PWADJTST TEST SUPPRESS IF ADJ BILLING               
         BZ    HAVEST2Y             NO                                          
         GOTO1 =A(PWTEST)                                                       
*                                                                               
HAVEST2Y CLI   RQNOAML,C'Y'        TEST SUBCON BUILDING AML                     
         BE    EQXIT                                                            
         EJECT                                                                  
* CLEAR 6000 BYTES IN BUYIDLST AREA                                             
         L     R0,BUYLIST                                                       
         LHI   R1,6000                                                          
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         XC    BUYID,BUYID                                                      
*                                                                               
         L     R0,MKTLIST                                                       
         L     R1,MKTLISTX                                                      
         SR    R1,R0                                                            
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
* BUILD ACTIVE MARKET LIST                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(1),SVAGYMD                                                   
         CLI   QORIGBUY,C'Y'                                                    
         BNE   *+8                                                              
         OI    KEY,X'08'                                                        
         MVC   KEY+1(2),SVCLT                                                   
         MVC   KEY+3(1),SVPRDCD                                                 
*                                                                               
         TM    RCOPTS,RCOPT_INCLMKT0                                            
         BO    HAVEST2Z                                                         
         CLC   =C'0000',QMKT       UNLESS MKT 0000 REQ                          
         BE    *+8                                                              
         MVI   KEY+5,1             SET LOW MKT NUM                              
*                                                                               
HAVEST2Z L     R7,BUYLIST          POINT R7 TO BUYID AREA                       
         CLI   FCRDBUYS,C'I'                                                    
         BNE   AMLNOTID                                                         
*                                                                               
         L     R7,MKTLIST          POINT R7 TO ACTIVE MKTS                      
*                                                                               
         XC    MLRQID,MLRQID                                                    
         MVI   MLRQIDL,0                                                        
         CLI   QSTA+4,C'/'         TEST CABLE                                   
         BE    *+12                YES                                          
         CLI   QSTA,C'0'           NUMERIC STATION IS SPECIFIC ID               
         BL    AMLNOTID                                                         
*                                                                               
         MVC   WORK(4),QSTA                                                     
         LA    R1,WORK+3                                                        
         LA    R2,4                                                             
         CLI   0(R1),C' '                                                       
         BNE   *+12                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10                                                          
         DC    H'0'                                                             
*                                                                               
         CLI   0(R1),C'*'                                                       
         BNE   *+14                                                             
         MVI   0(R1),C' '                                                       
         BCTR  R1,0                                                             
         BCT   R2,*-14                                                          
         BCTR  R2,R0               ACTUAL ID COMPARE LEN (-1)                   
         STC   R2,MLRQIDL          SAVE COMPARE LENGTH                          
         MVC   MLRQID,WORK         SAVE REQ'D ID                                
*                                                                               
AMLNOTID CLC   =C'ALL',QMKT        TEST 'ALL' MARKETS                           
         BNE   EQXIT               NO                                           
*                                                                               
         CLI   FCRDBUYS,C'Y'       TEST BUYS PROCESSED                          
         BE    AML2                                                             
         CLI   FCRDBUYS,C'I'                                                    
         BE    AML2                                                             
         CLI   FCRDGOAL,C'A'       TEST COMBINED=SPOT/NET GOALS                 
         BE    AML20                                                            
         CLI   FCRDGOAL,C'Y'       TEST GOALS PROCESSED                         
         BE    AML20                                                            
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BE    AML20                                                            
         CLI   FCRDGOAL,C'B'       TEST BILLS PROCESSED                         
         BE    AML35                                                            
         CLI   FCRDGOAL,C'P'       TEST PEPSI LOCKIN RECS PROCESSED             
         BE    AML40                                                            
         B     EQXIT                                                            
         EJECT                                                                  
AML2     GOTO1 HIGH                                                             
*                                                                               
AML4     CLC   KEY(4),KEYSAVE      A-M/CLT/PRD                                  
         BNE   AMLIDX              NO                                           
* FILTER ON EST                                                                 
         CLI   BEST,0              TEST 'NO' OPTION                             
         BE    AML16                                                            
         CLC   KEY+9(1),BEST                                                    
         BE    AML6                MARKET ACTIVE                                
         BL    AML10               READ FOR EST                                 
         CLC   KEY+9(1),BESTEND                                                 
         BH    AML12               READ NEXT STA                                
* MARKET IS ACTIVE                                                              
AML6     CLI   FCRDBUYS,C'I'       TEST IN ID SEQ                               
         BE    AMLID                                                            
*                                                                               
         LH    RE,KEY+4                                                         
         SR    RF,RF                                                            
         SRDL  RE,1                                                             
         AR    RE,R7               POINT TO BYTE IN TABLE                       
         LA    R0,X'10'            ACTIVE BUY IN EVEN NUMBER MKT                
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         LA    R0,X'01'            ACTIVE BUY IN ODD NUMBER MKT                 
         IC    RF,0(RE)                                                         
         OR    RF,R0                                                            
         STC   RF,0(RE)                                                         
         B     AML14               NEXT MARKET                                  
* READ FOR ESTIMATE                                                             
AML10    MVC   KEY+9(1),BEST                                                    
         XC    KEY+10(3),KEY+10                                                 
         B     AML2                                                             
* READ FOR NEXT STA                                                             
AML12    MVC   KEY+9(4),=4X'FF'                                                 
         B     AML2                                                             
* READ FOR NEXT MKT                                                             
AML14    MVC   KEY+6(4),=4X'FF'                                                 
         B     AML2                                                             
* 'NO' OPTION                                                                   
AML16    SR    RE,RE                                                            
         IC    RE,KEY+9                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0             TEST IN LIST                                 
         BNE   AML6                YES                                          
* READ FOR NEXT EST                                                             
         MVC   KEY+10(3),=4X'FF'                                                
         B     AML2                                                             
         EJECT                                                                  
*=================================================================              
* FOR ID SEQUENCE, GET MGRPID FROM BUYREC AND BUILD ACTMKT TABLE                
*=================================================================              
                                                                                
AMLID    GOTO1 GETBUY                                                           
         L     R6,ADBUY                                                         
         USING BUYREC,R6                                                        
         MVI   BYTE,X'70'          FIND ID ELEM                                 
         LA    R6,BDELEM                                                        
         BRAS  RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   MLRQID,C' '         TEST ID IN REQUEST                           
         BNH   AMLID2                                                           
*                                                                               
         LLC   RE,MLRQIDL          GET COMPARE LENGTH                           
         EX    R2,*+8                                                           
         B     *+10                                                             
         CLC   MLRQID(0),4(R6)     MATCH REQUESTED GROUP                        
         BNE   AML14                                                            
*                                                                               
AMLID2   PACK  DUB(3),4(5,R6)      CODE/LEN/TYPE/SCH/GRP                        
         MVC   0(2,R7),DUB         MOVE MGRP                                    
         MVC   2(2,R7),BUYKMKT     MOVE MKT                                     
         OI    4(R7),X'01'         SET BUYS IN MKT                              
         LA    R7,5(R7)                                                         
         B     AML14               AND ON TO NEXT MKT                           
*                                                                               
AMLIDX   CLI   FCRDBUYS,C'I'       TEST ID SEQ                                  
         BNE   AML20               NO                                           
* SORT BY ID                                                                    
         S     R7,MKTLIST                                                       
         BZ    NEQXIT                                                           
         SR    R6,R6                                                            
         D     R6,=F'5'            GIVES NUMBER OF ENTRIES                      
         LR    R0,R7                                                            
         GOTO1 XSORT,DMCB,MKTLIST,(R0),5,4,0                                    
         B     EQXIT                                                            
*                                  FIELDS USED IN ID POINTER READS              
MLRQID   DS    CL4                 REQUESTED ID                                 
MLRQIDL  DS    X                   LENGTH FOR REQ'D ID COMPARE                  
         EJECT                                                                  
* CHECK GOALREC SPEC                                                            
                                                                                
AML20    CLI   COMSCORE,C'1'       SKIP GOALS FOR COMSCORE PASS 1               
         BE    AML35                                                            
*                                                                               
         CLI   FCRDGOAL,C'A'       TEST COMBINED=SPOT/NET GOALS                 
         BE    *+8                                                              
         CLI   FCRDGOAL,C'Y'                                                    
         BE    *+12                                                             
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BNE   AML35                                                            
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,2                                                            
         MVC   KEY+1(1),SVAGYMD                                                 
         CLI   FCRDGOAL,C'A'                                                    
         BNE   *+12                                                             
         XI    KEY+1,X'08'         TURN OFF COMBINED                            
         OI    KEY+1,X'01'         SET FOR SPOT                                 
AML20A   MVC   KEY+2(2),SVCLT                                                   
         MVC   KEY+4(1),SVPRDCD                                                 
         CLI   KEY+4,X'FF'         CHECK POL                                    
         BNE   *+8                                                              
         MVI   KEY+4,0             SET FOR ALL BRANDS                           
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BNE   *+8                                                              
         MVI   KEY+4,X'FF'         YES - SET PRD TO POL                         
*                                                                               
AML22    GOTO1 HIGH                                                             
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BE    AMLCPP                                                           
*                                                                               
         CLC   KEY(5),KEYSAVE      02/A-M/CLT/PRD                               
         BE    AML23                                                            
         CLC   KEY(4),KEYSAVE      02/A-M/CLT                                   
         BNE   AML32                                                            
* NEW PRODUCT                                                                   
         CLI   KEY+4,X'FF'         SKIP CPP GUIDE                               
         BE    AML32                                                            
         CLI   SVPRDCD,X'FF'       TEST POL REQUEST                             
         BNE   AML32                                                            
         LA    R1,KEY+4            POINT TO PRD                                 
         BAS   RE,POLPGR           TEST POL BY PRDGRP                           
         BE    AML23                                                            
         MVC   KEY+5(4),=4X'FF'    FORCE NEXT PRD                               
         B     AML22                                                            
* FILTER ON EST                                                                 
AML23    CLI   BEST,0              TEST 'NO' OPTION                             
         BE    AML30                                                            
         CLC   KEY+7(1),BEST                                                    
         BE    AML24               MARKET ACTIVE                                
         BL    AML26               READ FOR EST                                 
         CLC   KEY+7(1),BESTEND                                                 
         BH    AML28               NEXT MKT                                     
* MARKET IS ACTIVE                                                              
AML24    MVC   HALF,KEY+5                                                       
         LH    RE,HALF                                                          
         SR    RF,RF                                                            
         SRDL  RE,1                                                             
         AR    RE,R7               POINT TO BYTE IN TABLE                       
         LA    R0,X'20'            ACTIVE GOAL IN EVEN MKT                      
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         LA    R0,X'02'            ACTIVE GOAL IN ODD MKT                       
         IC    RF,0(RE)            RETRIEVE TABLE                               
         OR    RF,R0               'OR' IN VALUE                                
         STC   RF,0(RE)                                                         
         B     AML28                                                            
         SPACE 2                                                                
* READ FOR ESTIMATE                                                             
AML26    MVC   KEY+7(1),BEST                                                    
         XC    KEY+8(5),KEY+8                                                   
         B     AML22                                                            
* NEXT MKT                                                                      
AML28    MVC   KEY+7(4),=4X'FF'                                                 
         B     AML22                                                            
         SPACE 2                                                                
* 'NO' OPTION                                                                   
AML30    SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0             TEST EST IN LIST                             
         BNE   AML24               YES                                          
         MVC   KEY+8(4),=4X'FF'    NEXT EST                                     
         B     AML22                                                            
         SPACE 2                                                                
AML32    CLI   FCRDGOAL,C'A'       COMBINED=SPOT + NETWORK                      
         BNE   AML35                                                            
         TM    KEYSAVE+1,X'02'                                                  
         BO    AML35                                                            
         NI    KEYSAVE+1,X'F0'                                                  
         OI    KEYSAVE+1,X'03'                                                  
         XC    KEY,KEY                                                          
         MVC   KEY(2),KEYSAVE                                                   
         B     AML20A                                                           
         SPACE 2                                                                
AMLCPP   CLC   KEY(5),KEYSAVE      02/A-M/CLT/PRD                               
         BNE   AML35                                                            
         B     AML23                                                            
         EJECT                                                                  
* CHECK BILL SPEC                                                               
*                                                                               
AML35    CLI   COMSCORE,C'1'                                                    
         BE    AML40                                                            
         CLI   FCRDGOAL,C'B'       TEST BILLS PROCESSED                         
         BNE   AML40                                                            
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0E01'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         MVC   KEY+3(2),SVCLT                                                   
         MVC   KEY+5(1),SVPRDCD                                                 
         CLI   SVPRDCD,X'FF'       TEST POL REQUEST                             
         BNE   *+8                                                              
         MVI   KEY+5,0             SET FOR ALL BRANDS                           
         MVC   KEY+6(1),BEST                                                    
*                                                                               
AML35D   DS    0H                                                               
         XC    KEY+7(6),KEY+7                                                   
*                                  (ZERO IF NONE)                               
AML36    GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(6),KEYSAVE      0E01/A-M/CLT/PRD                             
         BE    AML36X                                                           
         CLC   KEY(5),KEYSAVE      0E01/A-M/CLT                                 
         BNE   AML40                                                            
* NEW PRODUCT                                                                   
         CLI   SVPRDCD,X'FF'       TEST POL REQUEST                             
         BNE   AML40                                                            
         LA    R1,KEY+5                                                         
         BAS   RE,POLPGR           TEST POL BY PRDGRP                           
         BE    AML36X                                                           
         MVC   KEY+6(4),=4X'FF'    FORCE NEXT PRD                               
         B     AML36                                                            
AML36X   CLI   BEST,0                                                           
         BE    AML38                                                            
         CLC   KEY+6(1),BEST                                                    
         BE    AML37               MARKET ACTIVE                                
         CLC   KEY+6(1),BESTEND                                                 
         BH    AML40               IF EST HIGH, DONE                            
* MARKET IS ACTIVE                                                              
AML37    MVC   HALF,KEY+7                                                       
         LH    RE,HALF                                                          
         SR    RF,RF                                                            
         SRDL  RE,1                                                             
         AR    RE,R7                                                            
         LA    R0,X'20'            ACTIVE IN EVEN MKT                           
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         LA    R0,X'02'            ACTIVE IN ODD MKT                            
         IC    RF,0(RE)                                                         
         OR    RF,R0                                                            
         STC   RF,0(RE)                                                         
         B     AML39                                                            
         SPACE 3                                                                
* 'NO' OPTION                                                                   
AML38    LLC   RE,KEY+6                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   AML37               GO POST MKT ACTIVE                           
*                                                                               
AML39    DS    0H                  NEXT MKT                                     
         MVC   KEY+9(4),=4X'FF'                                                 
         B     AML36                                                            
*                                                                               
         SPACE 2                                                                
* CHECK PEPSI SPEC                                                              
*                                                                               
AML40    CLI   COMSCORE,C'1'                                                    
         BE    AML50                                                            
         CLI   FCRDGOAL,C'P'       TEST PEPSI LOCKIN PROCESSED                  
         BNE   AML50                                                            
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D15'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         MVC   KEY+3(2),SVCLT                                                   
         MVC   KEY+5(1),SVPRDCD                                                 
         CLI   SVPRDCD,X'FF'       TEST POL REQ                                 
         BNE   *+8                                                              
         MVI   KEY+5,0                                                          
*                                                                               
AML41    GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(6),KEYSAVE      0D15/A-M/CLT/PRD                             
         BE    AML41X                                                           
         CLC   KEY(5),KEYSAVE      0D15/A-M/CLT                                 
         BNE   AML50                                                            
* NEW PRODUCT                                                                   
         CLI   SVPRDCD,X'FF'       TEST POL REQ                                 
         BNE   AML50                                                            
         LA    R1,KEY+5                                                         
         BAS   RE,POLPGR           TEST POL BY PRDGRP                           
         BE    AML41X                                                           
         MVC   KEY+6(4),=4X'FF'                                                 
         B     AML41                                                            
* FILTER ON EST                                                                 
AML41X   CLI   BEST,0              TEST 'NO' OPTION                             
         BE    AML44                                                            
         CLC   KEY+8(1),BEST                                                    
         BE    AML42                                                            
         BL    AML43                                                            
         CLC   KEY+8(1),BESTEND                                                 
         BH    AML43X              NEXT MKT                                     
* MARKET IS ACTIVE                                                              
AML42    MVC   HALF,KEY+6                                                       
         LH    RE,HALF                                                          
         SR    RF,RF                                                            
         SRDL  RE,1                                                             
         AR    RE,R7                                                            
         LA    R0,X'20'            ACTIVE IN EVEN MKT                           
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         LA    R0,X'02'            ACTIVE IN ODD MKT                            
         IC    RF,0(RE)                                                         
         OR    RF,R0               'OR' IN VALUE                                
         STC   RF,0(RE)                                                         
         B     AML43X              NEXT MKT                                     
         SPACE 2                                                                
* READ FOR EST                                                                  
AML43    MVC   KEY+8(1),BEST                                                    
         XC    KEY+9(4),KEY+9                                                   
         B     AML41                                                            
* NEXT MKT                                                                      
AML43X   MVC   KEY+8(4),=4X'FF'                                                 
         B     AML41                                                            
* 'NO' OPTION                                                                   
AML44    LLC   RE,KEY+8                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0             TEST EST IN LIST                             
         BNE   AML42               YES                                          
         MVC   KEY+9(4),=4X'FF'    NEXT EST                                     
         B     AML41                                                            
         EJECT                                                                  
* DECODE TABLE IN BUYIDLST TO MKTLIST                                           
                                                                                
AML50    CLI   QMGR,C'0'           TEST FOR MARKET RANK                         
         BL    AML51                                                            
         CLI   QMGR,C'9'                                                        
         BH    AML51                                                            
         GOTO1 =V(GETMKTRK),DMCB,(RA)  YES - BUILD MKT RANK MKTLIST             
         B     EQXIT                                                            
*                                                                               
AML51    LA    R0,2500                                                          
         AR    R0,R0               5000 (SO MAX MKT NUM IS 9999)                
         L     R1,MKTLIST          NEW LIST                                     
         L     R2,BUYLIST          OLD LIST                                     
         SR    R3,R3               MKT NUM                                      
         SR    R4,R4                                                            
*                                                                               
AML52    IC    R4,0(R2)                                                         
         SRDL  R4,4                                                             
         SRL   R5,28                                                            
         LTR   R4,R4                                                            
         BZ    AML54                                                            
         LTR   R3,R3               DONT DO MARKET ZERO                          
         BNZ   AML53                                                            
         TM    RCOPTS,RCOPT_INCLMKT0  PROGRAM ASK TO INCLUDE MKT0               
         BO    AML53                                                            
         CLC   =C'0000',QMKT       UNLESS SPECIFIC REQUEST                      
         BNE   AML54                                                            
AML53    MVC   0(2,R1),=X'9999'    SET UNDEFINED GRP                            
         STH   R3,2(R1)            MKT NUM                                      
         STC   R4,4(R1)            IND                                          
         LA    R1,5(R1)            NEXT NEW ENTRY                               
         C     R1,MKTLISTX         CHECK LIST FULL                              
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
AML54    LA    R3,1(R3)            BUMP MKT NUM                                 
         LTR   R5,R5                                                            
         BZ    AML56                                                            
         MVC   0(2,R1),=X'9999'    SET UNDEFINED GRP                            
         STH   R3,2(R1)            MKT NUM                                      
         STC   R5,4(R1)            IND                                          
         LA    R1,5(R1)            NEXT NEW ENTRY                               
         C     R1,MKTLISTX         CHECK LIST FULL                              
         BL    *+6                                                              
         DC    H'0'                TOO MANY ACTIVE MARKETS                      
*                                                                               
AML56    LA    R3,1(R3)            NEXT MKT NUM                                 
         LA    R2,1(R2)            NEXT OLD ENTRY                               
         BCT   R0,AML52                                                         
*                                                                               
         B     EQXIT                                                            
         SPACE 2                                                                
* TEST FOR POL PRDGRP REQUEST                                                   
* ON ENTRY R1 POINTS TO PRD CODE                                                
*                                                                               
POLPGR   CLI   SVPRDCD,X'FF'       TEST POL REQ                                 
         BNE   POLPGRX1            NO                                           
         CLI   QPGR,C' '           TEST PRDGRPS                                 
         BE    POLPGRX1            NO                                           
* TEST PRD IN THIS PRDGRP                                                       
         L     RF,SVPRDADR                                                      
POLPGR2  CLC   5(1,RF),0(R1)       MATCH PRD CODE                               
         BE    POLPGRX1                                                         
         CLC   0(2,RF),6(RF)       NEXT PRD SAME PRDGRP                         
         BNE   POLPGRX2                                                         
         LA    RF,6(RF)                                                         
         B     POLPGR2                                                          
POLPGRX1 CR    RE,RE               SET CC EQ                                    
         BR    RE                                                               
POLPGRX2 LTR   RE,RE               SET CC NEQ                                   
         BR    RE                                                               
         EJECT                                                                  
NEXTMGR  GOTO1 =A(NMGR)                                                         
         BE    EQXIT                                                            
         B     NEQXIT                                                           
         SPACE 2                                                                
NEXTMKT  DS    0H                                                               
         XC    SVIDADR,SVIDADR     CLEAR POINTER                                
         XC    SVSTLADR,SVSTLADR   CLEAR STATION LIST POINTER                   
         CLI   QMGR,C' '           TEST MARKET GROUPS                           
         BNE   NMKT20                                                           
         CLC   =C'ALL',QMKT        TEST 'ALL' MARKETS                           
         BE    NMKT10              YES                                          
* SINGLE MARKET REQUEST                                                         
         OC    SVMKTADR,SVMKTADR         TEST FIRST TIME                        
         BNZ   NEQXIT              NO                                           
* CREATE A FAKE LIST ENTRY FOR THIS MARKET                                      
         PACK  DUB,QMKT                                                         
         CVB   R0,DUB                                                           
         L     R2,MKTLIST                                                       
         XC    0(10,R2),0(R2)                                                   
         STH   R0,2(R2)            SET MARKET IN LIST                           
         MVI   4(R2),X'03'         INDICATE BUY/GOAL ACTIVITY                   
         EJECT                                                                  
* 'ALL' MARKETS, NO MKTGRPS                                                     
*                                                                               
NMKT10   DS    0H                                                               
         L     R2,SVMKTADR         GET LAST MARKET POINTER                      
         LA    R2,5(R2)            NEXT ENTRY                                   
         OC    SVMKTADR,SVMKTADR         TEST FIRST TIME                        
         BNZ   *+8                                                              
         L     R2,MKTLIST          POINT TO FIRST LIST ENTRY                    
         ST    R2,SVMKTADR                                                      
*                                                                               
NMKT10A  MVC   SVMKT,2(R2)                                                      
         OC    0(5,R2),0(R2)       TEST E-O-L                                   
         BE    NEQXIT              YES                                          
         CLI   SVMKT,X'FF'         TEST DUMMY MARKET                            
         BE    NMKT11                                                           
* FETCH MARKET RECORD                                                           
         CLI   FCGETMKT,C'Y'       USER WANT MARKET                             
         BNE   NMKT11                                                           
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'M'                                                         
         MVC   KEY+1(1),QMED                                                    
         LH    R0,SVMKT                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  KEY+2(4),DUB                                                     
         MVC   KEY+6(2),QAGY                                                    
         CLI   QMGR,C' '           TEST MARKET GRP                              
         BNE   NMKT10B             YES                                          
         CLI   QSTA+4,C'/'         TEST CABLE                                   
         BE    NMKT10B             YES                                          
         CLI   QSTA,C'0'           TEST STARTING MARKET NO. IN QSTA             
         BL    NMKT10B             NO                                           
         CLC   KEY+2(4),QSTA                                                    
         BL    NMKT10                                                           
*                                                                               
NMKT10B  DS    0H                                                               
*                                                                               
         GOTO1 READMKT                                                          
         EJECT                                                                  
* CHECK MARKET FILTER                                                           
         CLC   =C'ALL',QMKT                                                     
         BNE   NMKT11                                                           
         CLI   QMKT+3,C' '                                                      
         BE    NMKT11                                                           
         CLI   RQMKTFLT,C'Y'                                                    
         BNE   NMKT11                                                           
         L     R6,ADMARKET                                                      
         USING MKTREC,R6                                                        
         L     RE,ADCLT                                                         
         LA    RE,CPROF+3-CLTHDR(RE) POINT TO RTG SVC                           
         LA    RF,MKTCLAS1                                                      
         CLC   MKTRS1,0(RE)                                                     
         BE    NMKT10C                                                          
         LA    RF,MKTCLAS2                                                      
         CLC   MKTRS2,0(RE)                                                     
         BNE   NEXTMKT                                                          
NMKT10C  CLC   0(1,RF),QMKT+3      ACT SWEEP CLASS TO REQUESTED                 
         BH    NEXTMKT                                                          
         CLI   0(RF),C'1'                                                       
         BL    NEXTMKT                                                          
*                                                                               
NMKT11   MVI   SVBUYNAM,C'*'                                                    
         MVC   SVBUYNAM+1(23),SVBUYNAM                                          
*                                                                               
         CLI   COMSCORE,C'1'                                                    
         BE    NMKT11X                                                          
*                                                                               
         CLI   RQGETNAM,C'Y'       TEST TO READ BUYER/BILLER NAMES              
         BNE   NMKT11X             NO                                           
         L     R6,ADAGY                                                         
         USING AGYHDR,R6                                                        
         CLI   AGYPROF+14,C'Y'     FEATURE MUST BE IN AGYHDR                    
         BNE   NMKT11X                                                          
         DROP  R6                                                               
*                                                                               
         CLI   QUESTOR,C'='        TEST OVERRIDE                                
         BNE   *+20                                                             
         MVC   SVBUYNAM,QUESTOR    MOVE OVERRIDE TO ALL NAMES                   
         MVC   SVBILNAM,QUESTOR                                                 
         B     NMKT11X                                                          
*                                                                               
         GOTO1 =A(GETNAME)                                                      
         EJECT                                                                  
*======================================================*                        
* BUILD BUY KEY                                        *                        
*======================================================*                        
         SPACE 1                                                                
NMKT11X  XC    KEY,KEY                                                          
         MVC   KEY(1),SVAGYMD                                                   
         CLI   QORIGBUY,C'Y'                                                    
         BNE   *+8                                                              
         OI    KEY,X'08'                                                        
         MVC   KEY+1(2),SVCLT                                                   
         MVC   KEY+3(1),SVPRDCD                                                 
*                                                                               
         CLI   QMED,C'N'           FOR CANADIAN NETWORK                         
         BNE   NMKT11Y             BUYS ARE ALWAYS IN MARKET 0000               
         CLI   QPROG,C'K'      ****FOR K PROGRAMS ONLY (K4,K5)                  
         BNE   NMKT11Y         ****TEMPROARY PROTECTION??                       
         CLI   COUNTRY,C'C'                                                     
         BE    *+10                                                             
*                                                                               
NMKT11Y  DS    0H                                                               
         MVC   KEY+4(2),SVMKT                                                   
         MVC   KEY+6(3),SVSTA                                                   
         MVC   KEY+9(1),BEST                                                    
*                                                                               
NMKT12   DS    0H                                                               
         XC    SVBUYKEY,SVBUYKEY                                                
         TM    4(R2),X'01'         TEST FOR BUY ACTIVITY                        
         BZ    *+10                                                             
         MVC   SVBUYKEY,KEY                                                     
         EJECT                                                                  
* BUILD GOAL (OR BILL OR PEPSI) KEY                                             
         XC    SVGLKEY,SVGLKEY                                                  
         TM    4(R2),X'02'         TEST ACTIVE                                  
         BZ    NMKT30                                                           
         CLI   FCRDGOAL,C'B'       TEST BILLS                                   
         BE    NMKT30              NO ACTUAL 'GOAL' READ FOR BILLS              
         CLI   FCRDGOAL,C'P'       TEST PEPSI                                   
         BE    NMKT13                                                           
* GOALS                                                                         
         XC    KEY,KEY                                                          
         MVI   KEY,2                                                            
         MVC   KEY+1(1),SVAGYMD                                                 
         MVC   KEY+2(2),SVCLT                                                   
         MVC   KEY+4(1),SVPRDCD                                                 
         CLI   SVPRDCD,X'FF'                                                    
         BNE   *+8                                                              
         MVI   KEY+4,0                                                          
         MVC   KEY+5(2),SVMKT                                                   
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BNE   *+8                                                              
         MVI   KEY+4,X'FF'         YES - SET PRD TO POL                         
         B     NMKT13X                                                          
* PEPSI                                                                         
NMKT13   XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D15'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         MVC   KEY+3(2),SVCLT                                                   
         MVC   KEY+5(1),SVPRDCD                                                 
         CLI   SVPRDCD,X'FF'                                                    
         BNE   *+8                                                              
         MVI   KEY+5,0                                                          
         MVC   KEY+6(2),SVMKT                                                   
*                                                                               
NMKT13X  MVC   SVGLKEY,KEY                                                      
         B     NMKT30                                                           
         EJECT                                                                  
* 'ALL' MARKETS BY MKTGRP                                                       
*                                                                               
NMKT20   DS    0H                                                               
         L     R2,SVMKTADR         GET LIST POINTER (SET BY NEXTMGR)            
         OC    SVMKT,SVMKT         TEST FIRST TIME THIS MKTGRP                  
         BZ    NMKT22              YES                                          
         CLC   0(2,R2),5(R2)       TEST NEXT MKT IN SAME MKTGRP                 
         BNE   NEQXIT              NO                                           
         LA    R2,5(R2)            NEXT MKT                                     
         ST    R2,SVMKTADR                                                      
*                                                                               
NMKT22   UNPK  DUB,5(3,R2)         UNPACK NEXT MKTGRP                           
         MVC   SVNXTMGR,DUB+3                                                   
         OC    5(2,R2),5(R2)       TEST E-O-L                                   
         BNZ   *+10                NO                                           
         XC    SVNXTMGR,SVNXTMGR                                                
         B     NMKT10A                                                          
         SPACE 2                                                                
NMKT30   TM    4(R2),X'01'         TEST BUYS THIS MARKET                        
         BZ    NMKT40              NO                                           
         GOTO1 FCNXTSTA                                                         
         BE    EQXIT                                                            
         XC    SVBUYKEY,SVBUYKEY   CLEAR KEY IF NO BUYS                         
*                                                                               
* GOALS ONLY                                                                    
*                                                                               
NMKT40   DS    0H                                                               
         TM    4(R2),X'02'         TEST FOR GOALS                               
         BZ    NMKT45              NO                                           
*                                                                               
         MVC   KEY,SVGLKEY         PROVIDE START KEY FOR FRSTGOAL               
         GOTO1 FCFRSGL                                                          
         BNE   NMKT45                                                           
         MVC   BMKT,SVMKT          SET MARKET NUM                               
         B     EQXIT                                                            
*                                                                               
NMKT45   B     NEXTMKT             NO ACTIVE BUYS OR GOALS                      
         EJECT                                                                  
*============================================================*                  
* CABLE STATIONS LOOK LIKE X'XX00NN' WHERE XX >= E8          *                  
* AND NN IS THE NETWORK DESIGNATION IN 7 BITS                *                  
* IN APR/98 STATION FILTERING MOVED TO STAPACK               *                  
*============================================================*                  
         SPACE 1                                                                
NEXTSTA  MVC   KEY(9),SVBUYKEY     A-M/CLT/PRD/MKT/STA                          
         LARL  RE,BLDSVCBL                                                      
         MVI   0(RE),C'N'                                                       
*                                                                               
NSTA2    CLC   =C'ALL',QSTA        TEST ALL STATION REQUEST                     
         BE    NSTA10              YES                                          
         CLI   QSTA+4,C'/'         TEST LOCAL CABLE STATION                     
         BNE   NSTA2A              NO                                           
         CLI   QCBLNET,C' '        TEST NETWORK FILTER                          
         BH    NSTA2B                                                           
*                                                                               
         CLI   COMSCORE,C'1'       TEST COMSCORE PASS 1                         
         JE    NSTA2B                                                           
*                                                                               
         CLI   RQALPHA,C'Y'        TEST ALPHA SEQ REQUEST                       
         BNE   NSTA2B                                                           
         LARL  RE,BLDSVCBL                                                      
         MVI   0(RE),C'Y'                                                       
         B     NSTA10                                                           
*                                                                               
NSTA2A   CLI   QSTA,C'0'           TEST MKTGRP REQUEST (NUM STA)                
         BNL   NSTA10              YES                                          
*                                                                               
NSTA2B   OC    SVSTA,SVSTA         TEST FIRST TIME                              
         BE    NSTA4               YES - CONTINUE                               
         CLI   STAMASK+3,X'FF'     TEST DROPPING MEDIA BITS                     
         BE    NSTA3               NO                                           
         MVC   KEY,SVBUYKEY                                                     
         MVC   KEY+9(4),=4X'FF'    FORCE NEXT STATION                           
         B     NSTA8                                                            
*                                                                               
NSTA3    OC    SVIDADR,SVIDADR     TEST IN ID SEQ PROCESSING                    
         BZ    NEQXIT              NO - DONE                                    
*                                                                               
* NEED TO SEE IF LAST ID PROCESSED YET                                          
*                                                                               
         L     R5,BUYLIST                                                       
         CLC   SVIDADR(1),0(R5)    MATCH PREVIOUS ID                            
         BE    *+12                                                             
         LA    R5,13(R5)                                                        
         B     *-14                                                             
         CLI   13(R5),0            TEST ANOTHER ID IN LIST                      
         BE    NEQXIT              NO - DONE                                    
         B     NBID                YES - PROCESS AND EXIT                       
*                                                                               
NSTA4    MVC   DUB(5),QSTA                                                      
         MVC   DUB+5(3),QCBLNET                                                 
         GOTO1 MSPACK,DMCB,QMKT,DUB,WORK                                        
         MVC   SVSTA,WORK+2                                                     
         MVC   KEY+6(3),SVSTA                                                   
         EJECT                                                                  
         CLI   QMED,C'R'                                                        
         BE    NSTA5                                                            
         SPACE 1                                                                
*=============================================================*                 
* FOR CANADIAN T/N/C AND LOCAL CABLE NEED TO DROP MEDIA BITS  *                 
*=============================================================*                 
         SPACE 1                                                                
         L     R0,=X'00FFFF00'                                                  
         CLI   COUNTRY,C'C'                                                     
         BE    NSTA6                                                            
         CLI   QSTA+4,C'/'         TEST CABLE NET                               
         BNE   NSTA5               NO, FULL COMPARE                             
         L     R0,=X'00FFFF80'     SET MASK TO DROP NETWORK                     
         CLI   QCBLNET,C'A'        TEST CABLE NET FILTER                        
         BL    NSTA6               NO - DROP LAST BITS                          
*                                  ONE NETWORK (OR NOT LOCAL CABLE)             
NSTA5    L     R0,=X'00FFFFFF'     ==> FULL COMPARE                             
*                                                                               
NSTA6    ST    R0,STAMASK                                                       
         ICM   RE,7,SVSTA                                                       
         N     RE,STAMASK                                                       
         STCM  RE,7,SVSTA                                                       
         ICM   RE,7,KEY+6                                                       
         N     RE,STAMASK                                                       
         STCM  RE,7,KEY+6                                                       
*                                                                               
NSTA8    DS    0H                                                               
         GOTO1 FCFRSBUY                                                         
         BNE   NEQXIT                                                           
         ICM   RE,7,SVSTA          GET REQUESTED STATION                        
         N     RE,STAMASK          DROP HOB (AT LEAST)                          
         ICM   RF,7,KEY+6          GET ACTUAL STATION                           
         N     RF,STAMASK          DROP MEDIA BITS AS REQUIRED                  
         CR    RE,RF               TEST REQ TO ACTUAL                           
         BNE   NEQXIT                                                           
         MVC   BMKTSTA,KEY+4       SET MKT/STA                                  
         B     NSTA22                                                           
         EJECT                                                                  
NSTA10   CLI   FCRDBUYS,C'N'                                                    
         BE    NSTA20                                                           
*                                                                               
         CLI   COMSCORE,C'1'                                                    
         JE    NSTA20                                                           
*                                                                               
         CLI   RQALPHA,C'Y'        TEST STATIONS IN ALPHA SEQ                   
         BNE   NSTA20              NO                                           
         CLI   QMED,C'R'           TEST RADIO                                   
         BE    NSTA20              YES- FILE IS IN SEQUENCE                     
         CLI   QCBLNET,C'A'        TEST CABLE NETWORK FILTER                    
         BNL   NSTA20              YES - THEN NO NTWKS TO SEQUENCE              
         CLI   QBYID,C'Y'          TEST REPORT IN ID SEQ                        
         BE    NSTA20              YES - NO ALPHA SEQ                           
*                                                                               
         OC    SVSTLADR,SVSTLADR   TEST STATION LIST BUILT YET                  
         BNZ   NSTA12                                                           
         BRAS  RE,BLDSTLST                                                      
*                                                                               
         L     RE,ADSTALST         POINT TO START OF LIST                       
         B     NSTA14                                                           
*                                                                               
NSTA12   L     RE,SVSTLADR                                                      
         LA    RE,12(RE)                                                        
*                                                                               
NSTA14   ST    RE,SVSTLADR                                                      
         OC    0(3,RE),0(RE)       TEST EOL                                     
         BZ    NEQXIT                                                           
         MVC   KEY,SVBUYKEY        RESTORE PREVIOUS KEY                         
         MVC   KEY+6(3),0(RE)      NEXT STATION                                 
         XC    KEY+9(4),KEY+9                                                   
         GOTO1 FCFRSBUY                                                         
         BNE   NSTA12                                                           
         MVC   BMKTSTA,KEY+4       SET ACTUAL MARKET/STATION                    
         B     NSTA22                                                           
*                                                                               
NSTA20   MVC   KEY+9(4),=4X'FF'    FORCE NEXT STA                               
         GOTO1 FCFRSBUY                                                         
         BNE   NEQXIT                                                           
         MVC   BMKTSTA,KEY+4       SET ACTUAL MARKET/STATION                    
*                                                                               
NSTA22   CLI   QSTA+4,C'/'         TEST CABLE ONLY REQUEST                      
         BNE   NSTA23                                                           
         CLI   KEY+6,X'E8'         TEST CABLE STATION                           
         BL    NSTA10              NO - SKIP                                    
         B     NSTA24                                                           
*                                                                               
NSTA23   CLI   QSTA+4,C'-'         TEST SPOT ONLY REQUEST                       
         BNE   NSTA24              NO                                           
         CLI   KEY+6,X'E8'         TEST CABLE STATION                           
         BNL   NSTA10              YES - SKIP                                   
*                                                                               
NSTA24   CLI   QCBLNET,C'A'        TEST CABLE NETWORK FILTER                    
         BL    NSTA26              NO                                           
*                                                                               
         L     R1,=A(STAWORK)                                                   
         XC    0(L'STAWORK,R1),0(R1)                                            
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'K'        ASK FOR NETWORK FILTER                       
         MVC   STAPAGY,AGY                                                      
         MVC   STAPCTRY,COUNTRY                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPMKST,KEY+4      MKTSTA                                       
         MVC   STAPQNET,QCBLNET    CABLE NETWORK                                
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         CLI   STAPERR,QSTP_YES     TEST PASSED FILTER                          
         BNE   NSTA10                                                           
         DROP  R1                                                               
         EJECT                                                                  
*=================================================================*             
* GET STATION RECORD                                              *             
*=================================================================*             
         SPACE 1                                                                
NSTA26   CLI   RQSTAFLT,C' '       ALWAYS NEED STAREC IF FILTERING              
         BNE   NSTA28                                                           
         CLC   =C'SIZE=',QUESTOR                                                
         BE    NSTA28                                                           
         CLI   FCGETSTA,C'Y'       USER WANT STATION REC                        
         BNE   NSTAX                                                            
*                                                                               
NSTA28   MVC   KEYSAVE,KEY         SAVE KEY                                     
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'S'                                                         
         MVC   KEY+1(1),QMED                                                    
         GOTO1 MSUNPK,DMCB,BMKTSTA,WORK,KEY+2                                   
*                                                                               
         CLI   KEY+6,C'A'                                                       
         BE    NSTA29                                                           
         CLI   KEY+6,C'F'                                                       
         BE    NSTA29                                                           
         CLI   KEY+6,C'L'                                                       
         BE    NSTA29                                                           
         CLI   KEY+6,C'D'          DIGITAL VIDEO                                
         BE    NSTA29                                                           
         CLI   KEY+6,C'S'          STREAMING                                    
         BE    NSTA29                                                           
         CLI   KEY+6,C'C'          IHEART RADIO                                 
         BE    NSTA29                                                           
         MVC   KEY+6(1),QMED                                                    
*                                                                               
NSTA29   MVC   KEY+7(2),QAGY                                                    
         MVC   KEY+9(3),CLT                                                     
         GOTO1 READSTA                                                          
         MVC   KEY,KEYSAVE         RESTORE KEY                                  
         CLI   MODE,DISKERR                                                     
         BE    NEXTSTA                                                          
*                                                                               
         L     R6,ADSTAT                                                        
         USING STAREC,R6                                                        
         MVC   SVSTAMSF,SSVCFEE    SAVE MEDIA SERVICE FEE                       
         CLI   RQSTAFLT,C' '       TEST TO FILTER ON AFFL                       
         BE    NSTA32              NO                                           
*                                                                               
         CLI   COUNTRY,C'C'        TEST CANADIAN                                
         BE    NSTA30              YES                                          
*                                                                               
         CLC   RQSTAFLT(1),SNETWRK                                              
         BNE   NEXTSTA                                                          
         B     NSTA32                                                           
STAMASK  DS    F                                                                
CBLMASK  DS    F                                                                
         EJECT                                                                  
*==============================================================*                
* SINCE CANADIAN NTWKS ALL HAVE SAME LETTERS, USE STATION SIZE *                
*==============================================================*                
         SPACE 1                                                                
NSTA30   CLC   RQSTAFLT(1),SSIZE                                                
         BNE   NEXTSTA                                                          
         B     NSTAX                                                            
         SPACE 1                                                                
* FILTER ON SIZE IF NEEDED                                                      
         SPACE 1                                                                
NSTA32   CLC   =C'SIZE=',QUESTOR                                                
         BNE   NSTAX                                                            
         L     R6,ADSTAT                                                        
         USING STAREC,R6                                                        
         CLC   SSIZE,QUESTOR+5                                                  
         BNE   NEXTSTA                                                          
*                                                                               
NSTAX    B     EQXIT                                                            
         EJECT                                                                  
FRSTBUY  CLI   FCRDBUYS,C'N'                                                    
         BNE   FB1                                                              
         XC    SVBUYKEY,SVBUYKEY                                                
         B     NEQXIT                                                           
*                                                                               
FB1      DC    0H'0'                                                            
         MVC   SVBUYKEY,KEY                                                     
         XC    SVPKGKEY,SVPKGKEY                                                
         L     RE,=A(SVPKGEL)                                                   
         XC    0(L'SVPKGEL,RE),0(RE)                                            
         BAS   RE,SETDMBTS                                                      
         OC    SVIDADR,SVIDADR     TEST IN ID SEQ PROCESSING                    
         BNZ   NBID                                                             
*                                                                               
FB2      GOTO1 HIGH                                                             
         B     FB4                                                              
*                                                                               
FB3      GOTO1 SEQ                                                              
*                                                                               
FB4      CLC   KEY(9),SVBUYKEY     SAME A-M/CLT/PRD/MKT/STA                     
         BE    FB5                                                              
         OC    SVSTLADR,SVSTLADR   TEST RUNNING IN ALPHA SEQ                    
         BNZ   NEQXIT              YES                                          
         CLC   KEY(6),SVBUYKEY     SAME A-M/CLT/PRD/MKT                         
         BNE   NEQXIT                                                           
* FILTER ON EST                                                                 
FB5      CLI   BEST,0                                                           
         BE    FB8                                                              
         CLC   KEY+9(1),BEST                                                    
         BE    FB10                                                             
         BL    FB6                                                              
         CLC   KEY+9(1),BESTEND                                                 
         BNH   FB10                                                             
         MVC   KEY+9(4),=4X'FF'    NEXT STA                                     
         B     FB2                                                              
* READ FOR EST                                                                  
FB6      MVC   KEY+9(1),BEST                                                    
         XC    KEY+10(3),KEY+10                                                 
         B     FB2                                                              
* 'NO' OPTION                                                                   
FB8      SR    RE,RE                                                            
         IC    RE,KEY+9                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   FB10                                                             
         MVC   KEY+10(3),=4X'FF'   NEXT EST                                     
         B     FB2                                                              
         EJECT                                                                  
FB10     DS    0H                                                               
         CLI   RQNOPSSV,C'N'       TEST EXCLUDE PASSIVE POINTERS                
         BE    *+12                NO-USE IT                                    
         CLI   KEY+10,0            ELSE TEST PASSIVE                            
         BNE   FB3                                                              
*                                                                               
         GOTO1 GETBUY                                                           
         CLC   QPROG,=C'UB'        UB (BUY EXTRACT UPDATE) REPORT?              
         BNE   FB10A                                                            
         TM    DMCB+8,X'10'        TEST NOT FOUND                               
         BO    FB3                                                              
         B     FB10B                                                            
*                                                                               
FB10A    TM    DMCB+8,X'12'        TEST NOT FOUND                               
         BNZ   FB3                                                              
*                                                                               
FB10B    CLI   FCRDPKG,C'N'        TEST READING LINKED PACKAGES                 
         BE    FB12                NO - PROCESS                                 
         GOTO1 =A(NBSLVTST)        TEST FOR SLAVE LINE                          
         BE    FB3                 YES - SLAVE LINE                             
*                                                                               
FB12     MVC   SVBUYKEY,KEY                                                     
         CLI   QBYID,C'Y'          TEST ID SEQ REQUEST                          
         BE    FBID                                                             
         B     EQXIT                                                            
         EJECT                                                                  
NEXTBUY  DS    0H                                                               
         BAS   RE,SETDMBTS                                                      
         OC    SVIDADR,SVIDADR     TEST IN ID SEQ PROCESSING                    
         BNZ   NBID                                                             
         CLC   SVBUYKEY,KEY        TEST SEQUENCE BROKEN                         
         BE    NB1                 N                                            
         MVC   KEY(13),SVBUYKEY    RESTORE SEQ                                  
         GOTO1 HIGH                                                             
*                                                                               
NB1      DS    0H                                                               
         L     RE,=A(SVPKGEL)                                                   
         CLI   0(RE),0             TEST IN LINKED PKG                           
         BNE   NEXTPKG                                                          
         B     NB3                                                              
*                                                                               
NB2      GOTO1 HIGH                                                             
         B     NB4                                                              
*                                                                               
NB3      GOTO1 SEQ                                                              
*                                                                               
NB4      CLC   KEY(9),SVBUYKEY     SAME A-M/CLT/PRD/MKT/STA                     
         BE    NB12                                                             
         OC    SVSTLADR,SVSTLADR   TEST RUNNING IN ALPHA SEQ                    
         BZ    NB10                NO                                           
         CLI   SVBUYKEY+6,X'E8'    TEST CABLE                                   
         BL    NB10                NO                                           
         TM    RQOPTS,RQOPTS_HDEND  TEST BREAK FOR HEADEND ONLY                 
         BZ    NEQXIT               NO                                          
*                                                                               
         L     R1,SVSTLADR          GET CURRENT LIST ENTRY                      
         SR    RE,RE                                                            
         ICM   RE,7,0(R1)                                                       
         SRL   RE,7                                                             
         SR    RF,RF                                                            
         ICM   RF,7,12(R1)                                                      
         SRL   RF,7                                                             
         CR    RE,RF               TEST NEXT IS SAME HEADEND                    
         BNE   NEQXIT              NO                                           
*                                                                               
         MVC   KEY,SVBUYKEY        RESTORE ORIGINAL KEY                         
         B     NSTA12              GO READ NEXT STATION W/O STALAST             
*                                                                               
NB10     CLC   KEY(6),SVBUYKEY     SAME A-M/CLT/PRD/MKT                         
         BNE   NEQXIT                                                           
         CLI   SVBUYKEY+6,X'E8'    TEST CABLE                                   
         BL    NEQXIT               NO                                          
         TM    RQOPTS,RQOPTS_HDEND  TEST BREAK FOR HEADEND ONLY                 
         BZ    NEQXIT               NO                                          
         SR    RE,RE                                                            
         ICM   RE,7,KEY+6                                                       
         SR    RF,RF                                                            
         ICM   RF,7,SVBUYKEY+6                                                  
         SRL   RE,7                                                             
         SRL   RF,7                                                             
         CR    RE,RF               TEST SAME HEADEND                            
         BNE   NEQXIT                                                           
* FILTER ON EST                                                                 
NB12     CLI   BEST,0              TEST 'NO' OPTION                             
         BE    NB16                                                             
         CLC   KEY+9(1),BEST                                                    
         BE    NB20                                                             
         BL    NB14                                                             
         CLC   KEY+9(1),BESTEND                                                 
         BNH   NB20                                                             
         TM    RQOPTS,RQOPTS_HDEND  TEST BREAK FOR HEADEND ONLY                 
         BZ    NEQXIT               NO                                          
         MVC   KEY+9(4),=4X'FF'     FORCE NEXT STATION                          
         B     NB2                                                              
         EJECT                                                                  
* READ FOR EST                                                                  
NB14     MVC   KEY+9(1),BEST                                                    
         XC    KEY+10(3),KEY+10                                                 
         B     NB2                                                              
* 'NO' OPTION                                                                   
NB16     SR    RE,RE                                                            
         IC    RE,KEY+9                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   NB20                                                             
NB18     MVC   KEY+10(3),=4X'FF'   NEXT EST                                     
         B     NB2                                                              
*                                                                               
NB20     DS    0H                                                               
         CLI   RQNOPSSV,C'N'       TEST EXCLUDE PASSIVE POINTERS                
         BE    *+12                NO-USE IT                                    
         CLI   KEY+10,0            ELSE TEST PASSIVE                            
         BNE   NB3                                                              
         GOTO1 GETBUY                                                           
         CLC   QPROG,=C'UB'        UB (BUY EXTRACT UPDATE) REPORT?              
         BNE   NB21A                                                            
         TM    DMCB+8,X'10'        TEST NOT FOUND                               
         BO    NB1                                                              
         B     NB21B                                                            
*                                                                               
NB21A    TM    DMCB+8,X'12'        TEST NOT FOUND                               
         BNZ   NB1                                                              
*                                                                               
NB21B    CLI   FCRDPKG,C'N'        TEST READING LINKED PACKAGES                 
         BE    NB22                NO - PROCESS                                 
         GOTO1 =A(NBSLVTST)        TEST FOR SLAVE LINE                          
         BE    NB3                 RETURN 1 - SLAVE LINE                        
*                                                                               
NB22     MVC   SVBUYKEY,KEY                                                     
         B     EQXIT                                                            
         EJECT                                                                  
*=================================================================              
* BUILD ID LIST AND TABLE OF BUYS                                               
*=================================================================              
                                                                                
IDMAX    EQU   255                 TABLE HOLDS 255 IDS (13 BYTES)               
IDTABDSP EQU   256*13              DISPLACEMENT TO FIRST BUY                    
*                                                                               
FBID     L     RE,BUYLIST                                                       
         L     RF,=A(BUYLISTL)                                                  
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     R4,BUYLIST          R4 POINTS TO NEXT ID SLOT                    
         LA    R5,IDTABDSP(R4)     R5 POINTS TO NEXT ID/EST/LINE SLOT           
*                                                                               
         CLI   0(R4),0             ANY DATA IN THE TABLE?                       
         BE    FBID10              NO - GET 1 IN SO WE DON'T SKIP MKT!          
*                                                                               
FBID2    L     R6,ADBUY                                                         
         LA    R6,24(R6)                                                        
*                                                                               
         OC    SVIDFILT,SVIDFILT   TEST ANY DATE FILTERS                        
         BZ    FBID10                                                           
*                                                                               
FBID4    LLC   R0,1(R6)            FIND A SPOT IN FILTER PERIOD                 
         AR    R6,R0                                                            
         CLI   0(R6),0             IF END OF BUY                                
         BE    FBID30                NO SPOTS IN PERIOD                         
         CLI   0(R6),X'0B'                                                      
         BL    FBID4                                                            
         CLI   0(R6),X'0C'                                                      
         BH    FBID4                                                            
         CLC   2(2,R6),SVIDFILT    SPOT BEFORE START DATE                       
         BL    FBID4                                                            
         CLC   2(2,R6),SVIDFILT+2  SPOT AFTER END DATE                          
         BH    FBID4                                                            
*                                                                               
FBID10   L     R6,ADBUY                                                         
         LA    R6,24(R6)                                                        
*                                                                               
FBID12   LLC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BNE   FBID14                                                           
         LA    R6,=CL15'   **UNKNOWN**'                                         
         CLI   QLANG,C'F'                                                       
         BNE   *+8                                                              
         LA    R6,=CL15'   **INCONNU**'                                         
         B     FBID16                                                           
*                                                                               
FBID14   CLI   0(R6),X'70'                                                      
         BNE   FBID12                                                           
         OC    3(12,R6),SPACES     UPPER CASE AND NULLS TO SPACES               
*                                                                               
FBID16   L     RE,BUYLIST                                                       
         SR    R0,R0                                                            
*                                                                               
FBID20   CR    RE,R4               TEST AT LAST ENTRY                           
         BNL   FBID24              YES - NOT IN LIST                            
         CLC   1(12,RE),3(R6)      ELSE TEST MATCH                              
         BE    FBID26                                                           
         LA    RE,13(RE)                                                        
         BCT   R0,FBID20           COUNT SLOT NUMBER AND LOOP                   
*                                                                               
FBID24   BCTR  R0,0                                                             
         LPR   R0,R0                                                            
         STC   R0,0(RE)            SET ID NUMBER                                
         MVC   1(12,RE),3(R6)                                                   
         LA    R4,13(R4)           AND SET NEW BUFFER END                       
         L     RF,BUYLIST                                                       
         LA    RF,IDMAX*13(RF)                                                  
         CR    R4,RF               TEST PAST END OF BUFFER                      
         BL    *+6                                                              
         DC    H'0'                TOO MANY ID'S                                
*                                                                               
FBID26   MVC   0(1,R5),0(RE)       SET ID NUMBER                                
         MVI   1(R5),0             CLEAR JUST IN CASE                           
         CLI   SVBUYKEY+6,X'E8'    CABLE?                                       
         BL    FBID28              NO                                           
         TM    RQOPTS,RQOPTS_HDEND BREAK FOR HEADEND ONLY?                      
         BZ    FBID28              NO                                           
         MVC   1(1,R5),KEY+8       SAVE LAST BYTE OF STATION                    
*                                                                               
FBID28   MVC   2(4,R5),KEY+9       SAVE EST/INDS/LINE                           
         LA    R5,6(R5)                                                         
         L     RF,BUYLIST                                                       
         A     RF,=A(BUYLISTL)                                                  
         CR    R5,RF                                                            
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
FBID30   GOTO1 FCNXTBUY                                                         
         BE    FBID2                                                            
                                                                                
* END OF STATION - SORT LIST OF ID'S                                            
                                                                                
         AHI   R4,-13              BACK UP TO LAST ENTRY                        
         LLC   R0,0(R4)            AND PICK UP NUMBER                           
         GOTO1 XSORT,DMCB,BUYLIST,(R0),13,12,1                                  
*                                                                               
         L     R5,BUYLIST                                                       
         B     NBID10                                                           
*                                                                               
NBID     CLI   MODE,REREAD                                                      
         BE    NBID14                                                           
*                                                                               
NBID2    L     R5,SVIDADR                                                       
         OC    0(3,R5),0(R5)       TEST E-O-L LAST TIME                         
         BNZ   NBID16              NO                                           
*                                                                               
* REACHED EOL LAST TIME - FIND NEXT ID IN TABLE                                 
*                                                                               
NBID4    L     R5,BUYLIST                                                       
         CLC   SVIDADR(1),0(R5)    MATCH PREVIOUS ID                            
         BE    NBID6                                                            
         LA    R5,13(R5)                                                        
         B     *-14                                                             
*                                                                               
NBID6    XC    SVIDADR,SVIDADR                                                  
         LA    R5,13(R5)           POINT TO NEXT ID                             
         CLI   0(R5),0             TEST NO MORE                                 
         BE    FRSTBUY             NO MORE - RETURN TO NORMAL PROC              
*                                                                               
NBID10   MVC   SVIDADR(1),0(R5)    SET ID NUMBER                                
         MVC   BUYID,1(R5)         SET NAME                                     
*                                                                               
NBID14   L     R5,BUYLIST          START AT TOP OF BUFFER                       
         LA    R5,IDTABDSP(R5)     POINT TO FIRST BUY                           
         B     *+8                                                              
*                                                                               
NBID16   LA    R5,6(R5)                                                         
         OC    0(3,R5),0(R5)       TEST E-O-L                                   
         BNZ   NBID18                                                           
         STCM  R5,7,SVIDADR+1      SAVE ADDRESS OF E-O-L FOR NEXT TIME          
         B     NEQXIT                                                           
*                                                                               
NBID18   CLC   SVIDADR(1),0(R5)    TEST SAME ID                                 
         BNE   NBID16                                                           
         STCM  R5,7,SVIDADR+1      SAVE ADDRESS                                 
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(9),SVBUYKEY                                                  
         CLI   SVBUYKEY+6,X'E8'    CABLE?                                       
         BL    NBID20              NO                                           
         TM    RQOPTS,RQOPTS_HDEND BREAK FOR HEADEND ONLY?                      
         BZ    NBID20              NO                                           
         MVC   KEY+8(1),1(R5)      NETWORK                                      
*                                                                               
NBID20   MVC   KEY+9(4),2(R5)      RESTORE EST/INDS/LINE                        
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVBUYKEY,KEY                                                     
         GOTO1 GETBUY                                                           
                                                                                
* THIS EXIT SAME AS EQXIT FROM FRSTBUY/NEXTBUY                                  
                                                                                
         B     EQXIT               AND PROCESS REC                              
*                                                                               
SETDMBTS MVI   DMOUTBTS,X'ED'      DO NOT TEST NOT FOUND                        
         CLI   RQDEL,C'N'                                                       
         BER   RE                                                               
         MVI   DMINBTS,X'88'                                                    
         BR    RE                                                               
         SPACE 1                                                                
* TEST ACTUAL BUY FOR RIGHT ID IN ID POINTER READ                               
*                                                                               
TSTID    DS    0H                                                               
         L     R6,ADBUY                                                         
         LA    R6,24(R6)                                                        
*                                                                               
TSTID4   LLC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    TSTIDNO                                                          
         CLI   0(R6),X'70'                                                      
         BNE   TSTID4                                                           
         CLC   3(5,R6),MGR3        FULL MGR ALWAYS IN MGR3                      
         BNE   TSTIDNO                                                          
*                                                                               
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
TSTIDNO  LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*=========================================================                      
* FETCH NEXT LINE IN PKG                                                        
*=========================================================                      
                                                                                
NEXTPKG  MVC   KEY,SVBUYKEY        MOVE LAST BUY KEY                            
         MVC   BYTE,KEY                                                         
         NI    BYTE,X'0F'          DROP AGY                                     
         CLI   BYTE,8              TEST MEDIA = CMBD                            
         BE    NPKG1                                                            
*                                                                               
         LA    R2,KEY+12           1-BYTE LINE NUM FOR POL BRND                 
         CLI   KEY+10,X'FF'        TEST FOR IT                                  
         BE    NPKG2                                                            
         LA    R2,KEY+11           LINE NUM FOR POL                             
         B     NPKG2                                                            
                                                                                
* SPECIAL PROCESSING FOR CANADIAN COMBINED *                                    
                                                                                
NPKG1    LA    R2,KEY+12                                                        
         CLI   KEY+11,0                                                         
         BE    NPKG2                                                            
         LA    R2,KEY+11                                                        
*                                                                               
NPKG2    L     R5,=A(SVPKGEL)                                                   
         TM    2(R5),X'10'         TEST 2-BYTE LINE NUMBERS                     
         BO    NPKG10                                                           
*                                                                               
         LA    R5,3(R5)            POINT TO FIRST LINE NUMBER                   
         CLC   SVPKGKEY,KEY        TEST FIRST TIME (HAVE MASTER)                
         BE    NPKG8                                                            
*                                                                               
NPKG4    CLC   0(1,R2),0(R5)                                                    
         BE    NPKG6                                                            
         LA    R5,1(R5)                                                         
         CLI   0(R5),0                                                          
         BNE   NPKG4                                                            
         DC    H'0'                                                             
*                                                                               
NPKG6    LA    R5,1(R5)                                                         
*                                                                               
NPKG8    CLI   0(R5),0             TEST E-O-L                                   
         BE    NPKG22                                                           
*                                                                               
         MVC   0(1,R2),0(R5)       MOVE LINE TO KEY                             
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(12),KEYSAVE                                                  
         BE    NPKG20                                                           
         MVC   KEY(13),KEYSAVE     RESTORE LINE NOT FOUND AND TRY NEXT          
         B     NPKG6                                                            
                                                                                
* THIS CODE FOR 2-BYTE LINE NUMBERS *                                           
                                                                                
NPKG10   LA    R5,3(R5)            FIRST PACKAGE LINE NUMBER                    
         CLC   SVPKGKEY,KEY        TEST FIRST TIME (HAVE MASTER)                
         BE    NPKG16                                                           
*                                                                               
NPKG12   CLC   KEY+11(2),0(R5)                                                  
         BE    NPKG14                                                           
         LA    R5,2(R5)                                                         
         OC    0(2,R5),0(R5)                                                    
         BNE   NPKG12                                                           
         DC    H'0'                                                             
*                                                                               
NPKG14   LA    R5,2(R5)            NEXT LINE NUMBER                             
*                                                                               
NPKG16   OC    0(2,R5),0(R5)       TEST E-O-L                                   
         BZ    NPKG22                                                           
* SET FOR NEXT PKG LINE                                                         
         MVC   KEY+11(2),0(R5)                                                  
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(13),KEYSAVE                                                  
         BE    NPKG20                                                           
         MVC   KEY(13),KEYSAVE     RESTORE LINE NOT FOUND AND TRY NEXT          
         B     NPKG14                                                           
*                                                                               
NPKG20   GOTO1 GETBUY                                                           
*                                                                               
         MVC   SVBUYKEY,KEY                                                     
         B     EQXIT                                                            
*                                                                               
* END OF PKG                                                                    
*                                                                               
NPKG22   MVC   KEY(13),SVPKGKEY    RESTORE MASTER LINE                          
         L     RE,=A(SVPKGEL)                                                   
         XC    0(L'SVPKGEL,RE),0(RE)                                            
         GOTO1 HIGH                                                             
         B     NB3                                                              
         EJECT                                                                  
FRSTGOAL CLI   FCRDGOAL,C'Y'                                                    
         BE    NG1                                                              
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BE    NG1                                                              
         CLI   FCRDGOAL,C'B'       IF BILLS PROCESSED,                          
         BE    EQXIT                EXISTENCE OF MKT IS ENOUGH                  
         CLI   FCRDGOAL,C'P'                                                    
         BE    FRSTPEP                                                          
         CLI   FCRDGOAL,C'A'                                                    
         BE    FRSTCOMG                                                         
         XC    SVGLKEY,SVGLKEY                                                  
         B     NEQXIT                                                           
*                                                                               
NG1      MVC   SVGLKEY,KEY                                                      
*                                                                               
NG2      GOTO1 HIGH                                                             
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BE    NG4                                                              
         CLI   KEY+4,X'FF'         TEST CPP GUIDE                               
         BE    NEQXIT                                                           
         B     NG4                                                              
*                                                                               
NEXTGOAL DS    0H                                                               
         CLI   FCRDGOAL,C'A'       DOING SPOT AND NETWORK                       
         BE    NEXTCOMG                                                         
         CLI   FCRDGOAL,C'B'       IF BILLS PROCESSED                           
         BE    NEQXIT               FORCE MKTLAST                               
         CLI   FCRDGOAL,C'P'                                                    
         BE    NEXTPEP                                                          
         CLC   SVGLKEY,KEY         TEST SEQUENCE BROKEN                         
         BE    NG3                 NO                                           
         MVC   KEY(13),SVGLKEY     RESTORE SEQ                                  
         GOTO1 HIGH                                                             
*                                                                               
NG3      GOTO1 SEQ                                                              
*                                                                               
NG4      CLC   KEY(7),SVGLKEY      02/A-M/CLT/PRD/MKT                           
         BNE   NG20                                                             
* FILTER ON EST                                                                 
         CLI   BEST,0                                                           
         BE    NG8                                                              
         CLC   KEY+7(1),BEST                                                    
         BE    NG10                                                             
         BL    NG6                                                              
         CLC   KEY+7(1),BESTEND                                                 
         BNH   NG10                                                             
         B     NG20                                                             
* READ FOR EST                                                                  
NG6      MVC   KEY+7(1),BEST                                                    
         XC    KEY+8(5),KEY+8                                                   
         B     NG2                                                              
* 'NO' OPTION                                                                   
NG8      SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   NG10                                                             
         MVC   KEY+8(4),=4X'FF'    NEXT EST                                     
         B     NG2                                                              
*                                                                               
NG10     DS    0H                                                               
         GOTO1 GETGOAL                                                          
*                                                                               
         MVC   SVGLKEY,KEY                                                      
         B     EQXIT                                                            
*                                                                               
NG20     CLI   SVPRDCD,X'FF'       TEST POL PROCESSING                          
         BNE   NEQXIT                                                           
*                                                                               
         CLI   FCRDGOAL,C'C'       TEST CPP PROCESSING                          
         BE    NEQXIT                                                           
*                                                                               
         CLC   KEY(4),SVGLKEY      02/A-M/CLT                                   
         BNE   NEQXIT                                                           
* READ FOR NEXT PRD                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(5),SVGLKEY      02/A-M/CLT/PRD                               
         IC    RE,KEY+4            GET PRD CD                                   
         LA    RE,1(RE)                                                         
         STC   RE,KEY+4                                                         
NG21     DS    0H                                                               
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(4),KEYSAVE      02/A-M/CLT                                   
         BNE   NEQXIT                                                           
*                                                                               
         CLI   KEY+4,X'FF'         TEST CPP GUIDE                               
         BE    NEQXIT                                                           
*                                                                               
         LA    R1,KEY+4                                                         
         BAS   RE,POLPGR                                                        
         BE    NG22                                                             
         MVC   KEY+5(4),=4X'FF'                                                 
         B     NG21                                                             
*                                                                               
NG22     MVC   KEY+5(2),SVMKT      MOVE MARKET                                  
         MVC   KEY+7(1),BEST       AND FIRST ESTIMATE (OR ZERO)                 
         XC    KEY+8(5),KEY+8                                                   
         B     FRSTGOAL                                                         
         EJECT                                                                  
FRSTCOMG CLI   FCRDGOAL,C'A'                                                    
         BE    NCG1                                                             
         XC    SVGLKEY,SVGLKEY                                                  
         B     NEQXIT                                                           
*                                                                               
NCG1     MVC   SVGLKEY,KEY                                                      
         TM    SVGLKEY+1,X'08'                                                  
         BZ    *+8                                                              
         NI    SVGLKEY+1,X'F0'                                                  
         OI    SVGLKEY+1,X'01'                                                  
         MVC   KEY,SVGLKEY                                                      
*                                                                               
NCG2     GOTO1 HIGH                                                             
         CLI   KEY+4,X'FF'         TEST CPP GUIDE                               
         BE    NCG24                                                            
         B     NCG4                                                             
*                                                                               
NEXTCOMG DS    0H                                                               
         CLC   SVGLKEY,KEY         TEST SEQUENCE BROKEN                         
         BE    NCG3                NO                                           
         MVC   KEY(13),SVGLKEY     RESTORE SEQ                                  
         GOTO1 HIGH                                                             
*                                                                               
NCG3     GOTO1 SEQ                                                              
*                                                                               
NCG4     CLC   KEY(7),SVGLKEY      02/A-M/CLT/PRD/MKT                           
         BNE   NCG20                                                            
* FILTER ON EST                                                                 
         CLI   BEST,0                                                           
         BE    NCG8                                                             
         CLC   KEY+7(1),BEST                                                    
         BE    NCG10                                                            
         BL    NCG6                                                             
         CLC   KEY+7(1),BESTEND                                                 
         BNH   NCG10                                                            
         B     NCG20                                                            
* READ FOR EST                                                                  
NCG6     MVC   KEY+7(1),BEST                                                    
         XC    KEY+8(5),KEY+8                                                   
         B     NCG2                                                             
* 'NO' OPTION                                                                   
NCG8     SR    RE,RE                                                            
         IC    RE,KEY+7                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   NCG10                                                            
         MVC   KEY+8(4),=4X'FF'    NEXT EST                                     
         B     NCG2                                                             
*                                                                               
NCG10    DS    0H                                                               
         GOTO1 GETGOAL                                                          
*                                                                               
         MVC   SVGLKEY,KEY                                                      
         B     EQXIT                                                            
*                                                                               
NCG20    CLI   SVPRDCD,X'FF'       TEST POL PROCESSING                          
         BNE   NCG24                                                            
*                                                                               
         CLC   KEY(4),SVGLKEY      02/A-M/CLT                                   
         BNE   NCG24                                                            
* READ FOR NEXT PRD                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(5),SVGLKEY      02/A-M/CLT/PRD                               
         IC    RE,KEY+4            GET PRD CD                                   
         LA    RE,1(RE)                                                         
         STC   RE,KEY+4                                                         
NCG21    DS    0H                                                               
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(4),KEYSAVE      02/A-M/CLT                                   
         BNE   NCG24                                                            
*                                                                               
         CLI   KEY+4,X'FF'         TEST CPP GUIDE                               
         BE    NCG24                                                            
*                                                                               
         LA    R1,KEY+4                                                         
         BAS   RE,POLPGR                                                        
         BE    NCG22                                                            
         MVC   KEY+5(4),=4X'FF'                                                 
         B     NCG21                                                            
*                                                                               
NCG22    MVC   KEY+5(2),SVMKT      MOVE MARKET                                  
         MVC   KEY+7(1),BEST       AND FIRST ESTIMATE (OR ZERO)                 
         XC    KEY+8(5),KEY+8                                                   
         B     FRSTCOMG                                                         
         SPACE 2                                                                
NCG24    TM    SVGLKEY+1,X'02'     DOING NETWORK                                
         BO    NCG26                                                            
         NI    SVGLKEY+1,X'F0'     NO - SET IT UP                               
         OI    SVGLKEY+1,X'03'                                                  
         XC    KEY,KEY                                                          
         CLI   SVPRDCD,X'FF'                                                    
         BNE   *+8                                                              
         MVI   SVGLKEY+4,1                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(7),SVGLKEY                                                   
         B     NCG2                                                             
         SPACE 2                                                                
NCG26    NI    SVGLKEY+1,X'F0'                                                  
         OI    SVGLKEY+1,X'08'                                                  
         B     NEQXIT                                                           
         EJECT                                                                  
FRSTPEP  MVC   SVGLKEY,KEY                                                      
*                                                                               
NP2      GOTO1 HIGH                                                             
         B     NP4                                                              
*                                                                               
NEXTPEP  DS    0H                                                               
         CLC   SVGLKEY,KEY         TEST SEQUENCE BROKEN                         
         BE    NP3                                                              
         MVC   KEY,SVGLKEY                                                      
         GOTO1 HIGH                                                             
*                                                                               
NP3      GOTO1 SEQ                                                              
*                                                                               
NP4      CLC   KEY(8),SVGLKEY      0D15/A-M/CL/PR/MKT                           
         BNE   NP20                                                             
* FILTER ON EST                                                                 
         CLI   BEST,0                                                           
         BE    NP8                                                              
         CLC   KEY+8(1),BEST                                                    
         BE    NP10                                                             
         BL    NP6                                                              
         CLC   KEY+8(1),BESTEND                                                 
         BNH   NP10                                                             
         B     NP20                                                             
* READ FOR EST                                                                  
NP6      MVC   KEY+8(1),BEST                                                    
         XC    KEY+9(4),KEY+9                                                   
         B     NP2                                                              
* 'NO' OPTION                                                                   
NP8      LLC   RE,KEY+8                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   NP10                                                             
         MVC   KEY+9(4),=4X'FF'    NEXT EST                                     
         B     NP2                                                              
*                                                                               
NP10     DS    0H                                                               
         GOTO1 GETGOAL             READ REC INTO GOALREC                        
*                                                                               
         MVC   SVGLKEY,KEY                                                      
         B     EQXIT                                                            
         EJECT                                                                  
NP20     CLI   SVPRDCD,X'FF'                                                    
         BNE   NEQXIT                                                           
*                                                                               
         CLC   KEY(5),SVGLKEY      0D15/A-M/CLT                                 
         BNE   NEQXIT                                                           
* READ FOR NEXT PRD                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(6),SVGLKEY      0D15/A-M/CLT/PRD                             
         CLI   KEY+5,X'FF'         TEST POL -- IGNORE                           
         BE    NEQXIT                                                           
         IC    RE,KEY+5                                                         
         LA    RE,1(RE)                                                         
         STC   RE,KEY+5                                                         
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(5),KEYSAVE      0D15/A-M/CLT                                 
         BNE   NEQXIT                                                           
*                                                                               
         LA    R1,KEY+5                                                         
         BAS   RE,POLPGR                                                        
         BE    NP22                                                             
         MVC   KEY+6(4),=4X'FF'                                                 
         B     NP20                                                             
*                                                                               
NP22     MVC   KEY+6(2),SVMKT                                                   
         MVC   KEY+8(1),BEST                                                    
         XC    KEY+9(4),KEY+9                                                   
         B     FRSTPEP                                                          
         EJECT                                                                  
*                   ROUTINE TO GO TO APPLICATION PROGRAMS                       
         SPACE 2                                                                
FCGO     DS    0H                                                               
         MVC   SVMODE,MODE                                                      
         CLI   RCTRACE,C'Y'                                                     
         BNE   GO2                                                              
         MVC   P,SPACES                                                         
         MVC   P(10),=C'MODE=X''  '''                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,DMCB,MODE,P+7,1,=C'N'                                     
         BAS   RE,GETMODE                                                       
         GOTO1 REPORT                                                           
         SPACE 2                                                                
GO2      EQU   *                                                                
         GOTO1 APPLIC,DMCB,WORKC                                                
         SPACE 2                                                                
         B     EQXIT                                                            
         SPACE 2                                                                
NEXTEL   CLI   0(R6),0                                                          
         JE    NEXTELX                                                          
         SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         JNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         CLC   BYTE(1),0(R6)                                                    
         BER   RE                                                               
         J     NEXTEL                                                           
NEXTELX  LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
* ROUTINE TO EXPAND MODE NAME                                                   
*                                                                               
GETMODE  MVC   P+20(8),=C'UNKNOWN '                                             
         L     RF,=A(MODETAB)                                                   
*                                                                               
GETMODE2 CLC   MODE,0(RF)                                                       
         BE    GETMODE4                                                         
         CLI   0(RF),X'FF'                                                      
         BCR   8,RE                                                             
         LA    RF,9(RF)                                                         
         B     GETMODE2                                                         
*                                                                               
GETMODE4 MVC   MODEALPH,1(RF)                                                   
         MVC   P+20(8),1(RF)                                                    
         BR    RE                                                               
         LTORG                                                                  
*                                                                               
MEDLIST  DC    C'RTNX'       SPOT MEDIA LIST                                    
         DC    X'FF'         END OF LIST                                        
*                                                                               
         EJECT                                                                  
MODETAB  DS    0C                                                               
         DC    AL1(RUNFRST),CL8'RUNFRST '                                       
         DC    AL1(REQFRST),CL8'REQFRST '                                       
         DC    AL1(OFCFRST),CL8'OFCFRST '                                       
         DC    AL1(CLTFRST),CL8'CLTFRST '                                       
         DC    AL1(PGR1FRST),CL8'PGR1FRST'                                      
         DC    AL1(PGR2FRST),CL8'PGR2FRST'                                      
         DC    AL1(PGR3FRST),CL8'PGR3FRST'                                      
         DC    AL1(PRDFRST),CL8'PRDFRST '                                       
         DC    AL1(ESTTEST),CL8'ESTTEST '                                       
         DC    AL1(ESTFRST),CL8'ESTFRST '                                       
         DC    AL1(MGR1FRST),CL8'MGR1FRST'                                      
         DC    AL1(MGR2FRST),CL8'MGR2FRST'                                      
         DC    AL1(MGR3FRST),CL8'MGR3FRST'                                      
         DC    AL1(MKTFRST),CL8'MKTFRST '                                       
         DC    AL1(STAFRST),CL8'STAFRST '                                       
*                                                                               
         DC    AL1(PROCBUY),CL8'PROCBUY '                                       
         DC    AL1(PROCGOAL),CL8'PROCGOAL'                                      
         DC    AL1(PROCBILL),CL8'PROCBILL'                                      
         DC    AL1(PROCEST),CL8'PROCEST'                                        
         DC    AL1(PROCREC),CL8'PROCREC '                                       
         DC    AL1(REREAD),CL8'REREAD'                                          
*                                                                               
         DC    AL1(STALAST),CL8'STALAST '                                       
         DC    AL1(MKTLAST),CL8'MKTLAST '                                       
         DC    AL1(MGR3LAST),CL8'MGR3LAST'                                      
         DC    AL1(MGR2LAST),CL8'MGR2LAST'                                      
         DC    AL1(MGR1LAST),CL8'MGR1LAST'                                      
         DC    AL1(ESTLAST),CL8'ESTLAST '                                       
         DC    AL1(PRDLAST),CL8'PRDLAST '                                       
         DC    AL1(PGR3LAST),CL8'PGR3LAST'                                      
         DC    AL1(PGR2LAST),CL8'PGR2LAST'                                      
         DC    AL1(PGR1LAST),CL8'PGR1LAST'                                      
         DC    AL1(CLTLAST),CL8'CLTLAST '                                       
         DC    AL1(OFCLAST),CL8'OFCLAST '                                       
         DC    AL1(REQLAST),CL8'REQLAST '                                       
         DC    AL1(RUNLAST),CL8'RUNLAST '                                       
*                                                                               
         DC    AL1(DISKERR),CL8'DISKERR '                                       
         DC    AL1(GOTONXTR),CL8'GOTONXTR'                                      
*                                                                               
         DC    X'FF'                                                            
         LTORG                                                                  
*                                                                               
         DROP  RB,R9,RC                                                         
         EJECT                                                                  
         ENTRY ENDREQ                                                           
ENDREQ   DS    0H                                                               
         USING *,RF                                                             
         L     RA,=V(SPWORKC)                                                   
         LA    R8,2048(RA)                                                      
         LA    R8,2048(R8)                                                      
         USING SPWORKD,RA,R8                                                    
*                                                                               
         L     RD,BASERD                                                        
         LM    R0,RC,20(RD)        DONT CLOBBER RF - IT'S THE BASE REG          
         L     RE,=A(FC30)         AND WE USE RE RIGHT HERE                     
         BR    RE                  NOW BACK TO FILCON WITH ANY LUCK             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
******************************************************************              
* BUILD LIST OF ALL CLIENTS IN AN OFFICE LIST AND SORT BY OFFICE *              
******************************************************************              
         SPACE 1                                                                
BLDOFC   NMOD1 0,**BLDOFC                                                       
         L     R0,=V(OFCLIST)                                                   
         L     R1,=V(OFCLISTX)                                                  
         SR    R1,R0               R1=LEN OF LSIT                               
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         XC    SVOFCLST,SVOFCLST   CLEAR SAVED OFFICE LIST                      
*                                                                               
         L     R4,=V(OFCLIST)                                                   
         SR    R5,R5               CLEAR COUNTER                                
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAGYMD                                                 
*                                                                               
BLDOFC2  DS    0H                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(2),KEYSAVE      SAME A-M                                     
         BNE   BLDOFC10                                                         
         GOTO1 GETCLT                                                           
*                                                                               
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
*                                                                               
         XC    WORK,WORK                                                        
OFFD     USING OFFICED,WORK                                                     
         MVI   OFFD.OFCSYS,C'S'                                                 
*&&DO                                                                           
*=============================================================                  
*=============================================================                  
* THIS CODE IS WAITING UNTIL NET PROFILES HAVE BEEN DEFINED !!                  
*=============================================================                  
*=============================================================                  
         CLI   QMED,C'N'           TEST NETWORK                                 
         BNE   *+8                                                              
         MVI   OFFD.OFCSYS,C'N'                                                 
*&&                                                                             
         CLC   QCLT(2),=C'$*'      IF DOING ALL OFFICES                         
         BE    BLDOFC4             SKIP LIMIT ACCESS                            
         MVC   OFFD.OFCAUTH,QCLT                                                
         MVC   OFFD.OFCLMT(3),QCLT                                              
*                                                                               
BLDOFC4  MVC   OFFD.OFCAGY,SVAGY                                                
         MVC   OFFD.OFCOFC,COFFICE                                              
*                                                                               
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         L     RF,VOFFICER                                                      
         DROP  RE                                                               
         GOTO1 (RF),DMCB,(C'2',WORK),ACOMFACS                                   
         CLI   0(R1),0             TEST INCLUDE THIS CLIENT                     
         BNE   BLDOFC8             NO                                           
*                                                                               
BLDOFC6  MVC   0(2,R4),OFFD.OFCOFC2   MOVE 2CHAR OFFICE CODE                    
         OC    0(2,R4),0(R4)       MAKE SURE NOT X'0000'                        
         BNZ   *+10                                                             
         MVC   0(2,R4),SPACES                                                   
         MVC   2(2,R4),CKEY+2         AND CLIENT CODE                           
         AHI   R4,4                                                             
         C     R4,=V(OFCLISTX)                                                  
         BL    *+6                                                              
         DC    H'0'                NEED TO EXPAND CLIENT LIST                   
         BCTR  R5,0                BUMP COUNTER                                 
*                                                                               
BLDOFC8  ICM   RE,3,KEY+2                                                       
         AHI   RE,1                FORCE NEXT CLIENT CODE                       
         STCM  RE,3,KEY+2                                                       
         B     BLDOFC2                                                          
*                                                                               
BLDOFC10 LPR   R5,R5               SORT CLIENT WITHIN OFFICE                    
         BZ    BLDOFCX                                                          
         TM    RCOPTS,RCOPT_OFCSORT   TEST DO NOT SORT                          
         BO    BLDOFCX                                                          
         L     R4,=V(OFCLIST)                                                   
         GOTO1 XSORT,DMCB,(R4),(R5),4,4,0                                       
BLDOFCX  XIT1                                                                   
         DROP  OFFD                                                             
         LTORG                                                                  
         EJECT                                                                  
*=========================================================*                     
* SUBROUTINE TO READ BUYER/BILLER NAME FROM STATUS RECORD *                     
*=========================================================*                     
         SPACE 1                                                                
GETNAME  NMOD1 0,*GETNAME                                                       
*                                                                               
GETNAM2  XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING STATKEY,R6                                                       
*                                                                               
         MVC   STKTYPE,=X'0D71'                                                 
         MVC   STKAGMD,SVAGYMD                                                  
         MVC   STKCLT,BCLT                                                      
         MVC   STKPRD,BPRD                                                      
         MVC   STKEST,BEST                                                      
         MVC   STKMKT,SVMKT                                                     
*                                                                               
         CLC   KEY(5),SVSTKEY      DO WE HAVE THE RIGHT PROFILE                 
         BE    GETNAM4                                                          
         MVC   SVSTKEY,KEY         SAVE TYPE/A-M/CLT                            
* GET THE ST PROFILE NOW                                                        
         XC    KEY,KEY                                                          
         MVC   KEY(4),=C'S0ST'                                                  
         MVC   KEY+4(2),QAGY                                                    
         MVC   KEY+6(1),QMED                                                    
         MVC   KEY+7(3),QCLT                                                    
         MVI   KEY+10,C'*'                                                      
         L     RE,ADCLT                                                         
         MVC   KEY+11(1),COFFICE-CLTHDR(RE)                                     
         XC    SVSTPROF,SVSTPROF    CLEAR IN CASE NOT FOUND                     
         MVC   SVSTPROF(3),=C'CME'  SET DEFAULT TO CLT/MKT/EST                  
*                                                                               
         GOTO1 GETPROF,DMCB,KEY,SVSTPROF,DATAMGR                                
         B     GETNAM2             GO BUILD KEY AGAIN                           
*                                                                               
GETNAM4  CLI   SVSTPROF+0,C'P'     TEST DATA BY PRODUCT                         
         BE    *+8                                                              
         MVI   STKPRD,0                                                         
*                                                                               
         CLI   SVSTPROF+2,C'E'     TEST DATA BY ESTIMATE                        
         BE    *+8                                                              
         MVI   STKEST,0                                                         
*                                                                               
         CLC   BEST,STKEST         TEST AT SAME ESTIMATE LEVELS                 
         BE    GETNAM6             IF YES, GET NAME                             
         CLI   BEST,0              TEST REQUEST ACROSS ESTIMATES                
         BE    GETNAMX             YES - CAN'T GIVE THEM A NAME                 
*                                                                               
GETNAM6  DS    0H                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GETNAMX                                                          
*                                                                               
         L     R6,ADBUY                                                         
         ST    R6,AREC                                                          
         GOTO1 GET                 READ INTO BUYREC AREA                        
*                                                                               
         USING STATD,R6                                                         
         CLI   BPBUYER,C' '                                                     
         BNH   *+10                                                             
         MVC   SVBUYNAM,BPBUYER                                                 
         CLI   BPPAYER,C' '                                                     
         BNH   *+10                                                             
         MVC   SVBILNAM,BPPAYER                                                 
         DROP  R6                                                               
*                                                                               
GETNAMX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
SVSTKEY  DC    XL5'00'             TYPE/A-M/CLT                                 
SVSTPROF DC    CL16'0'                                                          
         LTORG                                                                  
         EJECT                                                                  
*=======================================================*                       
* SUBROUTINE TO FIND NEXT CLIENT GROUP (SPOT)           *                       
*=======================================================*                       
         SPACE 1                                                                
         DS    0H                                                               
NEXTCGR  NMOD1 0,**NCGR**                                                       
         CLI   QMED,C'N'           TEST NETWORK                                 
         BNE   NCGR2                                                            
         CLI   COUNTRY,C'C'        TEST CANADIAN                                
         BNE   NCGN                NO - NETPAK                                  
*                                                                               
NCGR2    OC    SVCGRKEY,SVCGRKEY   TEST FIRST TIME                              
         BNZ   NCGR40              NO                                           
* READ AND PROCESS GROUP DEFINITION RECORD                                      
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING GRPRECD,R6                                                       
*                                                                               
         MVI   GRPKTYP,GRPKTYPQ                                                 
         MVI   GRPKSTYP,GRPKCTYQ                                                
         MVC   GRPKAGMD,SVAGYMD                                                 
         MVC   GRPKID,QCLGID                                                    
         GOTO1 HIGH                                                             
         DROP  R6                                                               
*                                                                               
         CLC   KEY(4),KEYSAVE      SAME TYPE/A-M/ID                             
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R6,ADCLTGRP                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
* EXTRACT LENGTHS/TITLES                                                        
         LA    R6,24(R6)                                                        
         MVI   BYTE,X'10'                                                       
         BAS   RE,NCGNXT2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING GRPBRKD,R6                                                       
NCGR4    XC    CGR1(42),CGR1                                                    
         XC    CGR2(42),CGR2                                                    
         MVC   CGR1BK,GRPBK1                                                    
         MVC   CGR2BK,GRPBK2                                                    
         LLC   RE,GRPBK1LN                                                      
         STC   RE,CGR1LEN                                                       
         LLC   R0,GRPBK2LN                                                      
         AR    RE,R0                                                            
         STC   RE,CGR2LEN                                                       
         DROP  R6                                                               
         EJECT                                                                  
         LA    R6,KEY                                                           
         USING GRPRECD,R6                                                       
*                                                                               
         CLC   =C'ALL',QCLGRP                                                   
         BE    NCGR8                                                            
         MVC   FULL,QCLGRP                                                      
         LA    R1,FULL                                                          
         LA    R0,4                                                             
*                                                                               
NCGR6    CLI   0(R1),C' '                                                       
         BE    *+12                                                             
         CLI   0(R1),C'*'                                                       
         BNE   *+8                                                              
         MVI   0(R1),0                                                          
         LA    R1,1(R1)                                                         
         BCT   R0,NCGR6                                                         
*                                                                               
         PACK  DUB,FULL(5)         PACK 1 EXTRA CHARACTER                       
         MVC   GRPKCODE,DUB+5                                                   
*                                                                               
NCGR8    OC    GRPKCODE,GRPKCODE                                                
         BNZ   *+12                                                             
         LA    R0,1                                                             
         STCM  R0,3,GRPKCODE                                                    
*                                                                               
NCGR10   DS    0H                                                               
         GOTO1 HIGH                                                             
*                                                                               
NCGR20   CLC   KEY(4),KEYSAVE      TEST SAME SCHEME                             
         BNE   NCGRNEQ                                                          
         EJECT                                                                  
NCGR22   CLC   =C'ALL',QCLGRP                                                   
         BE    NCGR30                                                           
* CALCULATE NUMBER OF DIGITS FOR COMPARE                                        
         LA    RE,QCLGRP+3                                                      
         LA    RF,3                                                             
NCGR24   CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         CLI   0(RE),C'*'                                                       
         BNE   NCGR26                                                           
         BCTR  RE,0                                                             
         BCT   RF,NCGR24                                                        
* RF IS SET FOR EX ON GROUPS                                                    
NCGR26   UNPK  DUB,GRPKCODE(3)     DUB HAS X'000000F1F2F3F4XX'                  
         EX    RF,*+8                         0 1 2 3 4 5 6 7                   
         B     *+10                                                             
         CLC   QCLGRP(0),DUB+3 *EXECUTED*                                       
         BNE   NCGRNEQ                                                          
         DROP  R6                                                               
         EJECT                                                                  
* PROCESS THIS GROUP *                                                          
         SPACE 1                                                                
NCGR30   XC    SVCGRKEY,SVCGRKEY                                                
         MVC   SVCGRKEY(6),KEY     SAVE TYPE/A-M/ID/CODE                        
         L     R6,ADCLTGRP                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         LA    R6,24(R6)                                                        
         MVI   BYTE,X'20'                                                       
         BAS   RE,NCGNXT2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
* MOVE GROUP CODES AND NAMES FOR USER                                           
         USING GRPGRPD,R6                                                       
         MVC   CGR1(1),QCLGID                                                   
         MVC   CGR1NM,GRPGNAM1                                                  
         MVC   CGR2(1),QCLGID                                                   
         MVC   CGR2NM,GRPGNAM2                                                  
*                                                                               
         L     RE,ADCLTGRP         POINT TO START OF RECORD                     
         UNPK  DUB,GRPKCODE-GRPKEY(3,RE)                                        
         LLC   RE,CGR1LEN          GET BREAK 1 LEN                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CGR1+1(0),DUB+3                                                  
         LLC   RE,CGR2LEN          GET BREAK 2 LEN                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CGR2+1(0),DUB+3                                                  
         B     NCGR42                                                           
         DROP  R6                                                               
         EJECT                                                                  
*===========================================================*                   
* NOT FIRST TIME - ADVANCE TO NEXT CLIENT OR NEXT GROUP     *                   
* SVCGRKEY HAS 0D 04 AM ID CD CD CL CL CL                   *                   
*               0  1  2  3  4  5  6  7  8                   *                   
*===========================================================*                   
         SPACE 1                                                                
NCGR40   L     R6,ADCLTGRP         ADVANCE TO PREVIOUS ELEMENT                  
         LA    R6,24(R6)                                                        
         MVI   BYTE,X'30'                                                       
         BAS   RE,NCGNXT2                                                       
         B     NCGR44                                                           
*                                                                               
NCGR42   MVI   BYTE,X'30'                                                       
         BAS   RE,NCGNXTEL                                                      
NCGR44   BNE   NCGR50                                                           
*                                                                               
         CLC   SVCGRKEY+6(3),2(R6)  SAVED CODE TO ELEMENT                       
         BNL   NCGR42               CONTINUE IF SAVED LOW OR EQ                 
         MVC   SVCGRKEY+6(3),2(R6)  SAVE CURRENT ELEMENT VALUE                  
         GOTO1 CLPACK,DMCB,2(R6),SVCLT                                          
*                                                                               
         CR    RB,RB               SET CC EQ                                    
         B     NCGRX                                                            
*                                                                               
NCGRNEQ  LTR   RB,RB                                                            
*                                                                               
NCGRX    XIT1                                                                   
*                                                                               
NCGR50   LA    R6,KEY              POINT FOR LATER DSECT USAGE                  
         XC    KEY,KEY             READ FOR NEXT GROUP RECORD                   
         L     RE,ADCLTGRP                                                      
         MVC   KEY(13),0(RE)                                                    
         GOTO1 HIGH                                                             
         GOTO1 SEQ                                                              
         CLC   KEY(6),KEYSAVE      SAME 0D04/AM/GRP/GRPID                       
         BNE   NCGR20              NO - TEST TO GO ON                           
* GET RECORD AND CONTINUE PROCESSING THIS GROUP                                 
         L     R6,ADCLTGRP                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
         B     NCGR40                                                           
         EJECT                                                                  
*=======================================================*                       
* SUBROUTINE TO FIND NEXT CLIENT GROUP (NET)            *                       
*=======================================================*                       
         SPACE 1                                                                
NCGN     DS    0H                                                               
         OC    SVCGRKEY,SVCGRKEY   TEST FIRST TIME                              
         BNZ   NCGN10              NO                                           
* READ AND PROCESS GROUP DEFINITION RECORD                                      
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CLGRECD,R6                                                       
*                                                                               
         MVC   CLGKTYP,=X'0D06'                                                 
         MVC   CLGKAGMD,SVAGYMD                                                 
         MVC   CLGKID,QCLGID                                                    
         GOTO1 HIGH                                                             
         DROP  R6                                                               
*                                                                               
         CLC   KEY(4),KEYSAVE      SAME TYPE/A-M/ID                             
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R6,ADCLTGRP                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
* EXTRACT LENGTHS/TITLES                                                        
         LA    R6,24(R6)                                                        
         MVI   BYTE,X'10'                                                       
         BAS   RE,NCGNXT2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING GRPBRKD,R6                                                       
NCGN4    XC    CGR1(42),CGR1                                                    
         XC    CGR2(42),CGR2                                                    
         MVC   CGR1BK,GRPBK1                                                    
         MVC   CGR2BK,GRPBK2                                                    
         LLC   RE,GRPBK1LN                                                      
         STC   RE,CGR1LEN                                                       
         LLC   R0,GRPBK2LN                                                      
         AR    RE,R0                                                            
         STC   RE,CGR2LEN                                                       
         DROP  R6                                                               
         EJECT                                                                  
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CLGRECD,R6                                                       
         MVC   CLGCTYP,=X'0D86'                                                 
         MVC   CLGCAGMD,SVAGYMD                                                 
         MVC   CLGCID,QCLGID                                                    
*                                                                               
         CLC   =C'ALL',QCLGRP                                                   
         BE    NCGN8                                                            
         MVC   FULL,QCLGRP                                                      
         LA    R1,FULL                                                          
         LA    R0,4                                                             
*                                                                               
NCGN6    CLI   0(R1),C' '                                                       
         BE    *+12                                                             
         CLI   0(R1),C'*'                                                       
         BNE   *+8                                                              
         MVI   0(R1),0                                                          
         LA    R1,1(R1)                                                         
         BCT   R0,NCGN6                                                         
*                                                                               
         PACK  DUB,FULL(5)         PACK 1 EXTRA CHARACTER                       
         MVC   CLGCGRP,DUB+5                                                    
*                                                                               
NCGN8    OC    CLGCGRP,CLGCGRP                                                  
         BNZ   *+12                                                             
         LA    R0,1                                                             
         STCM  R0,3,CLGCGRP                                                     
         B     NCGN12                                                           
*                                                                               
NCGN10   XC    KEY,KEY             READ NEXT CLIENT IN GROUP                    
         LA    R6,KEY                                                           
         USING CLGRECD,R6                                                       
         MVC   KEY,SVCGRKEY                                                     
         MVI   KEY+12,X'FF'        FORCE SEQ                                    
*                                                                               
NCGN12   DS    0H                                                               
         GOTO1 HIGH                                                             
         MVC   SVCGRKEY,KEY        SAVE NEW KEY                                 
         MVC   SVCLT,CLGCCLT       MOVE CLIENT CODE                             
*                                                                               
         CLC   KEY(8),KEYSAVE      SAME TY/A-M/ID/GR                            
         BE    NCGNEQX                                                          
         SPACE 2                                                                
NCGN20   CLC   KEY(6),KEYSAVE      TEST SAME SCHEME                             
         BNE   NCGNNEQ             NO - FINISHED                                
         SPACE 2                                                                
NCGN22   CLC   =C'ALL',QCLGRP                                                   
         BE    NCGN30                                                           
* CALCULATE NUMBER OF DIGITS FOR COMPARE                                        
         LA    RE,QCLGRP+3                                                      
         LA    RF,3                                                             
NCGN24   CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         CLI   0(RE),C'*'                                                       
         BNE   NCGN26                                                           
         BCTR  RE,0                                                             
         BCT   RF,NCGN24                                                        
* RF IS SET FOR EX ON GROUPS                                                    
NCGN26   UNPK  DUB,CLGCGRP(3)      DUB HAS X'000000F1F2F3F4XX'                  
         EX    RF,*+8                         0 1 2 3 4 5 6 7                   
         B     *+10                                                             
         CLC   QCLGRP(0),DUB+3 *EXECUTED*                                       
         BNE   NCGNNEQ                                                          
         DROP  R6                                                               
         EJECT                                                                  
* PROCESS THIS GROUP *                                                          
         SPACE 1                                                                
NCGN30   XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CLGRECD,R6                                                       
         MVC   CLGKTYP,=X'0D06'    SET TO READ GROUP RECORD                     
         MVC   CLGKAGMD,SVAGYMD                                                 
         MVC   CLGKID(3),SVCGRKEY+(CLGCID-CLGKEY)                               
         GOTO1 HIGH                                                             
         CLC   KEY(6),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R6,ADCLTGRP                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         LA    R6,24(R6)                                                        
         MVI   BYTE,X'20'                                                       
         BAS   RE,NCGNXT2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
* MOVE GROUP CODES AND NAMES FOR USER                                           
         USING GRPGRPD,R6                                                       
         MVC   CGR1(1),QCLGID                                                   
         MVC   CGR1NM,GRPGNAM1                                                  
         MVC   CGR2(1),QCLGID                                                   
         MVC   CGR2NM,GRPGNAM2                                                  
*                                                                               
         L     RE,ADCLTGRP         POINT TO START OF RECORD                     
         UNPK  DUB,GRPKCODE-GRPKEY(3,RE)                                        
         LLC   RE,CGR1LEN          GET BREAK 1 LEN                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CGR1+1(0),DUB+3                                                  
         LLC   RE,CGR2LEN          GET BREAK 2 LEN                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CGR2+1(0),DUB+3                                                  
         DROP  R6                                                               
*                                                                               
NCGNEQX  CR    RB,RB               SET CC EQ                                    
         B     NCGNX                                                            
*                                                                               
NCGNNEQ  LTR   RB,RB                                                            
*                                                                               
NCGNX    XIT1                                                                   
         SPACE 2                                                                
NCGNXTEL CLI   0(R6),0                                                          
         BE    NCGNXTX                                                          
         SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
NCGNXT2  CLC   BYTE(1),0(R6)                                                    
         BER   RE                                                               
         B     NCGNXTEL                                                         
NCGNXTX  LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO BUILD MKTGRP LIST - MOVED HERE DUE TO BASE REG PROBLEMS *          
**********************************************************************          
         SPACE 1                                                                
         DC    0H'0'                                                            
NMGR     NMOD1 0,**NMGR**                                                       
*                                                                               
         CLI   QMGR,C' '           TEST FOR MKTGRPS                             
         BE    NMGRNEQX            NO                                           
         CLI   QMGR,C'9'           FA-FF ARE MKTGRPS                            
         BH    *+12                                                             
         CLI   QMGR,C'0'           IF NUMERIC NO MKT GROUPS                     
         BNL   MGRK10              -- MARKET RANK                               
*                                                                               
         OC    SVMGRKEY,SVMGRKEY                                                
         BNZ   NMGR42                                                           
* READ MGRDEF REC TO TEST EXCEPTION LIST                                        
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D02'                                                  
         MVC   KEY+2(1),SVAGYMD                                                 
         CLI   QMGR,C' '           01-3F ARE ALL CLIENT GROUPS                  
         BL    NMGR1                                                            
         CLI   QMGR,C'F'                                                        
         BH    NMGR1                                                            
         MVC   KEY+3(2),SVCLT      ID'S A-F NEED CLIENT                         
*                                                                               
NMGR1    MVC   KEY+8(1),QMGR                                                    
         GOTO1 HIGH                                                             
         CLC   KEY(9),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         MVC   KEY(2),=X'0D82'     SET KEY FOR MGRP PASSIVES                    
*                                                                               
         L     R6,AREC                                                          
         USING MKGRECD,R6                                                       
         LA    R6,MKGEL            POINT TO FIRST EL                            
         MVI   BYTE,2                                                           
         DROP  R6                                                               
*                                                                               
         CLI   QMGR,C' '           01-3F ARE ALL CLIENT GROUPS                  
         BL    NMGR3                                                            
         CLI   QMGR,C'F'           TEST ID A-F                                  
         BH    NMGR3               NO                                           
* FOR ID A-F CHECK PRDGRP ASSGNS                                                
         CLI   SVMKGPGA,0          TEST ASSGNS BY PRDGRP                        
         BE    NMGR4               NO                                           
         MVC   KEY+5(1),QPGR       MOVE PGRPID TO KEY                           
         OI    KEY+5,X'40'         INSURE UPPER CASE                            
* NOW CHECK EXCEPTION LIST FOR ACTUAL PRDGRP IN PROCESS                         
NMGR2    BAS   RE,NMGNXTEL                                                      
         BNE   NMGR4                                                            
         CLC   KEY+5(1),2(R6)      RIGHT ID                                     
         BNE   NMGR2               NO                                           
         UNPK  DUB,3(3,R6)         UNPK THIS ENTRY                              
         UNPK  WORK(8),SVPGRKEY+6(3)  UNPK PRDGRP IN PROCESS                    
         IC    RE,SVMKGPGA         GET PRDGRP BREAK 1 DIGITS                    
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   DUB+3(0),WORK+3  **EXECUTED**                                    
         BNE   NMGR2               SKIP IF DON'T AGREE FOR BRK 1 DIGITS         
         MVC   KEY+6(2),3(R6)      MOVE BREAK1 DIGITS TO KEY                    
         B     NMGR4                                                            
         SPACE 2                                                                
* FOR ID G-K CHECK FOR OFFICE AND/OR CLIENT EXCEPTION                           
*                                                                               
NMGR3    LR    R0,R6               SAVE R6 (SET ABOVE)                          
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVI   HALF,C'*'                                                        
         MVC   HALF+1(1),COFFICE   BUILD OFFICE OVERRIDE                        
         DROP  R6                                                               
         LR    R6,R0               POINT R6 BACK TO FIRST EL                    
*                                                                               
NMGR3A   BAS   RE,NMGNXTEL                                                      
         BNE   NMGR4                                                            
         CLC   CLT,2(R6)           CHECK UNPACKED CLIENT CODE                   
         BE    NMGR3B              IF MATCH ON CLT, THIS IS OVERRIDE            
         CLC   HALF,2(R6)          ELSE CHECK OFFICE MATCH                      
         BNE   NMGR3A              NO                                           
         MVC   KEY+3(2),HALF       AND USE IT UNLESS FIND CLT MATCH             
         B     NMGR3A                                                           
NMGR3B   MVC   KEY+3(2),SVCLT      MOVE PACKED CLIENT TO MGR KEY                
*                                                                               
NMGR4    CLC   =C'ALL',QSTA        TEST ALL MKTGRPS                             
         BNE   NMGR20              NO-MUST BE ONE MKTGRP                        
         EJECT                                                                  
* ALL MKTGRPS                                                                   
*                                                                               
         GOTO1 HIGH                                                             
         B     NMGR6                                                            
*                                                                               
NMGR5    GOTO1 SEQ                                                              
*                                                                               
NMGR6    CLC   KEY(6),KEYSAVE      TY/A-M/CLT/PGRPID                            
         BNE   NMGR40                                                           
         CLI   SVMKGPGA,0          TEST ASSGNS BY PRDGRP                        
         BNE   NMGR6A              YES                                          
         OC    KEY+5(3),KEY+5      NO - TEST PRDGRP PRESENT                     
         BNZ   NMGR40              YES - DONE                                   
         B     NMGR7               NO - TEST MKTGRP                             
*                                                                               
* ASSGNS BY PRDGRP                                                              
*                                                                               
NMGR6A   OC    KEYSAVE+6(2),KEYSAVE+6  TEST READING DEFAULT PRDGRP              
         BNZ   NMGR6B                  NO - READING EXCEPTIONS                  
         OC    KEY+6(2),KEY+6          TEST PRDGRP PRESENT                      
         BNZ   NMGR40                  YES - NO MORE DEFAULT RECS               
         B     NMGR7                   NO - TEST MKTGRP                         
*                                                                               
NMGR6B   UNPK  WORK(8),SVPGRKEY+6(3)   UNPK PRDGRP IN PROCESS                   
         UNPK  DUB,KEY+6(3)           UNPK ACTUAL PRDGRP                        
         IC    RE,SVMKGPGA            GET PRIMARY BREAK DIGITS                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   DUB+3(0),WORK+3  **EXECUTED**                                    
         BH    NMGR40                                                           
         BL    NMGR5                                                            
*                                                                               
NMGR7    CLC   QMGR,KEY+8          TEST RIGHT MGRPID                            
         BNE   NMGR5                                                            
* FIND ENTRY IN LIST                                                            
         L     R1,MKTLIST                                                       
*                                                                               
NMGR8    CLC   2(2,R1),KEY+11      MATCH MKT                                    
         BE    NMGR10                                                           
         LA    R1,5(R1)                                                         
         OC    2(2,R1),2(R1)                                                    
         BNZ   NMGR8               MKT ALREADY IN LIST                          
         CLI   RQALLMKT,C'Y'       OPTION TO INCLUDE ALL MKTS                   
         BNE   NMGR5               NO, ON TO NEXT                               
         MVC   2(2,R1),KEY+11      SET MKT NUMBER                               
NMGR10   MVC   0(2,R1),KEY+9       MOVE GRP NUM                                 
         B     NMGR5                                                            
         EJECT                                                                  
* ONE MKTGRP                                                                    
*                                                                               
NMGR20   MVC   WORK(4),QSTA                                                     
         LA    R1,WORK+3                                                        
         LA    R2,4                                                             
         CLI   0(R1),C' '                                                       
         BNE   *+12                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10                                                          
         DC    H'0'                                                             
*                                                                               
         CLI   0(R1),C'*'                                                       
         BNE   *+14                                                             
         MVI   0(R1),C' '                                                       
         BCTR  R1,0                                                             
         BCT   R2,*-14             R2 HAS COMPARE LEN ON EXIT                   
*                                                                               
         PACK  DUB,WORK(5)                                                      
         MVC   KEY+9(2),DUB+5      SET LOW MKTGRP                               
*                                                                               
         BCTR  R2,0                SET FOR EX CLC                               
         GOTO1 HIGH                                                             
         B     NMGR24                                                           
*                                                                               
NMGR22   GOTO1 SEQ                                                              
*                                                                               
NMGR24   CLC   KEY(6),KEYSAVE      TY/A-M/CLT/PGRPID                            
         BNE   NMGR40                                                           
         CLI   SVMKGPGA,0          TEST ASSGNS BY PRDGRP                        
         BNE   NMGR24A             YES                                          
         OC    KEY+5(3),KEY+5      TEST PRDGRP PRESENT                          
         BNZ   NMGR40              YES - DONE                                   
         B     NMGR25              NO - TEST MKTGRP                             
*                                                                               
* ASSGNS BY PRDGRP                                                              
*                                                                               
NMGR24A  OC    KEYSAVE+6(2),KEYSAVE+6  TEST READING DEFAULT PRDGRP              
         BNZ   NMGR24B                 NO - READING EXCEPTIONS                  
         OC    KEY+6(2),KEY+6          TEST PRDGRP PRESENT                      
         BNZ   NMGR40                  YES - NO MORE DEFAULT RECS               
         B     NMGR25                  NO - TEST MKTGRP                         
         B     NMGR22                                                           
*                                                                               
NMGR24B  UNPK  WORK(8),SVPGRKEY+6(3)  UNPK PRDGRP IN PROCESS                    
         UNPK  DUB,KEY+6(3)           UNPK ACTUAL PRDGRP                        
         IC    RE,SVMKGPGA            GET PRIMARY BREAK DIGITS                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   DUB+3(0),WORK+3  **EXECUTED**                                    
         BH    NMGR40                                                           
         BL    NMGR22                                                           
*                                                                               
NMGR25   CLC   QMGR,KEY+8          TEST RIGHT MGRPID                            
         BNE   NMGR22                                                           
*                                                                               
         UNPK  DUB,KEY+9(3)        UNPK 1 EXTRA BYTE                            
         EX    R2,*+8              TEST SAME MKTGRP                             
         B     *+10                                                             
         CLC   DUB+3(0),QSTA  * EXECUTED *                                      
         BNE   NMGR40                                                           
*                                                                               
         L     R1,MKTLIST                                                       
NMGR27   CLC   2(2,R1),KEY+11      MATCH MKT                                    
         BE    NMGR27X                                                          
         LA    R1,5(R1)                                                         
         OC    0(2,R1),0(R1)                                                    
         BNZ   NMGR27              MKT ALREADY IN LIST                          
         CLI   RQALLMKT,C'Y'       OPTION TO INCLUDE ALL MKTS                   
         BNE   NMGR22              NO, ON TO NEXT                               
         MVC   2(2,R1),KEY+11      SET MKT NUMBER                               
NMGR27X  MVC   0(2,R1),KEY+9       MOVE GROUP NUM                               
         B     NMGR22                                                           
         EJECT                                                                  
NMGR40   SR    R0,R0               COUNT MARKETS IN LIST                        
         L     R1,MKTLIST                                                       
         OC    2(2,R1),2(R1)                                                    
         BZ    *+12                                                             
         LA    R1,5(R1)                                                         
         BCT   R0,*-14                                                          
*                                                                               
         LPR   R0,R0                                                            
         BZ    NMGRNEQX                                                         
         GOTO1 XSORT,DMCB,MKTLIST,(R0),5,5,0                                    
*                                                                               
NMGR40B  DS    0H                                                               
         MVC   SVMGRKEY(9),KEYSAVE   SAVE THRU MGRPID                           
         MVC   SVMGRKEY(2),=X'0D02'                                             
*                                                                               
         L     RE,MKTLIST                                                       
         B     NMGR44                                                           
         SPACE 2                                                                
* FIND NEXT MKTGRP                                                              
*                                                                               
NMGR42   L     RE,SVMKTADR         GET LAST MKT PROCESSED                       
         LA    RE,5(RE)            BUMP TO NEXT                                 
         OC    0(2,RE),0(RE)       TEST E-O-L                                   
         BZ    NMGRNEQX                                                         
*                                                                               
NMGR44   ST    RE,SVMKTADR                                                      
         C     RE,MKTLIST          TEST FIRST TIME (START OF LIST)              
         BE    *+16                                                             
         UNPK  DUB,SVMGRKEY+9(3)                                                
         MVC   SVOLDMGR,DUB+3      ELSE SAVE LAST MKTGRP                        
         CLC   =X'9999',0(RE)      CHECK FOR DUMMY                              
         BE    NMGR50              GO BUILD IT                                  
         MVC   SVMGRKEY+9(2),0(RE)                                              
         MVC   KEY(13),SVMGRKEY                                                 
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GETMKTGR                                                         
         EJECT                                                                  
HAVMKTGR MVC   BMGR,SVMGRKEY+8     MOVE ID AND GRP                              
*                                                                               
         XC    SVMKT(SVDATAX-SVMKT),SVMKT                                       
         CLI   QMGR,C' '                                                        
         BNE   *+10                                                             
         XC    SVMKTADR,SVMKTADR                                                
         B     NMGREQX                                                          
         SPACE 2                                                                
NMGR50   CLC   =C'ALL',QMKT                                                     
         BNE   NMGRNEQX                                                         
         CLC   =C'ALL',QSTA        TEST 'ALL' MKTGRPS                           
         BNE   NMGRNEQX                                                         
*                                                                               
         MVC   MGR1+1(4),=C'9999'                                               
         MVC   MGR1NM,=CL24'*** UNDEFINED ***'                                  
         CLI   QLANG,C'F'                                                       
         BNE   *+10                                                             
         MVC   MGR1NM,=CL24'*** INCONNU ***'                                    
         MVC   MGR2+1(4),=C'9999'                                               
         MVC   MGR2NM,MGR1NM                                                    
         MVC   MGR3+1(4),=C'9999'                                               
         MVC   MGR3NM,MGR1NM                                                    
*                                                                               
         MVC   SVMGRKEY+9(2),=X'9999'                                           
*                                                                               
         CLI   MGRLEN,2                                                         
         BE    NMGR52                                                           
         MVC   MGR1N,MGR1                                                       
         MVC   MGR2N,MGR2                                                       
         MVC   MGR3N,MGR3                                                       
         B     NMGR54                                                           
*                                                                               
NMGR52   MVC   MGR1N+2(4),MGR1+1   LEAVE 2 CHAR ALPHA PRESENT                   
         MVC   MGR2N+2(4),MGR2+1                                                
         MVC   MGR3N+2(4),MGR3+1                                                
*                                                                               
NMGR54   B     HAVMKTGR                                                         
*                                                                               
NMGREQX  CR    RB,RB                                                            
         B     *+6                                                              
NMGRNEQX LTR   RB,RB                                                            
         XIT1                                                                   
         SPACE 2                                                                
NMGNXTEL CLI   0(R6),0                                                          
         BE    NMGNXTX                                                          
         SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   BYTE(1),0(R6)                                                    
         BER   RE                                                               
         B     NMGNXTEL                                                         
NMGNXTX  LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 2                                                                
* SET GROUP LITERALS FOR MARKET RANKING                                         
MGRK10   L     R2,SVMKTADR         CHECK FOR NEXT MARKET                        
         LA    R2,5(R2)            NOT ADDED LAST MKT IN LST GRP                
         OC    SVMGRKEY,SVMGRKEY                                                
         BNZ   MGRK20                                                           
         L     R2,MKTLIST          FIRST TIME                                   
*                                                                               
MGRK20   ST    R2,SVMKTADR                                                      
         OC    0(5,R2),0(R2)       END OF MKT LIST                              
         BZ    NMGRNEQX                                                         
         MVC   SVMGRKEY(2),=X'0D02'                                             
         MVC   SVMGRKEY+8(2),0(R2) USE AS FIRST TIME INDICATOR                  
         LH    RE,0(R2)            RE HAS TABLE DISPLACMENT                     
         LA    RE,MKRKLITS-10(RE)                                               
         MVC   MGR1(4),0(RE)                                                    
         MVC   MGR1NM,=CL24'MARKET RANK'                                        
         MVC   MGR1NM+12(6),4(RE)                                               
         MVC   MGR1BK,=CL12'MKT GRP'                                            
         MVI   MGR1LEN,4           SET + TO AVOID GROUP2/3 BREAKS               
         MVI   MGR2LEN,4                                                        
         MVI   MGR3LEN,4                                                        
         B     HAVMKTGR                                                         
         SPACE                                                                  
* A LIST OF MARKET LITERALS TO PRINT ON REPORT                                  
MKRKLITS DC    CL10'*10*1-10'                                                   
         DC    CL10'*20*11-20'                                                  
         DC    CL10'*30*21-30'                                                  
         DC    CL10'*40*31-40'                                                  
         DC    CL10'*50*41-50'                                                  
         DC    CL10'*10051-100'                                                 
         DC    CL10'100+101+'                                                   
         LTORG                                                                  
         EJECT                                                                  
*******************************************************************             
* SUBROUTINE TO BUILD FILM LIST BUFFER IN CMLTAB                  *             
* BUFFER FORMAT IS                                                *             
*                                                                 *             
*   CLASS(4) SEQ1(2) SEQ2(2) . . . SEQX(2) X'0000'=END OF CLASS   *             
*   CLASS(4) SEQ1(2) SEQ2(2) . . .                                *             
*   X'00000000' = END OF TABLE                                    *             
*                                                                 *             
*******************************************************************             
         SPACE 2                                                                
BLDFLMS  NMOD1 0,**BLDFLM                                                       
*                                                                               
         L     R1,CMLTAB                                                        
         L     R0,CMLTABX                                                       
         BAS   RE,BLDFCLR                                                       
*                                                                               
         L     R1,MKTLIST                                                       
         L     R0,MKTLISTX                                                      
         BAS   RE,BLDFCLR                                                       
*                                                                               
         L     R9,MKTLIST          TEMPORARY BUILD AREA                         
         SR    R7,R7               ENTRY COUNTER                                
         CLI   QFILTER+1,C' '                                                   
         BNE   BLDF1                                                            
         MVC   0(4,R9),=C'ZZZZ'    SET MISSING FILM ENTRY                       
         MVC   4(2,R9),=X'FFFF'    SET HIGH SEQ                                 
         LA    R9,6(R9)                                                         
         BCTR  R7,0                AND SET COUNTER TOO                          
*                                                                               
BLDF1    L     R6,ADBUY            IO AREA                                      
         ST    R6,AREC                                                          
         USING CMLRECD,R6                                                       
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0A21'                                                  
         MVC   KEY+2(3),BAGYMD     A-M/CLT                                      
         GOTO1 HIGH                                                             
*                                                                               
BLDF2    CLC   KEY(5),KEYSAVE      SAME TYPE/A-M/CLT                            
         BNE   BLDF4                                                            
         GOTO1 GET                                                              
         MVC   0(4,R9),CMLCLASS                                                 
         OC    0(4,R9),0(R9)       TEST MISSING CLASS                           
         BNZ   *+10                                                             
         MVC   0(4,R9),=C'ZZZZ'                                                 
         MVC   4(2,R9),CMLSEQ+1    SAVE 2 BYTES OF SEQUENCE NUMBER              
*                                                                               
         CLI   QFILTER+1,C' '      TEST LIMIT TO ONE CLASS                      
         BE    *+14                NO                                           
         CLC   QFILTER+1(1),0(R9)  TEST INCLUDE THIS CLASS                      
         BNE   BLDF3               NO - SKIP                                    
*                                                                               
         BCTR  R7,0                BUMP COUNTER                                 
         LA    R9,6(R9)            BUMP POINTER                                 
*                                                                               
         L     R0,MKTLISTX                                                      
         CR    R9,R0                                                            
         BL    *+6                                                              
         DC    H'0'                                                             
BLDF3    GOTO1 SEQ                                                              
         B     BLDF2                                                            
*                                                                               
BLDF4    XC    0(6,R9),0(R9)       CLEAR POSSIBLE EXTRA ENTRY                   
         LPR   R7,R7                                                            
         GOTO1 XSORT,DMCB,MKTLIST,(R7),6,6,0  ** SORT **                        
         SPACE 1                                                                
* NOW BUILD BUFFER *                                                            
         SPACE 1                                                                
         L     R6,CMLTAB                                                        
         L     R9,MKTLIST                                                       
*                                                                               
BLDF10   MVC   0(4,R6),0(R9)       MOVE CLASS                                   
         LA    R6,4(R6)            POSITION FOR FIRST SEQ                       
*                                                                               
BLDF12   MVC   0(2,R6),4(R9)       MOVE SEQ                                     
         LA    R6,2(R6)                                                         
         CLC   0(4,R9),6(R9)       NEXT FILM SAME CLASS                         
         BNE   BLDF14                                                           
         LA    R9,6(R9)                                                         
         B     BLDF12                                                           
*                                                                               
BLDF14   LA    R6,2(R6)            LEAVE 2X'00' AS EOL FLAG                     
*                                                                               
         LA    R9,6(R9)            NEXT FILM                                    
         OC    0(6,R9),0(R9)       TEST MORE FILMS                              
         BNZ   BLDF10              YES                                          
*                                                                               
         MVC   CMLPTR,CMLTAB       SET POINTER TO TABLE START                   
*                                                                               
         L     R0,CMLTABX                                                       
         CR    R6,R0                                                            
         BNH   *+6                                                              
         DC    H'0'                CMLTAB NEEDS MORE ROOM                       
*                                                                               
BLDFX    XIT1                                                                   
         SPACE 1                                                                
* SUBROUTINE CLEARS FROM R1 TO R0                                               
* RE IS RETURN REG, R0,R1,RF ARE DESTROYED                                      
*                                                                               
BLDFCLR  SR    R0,R1               GET LENGTH IN R0                             
         LA    RF,256                                                           
BLDFCLR2 CR    R0,RF                                                            
         BNH   BLDFCLR4                                                         
         XC    0(256,R1),0(R1)                                                  
         AR    R1,RF                                                            
         SR    R0,RF                                                            
         B     BLDFCLR2                                                         
BLDFCLR4 LR    RF,R0                                                            
         BCTR  RF,0                                                             
         EX    RF,*+6                                                           
         BR    RE                                                               
         XC    0(0,R1),0(R1) *EXECUTED*                                         
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
************************************************************                    
*                                                          *                    
* SUB-ROUTINE TO SEE IF ESTIMATE(S) REQUESTED HAVE ANY     *                    
* BILLING ON RQBILLDT - CALLED FROM HAVEST                 *                    
*                                                          *                    
* ON ENTRY, KEY CONTAINS FIRST ESTIMATE FOR REQUEST        *                    
*                                                          *                    
* ON EXIT, CC=EQ IF BILLING FOUND                          *                    
*          CC=NEQ IF NO BILLING                            *                    
*                                                          *                    
************************************************************                    
         SPACE 2                                                                
ESTBILL  NMOD1 0,**ESTBIL                                                       
*                                                                               
         GOTO1 DATCON,DMCB,(3,RQBILLDT),BDATEFLT                                
         LLC   RE,RQBILLDT+1       GET MONTH                                    
         LLC   R1,RQBILLDT         GET YEAR                                     
         SR    R0,R0                                                            
         D     R0,=F'10'           FIND YEAR REMAINDER                          
         SLL   R0,4                SHIFT YEAR TO HIGH ORDER NIBBLE              
         OR    R0,RE               COMBINE YEAR AND MONTH                       
         STC   R0,BMONFLT          SAVE YEAR/MONTH                              
         CLC   =C'POL',KEY+4       TEST FOR 'POL' REQUEST                       
         BE    EB20                YES                                          
*                                                                               
EB2      GOTO1 HIGH                                                             
         CLI   BEST,0              TEST FOR SINGLE ESTIMATE REQUEST             
         BE    EB8                 NO-MUST BE EST=NO                            
         CLI   BESTEND,0                                                        
         BNE   EB5                 NO-MUST BE ESTIMATE SERIES                   
*                                                                               
* ONE PRODUCT/ESTIMATE                                                          
*                                                                               
EB3      LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
*                                                                               
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE   TEST SAME EST                      
         BNE   EBNO                NO-FINISHED                                  
         CLC   BKEYMBIL,BMONFLT    TEST BILL DONE IN RIGHT YEAR/MONTH           
         BNE   EB3                 NO-TRY NEXT ONE                              
*                                                                               
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT      MATCH ON BILL DATE                           
         BE    EBYES                                                            
         B     EB3                                                              
*                                                                               
* ONE PRODUCT/SERIES OF ESTIMATES                                               
*                                                                               
EB5      LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
         CLC   BKEY(BKEYEST-BKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EBNO                NO-ALL DONE                                  
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE TEST SAME ESTIMATE                   
         BE    EB6                 YES-MUST BE BILL RECORD                      
*                                                                               
         CLC   BKEYEST,BESTEND     TEST IF W/IN ESTIMATE SERIES                 
         BH    EBNO                PAST LIMIT-ALL DONE                          
         MVC   KEYSAVE(L'BKEY),BKEY SAVE NEW ESTIMATE                           
         B     EB5                  AND GO READ FOR A BILL                      
*                                                                               
EB6      CLC   BKEYMBIL,BMONFLT    TEST FOR RIGHT YEAR/MONTH                    
         BNE   EB5                 NO-TRY NEXT ONE                              
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT      MATCH ON FILTER DATE                         
         BE    EBYES                                                            
         B     EB5                                                              
*                                                                               
* ONE PRODUCT - ESTIMATE=NO                                                     
*                                                                               
EB8      LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
         CLC   BKEY(BKEYEST-BKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EBNO                NO-STOP                                      
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE  TEST SAME ESTIMATE                  
         BE    EB10                YES-MUST BE BILL RECORD                      
*                                                                               
         MVC   BKEY,KEYSAVE        RESTORE PREVIOUS ESTIMATE                    
         LLC   R1,BKEYEST          GET LAST ESTIMATE                            
         LA    RE,ESTLST(R1)       INDEX INTO ESTIMATE LIST                     
*                                                                               
EB9      LA    R1,1(R1)            INCREMENT ESTIMATE NUMBER                    
         CH    R1,=H'255'                                                       
         BH    EBNO                                                             
         LA    RE,1(RE)                                                         
         CLI   0(RE),0             TEST IF ACTIVE ESTIMATE                      
         BE    EB9                 NO-TRY NEXT ONE                              
         MVC   BKEYEST,0(RE)       SET NEXT ESTIMATE                            
         XC    BKEYYSRV(BLEN-BKEYYSRV),BKEYYSRV  CLEAR REST OF KEY              
         GOTO1 HIGH                                                             
         B     EB8                                                              
*                                                                               
EB10     CLC   BKEYMBIL,BMONFLT    TEST YEAR/MONTH                              
         BNE   EB8                                                              
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT                                                   
         BE    EBYES                                                            
         B     EB8                                                              
*                                                                               
* 'POL' ESTIMATE READ                                                           
*                                                                               
EB20     L     RE,ADCLT                                                         
         USING CLTHDR,RE                                                        
**>      LA    R1,CLIST-4          INITIALIZE PRODUCT LIST POINTER              
         L     R1,VCLIST           COMBINED CLIST+CLIST2                        
         AHI   R1,-4                                                            
         ST    R1,ANXTPRD                                                       
         CLI   BEST,0              TEST FOR SINGLE ESTIMATE                     
         BE    EB30                NO-MUST BE ESTIMATE=NO                       
         CLI   BESTEND,0                                                        
         BNE   EB25                MUST BE ESTIMATE SERIES                      
*                                                                               
* PRODUCT 'POL' - SINGLE ESTIMATE                                               
*                                                                               
EB22     LA    R6,KEY                                                           
         USING ESTHDR,R6                                                        
         MVC   EKEY,KEYSAVE        RESTORE PREVIOUS KEY                         
         BAS   RE,GETPCOD          GET NEXT PRODUCT                             
         BNE   EBNO                NO MORE PRODUCTS                             
         MVC   EKEYPRD,THREE       SET NEW PRODUCT                              
         MVC   EKEYEST,BEST        SET ESTIMATE NUMBER                          
         XC    EKEYEST+1(ELEN-EKEYEST-1),EKEYEST+1                              
         GOTO1 HIGH                                                             
         CLC   EKEY,KEYSAVE        TEST IF ESTIMATE FOUND                       
         BNE   EB22                NO-TRY NEXT PRODUCT                          
*                                                                               
EB23     LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE  TEST SAME ESTIMATE                  
         BNE   EB22                GET NEXT PRODUCT                             
*                                                                               
         CLC   BKEYMBIL,BMONFLT    FILTER ON BILL YEAR/MONTH                    
         BNE   EB23                NO-GET NEXT BILL                             
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT      MATCH ON BILL DATE                           
         BE    EBYES                                                            
         B     EB23                                                             
*                                                                               
* PRODUCT 'POL' - SERIES OF ESTIMATES                                           
*                                                                               
EB25     LA    R6,KEY                                                           
         USING ESTHDR,R6                                                        
         BAS   RE,GETPCOD          GET NEXT PRODUCT                             
         BNE   EBNO                                                             
         MVC   EKEY,KEYSAVE        RESTORE PREVIOUS KEY                         
         MVC   EKEYPRD,THREE       SET NEW PRODUCT                              
         MVC   EKEYEST,BEST        AND FIRST ESTIMATE                           
         XC    EKEYEST+1(ELEN-EKEYEST-1),EKEYEST+1                              
*                                                                               
EB26     LA    R6,KEY                                                           
         GOTO1 HIGH                                                             
         CLC   EKEY(EKEYEST-EKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EB25                NO-GET NEXT PRODUCT                          
         CLC   EKEYEST,BESTEND     TEST W/IN SERIES LIMIT                       
         BH    EB25                NO-NEXT PRODUCT                              
*                                                                               
EB27     LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
         CLC   BKEY(BKEYEST-BKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EB25                NO                                           
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE  TEST SAME ESTIMATE                  
         BE    EB28                YES-MUST BE BILL                             
*                                                                               
         CLC   BKEYEST,BESTEND     TEST W/IN SERIES LIMIT                       
         BH    EB25                NO-NEXT PRODUCT                              
         MVC   KEYSAVE(L'BKEY),BKEY  YES-SAVE NEW ESTIMATE                      
         B     EB27                  AND GO READ THE BILLS                      
*                                                                               
EB28     CLC   BKEYMBIL,BMONFLT                                                 
         BNE   EB27                                                             
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT                                                   
         BE    EBYES                                                            
         B     EB27                                                             
*                                                                               
* PRODUCT 'POL' - ESTIMATE=NO                                                   
*                                                                               
EB30     LA    R6,KEY                                                           
         USING ESTHDR,R6                                                        
         MVC   EKEY,KEYSAVE        RESTORE PREVIOUS KEY                         
         BAS   RE,GETPCOD          GET NEXT PRODUCT                             
         BNE   EBNO                NO MORE PRODUCTS                             
         MVC   EKEYPRD,THREE       SET NEW PRODUCT                              
*                                                                               
         LA    RE,ESTLST           GET FIRST ACTIVE ESTIMATE                    
         CLI   0(RE),0                                                          
         BNE   *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
*                                                                               
EB31     MVC   EKEYEST,0(RE)       SET ESTIMATE                                 
         XC    EKEYEST+1(ELEN-EKEYEST-1),EKEYEST+1                              
*                                                                               
EB32     GOTO1 HIGH                                                             
         CLC   EKEY(EKEYEST-EKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EB30                NO-TRY NEXT PRODUCT                          
         CLC   EKEY,KEYSAVE        TEST IF ESTIMATE FOUND                       
         BE    EB36                YES                                          
*                                                                               
EB33     MVC   EKEY,KEYSAVE        RESTORE PREVIOUS KEY                         
         LLC   R1,EKEYEST          GET EST NUMBER                               
         LA    RE,ESTLST(R1)                                                    
*                                                                               
EB34     LA    R1,1(R1)            INCREMENT ESTIMATE NUMBER                    
         CH    R1,=H'255'                                                       
         BH    EB30                NEXT PRODUCT                                 
         LA    RE,1(RE)                                                         
         CLI   0(RE),0             TEST FOR ACTIVE ESTIMATE                     
         BE    EB34                NO                                           
         B     EB31                YES-NOW TRY AND READ IT                      
*                                                                               
EB36     LA    R6,KEY                                                           
         USING BILLREC,R6                                                       
         GOTO1 SEQ                                                              
         CLC   BKEY(BKEYEST-BKEY),KEYSAVE  TEST SAME PRODUCT                    
         BNE   EB30                                                             
         CLC   BKEY(BKEYYSRV-BKEY),KEYSAVE  TEST SAME EST                       
         BNE   EB33                NO-FIND THE NEXT ONE                         
*                                                                               
EB38     CLC   BKEYMBIL,BMONFLT                                                 
         BNE   EB36                                                             
         GOTO1 GETBILL                                                          
         L     R6,ADBILL                                                        
         CLC   BDATE,BDATEFLT                                                   
         BE    EBYES                                                            
         B     EB36                                                             
*                                                                               
EBYES    CR    RB,RB                                                            
         B     EBX                                                              
*                                                                               
EBNO     LTR   RB,RB                                                            
*                                                                               
EBX      XIT1                                                                   
*                                                                               
ANXTPRD  DS    A                                                                
BDATEFLT DS    CL6                                                              
BMONFLT  DS    X                                                                
         DROP  R6                                                               
         EJECT                                                                  
* SUB-ROUTINE TO GET NEXT PRODUCT CODE                                          
*                                                                               
* ON ENTRY, ANXTPRD POINTS TO PREVIOUS CLIST POSITION                           
* ON EXIT,  THREE CONTAINS NEXT PRODUCT CODE                                    
*           CC=EQ FOR PRODUCT FOUND, CC=NEQ IF NO PRODUCT                       
*                                                                               
GETPCOD  NTR1                                                                   
         XC    THREE,THREE                                                      
         L     RE,ADCLT                                                         
***>     LA    RF,CLIST+880                                                     
         L     RF,VCLIST                                                        
         AH    RF,=H'3004'         END OF VCLIST                                
         L     R1,ANXTPRD                                                       
*                                                                               
GETPCOD2 LA    R1,4(R1)            NEXT TABLE ENTRY                             
         CR    R1,RF               TEST PAST TABLE END                          
         BNL   GETPCODN                                                         
         OC    0(4,R1),0(R1)       TEST FOR EOT                                 
         BZ    GETPCODN                                                         
         CLC   0(3,R1),=C'POL'     EXCLUDE POL                                  
         BE    GETPCOD2                                                         
         MVC   THREE,0(R1)         EXTRACT NEW CODE                             
         ST    R1,ANXTPRD          SAVE POINTER                                 
         CR    RB,RB                                                            
         B     GETPCODX                                                         
*                                                                               
GETPCODN LTR   RB,RB                                                            
*                                                                               
GETPCODX XIT1                                                                   
         DROP  RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*==============================================================*                
* REDIRECT CALLS FOR MSPACK/MSUNPK TO STAPACK                  *                
* USER IS RESPONSIBLE FOR MODIFYING COUNTRY IF HE SWITCHES     *                
* FROM US TO CANADIAN AGENCY                                   *                
*==============================================================*                
         SPACE 1                                                                
FCMSPACK NMOD1 0,**FMSP**                                                       
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R8,2048(RA)                                                      
         LA    R8,2048(R8)                                                      
         USING SPWORKD+4096,R8                                                  
*                                                                               
         LM    R5,R7,0(R1)         GET A(MKT)/A(STA)/A(MKTSTA)                  
         LR    R2,R1               SAVE OFF PARAM LIST                          
*                                                                               
         XC    STAWORK,STAWORK                                                  
         LA    R1,STAWORK                                                       
         USING STAPACKD,R1                                                      
*                                                                               
         MVI   STAPACT,C'P'                                                     
         MVC   STAPAGY,AGY                                                      
         MVC   STAPCTRY,COUNTRY                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPQMKT,0(R5)      MARKET                                       
         MVC   STAPQSTA(8),0(R6)   STATION                                      
* FOR NEW CABLE STATION COMPATABILITY                                           
         CLI   STAPCTRY,C'C'       TEST CANADA                                  
         BNE   FCMSP2                                                           
         CLI   STAPMED,C'N'        TEST NETWORK                                 
         BNE   FCMSP2                                                           
         MVC   STAPQNET,=C'   '                                                 
*                                                                               
FCMSP2   GOTO1 VSTAPACK,(R1)                                                    
         CLI   STAPERR,0                                                        
         BNE   FCMSPERR                                                         
         MVC   0(5,R7),STAPMKST    RETURN RESULT                                
         CR    R1,R1                                                            
         XIT1                                                                   
*                                                                               
FCMSPERR TM    0(R2),X'40'         USER WANT ERR CODE INSTEAD OF DEATH?         
         BO    FCMSPER2            YES                                          
         L     RF,=A(FCMERR)                                                    
         BASR  RE,RF                                                            
         DC    H'0'                                                             
*                                                                               
FCMSPER2 MVC   0(1,R2),STAPERR     RETURN ERROR CODE                            
         LTR   R1,R1               RETURN BAD CC                                
         XIT1                                                                   
*                                                                               
         DROP  R1                                                               
         LTORG                                                                  
         EJECT                                                                  
FCMSUNPK NMOD1 0,**FMSU**                                                       
*                                                                               
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R8,2048(RA)                                                      
         LA    R8,2048(R8)                                                      
         USING SPWORKD+4096,R8                                                  
*                                                                               
         LM    R5,R7,0(R1)         GET A(MKTSTA)/A(MKT)/A(STA)                  
         LR    R2,R1               SAVE OFF FISRT PARAM                         
*                                                                               
         XC    STAWORK,STAWORK                                                  
         LA    R1,STAWORK                                                       
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,AGY                                                      
         MVC   STAPCTRY,COUNTRY                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPMKST,0(R5)      MKTSTA                                       
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
         OC    STAPSTA,STAPSTA     TEST INPUT STATION IS X'000000'              
         BZ    FCMSU2              YES - JUST RETURN MARKET !                   
*                                                                               
         CLI   STAPERR,0                                                        
         BNE   FCMSUERR                                                         
*                                                                               
FCMSU2   MVC   0(4,R6),STAPQMKT    RETURN RESULT                                
         MVC   0(5,R7),STAPQSTA                                                 
         LTR   R5,R5               TEST 8 BYTE STATION REQ                      
         BNM   *+10                                                             
         MVC   0(8,R7),STAPQSTA                                                 
         CR    R1,R1                                                            
         XIT1                                                                   
*                                                                               
FCMSUERR TM    0(R2),X'40'         USER WANT ERR CODE INSTEAD OF DEATH?         
         BO    FCMSUER2            YES                                          
         L     RF,=A(FCMERR)                                                    
         BASR  RE,RF                                                            
         CLI   STAPMKST+2,X'E8'    DON'T DIE IF IT'S CABLE                      
         BNL   *+6                 BUT DIE FOR ALL OTHERS                       
         DC    H'0'                                                             
         CR    R1,R1                                                            
         XIT1                                                                   
*                                                                               
FCMSUER2 MVC   0(1,R2),STAPERR     RETURN ERROR CODE                            
         LTR   R2,R2               RETURN BAD CC                                
         XIT1                                                                   
         DROP  R1                                                               
*                                                                               
FCMERR   NTR1  BASE=*                                                           
         LR    R4,R1                                                            
         GOTO1 HEXOUT,DMCB,(R4),P+20,32,=C'TOG'                                 
         MVC   P(17),=C'** MSP/MSU ERR **'                                      
         GOTO1 REPORT                                                           
*                                                                               
* MOVE DATA TO VISIBLE LOCATION IN DUMP (LINE DOESN'T ALWAYS PRINT)             
*                                                                               
         GOTO1 HEXOUT,DMCB,(R4),P+20,32,=C'TOG'                                 
         MVC   P(17),=C'** MSP/MSU ERR **'                                      
         XIT1                                                                   
*                                                                               
         DS    0D                                                               
STAWORK  DS    XL32                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*==============================================================*                
* TEST IF LINE IS IN A LINKED PACKAGE                          *                
* IF SO , RETURN WITH A CC OF EQ WHICH SKIPS PROCESSING        *                
*==============================================================*                
         SPACE 1                                                                
         DS    0D                                                               
NBSLVTST NTR1  BASE=*,LABEL=*                                                   
                                                                                
         L     R6,ADBUY                                                         
         USING BUYREC,R6                                                        
         LA    R2,BDELEM                                                        
         SR    R0,R0                                                            
*                                                                               
SLVT2    IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    SLVTNEQX            TAKE RETURN TO PROCESS                       
         CLI   0(R2),5                                                          
         BNE   SLVT2                                                            
         TM    2(R2),X'01'         TEST MASTER OR SLAVE                         
         BZ    SLVT4               BRANCH IF SLAVE                              
* LINE IS A MASTER                                                              
         MVC   SVPKGKEY,KEY        SAVE MASTER KEY                              
         SR    RF,RF                                                            
         IC    RF,1(R2)                                                         
         BCTR  RF,0                SET FOR EX                                   
         EX    RF,*+8              SAVE                                         
         B     *+10                                                             
         MVC   SVPKGEL(0),0(R2)  *EXECUTED*                                     
         B     SLVTNEQX            TAKE RETURN 2 - PKG DATA SAVED               
         EJECT                                                                  
*=================================================================*             
* LINE IS A SLAVE - MAKE SURE THERE IS A MASTER                   *             
* POINTER WITH THIS LINE NUMBER                                   *             
*=================================================================*             
         SPACE 1                                                                
SLVT4    CLI   KEY+3,X'FF'         TEST POL MASTER POINTER                      
         BE    SLVTEQX             YES - EXIT                                   
         MVC   BYTE,KEY                                                         
         NI    BYTE,X'0F'          DROP AGY                                     
         CLI   BYTE,8              TEST MEDIA = CMBD                            
         BE    SLVT5                                                            
*                                                                               
         CLI   KEY+10,X'FF'        TEST POL PASSIVE                             
         BNE   SLVTEQX             NO - EXIT                                    
*                                                                               
SLVT5    MVC   SVPKGEL(18),KEY     TEMP SAVE AREA FOR KEY                       
         MVI   SVPKGFLG,C'Y'       SET FLAG FOR LINE IS A SLAVE                 
*                                                                               
SLVT6    MVC   KEY+12(1),3(R2)     SET MASTER LINE NUM IN KEYARG                
         TM    2(R2),X'10'         TEST 2-BYTE LINE NUMBER                      
         BZ    *+10                                                             
         MVC   KEY+11(2),3(R2)                                                  
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE     TEST MASTER ON FILE FOR THIS BRAND           
         BNE   SLVT10              NO - IGNORE PACKAGE                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE     TEST MASTER ON FILE FOR THIS BRAND           
         BE    SLVT12              YES - PROCESS AS PACKAGE                     
*                                                                               
SLVT10   MVI   SVPKGFLG,C'N'           SET FLAG TO IGNORE PKG                   
*                                                                               
SLVT12   MVC   KEY(18),SVPKGEL         RESTORE KEY                              
         GOTO1 HIGH                                                             
*                                                                               
SLVT14   XC    SVPKGEL,SVPKGEL                                                  
         CLI   SVPKGFLG,C'Y'       TEST TO PROCESS AS PACKAGE                   
         BNE   SLVTNEQX            NO - TAKE RETURN TO PROCESS NOW              
*                                                                               
SLVTEQX  CR    RE,RE               SET CC EQ                                    
         B     *+6                                                              
*                                                                               
SLVTNEQX LTR   RE,RE               SET CC NEQ                                   
*                                                                               
         XIT1                                                                   
SVPKGEL  DC    XL256'00'                                                        
SVPKGFLG DC    X'00'                                                            
         LTORG                                                                  
         EJECT                                                                  
*=============================================================*                 
* READ PW RECORDS TO SEE IF ANY ADJUSTMENT BILLING HAS BEEN   *                 
* DONE FOR ANY MARKET. IF IT HAS, SET FLAG ON IN RQFLAG       *                 
*=============================================================*                 
         SPACE 1                                                                
PWTEST   NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*PWTEST*'                                                    
*                                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING PWRECD,R5                                                        
*                                                                               
         MVC   PWKTYP,=X'0D7A'                                                  
         MVC   PWKAGMD,BAGYMD                                                   
         MVC   PWKCLT,BCLT                                                      
         MVC   PWKPRD,BPRD                                                      
         MVC   PWKEST,BEST                                                      
         DROP  R5                                                               
*                                                                               
         GOTO1 HIGH                                                             
         B     PW12                                                             
*                                                                               
PW10     GOTO1 SEQ                                                              
*                                                                               
PW12     CLC   KEY(7),KEYSAVE      TYPE/A-M/CLT/PRD/EST                         
         BNE   PWX                                                              
*                                                                               
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         MVI   BYTE,PWDOLCDQ                                                    
         L     R6,ADBUY                                                         
         LA    R6,24(R6)           POINT TO FIRST ELEMENT                       
         USING PWDOLEL,R6                                                       
*                                                                               
PW20     BRAS  RE,NEXTEL                                                        
         BNE   PW10                                                             
         OC    PWDOLBIL,PWDOLBIL                                                
         BZ    PW20                                                             
         CLC   PWDOLBIL,=X'80000000'                                            
         BNE   *+14                                                             
         CLC   QPROG,=C'D4'                                                     
         BE    PW20                                                             
         OI    RQFLAG,RQFLAG_PWADJ   SET FLAG THERE IS ADJ BILLING              
*                                                                               
PWX      XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*============================================================                   
* NEXT REQUEST                                                                  
*============================================================                   
NREQ     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLI   RCMULTIQ,C'Y'       DOING MULTI-MEDIA REQUEST?                   
         BE    NREQ8               SKIP SOME OF THIS                            
*                                                                               
NREQ2    L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    NREQ2A              YES, SO NEED TO GET REQUEST CARDS            
         CLI   COMSCORE,C'1'                                                    
         JH    NREQ3               SKIP IF COMSCORE PASS 2                      
*                                                                               
NREQ2A   MVC   QSAVE,QRECORD       SAVE PREVIOUS REQUEST                        
         GOTO1 =V(REQSTART)                                                     
*                                                                               
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   COMSCORE,MCCSMODE   SAVE COMSCORE PASS NUM                       
         MVC   QAREA(80),MCREQREC  MASTER MOVES MCIO(80) TO MCREQREC            
         MVC   QAREA2,SPACES       CLEAR CARD 2 AREA                            
         CLI   MCRQNUM,2           TEST 2 REQUEST CARDS                         
         JL    *+10                                                             
         MVC   QAREA2,MCIO+L'MCREQREC  MOVE SECOND CARD TO SPWORK               
         DROP  R1                                                               
*                                                                               
         CLC   =C'/*',QRECORD                                                   
         BE    NREQNE                                                           
         CLC   =C'/&&',QRECORD                                                  
         BE    NREQNE                                                           
*                                                                               
         CLC   =C'DUMP',QRECORD                                                 
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   COMSCORE,C'1'       TEST COMSCORE PASS 1                         
         JNE   NREQ3               NO                                           
         GOTOR =V(JCREQSTR)                                                     
*                                                                               
NREQ3    MVI   RCMULTIQ,C'N'                                                    
         CLI   QMED,C'*'           MULTI-MEDIA REQUEST?                         
         BNE   *+12                                                             
         MVI   RCMULTIQ,C'Y'                                                    
         MVI   QMED,C'R'           START WITH RADIO                             
*                                                                               
NREQ4    DS    0H                                                               
         CLC   =C'**',QAGY                                                      
         BE    NREQ14              DO AS LITTLE AS POSSIBLE                     
         CLC   QAGY,RCAGENCY                                                    
         BE    NREQ6                                                            
         CLI   RCFILTER,C'Y'       FILTER ON AGY                                
         BE    NREQ2                                                            
NREQ6    DS    0H                                                               
         L     RF,ADAGY            TEST HAVE RIGHT AGYHDR                       
         CLC   QAGY,AGYKAGY-AGYHDR(RF)                                          
         BE    NREQ8                                                            
*                                  GET AGY REC                                  
         XC    KEY,KEY                                                          
         MVI   KEY,X'06'                                                        
         MVC   KEY+1(2),QAGY                                                    
         GOTO1 READ                                                             
         GOTO1 GETAGY                                                           
*                                                                               
NREQ8    DS    0H                                                               
         MVC   RCAGENCY,QAGY                                                    
*                                                                               
         L     R6,ADAGY                                                         
         USING AGYHDR,R6                                                        
         MVC   COUNTRY,AGYPROF+7   SET COUNTRY CODE !                           
         DROP  R6                                                               
*                                                                               
         L     R1,ADBLOCK          INITIALIZE DBLOCK                            
         LTR   R1,R1                                                            
         BZ    NREQ10                                                           
         USING DBLOCK,R1                                                        
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBSELAGY,QAGY                                                    
         MVC   DBCOMFCS,ACOMFACS                                                
         MVI   DBSELMED,C'T'                                                    
         CLI   QMED,C'T'                                                        
         BNE   NREQ10                                                           
         CLI   COUNTRY,C'C'        CHECK FOR CANADIAN AGENCY                    
         BNE   *+8                                                              
         MVI   DBSELMED,C'C'                                                    
         DROP  R1                                                               
*                                                                               
NREQ10   AP    RCRQTOT,=P'1'                                                    
*                                                                               
         CLC   QMKT,SPACES                                                      
         BNE   *+10                                                             
         MVC   QMKT(3),=C'ALL'                                                  
*                                                                               
         CLC   QSTA,SPACES                                                      
         BNE   *+10                                                             
         MVC   QSTA(3),=C'ALL'                                                  
*                                                                               
         CLC   =C'ALL',QSTA                                                     
         BE    *+18                                                             
*                                                                               
         CLI   QSTA+4,C' '                                                      
         BNE   *+10                                                             
         MVC   QSTA+4(1),QMED                                                   
* GET QSTART AND QEND IN Y2K COMPAT FORMAT IF POSSIBLE                          
         LA    R1,QSTART                                                        
         BAS   RE,QY2K                                                          
         LA    R1,QEND                                                          
         BAS   RE,QY2K                                                          
*                                                                               
         CLC   QSTART,SPACES       IF THERE IS NO START DATE                    
         BNH   NREQ11                                                           
         CLC   QEND,SPACES         OR END DATE                                  
         BNH   NREQ11               SKIP TEST                                   
         CLC   QSTART,QEND         QSTART <= QEND?                              
         BNH   NREQ11               YES                                         
         MVC   P(33),=C'*** START PERIOD > END PERIOD ***'                      
         GOTO1 REPORT                                                           
         DC    H'0'                                                             
*                                                                               
QY2K     NTR1                      IF YEAR IS NUMERIC, --> Y2K FMT              
         LA    R4,2                                                             
         LR    R5,R1                                                            
*                                                                               
QY2KA    CLI   0(R5),C'0'                                                       
         BL    QY2KX                                                            
         CLI   0(R5),C'9'                                                       
         BH    QY2KX                                                            
         LA    R5,1(R5)                                                         
         BCT   R4,QY2KA                                                         
*                                                                               
         LR    R5,R1                                                            
         MVC   WORK(2),0(R5)                                                    
         MVC   WORK+2(2),=C'01'                                                 
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,WORK,WORK+6                                          
         MVC   0(2,R5),WORK+6      CHANGE YEAR BYTES                            
QY2KX    XIT1                                                                   
*                                                                               
NREQ11   MVC   CURTAB,=X'00000000215B4040' CURRENCY PREFIXED WITH C'$'          
         CLI   QLANG,C'F'          FRENCH REPORT REQUEST?                       
         BNE   NREQ12                                                           
         MVI   CURTAB+4,X'11'      CURRENCY SUFFIXED WITH C'$'                  
         MVI   RCCTRY,8            COUNTRY CANADA                               
         MVI   RCLANG,4            LANGUAGE FRENCH                              
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVI   MCCTRY,8            THESE MUST ALSO BE SET IN MASTC. . .         
         MVI   MCLANG,4            . . . SO THAT CTRY MACRO WILL WORK           
         DROP  R1                                                               
*                                                                               
NREQ12   CLI   QPROG,C'U'          SKIP REPORT FOR TURNAROUNDS                  
         BE    NREQ13                                                           
*                                                                               
         CLI   RCMULTIQ,C'Y'       MULTI-MEDIA REQUEST?                         
         BE    NREQ13A                                                          
         GOTO1 REQREP,DMCB                                                      
         OC    0(4,R1),0(R1)                                                    
         BZ    NREQ13                                                           
         AP    RCRQERR,=P'1'                                                    
         B     NREQ2                                                            
*                                                                               
NREQ13   AP    RCRQVAL,=P'1'                                                    
*                                                                               
* REMEMBER TO FIX EBCDIC AGENCY IF REQUIRED                                     
NREQ13A  CLC   SVAGY(3),QAGY       TEST SAME AGY/MED                            
         BE    NREQ14                                                           
         MVC   SVAGY(3),QAGY                                                    
         CLC   QPROG,=C'74'        SP74 DOES NOT REQUIRE MEDIA                  
         BE    NREQ13X                                                          
* FETCH NEW MEDIA                                                               
         GOTO1 MEDGET,DMCB,(QMED,SVAGY),DATAMGR,WORK                            
         CLI   RCMULTIQ,C'Y'       MULTI-MEDIA REQ?                             
         BNE   NREQ13K                                                          
*                                                                               
         CLI   DMCB+8,X'FF'                                                     
         BNE   NREQ13M                                                          
*                                                                               
NREQRQ1  LA    RF,NMEDLIST          IF MEDIA NOT VALID                          
*                                   OR CLIENT IS NOT FOUND                      
*                                   TRY THE NEXT MEDIA                          
NREQRQ2  CLC   QMED,0(RF)                                                       
         BE    NREQRQ4                                                          
         LA    RF,1(RF)                                                         
         B     NREQRQ2                                                          
NREQRQ4  DS    0H                                                               
         CLI   1(RF),X'FF'        NEXT IS LAST MEDIA                            
         BE    NREQRQ6            MEANS I'M DONE                                
*                                                                               
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   QAREA(80),MCREQREC  RESTORE ORIGINAL REQUEST                     
*                                                                               
         DROP  R1                                                               
*                                                                               
         MVC   QMED,1(RF)          THEN ALTER MEDIA                             
         B     NREQ13A             AND TRY ANOTHER MEDGET                       
*                                                                               
NREQRQ6  B     NREQ2               SKIP TO NEXT REQUEST                         
*                                                                               
*                                                                               
NREQ13M  DS    0H                  VALID MEDIA FOR MULTI-MEDIA REQ              
*                              NOW SEE IF CLIENT EXISTS FOR THIS MEDIA          
         CLC   =C'ALL',QCLT        CAN ONLY DO FOR ONE CLIENT REQUESTS          
         BE    NREQ13P                                                          
         CLI   QCLT,C'*'           TEST OFFICE REQUEST                          
         BE    NREQ13P                                                          
         CLI   QCLT,C'$'           TEST OFFICE LIST                             
         BE    NREQ13P                                                          
*                                                                               
         GOTO1 CLPACK,DMCB,QCLT,SVCLT                                           
*                                                                               
         CLI   0(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),WORK       AGY/MED FROM MEDGET                          
         MVC   KEY+2(2),SVCLT                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(4),KEYSAVE      SEE IF CLIENT EXISTS                         
         BNE   NREQRQ1             IF NOT, TRY ANOTHER MEDIA                    
*                                  FINALLY DO REQREP                            
NREQ13P  GOTO1 REQREP,DMCB                                                      
         OC    0(4,R1),0(R1)                                                    
         BZ    NREQ13M2                                                         
         AP    RCRQERR,=P'1'       HOPEFULLY WON'T FIND AN ERROR                
         B     NREQ2               IF SO JUST SKIP TO NEXT REQUEST              
*                                                                               
NREQ13M2 AP    RCRQVAL,=P'1'                                                    
         MVC   WORK+12(11),WORK     SAVE MEDGET'S RESULT                        
         CLC   QMKT,SPACES         RE-SET SOME STUFF                            
         BNE   *+10                                                             
         MVC   QMKT(3),=C'ALL'                                                  
*                                                                               
         CLC   QSTA,SPACES                                                      
         BNE   *+10                                                             
         MVC   QSTA(3),=C'ALL'                                                  
*                                                                               
         CLC   =C'ALL',QSTA                                                     
         BE    *+18                                                             
*                                                                               
         CLI   QSTA+4,C' '                                                      
         BNE   *+10                                                             
         MVC   QSTA+4(1),QMED                                                   
* GET QSTART AND QEND IN Y2K COMPAT FORMAT IF POSSIBLE                          
         LA    R1,QSTART                                                        
         BAS   RE,QY2K                                                          
         LA    R1,QEND                                                          
         BAS   RE,QY2K                                                          
         MVC   WORK(11),WORK+12    RESTORE MEDGET'S RESULT                      
*                                                                               
         B     NREQ13X                                                          
*                                                                               
*        GET HERE FOR NON MULTI-MEDIA REQUESTS                                  
*                                                                               
NREQ13K  CLI   DMCB+8,X'FF'        TEST MEDIA NOT VALID                         
         BE    NREQ2               SKIP TO NEXT REQUEST                         
*                                                                               
NREQ13X  XC    SVOFCLST,SVOFCLST   CLEAR PREVIOUS OFFICE LIST                   
*                                                                               
         MVC   SVAGYMD,WORK        BINARY A/M                                   
         MVC   BAGYMD,WORK                                                      
         PACK  BAGY,BAGYMD                                                      
         NI    BAGY,X'0F'          GIVE USER 1 BYTE AGY/RIGHT ALIGN             
         MVC   MED,QMED                                                         
         MVC   MEDNM,WORK+1                                                     
*                                                                               
NREQ14   DS    0H                                                               
         MVI   RQDPOVRD,C' '          RESET DPT MENU OVERRIDE                   
         MVI   RQRDPOL,C'N'                                                     
         MVI   RQSTAFLT,C' '                                                    
         MVI   RQBRDMON,C'Y'                                                    
         MVI   RQPRDEST,C'N'                                                    
         MVI   RQESTFLT,C'Y'                                                    
         MVI   RQMKTFLT,C'Y'                                                    
         MVI   RQPRGTYP,C' '                                                    
         MVC   PGR1(126),SPACES                                                 
         MVI   PGR1LEN,0                                                        
         MVI   PGR2LEN,0                                                        
         MVI   PGR3LEN,0                                                        
         MVC   MGR1(126),SPACES                                                 
         MVI   MGR1LEN,0                                                        
         MVI   MGR2LEN,0                                                        
         MVI   MGR3LEN,0                                                        
         MVC   RQCOST2,QCOST2         SET OPTION FROM REQUEST                   
         XC    BCLT(21),BCLT                                                    
         XC    BDATES,BDATES                                                    
         XC    SVDATA(SVDATAX-SVDATA),SVDATA                                    
         XC    SVCGRKEY,SVCGRKEY                                                
*                                                                               
         CLC   =C'**',QAGY                                                      
         BE    NREQ20                                                           
*                                                                               
         MVC   WORK(12),=CL12'S000'                                             
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
         CLC   QCLT,=C'ALL'                                                     
         BE    NREQ16                                                           
         CLI   QCLT,C'*'           TEST OFFICE REQUEST                          
         BE    NREQ16                                                           
         CLI   QCLT,C'$'           TEST OFFICE LIST                             
         BE    NREQ16                                                           
* READ CLIENT HEADER FOR OFFICE CODE TO READ OFFICE PROFILE                     
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAGYMD                                                 
         GOTO1 CLPACK,DMCB,QCLT,KEY+2                                           
         GOTO1 READ                                                             
         GOTO1 GETCLT                                                           
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  R6                                                               
NREQ16   DS    0H                                                               
         GOTO1 GETPROF,DMCB,WORK,SPOTPROF,DATAMGR                               
         MVC   WORK+2(2),RCPROG                                                 
         GOTO1 (RF),(R1),,PROGPROF                                              
         SPACE 1                                                                
         MVI   BFILTER,0           CLEAR FILM TYPE                              
         XC    CMLPTR,CMLPTR       CLEAR CMML LIST POINTER                      
*                                                                               
NREQ20   CR    RB,RB                                                            
         B     NREQX                                                            
*                                                                               
NREQNE   LTR   RB,RB                                                            
*                                                                               
NREQX    XIT1                                                                   
         LTORG                                                                  
*                                                                               
*                                                                               
NMEDLIST DC    C'RTNX'       SPOT MEDIA LIST                                    
         DC    X'FF'         END OF LIST                                        
*                                                                               
         EJECT                                                                  
*=============================================================                  
* BUILD LIST OF STATIONS IN MARKET AND SORT TO ALPHA SEQ                        
* LIST FORMAT IS PACKED STATION (3)                                             
*                DC X'00'                                                       
*                UNPACKED STATION (8)                                           
*=============================================================                  
         SPACE 1                                                                
BLDSTLST NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   BLDSVBTS,DMINBTS    SAVE DMINBTS/DMOUTBTS                        
         MVI   DMINBTS,X'08'       PASS DELETES                                 
         MVI   DMOUTBTS,X'FD'                                                   
*                                                                               
         L     R1,=A(STAWORK)      INITIALIZE STAWORK                           
         XC    0(L'STAWORK,R1),0(R1)                                            
         USING STAPACKD,R1                                                      
         MVC   STAPAGY,AGY                                                      
         MVC   STAPCTRY,COUNTRY                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         DROP  R1                                                               
*                                                                               
         L     R0,ADSTALST         CLEAR STATION LIST BUILD AREA                
         LHI   R1,12*1024                                                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   SVBUYKEY,KEY        SAVE INITIAL BUY KEY                         
         L     R4,ADSTALST         INITIALIZE LIST POINTER                      
         LHI   R5,1024-1           MAXIMUM ENTRIES                              
*                                                                               
         CLI   BLDSVCBL,C'Y'       TEST ONE SYSCODE REQUEST                     
         BNE   BLST2                                                            
*                                                                               
         L     R1,=A(STAWORK)                                                   
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'P'                                                     
         MVC   STAPQMKT,QMKT                                                    
         MVC   STAPQSTA,QSTA       MKTSTA                                       
         GOTO1 VSTAPACK,(R1)                                                    
         CLI   STAPERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   KEY+6(3),STAPSTA    SET STATION IN KEY                           
         MVC   SVBUYKEY,KEY        AND SAVE KEY AGAIN                           
         DROP  R1                                                               
*                                                                               
BLST2    GOTO1 HIGH                                                             
         B     BLST6                                                            
*                                                                               
BLST4    GOTO1 SEQ                                                              
*                                                                               
BLST6    CLC   KEY(6),SVBUYKEY     SAME A-M/CLT/PRD/MKT                         
         BNE   BLST30                                                           
*                                                                               
         CLI   BLDSVCBL,C'Y'       TEST ONE SYSCODE REQUEST                     
         BNE   BLST7                                                            
*                                                                               
         L     R0,=X'00FFFF80'     SET MASK TO DROP NETWORK                     
         ICM   RE,7,KEY+6          GET ACTUAL STATION                           
         NR    RE,R0               DROP NETWORK BITS                            
         ICM   RF,7,SVBUYKEY+6     GET REQUESTED STATION                        
         NR    RF,R0                                                            
         CR    RE,RF               TEST SAME SYSCODE                            
         BNE   BLST30                                                           
*                                                                               
BLST7    CLI   BEST,0              FILTER ON ESTIMATE                           
         BE    BLST10                                                           
         CLC   KEY+9(1),BEST                                                    
         BE    BLST20                                                           
         BL    BLST8                                                            
         CLC   KEY+9(1),BESTEND                                                 
         BNH   BLST20                                                           
         MVC   KEY+9(4),=4X'FF'    NEXT STA                                     
         B     BLST2                                                            
*                                                                               
BLST8    MVC   KEY+9(1),BEST       READ FOR ESTIMATE                            
         XC    KEY+10(3),KEY+10                                                 
         B     BLST2                                                            
*                                                                               
BLST10   SR    RE,RE               ESTIMATE=NO OPTION                           
         IC    RE,KEY+9                                                         
         LA    RE,ESTLST(RE)                                                    
         CLI   0(RE),0                                                          
         BNE   BLST20                                                           
         MVC   KEY+10(3),=4X'FF'   NEXT EST                                     
         B     BLST2                                                            
         SPACE 1                                                                
*===============================================================                
* GET UNPACKED STATION AND SAVE IN LIST                                         
*===============================================================                
         SPACE 1                                                                
BLST20   GOTO1 GETBUY              REC MUST NOT BE DELETED                      
         L     RE,ADBUY                                                         
         TM    15(RE),X'80'        TEST DELETED                                 
         BO    BLST4                                                            
         TM    DMCB+8,X'12'        TEST NOT FOUND OR DELETED                    
         BNZ   BLST4                                                            
*                                                                               
         L     R1,=A(STAWORK)                                                   
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPMKST,KEY+4      MKTSTA                                       
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         CLI   STAPERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                <<<IN THEORY, TEST ONLY                      
*                                                                               
         MVC   0(3,R4),KEY+6       SAVE PACKED STATION                          
         MVC   4(5,R4),STAPQSTA    EBCDIC STATION                               
         MVC   9(3,R4),STAPQNET    NETWORK                                      
*                                                                               
         MVC   KEY+9(4),=4X'FF'    FORCE NEXT STA                               
*                                                                               
         LA    R4,12(R4)           NEXT LIST ENTRY                              
         BCT   R5,BLST2                                                         
         DC    H'0'                                                             
         DROP  R1                                                               
         SPACE 1                                                                
*============================================================                   
* SORT STATION LIST BY ALPHA STATION                                            
*============================================================                   
         SPACE 1                                                                
BLST30   MVC   KEY,SVBUYKEY        RESTORE INITIAL BUY KEY                      
*                                                                               
         LHI   R0,1024-1                                                        
         SR    R0,R5               GIVES NUMBER OF RECORDS                      
         BZ    BLSTX                                                            
         GOTO1 XSORT,DMCB,ADSTALST,(R0),12,8,4                                  
BLSTX    MVC   DMINBTS(2),BLDSVBTS                                              
         XIT1                                                                   
BLDSVBTS DS    XL2                                                              
BLDSVCBL DS    C                                                                
         DS    X                   ALIGNMENT                                    
         LTORG                                                                  
         EJECT                                                                  
*=============================================================                  
* CALL OFFICER TO SEE IF ACCESS TO THIS CLIENT IS AUTHORIZED                    
* IF NOT, EXIT WITH CC NEQ TO GO ON TO NEXT CLIENT                              
*=============================================================                  
                                                                                
CALLOFCR NTR1  BASE=*,LABEL=*                                                   
         L     R6,ADCLT                                                         
         USING CLTHDR,R6                                                        
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
*                                                                               
         XC    WORK,WORK                                                        
OFFD     USING OFFICED,WORK                                                     
         MVI   OFFD.OFCSYS,C'S'                                                 
         MVC   OFFD.OFCLMT,MCRHACCS    N.B. - MOVES 2 CHARS ONLY                
         MVC   OFFD.OFCAUTH,MCRHACCS                                            
         MVC   OFFD.OFCAGY,SVAGY                                                
         MVC   OFFD.OFCOFC,COFFICE                                              
         MVC   OFFD.OFCACCSC,CACCESS                                            
         MVI   OFFD.OFCACCSM,X'FF'  IGNORE MKT LIMIT ACCESS FOR CLT             
         MVC   OFFD.OFCCLT,CLT                                                  
         MVC   OFFD.OFCSAGMD,BAGYMD                                             
         MVC   OFFD.OFCSECD,MCASECRT                                            
         DROP  R1,R6                                                            
*                                                                               
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         L     RF,VOFFICER                                                      
         DROP  RE                                                               
         GOTO1 (RF),DMCB,(C'2',WORK),(0,ACOMFACS)                               
         CLI   0(R1),0              =0 MEANS HAVE ACCESS                        
         XIT1                       EXIT WITH CC SET                            
         DROP  OFFD                                                             
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE DDOFFICED                                                      
DBDUMMY  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DDCOMFACS                                                      
SPGEND   DSECT                                                                  
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE SPGENAGY                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENBILL                                                      
       ++INCLUDE SPGENBUY                                                       
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPGENMKG                                                       
       ++INCLUDE SPTRCMML                                                       
       ++INCLUDE SPGENSTAT                                                      
       ++INCLUDE SPGENGRP                                                       
       ++INCLUDE SPGENCLG                                                       
       ++INCLUDE SPGENWIPW                                                      
       ++INCLUDE SPGETBUYD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'138SPFILCON  07/20/20'                                      
         END                                                                    
