*          DATA SET MYBINSR31  AT LEVEL 008 AS OF 07/24/06                      
*CATALP MYBIN31                                                                 
         SPACE 1                                                   *            
*==================================================================*            
*                                                                  *            
*   PARAMETER 1            A(RECORD TO BE ADDED OR FOUND)          *            
*                                                                  *            
*   PARAMETER 2            A(TABLE)                                *            
*                                                                  *            
*   PARAMETER 3            NUMBER OF RECORDS IN TABLE SO FAR.      *            
*                          THIS WILL BE UPDATED BY BINSRCH.        *            
*                                                                  *            
*   PARAMETER 4  BYTE 0    X'00' = FIND A RECORD                   *            
*                          X'01' = INSERT IF NOT FOUND             *            
*                          X'02' = READ HIGH                       *            
*                          X'80' = DELETE                          *            
*                BYTE 1    **SPARE**                               *            
*                BYTE 2-3  L'RECORD                                *            
*                                                                  *            
*   PARAMETER 5  BYTE 0    DISPLACEMENT OF KEY INTO RECORD         *            
*                BYTE 1-3  L'KEY (MAX=255)                         *            
*                                                                  *            
*   PARAMETER 6            MAXIMUM NUMBER OF RECORDS IN TABLE      *            
*                                                                  *            
*   AFTER BINSRCH                                                  *            
*   -------------                                                  *            
*                                                                  *            
*   PARAMETER 1            X'80' = RECORD NOT FOUND                *            
*                          A(RECORD FOUND) OR                      *            
*                          A(WHERE RECORD HAS BEEN INSERTED) OR    *            
*                          A(0) WHEN TABLE IS FULL                 *            
*==================================================================*            
MYBIN31  TITLE '31-BIT BINARY SEARCH ROUTINES'                                  
MYBIN31  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKX-WRKD,MYBIN31                                                
         USING WRKD,RC                                                          
*                                                                               
         LR    R9,R1                                                            
         USING BSPARA,R9                                                        
*                                                                               
         LM    R2,R3,BSP2          R2-->TABLE   R3-->COUNT                      
*                                                                               
         LH    R4,BSP4+2           R4 --> RECORD LENGTH                         
         LH    R5,BSP5+2           R5 --> KEY LENGTH                            
         BCTR  R5,0                  -1 (FOR EX)                                
         SR    R6,R6                                                            
         ICM   R6,1,BSP5           R6 --> KEY DSPL                              
*                                                                               
         L     R7,BSP6             R7 --> MAX RECORDS                           
         L     R8,BSP1             R8 --> A(RECORD)                             
*                                                                               
         LTR   R3,R3               ANY DATA IN TABLE                            
         BNZ   BS1                  YES - SEARCH FOR EQUAL                      
         TM    BSP4,X'01'          INSERT IF NOT FOUND                          
         BO    AFRST                YES - ADD TO TABLE                          
*                                                                               
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         XC    BSPAREC,BSPAREC                                                  
         B     BSEXIT                                                           
         EJECT                                                                  
AFRST    LA    R3,1(R3)            BUMP RECORD COUNTER                          
         ST    R3,BSPNOR                                                        
         BCTR  R4,0                R4 --> RECLEN-1                              
         ST    R2,BSPAREC                                                       
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
*                                                                               
AFRST1   CH    R4,=H'256'                                                       
         BL    AFRST2                                                           
         MVC   0(255,R2),0(R8)                                                  
         LA    R2,255(R2)                                                       
         LA    R8,255(R8)                                                       
         SH    R4,=H'255'                                                       
         B     AFRST1                                                           
*                                                                               
AFRST2   LTR   R4,R4                                                            
         BZ    *+8                                                              
         EX    R4,*+8                                                           
         B     BSEXIT                                                           
         MVC   0(0,R2),0(R8)                                                    
         EJECT                                                                  
BS1      LR    R1,R3               CALCULATE END OF TABLE                       
         BCTR  R1,0                                                             
         MH    R1,BSPLENR+2                                                     
         AR    R1,R2                                                            
         LA    RF,0(R6,R8)         CALCULATE KEY DISPLACEMENTS                  
         LA    RA,0(R6,R2)                                                      
         LR    R0,R4               INCREMENT VALUE                              
* ===> TEST ONLY                                                                
         MVC   HOLD(8),=C'**KEY=**'                                             
         MVC   HOLD+8(48),0(RF)                                                 
         MVC   HOLD+56(8),=C'**REGS**'                                          
         STM   R0,RF,HOLD+64       <===== TEST ONLY                             
* ===> TEST ONLY                                                                
         CH    R3,=H'10'           LT. 10 ENTRIES - LINEAR SEARCH               
         BH    BSS1                                                             
         AR    R1,R6               BUMP BY KEY DISPLACEMENT                     
BS2      EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RA),0(RF)                                                    
         BNE   BS3                                                              
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         B     SETOUTA                                                          
BS3      BH    *+8                                                              
         BXLE  RA,R0,BS2                                                        
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         B     BS4                                                              
         EJECT                                                                  
BSS1     AR    R1,R4               BINARY SEARCH ROUTINE                        
         BCTR  R1,R0                                                            
         LR    R0,R4                                                            
         SR    R2,R0                                                            
         SR    R1,R2                                                            
         BXLE  0,0,*                                                            
         AR    R1,R2                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CR    R0,R4                                                            
         BL    BSS3                                                             
         BXH   R2,R0,BSS2                                                       
         LA    RA,0(R6,R2)                                                      
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),0(RA)                                                    
         BE    SETOUTA                                                          
         BHR   RE                                                               
*                                                                               
BSS2     SR    R2,R0                                                            
         BR    RE                                                               
*                                                                               
BSS3     LA    R1,1(R1)                                                         
         AR    R2,R4                                                            
*                                                                               
BS4      TM    BSP4,X'01'          ADD                                          
         BO    *+12                 YES - ADD IN SEQUENCE                       
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         B     BSEXIT                                                           
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         L     R3,BSPNOR                                                        
         B     BS5                                                              
*                                                                               
BS4A     XC    BSPAREC,BSPAREC     NO - RETURN END OF TABLE                     
         B     BSEXIT                                                           
*                                                                               
BS5      MVI   BSP4,X'01'          SLIDE TABLE FOR INSERT                       
         CH    R3,=H'10'                                                        
         BH    *+6                                                              
         AR    R1,R4                                                            
         LR    RF,R1                                                            
         CR    R3,R7               INPUT NO ENTRY LT. MAX NO ENTRY              
         BL    *+8                  YES - ADD ENTRY                             
         B     BS4A                 NO - RETURN END OF TABLE                    
         CR    R2,RF                                                            
         BE    AFRST                                                            
         EJECT                                                                  
SLD1     LR    RA,RF                                                            
         SR    RA,R2               LENGTH OF MOVE                               
         LR    RE,RF                                                            
         AR    RF,R4                                                            
*                                                                               
SLD1A    CH    RA,=H'255'                                                       
         BL    SLD2                                                             
         SH    RE,=H'255'                                                       
         SH    RF,=H'255'                                                       
         MVC   HOLD,0(RE)                                                       
         MVC   0(255,RF),HOLD                                                   
         SH    RA,=H'255'                                                       
         B     SLD1A                                                            
*                                                                               
SLD2     SR    RE,RA                                                            
         SR    RF,RA                                                            
         LTR   RA,RA                                                            
         BZ    AFRST                                                            
         BCTR  RA,0                                                             
         EX    RA,*+8                                                           
         B     *+10                                                             
         MVC   HOLD(1),0(RE)                                                    
         EX    RA,*+8                                                           
         B     AFRST                                                            
         MVC   0(1,RF),HOLD                                                     
*                                                                               
SETOUTA  ST    R2,BSPAREC          SET OUTPUT RECORD ADDRESS                    
         NI    BSP1,X'7F'          TURN OFF NOT FOUND                           
*                                                                               
         TM    BSP4,X'80'          DELETE RECORD FROM BINSRCH TABLE             
         BZ    BSEXIT2                                                          
         L     R5,BSPNOR           SET END OF TABLE ADDRESS                     
         BCTR  R5,0                DECREMENT FOR DELETED SLOT                   
         MH    R5,BSPLENR+2                                                     
         A     R5,BSP2             ADD TABLE ADDRESS                            
         SR    R5,R2               SET CURRENT RECORD                           
*                                                                               
DEL1     CH    R5,=H'255'                                                       
         BL    DEL2                                                             
         LR    RF,R2                                                            
         A     RF,BSPLENR                                                       
         MVC   0(255,R2),0(RF)                                                  
         SH    R5,=H'255'                                                       
         AH    R2,=H'255'                                                       
         B     DEL1                                                             
*                                                                               
DEL2     LTR   R5,R5               END                                          
         BZ    DEL3                 YES - DECREMENT TABLE COUNT                 
         LR    RF,R2                                                            
         A     RF,BSPLENR                                                       
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
*                                                                               
DEL3     L     R5,BSPNOR                                                        
         BCTR  R5,0                                                             
         ST    R5,BSPNOR                                                        
         EJECT                                                                  
BSEXIT   TM    BSP4,X'02'          TEST READ HIGH                               
         BZ    BSEXIT2                                                          
         XC    BSPAREC,BSPAREC                                                  
         MH    R7,BSPLENR+2        CALCULATE TABLE END ADDRESS                  
         A     R7,BSPSTRT                                                       
         BCTR  R7,0                                                             
         CLR   R2,R7               FZSA                                         
         BL    *+8                                                              
         B     BSEXIT2              YES - MAINTAIN REC NOT FOUND                
         ST    R2,BSPAREC                                                       
         NI    BSP1,X'7F'                                                       
BSEXIT2  XIT1                                                                   
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
HOLD     DS    CL256                                                            
WRKX     EQU   *                                                                
       ++INCLUDE DDBSPARA                                                       
         ORG   BSPARA                                                           
BSP1     DS    F                                                                
BSP2     DS    F                                                                
BSP3     DS    F                                                                
BSP4     DS    F                                                                
BSP5     DS    F                                                                
BSP6     DS    F                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008MYBINSR31 07/24/06'                                      
         END                                                                    
