*          DATA SET TALDPRGE   AT LEVEL 038 AS OF 03/07/16                      
*CATALP TALDPRGE                                                                
         TITLE 'LDPURGE - TALENT - LOAD PURGE TEST'                             
*                                                                               
*        UPSI  XXXXXXX1 ACTIVATE FILE MAINTENANCE                               
*                       DROP RECORDS WITH DELETE BIT ON                         
*                       DROP FTRACK ELEMENTS OLDER THAN 24 MONTHS               
*                                                                               
*        UPSI  XXXXXX1X DROP RECORDS WITH X'01' STATUS BIT ON                   
*                       THESE ARE CHECKS AND COMMERCIALS                        
*                       NOTE: RELATED CO RECS NOT PREVIOUSLY MARKED             
*                             ALSO GET DROPPED                                  
*                                                                               
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
*                                                                               
         ENTRY TOTPRGS                                                          
         ENTRY INVCASH                                                          
         ENTRY COMFLAG                                                          
*                                                                               
         NMOD1 0,TALDPRGE                                                       
         SPACE 2                                                                
         ST    R1,SAVER1           STORE PARAMETER REG IN SAVER1                
         L     R8,0(R1)            R8=A(RECORD)                                 
         USING TLRCD,R8                                                         
*                                                                               
         NC    0(3,R8),0(R8)                                                    
         BZ    XIT                 IGNORE HEADER RECORDS                        
*                                                                               
         CLI   0(R8),X'FF'         IF TRAILER RECORD                            
         BE    XIT                                                              
*                                                                               
* TALENT FILE DELETE MECHANISM (UPSI=XXXXXXX1)                                  
*                                                                               
         L     RE,=V(UPSIVAL)      COPY UPSI                                    
         TM    0(RE),X'01'         TEST FOR DELETION REQUESTED                  
         BZ    XIT                 XIT IF NOT                                   
*                                                                               
         OC    TODAY0,TODAY0       IF DATES NOT PREVIOUSLY SET                  
         BNZ   LDPRGE10                                                         
         GOTO1 =V(DATCON),DMCB,(5,0),(0,TODAY0)      GET TODAY'S DATE           
         GOTO1 =V(ADDAY),DMCB,TODAY0,WORK,-30                                   
         GOTO1 =V(DATCON),DMCB,(0,WORK),(1,TODY1_30) MINUS 30 DAYS              
         GOTO1 =V(ADDAY),DMCB,(C'Y',TODAY0),WORK,-1                             
         GOTO1 =V(DATCON),DMCB,(0,WORK),(1,TODY1_1)  MINUS 1 YEAR               
         GOTO1 =V(ADDAY),DMCB,(C'Y',TODAY0),WORK,-2                             
         GOTO1 =V(DATCON),DMCB,(0,WORK),(1,TODY1_2)  MINUS 2 YEARS              
*                                                                               
         GOTO1 =V(DATCON),DMCB,(5,0),(1,SNDDVCUT)                               
         ZIC   RE,SNDDVCUT         IF TODAY IS MARCH OR EARLIER                 
         SHI   RE,2                SET TO DELETE SENT ADVICES FROM              
         CLI   SNDDVCUT+1,3        3 YEARS AGO                                  
         BH    *+6                 IF LATER THAN MARCH, SET TO                  
         BCTR  RE,0                DELETE FROM 2 YEARS AGO                      
         STC   RE,SNDDVCUT                                                      
         MVC   SNDDVCUT+1(2),=X'1231'                                           
*                                                                               
LDPRGE10 CLI   TLRCCD,TLDVCDQ      ADVICE RECORDS                               
         BNE   LDPRGE20                                                         
         BAS   RE,DVTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
LDPRGE20 CLI   TLRCCD,TLDCCDQ      ADVICE CAST RECORDS                          
         BNE   *+12                                                             
         BAS   RE,DCTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLNXCDQ      HOLD RECORDS                                 
         BNE   *+12                                                             
         BAS   RE,NXTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLWTCDQ      WEB TRANSACTION RECORDS                      
         BNE   *+12                                                             
         BAS   RE,WTTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLSYCDQ      FILENAME RECORDS                             
         BNE   *+12                                                             
         BAS   RE,FNTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLINCDQ      INVOICE RECORDS                              
         BNE   *+12                                                             
         BAS   RE,INTEST1          TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLJBCDQ      JOB RECORDS                                  
         BNE   *+12                                                             
         BAS   RE,JBTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLSCCDQ      SCREEN RECORDS                               
         BNE   *+12                                                             
         BAS   RE,SCTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLCACDQ      CAST RECORDS                                 
         BNE   *+8                                                              
         BAS   RE,FTTEST           SEE IF FTRACK CAN BE DELETED                 
*                                                                               
         CLI   TLRCCD,TLSYCDQ      SYSTEM RECORD                                
         BNE   *+8                                                              
         BAS   RE,SYTEST           SEE IF VIOLATION EL'S CAN BE DELETED         
         EJECT                                                                  
*                                                                               
* TALENT FILE ADDITIONAL DELETE MECHANISM (UPSI=XXXXXX1X)                       
*                                                                               
         L     RE,=V(UPSIVAL)      COPY UPSI                                    
         TM    0(RE),X'02'         TEST FOR ADDITIONAL DELETION                 
         BZ    DECIDE                                                           
*                                                                               
         CLI   TLRCCD,TLCKCDQ      CHECK RECORDS                                
         BNE   *+12                                                             
         BAS   RE,CKTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLCOCDQ      COMMERCIAL RECORDS                           
         BNE   *+12                                                             
         BAS   RE,COTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLCACDQ      CAST RECORDS                                 
         BNE   *+12                                                             
         BAS   RE,CATEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLINCDQ      INVOICE RECORDS                              
         BNE   *+12                                                             
         BAS   RE,INTEST2          TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLUHCDQ      USAGE HISTORY RECORDS                        
         BNE   *+12                                                             
         BAS   RE,UHTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         CLI   TLRCCD,TLHCCDQ      HISTORY COMMENT RECORDS                      
         BNE   *+12                                                             
         BAS   RE,HCTEST           TEST WHETHER WE NEED TO DELETE               
         BE    DELETE                                                           
*                                                                               
         B     DECIDE              DECIDE IF WE SHOULD DELETE                   
         EJECT                                                                  
         USING TLRCD,R8                                                         
DELETE   OI    TLRCSTAT,X'80'      DELETING IN DUMP                             
         SPACE 1                                                                
*&&DO                                                                           
         L     RA,=V(CPRINT)       TEMP CODE TO PRINT DELETED RECORDS           
         USING DPRINT,RA                                                        
         MVC   P(7),=CL7'DELETE:'                                               
         GOTO1 =V(HEXOUT),DMCB,TLRCKEY,P+7,L'TLRCKEY,=C'TOG'                    
         GOTO1 =V(PRINTER)                                                      
*&&                                                                             
         SPACE 1                                                                
DECIDE   TM    TLRCSTAT,X'80'      IS RECORD DELETED ?                          
         BZ    XIT                 NO - NO NEED TO PURGE                        
         L     R1,SAVER1                                                        
         MVI   0(R1),X'FF'         YES - RETURN PURGE FLAG                      
         B     XIT                                                              
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING OLD/COMPLETED ADVICE RECORDS                        
*                                                                               
         USING TLDVD,R8            R8=A(RECORD)                                 
DVTEST   NTR1                                                                   
*                                                                               
         USING TAACD,R4                                                         
         LR    R4,R8                                                            
         MVI   ELCODE,TAACELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
DVA      BAS   RE,NEXTEL                                                        
         BNE   DVB                                                              
         OC    TAACSCR,TAACSCR     IF LAST ACTIVITY DATE IS EQUAL               
         BNZ   DVA                 TO OR EARLIER THAN CUTOFF DATE               
         CLC   TAACCDTE,SNDDVCUT   DELETE IT                                    
         BNH   DV50                                                             
         DROP  R4                                                               
*                                                                               
         USING TADVD,R4                                                         
DVB      MVI   ELCODE,TADVELQ      GET ADVICE DETAILS EL.                       
         LR    R4,R8                                                            
         BAS   RE,GETEL                                                         
         BNE   DV30                                                             
*                                                                               
         OC    TADVSDTE,TADVSDTE   IF ADVICE HAS A SENT DATE                    
         BZ    DV00                                                             
         CLC   TADVSDTE,SNDDVCUT   DELETE IF NOT LATER THAN                     
         BNH   DV50                CUTOFF DATE                                  
*                                                                               
DV00     CLI   TADVTYPE,TADVTYPS   IF SESSION ADVICE                            
         BNE   DV30                                                             
         OC    TADVRDTE,TADVRDTE   AND ADVICE IS RECEIVED                       
         BZ    DV10                                                             
         CLC   TADVRDTE,TODY1_30   IF RCV DATE < 30 DAYS OLD                    
         BH    DVNO                THEN NOT OK TO DELETE YET                    
         B     DV50                                                             
         DROP  R4                                                               
*                                                                               
         USING TAACD,R4                                                         
DV10     LR    R4,R8               ELSE, IF SESSION ADVICE NOT                  
         MVI   ELCODE,TAACELQ      RECEIVED YET                                 
         BAS   RE,GETEL            BUT ACTIVITY DATE => 1 YEAR OLD              
         B     *+8                 THEN OK TO DELETE                            
DV20     BAS   RE,NEXTEL                                                        
         BNE   DVNO                                                             
         OC    TAACSCR,TAACSCR                                                  
         BNZ   DV20                                                             
         CLC   TAACCDTE,TODY1_1                                                 
         BH    DVNO                                                             
         B     DV50                                                             
         DROP  R4                                                               
*                                                                               
         USING TAACD,R4                                                         
DV30     LR    R4,R8               ELSE, IF ADVICE WAS COMPLETED MORE           
         MVI   ELCODE,TAACELQ      THAN 30 DAYS AGO, OK TO DELETE               
         BAS   RE,GETEL                                                         
         B     *+8                                                              
DV40     BAS   RE,NEXTEL                                                        
         BNE   DVNO                                                             
         CLI   TAACSCR,X'7A'                                                    
         BNE   DV40                                                             
         CLC   TAACCDTE,TODY1_30                                                
         BH    DVNO                                                             
         DROP  R4                                                               
*                                                                               
         USING TANXD,R4                                                         
DV50     LR    R4,R8               ELSE IF ADVICE HAS TANX ELEMENT              
         MVI   ELCODE,TANXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   DV60                NO, OK TO DELETE                             
*                                                                               
         MVI   TLPVCD,TLPVCDQ      CHANGE KEY TO ARCHIVE ADVICE                 
         MVI   TLPVSCD,TLPVSCDQ                                                 
                                                                                
         L     R1,SAVER1                                                        
         MVI   0(R1),X'FE'         YES - RETURN CHANGED FLAG                    
*                                                                               
         B     DVNO                                                             
*                                                                               
DV60     BAS   RE,ADDADV           ADD TO TABLE FOR DCTEST                      
         BNE   DVNO                TABLE FULL- CAN'T DELETE THIS TIME           
*                                                                               
DVYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
DVNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SAVE ADVICE INFORMATION IN IN TABLE                   
         SPACE                                                                  
ADDADV   NTR1                                                                   
         OC    ANXTADV,ANXTADV     IF A(NEXT ENTRY) NOT DEFINED                 
         BNZ   *+10                                                             
         MVC   ANXTADV,=A(ADVTAB)  SET TO BEGINNING OF TABLE                    
*                                                                               
         USING ADVTABD,R2                                                       
         L     R2,ANXTADV          IF END OF TABLE                              
         CLI   0(R2),X'FF'                                                      
         BE    DVNO                                                             
*                                                                               
         MVC   ADVAGY,TLDVAGY      SAVE KEY INFORMATION                         
         MVC   ADVCID,TLDVCID                                                   
         MVC   ADVADV,TLDVADV                                                   
         LA    R2,ADVTABL(R2)      BUMP TO NEXT ENTRY                           
         ST    R2,ANXTADV          SAVE A(NEXT AVAILABLE ENTRY)                 
         B     DVYES                                                            
         DROP  R2                                                               
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING OLD ADVICE CAST RECORDS                             
*                                                                               
         USING TLDCD,R8            R8=A(RECORD)                                 
DCTEST   NTR1                                                                   
         L     R2,=A(ADVTAB)       R2=A(DELETED ADVICE TABLE)                   
         USING ADVTABD,R2                                                       
*                                                                               
DCTEST2  CLI   0(R2),X'FF'         TEST END OF TABLE                            
         BE    DCNO                                                             
         OC    0(ADVTABL,R2),0(R2) OR NO MORE ENTRIES                           
         BZ    DCNO                                                             
         CLC   TLDCAGY,ADVAGY      MATCH ON AGENCY,CID, AND ADVICE              
         BNE   DCTEST5                                                          
         CLC   TLDCCID,ADVCID                                                   
         BNE   DCTEST5                                                          
         CLC   TLDCADV,ADVADV                                                   
         BE    DCYES                                                            
*                                                                               
DCTEST5  LA    R2,ADVTABL(R2)      BUMP TO NEXT ENTRY                           
         B     DCTEST2                                                          
*                                                                               
DCYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
DCNO     LTR   R8,R8                                                            
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING OLD HOLD RECORDS                                    
*                                                                               
         USING TLNXD,R8            R8=A(RECORD)                                 
NXTEST   NTR1                                                                   
         TM    TLNXSTAT,TLNXSDEL   IF DELETED                                   
         BO    NX10                                                             
         TM    TLNXSTAT,TLNXSUSD   OR USED                                      
         BZ    NXNO                                                             
*                                                                               
NX10     MVC   THREE,TLNXDATE      USE DATE ADDED                               
         XC    THREE,XFFS                                                       
*                                                                               
         CLC   THREE,TODY1_30      IF ADDED DATE IS GT TODAY-30 DAYS            
         BH    NXNO                THEN NOT OK TO DELETE YET                    
*                                                                               
NXYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
NXNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*  ROUTINE HANDLES DROPPING OLD WEB TRANSACTION RECORDS                         
*                                                                               
         USING TLWTD,R8            R8=A(RECORD)                                 
WTTEST   NTR1                                                                   
*                                                                               
         CLC   TLWTDATE,TODY1_30   IF TRANSACTION DT IS GT TODAY-30 DYS         
         BH    WTNO                THEN NOT OK TO DELETE YET                    
*                                                                               
WTYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
WTNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*  ROUTINE HANDLES DROPPING FILENAME RECORDS                                    
*                                                                               
         USING TLSYD,R8            R8=A(RECORD)                                 
FNTEST   NTR1                                                                   
*                                                                               
         CLI   TLSYTYPE,TLSYSNFN   FILENAME RECORD?                             
         BL    FNNO                NO - DON'T DELETE                            
*                                                                               
         CLC   TLSYSNDT,TODY1_30   IF TRANSACTION DT IS GT TODAY-30 DYS         
         BH    FNNO                THEN NOT OK TO DELETE YET                    
*                                                                               
FNYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
FNNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING INVOICE RECORDS                                     
*                                                                               
         USING TLIND,R8            R8=A(RECORD)                                 
INTEST1  NTR1                                                                   
         LR    R4,R8                                                            
         TM    TLINSTAT,TLINSDEL   IF SOFT DELETED                              
         BZ    IN1NO                                                            
*                                                                               
         USING TAACD,R4                                                         
         LR    R4,R8                                                            
         MVI   ELCODE,TAACELQ      OK TO DELETE IF DELETED MORE THAN            
         BAS   RE,GETEL            1 YEAR AGO                                   
         B     *+8                                                              
IN110    BAS   RE,NEXTEL                                                        
         BNE   IN1NO                                                            
         CLI   TAACSCR,X'1F'                                                    
         BNE   IN110                                                            
         CLC   TAACCDTE,TODY1_1                                                 
         BH    IN1NO                                                            
         DROP  R4                                                               
*                                                                               
         AP    INCOUNT,=P'1'       ADD TO INVOICE DELETE COUNT                  
*                                                                               
IN1YES   XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
IN1NO    LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING OLD JOB RECORDS                                     
*                                                                               
         USING TLJBD,R8            R8=A(RECORD)                                 
JBTEST   NTR1                                                                   
         CLI   TLJBSPCL,TLJBSPJW   SPECIAL JWT JOBS                             
         BE    JB050                                                            
         CLI   TLJBSPCL,TLJBSPBD   SPECIAL BBDO JOBS                            
         BE    JB060                                                            
*                                                                               
         CLC   =C'0001ES',TLJBAGY  NEVER PURGE FQA JOB RECORDS                  
         BE    JBNO                                                             
         CLC   =C'GR',TLJBAGY      NEVER PURGE GREY JOB RECORDS                 
         BE    JBNO                                                             
*                                                                               
         MVC   THREE,TLJBDTE       DATE VALID                                   
         B     JB100                                                            
*                                                                               
JB050    MVC   THREE,TLJBDATE      JWT JOB DATE                                 
         B     JB100                                                            
*                                                                               
JB060    MVC   THREE,TLJBDAT       BBDO JOB DATE                                
*                                                                               
JB100    XC    THREE,XFFS                                                       
*                                                                               
         CLC   THREE,TODY1_30      IF DATE IS GT TODAY-30 DAYS                  
         BH    JBNO                THEN NOT OK TO DELETE YET                    
*                                                                               
JBYES    XR    R8,R8               ELSE SET TO DELETE - RETURN CC EQ            
JBNO     LTR   R8,R8                                                            
         B     XIT                                                              
*                                                                               
*  ROUTINE HANDLES DROPPING OLD SCREEN RECORDS                                  
*                                                                               
         USING TLSCD,R8            R8=A(RECORD)                                 
SCTEST   NTR1                                                                   
         MVC   BYTE,TLSCINV        ASSUME YEAR IN 1ST BYTE                      
         CLI   TLSCINV,X'20'       IF YYYYMM FORMAT,                            
         BE    SCT02                 CONVERT THE DATE                           
         CLI   TLSCINV,X'19'                                                    
         BNE   SCT04                                                            
*                                                                               
SCT02    UNPK  WORK(7),TLSCINV(4)                                               
         MVC   WORK+6(2),=C'01'    SET DAY TO 01                                
         GOTO1 =V(DATCON),DMCB,(9,WORK),(1,WORK)                                
         MVC   BYTE,WORK           DATE NOW PWOS                                
*                                                                               
SCT04    CLC   BYTE,TODY1_2        IF YEAR OF RECORD AT LEAST 2 YRS AGO         
         BH    SCNO                                                             
*                                                                               
SCYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
SCNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING FTRACK ELEMENTS                                     
*                                                                               
         USING TLCAD,R8            R8=A(RECORD)                                 
FTTEST   NTR1                                                                   
         LR    R4,R8               GET PAYMENT DETAILS ELEMENT                  
         MVI   ELCODE,TACRELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   FTTXIT              NOTHING TO LOOK AT                           
*                                                                               
         USING TACRD,R4                                                         
         OC    TACRSTRT(6),TACRSTRT ANY START OR END DATE?                      
         BZ    FTTXIT              NO, LEAVE ELEMENT                            
*                                                                               
         CLC   TACRSTRT,TODY1_2    IS FIRST BEFORE CUTOFF DATE?                 
         BNL   FTTXIT              NO, DON'T BOTHER LOOKING FURTHER             
*                                                                               
FTT02    MVI   TACREL,X'FF'        YES, MARK IT FOR DELETION                    
         BAS   RE,NEXTEL           ANY MORE?                                    
         BNE   FTT04               NO, DELETE WHAT WE HAVE                      
         CLC   TACRSTRT,TODY1_2    IS THIS ONE LOWER THAN CUTOFF?               
         BL    FTT02               YES                                          
*                                                                               
FTT04    EQU   *                                                                
*&&DO                                                                           
         CP    DUMPCT,=P'100'                                                   
         BH    FTT06                                                            
         AP    DUMPCT,=P'1'                                                     
         SR    RF,RF                                                            
         ICM   RF,3,TLCALEN-TLCAD(R8)                                           
         GOTO1 =V(PRNTBL),DMCB,0,0(R8),C'DUMP',(RF),=C'2D'                      
*&&                                                                             
FTT06    GOTO1 =V(HELLO),DMCB,(C'D',=C'TALFIL'),(X'FF',(R8)),0,0,0              
*                                                                               
         LR    RE,R8               NOW FIX PHYSICAL (IBM) RECORD LENGTH         
         SH    RE,=H'4'                                                         
         SR    RF,RF                                                            
         ICM   RF,3,TLCALEN                                                     
         LA    RF,4(RF)                                                         
         SLL   RF,16                                                            
         STCM  RF,15,0(RE)                                                      
*                                                                               
FTTXIT   B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING OF VIOLATION ELEMENTS ON SYSTEM RECORD              
*                                                                               
         USING TLSYD,R8            R8=A(RECORD)                                 
SYTEST   NTR1                                                                   
         LR    R4,R8               GET VIOLATION ELEMENTS                       
         MVI   ELCODE,TAAVELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
SYTEST10 BAS   RE,NEXTEL                                                        
         BNE   SYTESTX                                                          
*                                                                               
         USING TAAVD,R4                                                         
         CLC   TAAVDATE(1),TODY1_2 IF DATE OF ERROR AT LEAST 2 YRS AGO          
         BH    *+8                                                              
         MVI   TAAVEL,X'FF'        DELETE IT                                    
         B     SYTEST10                                                         
*                                                                               
SYTESTX  GOTO1 =V(HELLO),DMCB,(C'D',=C'TALFIL'),(X'FF',(R8)),0,0,0              
*                                                                               
         LR    RE,R8               NOW FIX PHYSICAL (IBM) RECORD LENGTH         
         SH    RE,=H'4'                                                         
         SR    RF,RF                                                            
         ICM   RF,3,TLSYLEN                                                     
         LA    RF,4(RF)                                                         
         SLL   RF,16                                                            
         STCM  RF,15,0(RE)                                                      
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING CHECK RECORDS MARKED FOR DELETION                   
*                                                                               
         USING TLCKD,R8            R8=A(RECORD)                                 
CKTEST   NTR1                                                                   
         TM    TLCKSTAT,TLCKSDEL   IF MARKED DELETED                            
         BZ    CKNO                                                             
         AP    CKCOUNT,=P'1'       ADD TO CHECK DELETE COUNT                    
*                                                                               
CKYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
CKNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING COMMERCIAL RECS MARKED FOR DELETION                 
*                                                                               
         USING TLCOD,R8            R8=A(RECORD)                                 
COTEST   NTR1                                                                   
         TM    TLCOSTAT,TLCOSDEL   IF MARKED DELETED                            
         BZ    CONO                                                             
         BAS   RE,ADDCOM           ADD INTERNAL COMMERCIAL NO TABLE             
         CLI   COMFLAG,C'Y'        IF TABLE NOT FULL                            
         BE    CONO                                                             
         AP    COCOUNT,=P'1'       ADD TO COMMERCIAL DELETE COUNT               
*                                                                               
COYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
CONO     LTR   R8,R8                                                            
         B     XIT                                                              
         SPACE 2                                                                
*              ROUTINE TO SAVE INTERNAL COMMERCIAL NUMBER IN TABLE              
         SPACE                                                                  
ADDCOM   NTR1                                                                   
         OC    ANXTCOM,ANXTCOM     IF A(NEXT ENTRY) NOT DEFINED                 
         BNZ   *+10                                                             
         MVC   ANXTCOM,=A(COMTAB)  SET TO BEGINNING OF TABLE                    
*                                                                               
         L     R2,ANXTCOM          IF END OF TABLE                              
         CLI   0(R2),X'FF'                                                      
         BNE   *+12                                                             
         MVI   COMFLAG,C'Y'        SET TABLE FULL INDICATOR                     
         B     ADDCOMX                                                          
*                                                                               
         MVC   0(4,R2),TLCOCOM     ELSE, SAVE INTERNAL COMMERCIAL NO            
         LA    R2,4(R2)            BUMP TO NEXT ENTRY                           
         ST    R2,ANXTCOM          SAVE A(NEXT AVAILABLE ENTRY)                 
*                                                                               
         OC    LOWINT,LOWINT       IF LOW NUMBER SET                            
         BZ    *+14                                                             
         CLC   TLCOCOM,LOWINT      AND IF LOWER THAN CURRENT LOWEST NO          
         BH    *+10                                                             
         MVC   LOWINT,TLCOCOM      SET NEW LOWEST INTERNAL COMML NO             
*                                                                               
         OC    HIGHINT,HIGHINT     IF HIGH NUMBER SET                           
         BZ    *+14                                                             
         CLC   TLCOCOM,HIGHINT     IF HIGHER THAN CURRENT HIGHEST NO            
         BL    *+10                                                             
         MVC   HIGHINT,TLCOCOM     SET NEW HIGHEST INTERNAL COMML NO            
*                                                                               
ADDCOMX  B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING CAST RECORDS                                        
*                                                                               
         USING TLCAD,R8            R8=A(RECORD)                                 
CATEST   NTR1                                                                   
         MVC   WORK(L'TLCACOM),TLCACOM                                          
         BAS   RE,CKCOMTB          CHECK COMMERCIAL REC DELETED                 
         BNE   CANO                                                             
         AP    CACOUNT,=P'1'       ADD TO CAST DELETE COUNT                     
*                                                                               
CAYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
CANO     LTR   R8,R8                                                            
         B     XIT                                                              
         SPACE 2                                                                
*  ROUTINE HANDLES DROPPING INVOICE RECORDS                                     
*                                                                               
         USING TLIND,R8            R8=A(RECORD)                                 
INTEST2  NTR1                                                                   
         LR    R4,R8               GET PAYMENT DETAILS ELEMENT                  
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   IN210                                                            
         USING TAPDD,R4                                                         
         MVC   WORK(L'TAPDCOM),TAPDCOM                                          
         BAS   RE,CKCOMTB          CHECK COMMERCIAL REC DELETED                 
         BNE   IN210                                                            
*                                                                               
         L     R1,TAPDGRS          PICK UP GROSS                                
         CVD   R1,DUB                                                           
         AP    INVCASH,DUB         AND ADD TO INVOICE DOLLAR TOTAL              
         B     IN230                                                            
*                                                                               
IN210    TM    TLINSTAT,TLINSDEL   IF SOFT DELETED                              
         BZ    IN2NO                                                            
*                                                                               
         USING TAACD,R4                                                         
         LR    R4,R8                                                            
         MVI   ELCODE,TAACELQ      OK TO DELETE IF DELETED MORE THAN            
         BAS   RE,GETEL            1 YEAR AGO                                   
         B     *+8                                                              
IN220    BAS   RE,NEXTEL                                                        
         BNE   IN2NO                                                            
         CLI   TAACSCR,X'1F'                                                    
         BNE   IN220                                                            
         CLC   TAACCDTE,TODY1_1                                                 
         BH    IN2NO                                                            
         DROP  R4                                                               
*                                                                               
IN230    AP    INCOUNT,=P'1'       ADD TO INVOICE DELETE COUNT                  
*                                                                               
IN2YES   XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
IN2NO    LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*  ROUTINE HANDLES DROPPING USAGE HISTORY RECORDS                               
*                                                                               
         USING TLUHD,R8            R8=A(RECORD)                                 
UHTEST   NTR1                                                                   
         MVC   WORK(L'TLUHCOM),TLUHCOM                                          
         BAS   RE,CKCOMTB          CHECK COMMERCIAL REC DELETED                 
         BNE   UHNO                                                             
         AP    UHCOUNT,=P'1'       ADD TO USAGE HISTORY DELETE COUNT            
*                                                                               
UHYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
UHNO     LTR   R8,R8                                                            
         B     XIT                                                              
         SPACE 3                                                                
*  ROUTINE HANDLES DROPPING HISTORY COMMENT RECORDS                             
*                                                                               
         USING TLHCD,R8            R8=A(RECORD)                                 
HCTEST   NTR1                                                                   
         MVC   WORK(L'TLHCCOM),TLHCCOM                                          
         BAS   RE,CKCOMTB          CHECK COMMERCIAL REC DELETED                 
         BNE   HCNO                                                             
         AP    HCCOUNT,=P'1'       ADD TO HISTORY COMMENT DELETE COUNT          
*                                                                               
HCYES    XR    R8,R8               SET TO DELETE - RETURN CC EQ                 
HCNO     LTR   R8,R8                                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK IF COMMERCIAL RECORD DELETED                    
*                                  XIT - YES - CC EQUAL                         
         SPACE                                                                  
CKCOMTB  NTR1                                                                   
*                                                                               
         CLC   WORK(4),LOWINT      IF WITHIN TABLE RANGE                        
         BL    CKCOMTBN                                                         
         CLC   WORK(4),HIGHINT                                                  
         BH    CKCOMTBN                                                         
*                                  THEN LOOP THROUGH TABLE                      
         LA    R2,COMTAB           R2=A(INTERNAL COMML NO TABLE)                
CKCOMTB5 CLI   0(R2),X'FF'         IF NOT END OF TABLE                          
         BE    CKCOMTBN                                                         
         OC    0(4,R2),0(R2)                                                    
         BZ    CKCOMTBN                                                         
         CLC   0(4,R2),WORK        MATCH ON INTERNAL COMMERCIAL NO.             
         BE    CKCOMTBY                                                         
         LA    R2,4(R2)            BUMP TO NEXT ENTRY                           
         B     CKCOMTB5            LOOP                                         
*                                                                               
CKCOMTBY XR    R8,R8               COMMERCIAL FOUND - RETURN CC EQ              
CKCOMTBN LTR   R8,R8                                                            
         B     XIT                                                              
         SPACE 3                                                                
         GETEL (R4),DATADISP,ELCODE                                             
         EJECT                                                                  
*                                                                               
XFFS     DC    3X'FF'                                                           
*                                                                               
DUMPCT   DC    PL3'0'                                                           
*                                                                               
DUB      DC    D'0'                                                             
WORK     DC    CL80' '                                                          
DMCB     DC    6F'0'                                                            
SAVERE   DC    F'0'                                                             
SAVER1   DC    F'0'                SAVE PARAMETER ADDRESS                       
LOWINT   DC    XL4'0'              LOWEST INT COMML NO IN CKCOMTB               
HIGHINT  DC    XL4'0'              HIGHEST INT COMML NO IN CKCOMTB              
ANXTCOM  DC    AL4(0)              A(NEXT AVAILABLE ENTRY) IN COMTAB            
ANXTADV  DC    AL4(0)              A(NEXT AVAILABLE ENTRY) IN ADVTAB            
DATADISP DC    AL2(TLRCELEM-TLRCD)                                              
ELCODE   DS    CL1                                                              
BYTE     DS    CL1                                                              
THREE    DS    XL3                                                              
COMFLAG  DC    C'N'                TABLE NOT FULL/MSG PRNTD IN TALDBALC         
TODAY0   DC    XL6'00'             TODAY'S DATE                                 
TODY1_30 DC    XL3'00'             TODAY'S DATE MINUS 30 DAYS                   
TODY1_1  DC    XL3'00'             TODAY'S DATE MINUS 1 YEAR                    
TODY1_2  DC    XL3'00'             TODAY'S DATE MINUS 2 YEARS                   
SNDDVCUT DC    XL3'00'             ADVICE SEND CUTOFF DATE                      
*                                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              TABLE INFORMATION PRINTED IN TALDBALC                            
         SPACE                                                                  
TOTPRGS  DS    0CL36                                                            
CKCOUNT  DC    PL8'0',CL28'CHECK RECORDS DELETED      ='                        
COCOUNT  DC    PL8'0',CL28'COMMERCIAL RECORDS DELETED ='                        
CACOUNT  DC    PL8'0',CL28'CAST RECORDS DELETED       ='                        
UHCOUNT  DC    PL8'0',CL28'USAGE HISTORY RECS DELETED ='                        
HCCOUNT  DC    PL8'0',CL28'HIST COMMENT RECS DELETED  ='                        
INCOUNT  DC    PL8'0',CL28'INVOICE RECORDS DELETED    ='                        
         DC    X'FF'                                                            
         SPACE 2                                                                
*              INVOICE DOLLAR AMOUNT PRINTED IN TALDBALC                        
         SPACE                                                                  
INVCASH  DC    PL8'0',CL28'INVOICE CASH DELETED       ='                        
         SPACE 2                                                                
         DS    0D                                                               
         DC    CL8'*COMTAB*'       INTERNAL COMMERCIAL NUMBER TABLE             
COMTAB   DC    (20000*4)X'00'                                                   
COMTABX  DC    X'FF'                                                            
         SPACE 2                                                                
         DS    0D                                                               
         DC    CL8'*ADVTAB*'       DELETED ADVICE INFO. TABLE                   
ADVTAB   DC    (15000*ADVTABL)X'00'                                             
ADVTABX  DC    X'FF'                                                            
         SPACE 2                                                                
*              DSECT TO COVER ADVTAB                                            
*                                                                               
ADVTABD  DSECT                                                                  
ADVAGY   DS    CL6                                                              
ADVCID   DS    CL12                                                             
ADVADV   DS    CL6                                                              
ADVTABL  EQU   *-ADVTABD                                                        
         EJECT                                                                  
*DDDPRINT                                                                       
*TAGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE TAGENFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'038TALDPRGE  03/07/16'                                      
         END                                                                    
