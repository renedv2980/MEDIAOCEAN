*          DATA SET SPBILMON   AT LEVEL 007 AS OF 05/01/02                      
*CATALP SPBILMON                                                                
         TITLE 'SPBILMON - SPOT/NET BUILD BILLMONTH TABLE'                      
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   PARM1 - BYTE  0    DATE CONTROL (AS FOR MOBILE)                             
*           BYTES 1-3  A(WHERE TO BUILD TABLE)                                  
*   PARM2 - BYTE  0    NOT USED                                                 
*           BYTES 1-3  A(START DATE)   YYMMDD                                   
*   PARM3 - BYTE  0    NOT USED                                                 
*           BYTES 1-3  A(END DATE)     YYMMDD                                   
*   PARM4 - BYTE  0    NOT USED                                                 
*           BYTES 1-3  A(MOBILE)                                                
*   PARM5 - BYTE  0    NOT USED                                                 
*           BYTES 1-3  A(DATCON)                                                
*                                                                               
*   RETURNED TABLE IS                                                           
*           BILLING MONTH   XL2    YM  BINARY                                   
*           START DATE      XL2    YMD COMPRESSED                               
*           END DATE        XL2    YMB COMPRESSED                               
*           TERMINATED BY X'FF'                                                 
*        ** ALLOW ENOUGH ROOM **                                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
SPBILMON CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**BLMO,CLEAR=YES                                          
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,SAVR1                                                         
         MVC   PARMS(24),0(R1)    SAVE PARMS                                    
         L     RF,4(R1)                                                         
         MVC   QSTART,0(RF)                                                     
         L     RF,8(R1)                                                         
         MVC   QEND,0(RF)                                                       
         GOTO1 DATCON,DMCB,QSTART,(2,BQSTARTP)                                  
         GOTO1 (RF),(R1),QEND,(2,BQENDP)                                        
*                                                                               
         MVC   WORK(12),QSTART                                                  
         PACK  DUB,QSTART+2(2)     GO BACK 26 MONTHS                            
         LA    RF,=P'2'                                                         
         SP    DUB,=P'2'                                                        
         BP    *+14                                                             
         LA    RF,=P'3'                                                         
         AP    DUB,=P'12'                                                       
*                                                                               
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+2(2),DUB                                                    
*                                                                               
         PACK  DUB,QSTART(2)                                                    
         SP    DUB,0(1,RF)         GO BACK 1 OR 2 YEARS                         
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(2),DUB                                                      
*                                                                               
         PACK  DUB,WORK+8(2)       AND FORWARD 2 MONTHS                         
         AP    DUB,=P'2'                                                        
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+8(2),DUB                                                    
         CP    DUB,=P'12'                                                       
         BNH   PR32D                                                            
         SP    DUB,=P'12'                                                       
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+8(2),DUB                                                    
*                                                                               
         PACK  DUB,WORK+6(2)                                                    
         AP    DUB,=P'1'                                                        
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+6(2),DUB                                                    
*                                                                               
PR32D    DS    0H                                                               
         IC    R0,PARMS+0          DATE CONTROL                                 
*                                                                               
         GOTO1 MOBILE,DMCB,(50,WORK),((R0),WKLST)                               
*                                                                               
* NOW WORK OUT PERIOD NUMBERS - FIND PLACES WHERE YEAR CHANGES                  
* AND BUILD TEMPORARY LIST IN WKLST2                                            
         LA    R5,WKLST2                                                        
         LA    R4,WKLST                                                         
*                                                                               
PR34A    DS    0H                                                               
         BAS   RE,CKNEWYR          TEST START OF NEW YEAR                       
         BE    PR34A2              YES                                          
         LA    R4,4(R4)                                                         
         B     PR34A                                                            
*                                                                               
PR34A2   DS    0H                  SET INITIAL YEAR                             
         ZIC   R3,2(R4)                                                         
         SRL   R3,1                                                             
*                                                                               
PR34B    DS    0H                                                               
         LA    R1,1                FOR PERIOD NUMBERS WITHIN YEAR               
PR34B2   DS    0H                                                               
         STC   R3,0(R5)            YEAR                                         
         STC   R1,1(R5)            MONTH                                        
         MVC   2(4,R5),0(R4)       START-END OF PERIOD                          
         LA    R5,6(R5)                                                         
         LA    R4,4(R4)                                                         
         CLI   0(R4),X'FF'         TEST E-O-L                                   
         BE    PR40                                                             
         LA    R1,1(R1)                                                         
         BAS   RE,CKNEWYR          TEST NEW YEAR                                
         BNE   PR34B2              NO - CONTINUE                                
         LA    R3,1(R3)            BUMP YEAR                                    
         B     PR34B                                                            
*                                                                               
*              NOW RETURN DATES WITHING START/END TO CALLER                     
PR40     DS    0H                                                               
         MVI   0(R5),X'FF'         SET END OF WKLST2                            
         LA    R5,WKLST2                                                        
         L     R4,PARMS+0          A(RETURNED TABLE)                            
         LA    R4,0(R4)                                                         
*                                                                               
PR42     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    PR46                                                             
         CLC   BQSTARTP,4(R5)                                                   
         BH    PR44                                                             
         CLC   BQENDP,2(R5)                                                     
         BL    PR44                                                             
*                                                                               
         MVC   0(6,R4),0(R5)                                                    
         LA    R4,6(R4)                                                         
*                                                                               
PR44     DS    0H                                                               
         LA    R5,6(R5)                                                         
         B     PR42                                                             
*                                                                               
PR46     DS    0H                                                               
         MVI   0(R4),X'FF'         SET EOL                                      
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
*                                  FIND START OF NEW YEAR                       
*                                  1) A PERIOD THAT SPANS YEAR CHANGE           
*                                     AND BEGINS NO FURTHER AWAY                
*                                     FROM 12/31 THAN IT ENDS                   
*                             OR   2) A PERIOD THAT STARTS BEFORE 1/14          
*                                                                               
CKNEWYR  DS    0H                                                               
         MVC   DUB(4),0(R4)                                                     
         NI    DUB,X'01'           STRIP YEAR                                   
         CLC   DUB(2),NEWYRLO                                                   
         BL    CKNYYES                                                          
*                                                                               
         CLC   DUB(2),PDDEC                                                     
         BNH   CKNYNO                                                           
*                                                                               
         NI    DUB+2,X'01'                                                      
         CLC   DUB+2(2),PDDEC                                                   
         BH    CKNYNO                                                           
*                                                                               
         NI    DUB+1,X'1F'         ISOLATE DAY                                  
         ZIC   RF,DUB+1                                                         
         LA    R0,30                                                            
         SR    R0,RF                                                            
         BNP   CKNYYES             STARTS ON 30TH OR 31ST                       
         STC   R0,DUB+4                                                         
*                                                                               
         NI    DUB+3,X'1F'         ISOLATE DAY                                  
         CLC   DUB+4(1),DUB+3                                                   
         BNH   CKNYYES                                                          
*                                                                               
CKNYNO   DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
CKNYYES  DS    0H                                                               
         SR    R0,R0                                                            
         BR    RE                                                               
*                                                                               
*                                                                               
NEWYRLO  DC    X'002E'             JAN14                                        
PDDEC    DC    X'0180'             DEC00                                        
*                                                                               
         LTORG                                                                  
*                                                                               
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
PARMS    DS    6F                                                               
         ORG   PARMS+12                                                         
MOBILE   DS    A                                                                
DATCON   DS    A                                                                
         ORG                                                                    
WORK     DS    XL64                                                             
SAVR1    DS    F                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
STAT     DS    X                                                                
BYTE     DS    X                                                                
QSTART   DS    CL6                                                              
QEND     DS    CL6                                                              
BQSTARTP DS    XL2                                                              
BQENDP   DS    XL2                                                              
WKLST    DS    XL300                                                            
WKLST2   DS    XL400                                                            
WORKDL   EQU   *-WORKD                                                          
         SPACE 2                                                                
*INCLUDE DDCOMFACSD                                                             
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007SPBILMON  05/01/02'                                      
         END                                                                    
