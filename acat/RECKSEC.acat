*          DATA SET RECKSEC    AT LEVEL 129 AS OF 05/18/15                      
*PROCESS USING(WARN(15))                                                        
*CATALP RECKSEC                                                                 
         TITLE 'RECKSEC' - REPPAK ACCESS SECURITY VALIDATION ROUTINE            
*                                                                               
* INPUT  PARAMETER 1 = A(SECURITY CONTROL BLOCK) (REGENSBLK)                    
*                                                                               
*        PARAMETER 2 = A(FLD HEADER FOR ERROR MESSAGE) (OPTIONAL)               
*                                                                               
*        PARAMETER 3 = A(TIOB) (OPTIONAL - FOR BEEP ON ERROR)                   
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
*        PARAMETER 4 = A(RFBLOCK)                                               
*                                                                               
*        PARAMETER 5 = VREPFACS                                                 
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
* OUTPUT          CC = EQUAL     - REQUEST VAILD                                
*                 CC = NOT EQUAL - REQUEST INVALID                              
*                                                                               
*        PARAMETER 1 = BYTE 0 ----- RETURN CODE ------ (ONLY ONE)               
*                             X'00' PAR REC READ, REQUEST VALID                 
*                             X'01' INVALID REQUEST ERROR TYPE 1                
*                             X'02' INVALID REQUEST ERROR TYPE 2                
*                             X'03' INVALID REQUEST ERROR TYPE 3                
*                             X'04' INVALID REQUEST ERROR TYPE 4                
*                             X'05' INVALID REQUEST ERROR TYPE 5                
*                             X'06' INVALID REQUEST ERROR TYPE 6                
*                             X'51' NO PAR RECORD ON FILE - VALID               
*                             X'52' NO PID NUMBER CONNECTED - VALID             
*                             X'60' SPCL CASE OWNER CODE - VALID                
*                                                                               
*        PARAMETER 2 = BYTE 0 ----- RETURN FLAGS ------ (MULTIPLE)              
*                             X'80' GROUP VALID ACCESS MATCHED                  
*                             X'40' OFFICE VALID ACCESS MATCHED                 
*                             X'20' STATION VALID ACCESS MATCHED                
*                             X'10' OWNER VALID ACCESS MATCHED                  
*                             X'08' MARKET VALID ACCESS MATCHED                 
*                             X'04' NOT USED                                    
*                             X'02' SALESPERSON VAILD ACCESS MATCHED            
*                                                                               
RECKSEC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*CKSEC*,R7,CLEAR=YES,RR=R5                        
         USING WORKD,RC                                                         
         ST    R5,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SVPARM                                                        
         L     RA,0(R1)            A(SBLOCK)                                    
         USING SECBLK,RA                                                        
         L     RF,12(R1)           P4                                           
         MVC   RFBLOCK,0(RF)       COMFACS,AGENCY CODE                          
         MVC   VREPFACS,16(R1)                                                  
*                                                                               
         L     RE,COMFACS          RESOLVE ADDRESSES                            
         USING COMFACSD,RE                                                      
         MVC   GETFACT,CGETFACT                                                 
         MVC   HELLO,CHELLO                                                     
         MVC   DATAMGR,CDATAMGR                                                 
         DROP  RE                                                               
*                                                                               
         MVI   STATUS,0            INITIALIZE                                   
         MVI   VASTAT,0                                                         
         XC    ERRCODE,ERRCODE                                                  
         MVI   ERRTYPE,0                                                        
*                                                                               
         MVC   AGENCYCT,AGENCY     SAVE CONNECTED REP CODE                      
*                                                                               
         XC    KEY,KEY             READ REPREC FOR MASTER REP CODE              
         LA    R6,KEY                                                           
         USING RREPREC,R6                                                       
         MVI   RREPKTYP,X'01'                                                   
         MVC   RREPKREP,AGENCY                                                  
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IOAREA                                                        
         CLC   RREPMAST,=X'FFFF'   MASTER?                                      
         BNE   *+12                                                             
         OI    STATUS,X'80'                                                     
         B     CKS030                                                           
         OC    RREPMAST,=X'4040'                                                
         CLC   RREPMAST,=X'4040'                                                
         BE    *+10                NO MASTER/SUBSID CONTROL                     
         MVC   AGENCY,RREPMAST     USE MASTER REP CODE TO READ PAR REC          
         DROP  R6                                                               
*                                                                               
CKS030   DS    0H                                                               
         GOTO1 GETFACT,DMCB,0      GET PID NUMBER                               
         L     RF,0(R1)                                                         
         MVC   PIDNUM,FAPASSWD-FACTSD(RF)                                       
*                                                                               
***>>    MVC   PIDNUM,=X'FFFF'    ****>>> FOR NOW <<<****                       
*                                                                               
         OC    PIDNUM,PIDNUM                                                    
         BNZ   CKS040                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'52'         NO PID NUMBER - SECURITY INACTIVE            
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS040   DS    0H                                                               
         BAS   RE,GETSEC           GET SECURITY RECORD                          
         BE    CKS100                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'51'         NO PAR REC FOUND                             
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS100   DS    0H                  MAIN ACCESS CHECK LOOP                       
         LA    R9,TYPES                                                         
         B     *+8                                                              
CKS110   DS    0H                                                               
         LA    R9,L'TYPES(R9)      NEXT TABLE ENTRY                             
         CLI   0(R9),X'FF'         END OF TABLE?                                
         BNE   CKS150              NO - PERFORM ACCESS CHECK                    
*                                                                               
         L     R1,SVPARM           DONE - *** RETURN REQUEST VALID ***          
         MVC   4(1,R1),VASTAT      RETURN VALID ACCESS FLAGS                    
*                                                                               
         TM    STATUS,XOWNFLG      SPECIAL CASE FLAG TRIPPED?                   
         BZ    CKS120              NO - REGULAR EXIT                            
         MVI   0(R1),X'60'         RETURN CODE = X'60'                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS120   DS    0H                                                               
         MVI   0(R1),0             RETURN FLAG = 0                              
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS150   DS    0H                  ACCESS CHECK                                 
*                                                                               
         BAS   RE,BLDBLK           EXPAND FLTR BLOCK CODES INTO BUFFER          
*                                                                               
         ICM   RF,15,13(R9)        PRE-CHECK HOOK ROUTINE                       
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
         BAS   RE,CKACC            CHECK VALID ACCESS                           
*                                                                               
         BAS   RE,CKBRK            CHECK BREAKS VALID                           
*                                                                               
         BAS   RE,CKFLT            CHECK FILTERS VALID                          
*                                                                               
         ICM   RF,15,17(R9)        POST-CHECK HOOK ROUTINE                      
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
         MVI   BUFFTYPE,PARBUFF    CLEAR PAR BUFFER (IF USED)                   
         BAS   RE,GETBUFF                                                       
         BNE   *+12                                                             
         L     RE,BUFFADDR                                                      
         MVI   0(RE),0                                                          
*                                                                               
         MVI   BUFFTYPE,BLKBUFF    CLEAR BLOCK BUFFER (IF USED)                 
         BAS   RE,GETBUFF                                                       
         BNE   *+12                                                             
         L     RE,BUFFADDR                                                      
         MVI   0(RE),0                                                          
*                                                                               
         B     CKS110              NEXT TABLE ENTRY                             
**********************************************************************          
TYPES    DS    0CL(TYPE2-TYPE1)                                                 
*                                                                               
* DISPL                                                                         
*   0     TEXT OF PAR LEVEL NAME                                                
*   7     SUBSIDIARY SECURITY ELEMENT ELCODE                                    
*   8     MASTER SECURITY ELEMENT ELCODE                                        
*   9     DISPLACEMENT OF FILTER IN SBLOCK DSECT                                
*   11    FLAG VALUE IN SBREAKS                                                 
*   12    FLAG VALUE IN SBBRK#4                                                 
*   13    A(PRE-CHECK HOOK ROUTINE)                                             
*   17    A(POST-CHECK HOOK ROUTINE)                                            
*   21    SET TYPE OR NULLS                                                     
*   23    RSECREC SUB-ELEM CODE FOR DETAIL LEVEL                                
*   24    LEVEL VALUE FOR VASTAT                                                
*   25    FILTER INHERITANCE FLAGS                                              
*                X'80' FILTER CAN BE INHERITED FROM STATION                     
*                X'40' FILTER CAN BE INHERITED FROM SALESPERSON                 
*   26    CODE BUFFER ELEMENT TYPE FOR CODE EXPANSION                           
*         NOTE: IF FILTERS CAN BE INHERITED FROM THIS TYPE, THEN UNIQUE         
*               ELEMENT TYPE CODE MUST BE USED                                  
*   27    MAXIMUM CHARACTER CODE LENGTH                                         
*                                                                               
**********************************************************************          
* WARNING: DO NOT CHANGE THE ORDER OF THESE ENTRIES                             
**********************************************************************          
TYPE1   DC    CL7'STATION',X'4041',AL2(SBSTATN-SBLOCK),X'2020'                  
        DC    AL4(NOHOOK),AL4(NOHOOK),C'ST',X'03',X'20',X'00'                   
        DC    AL1(STABUFF),AL1(5)                                               
*                                                                               
TYPE2   DC    CL7'SP/Team',X'6061',AL2(SBSALES-SBLOCK),X'0202'                  
        DC    AL4(NOHOOK),AL4(NOHOOK),C'SP',X'07',X'02',X'00'                   
        DC    AL1(SALBUFF),AL1(3)                                               
*                                                                               
        DC    CL7'GROUP',X'1011',AL2(SBGROUP-SBLOCK),X'8080'                    
        DC    AL4(NOHOOK),AL4(NOHOOK),C'GS',X'02',X'80',X'80'                   
        DC    AL1(BLKBUFF),AL1(2)                                               
*                                                                               
        DC    CL7'OWNER',X'3031',AL2(SBOWNER-SBLOCK),X'1010'                    
        DC    AL4(NOHOOK),AL4(NOHOOK),X'0000',X'06',X'10',X'80'                 
        DC    AL1(BLKBUFF),AL1(3)                                               
*                                                                               
        DC    CL7'MARKET',X'5051',AL2(SBMARKET-SBLOCK),X'0808'                  
        DC    AL4(NOHOOK),AL4(NOHOOK),X'0000',X'04',X'08',X'80'                 
        DC    AL1(BLKBUFF),AL1(4)                                               
*                                                                               
        DC    CL7'OFFICE',X'2021',AL2(SBOFFICE-SBLOCK),X'4040'                  
        DC    AL4(NOHOOK),AL4(NOHOOK),C'OF',X'05',X'40',X'40'                   
        DC    AL1(BLKBUFF),AL1(2)                                               
*                                                                               
        DC    X'FF'                                                             
**********************************************************************          
* CKACC - CHECK VALID ACCESS ROUTINE                                            
**********************************************************************          
CKACC    NTR1                                                                   
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,=X'1'),0         
         CLI   12(R1),0            GOT IT?                                      
         BNE   EXITOK              NO ACCESS RESTRICTIONS                       
         ZICM  R6,13(R1),3         ELEMENT                                      
*                                                                               
         MVI   ERRTYPE,1                                                        
         MVC   BUFFTYPE,26(R9)     FIND FILTER CODE BUFFER AREA                 
         BAS   RE,GETBUFF                                                       
         BE    *+6                                                              
         DC    H'0'                SHOULD HAVE A BUFFER SET UP                  
         L     R2,BUFFADDR                                                      
         CLI   3(R2),X'FF'         EMPTY BUFFER?                                
         BNE   CKACC100            NO - PROCEED TO ACCESS CHECK                 
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    ERROR               NO                                           
         BAS   RE,INHERIT                                                       
         BNE   ERROR               NOT INHERITED                                
*                                                                               
CKACC100 DS    0H                                                               
         MVI   ERRTYPE,2                                                        
         BAS   RE,BLDPAR           BUILD PAR CODE BUFFER                        
         L     R3,BUFFADDR         R3=PAR BUFFER                                
         TM    2(R2),X'80'         BLOCK EXCLUSION BUFFER?                      
         BO    CKACC140                                                         
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKACC120                                                         
*                                                                               
         BAS   RE,INCCOMP          CASE1: INC BLK VS. INC PAR                   
         BE    CKACC200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKACC120 DS    0H                                                               
         BAS   RE,EXCCOMP          CASE2: INC BLK VS. EXC PAR                   
         BE    CKACC200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKACC140 DS    0H                                                               
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKACC160                                                         
*                                                                               
         BAS   RE,ERROR            CASE3: EXC BLK VS. INC PAR                   
*                                                                               
CKACC160 DS    0H                  CASE4: EXC BLK VS. EXC PAR                   
         XR    R2,R3                                                            
         XR    R3,R2                                                            
         XR    R2,R3                                                            
         BAS   RE,INCCOMP                                                       
         BE    CKACC200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKACC200 DS    0H                                                               
         OC    VASTAT,24(R9)       VALID ACCESS MATCH FOR THIS LEVEL            
         B     EXITOK                                                           
**********************************************************************          
* CKBRK - CHECK BREAKS VALID ROUTINE                                            
**********************************************************************          
CKBRK    NTR1                                                                   
         CLI   SBBREAKS,0          DOES REPORT BREAK?                           
         BE    EXITOK              NO - VALID                                   
*                                                                               
         LA    R8,TYPES            TYPES TABLE (AGAIN)                          
CKBRK020 DS    0H                                                               
         CLI   0(R8),X'FF'         END OF TYPES?                                
         BE    EXITOK              YES - VALID                                  
         MVC   BYTE,SBBREAKS                                                    
         NC    BYTE,11(R8)         BREAK AT CURRENT BREAK LEVEL?                
         BZ    CKBRK200            NO - NEXT BREAK LEVEL                        
*                                                                               
         MVC   BYTE,SBBRK#4                                                     
         NC    BYTE,12(R8)         BYPASS CHECK AT CURRENT BREAK LEVEL?         
         BNZ   CKBRK200            YES - NEXT BREAK LEVEL                       
*                                                                               
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         MVC   BYTE,23(R8)         SUB ELEM CODE FOR THIS BREAK LEVEL           
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,BYTE),0          
         CLI   12(R1),0            GOT IT?                                      
         BNE   CKBRK200            NO BREAK RESTRICTIONS AT THIS LEVEL          
         ZICM  R6,13(R1),3         ELEMENT                                      
*                                                                               
         MVC   BUFFTYPE,26(R9)     FIND FILTER CODE BUFFER AREA                 
         BAS   RE,GETBUFF                                                       
         BE    *+6                                                              
         DC    H'0'                SHOULD HAVE A BUFFER SET UP                  
         L     R2,BUFFADDR                                                      
         CLI   3(R2),X'FF'         ANY FILTERS?                                 
         BNE   CKBRK100            YES - CONTINUE                               
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    CKBRK025            NO                                           
         BAS   RE,INHERIT                                                       
         BE    CKBRK100            GOT CODE                                     
*                                                                               
CKBRK025 DS    0H                                                               
         MVI   ERRTYPE,3                                                        
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK100 DS    0H                  NOW CHECK ALL BLKCODES IN PARCODES           
         MVI   ERRTYPE,4                                                        
         BAS   RE,BLDPAR           BUILD PAR CODE BUFFER                        
         L     R3,BUFFADDR         R3=PAR BUFFER                                
         TM    2(R2),X'80'         BLOCK EXCLUSION BUFFER?                      
         BO    CKBRK140                                                         
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKBRK120                                                         
*                                                                               
         BAS   RE,EXCCOMP          CASE1: INC BLK VS. INC PAR                   
         BE    CKBRK200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK120 DS    0H                                                               
         BAS   RE,INCCOMP          CASE2: INC BLK VS. EXC PAR                   
         BE    CKBRK200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK140 DS    0H                                                               
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKBRK160                                                         
*                                  CASE3: EXC BLK VS. INC PAR                   
         XR    R2,R3                                                            
         XR    R3,R2                                                            
         XR    R2,R3                                                            
         BAS   RE,INCCOMP                                                       
         BE    CKBRK200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK160 DS    0H                  CASE4: EXC BLK VS. EXC PAR                   
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK200 DS    0H                                                               
         LA    R8,L'TYPES(R8)      NEXT BREAK LEVEL                             
         B     CKBRK020                                                         
**********************************************************************          
* CKFLT - CHECK FILTERS VALID ROUTINE                                           
**********************************************************************          
CKFLT    NTR1                                                                   
         LA    R8,TYPES            TYPES TABLE (AGAIN)                          
CKFLT020 DS    0H                                                               
         CLI   0(R8),X'FF'         END OF TYPES?                                
         BE    EXITOK              YES - VALID                                  
*                                                                               
         ZICM  R3,9(R8),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER AT THIS LEVEL?                    
         BE    CKFLT200            NO - NEXT FILTER LEVEL                       
*                                                                               
         MVC   BYTE,SBBRK#4                                                     
         NC    BYTE,12(R8)         BYPASS CHECK AT CURRENT BREAK LEVEL?         
         BNZ   CKFLT200            YES - NEXT BREAK LEVEL                       
*                                                                               
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         MVC   BYTE,23(R8)         SUB ELEM CODE FOR THIS BREAK LEVEL           
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,BYTE),0          
         CLI   12(R1),0            GOT IT?                                      
         BNE   CKFLT200            NO BREAK RESTRICTIONS AT THIS LEVEL          
         ZICM  R6,13(R1),3         ELEMENT                                      
*                                                                               
         MVC   BUFFTYPE,26(R9)     FIND FILTER CODE BUFFER AREA                 
         BAS   RE,GETBUFF                                                       
         BE    *+6                                                              
         DC    H'0'                SHOULD HAVE A BUFFER SET UP                  
         L     R2,BUFFADDR                                                      
         CLI   3(R2),X'FF'         ANY FILTERS?                                 
         BNE   CKFLT100            YES - CONTINUE                               
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    CKFLT025            NO                                           
         BAS   RE,INHERIT                                                       
         BE    CKFLT100            GOT CODE                                     
*                                                                               
CKFLT025 DS    0H                                                               
         MVI   ERRTYPE,5                                                        
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT100 DS    0H                  NOW CHECK ALL BLKCODES IN PARCODES           
         MVI   ERRTYPE,6                                                        
         BAS   RE,BLDPAR           BUILD PAR CODE BUFFER                        
         L     R3,BUFFADDR         R3=PAR BUFFER                                
         TM    2(R2),X'80'         BLOCK EXCLUSION BUFFER?                      
         BO    CKFLT140                                                         
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKFLT120                                                         
*                                                                               
         BAS   RE,EXCCOMP          CASE1: INC BLK VS. INC PAR                   
         BE    CKFLT200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT120 DS    0H                                                               
         BAS   RE,INCCOMP          CASE2: INC BLK VS. EXC PAR                   
         BE    CKFLT200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT140 DS    0H                                                               
         TM    2(R3),X'80'         PAR EXCLUSION BUFFER?                        
         BO    CKFLT160                                                         
*                                  CASE3: EXC BLK VS. INC PAR                   
         XR    R2,R3                                                            
         XR    R3,R2                                                            
         XR    R2,R3                                                            
         BAS   RE,INCCOMP                                                       
         BE    CKFLT200                                                         
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT160 DS    0H                  CASE4: EXC BLK VS. EXC PAR                   
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT200 DS    0H                                                               
         LA    R8,L'TYPES(R8)      NEXT BREAK LEVEL                             
         B     CKFLT020                                                         
**********************************************************************          
* ERROR - GENERATES ERROR MSG, INTERRUPTS MODULE EXECUTION & EXITS              
*          ERRTYPE: CONTAINS NUMBER OF ERROR                                    
*          ERRCODE: CONTAINS CODE ACCESS CHECK FAILED ON IF APPLICABLE          
**********************************************************************          
ERROR    NTR1                                                                   
         L     R1,SVPARM                                                        
         ICM   R2,15,4(R1)         A(MSG FIELD HEADER)                          
         BZ    ERROR850            NO MSG FIELD                                 
         ZIC   R3,0(R2)                                                         
         SH    R3,=H'8'            MINUS HEADER                                 
         TM    1(R2),X'02'         EXT?                                         
         BZ    *+8                                                              
         SH    R3,=H'8'            MINUS EXT                                    
*                                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)       CLEAR MSG FIELD                              
         LA    R3,1(R2,R3)         END OF FIELD+1                               
         OI    1(R2),X'08'         HIGH INTENSITY                               
         OI    6(R2),X'80'         XMIT                                         
         LA    R2,8(R2)            FIRST TEXT SPOT IN FIELD                     
*                                                                               
         MVC   0(15,R2),=C'Access denied: '                                     
         LA    R2,15(R2)                                                        
*                                                                               
* ERRTYPE:1 - ACCESS RESTRICTED ON PAR, MISSING FILTER IN BLOCK                 
*                                                                               
         CLI   ERRTYPE,1                                                        
         BNE   ERROR050                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         ZIC   RF,BYTE                                                          
         AR    R2,RF                                                            
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:2 - ACCESS RESTRICTED ON PAR, INVALID FILTER IN BLOCK                 
*                                                                               
ERROR050 DS    0H                                                               
         CLI   ERRTYPE,2                                                        
         BNE   ERROR100                                                         
         MVC   0(20,R2),=C'No valid access for '                                
         LA    R2,20(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
*                                                                               
         LA    R4,6                                                             
         CLI   7(R9),X'60'         SALESMAN/TEAM?                               
         BNE   ERROR060                                                         
         LA    RF,WORK                                                          
         MVI   BYTE,3                                                           
         MVC   WORK(3),=C'S/P'     DEFAULT TO S/P NAME                          
         CLI   0(R3),X'FF'         TEAM IN S/P FILTER FIELD?                    
         BNE   ERROR060            NO                                           
         MVI   BYTE,4                                                           
         MVC   WORK(4),=C'Team'    USE TEAM NAME                                
         LA    R3,1(R3)            SKIP FLAG                                    
         BCTR  R4,0                                                             
ERROR060 BAS   RE,ADDWORD                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         LR    RF,R3                                                            
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES                                                   
         BNE   *+8                                                              
         LA    RF,ERRCODE                                                       
         LA    R4,1(R4)                                                         
         STC   R4,BYTE                                                          
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:3 - BREAKS IN BLOCK, DETAIL RESTRICTED IN PAR, MISSING FLTER          
*                                                                               
ERROR100 DS    0H                                                               
         CLI   ERRTYPE,3                                                        
         BNE   ERROR150                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:4 - BREAKS IN BLOCK, DETAIL RESTRICTED IN PAR, INVALID FLTER          
*                                                                               
ERROR150 DS    0H                                                               
         CLI   ERRTYPE,4                                                        
         BNE   ERROR200                                                         
         MVC   0(3,R2),=C'No '                                                  
         LA    R2,3(R2)                                                         
*                                                                               
         LA    RF,0(R8)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         MVC   0(12,R2),=C' detail for '                                        
         LA    R2,12(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
*                                                                               
         LA    R4,6                                                             
         CLI   7(R9),X'60'         SALESMAN/TEAM?                               
         BNE   ERROR160                                                         
         LA    RF,WORK                                                          
         MVI   BYTE,3                                                           
         MVC   WORK(3),=C'S/P'     DEFAULT TO S/P NAME                          
         CLI   0(R3),X'FF'         TEAM IN S/P FILTER FIELD?                    
         BNE   ERROR160            NO                                           
         MVI   BYTE,4                                                           
         MVC   WORK(4),=C'Team'    USE TEAM NAME                                
         LA    R3,1(R3)            SKIP FLAG                                    
         BCTR  R4,0                                                             
ERROR160 BAS   RE,ADDWORD                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         LR    RF,R3                                                            
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES                                                   
         BNE   *+8                                                              
         LA    RF,ERRCODE                                                       
         LA    R4,1(R4)                                                         
         STC   R4,BYTE                                                          
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:5 - FILTER IN BLOCK, DETAIL RESTRICTED IN PAR, MISSING FLTER          
*                                                                               
ERROR200 DS    0H                                                               
         CLI   ERRTYPE,5                                                        
         BNE   ERROR250                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:6 - FILTER IN BLOCK, DETAIL RESTRICTED IN PAR, INVALID FLTER          
*                                                                               
ERROR250 DS    0H                                                               
         CLI   ERRTYPE,6                                                        
         BNE   ERROR300                                                         
         MVC   0(3,R2),=C'No '                                                  
         LA    R2,3(R2)                                                         
*                                                                               
         LA    RF,0(R8)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         MVC   0(19,R2),=C'-level filters for '                                 
         LA    R2,19(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
*                                                                               
         LA    R4,6                                                             
         CLI   7(R9),X'60'         SALESMAN/TEAM?                               
         BNE   ERROR260                                                         
         LA    RF,WORK                                                          
         MVI   BYTE,3                                                           
         MVC   WORK(3),=C'S/P'     DEFAULT TO S/P NAME                          
         CLI   0(R3),X'FF'         TEAM IN S/P FILTER FIELD?                    
         BNE   ERROR260            NO                                           
         MVI   BYTE,4                                                           
         MVC   WORK(4),=C'Team'    USE TEAM NAME                                
         LA    R3,1(R3)            SKIP FLAG                                    
         BCTR  R4,0                                                             
ERROR260 BAS   RE,ADDWORD                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         LR    RF,R3                                                            
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES                                                   
         BNE   *+8                                                              
         LA    RF,ERRCODE                                                       
         LA    R4,1(R4)                                                         
         STC   R4,BYTE                                                          
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:7 - PAR REC CONTAINS MISUSE OF EXCLUSION SETS                         
*                                                                               
ERROR300 DS    0H                                                               
         CLI   ERRTYPE,7                                                        
         BNE   ERROR350                                                         
         MVC   0(28,R2),=C'PAR RECORD DEFINITION ERROR:'                        
         LA    R2,28(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:8 - UNABLE TO INHERIT CODE FROM EXCLUSION SET                         
*                                                                               
ERROR350 DS    0H                                                               
         CLI   ERRTYPE,8                                                        
         BNE   ERROR400                                                         
         MVC   0(28,R2),=C'FFFFFFFFFFFFFFFFFFFFFFFFFFF:'                        
         LA    R2,28(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
ERROR400 DS    0H                                                               
         MVC   0(5,R2),=C'*****'                                                
         LA    R2,5(R2)                                                         
*                                                                               
ERROR800 DS    0H                                                               
         L     R1,SVPARM                                                        
         L     RF,4(R1)            A(MSG FIELD HEADER)                          
         SR    R2,RF               LEN OF MSG                                   
         STC   R2,7(RF)                                                         
*                                                                               
         ICM   R3,15,8(R1)         ATIOB                                        
         BZ    ERROR850                                                         
         USING TIOBD,R3                                                         
         OI    TIOBINDS,TIOBALRM   BEEP                                         
         DROP  R3                                                               
*                                                                               
ERROR850 DS    0H                                                               
         L     R1,SVPARM                                                        
         MVC   0(1,R1),ERRTYPE     PASS ERROR CODE TO APPLICATION               
         MVC   4(1,R1),VASTAT      RETURN VALID ACCESS FLAGS                    
         L     RD,SAVERD           GET ALL THE WAY OUT                          
         B     EXITL               EXIT CC NOT EQUAL                            
*                                                                               
* ADDWORD - APPENDS WORD TO MESSAGE TEXT                                        
*                                                                               
*   BEFORE: RF=A(WORD TO APPEND)                                                
*           R2=A(NEXT SPACE IN MESSAGE TEXT)                                    
*           BYTE=MAX LENGTH OF WORD (SPACE OR NULL PADDED)                      
*                                                                               
*    AFTER: R2=A(NEXT AVAILABLE SPACE IN MSG)                                   
*           BYTE=ACTUAL LENGTH OF WORD APPENDED                                 
*                                                                               
*     NOTE: USES R1                                                             
*                                                                               
ADDWORD  DS    0H                                                               
         ZIC   R1,BYTE                                                          
         AR    R1,RF                                                            
ADDW010  DS    0H                                                               
         BCTR  R1,0                                                             
         CR    R1,RF                                                            
         BL    ADDW050                                                          
         CLI   0(R1),0                                                          
         BE    ADDW010                                                          
         CLI   0(R1),C' '                                                       
         BE    ADDW010                                                          
*                                                                               
         SR    R1,RF               LEN OF WORD-1                                
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
         LA    R1,1(R1)                                                         
         AR    R2,R1               BUMP INSERTION POINT                         
         STC   R1,BYTE                                                          
         BR    RE                                                               
ADDW050  DS    0H                  NO WORD                                      
         MVI   BYTE,0                                                           
         BR    RE                                                               
**********************************************************************          
* INCCOMP - INCLUSIVE BUFFER COMPARISON - CHECKS FOR PRESENCE OF ALL            
*           MEMBERS OF SET A IN SET B                                           
*                                                                               
*          R2 = A(SET A)                                                        
*          R3 = A(SET B)                                                        
*                                                                               
*          RETURNS: CC EQUAL: A IS SUBSET OF B                                  
*                   CC NOT EQUAL: A IS NOT SUBSET OF B                          
*                                 ERRCODE = 1ST CODE FAILED ON                  
**********************************************************************          
INCCOMP  NTR1                                                                   
         CLC   1(1,R2),1(R3)                                                    
         BE    *+6                                                              
         DC    H'0'                CAN'T HANDLE CODES OF UNEQUAL LENGTH         
         ZIC   R1,1(R2)                                                         
         BCTR  R1,0                                                             
*                                                                               
         LA    R2,3(R2)            FIRST CODE IN SET A                          
ICOMP010 DS    0H                                                               
         CLI   0(R2),X'FF'                                                      
         BE    EXITOK                                                           
         LA    R4,3(R3)                                                         
ICOMP015 DS    0H                                                               
         CLI   0(R4),X'FF'                                                      
         BNE   ICOMP020                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ERRCODE(0),0(R2)                                                 
         B     EXITL                                                            
ICOMP020 DS    0H                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(R4)                                                    
         BE    ICOMP025                                                         
         LA    R4,1(R1,R4)                                                      
         B     ICOMP015                                                         
ICOMP025 DS    0H                                                               
         LA    R2,1(R1,R2)                                                      
         B     ICOMP010                                                         
**********************************************************************          
* EXCCOMP - EXCLUSIVE BUFFER COMPARISON - CHECKS FOR PRESENCE OF ANY            
*           MEMBERS OF SET A IN SET B                                           
*                                                                               
*          R2 = A(SET A)                                                        
*          R3 = A(SET B)                                                        
*                                                                               
*          RETURNS: CC EQUAL: NONE OF A FOUND IN B                              
*                   CC NOT EQUAL: MEMBER OF A FOUND IN B                        
*                                 ERRCODE = 1ST CODE FAILED ON                  
**********************************************************************          
EXCCOMP  NTR1                                                                   
         CLC   1(1,R2),1(R3)                                                    
         BE    *+6                                                              
         DC    H'0'                CAN'T HANDLE CODES OF UNEQUAL LENGTH         
         ZIC   R1,1(R2)                                                         
         BCTR  R1,0                                                             
*                                                                               
         LA    R2,3(R2)            FIRST CODE IN SET A                          
ECOMP010 DS    0H                                                               
         CLI   0(R2),X'FF'                                                      
         BE    EXITOK                                                           
         LA    R4,3(R3)                                                         
ECOMP015 DS    0H                                                               
         CLI   0(R4),X'FF'                                                      
         BE    ECOMP025                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(R4)                                                    
         BNE   ECOMP020                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ERRCODE(0),0(R2)                                                 
         B     EXITL                                                            
ECOMP020 DS    0H                                                               
         LA    R4,1(R1,R4)                                                      
         B     ECOMP015                                                         
ECOMP025 DS    0H                                                               
         LA    R2,1(R1,R2)                                                      
         B     ECOMP010                                                         
**********************************************************************          
* BLDPAR - BUILD PAR CODE LIST IN APPROPRIATE BUFFER                            
*          PRE: R6 = A(CURRENT PAR SECURITY ELEMENT)                            
*          POST: BUFFADDR = A(PAR CODE BUFFER)                                  
**********************************************************************          
BLDPAR   NTR1                                                                   
         MVI   BUFFTYPE,PARBUFF    PAR CODE BUFFER AREA                         
         BAS   RE,GETBUFF                                                       
         L     R2,BUFFADDR         A(BUFFER TO BE USED)                         
*                                                                               
         MVI   0(R2),PARBUFF       RESET OR INITIALIZE BUFFER                   
         MVC   1(1,R2),27(R9)      CODE LENGTH                                  
         MVI   2(R2),0             NO FLAGS YET                                 
         MVI   3(R2),X'FF'         END MARKER                                   
         MVI   4(R2),0             END MARKER                                   
*                                                                               
         XC    SETS,SETS           CLEAR SET BUFFER                             
*                                                                               
         ZIC   R5,1(R6)            ELEM LEN                                     
         AR    R5,R6               END OF ELEM + 1                              
         LA    R6,RSECSCCL-RSECSCEL(R6)  1ST MINI-ELEM                          
BLDP050  DS    0H                                                               
         CR    R6,R5                                                            
         BNL   BLDP200             DONE WITH ELEMENT                            
*                                                                               
         CLI   7(R9),X'60'         S/P?                                         
         BNE   BLDP060             NO                                           
         CLI   1(R6),C'#'          SPECIAL TEAM ENTRY IN S/P FIELD?             
         BNE   BLDP060                                                          
         LA    R3,1(R6)            A(TEAM CODE W/PREFIX)                        
         BAS   RE,EXTEAM           EXPAND TEAM INTO S/P'S                       
         ZIC   R1,0(R6)                                                         
         LA    R6,1(R1,R6)         NEXT MINI-ELEM                               
         B     BLDP050                                                          
*                                                                               
BLDP060  DS    0H                                                               
*PROT*   OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
*PROT*   BZ    BLDP100             NO                                           
         CLC   21(2,R9),=X'0000'   SETS WITH THIS CODE TYPE?                    
         BE    BLDP100             NO                                           
         CLI   1(R6),C'*'          SET?                                         
         BNE   BLDP100             NO                                           
*                                                                               
         LA    R4,SETS             FIND FIRST OPENING IN SETS                   
         CLI   0(R4),0                                                          
         BE    *+12                                                             
         LA    R4,4(R4)                                                         
         B     *-12                                                             
*                                                                               
         ZIC   R1,0(R6)            SET LEN W/*                                  
         BCTR  R1,0                FOR *                                        
         BCTR  R1,0                FOR EX                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),2(R6)       PUT SET IN SET LIST                          
         LA    R6,3(R1,R6)         NEXT MINI-ELEM                               
         B     BLDP050                                                          
*                                                                               
BLDP100 DS     0H                                                               
         XC    WORK,WORK                                                        
         ZIC   R1,0(R6)            CODE LEN                                     
         BCTR  R1,0                FOR EX                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),1(R6)       PUT CODE IN CODE LIST                        
         LA    R3,WORK                                                          
         BAS   RE,ADDCODE                                                       
***>     LA    RF,0(R1,R2)         LAST CHAR IN CODE                            
***>     CLI   0(RF),C'+'          SPECIAL CASE?                                
***>     BNE   BLDP110             NO                                           
***>     MVI   0(RF),C' '          REMOVE '+'                                   
***>     MVI   4(R2),C'+'          PUT IT IN LAST SPOT                          
BLDP110 DS     0H                                                               
         LA    R1,1(R1)            UNDO BCTR                                    
         LA    R6,1(R1,R6)         NEXT MINI-ELEM                               
         B     BLDP050                                                          
*                                                                               
BLDP200 DS     0H                  NOW EXPAND SETS INTO PARCODES LIST           
         LA    R4,SETS                                                          
BLDP210 DS     0H                                                               
         OC    0(4,R4),0(R4)                                                    
         BZ    BLDP250             DONE WITH SETS                               
         BAS   RE,EXPSET                                                        
         LA    R4,4(R4)            NEXT SET                                     
         B     BLDP210                                                          
BLDP250 DS     0H                                                               
         B     EXIT                                                             
*                                                                               
**********************************************************************          
* BLDBLK - BUILD CODE LIST FROM FILTER BLOCK IN APPROPRIATE BUFFER              
**********************************************************************          
BLDBLK   NTR1                                                                   
         MVC   BUFFTYPE,26(R9)     FIND CODE BUFFER AREA                        
         BAS   RE,GETBUFF                                                       
         BNE   *+6                                                              
         DC    H'0'                SHOULDN'T ALREADY EXIST                      
         L     R2,BUFFADDR         A(BUFFER TO BE USED)                         
*                                                                               
         XC    SETS,SETS           CLEAR TEMP SET XPANSION AREA                 
*                                                                               
         MVC   0(1,R2),BUFFTYPE    BUFFER TYPE                                  
         MVC   1(1,R2),27(R9)      CODE LENGTH                                  
         MVI   2(R2),0             NO FLAGS YET                                 
         MVI   3(R2),X'FF'         END MARKER                                   
         MVI   4(R2),0             END MARKER                                   
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER?                                  
         BE    EXITL               NO FILTER - JUST LEAVE                       
*                                                                               
         CLI   7(R9),X'60'         S/P?                                         
         BNE   BLDB030             NO                                           
         CLI   0(R3),X'FF'         SPECIAL TEAM ENTRY IN S/P FIELD?             
         BNE   BLDB030                                                          
         BAS   RE,EXTEAM           EXPAND TEAM INTO S/P'S                       
         B     EXITOK                                                           
*                                                                               
BLDB030  DS    0H                                                               
*PROT*   OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
*PROT*   BZ    BLDB050             NO                                           
         CLC   21(2,R9),=X'0000'   SETS WITH THIS CODE TYPE?                    
         BE    BLDB050             NO                                           
         CLI   0(R3),C'*'          SET?                                         
         BNE   BLDB050             NO                                           
*                                                                               
         MVC   SETS(4),1(R3)       PUT SET IN BEGINNING OF LIST                 
         LA    R4,SETS             START OF LIST                                
BLDB040  DS    0H                                                               
         OC    0(4,R4),0(R4)       DONE WITH SET LIST?                          
         BZ    EXITOK              YES                                          
         BAS   RE,EXPSET           NO - EXPLODE SET                             
         LA    R4,4(R4)            NEXT SET                                     
         B     BLDB040                                                          
*                                                                               
BLDB050  DS    0H                                                               
         BAS   RE,ADDCODE          WRITE CODE TO BUFFER                         
         B     EXITOK                                                           
*                                                                               
**********************************************************************          
* EXPSET - EXPAND SET                                                           
*          R4 = SET IN SET LIST TO BE EXPANDED INTO CODE LIST                   
*          R2 = CODE LIST TO EXPAND CODES INTO                                  
**********************************************************************          
EXPSET   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSETREC,R6                                                       
         MVI   RSETKTYP,RSETKTYQ                                                
         MVC   RSETKREP,AGENCYCT   REP CODE                                     
         MVC   RSETKSET,21(R9)     SET TYPE FROM TABLE                          
         MVC   RSETKID,0(R4)       SET IDENTIFIER FROM SET LIST                 
         OC    RSETKSET(6),SPACES                                               
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BNE   EXITL               MISSING REC - GET OUT FOR NOW                
         DROP  R6                                                               
*                                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,1                                                         
         BAS   RE,GETEL                                                         
         BNE   EXPSET40                                                         
*                                                                               
         USING RSET1DES,R6                                                      
         TM    RSET1FLG,X'08'      EXCLUSION SET?                               
         BZ    EXPSET20            NO                                           
         OI    2(R2),X'80'         MARK BUFFER AS EXCLUSION                     
         CLI   3(R2),X'FF'         BUFFER EMPTY?                                
         BE    EXPSET25            YES - OK PROCEED                             
         MVI   ERRTYPE,7           ERROR - CAN'T COMBINE                        
         BAS   RE,ERROR                                                         
*                                                                               
EXPSET20 DS    0H                                                               
         TM    2(R2),X'80'         ALREADY HAVE EXCLUSION SET IN BUFF?          
         BZ    EXPSET25            NO - OK                                      
         MVI   ERRTYPE,7           YES - ERROR - CAN'T COMBINE                  
         BAS   RE,ERROR                                                         
*                                                                               
EXPSET25 DS    0H                                                               
         MVI   BYTE,0                                                           
         TM    RSET1FLG,X'80'      SET OF SETS?                                 
         BZ    EXPSET40            NO                                           
         MVI   BYTE,X'FF'          TEMP SET OF SETS FLAG                        
         DROP  R6                                                               
*                                                                               
         LA    R2,SETS             FIND NEXT OPEN SPOT IN SET LIST              
EXPSET30 DS    0H                                                               
         OC    0(4,R2),0(R2)                                                    
         BZ    EXPSET40                                                         
         LA    R2,4(R2)                                                         
         B     EXPSET30                                                         
*                                                                               
EXPSET40 DS    0H                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BNE   EXITL                                                            
         USING RSETMEMD,R6                                                      
         ZIC   R4,RSETMLEN         MEMBER LENGTH                                
         ZIC   R5,1(R6)                                                         
         AR    R5,R6               END OF ELEM+1                                
         LA    R3,RSETMEMB         FIRST MEMBER                                 
         DROP  R6                                                               
EXPSET50 DS    0H                                                               
         CR    R3,R5                                                            
         BNL   EXITOK              DONE                                         
         CLI   BYTE,X'FF'          SET OF SETS?                                 
         BE    EXPSET55            YES                                          
         BAS   RE,ADDCODE          WRITE CODE TO BUFFER                         
         B     EXPSET60                                                         
EXPSET55 DS    0H                                                               
         MVC   0(4,R2),0(R3)       MOVE SET TO SET LIST                         
         OC    0(4,R2),SPACES      SPACE PAD ENTRY                              
         LA    R2,4(R2)            NEXT SPOT IN SET LIST                        
EXPSET60 DS    0H                                                               
         LA    R3,0(R4,R3)         NEXT MEMBER                                  
         B     EXPSET50                                                         
**********************************************************************          
* EXTEAM - EXPAND TEAM INTO SALESPERSON LIST                                    
*          R3 = TEAM CODE (PREFIXED W/X'FF' OR C'#'                             
*          R2 = CODE LIST TO EXPAND CODES INTO                                  
**********************************************************************          
EXTEAM   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSALREC,R6                                                       
         MVC   RSALTTYP,=X'8601'                                                
         MVC   RSALTREP,AGENCYCT   REP CODE                                     
         MVC   RSALTTEM,1(R3)      SET IDENTIFIER FROM SET LIST                 
         OC    RSALTTEM,SPACES                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEY,KEY,0                     
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DISK ERROR                                   
*                                                                               
EXTEAM20 DS    0H                                                               
         CLC   KEY(RSALTSAL-RSALTTYP),KEYSAVE                                   
         BNE   EXITOK                                                           
*                                                                               
         LA    R3,RSALTSAL         SALESMAN CODE                                
         BAS   RE,ADDCODE          WRITE CODE TO BUFFER                         
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'REPDIR',KEY,KEY,0                     
         CLI   8(R1),0                                                          
         BE    EXTEAM20                                                         
         DC    H'0'                DISK ERROR                                   
*                                                                               
**********************************************************************          
* ADDCODE - ADD CODE TO END OF BUFFER (EXCLUDES DUPLICATE ADDS)                 
*           R2=BUFFER   R3=CODE                                                 
**********************************************************************          
ADDCODE  NTR1                                                                   
         ZIC   R1,1(R2)            CODE LEN                                     
         BCTR  R1,0                                                             
         LA    R4,3(R2)            FIRST CODE                                   
ADDC020  CLI   0(R4),X'FF'                                                      
         BE    ADDC050                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R4),0(R3)                                                    
         BE    EXITOK                                                           
         LA    R4,1(R1,R4)         NEXT CODE                                    
         B     ADDC020                                                          
ADDC050  DS    0H                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R4),SPACES                                                   
         LA    R4,1(R1,R4)                                                      
         MVC   0(2,R4),=X'FF00'    END OF BUFFER ELEM & END OF BUFFER           
         B     EXITOK                                                           
**********************************************************************          
* GETSEC - ROUTINE READS REP ACCESS SECURITY RECORD                             
**********************************************************************          
GETSEC   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
RSEC     USING RSECREC,R6                                                       
         MVC   RSEC.RSECKTYP(2),=X'1501'                                        
         MVC   RSEC.RSECKREP,AGENCY                                             
         MVC   RSEC.RSECKPID,PIDNUM                                             
         DROP  RSEC                                                             
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,RSECREC,0,RFBLOCK                   
         B     EXIT                                                             
**********************************************************************          
* INHERIT - ROUTINE ATTEMPTS TO INHERIT FILTER FROM OTHER FILTERS               
*           R2 = A(BUFFER TO INHERIT CODES TO)                                  
**********************************************************************          
INHERIT  NTR1                                                                   
         TM    25(R9),X'80'        INHERIT FROM STATION FILTER?                 
         BZ    INH300              TRY ANOTHER FILTER SOURCE                    
*                                                                               
         MVI   BUFFTYPE,STABUFF                                                 
         BAS   RE,GETBUFF                                                       
         BNE   INH300              NO STATIONS TO INHERIT FROM                  
         L     R4,BUFFADDR                                                      
         CLI   3(R4),X'FF'                                                      
         BE    INH300              NO STATIONS TO INHERIT FROM                  
         TM    2(R4),X'80'                                                      
         BO    INH300              CAN'T INHERIT FROM EXCLUSION                 
*                                                                               
         LA    R5,3(R4)            FIRST STATION CODE                           
*                                                                               
INH020   DS    0H                                                               
         CLI   0(R5),X'FF'         DONE INHERITING?                             
         BNE   INH030              NO - CONTINUE                                
         CLI   3(R2),X'FF'         DID WE INHERIT ANYTHING?                     
         BE    EXITL               NO SET CC                                    
         B     EXITOK              YES SET CC                                   
*                                                                               
INH030   DS    0H                                                               
         XC    WORK,WORK                                                        
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSTAREC,R6                                                       
         MVI   RSTAKTYP,X'02'                                                   
         MVC   RSTAKREP,AGENCYCT   USE SUBSIDIARY REP CODE FOR STA REC          
         MVC   RSTAKSTA,0(R5)                                                   
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                MISSING STATION RECORD                       
*                                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'51'                                                     
         BAS   RE,GETEL            MASTER STATION REC?                          
         BNE   INH050              NO - PROCEED                                 
         MVC   KEY(27),IOAREA      BUILD SUBSID STA REC KEY                     
         MVC   KEY+RSTAKREP-RSTAKEY(2),2(R6)                                    
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                MISSING STATION RECORD                       
*                                                                               
INH050   DS    0H                                                               
         LA    R6,IOAREA                                                        
         CLI   RSTAELEM,X'01'      HAVE 01 ELEM?                                
         BNE   INH250              CAN'T INHERIT ANYTHING                       
*                                                                               
         CLI   7(R9),X'10'         INHERIT GROUP?                               
         BNE   INH100                                                           
         MVC   WORK(L'RSTAGRUP),RSTAGRUP                                        
         CLC   WORK(L'RSTAGRUP),SPACES                                          
         BE    INH250                                                           
         LA    R3,WORK                                                          
         BAS   RE,ADDCODE                                                       
         B     INH250                                                           
*                                                                               
INH100   DS    0H                                                               
         CLI   7(R9),X'30'         INHERIT OWNER?                               
         BNE   INH150                                                           
         MVC   WORK(L'RSTAOWN),RSTAOWN                                          
         CLC   WORK(L'RSTAOWN),SPACES                                           
         BE    INH250                                                           
         LA    R3,WORK                                                          
         BAS   RE,ADDCODE                                                       
         B     INH250                                                           
         DROP  R6                                                               
*                                                                               
INH150   DS    0H                                                               
         CLI   7(R9),X'50'         INHERIT MARKET                               
         BE    *+6                                                              
         DC    H'0'                NO OTHER INHERITANCE POSSIBILITIES           
         MVI   ELCODE,X'08'                                                     
         BAS   RE,GETEL                                                         
         BNE   INH250                                                           
         USING RSTAXXEL,R6                                                      
         MVC   WORK(L'RSTAMKTC),RSTAMKTC                                        
         CLC   WORK(L'RSTAMKTC),SPACES                                          
         BE    INH250                                                           
         LA    R3,WORK                                                          
         BAS   RE,ADDCODE                                                       
         B     INH250                                                           
         DROP  R6                                                               
*                                                                               
INH250   DS    0H                                                               
         ZIC   R1,1(R4)                                                         
         LA    R5,0(R1,R5)         NEXT CODE IN BUFFER                          
         B     INH020                                                           
*                                                                               
INH300   DS    0H                                                               
         TM    25(R9),X'40'        INHERIT FROM S/P FILTER?                     
         BZ    EXITL               NO OTHER SOURCE POSSIBILITIES                
*                                                                               
         MVI   BUFFTYPE,SALBUFF                                                 
         BAS   RE,GETBUFF                                                       
         BNE   EXITL               NO S/P'S TO INHERIT FROM                     
         L     R4,BUFFADDR                                                      
         CLI   3(R4),X'FF'                                                      
         BE    EXITL               NO S/P'S TO INHERIT FROM                     
         TM    2(R4),X'80'                                                      
         BO    EXITL               CAN'T INHERIT FROM EXCLUSION                 
*                                                                               
         LA    R5,3(R4)            FIRST S/P CODE                               
*                                                                               
INH320   DS    0H                                                               
         CLI   0(R5),X'FF'         DONE INHERITING?                             
         BNE   INH330              NO - CONTINUE                                
         CLI   3(R2),X'FF'         DID WE INHERIT ANYTHING?                     
         BE    EXITL               NO SET CC                                    
         B     EXITOK              YES SET CC                                   
*                                                                               
INH330   DS    0H                                                               
         XC    WORK,WORK                                                        
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSALREC,R6                                                       
         MVI   RSALKTYP,X'06'                                                   
         MVC   RSALKREP,AGENCYCT   USE SUBSIDIARY REP CODE FOR S/P REC          
         MVC   RSALKSAL,0(R5)                                                   
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                MISSING S/P RECORD                           
*                                                                               
         LA    R6,IOAREA                                                        
         CLI   RSALELEM,X'01'      HAVE 01 ELEM?                                
         BNE   INH250              CAN'T INHERIT ANYTHING                       
*                                                                               
         CLI   7(R9),X'20'         INHERIT OFFICE                               
         BE    *+6                                                              
         DC    H'0'                NO OTHER POSSIBILITIES                       
         MVC   WORK(L'RSALOFF),RSALOFF                                          
         CLC   WORK(L'RSALOFF),SPACES                                           
         BE    INH450                                                           
         LA    R3,WORK                                                          
         BAS   RE,ADDCODE                                                       
         B     INH450                                                           
         DROP  R6                                                               
*                                                                               
INH450   DS    0H                                                               
         ZIC   R1,1(R4)                                                         
         LA    R5,0(R1,R5)         NEXT CODE IN BUFFER                          
         B     INH320                                                           
*                                                                               
***********************************************************************         
* GETBUFF - FIND BUFFER IN BUFFER AREA DEFINED BY BUFFTYPE OR NEXT              
*           AVAILABLE BUFFER AREA                                               
*                                                                               
*    PRE:  BUFFTYPE = BUFFER TYPE EQUATE TO FIND                                
*    POST: CC EQUAL = BUFFER FOUND ADDR IN BUFFADDR                             
*          CC NOT EQUAL = NOT FOUND A(NEXT AVAIL AREA) IN BUFFADDR              
***********************************************************************         
GETBUFF  NTR1                                                                   
         LA    R6,CBUFF                                                         
GETBUFF2 ST    R6,BUFFADDR                                                      
         CLC   0(1,R6),BUFFTYPE                                                 
         BE    EXITOK                                                           
         CLI   0(R6),0                                                          
         BE    EXITL                                                            
         LA    R2,3(R6)                                                         
         ZIC   R1,1(R6)                                                         
         CLI   0(R2),X'FF'                                                      
         BE    *+12                                                             
         LA    R2,0(R1,R2)                                                      
         B     *-12                                                             
         LA    R6,1(R2)                                                         
         B     GETBUFF2                                                         
*                                                                               
**********************************************************************          
* COMMON ROUTINES                                                               
**********************************************************************          
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITNO   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
OK       EQU   *                                                                
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
EXIT     DS    0H                  JUST EXIT                                    
         XIT1                                                                   
*                                                                               
NOHOOK   BR    RE                                                               
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
SPACES   DC    80C' '                                                           
         LTORG                                                                  
***>>>   FOR TESTING PURPOSES, KEEP THE SIZE OF THIS MODULE THE SAME            
         ORG   RECKSEC+(((*-RECKSEC)/2048)+1)*2048                              
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
RELO     DS    F                                                                
SVPARM   DS    F                                                                
SAVERD   DS    F                                                                
VREPFACS DS    A                                                                
GETFACT  DS    A                                                                
HELLO    DS    A                                                                
DATAMGR  DS    A                                                                
RFBLOCK  DS    0CL6                                                             
COMFACS  DS    A                                                                
AGENCY   DS    CL2                 REP CODE (MASTER)                            
AGENCYCT DS    CL2                 ACTUAL CONNECTED REP CODE (SUBSID)           
ELCODE   DS    X                                                                
BYTE     DS    X                                                                
PIDNUM   DS    CL2                 PASSWORD ID NUMBER                           
*                                                                               
AGRPCDS  DS    A                   A(EXPANDED GROUP FILTER CODES)               
AOWNCDS  DS    A                   A(EXPANDED OWNER FILTER CODES)               
AMKTCDS  DS    A                   A(EXPANDED MARKET FILTER CODES)              
ASTACDS  DS    A                   A(EXPANDED STATION FILTER CODES)             
AOFCCDS  DS    A                   A(EXPANDED OFFICE FILTER CODES)              
ASALCDS  DS    A                   A(EXPANDED SALESMAN FILTER CODES)            
*                                                                               
STATUS   DS    X                   PROGRAM STATUS FLAGS                         
MASTERQ  EQU   X'80'               MASTER REP CONNECTED                         
XOWNFLG  EQU   X'40'               HIT OWNER SPCL CASE                          
*                                                                               
VASTAT   DS    X                   VALID ACCESS STATUS                          
*              X'80'               MATCHED ON GROUP                             
*              X'40'               MATCHED ON OFFICE                            
*              X'20'               MATCHED ON STATION                           
*              X'10'               MATCHED ON OWNER                             
*              X'08'               MATCHED ON MARKET                            
*              X'04'               NOT USED                                     
*              X'02'               MATCHED ON SALESPERSON                       
*                                                                               
*                                                                               
ERRCODE  DS    CL6                 CODE VALIDATION FAILED ON                    
ERRTYPE  DS    X                   ERROR CODE FOR MSG GENERATION                
*            1 = ACCESS RESTRICTED, FILTER MISSING                              
*            2 = ACCESS RESTRICTED, FILTER INVALID                              
*                                                                               
*                                                                               
WORK     DS    CL64                                                             
BUFFTYPE DS    X                                                                
BUFFADDR DS    A                                                                
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
SETS     DS    CL120               30 X 4 SETS TO BE EXPANDED                   
*                                                                               
       ++INCLUDE REGENSEC                                                       
         ORG   RSECREC                                                          
SECREC   DS    XL1000                                                           
IOAREA   DS    XL1000                                                           
*                                                                               
* CODE BUFFER - CONTAINS ELEMENTS (DEFINED BELOW) CONTAINING CODE LISTS         
*                                                                               
* 1 BYTE ELEMENT ID                                                             
* 1 BYTE CODE MAX CHARACTER LENGTH                                              
* 1 BYTE STATUS: X'80'=EXCLUSION SET                                            
* VAR LEN CODE DATA                                                             
* X'FF' TERMINATOR                                                              
*                                                                               
CBUFF    DS    XL(CBUFFLNQ)                                                     
CBUFFLNQ EQU   10000                                                            
STABUFF  EQU   X'01'                                                            
SALBUFF  EQU   X'02'                                                            
BLKBUFF  EQU   X'03'                                                            
PARBUFF  EQU   X'04'                                                            
WORKDX   EQU   *                                                                
SECBLK   DSECT                                                                  
       ++INCLUDE REGENSBLK                                                      
         DSECT                                                                  
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE REGENSTA                                                       
       ++INCLUDE REGENSET                                                       
       ++INCLUDE REGENSAL                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FATIOB                                                         
       ++INCLUDE REPFACSQ                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'129RECKSEC   05/18/15'                                      
         END                                                                    
