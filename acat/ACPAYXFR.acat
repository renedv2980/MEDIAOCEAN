*          DATA SET ACPAYXFR   AT LEVEL 016 AS OF 02/26/04                      
*CATALP ACPAYXFR                                                                
         TITLE 'ACPAYXFR - COKE MEDIA / ACCOUNT INTERFACE'                      
ACPAYXFR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LWSEND-LWSD,ACPAYXFR,R9,CLEAR=YES                                
         USING LWSD,RC                                                          
         RELOC RELO                                                             
         ST    R1,ADPARAMS                                                      
         MVC   PARAMS(PARAMLEN),0(R1)                                           
         L     R8,ADCOMFAC                                                      
         USING COMFACSD,R8                                                      
         L     R7,ADPAYD                                                        
         USING PAYXFRD,R7                                                       
*                                                                               
         L     R2,ADMSGH           GET OUTPUT MESSAGE AREA                      
         XC    8(60,R2),8(R2)                                                   
*                                                                               
         L     R2,ADACT                                                         
         MVC   ACTION,0(R2)                                                     
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVI   DMINBTS,X'C0'                                                    
         MVI   DMOUTBTS,X'7D'                                                   
         GOTO1 CDATCON,DMCB,(5,0),(2,TODAY2)                                    
*                                                                               
         XC    DMCB(12),DMCB                                                    
         MVC   DMCB+4(3),=X'D9000A'                                             
         MVI   DMCB+7,QSQUASH                                                   
         GOTO1 CCALLOV,DMCB                                                     
         MVC   SQUASHER,0(R1)                                                   
*                                                                               
         MVI   DMCB+7,QADDTRN                                                   
         GOTO1 CCALLOV,DMCB                                                     
         MVC   ADDTRN,0(R1)                                                     
*                                                                               
         LA    R2,IO                                                            
         ST    R2,ATRANS                                                        
         ST    R2,TRNREC                                                        
         MVC   TRNCOMF,ADCOMFAC                                                 
         LA    RE,SELDGTAB         SET FOR SE LEDGER                            
         ST    RE,TRNLDG                                                        
         MVI   TRN#LDGS,1          SET NUMBER OF LEDGER TABLE ENTRIES           
*                                                                               
         LH    R1,=Y(IO2-LWSD)                                                  
         LA    R1,LWSD(R1)                                                      
         ST    R1,AACCOUNT                                                      
         LH    R1,=Y(IO3-LWSD)                                                  
         LA    R1,LWSD(R1)                                                      
         ST    R1,ACONTRA                                                       
         LH    R1,=Y(IO4-LWSD)                                                  
         LA    R1,LWSD(R1)                                                      
         ST    R1,ABUCKET                                                       
*                                                                               
         MVC   TRNACC,AACCOUNT                                                  
         MVC   TRNCAC,ACONTRA                                                   
         MVC   TRNBUK,ABUCKET                                                   
         EJECT                                                                  
VALCOM   MVC   COMPANY,XFRCOM                                                   
         LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKCULA,IO                                                      
         BNE   NTVCOM                                                           
*                                                                               
VALCOM2  GOTO1 GETL,DMCB,('CPYELQ',IO),0                                        
         CLI   ELERR,0             TEST FOR COMPANY ELEMENT FOUND               
         BNE   NTVCOMR                                                          
         L     R3,ELADDR                                                        
         USING CPYELD,R3                                                        
         MVC   TRNCPYS1,CPYSTAT1                                                
         MVC   TRNCPYS2,CPYSTAT2                                                
         MVC   TRNCPYS3,CPYSTAT3                                                
         MVC   TRNCPYS4,CPYSTAT4                                                
         MVC   TRNCPYS5,CPYSTAT5                                                
         MVC   TRNCPYS6,CPYSTAT6                                                
         MVC   TRNCPYS7,CPYSTAT7                                                
         MVC   TRNCPYS8,CPYSTAT8                                                
         CLI   CPYLN,CPYLN3Q                                                    
         BL    SETPAL                                                           
         MVC   TRNCPYS9,CPYSTAT9                                                
         MVC   TRNCPYSA,CPYSTATA                                                
                                                                                
SETPAL   LA    R0,PALAREA                                                       
         STCM  R0,15,TRNPAL                                                     
*                                                                               
         GOTO1 CGETFACT,DMCB,(X'80',0),F#UTLD                                   
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         TM    F@TTEST,X'80'       TEST UPD=NO                                  
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
*                                                                               
         GOTO1 CGETFACT,DMCB,(X'80',0),F#SELISD                                 
         L     R1,0(R1)                                                         
         USING F@SELISD,R1                                                      
         TM    F@SEIND,X'04'       TEST READ ONLY                               
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
         DROP  R1,R3                                                            
         EJECT                                                                  
*              VALIDATE MEDIA AND VEHICLE                                       
VALMED   OC    XFRMD,SPACES                                                     
         CLC   XFRMD,SPACES                                                     
         BE    NTVMED                                                           
         CLI   XFRMD+1,C' '                                                     
         BE    NTVMED                                                           
         MVC   MEDIA,XFRMD                                                      
*                                                                               
         OC    XFRVEH,SPACES       VALIDATE VEHICLE                             
         CLC   XFRVEH,SPACES                                                    
         BE    NTVVEH                                                           
         MVC   VEHICLE,XFRVEH                                                   
*                                                                               
         CLC   =C'ST',MEDIA        SPOT TV                                      
         BNE   VALMED3                                                          
         CLI   VEHICLE,C'0'        AND NUMERIC STATION                          
         BL    VALMED3                                                          
         MVI   MEDIA+1,C'C'        THEN MEDIA IS REALLY CABLE                   
         MVC   VEHICLE+5(2),=C'CA'                                              
*                                                                               
VALMED3  LA    R4,KEY              READ FOR MEDIA                               
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'3M'                                             
         MVC   ACTKCULA+3(2),MEDIA                                              
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVMED              NOT VALID MEDIA                              
         EJECT                                                                  
*              VALIDATE VEHICLE                                                 
VALVEH   LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'3M'                                             
         MVC   ACTKCULA+3(2),MEDIA                                              
         MVC   ACTKCULA+5(8),VEHICLE                                            
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVVEH              NOT VALID VEHICLE                            
         MVC   VEHNAM,SPACES                                                    
         GOTO1 GETL,DMCB,('NAMELQ',IO),0                                        
         CLI   ELERR,0                                                          
         BNE   NTVVEH              NO PRODUCT NAME                              
         L     R3,ELADDR                                                        
         USING NAMELD,R3                                                        
         ZIC   R1,NAMLN                                                         
         SH    R1,=Y(NAMLN1Q+1)                                                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   VEHNAM(0),NAMEREC    SAVE PRODUCT NAME                           
         SPACE 1                                                                
         GOTO1 CDATCON,DMCB,(1,XFRSTART),(3,BSTART)                             
         MVC   DEACN,SPACES                                                     
         GOTO1 GETL,DMCB,('OTHELQ',IO),0    GET DEFAULT ACN NUMBER              
         CLI   ELERR,0                                                          
         BNE   VALPRD              NO ACN NUMBER                                
         L     R3,ELADDR                                                        
         USING OTHELD,R3                                                        
GETACN   CLC   OTHDATE,SPACES                                                   
         BNE   *+14                                                             
         MVC   DEACN,OTHNUM        SAVE DEFAULT ACN                             
         B     GETACN2                                                          
         CLC   BSTART(2),OTHDATE   TEST BUY START VS. ACN START                 
         BL    GETACN2                                                          
         MVC   DEACN,OTHNUM        NEW ACN                                      
         B     VALPRD                                                           
*                                                                               
GETACN2  SR    R0,R0               GET NEXT X'23' ELEMENT                       
         IC    R0,OTHLN                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0             TEST EOR                                     
         BE    VALPRD                                                           
         CLI   OTHEL,OTHELQ                                                     
         BNE   GETACN2                                                          
         B     GETACN                                                           
         DROP  R3                                                               
         EJECT                                                                  
*              VALIDATE PRODUCT                                                 
VALPRD   OC    XFRPRD,SPACES                                                    
         CLC   XFRPRD,SPACES                                                    
         BE    NTVPRD                                                           
         CLI   XFRPRD+2,C' '                                                    
         BNE   NTVPRD                                                           
         MVC   PRD,XFRPRD                                                       
         LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'3P'                                             
         MVC   ACTKCULA+3(2),PRD                                                
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVPRD              NOT VALID PRODUCT                            
         MVC   PRDNAM,SPACES                                                    
         GOTO1 GETL,DMCB,('NAMELQ',IO),0                                        
         CLI   ELERR,0                                                          
         BNE   NTVPRD              NO PRODUCT NAME                              
         L     R3,ELADDR                                                        
         USING NAMELD,R3                                                        
         ZIC   R1,NAMLN                                                         
         SH    R1,=Y(NAMLN1Q+1)                                                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRDNAM(0),NAMEREC    SAVE PRODUCT NAME                           
*GET THE BUDGET CODE FOR PRODUCT                                                
GTYTD    MVC   XFRBUDPR,SPACES                                                  
         GOTO1 GETL,DMCB,('OTHELQ',IO),0                                        
         CLI   ELERR,0                                                          
         BNE   NTVBUDC             NO BUDGET CODE                               
         L     R3,ELADDR                                                        
         USING OTHELD,R3                                                        
         MVC   PRDBUDC,OTHNUM      SAVE PRODUCT BUDGET CODE                     
         MVC   XFRBUDPR(2),OTHNUM                                               
*                                  TEST CATEGORY                                
         CLC   XFRCAT,SPACES                                                    
         BNH   MISCAT                                                           
         EJECT                                                                  
*              VALIDATE ACN NUMBER                                              
VALACN   OC    XFRACN,SPACES                                                    
         CLC   XFRACN,SPACES                                                    
         BNE   *+10                                                             
         MVC   XFRACN,DEACN        IF NO ACN USE DEFAULT                        
         CLC   XFRACN,SPACES                                                    
         BE    NTVACN              MISSING ACN NUMBER                           
         MVC   ACN,XFRACN                                                       
         LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'SE'                                             
         MVC   ACTKCULA+3(5),ACN                                                
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVACN              NOT VALID ACN                                
         EJECT                                                                  
*              VALIDATE AGENCY                                                  
VALAGY   LA    R1,XFRAGYNO                                                      
         LA    R3,3                                                             
         CLI   0(R1),C'0'                                                       
         BL    NTVAGY                                                           
         CLI   0(R1),C'9'                                                       
         BH    NTVAGY                                                           
         LA    R1,1(R1)                                                         
         BCT   R3,*-20                                                          
         MVC   AGYCDE,XFRAGYNO                                                  
         LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA+8(3),AGYCDE                                             
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVAGY              NOT VALID AGENCY NUMBER                      
         GOTO1 GETL,DMCB,('RSTELQ',IO),0                                        
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                NO STATUS ELEMENT                            
         L     R3,ELADDR                                                        
         USING RSTELD,R3                                                        
         TM    RSTSTAT,X'20'                                                    
         BO    ACCLCK              ACCOUNT IS LOCKED                            
         SPACE 1                                                                
         MVI   BOTSW,C'B'          ASSUME BOTTLER BOUGHT                        
         CLC   AGYCDE,=C'100'                                                   
         BL    VALAGY3                                                          
         CLC   AGYCDE,=C'899'                                                   
         BH    VALAGY3                                                          
         MVI   BOTSW,C'A'                                                       
VALAGY3  DC    0H'0'                                                            
         DROP  R3                                                               
         EJECT                                                                  
*              VALIDATE ACN/AGENCY ON SX                                        
VALSX    CLI   XFREND,X'96'                                                     
         BNL   *+12                                                             
         CLI   BOTSW,C'B'                                                       
         BE    VALEND              DON'T NEED SX FOR BOTTLER BOUGHT             
         LA    R4,KEY                                                           
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(42),SPACES                                              
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'SX'                                             
         MVC   ACTKCULA+3(5),ACN                                                
         MVC   ACTKCULA+8(3),AGYCDE                                             
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   NTVAGY              NOT VALID AGENCY NUMBER                      
         GOTO1 GETL,DMCB,('RSTELQ',IO),0                                        
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                NO STATUS ELEMENT                            
         L     R3,ELADDR                                                        
         USING RSTELD,R3                                                        
         TM    RSTSTAT,X'20'                                                    
         BO    ACCLCK              ACCOUNT IS LOCKED                            
VALEND   DC    0H'0'                                                            
         DROP  R4                                                               
         EJECT                                                                  
*        GET THE YTD FIGURES                                                    
         ZAP   YTD,=P'0'                                                        
         ZAP   XFRYTD,=P'0'                                                     
         MVC   BUDST(1),XFREND     BUILD YTD (QTR - SPOT) START                 
         MVI   BUDST+1,1           AND END DATES                                
         MVC   BUDEN(1),XFREND                                                  
         MVI   BUDEN+1,X'12'                                                    
         MVI   QTRNUM,0                                                         
         CLC   MEDIA,=C'ST'        FOR SPOT GET QUARTERLY YTD/BUD               
         B     GTYTD6              MEL WANTS YTD                                
         BNE   GTYTD6                                                           
         LA    R1,BUDPER                                                        
GTYTD2   CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   0(1,R1),XFREND+1                                                 
         BE    GTYTD4                                                           
         LA    R1,4(R1)                                                         
         B     GTYTD2                                                           
GTYTD4   MVC   BUDST+1(1),1(R1)                                                 
         MVC   BUDEN+1(1),2(R1)                                                 
         MVC   QTRNUM,3(R1)        1ST, 2ND, 3RD, 4TH QTR                       
         SPACE 1                                                                
*        BUILD KEY FOR HISTORY BUCKETS                                          
GTYTD6   LA    R4,KEY                                                           
         USING CACRECD,R4                                                       
         MVC   KEY,SPACES                                                       
         MVC   CACKCPY(1),COMPANY                                               
         MVC   CACKUNT(2),=C'SE'                                                
         MVC   CACKACT(5),ACN                                                   
         MVC   CACKACT+5(3),AGYCDE                                              
         MVC   CACKCCPY,COMPANY                                                 
         MVC   CACKCUNT(2),=C'3P'                                               
         MVC   CACKCACT(2),PRD                                                  
         MVC   CACKCACT+2(2),MEDIA                                              
         MVI   CACKBTYP,C'E'                                                    
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   GTYTD30             NO HISTORY RECORD                            
*        GET YTD FROM GROSS(DEBIT) BUCKETS                                      
         LA    R4,IO                                                            
         LA    R3,ACCORFST(R4)                                                  
GTYTD10  CLI   0(R3),0                                                          
         BE    GTYTD30             END OF RECORD                                
         CLI   0(R3),X'45'                                                      
         BE    GTYTD12                                                          
GTYTD11  ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GTYTD10                                                          
         SPACE 1                                                                
         USING BUKELD,R3                                                        
GTYTD12  CLC   BUKYEAR(2),BUDST                                                 
         BL    GTYTD11                                                          
         CLC   BUKYEAR(2),BUDEN                                                 
         BH    GTYTD11                                                          
         AP    YTD,BUKDR                                                        
         B     GTYTD11                                                          
         SPACE 1                                                                
GTYTD30  CP    YTD,=P'0'                                                        
         BE    GTBUD               NO YTD                                       
         ZAP   PWRK,YTD                                                         
         DP    PWRK,=P'100'                                                     
         ZAP   YTD,PWRK(11)                                                     
         ZAP   XFRYTD,YTD                                                       
         DROP  R3,R4                                                            
         EJECT                                                                  
*        GET BUDGET FIGURES                                                     
         SPACE 1                                                                
GTBUD    ZAP   BUD,=P'0'                                                        
         ZAP   XFRBUDG,=P'0'                                                    
*        BUILD KEY FOR BUDGET RECORD                                            
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING BUDRECD,R4                                                       
         MVI   BUDKTYP,BUDKTYPQ                                                 
         MVC   BUDKCULA,SPACES                                                  
         MVC   BUDKCULA(1),COMPANY                                              
         MVC   BUDKCULA+1(2),=C'SE'                                             
         MVC   BUDKCULA+3(5),ACN                                                
         MVC   BUDKCULA+8(3),AGYCDE                                             
         MVC   BUDKWORK,SPACES                                                  
         MVC   BUDKCCPY(15),SPACES                                              
         MVC   BUDKCCPY(1),COMPANY                                              
         MVC   BUDKCCPY+1(2),=C'3P'                                             
         MVC   BUDKCCPY+3(2),PRDBUDC                                            
         MVC   BUDKCCPY+5(2),MEDIA                                              
         MVI   BUDKBUDN+1,1                                                     
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BNE   GTBUD30             NO BUDGET RECORD                             
*        GET BUDGET FOR PERIOD                                                  
         LA    R4,IO                                                            
         LA    R3,ACCORFST(R4)                                                  
GTBUD10  CLI   0(R3),0                                                          
         BE    GTBUD30             END OF RECORD                                
         CLI   0(R3),X'1D'                                                      
         BE    GTBUD12                                                          
GTBUD11  ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GTBUD10                                                          
         SPACE 1                                                                
         USING BAMELD,R3                                                        
GTBUD12  CLC   BAMMNTH,BUDST                                                    
         BL    GTBUD11                                                          
         CLC   BAMMNTH,BUDEN                                                    
         BH    GTBUD11                                                          
         AP    BUD,BAMBUDG                                                      
         B     GTBUD11                                                          
         DROP  R3                                                               
         SPACE 1                                                                
GTBUD30  CP    BUD,=P'0'                                                        
         BE    GTBUDX              NO BUDGET                                    
         ZAP   PWRK,BUD                                                         
         DP    PWRK,=P'100'                                                     
         ZAP   BUD,PWRK(11)                                                     
         ZAP   XFRBUDG,BUD                                                      
         SPACE 1                                                                
GTBUDX   CLC   ACTION(3),=C'GET'                                                
         BE    NOERRS           NO ERRORS RETURN                                
         CLC   ACTION(4),=C'POST'                                               
         BE    EDINV                                                            
         DC    H'0'                INVALID ACTION                               
         DROP  R4                                                               
         EJECT                                                                  
*        EDIT INVOICES/AMOUNTS                                                  
EDINV    LA    R3,XFRDATA                                                       
         USING XFRINVD,R3                                                       
         LA    R0,5                                                             
EDINV1   OC    XFRINV,SPACES                                                    
         LA    RE,XFRGROSS                                                      
         LA    RF,3                                                             
         CP    0(L'XFRGROSS,RE),=P'0'                                           
         BNE   EDINV3                                                           
         LA    RE,L'XFRGROSS(RE)                                                
         BCT   RF,*-14                                                          
         CLC   XFRINV,SPACES                                                    
         BNE   NTVINV              INVALID INVOICE NUMBER(NO AMOUNTS)           
         B     EDINV5                                                           
EDINV3   CLC   XFRINV,SPACES                                                    
         BE    MISINV              MISSING INVOICE NUMBER                       
EDINV5   LA    R3,XFRINLEN(R3)                                                  
         BCT   R0,EDINV1                                                        
         SPACE 1                                                                
         LA    R4,WORK             GET LEDGER INFO FOR ADDTRN                   
         MVC   WORK,SPACES                                                      
         USING ACTRECD,R4                                                       
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'SE'                                             
         GOTO1 GETLED,DMCB,WORK,SELDGTAB                                        
         LA    R4,WORK                                                          
         MVC   WORK,SPACES                                                      
         MVC   ACTKCULA(1),COMPANY                                              
         MVC   ACTKCULA+1(2),=C'SX'                                             
         GOTO1 GETLED,DMCB,WORK,SXLDGTAB                                        
         DROP  R4                                                               
*                                                                               
         LA    R3,XFRDATA                                                       
         LA    R0,5                                                             
*                                                                               
EDINV8   LA    RE,XFRGROSS                                                      
         LA    RF,3                                                             
         CP    0(L'XFRGROSS,RE),=P'0'                                           
         BNE   *+16                                                             
         LA    RE,L'XFRGROSS(RE)                                                
         BCT   RF,*-14                                                          
         B     EDINV10             NOTHING TO POST                              
         BAS   RE,BLDCOM           BUILD COMMENTS FIELD                         
         MVC   INVOICE,XFRINV                                                   
         MVC   ADVDTE,XFREND                                                    
         ZAP   GROSS,XFRGROSS                                                   
         ZAP   NET,XFRNET                                                       
         AP    NET,XFRCD           PRINT PAY SUBTRACTED CD FROM NET             
         ZAP   CSHD,XFRCD                                                       
         XC    ESTN,ESTN                                                        
         CLI   XFREST,C'0'                                                      
         BL    EDINV09                                                          
         PACK  DUB,XFREST                                                       
         CVB   R1,DUB                                                           
         STCM  R1,3,ESTN                                                        
EDINV09  BAS   RE,TRNADD           ADD TRANSACTIONS TO FILE                     
EDINV10  LA    R3,XFRINLEN(R3)                                                  
         BCT   R0,EDINV8                                                        
*                                                                               
         CLI   POSTSW,C'Y'         TEST ANY POSTINGS MADE                       
         BNE   NOERRS              NO-ALL DONE                                  
*                                                                               
         MVI   TRNINDS,TRNILAST    YES-LAST TIME CALL FOR ADDTRN                
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         B     NOERRS                                                           
         EJECT                                                                  
*        ADD TRANSACTION                                                        
         SPACE 1                                                                
TRNADD   NTR1                                                                   
         USING TRNRECD,R4                                                       
         LA    R4,IO                                                            
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,COMPANY                                                  
         MVC   TRNKUNT(2),=C'SE'                                                
         MVC   TRNKACT(5),ACN                                                   
         MVC   TRNKACT+5(3),AGYCDE                                              
         MVC   TRNKCCPY(1),COMPANY                                              
         MVC   TRNKCUNT(2),=C'3P'                                               
         MVC   TRNKCACT(2),PRD      PRODUCT                                     
         MVC   TRNKCACT+2(2),MEDIA    MEDIA                                     
         MVC   TRNKCACT+4(8),VEHICLE  VEHICLE                                   
         MVC   TRNKDATE,ADVDTE                                                  
*                                                                               
         LA    RF,INVOICE+L'INVOICE-1    GET LAST SIX                           
         LA    R1,L'INVOICE                                                     
         CLI   0(RF),C' '                                                       
         BH    *+12                                                             
         BCTR  RF,0                                                             
         BCT   R1,*-10                                                          
         DC    H'0'                      NO INVOICE NUMBER?                     
         CH    R1,=H'6'                                                         
         BNL   *+12                                                             
         LA    RF,INVOICE                                                       
         B     *+8                                                              
         SH    RF,=H'5'                                                         
         MVC   TRNKREF,0(RF)                                                    
*                                                                               
         MVI   TRNKSBR,0                                                        
         XC    TRNKSTA(10),TRNKSTA                                              
         SPACE 1                                                                
*              BUILD ELEMENTS                                                   
         USING TRNELD,R3                                                        
         LA    R3,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q                                                    
         MVC   TRNDATE,ADVDTE                                                   
         MVC   TRNREF,TRNKREF                                                   
         OC    TRNREF,SPACES                                                    
         MVI   TRNSUB,0                                                         
         MVI   TRNTYPE,0                                                        
         MVI   TRNSTAT,X'80'                                                    
         MVC   TRNBTCH,SPACES                                                   
         ZIC   R1,TRNDATE          CONVERT YYMMDD TO MOS                        
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         STC   R1,TRNBTCH                                                       
         OI    TRNBTCH,X'F0'                                                    
         ZIC   R1,TRNDATE+1                                                     
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         STC   R1,TRNBTCH+1                                                     
         OI    TRNBTCH+1,X'F0'                                                  
         CLI   TRNDATE+1,X'10'                                                  
         BL    TRNADD3                                                          
         MVI   TRNBTCH+1,C'A'                                                   
         CLI   TRNDATE+1,X'11'                                                  
         BL    TRNADD3                                                          
         MVI   TRNBTCH+1,C'B'                                                   
         CLI   TRNDATE+1,X'12'                                                  
         BL    TRNADD3                                                          
         MVI   TRNBTCH+1,C'C'                                                   
         SPACE 1                                                                
TRNADD3  MVC   TRNANAL,SPACES                                                   
         ZAP   TRNAMNT,=P'0'       IF <96  AMOUNT=0                             
         CLI   ADVDTE,X'96'                                                     
         BL    TRNADD4                                                          
         ZAP   TRNAMNT,NET         IF >=96 AMOUNT= NET - CASH DISCOUNT          
         SP    TRNAMNT,CSHD                                                     
TRNADD4  CLI   BOTSW,C'A'                                                       
         BE    *+10                                                             
         ZAP   TRNAMNT,=P'0'       BOTTLER OR MCCANN = NO AMOUNT                
         SPACE 1                                                                
TRNADD5  ZIC   R1,COMLEN                                                        
         LTR   R1,R1                                                            
         BZ    TRNADD7                                                          
         ZIC   R0,TRNLN           UPDATE TRANSACTION LENGTH                     
         AR    R0,R1                                                            
         STC   R0,TRNLN                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TRNNARR(0),COMMENT    COMMENT TO ELEMENT                         
TRNADD7  GOTO1 ADDL,DMCB,IO,ELEMENT          ADD TRANSACTION ELEMENT            
         SPACE 1                                                                
         USING SCIELD,R3                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN2Q                                                    
         MVI   SCITYPE,SCITGRNT                                                 
         ZAP   SCIGRS,GROSS                                                     
         ZAP   SCINET,NET                                                       
         GOTO1 ADDL,DMCB,IO,ELEMENT          SUBSIDIARY CASH ELEMENT            
         SPACE 1                                                                
         USING XPYELD,R3                                                        
         LA    R3,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   XPYEL,XPYELQ        X'46' EXTRA PAYMENT ELEM                     
         MVI   XPYLN,XPYLN2Q                                                    
         ZAP   XPYCD,CSHD                                                       
         MVC   XPYEST,ESTN                                                      
         GOTO1 CDATCON,DMCB,(1,XFRSTART),(X'20',XPYPER)                         
         GOTO1 CDATCON,DMCB,(1,XFREND),(X'20',XPYPER+6)                         
*                                                                               
         MVC   XPYCLI,SPACES       KEEP VEHICLE NAME IN 46 ELEMENT              
         MVC   XPYPRO,SPACES                                                    
         MVC   XPYCLI(36),VEHNAM                                                
         OC    XPYCLI(36),SPACES                                                
         MVC   XPYINV,SPACES                                                    
         MVC   XPYINV(L'INVOICE),INVOICE                                        
         MVC   XPYMKT,XFRMKT                 SPOT MARKET                        
         MVC   XPYCAT,XFRCAT                 SPOT ESTIMATE CATEGORY             
         GOTO1 ADDL,DMCB,IO,ELEMENT          ADD EXTRA PAY ELEMENT              
         SPACE 1                                                                
*              ADD A 60 ELEMENT                                                 
         USING TRSELD,R3                                                        
         LA    R3,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TRSEL,TRSELQ                  X'60' ELEMENT                      
         MVI   TRSLN,TRSLNQ                                                     
         MVC   TRSDATE,TODAY2                                                   
         CLI   ADVDTE,X'96'        IF >=96 MARK APPROVED                        
         BL    *+10                                                             
         MVC   TRSUPDT,TODAY2                MARK APPROVED                      
         GOTO1 ADDL,DMCB,IO,ELEMENT          ADD STATUS ELEMENT                 
         EJECT                                                                  
*ADDTRN WILL ADD TRANSACTION/UPDATE BALANCE AND CONTRA ACCOUNT                  
*                                                                               
         MVC   TRNBMOS,ADVDTE      POSTING YYMM PWO                             
         SR    RE,RE                                                            
         ICM   RE,3,TRNBSEQN                                                    
         LA    RE,1(RE)                                                         
         STCM  RE,3,TRNBSEQN                                                    
         MVC   TRNCACNM,PRDNAM    CONTRA/ACC NAME IS PRODUCT                    
         MVI   TRNINDS1,TRNIDNMR   DO NOT MARK REVERSALS                        
*                                                                               
ADDTRN9  LA    RE,SELDGTAB         SET FOR SE LEDGER                            
         ST    RE,TRNLDG                                                        
         MVI   TRN#LDGS,1          SET NUMBER OF LEDGER TABLE ENTRIES           
         MVC   KEY2,IO             SAVE KEY                                     
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   POSTSW,C'Y'         NOTE ONE ITEM POSTED                         
         SPACE 1                                                                
*NOW UPDATE TYPE 'E' BUCKET RECORD                                              
         SPACE 1                                                                
         OI    TRNINDS1,TRNIDNAT+TRNIDNUB                                       
         MVC   TRNKCULC+7(L'TRNKCULC-7),SPACES                                  
         MVI   BYTE,C' '          GET 'E' BUCKET/ADDTRN CHANGED TO C' '         
         L     RF,TRNREC                                                        
         GOTO1 GETL,DMCB,('SCIELQ',(RF)),(1,BYTE)                               
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         USING SCIELD,R3                                                        
         L     R3,ELADDR           CHANGE BUCKET TYPE TO GET                    
         MVI   SCITYPE,SCITCOKE    CORRECT BUCKET ADDED                         
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    TRNADD10                                                         
         DC    H'0'                                                             
         EJECT                                                                  
*ADD SX TRANSACTION FROM SE RECORD IN IO                                        
         SPACE 1                                                                
         USING ACTRECD,R4                                                       
TRNADD10 DS    0H                                                               
         CLI   ADVDTE,X'96'        IF >=96 DO NOT ADD SX                        
         BL    XIT                                                              
         LA    R4,IO                                                            
         MVC   IO(L'KEY2),KEY2         RESTORE ORIGINAL SE KEY                  
         MVC   ACTKCULA+1(2),=C'SX'    AND CHANGE TO SX                         
*                                                                               
         USING TRNELD,R3           UPDATE X'44' TRNEL                           
         LA    RF,IO                                                            
         GOTO1 GETL,DMCB,('TRNELQ',(RF)),0                                      
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R3,ELADDR                                                        
         NI    TRNSTAT,X'FF'-X'80' CREDIT SX                                    
*                                                                               
         USING SCIELD,R3           FIX X'50' SCIEL- ADDTRN CHANGED TYPE         
         LA    RF,IO                                                            
         GOTO1 GETL,DMCB,('SCIELQ',(RF)),0                                      
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R3,ELADDR                                                        
         MVI   SCITYPE,SCITGRNT                                                 
*                                                                               
         USING MPYELD,R3           BUILD & ADD MANUAL PAYMENT ELEMENT           
         LA    R3,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   MPYEL,MPYELQ        X'64'                                        
         MVI   MPYLN,MPYLNQ                                                     
         MVC   MPYNO,SPACES                                                     
         ZAP   MPYAMNT,=P'0'                                                    
         MVC   MPYBNK,SPACES                                                    
         GOTO1 ADDL,DMCB,IO,ELEMENT                                             
*                                                                               
         LA    RE,SXLDGTAB         SET FOR SX LEDGER                            
         ST    RE,TRNLDG                                                        
         MVI   TRN#LDGS,1          SET NUMBER OF LEDGER TABLE ENTRIES           
         MVI   TRNINDS1,TRNIDNMR   DO NOT MARK REVERSALS                        
         OI    TRNINDS1,TRNICOKE   IGNORE COKE EXPENDITURE ELEM                 
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    XIT                                                              
         DC    H'0'                                                             
         EJECT                                                                  
*              BUILD COMMENTS FIELD                                             
BLDCOM   NTR1                                                                   
         MVI   COMLEN,0                                                         
         MVI   COMMENT,C' '                                                     
         MVC   COMMENT+1(L'COMMENT-1),COMMENT                                   
         SR    R2,R2                                                            
         LA    R5,COMMENT                                                       
         USING XFRINVD,R3                                                       
BLDCOM3  OC    XFRNARR,SPACES                                                   
         CLC   XFRNARR,SPACES      FIRST TIME ALL BLANK GET OUT                 
         BE    BLDCOM5                                                          
         MVC   0(50,R5),XFRNARR                                                 
         AH    R2,=H'51'                                                        
         STC   R2,COMLEN                                                        
         AR    R5,R2                                                            
         LA    R3,XFRINLEN(R3)                                                  
         BCT   R0,*+8                                                           
         B     BLDCOM5             END OF ITEMS                                 
         CLC   XFRINV,SPACES       IF INVOICE NUMBER CAN'T BE                   
         BNE   BLDCOM5             A CONTINUED COMMENT                          
         B     BLDCOM3                                                          
BLDCOM5  CLC   COMMENT,SPACES                                                   
         BE    XIT                 NO COMMENTS                                  
         ZIC   R0,COMLEN                                                        
         GOTO1 SQUASHER,DMCB,COMMENT,(R0)                                       
         MVC   COMLEN,DMCB+7                                                    
         B     XIT                                                              
         EJECT                                                                  
*              SUB-ROUTINE TO GET A LEDGER RECORD AND BUILD A LEDGER            
*              TABLE ENTRY                                                      
*                                                                               
*              AT ENTRY, P1=A(KEY) AND P2=A(LEDGER TABLE ENTRY)                 
*                                                                               
GETLED   NTR1  ,                                                                
         LM    R5,R6,0(R1)                                                      
         MVC   KEY(42),SPACES                                                   
         LA    R4,KEY                                                           
         MVC   ACTKCULA(3),0(R5)  GET COMPANY/UNIT/LEDGER                       
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKCULA,IO                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING LDGTABD,R6                                                       
GETLED2  XC    LDGTABD(LDGTABL),LDGTABD                                         
         MVC   LDGTUL,ACTKCULA+1                                                
         LA    R3,ACCORFST(R4)                                                  
         SR    R0,R0                                                            
*                                                                               
GETLED4  CLI   0(R3),0             TEST FOR EOR                                 
         BE    GETLEDX             YES                                          
         CLI   0(R3),LDGELQ       TEST FOR LEDGER ELEMENT                       
         BNE   GETLED5             NO                                           
         USING LDGELD,R3                                                        
         MVC   LDGTTYPE,LDGTYPE                                                 
         MVC   LDGTLIKE,LDGLIKE                                                 
         MVC   LDGTOFFP,LDGOPOS                                                 
         B     GETLED8                                                          
*                                                                               
GETLED5  CLI   0(R3),ACLELQ                                                     
         BNE   GETLED6                                                          
         USING ACLELD,R3                                                        
         MVC   LDGTLVA,ACLVALS                                                  
         MVC   LDGTLVB,ACLVALS+(L'ACLVALS)                                      
         MVC   LDGTLVC,ACLVALS+(L'ACLVALS*2)                                    
         MVC   LDGTLVD,ACLVALS+(L'ACLVALS*3)                                    
         B     GETLED8                                                          
*                                                                               
GETLED6  CLI   0(R3),RSTELQ                                                     
         BNE   GETLED8                                                          
*                                                                               
         USING RSTELD,R3                                                        
         MVC   LDGTSEC,RSTSECY+1                                                
*                                                                               
GETLED8  IC    R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GETLED4                                                          
*                                                                               
GETLEDX  B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO ADD AN ELEMENT                                        
         SPACE 1                                                                
*              P1   A(RECORD)                                                   
*              P2   A(ELEMENT)                                                  
         SPACE 1                                                                
ADDL     NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         SPACE 1                                                                
         MVC   HALF,ACCORLEN(R2)                                                
         ZIC   R4,1(R3)                                                         
         AH    R4,HALF                                                          
         CH    R4,=H'1000'                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         GOTO1 CHELLO,ELIST,(C'P',=C'ACCOUNT '),(R2),(R3)                       
         CLI   ELERR,0                                                          
         BE    XIT                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
         EJECT                                                                  
*              ROUTINE TO DELETE AN ELEMENT                                     
         SPACE 1                                                                
*              P1   BYTE 0    ELEMENT CODE                                      
*                   BYTE 1-3  A(RECORD)                                         
*              P2   BYTE 0    LENGTH OF SEARCH ARGUMENT                         
*                   BYTE 1-3  A(SEARCH ARGUMENT)                                
         SPACE 1                                                                
DELL     NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         ZIC   R4,0(R1)                                                         
         ZIC   R5,4(R1)                                                         
         GOTO1 CHELLO,ELIST,(C'G',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         SPACE 1                                                                
         CLI   ELERR,0                                                          
         BNE   XIT                                                              
         GOTO1 CHELLO,ELIST,(C'D',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO GET AN ELEMENT                                        
         SPACE 1                                                                
*              P1   BYTE 0    ELEMENT CODE                                      
*                   BYTE 1-3  A(RECORD)                                         
*              P2   BYTE 0    LENGTH OF SEARCH ARGUMENT                         
*                   BYTE 1-3  A(SEARCH ARGUMENT)                                
         SPACE 1                                                                
GETL     NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         ZIC   R4,0(R1)                                                         
         ZIC   R5,4(R1)                                                         
         GOTO1 CHELLO,ELIST,(C'G',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         B     XIT                                                              
         EJECT                                                                  
*              INTERFACE TO DATAMGR                                             
         SPACE 1                                                                
*              PARAM 1        A(KEY)                                            
*              PARAM 2        A(IOAREA)                                         
         SPACE 1                                                                
HIGH     MVC   COMMAND,=C'DMRDHI'                                               
         B     DMRALL                                                           
         SPACE 1                                                                
SEQ      MVC   COMMAND,=C'DMRSEQ'                                               
         B     DMRALL                                                           
         SPACE 1                                                                
READ     MVC   COMMAND,=C'DMREAD'                                               
         B     DMRALL                                                           
         SPACE 1                                                                
WRITE    MVC   COMMAND,=C'DMWRT '                                               
         B     DMRALL                                                           
         SPACE 1                                                                
ADD      MVC   COMMAND,=C'DMADD '                                               
         B     DMRALL                                                           
         SPACE 1                                                                
DMRALL   NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         GOTO1 CDATAMGR,DMWK,(DMINBTS,COMMAND),=C'ACCOUNT',(R2),(R3)            
         MVC   DMBYTE,DMCB+8                                                    
         NC    DMBYTE,DMOUTBTS                                                  
         BZ    XIT                                                              
         DC    H'0'                                                             
XIT      XIT1                                                                   
         EJECT                                                                  
*              ERROR ROUTINES                                                   
NTVCOM   L     R2,ADMSGH                                                        
         MVC   8(L'COMMSG,R2),COMMSG                                            
         B     ERROR                                                            
NTVCOMR  L     R2,ADMSGH                                                        
         MVC   8(L'COMRMSG,R2),COMRMSG                                          
         B     ERROR                                                            
NTVMED   L     R2,ADMSGH                                                        
         MVC   8(L'MEDMSG,R2),MEDMSG                                            
         B     ERROR                                                            
NTVVEH   L     R2,ADMSGH                                                        
         MVC   8(L'VEHMSG,R2),VEHMSG                                            
         B     ERROR                                                            
NTVPRD   L     R2,ADMSGH                                                        
         MVC   8(L'PRDMSG,R2),PRDMSG                                            
         B     ERROR                                                            
NTVACN   L     R2,ADMSGH                                                        
         MVC   8(L'ACNMSG,R2),ACNMSG                                            
         B     ERROR                                                            
NTVAGY   L     R2,ADMSGH                                                        
         MVC   8(L'AGYMSG,R2),AGYMSG                                            
         B     ERROR                                                            
NTVBUDC  L     R2,ADMSGH                                                        
         MVC   8(L'BUDCMSG,R2),BUDCMSG                                          
         B     ERROR                                                            
NTVINV   L     R2,ADMSGH                                                        
         MVC   8(L'INVMSG,R2),INVMSG                                            
         B     ERROR                                                            
MISINV   L     R2,ADMSGH                                                        
         MVC   8(L'MSINVMSG,R2),MSINVMSG                                        
         B     ERROR                                                            
MISCAT   L     R2,ADMSGH                                                        
         MVC   8(L'MSCATMSG,R2),MSCATMSG                                        
         B     ERROR                                                            
ACCLCK   L     R2,ADMSGH                                                        
         MVC   8(L'ACLCKMSG,R2),ACLCKMSG                                        
         B     ERROR                                                            
ERROR    L     R1,ADPARAMS                                                      
         MVI   8(R1),X'FF'                                                      
         B     XIT                                                              
NOERRS   L     R1,ADPARAMS                                                      
         MVI   8(R1),0                                                          
         B     XIT                                                              
         EJECT                                                                  
*              CONSTANTS                                                        
COMMSG   DC    C'** ERROR ** INVALID COMPANY CODE'                              
COMRMSG  DC    C'** ERROR ** INVALID COMPANY RECORD'                            
MEDMSG   DC    C'** ERROR ** INVALID MEDIA CODE'                                
VEHMSG   DC    C'** ERROR ** INVALID VEHICLE CODE'                              
PRDMSG   DC    C'** ERROR ** INVALID PRODUCT CODE'                              
ACNMSG   DC    C'** ERROR ** INVALID ACN NUMBER'                                
AGYMSG   DC    C'** ERROR ** INVALID AGENCY NUMBER'                             
BUDCMSG  DC    C'** ERROR ** INVALID BUDGET CODE'                               
INVMSG   DC    C'** ERROR ** INVALID INVOICE NUMBER'                            
MSINVMSG DC    C'** ERROR ** MISSING INVOICE NUMBER'                            
MSCATMSG DC    C'** ERROR ** MISSING CATEGORY NUMBER'                           
ACLCKMSG DC    C'** ERROR ** SE ACCOUNT IS LOCKED'                              
         SPACE 1                                                                
*              SPOT MONTH                                                       
*              BUDGET START MONTH                                               
*              BUDGET END MONTH                                                 
*              QUARTER NUMBER                                                   
BUDPER   DC    X'01',X'0103',X'01'                                              
         DC    X'02',X'0103',X'01'                                              
         DC    X'03',X'0103',X'01'                                              
         DC    X'04',X'0406',X'02'                                              
         DC    X'05',X'0406',X'02'                                              
         DC    X'06',X'0406',X'02'                                              
         DC    X'07',X'0709',X'03'                                              
         DC    X'08',X'0709',X'03'                                              
         DC    X'09',X'0709',X'03'                                              
         DC    X'10',X'1012',X'04'                                              
         DC    X'11',X'1012',X'04'                                              
         DC    X'12',X'1012',X'04'                                              
         DC    X'FF'                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR STORAGE                                                
LWSD     DSECT                                                                  
RELO     DS    F                                                                
ADPARAMS DS    F                   A(INPUT PARAMETERS)                          
PARAMS   DS    0F                  INPUT PARAMETERS                             
ADACT    DS    A                   A(ACTION)                                    
ADPAYD   DS    A                   A(ACPAYD AREA)                               
ADMSGH   DS    A                   A(ERROR MESSAGE FLDHDR)                      
ADCOMFAC DS    A                   A(COMFACS)                                   
PARAMLEN EQU   *-PARAMS                                                         
         SPACE 1                                                                
ADDTRN   DS    V                                                                
SQUASHER DS    A                                                                
ACTION   DS    CL4                                                              
         SPACE 1                                                                
ATRANS   DS    A                                                                
AACCOUNT DS    A                                                                
ACONTRA  DS    A                                                                
ABUCKET  DS    A                                                                
         SPACE 1                                                                
TODAY2   DS    CL2                                                              
COMPANY  DS    CL1                                                              
ACN      DS    CL5                                                              
AGYCDE   DS    CL3                                                              
PRD      DS    CL2                                                              
MEDIA    DS    CL2                                                              
VEHICLE  DS    CL8                                                              
ADVDTE   DS    CL3                                                              
INVOICE  DS    CL11                                                             
GROSS    DS    PL6                                                              
NET      DS    PL6                                                              
NETPCT   DS    PL3                                                              
CDPCT    DS    PL2                                                              
CSHD     DS    PL6                                                              
PWRK     DS    CL13                                                             
YEAR     DS    CL1                                                              
YTD      DS    PL6                                                              
BUD      DS    PL6                                                              
DEACN    DS    CL5                 DEFAULT ACN                                  
ESTN     DS    XL2                                                              
BSTART   DS    XL3                 BINARY START DATE                            
         SPACE 1                                                                
POSTSW   DS    CL1                                                              
BOTSW    DS    CL1                                                              
PRDBUDC  DS    CL2                                                              
PRDNAM   DS    CL36                                                             
VEHNAM   DS    CL36                                                             
BUDST    DS    CL2                                                              
BUDEN    DS    CL2                                                              
QTRNUM   DS    CL1                                                              
COMLEN   DS    CL1                                                              
COMMENT  DS    CL250                                                            
         SPACE 1                                                                
DMCB     DS    6F                                                               
DMWK     DS    6F                                                               
ADTRANS  DS    A                                                                
DRCR     DS    CL1                 DEBIT(D) OR CREDIT (C)                       
COMMAND  DS    CL8                                                              
DMINBTS  DS    CL1                                                              
DMOUTBTS DS    CL1                                                              
DMBYTE   DS    CL1                                                              
WORK     DS    CL64                                                             
SPACES   DS    CL132                                                            
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    CL1                                                              
         SPACE 1                                                                
ELIST    DS    3F                  FOR GETL, ADDEL, DELEL                       
ELERR    DS    CL1                                                              
         ORG   ELERR               ERROR CODE FROM HELLO                        
ELADDR   DS    F                   ADDRESS OF ELEMENT FROM HELLO                
         DS    2F                                                               
ELEMENT  DS    CL255                                                            
         SPACE 1                                                                
SELDGTAB DS    XL(LDGTABL+1)       SE LEDGER INFO                               
SXLDGTAB DS    XL(LDGTABL+1)       SX LEDGER INFO                               
                                                                                
PALAREA  DS    XL20                P&L BUCKET AREA                              
         SPACE 2                                                                
       ++INCLUDE ACADDTRND                                                      
         SPACE 1                                                                
KEY      DS    CL42                                                             
KEY2     DS    CL42                                                             
IO       DS    CL2048                                                           
IO2      DS    CL2048                                                           
IO3      DS    CL2048                                                           
IO4      DS    CL2048                                                           
         SPACE 1                                                                
LWSEND   EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE ACPAYXFRD                                                      
         EJECT                                                                  
       ++INCLUDE ACLDGTABD                                                      
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016ACPAYXFR  02/26/04'                                      
         END                                                                    
