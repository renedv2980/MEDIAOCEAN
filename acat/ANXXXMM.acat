*          DATA SET ANXXXMM    AT LEVEL 057 AS OF 05/01/02                      
*CATALP SPI2MM                                                                  
         TITLE 'SPI2MM - SI2 MATCHMAKER MODULE'                                 
         PRINT NOGEN                                                            
SPI2MM   CSECT                                                                  
         NMOD1 WORKDL,**I2MM,CLEAR=YES                                          
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         USING IELEMD,R8                                                        
         LA    R5,MINBLOCK                                                      
         USING MINBLKD,R5                                                       
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VMINIO,CMINIO-COMFACSD(RF)                                       
         MVC   VPERVERT,CPERVERT-COMFACSD(RF)                                   
*        MVI   TRACEOPT,0          TRACE BITS                                   
         MVI   TRACEOPT,X'FF'      TRACE BITS  *****  TRACE VERSION ***         
*                                  X'80' = MIMOUT                               
*                                  X'40' = NEWPSV                               
*                                  X'20' = BLDS                                 
*                                  X'10' = FIXS                                 
*                                  X'08' = BLDPKT                               
*                                                                               
         CLI   MULTIMON,C'Y'       IF MULTIPLE MONTHS                           
         BE    EXIT                NO MM STUFF                                  
*                                                                               
         CLC   QENDAUTO,=C'ALL'    IF PIGGY IS ALL                              
         BE    EXIT                NO MM STUFF                                  
         CLC   QENDAUTO,=C'YES'    IF PIGGY IS YES                              
         BE    EXIT                NO MM STUFF                                  
*                                                                               
         CLI   0(R1),C'C'          COMMENTS MODE                                
         BNE   SM02                NO - INVOICE MODE                            
         B     MMCOM                                                            
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
SM02     L     R1,ALINV                                                         
         S     R1,AFINV                                                         
         BNP   SMX                 IF NO INVOICE ITEMS, FORGET IT               
         LA    R1,1(R1)                                                         
*                                                                               
         BAS   RE,MNINIT           INITIALIZE MINIO                             
         BAS   RE,BLDSTAT          BUILD STATUS ELEM                            
         CLC   SVINVCT,=H'1'       IF MORE THAN ONE INVOICE                     
         BL    SMX                 IF NO INVS, SKIP THE WHOLE THING             
         BNH   SM04                IF ONLY ONE INVOICE, SKIP SORT               
*                                  ELSE SORT ON INVOICE NUMBER                  
         SR    R0,R0                                                            
         LA    R4,IELI2LEN         I2(OFF-LINE) LENGTH                          
         DR    R0,R4               R1= NO OF ITEMS                              
         ST    R1,DMCB+4                                                        
         LA    R3,IINVID-IELEM                                                  
         GOTO1 XSORT,DMCB,AFINV,,(R4),2,(R3)                                    
*                                                                               
SM04     DS    0H                                                               
         L     R8,AFINV                                                         
         XC    LASTINV,LASTINV                                                  
*                                                                               
SM06     DS    0H                                                               
         C     R8,ALINV                                                         
         BNL   SM40                                                             
*                                                                               
         CLC   LASTINV,IINVID      SAME INVOICE?                                
         BE    SM30                                                             
         OC    LASTINV,LASTINV     NO, IS IT THE FIRST                          
         BNE   SM40                NO, CLOSE LAST INVOICE                       
*                                                                               
SM20     DS    0H                  NEW INVOICE                                  
         MVC   LASTINV,IINVID                                                   
         TM    IINVID,X'80'        FROM OLD STYLE INVOICE- SKIP                 
         BNZ   SM38                                                             
         SR    RF,RF               SET MINIO RECORD KEY                         
         ICM   RF,3,IINVID                                                      
         BCTR  RF,0                                                             
         MH    RF,=Y(32)                                                        
         A     RF,AIKLIST                                                       
         MVC   MINMKEY(32),0(RF)                                                
*                                                                               
         GOTO1 VMINIO,DMCB,('MINOPN',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    MINEKEY,MINEKEY     GET HEADER ELEM                              
         MVI   MINEKEY,SNVHDELQ                                                 
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                MISSING HEADER ELEM                          
*                                                                               
         MVC   SVHDELM,MELEM       SAVE HEADER ELEM                             
*                                                                               
         BAS   RE,FIXSTAT          DEAL WITH EXISTING STATUS ELEMS              
*                                                                               
****     MVC   HALF,MINMKEY+SNVKMOS-SNVKEY   GET START OF PERIOD                
****     XC    HALF,=X'FFFF'       COMPLEMENT                                   
*                                                                               
         MVC   HALF,SVHDELM+SNVHDSDT-SNVHDEL  GET START FROM INVOICE!!          
         GOTO1 DATCON,DMCB,(2,HALF),SAVSTRT                                     
*                                                                               
****     GOTO1 GETBROAD,(R1),WORK,WORK+6                                        
****     MVC   SAVSTRT,WORK+6                                                   
*                                                                               
SM30     DS    0H                  SAME INVOICE                                 
         XC    MINEKEY,MINEKEY                                                  
         LA    R3,MINEKEY                                                       
         USING SNVIDELD,R3                                                      
         MVI   SNVIDEL,SNVIDELQ                                                 
         MVC   SNVIDSEQ-1(1),IMNSEQ     SEQ #                                   
*                                                                               
         GOTO1 DATCON,(R1),(2,IDAT),WORK                                        
         GOTO1 VPERVERT,(R1),SAVSTRT,WORK                                       
         ZIC   RF,DMCB+9           NUMBER OF DAYS (1 BYTE ONLY)                 
         BCTR  RF,0                PERVERT RETURNS INCLUSIVE COUNT              
         STC   RF,SNVIDDAY-1                                                    
*                                                                               
         TM    ISTAT2,ISTTCNV      IF TIME IS MILITARY                          
         BNZ   SM32D               MUST CONVERT TO MINUTES                      
*                                                                               
         SR    RF,RF               ELSE ONLY ADJUST TO 6AM                      
         ICM   RF,3,ITIM                                                        
         SH    RF,=H'360'          SUBTRACT 6 HOURS                             
         STCM  RF,3,SNVIDTIM-1     SO 6AM IS 0 MINUTES                          
         B     SM33                                                             
*                                                                               
SM32D    DS    0H                                                               
         MVC   HALF,ITIM                                                        
         BAS   RE,CVMIN                                                         
         MVC   SNVIDTIM-1(2),HALF                                               
*                                                                               
SM33     DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINRD',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R3,MELEM                                                         
*                                                                               
         XC    SNVIDBES(10),SNVIDBES    <--- NOTE LEN = ALL BUY DATA            
*                                                                               
         SR    R6,R6               FIND BUY LINE FOR THIS ITEM                  
         ICM   R6,7,IBY                                                         
         BZ    SM36                                                             
         BCTR  R6,0                                                             
         A     R6,AFBY                                                          
         USING BYELEMD,R6                                                       
*                                  SET BUY DATA FOR MATCHMAKER                  
         MVC   SNVIDBES,BYEST          ESTIMATE                                 
         MVC   SNVIDBLN,BYLIN          LINE                                     
         MVC   SNVIDBDT,BYSDT          DATE                                     
         MVC   SNVIDBNO,BYSPT          SPOT NUMBER WITHIN DATE                  
         MVC   SNVIDBOR,BYORBNO        ORBIT LINE                               
*                                                                               
         DROP  R6                                                               
*                                                                               
SM36     DS    0H                                                               
         TM    TRACEOPT,X'80'                                                   
         BZ    SM36A                                                            
         MVC   P(6),=C'IELEM '                                                  
         LA    R2,IELI2LEN                                                      
         GOTO1 HEXOUT,DMCB,IELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
SM36A    CLI   QBOOK1,C' '         TEST DEMO RUN                                
         BE    SM36X                NO                                          
         L     R6,DEMPARS+4                                                     
         LA    R4,MMW                                                           
         L     R0,DEMPARS+8                                                     
         LTR   R0,R0                                                            
         BNP   SM36X                                                            
         XC    MMW,MMW                                                          
         XC    MMFULL,MMFULL                                                    
*                                                                               
SM36B    MVC   0(3,R4),0(R6)                                                    
*                                                                               
         CLC   0(3,R6),DCLIST      IF FIRST DEMO FOR REQ                        
         BNE   *+8                                                              
         ST    R4,MMFULL           SAVE POSITION IN MMW                         
*                                                                               
         LA    R4,8(R4)                                                         
         A     R6,DEMPARS+12                                                    
         BCT   R0,SM36B                                                         
*                                                                               
         OC    MMW,MMW             TEST ANY DEMOS                               
         BZ    SM36X                                                            
*                                                                               
         MVC   SVITIM,ITIM         SAVE ITIM                                    
         LA    R2,ITIM                                                          
         LA    R4,WORK                                                          
         BAS   RE,FTIM             FORMAT TIME FOR DEMO LOOKUP                  
         MVC   ITIM,DUB                                                         
*                                                                               
         GOTO1 AACHDEM,DMCB,IELEM,MMW,0 LOOK UP DEMO                            
         L     R4,MMFULL                                                        
         MVC   SNVIDDEM,6(R4)                                                   
         MVC   ITIM,SVITIM         RESET                                        
*                                                                               
SM36X    TM    TRACEOPT,X'80'                                                   
         BZ    SM37                                                             
         MVC   P(6),=C'MMOUT '                                                  
         ZIC   R2,MELEM+1                                                       
         GOTO1 HEXOUT,DMCB,MELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
SM37     DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SM38     DS    0H                                                               
         LA    R8,IELI2LEN(R8)                                                  
         B     SM06                                                             
*                                                                               
SM40     DS    0H                                                               
         CLI   MINOPEN,C'Y'                                                     
         BE    *+6                                                              
         DC    H'0'                MINIO SET SHOULD BE OPEN                     
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,SVSTSEQ                                                     
         LA    R1,1(R1)                                                         
         STCM  R1,3,SVMMELM+SNVMMSEQ-SNVMMELD                                   
*                                                                               
         MVC   MELEM,SVMMELM                                                    
         GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,MINEDUP      DUPE ON ADD OK?                              
         BE    SM41                                                             
         CLI   MINERR,0                                                         
         BE    SM41                                                             
         DC    H'0'                                                             
*                                                                               
SM41     DS    0H                                                               
*                                                                               
         LA    R3,SVMMELM          'NEW' STATUS ELEM                            
         BAS   RE,BLDPSSV          BUILD PASSIVE FOR IT                         
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'XSPDIR',XKEY2,XKEY3,0                 
         CLC   XKEY2(L'SNVKEY),XKEY3                                            
         BE    SM41D               ALREADY ON FILE - CHECK STATUS               
*                                                                               
         TM    TRACEOPT,X'40'                                                   
         BZ    SM41C                                                            
         MVC   P(6),=C'ADDPSV'                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
*                                                                               
SM41C    CLI   RCWRITE,C'Y'        ADD TO FILE                                  
         BNE   SM41F                                                            
         GOTO1 DATAMGR,DMCB,=C'DMADD',=C'XSPDIR',XKEY2,XKEY2,0                  
         TM    DMCB+8,X'20'        DUPE ON ADD OK?                              
         BNZ   SM41F                                                            
         CLI   DMCB+8,0                                                         
         BE    SM41F                                                            
         DC    H'0'                                                             
*                                                                               
SM41D    DS    0H                      KEY ALREADY ON FILE -CHK STATUS          
         CLC   XKEY2+33(1),XKEY3+33    SAME STATUS                              
         BE    SM41F                   THEN NOTHING TO DO                       
*                                                                               
         TM    TRACEOPT,X'40'                                                   
         BZ    SM41E                                                            
         MVC   P(6),=C'WRTPSV'                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
*                                                                               
SM41E    CLI   RCWRITE,C'Y'        WRITE OUT NEW STATUS                         
         BNE   SM41F                                                            
         GOTO1 DATAMGR,DMCB,=C'DMWRT',=C'XSPDIR',XKEY2,XKEY2,0                  
         CLI   DMCB+8,0                                                         
         BE    SM41F                                                            
         DC    H'0'                                                             
*                                                                               
SM41F    DS    0H                                                               
         NI    MINMKEY+SNVDSTAT+1-SNVKEY,X'FF'-X'80'                            
         CLI   MATSTAT,0                                                        
         BNE   *+8                                                              
         OI    MINMKEY+SNVDSTAT+1-SNVKEY,X'80'    SET SUCCESSFUL MATCH          
*                                                                               
         NI    MINMKEY+SNVDSTAT+1-SNVKEY,X'FF'-X'20'                            
         CLI   MKUNWIRE,C'N'       DO ALL BUYERS DO UNWIRED?                    
         BNE   SM41X               IF YES NOTHING TO SET                        
         L     RF,ADEST                                                         
         CLI   EPROF-ESTHDR(RF),C'U'    UNWIRED ESTIMATE                        
         BNE   SM41X                                                            
         OI    MINMKEY+SNVDSTAT+1-SNVKEY,X'20'    SET UNWIRED INVOICE           
*                                                                               
SM41X    GOTO1 VMINIO,DMCB,('MINCLS',MINBLKD)                                   
*                                                                               
SM42     DS    0H                                                               
         C     R8,ALINV                                                         
         BNH   SM20                                                             
*                                                                               
SMX      DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        BLDSTAT- BUILD STATUS ELEMENT                                          
*                                                                               
*          NOTE- STATUS DATA IS FOR WHOLE MATCH, NOT JUST ONE INVOICE           
*                                                                               
***********************************************************************         
*                                                                               
BLDSTAT  NTR1                                                                   
         XC    SVMMELM,SVMMELM                                                  
         LA    R3,SVMMELM                                                       
         USING SNVMMELD,R3                                                      
         MVI   SNVMMEL,SNVMMELQ                                                 
         MVI   SNVMMLEN,SNVMMLNQ                                                
*                                                                               
         L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
BLDS02   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPRD,3(RF)          MATCH ON HEX PRODUCT                         
         BE    BLDS04                                                           
         LA    RF,4(RF)                                                         
         B     BLDS02                                                           
BLDS04   MVC   SNVMMRP1,0(RF)                   PRODUCT                         
*                                                                               
         MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BPIGGY,0            PIGGY                                        
         BE    BLDS10                                                           
         L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
BLDS06   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    BLDS08                                                           
         LA    RF,4(RF)                                                         
         B     BLDS06                                                           
BLDS08   MVC   SNVMMRP2,0(RF)                   PRODUCT                         
*                                                                               
BLDS10   MVC   SNVMMRE1,BEST                                                    
         MVC   SNVMMRE2,BESTEND                                                 
         MVC   SNVMMMKT,BMKT                                                    
         MVC   SNVMMDAT,TODAYP                                                  
         MVC   SNVMMBK,QBOOK1                                                   
         CLI   MATSTAT,0           MATCH OK?                                    
         BNE   *+8                                                              
         OI    SNVMMMST,SNVMMSCQ   YES                                          
         MVI   SNVMMGN,C'G'        GROSS                                        
         MVC   SNVMMTOT,IGRS                                                    
         MVC   SNVMMSPS,ISPTS                                                   
         CLI   NETINV,C'Y'                                                      
         BNE   *+14                                                             
         MVI   SNVMMGN,C'N'        OR NET                                       
         MVC   SNVMMTOT,INET                                                    
         DROP  R3                                                               
*                                                                               
         TM    TRACEOPT,X'20'                                                   
         BZ    BLDS20                                                           
         MVC   P(6),=C'BLDS  '                                                  
         ZIC   R2,SVMMELM+1                                                     
         GOTO1 HEXOUT,DMCB,SVMMELM,P+6,(R2),=C'N'                               
         GOTO1 REPORT                                                           
*                                                                               
BLDS20   DS    0H                                                               
*                                                                               
BLDSX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        FIXSTAT- DEAL WITH EXISTING STATUS ELEMS                               
***********************************************************************         
*                                                                               
FIXSTAT  NTR1                                                                   
         XC    SVSTSEQ,SVSTSEQ     CLEAR STATUS ELEM SEQUENCE                   
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMMELD,R3                                                      
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVMMELQ                                                 
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         B     FXS02D                                                           
FXS02    DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINSEQ',MINBLKD)                                   
FXS02D   DS    0H                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   FXS30                                                            
*                                                                               
*       IF NOT SAME REQUESTED PRD/EST THEN DELETE AND ADD NEW LATER             
*                                                                               
         CLC   SNVMMRP1(8),SVMMELM+SNVMMRP1-SNVMMELD                            
         BNE   FXS03                                                            
*                                  ALSO DEAL WITH MKT CHANGE                    
         CLC   SNVMMMKT,SVMMELM+SNVMMMKT-SNVMMELD                               
         BNE   FXS03                                                            
*                                                                               
FXS02H   DS    0H                  IF THE SAME -                                
         CLC   SNVMMSEQ,SVSTSEQ    KEEP HIGHEST SEQ NUMBER - WHY?               
         BNH   *+10                                                             
         MVC   SVSTSEQ,SNVMMSEQ                                                 
         B     FXS06               BUT STILL GO DELETE OLD E8 ELEM              
*                                  WE'LL ADD THE NEW ONE LATER                  
FXS03    DS    0H                                                               
         TM    TRACEOPT,X'10'                                                   
         BZ    FXS04                                                            
         MVC   P(6),=C'FIXS  '                                                  
         ZIC   R2,MELEM+1                                                       
         GOTO1 HEXOUT,DMCB,MELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
FXS04    DS    0H                                                               
         BAS   RE,BLDPSSV          BUILD PASIVE POINTER IN XKEY2                
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'XSPDIR',XKEY2,XKEY2,0                 
         CLI   DMCB+8,0                      UNLESS ALREADY GONE                
         BNE   FXS06                                                            
         OI    XKEY2+SNVDSTAT-SNVKEY,X'80'   DELETE THE PASSIVE                 
         CLI   RCWRITE,C'Y'                                                     
         BNE   FXS06                                                            
         GOTO1 (RF),(R1),=C'DMWRT',=C'XSPDIR',XKEY2,XKEY2,0                     
*                                                                               
FXS06    DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINDEL',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     FXS02               NEXT ELEM                                    
         DROP  R3                                                               
*                                                                               
FXS30    DS    0H                                                               
*                                                                               
FIXSX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        BLDPSSV- BUILD A PASSIVE KEY BASED ON STATUS ELEM                      
*                                                                               
*               - ON INPUT R3 POINTS TO ELEM                                    
*               - KEY IS BUILT IN XKEY2                                         
*                                                                               
***********************************************************************         
*                                                                               
BLDPSSV  NTR1                                                                   
         USING SNVMMELD,R3                                                      
         XC    XKEY2,XKEY2                                                      
         LA    R2,XKEY2                                                         
         USING SNVPKEY,R2                                                       
*                                                                               
         MVI   SNVPTYP,SNVPTYPQ    X'0E'                                        
         MVI   SNVPSUB,SNVPSUBQ    X'83'                                        
         MVC   SNVPAM,BAGYMD                                                    
         MVC   SNVPMKT,SNVMMMKT                 MARKET                          
         MVC   SNVPSTA,BMKTSTA+2                STATION                         
         MVC   SNVPMOS,MINMKEY+SNVKMOS-SNVKEY   MONTH OF SERVICE                
         MVC   SNVPCLT,BCLT                     CLIENT                          
*                                                                               
         MVC   SNVPPRD,SNVMMRP1                 PRODUCTS                        
         MVC   SNVPPRD2,SNVMMRP2                                                
*                                                                               
         MVC   SNVPEST,SNVMMRE1                 ESTIMATE                        
         MVC   SNVPEST2,SNVMMRE2                ESTIMATE                        
         MVC   SNVPINV,MINMKEY+SNVKINV-SNVKEY   INVOICE NUMBER                  
*                                                                               
         NI    SNVPSTAT+1,X'FF'-X'80'    CLEAR                                  
         CLI   MATSTAT,0           SUCCESSFUL MATCH                             
         BNE   *+8                                                              
         OI    SNVPSTAT+1,X'80'    YES SUCCESSFUL                               
*                                                                               
         NI    SNVPSTAT+1,X'FF'-X'20'    CLEAR                                  
         CLI   MKUNWIRE,C'N'       DO ALL BUYERS DO UNWIRED?                    
         BNE   BLDP10              IF YES NOTHING TO SET                        
         L     RF,ADEST                                                         
         CLI   EPROF-ESTHDR(RF),C'U'    UNWIRED ESTIMATE                        
         BNE   BLDP10                                                           
         OI    SNVPSTAT+1,X'20'    YES UNWIRED INVOICE                          
*                                                                               
*                                  GET DA FROM MINIO RECORD TABLE               
BLDP10   ZIC   RF,MINEKLEN         ELEM KEY LENGTH                              
         LA    RF,MNTKEYH-MNTABD(RF) PLUS FIXED TABLE LENGTH                    
         STH   RF,HALF             TABLE ENTRY LENGTH                           
         ICM   RF,3,MINNRECS                                                    
         BCTR  RF,0                                                             
         MH    RF,HALF                                                          
         A     RF,MINRTAB          GET 'FF' REC DISK ADDR                       
         MVC   XKEY2+SNVDDA-SNVKEY(4),0(RF)                                     
*NOP THIS TENDS TO UNDO THE STATUS JUST SET ABOVE                               
*NOP     MVC   XKEY2+SNVDSTAT+1-SNVKEY(1),SNVMMMST                              
*                                                                               
         DROP  R3                                                               
         DROP  R2                                                               
*                                                                               
         TM    TRACEOPT,X'01'                                                   
         BZ    BLDP14                                                           
         MVC   P(6),=C'BLDPK '                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
*                                                                               
BLDP14   DS    0H                                                               
*                                                                               
BLDPX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE INITIALIZES MINIO VARIABLES                                      
***********************************************************************         
*                                                                               
MNINIT   NTR1                                                                   
         LA    R0,MINBLOCK         CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINCOMF,ACOMFACS    A(COMFACS)                                   
         MVC   MINRECUP,RECUP      A(RECUP)                                     
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,=CL8'XSPFIL'     FILE NAME                                
         MVC   MINDIR,=CL8'XSPDIR'     DIR NAME                                 
         MVI   MINFKLEN,L'SNVKEY   KEY LENGTH                                   
         MVI   MINEKLEN,L'SNVKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'SNVKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'SNVDSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(3975) MAXIMUM RECORD LENGTH                        
         LA    RF,MBUFFR           A(MINIO BUFFER)                              
         ST    RF,MINBUFF                                                       
         MVI   MINNBUF,1           USE ONE BUFFER                               
*                                                                               
         LA    R1,MRECTAB          A(AREA FOR RECORD TABLE)                     
         ST    R1,MINRTAB                                                       
         MVC   MINRTABL,=Y(MRECTABL)  LENGTH OF RECORD TABLE                    
*                                                                               
         LA    RE,MELEM            A(AREA FOR ELEM OR CLUSTER)                  
         ST    RE,MINELEM                                                       
         MVC   MINMAXEL,=Y(L'MELEM)   MAX LENGTH OF ELEM OF CLUSTER             
         XC    0(L'MELEM,RE),0(RE)   CLEAR MINELEM AREA                         
         MVC   MINWRITE,RCWRITE                                                 
         MVI   MINDELSW,C'N'       BYPASS DELETES                               
*                                                                               
MINITX   B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*     CVMIN - MILITARY TIMES TO MINUTES (600A = 0 MINS)                         
*             INPUT AND OUTPUT IN HALF                                          
***********************************************************************         
         SPACE 2                                                                
CVMIN    NTR1                                                                   
         LH    R5,HALF                                                          
         CH    R5,=H'600'                                                       
         BNL   *+8                                                              
         AH    R5,=H'2400'                                                      
         SR    R4,R4                                                            
         D     R4,=F'100'                                                       
         MH    R5,=H'60'                                                        
         AR    R4,R5               R4 = MINUTES                                 
         STC   R4,HALF+1                                                        
         SRL   R4,8                                                             
         STC   R4,HALF                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        FORMAT TIME FOR DEMO LOOK UP                                           
***********************************************************************         
FTIM     DS    0H                                                               
         MVC   DUB(2),0(R2)                                                     
         LH    R1,DUB                                                           
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         MH    R1,=H'100'                                                       
         AR    R0,R1                                                            
         CH    R0,=H'2400'                                                      
         BNH   *+8                                                              
         SH    R0,=H'2400'                                                      
         STH   R0,DUB                                                           
         B     FTIM5                                                            
*                                                                               
FTIM4    DS    0H                                                               
         MVC   DUB(2),0(R2)                                                     
*                                                                               
FTIM5    DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 TIMUNPK,DMCB,DUB,(R4)                                            
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*        PRINT I2 MATCHMAKER COMMENTS                                           
***********************************************************************         
*                                                                               
         USING XCOMKEY,R2                                                       
MMCOM    XC    XKEY2,XKEY2                                                      
         LA    R2,XKEY2                                                         
         MVC   COMI2K,=X'0D0C'                                                  
         MVC   COMI2KAM,BAGYMD                                                  
         MVI   COMI2KTY,C'I'                                                    
         MVC   COMI2KCL,BCLT                                                    
*                                                                               
         L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
MMC04    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPRD,3(RF)          MATCH ON HEX PRODUCT                         
         BE    MMC06                                                            
         LA    RF,4(RF)                                                         
         B     MMC04                                                            
MMC06    MVC   COMI2KPR,0(RF)                   PRODUCT                         
*                                                                               
         MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BPIGGY,0            PIGGY                                        
         BE    MMC12                                                            
         L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
MMC08    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    MMC10                                                            
         LA    RF,4(RF)                                                         
         B     MMC08                                                            
MMC10    MVC   COMI2KP2,0(RF)      PIGGY                                        
*                                                                               
MMC12    MVC   COMI2KES,BEST                                                    
         CLC   BEST,BESTEND                                                     
         BE    *+10                                                             
         MVC   COMI2KE2,BESTEND                                                 
         MVC   COMI2KST,BMKTSTA+2                                               
         GOTO1 DATCON,DMCB,(2,BQENDP),(3,WORK)                                  
         MVC   COMI2KYM,WORK                                                    
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'XSPDIR',XKEY2,XKEY3,0                 
         CLC   XKEY2(L'XCOMKEY),XKEY3                                           
         BNE   MMCX                NO COMMENTS                                  
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'XSPFIL',XKEY3+36,            X        
               MRECTAB,DMWORK                                                   
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,MRECTAB          A(COMMENT REC)                               
         LA    R6,XCOMEL           FIRST ELEM                                   
MMC14    CLI   0(R6),0             EOR                                          
         BE    MMCX                                                             
         CLI   0(R6),X'05'                                                      
         BE    MMC30                                                            
MMC16    ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     MMC14                                                            
*                                                                               
MMC30    SR    R2,R2                                                            
         IC    R2,1(R6)                                                         
         AHI   R2,-3               GIVES COMMENT LENGTH -1                      
         BM    MMC16                                                            
         EX    R2,*+4                                                           
         MVC   P(0),2(R6)                                                       
         GOTO1 REPORT                                                           
         B     MMC16                                                            
*                                                                               
MMCX     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
SAVR1    DS    F                   CONTENTS OF R1 ON ENTRY                      
SAVERD   DS    F                   CONTENTS OF RD ON ENTRY                      
SVSNVKEY DS    CL32                SAVED KEY                                    
OLDMGR   DS    XL3                                                              
OLDMKT   DS    XL2                                                              
SVITIM   DS    XL2                                                              
VMINIO   DS    V                                                                
VPERVERT DS    V                                                                
SAVSTRT  DS    CL6                                                              
TRACEOPT DS    CL1                                                              
SVSTSEQ  DS    XL2                                                              
BPIGGY   DS    XL1                                                              
*                                                                               
TMPELEM  DS    XL40                INVOICE TABLE ELEMENT                        
FLMELEM  DS    XL20                FILM TABLE ELEMENT                           
*                                                                               
LASTINV  DS    XL2                                                              
SVMMELM  DS    XL(SNVMMLNQ+1)        SAVED STATUS ELEM                          
SVHDELM  DS    XL(SNVHDLN2+1)        SAVED STATUS ELEM                          
XKEY2    DS    XL64                                                             
XKEY3    DS    XL64                                                             
*                                                                               
MMW      DS    CL200                                                            
MMFULL   DS    F                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*MELEM*'                                                     
MELEM    DS    XL256               MINIO ELEMENT                                
         DS    0D                                                               
         DC    CL8'*MBLOCK*'                                                    
MINBLOCK DS    CL(MINBLKL)                                                      
*                                                                               
MRECTABL EQU   2000                                                             
         DS    0D                                                               
         DC    CL8'*MRTAB*'                                                     
MRECTAB  DS    XL(MRECTABL)                                                     
*                                                                               
         DS    0D                                                               
         DC    CL8'*MBUFF*'                                                     
MBUFFR   DS    XL(3975*2)          ROOM FOR 2 BUFFERS                           
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPI2WKN                                                     
       ++INCLUDE DDMINBLK                                                       
       ++INCLUDE SPGENSNV                                                       
       ++INCLUDE SPGENMKG                                                       
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENXCOM                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'057ANXXXMM   05/01/02'                                      
         END                                                                    
