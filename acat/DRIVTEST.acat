*          DATA SET DRIVTEST   AT LEVEL 006 AS OF 05/01/02                      
*CATALP DRIVTEST                                                                
         TITLE 'DRIVTEST - CONDITION TESTER FOR DRIVER'                         
DRIVTEST CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 100,**DTST**                                                     
         USING DTESTD,RC                                                        
         L     RA,0(R1)            PARAMETER 1 A(GLOBAL)                        
         L     R3,4(R1)            PARAMETER 2 A(CONDITIONAL LIST)              
         USING GLOBALD,RA                                                       
         EJECT                                                                  
*              ROUTINE TO EXECUTE CONDITIONALS                                  
         SPACE 3                                                                
         LA    R0,4                MAX 4 CONDITIONS ALLOWED                     
         SPACE 1                                                                
CONT2    OC    1(3,R3),1(R3)       ANY MORE CONDITIONS                          
         BZ    YES                                                              
         TM    0(R3),X'40'                                                      
         BO    CONT6                                                            
         CLI   GLMODE,GLINIT       INITIALIZATION TESTS                         
         BNE   CONTEND                                                          
         L     R2,0(R3)                                                         
         TM    0(R3),X'80'         IF TEST                                      
         BO    CONT4                                                            
         BAS   RE,DOCON                                                         
         BNE   NO                  IF NO - FAILS                                
         B     CONTEND                                                          
         SPACE 1                                                                
CONT4    BAS   RE,DOCON            NOT TEST                                     
         BE    NO                  IF YES - FAILS                               
         B     CONTEND                                                          
         SPACE 1                                                                
CONT6    CLI   GLMODE,GLINIT       RUN TIME TESTS                               
         BE    CONTEND                                                          
         L     R2,0(R3)                                                         
         TM    0(R3),X'80'         IF TEST                                      
         BO    CONT8                                                            
         BAS   RE,DOCON                                                         
         BNE   NO                  IF NO - FAILS                                
         B     CONTEND                                                          
         SPACE 1                                                                
CONT8    BAS   RE,DOCON            NOT TEST                                     
         BE    NO                  IF YES - FAILS                               
         SPACE 1                                                                
CONTEND  LA    R3,4(R3)                                                         
         BCT   R0,CONT2                                                         
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE TO EXECUTE SINGLE CONDITION                              
         SPACE 3                                                                
DOCON    NTR1                                                                   
*                                  R2=A(DRIVE TABLE CONDITION)                  
         USING DRCOND,R2                                                        
         ZIC   R0,DRCONNCN         R0=NUMBER OF CONDITIONS                      
         LA    R4,DRCONCON         R4=A(CONDITION)                              
         DROP  R2                                                               
         USING DRCONCON,R4                                                      
         SPACE 1                                                                
DOCON2   L     RE,DRCONAF1         RE=A(FIELD 1)                                
         ZIC   R1,DRCONTYP         R1=L'COMP                                    
         LA    RF,DRCONVAL         RF=A(FIELD 2)                                
         ZIC   R5,DRCONOP          R5=OPERATOR NO                               
         LA    R5,TRUTH(R5)                                                     
         LTR   R1,R1                                                            
         BNZ   DOCON4                                                           
         LA    R1,1                                                             
         L     RF,DRCONAF2                                                      
         SPACE 1                                                                
DOCON4   BCTR  R1,0                                                             
         EX    R1,VARCON                                                        
         BH    DOCON6                                                           
         LA    R5,6(R5)                                                         
         BE    DOCON6                                                           
         LA    R5,6(R5)                                                         
         SPACE 1                                                                
DOCON6   CLI   DRCONIOA,C'I'       IF                                           
         BNE   DOCON8                                                           
         MVC   RESULT,0(R5)                                                     
         B     DOCONEND                                                         
         SPACE 1                                                                
DOCON8   CLI   DRCONIOA,C'O'       OR                                           
         BNE   DOCON10                                                          
         CLI   0(R5),C'Y'                                                       
         BE    YES                                                              
         B     DOCONEND                                                         
         SPACE 1                                                                
DOCON10  CLI   0(R5),C'N'          AND                                          
         BE    NO                                                               
         SPACE 1                                                                
DOCONEND LA    R4,L'DRCONCON(R4)                                                
         BCT   R0,DOCON2                                                        
         CLI   RESULT,C'Y'                                                      
         B     XIT                                                              
         SPACE 1                                                                
VARCON   CLC   0(0,RE),0(RF)                                                    
         SPACE 1                                                                
TRUTH    DS    0H                  VALUES FOR EQ GT LT NE NL NH                 
         DC    C'NYNYYN'           IF TEST IS HIGH                              
         DC    C'YNNNYY'           IF TEST IS EQUAL                             
         DC    C'NNYYNY'           IF TEST IS LOW                               
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR TESTING MODULE                                         
         SPACE 3                                                                
DTESTD   DSECT                                                                  
         SPACE 1                                                                
RESULT   DS    CL1                                                              
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DRIVTEST  05/01/02'                                      
         END                                                                    
