*          DATA SET REGENPCBY  AT LEVEL 040 AS OF 05/01/02                      
*CATALP REGENPCB                                                                
         TITLE 'REGENPCBY - REPPAK BUY LINE DISPLAY FOR COMBO ORDERS'           
*              PARAMETER 1 =       A(BUYREC)                                    
*                        2 =       A(OUTPUT AREA)                               
*                                       BYTE 0 = MAXIMUM LINES                  
*                                       OUTPUT AREA = 81 X MAX LINES            
*                                       BYTE 0 = NUMBER LINES ON RETURN         
*                                       THIS CAN BE 0 (DELETES)                 
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                       WORD 1 = A(UNDAY)                       
*                                            2 = A(UNTIME)                      
*                                            3 = A(DATCON)                      
*                                            4 = A(ADDAY)                       
*                                            IF 4 IS 0 THEN ALTERNATE           
*                                            WEEKS ARE NOT EXPLODED             
*                        4 =       A(CONREC)                                    
*                                                                               
*                        5 =       A(CONTRACT PROGRAM PROFILES)                 
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* 14JUL/92 (BU ) --- ORIGINAL ENTRY                                             
*                                                                               
* 22SEP/92 (BU ) --- CHANGE 'SEC' FIELD TO 'TYP', RELOCATE IT...                
*                                                                               
* 16MAR/93 (SKU) --- CHANGE SLL OF 29 TO 25 FOR CLEARING RBUYCOMB X'80'         
*                                                                               
* 13APR/95 (BU ) --- MULTIPLE MG MISSED DATES                                   
*                                                                               
* 01JUN/95 (SKU) --- MAKEGOOD CONTINUATION TO 2ND COMMENT LINE SUPPORT          
*                                                                               
* 13JUL/95 (SKU) --- FIX CONFIRMATION PRINT BUG                                 
*                                                                               
* 13OCT/95 (BU ) --- ADD KATZ DISPLAY CHANGES:  BAD INFO                        
*                                                                               
* 12FEB/96 (SKU) --- SKIP X'07' CREDIT FROM TOTAL SPOT COUNT                    
*                                                                               
* 20JUN/96 (SKU) --- FIX CONFIRMATION PRINT BUG                                 
*                                                                               
* 21DEC/98 (RHV) --- HANDLE NON-DELETED CANCELLED LINES                         
*                                                                               
* 02JUN/00 (BU ) --- UPGRADE FOR TRADE PROCESSING                               
*                                                                               
*                                                                               
*                    ***  END TOMBSTONE  ***                                    
***********************************************************************         
REGENPCB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDLQ,REGENPCB                                                 
         USING WORKD,RC                                                         
MAINLINE EQU   *                                                                
         L     RA,0(R1)            DSECT FOR BUYREC                             
         ST    R1,SAVER1                                                        
         USING BUYREC,RA                                                        
         L     R9,4(R1)            OUTPUT AREA                                  
         USING PD,R9               DISPLAY LINE DSECT                           
         USING CONREC,R8                                                        
         L     R8,12(R1)                                                        
*                                                                               
         ICM   RE,15,16(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   PROFILES,0(RE)      SAVE CONTRACT PROFILES                       
*                                                                               
         L     R2,8(R1)            ROUTINES                                     
         MVC   VOUTDAY(16),0(R2)                                                
         MVI   MAX,0                                                            
         MVC   MAX+1(1),4(R1)      MAX LINES                                    
         MVI   4(R1),0             NO LINES SO FAR                              
         MVC   DATADISP,=H'34'                                                  
         TM    RBUYCNTL,X'C0'      VOID POINTER?                                
         BO    EXIT                                                             
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    MLIN0005                                                         
         TM    RBUYCNTL,X'80'      DELETED?                                     
         BZ    MLIN0010                                                         
         CLI   RBUYCHGI,C'X'       DELETED (NOT CANCELLED?)                     
         BE    EXIT                                                             
* MUST HAVE BEEN CANCELLED                                                      
MLIN0005 DS    0H                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    MLIN0010            YES                                          
         TM    RCONMODR+1,X'C0'    ACE/GRAPHNET                                 
         BNZ   MLIN0010            DO CANCELLED ACE/GRAPHNET BUYS LATER         
         CLC   RBUYCREA,RBUYCHGD   CREATED SAME DAY AS CANCEL?                  
         BE    EXIT                                                             
         CLC   RBUYKMOD,RCONMOD    BUY K LEVEL V K MOD NUM                      
         BL    EXIT                                                             
*                                                                               
* GET TODAY'S DATE                                                              
MLIN0010 EQU   *                                                                
         GOTO1 VDATCON,DMCB,(5,0),(3,TODAY)                                     
*                                                                               
         LR    RE,R9                                                            
         LH    RF,MAX              MAXIMUM LINES                                
         MH    RF,=Y(L'PRTLN)      LINE COUNT                                   
*                                                                               
*         XCEF                      CLEAR OUTPUT AREA                           
         XCEF                                                                   
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   MLIN0020                                                         
* DISPLAY PLAN TOTAL REC LINE                                                   
         L     R1,SAVER1                                                        
         MVI   4(R1),1             1 LINE                                       
*                                                                               
*   THIS COMMENT IS NOW DISPLAYED ORIENTED TO THE PLAN FIELD RATHER             
*     THAN THE RATE FIELD, AS THE RATES ARE NOW DISPLAYED ON A                  
*     SEPARATE LINE FOR ALL ORDERS WITHIN THE COMBO                             
*                                                                               
         MVC   PPLN-28(27),=C'WEEKLY RATE FOR PLAN XXX IS'                      
         MVC   PPLN-7(3),RBUYKPLN                                               
         MVC   DUB(4),RBUYCOS                                                   
         TM    RBUYCOMB,X'80'      IS 'NA' FLAG SET?                            
         BNE   MLIN0012            NO                                           
         MVC   PPLN+8(2),=C'NA'    YES - SET VALUE TO 'NA'                      
         B     MLIN0014                                                         
MLIN0012 EQU   *                                                                
         L     R4,DUB                                                           
         EDIT  (R4),(10,PPLN),2,FLOAT=-,COMMAS=YES,ALIGN=LEFT                   
         EDIT  (2,RBUYTSPT),(4,PTOT-1)                                          
MLIN0014 EQU   *                                                                
         MVC   PPLNFLAG,=C'PLAN'   SET LINE FLAG TO 'PLAN'                      
         B     EXIT                                                             
*                                                                               
*                                                                               
*              BUILD PRINT LINE                                                 
* CHECK FOR MODIFICATION CODE PRINT                                             
MLIN0020 TM    RCONMODR+1,X'C0'    X'C0'=ACE/GRAPHNET CONTRACT                  
         BZ    MLIN0100                                                         
         SPACE 1                                                                
         SR    R4,R4                                                            
         LA    R1,RCONELEM                                                      
MLIN0030 IC    R4,1(R1)            FIND X'1F' ELEMENT                           
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ELEMENT SHOULD BE THERE                      
         CLI   0(R1),X'1F'                                                      
         BNE   MLIN0030                                                         
         SPACE 1                                                                
         MVC   SVCONF,6(R1)        SAVE CONFIRMED STATUS                        
         TM    6(R1),X'80'         X'80'=NOT CONFIRMED                          
         BZ    MLIN0060                                                         
         SPACE 1                                                                
         IC    R4,1(R1)                                                         
         AR    R1,R4               GET X'20' (SEND) ELEMENT                     
         CLI   0(R1),X'20'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         CLI   5(R1),1             DON'T PRINT CHANGES WHEN                     
         BNE   MLIN0040            REP VERSION IS 1 AND                         
         CLI   14(R1),0            STATION VERSION IS 0                         
         BNE   MLIN0040                                                         
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    MLIN0120                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0120                                                         
         SPACE 1                                                                
MLIN0040 CLC   5(1,R1),14(R1)      COMPARE REP TO STATION VERSION               
         BH    MLIN0050  THEN COMPARE BUY VERSION TO HIGHER OF THE TWO          
*                        & DON'T PRINT CHANGES FOR OLD VERSIONS                 
         CLC   RBUYVER,14(R1)      STATION VERSION                              
         BE    MLIN0110                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0120            OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    MLIN0110                                                         
         B     EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
MLIN0050 CLC   RBUYVER,5(R1)       REP VERSION                                  
         BE    MLIN0110                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0120            OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    MLIN0110                                                         
         B     EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
* CONFIRMED -  ONLY PRINT CHANGES FROM LAST MOD NUMBER, AND DON'T               
*              PRINT CHANGE CODE O (ORDER COMMENT)                              
         SPACE 1                                                                
MLIN0060 DS    0H                                                               
*                                                                               
         CLI   RCONMOD,0           DON'T PRINT CHANGES FOR MOD 0                
         BE    MLIN0070                                                         
*                                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    MLIN0065                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0065            CHECK IF CANCELLED AND ADDED IN THE          
         LA    R6,RBUYREC          SAME MOD NUM                                 
         MVI   ELCODE,X'10'        EXTRA DESCRIPTION ELEMENT PRESENT?           
         BAS   RE,GETEL                                                         
         BNE   MLIN0065                                                         
         USING RBUYXXEL,R6         COMPARE                                      
         CLC   RBUYXXMD,RBUYKMOD   MOD NUMBER WHEN BUY WAS ADDED AND            
         BE    EXIT                MOD NUMBER WHEN IT WAS CANCELLED             
         DROP  R6                  DON'T PRINT LINE IF SAME                     
*                                                                               
MLIN0065 DS    0H                                                               
         ZIC   R4,RCONMOD                                                       
         BCTR  R4,0                GET LAST MOD NUMBER                          
         ZIC   R1,RBUYKMOD                                                      
         CR    R1,R4                                                            
         BE    MLIN0080                                                         
MLIN0070 DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0120            OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
MLIN0080 DS    0H                                                               
         LA    R4,PCHG                                                          
         CLI   RBUYCHGI,C'O'       ORDER COMMENT                                
         BE    MLIN0090                                                         
         MVC   0(1,R4),RBUYCHGI                                                 
         LA    R4,1(R4)                                                         
         MVI   0(R4),C' '          MOVE BLANK TO 2ND CHARACTER                  
         SPACE 1                                                                
MLIN0090 CLI   RBUYCHGI+1,C'O'     ORDER COMMENT                                
         BE    MLIN0120                                                         
         CLI   RBUYCHGI+1,C' '     BLANK                                        
         BE    MLIN0120                                                         
         MVC   0(1,R4),RBUYCHGI+1                                               
         OC    PCHG,=C'  '         SPACES                                       
         B     MLIN0120                                                         
         SPACE 1                                                                
* FOR NON ACE/GRAPHNET CONTRACTS                                                
         SPACE 1                                                                
MLIN0100 DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   MLIN0105                                                         
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    MLIN0110                                                         
MLIN0105 DS    0H                                                               
         CLI   RCONMOD,0           K MOD NUM                                    
         BE    MLIN0120                                                         
         CLI   RCONMOD,X'FF'                                                    
         BE    MLIN0120                                                         
         CLI   RBUYKMOD,X'FF'                                                   
         BE    MLIN0120                                                         
         CLI   RBUYKMOD,0          BUY K MOD NUM                                
         BE    MLIN0120                                                         
         CLC   RBUYKMOD,RCONMOD                                                 
         BL    MLIN0120                                                         
*                                                                               
MLIN0110 MVC   PCHG,RBUYCHGI                                                    
*                                                                               
MLIN0120 EDIT  (1,RBUYKLIN),(3,PLIN)    LINE NUMBER                             
*                                                                               
         MVC   DMCB(2),RBUYDUR     LENGTH                                       
         NI    DMCB,X'7F'          MINUTE IND                                   
         EDIT  (2,DMCB),(3,PLEN)                                                
         TM    RBUYDUR,X'80'       MINUTES?                                     
         BZ    MLIN0130                                                         
         MVC   PLEN-1(3),PLEN                                                   
         MVI   PLEN+2,C'M'                                                      
*                                                                               
MLIN0130 MVC   PCLS,RBUYCLS        CLASS                                        
         MVC   PSEC,RBUYSEC        SECTION                                      
         MVC   PTYP,RBUYTYP        TYPE                                         
*                                                                               
* DISPLAY FLIGHT NUMBER IF NO SECTION EXISTS FOR BUY                            
*                                                                               
MLIN0140 EQU   *                                                                
*                                                                               
*                                                                               
         CLI   RBUYFLT,0           DOES BUY HAVE A FLIGHT                       
         BE    MLIN0150            NO                                           
         OC    VADDAY,VADDAY       ONLY DO IT IF ON-LINE                        
         BNZ   MLIN0150                                                         
         CLI   PSEC,0              TEST FOR ABSENCE OF SECTION                  
         BE    *+12                                                             
         CLI   PSEC,C' '                                                        
         BNE   MLIN0150                                                         
         MVI   PSEC,C'F'           FLIGHT CODE PREFIX                           
         EDIT  (B1,RBUYFLT),(2,PSEC+1),ALIGN=LEFT                               
*                                                                               
MLIN0150 MVC   PPLN,RBUYKPLN       PLAN                                         
         CLC   RBUYKPLN,=3X'FF'                                                 
         BNE   *+10                                                             
         MVC   PPLN,=3C' '                                                      
*                                                                               
*                                                                               
         CLC   RBUYKPLN,=3X'FF'    PLAN                                         
         BE    MLIN0160                                                         
         MVC   PRAT+3(4),=C'PLAN'                                               
         B     MLIN0170                                                         
MLIN0160 EQU   *                                                                
         TM    RBUYCOMB,X'80'      IS 'NA' FLAG SET?                            
         BNO   MLIN0165            NO                                           
         MVC   PRAT+8(2),=C'NA'    YES - SET RATE TO 'NA'                       
         B     MLIN0170                                                         
MLIN0165 EQU   *                                                                
         EDIT  (4,RBUYCOS),(10,PRAT),2,FLOAT=-,COMMAS=YES                       
         TM    RBUYFLG2,X'02'      TRADE BUY?                                   
         BNO   MLIN0170            NO                                           
         MVC   PTOT+4(2),=C'TR'    INSERT TRADE FLAG                            
*                                                                               
MLIN0170 MVC   DUB(2),RBUYLEN      REC LENGTH                                   
         LH    R5,DUB                                                           
         LA    R5,RBUYREC(R5)      REC END FOR BXLE                             
*              PREPARE TO GET DAY-TIME ELEMENTS                                 
         BCTR  R5,R0                                                            
         BCTR  R5,R0                                                            
         SR    R4,R4                                                            
         LA    R3,RBUYELEM         FIRST ELEM                                   
* GET DAY SPAN                                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RE,RBUYSTED                                                      
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CR    RE,RF                                                            
         BNH   *+8                                                              
         LA    RF,7(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,DAY1                                                          
*                                                                               
         SH    R9,=Y(L'PRTLN)                                                   
*                                                                               
         SR    R6,R6               LINE CTR FOR DAY-TIME LINES                  
         B     MLIN0190            NO DATE ELEM                                 
*              DISPLAY DAY-TIME                                                 
MLIN0180 EQU   *                                                                
         CLC   =X'FFFF',2(R3)      CONVERT ERROR DATA?                          
         BNE   MLIN0182            NO                                           
         BAS   RE,KZDAYERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     MLIN0184                                                         
MLIN0182 EQU   *                                                                
         GOTO1 VOUTDAY,DMCB,3(R3),2(R3),PDAY                                    
MLIN0184 EQU   *                                                                
*                                                                               
         CLC   =X'FFFF',4(R3)      CONVERT ERROR DATA?                          
         BNE   MLIN0186            NO                                           
         BAS   RE,KZTIMERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     MLIN0188                                                         
MLIN0186 EQU   *                                                                
         GOTO1 VUNTIME,DMCB,4(R3),PTIM                                          
MLIN0188 EQU   *                                                                
         B     MLIN0190                                                         
         EJECT                                                                  
*   RETRIEVE THE X'22' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        DAY FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,               
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZDAYERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZDA0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'22'                 
         CLI   0(R6),X'22'         ERROR ELEMENT?                               
         BNE   KZDA0020            NO  - GO BACK FOR NEXT                       
         MVC   PDAY(10),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'32' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        TIME FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,              
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZTIMERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZTM0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'32'                 
         CLI   0(R6),X'32'         ERROR ELEMENT?                               
         BNE   KZTM0020            NO  - GO BACK FOR NEXT                       
         MVC   PTIM(11),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
MLIN0190 IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4680                                                         
*                                                                               
         CLI   0(R3),2             DAY TIME ELEM?                               
         BNE   MLIN0200                                                         
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R6,1(R6)            DAY-TIME CTR                                 
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    MLIN0190                                                         
         B     MLIN0180            DISPLAY NEXT DAY-TIME                        
*                                                                               
*              GET DATE ELEMENTS                                                
MLIN0200 SR    R7,R7               DATE LINE CTR                                
         L     R1,SAVER1                                                        
         L     R9,4(R1)                                                         
         SH    R9,=Y(L'PRTLN)                                                   
         CLI   0(R3),3             DATE ELEM                                    
         BNE   MLIN0360            IF NONE JUST CARRY ON                        
         B     MLIN0350                                                         
* PROCESS DATE ELEMENT                                                          
MLIN0210 SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RF,9(R3)            NO. PER WEEK                                 
* DISPLAY NPW                                                                   
         TM    RBUYCOMB,X'80'      IS 'NA' FLAG SET?                            
         BNO   MLIN0212            NO                                           
         ZIC   RF,RBUYCOMB                                                      
         SLL   RF,25               DROP HIGH-ORDER BIT                          
         SRL   RF,25                                                            
         MVI   PNPW+4,C'*'         YES - FLAG 'PHANTOM' NPW                     
MLIN0212 EQU   *                                                                
         EDIT  (RF),(3,PNPW)                                                    
         SR    R2,R2                                                            
         IC    R2,10(R3)           NO. OF WEEKS                                 
* DISPLAY NO. OF WEEKS IF NO CLASS                                              
         CLI   PCLS,0                                                           
         BE    *+12                                                             
         CLI   PCLS,C' '                                                        
         BNE   MLIN0220                                                         
         EDIT  (R2),(2,PCLS)                                                    
         MVI   PCLS+2,C'W'                                                      
* DISPLAY TOTAL SPOTS MINUS MISSED SPOTS                                        
***>0220 TM    RBUYCNTL,X'80'                                                   
MLIN0220 CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    MLIN0290                                                         
         SPACE 1                                                                
         LA    R1,RBUYELEM                                                      
         IC    R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BE    MLIN0230                                                         
         CLI   0(R1),4                                                          
         BNE   *-18                                                             
         CLC   2(3,R1),=C'CR='                                                  
         BE    MLIN0240            IGNORE THE MINUS BUY CHECK                   
         SPACE 1                                                                
MLIN0230 TM    RBUYCOS,X'80'                                                    
         BO    MLIN0290                                                         
MLIN0240 MR    RE,R2               TOTAL SPOTS IN RF                            
* GET MISSED SPOTS, IF ANY                                                      
         LA    R1,RBUYELEM                                                      
MLIN0250 CLI   0(R1),0             LAST?                                        
         BE    MLIN0280                                                         
         CLI   0(R1),6             MISSED ELEM?                                 
         BE    MLIN0270                                                         
*        CLI   0(R1),7             MISSED CREDIT ISSUED                         
*        BE    MLIN0270                                                         
MLIN0260 IC    R4,1(R1)                                                         
         AR    R1,R4               NEXT ELEM                                    
         B     MLIN0250                                                         
* CHECK MISSED DATE AGAINST DATE ELEM                                           
MLIN0270 CLC   2(3,R1),2(R3)        START                                       
         BL    MLIN0260                                                         
         CLC   2(3,R1),5(R3)       END                                          
         BH    MLIN0260                                                         
* MISSED ELEM APPLIES, SUBTRACT MISSED SPOTS                                    
         IC    R4,6(R1)            MISSED SPOTS                                 
         SR    RF,R4                                                            
         B     MLIN0260                                                         
* DISPLAY TOTAL SPOTS FOR DATE ELEM                                             
MLIN0280 EQU   *                                                                
         TM    RBUYCOMB,X'80'      IS 'NA' FLAG SET?                            
         BNO   MLIN0285            NO                                           
         MVI   PTOT+3,C'*'         FLAG 'PHANTOM' SPOTS                         
MLIN0285 EQU   *                                                                
         EDIT  (RF),(4,PTOT-1)                                                  
*                                                                               
*    TEST FOR ALT WEEKS                                                         
*                                                                               
MLIN0290 TM    8(R3),X'40'         ALT?                                         
         BZ    MLIN0320                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    MLIN0320                                                         
*                                                                               
* MUST BE OFF-LINE CONTRACT - EXPLODE ALTERNATE WEEKS                           
*                                                                               
* GET START-END IN WORK                                                         
*                                                                               
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK                                      
*                                                                               
         GOTO1 (RF),(R1),(3,5(R3)),WORK+6                                       
MLIN0300 GOTO1 VDATCON,(R1),WORK,(4,PDAT)                                       
         MVI   PDAT+11,C'A'                                                     
         L     R2,DAY1                                                          
         LTR   R2,R2               ROTATOR?                                     
         BZ    MLIN0310                                                         
* GET END DATE OF WEEK                                                          
         GOTO1 VADDAY,(R1),WORK,WORK+12,(R2)                                    
*                                                                               
         GOTO1 VDATCON,(R1),WORK+12,(4,PDAT+6)                                  
         MVI   PDAT+5,C'-'                                                      
* GET NEXT ALTERNATE WEEK                                                       
MLIN0310 MVC   DUB(6),WORK                                                      
         GOTO1 VADDAY,(R1),DUB,WORK,14                                          
         CLC   WORK(6),WORK+6      PAST ELEM END DATE?                          
         BH    MLIN0340                                                         
*                                                                               
* DISPLAY NEXT ALTERNATE WEEK                                                   
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R7,1(R7)            LINE CTR                                     
         CH    R7,MAX                                                           
         BNH   MLIN0300                                                         
         LR    R6,R7                                                            
         B     MLIN4680                                                         
MLIN0320 GOTO1 VDATCON,DMCB,(3,2(R3)),(4,PDAT)                                  
         CLC   2(3,R3),5(R3)       SAME START-END?                              
         BE    MLIN0330                                                         
*                                                                               
         MVI   PDAT+5,C'-'                                                      
*              CONVERT ELEMENT END DATE TO MMMDD                                
         GOTO1 VDATCON,DMCB,(3,5(R3)),(4,PDAT+6)                                
*                                                                               
MLIN0330 TM    8(R3),X'40'         ALTERNATE WEEKS?                             
         BZ    MLIN0340                                                         
         MVI   PDAT+11,C'A'                                                     
         LA    R9,L'PRTLN(R9)                                                   
         LA    R7,1(R7)                                                         
         CH    R7,MAX                                                           
         BNH   *+10                                                             
         LR    R6,R7                                                            
         B     MLIN4680                                                         
         MVC   PDAT(15),=C'ALTERNATE WEEKS'                                     
*              GET NEXT ELEM                                                    
MLIN0340 IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN0360                                                         
*                                                                               
         CLI   0(R3),3             ANOTHER DATE ELEM?                           
         BNE   MLIN0360                                                         
         TM    8(R3),X'40'         ALT WEEKS                                    
         BZ    MLIN0350                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    MLIN0350                                                         
         MVI   PDAT+79,C'*'        IND ANOTHER ALT ELEM                         
MLIN0350 LA    R7,1(R7)            DATE CTR                                     
         LA    R9,L'PRTLN(R9)                                                   
         CH    R7,MAX                                                           
         BNH   MLIN0210            DISPLAY NEXT DATE ELEM                       
         LR    R6,R7                                                            
         B     MLIN4680                                                         
*              PREPARE TO DISPLAY COMMENTS                                      
MLIN0360 CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    MLIN4680                                                         
         CLI   0(R3),4             COMMENT ELEM?                                
         BE    MLIN0370                                                         
         CLI   0(R3),0             END OF RECORD?                               
         BE    MLIN4680                                                         
         B     MLIN4520            CHECK FOR ORDER COMMENTS                     
         SPACE 1                                                                
MLIN0370 LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)         POSITION TO NEXT AVAILABLE LINE              
*                                                                               
*                                                                               
*                                                                               
*              DISPLAY COMMENT                                                  
MLIN0380 CLC   2(3,R3),=C'CR='     CREDIT FOR MISSED                            
         BNE   MLIN0400                                                         
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
MLIN0390 IC    R4,1(R3)                                                         
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4320                                                         
         CLI   0(R3),0                                                          
         BE    MLIN4320                                                         
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   MLIN0390                                                         
         SPACE 1                                                                
         MVC   PDAY+6(29),=C'CREDIT FOR SPOT          LINE'                     
         CLI   9(R3),2             NO. OF MISSED                                
         BL    *+10                                                             
         MVC   PDAY+6(29),=C'CREDIT FOR SPOTS         LINE'                     
         SPACE 1                                                                
         EDIT  (1,2(R3)),(2,PDAY+36),ALIGN=LEFT                                 
         B     MLIN4320                                                         
         SPACE 1                                                                
MLIN0400 CLC   2(3,R3),=C'MG='     MAKE-GOOD?                                   
         BNE   MLIN4440                                                         
         LA    R2,PDAT             SET A(PRINTOUT)                              
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
         MVC   PDAY+6(18),=C'MAKE-GOOD FOR LINE'                                
         MVI   MGSPOT#,0           CLEAR COUNTER 1                              
         MVI   MGCNTR#,0           CLEAR COUNTER 2                              
* GET MAKE-GOOD ELEMENT                                                         
MLIN4000 EQU   *                                                                
         ZIC   RF,MGCNTR#          INCREMENT COUNTER                            
         LA    RF,1(RF)                                                         
         STC   RF,MGCNTR#                                                       
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4320                                                         
         CLI   0(R3),0                                                          
         BE    MLIN4320                                                         
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   MLIN4000                                                         
         CLI   MGCNTR#,3           MORE THAN 3 MG ELEMENTS?                     
         BNH   MLIN4040            NO                                           
         MVI   MGCNTR#,1           YES - RESET COUNTER TO 1                     
         LA    R9,L'PRTLN(R9)      BUMP TO NEXT OUTPUT LINE                     
         MVC   PDAY+6(18),=C'MAKE-GOOD FOR LINE'                                
         LA    R2,PDAT             RESET A(PRINTOUT)                            
         LA    R6,1(R6)            BUMP LINE COUNTER                            
         CH    R6,MAX                                                           
         BH    MLIN4680            MAX EXCEEDED:  EXIT                          
* DISPLAY MAKE-GOOD REFERENCE                                                   
MLIN4040 EQU   *                                                                
         EDIT  (1,2(R3)),(3,PDAY+25),ALIGN=LEFT                                 
         GOTO1 VDATCON,DMCB,(3,3(R3)),(4,(R2))    MISSED DATE                   
*                                                                               
         CLI   9(R3),1             1 MISSED SPOT?                               
         BE    MLIN4080            NO  - CHECK FOR 0/>1                         
         ZIC   RF,MGSPOT#          YES - BUMP SPOT NUMBER COUNT                 
         LA    RF,1(RF)               BECAUSE ONLY 1ST NON-1 COUNT              
         STC   RF,MGSPOT#             GETS FULL 'SPOTS' PRINTOUT                
         CLI   9(R3),1             RESET CONDITION CODE                         
         B     MLIN4120                                                         
MLIN4080 EQU   *                                                                
         LA    R2,6(R2)            YES - BUMP TO NEXT PRINT POS                 
         B     MLIN4000               GO BACK LOOK FOR ANOTHER                  
MLIN4120 EQU   *                                                                
         BNL   MLIN4200                                                         
         CLI   MGSPOT#,1           FIRST TIME?                                  
         BH    MLIN4160            NO                                           
         MVC   6(9,R2),=C'(0 SPOTS)'                                            
         LA    R2,16(R2)           BUMP TO NEXT POSITION                        
         B     MLIN4000            GO BACK FOR NEXT                             
MLIN4160 EQU   *                                                                
         MVC   6(3,R2),=C'(0)'                                                  
         LA    R2,10(R2)           BUMP TO NEXT POSITION                        
         B     MLIN4000            GO BACK FOR NEXT                             
MLIN4200 EQU   *                                                                
         EDIT  (1,9(R3)),(3,5(R2))                                              
         CLI   MGSPOT#,1           FIRST TIME?                                  
         BH    MLIN4240            NO                                           
         MVC   9(6,R2),=C'SPOTS)'                                               
         B     MLIN4280                                                         
MLIN4240 EQU   *                                                                
         MVI   8(R2),C')'                                                       
         BAS   RE,SETPAREN                                                      
         LA    R2,10(R2)           BUMP TO NEXT POSITION                        
         B     MLIN4000            GO BACK FOR NEXT                             
MLIN4280 EQU   *                                                                
         BAS   RE,SETPAREN                                                      
         LA    R2,16(R2)           BUMP TO NEXT PRINT POSITION                  
         B     MLIN4000            GO BACK FOR NEXT                             
         EJECT                                                                  
SETPAREN NTR1                                                                   
         LA    RE,4(R2)                                                         
         LA    RE,1(RE)                                                         
         CLI   0(RE),C' '                                                       
         BE    *-8                                                              
         BCTR  RE,R0                                                            
         MVI   0(RE),C'('                                                       
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
MLIN4320 EQU   *                                                                
         LM    R3,R5,DMCB+12       BXLE                                         
* CHECK FOR COMMENT ALONG WITH MG=                                              
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         IC    RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
MLIN4360 CLI   0(RE),C' '                                                       
         BE    MLIN4400                                                         
         CLI   0(RE),C'+'          CHECK FOR CONTINUATION CHAR                  
         BE    MLIN4490                                                         
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    MLIN4360                                                         
         B     MLIN4480                                                         
* MOVE REST OF COMMENT                                                          
MLIN4400 SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    MLIN4480                                                         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)      NEXT LINE                                    
         CH    R6,MAX                                                           
         BH    MLIN4680                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     MLIN4480                                                         
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
MLIN4440 IC    R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
         MVC   PRAT(3),=C'COM'     SET COMMENT FLAG                             
*                                                                               
MLIN4480 LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4680                                                         
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   MLIN4560            CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BNH   MLIN4440            DISPLAY NEXT COMMENT                         
         B     MLIN4680            OR RETURN                                    
*                                                                               
MLIN4490 DS    0H                  CONTINUING MAKEGOOD IN 2ND CMMT LINE         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4680                                                         
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   MLIN4560            CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    MLIN4680            DISPLAY NEXT COMMENT OR RETURN               
*                                                                               
MLIN4500 DS    0H                                                               
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         IC    RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
MLIN4510 CLI   0(RE),C' '                                                       
         BE    MLIN4515                                                         
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    MLIN4510                                                         
         B     MLIN4518                                                         
* MOVE REST OF COMMENT                                                          
MLIN4515 SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    MLIN4518                                                         
         CH    R6,MAX                                                           
         BH    MLIN4680                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     MLIN4518                                                         
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
         IC    R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
         MVC   PRAT(3),=C'COM'     SET COMMENT FLAG                             
*                                                                               
MLIN4518 LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     MLIN4680                                                         
*                                                                               
MLIN4520 LR    R7,R6               IF NO REGULAR BUY CMTS,                      
         MH    R7,=Y(L'PRTLN)      POSITION TO NEXT AVAILABLE LINE              
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)                                                      
*                                                                               
MLIN4560 CLI   0(R3),X'84'         ORDER COMMENT?                               
         BE    MLIN4600                                                         
         IC    R4,1(R3)                                                         
         BXLE  R3,R4,MLIN4560      END OF RECORD?                               
         B     MLIN4680                                                         
         SPACE 1                                                                
MLIN4600 CH    R6,MAX              MAXIMUM LINES?                               
         BNL   MLIN4680                                                         
         SPACE 1                                                                
         TM    RCONMODR+1,X'C0'    IF ACE OR GRAPHNET                           
         BZ    MLIN4640                                                         
         TM    SVCONF,X'40'        AND CONFIRMED                                
         BZ    MLIN4640                                                         
***>     TM    RBUYCNTL,X'80'      AND CANCELLED                                
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    MLIN4680            DON'T PRINT ORDER COMMENTS                   
         SPACE 1                                                                
MLIN4640 MVC   PDAY+6(4),=C'SOC-'  STATION ORDER CMT                            
         TM    2(R3),X'80'                                                      
         BZ    *+8                                                              
         MVI   PDAY+6,C'R'         REP ORDER CMT (ROC)                          
         IC    R4,1(R3)                                                         
         SH    R4,=H'4'                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PDAY+10(0),3(R3)                                                 
         CLC   3(3,R3),=C'#DS'     INDICATES DON'T SEND THIS LINE               
         BNE   *+8                                                              
         MVI   NOSEND,C'D'                                                      
         B     MLIN4480            LOOP BACK FOR OTHER ORDER CMTS               
*                                                                               
*              RETURN                                                           
MLIN4680 L     R1,SAVER1                                                        
         CH    R6,MAX                                                           
         BNH   *+8                                                              
         LH    R6,MAX                                                           
         STC   R6,4(R1)            LINE CTR                                     
         MVI   8(R1),0                                                          
         CLI   NOSEND,C'D'                                                      
         BNE   *+8                                                              
         MVI   8(R1),C'D'          INDICATE THIS LINE IS 'DON'T SEND'           
EXIT     XMOD1 1                                                                
*                                                                               
MOVECOM  MVC   PDAY+6(0),2(R3)                                                  
*                                                                               
FULL     DS    F                   LOCAL STORAGE                                
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
*                                                                               
*                                                                               
* PRINT LINE DSECT:  ALTERNATE TWO-LINE DISPLAY FOR COMBO ORDERS                
       ++INCLUDE REGENPBYC                                                      
*                                                                               
WORKD    DSECT                                                                  
WORK     DS    CL17                                                             
SVCONF   DS    CL1                 CONFIRMED STATUS FOR ACE/GRAPHNET            
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(UNTIME)                                    
VDATCON  DS    A                   A(DATCON)                                    
VADDAY   DS    A                   A(ADDAY)                                     
DMCB     DS    6F                                                               
DUB      DS    D                                                                
SAVER1   DS    F                                                                
MAX      DS    H                                                                
TODAY    DS    CL3                                                              
DAY1     DS    F                                                                
NOSEND   DS    X                   DON'T SEND STATUS                            
MGSPOT#  DS    X                                                                
MGCNTR#  DS    X                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
PROFILES DS    XL8                 CONTRACT PROGRAM PROFILES                    
WORKDLQ  EQU   *-WORKD                                                          
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
       ++INCLUDE RECNTPROF                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'040REGENPCBY 05/01/02'                                      
         END                                                                    
