*          DATA SET SPORDAMNT  AT LEVEL 002 AS OF 10/23/19                      
*CATALP SPORDAMT                                                                
         TITLE 'SPORDAMT - SPOT DARE ORDER ACTIVITY MAINTENANCE'                
         SPACE 2                                                                
***********************************************************************         
*                                                                     *         
* THIS SUBROUTINE ANALYZES SPOTPAK BUY RECORDS AND MAINTAINS A TABLE  *         
* OF THE WEEKS OF ACTIVITY BY PRODUCT AND PARTNER. THIS DATA IS USED  *         
* BY THE CALLING PROGRAM TO UPDATE ORDER 'NEED TO RESEND' FLAG        *         
*                                                                     *         
* THE PROGRAM PROCESSES BUY RECORD ACTIVITY                                     
*                                                                     *         
* PARAMETER LIST IS +0  00  A(ORDATABLE) FOR ACTIONS 1/2/3/4/5        *         
*                                                                     *         
*                   +4  00  A(BUYREC)   FOR ACTIONS 1/2/3             *         
*                                       WHICH POST BUYREC ACTIVITY    *         
*                                       TO THE TABLE                  *         
*                                                                     *         
* DETAILS OF THE TABLE STRUCTURE ARE IN A DSECT AT THE END OF THIS    *         
* LISTING. SEVERAL PARAMETERS ARE REQUIRED IN THE TABLE.              *         
*                                                                     *         
***********************************************************************         
*                                                                               
* WHO  DDMMMYR LVL WHAT                                                         
* ---- ------- --- ----                                                         
* HWON SEP0716 001 INITIAL BUILD                                                
*                                                                               
***********************************************************************         
ORDAMNT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,ORDAMNT                                              
         USING WORKD,RC                                                         
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
*                                                                               
         MVC   SVPARS,0(R1)        SAVE CALLING PARAMETERS                      
*                                                                               
         L     RA,SVTABADR         GET TABLE ADDRESS                            
         USING ORDATABD,RA                                                      
*                                                                               
         MVI   ORDAERR,ORDANOAC    PRESET 'NO-ACTIVITY' FLAG                    
*                                                                               
         CLI   ORDAACTN,ORDACOPY   TEST POST COPY                               
         BE    BUY                                                              
         CLI   ORDAACTN,ORDACHG    TEST POST CHANGE                             
         BE    BUY                                                              
         CLI   ORDAACTN,ORDAADD    TEST POST ADD                                
         BE    BUY                                                              
         CLI   ORDAACTN,ORDAINIT   TEST INITIALIZATION ONLY                     
         BE    INIT                                                             
         DC    H'0'                                                             
*                                                                               
NEQXIT   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
*                                                                               
EQXIT    CR    RB,RB               SET CC EQUAL                                 
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* SUBROUTINE TO INITIALIZE DATE TABLE *                                         
*=====================================================================*         
INIT     NTR1                                                                   
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* PROCESS BUY COPY/CHANGE/ADD                                                   
*=====================================================================*         
         USING ORDTRKD,FLDTRACK                                                 
BUY      DS    0H                                                               
         XC    FLDTRACK,FLDTRACK   INITIALIZE FLDTRACK                          
*                                                                               
*============================================                                   
* SAVE BUY FIELDS BEING TRACKED TO FLDTRACK                                     
*============================================                                   
         L     R2,SVRECADR         POINT TO BUYREC                              
         LA    R2,BDELEM-BUYREC(R2)                                             
         MVI   ELCODE,BDCODEQ      X'01'- PRIMARY                               
         BAS   RE,NEXTEL2                                                       
         JNE   *+2                                                              
         MVC   ORDTDAY,BDDAY-BDELEM(R2)                                         
         MVC   ORDTSTIM,BDTIMST-BDELEM(R2)                                      
         MVC   ORDTETIM,BDTIMEND-BDELEM(R2)                                     
         MVC   ORDTSEC,BDSEC-BDELEM(R2)                                         
         MVC   ORDTCOST,BDCOST-BDELEM(R2)                                       
         MVC   ORDTCIND,BDCIND-BDELEM(R2)                                       
         MVC   ORDTCIN2,BDCIND2-BDELEM(R2)                                      
*                                                                               
         MVI   ORDTYPE,ORDCASH                                                  
         L     RF,ORDAAREP                                                      
         OC    0(4,RF),0(RF)       HAVE TRADE REP?                              
         BZ    BUY010                                                           
         CLC   0(2,RF),BDREP-BDELEM(R2)                                         
         BH    BUY010                                                           
         CLC   2(2,RF),BDREP-BDELEM(R2)                                         
         BL    BUY010                                                           
         MVI   ORDTYPE,ORDTRADE                                                 
*                                                                               
BUY010   L     R2,SVRECADR         POINT TO BUYREC                              
         LA    R2,BDELEM-BUYREC(R2)                                             
         MVI   ELCODE,NDCORGQ      X'02'- ORIGINAL DEMO                         
         BAS   RE,NEXTEL                                                        
         JNE   *+2                                                              
*                                                                               
         LLC   RE,1(R2)                                                         
         LAY   RE,NDELEM-NDEMNO(RE)                                             
         SRL   RE,3                GET NUMBER OF DEMOS                          
         LTR   RE,RE               HAVE 0 DEMOS??                               
         BZ    BUY040               SKIP DEMOS                                  
*                                                                               
         LA    R2,NDEMNO-NDELEM(R2)  POINT TO BUY DEMO LIST                     
         L     RF,ORDAADEM           POINT TO ESTHDR                            
BUY020   CLC   0(L'NDEMNO,R2),0(RF)                                             
         BE    BUY030                                                           
         LA    R2,NDEMLNQ(R2)                                                   
         BCT   RE,BUY020                                                        
         B     BUY040              SKIP DEMO                                    
*                                                                               
BUY030   MVC   ORDTDEMO,4(R2)                                                   
*                                                                               
BUY040   L     R2,SVRECADR         POINT TO BUYREC                              
         LA    R2,BDELEM-BUYREC(R2)                                             
         MVI   ELCODE,X'71'        X'71'- COST 2 VALUE ELEM                     
         BAS   RE,NEXTEL                                                        
         JNE   BUY050                                                           
         ICM   RF,15,2(R2)                                                      
         CVD   RF,DUB                                                           
         MVC   ORDTCS2V,DUB+3                                                   
*                                                                               
BUY050   L     R2,SVRECADR         POINT TO BUYREC                              
         LA    R2,BDELEM-BUYREC(R2)                                             
         MVI   ELCODE,X'73'        X'73'- COST 2 FACTOR ELEM                    
         BAS   RE,NEXTEL                                                        
         JNE   BUY070                                                           
         ICM   RF,15,2(R2)                                                      
         CVD   RF,DUB                                                           
         MVC   ORDTCS2F,DUB+3                                                   
*                                                                               
*============================================                                   
* CREATE ORDADATA/ACTIVITY ENTRY IN DATATAB                                     
*============================================                                   
BUY070   L     R2,SVRECADR         GET BUYREC ADDRESS                           
         USING BUYRECD,R2                                                       
*                                                                               
         LAY   RE,WORKTAB          INITIALIZE WORKTAB                           
         LAY   RF,WORKTABX-WORKTAB                                              
         XCEFL                                                                  
*                                                                               
         XC    DATATAB,DATATAB     INITIALIZE DATATAB                           
*                                                                               
T#       USING ORDADATA,DATATAB                                                 
         MVC   T#.ORDATYPE,ORDTYPE SET CASH/TRADE                               
*                                                                               
         CLI   BUYKPRD,X'FF'       TEST POL BUY                                 
         BE    BUY100                                                           
*                                                                               
*=======================================================                        
* NON-POL BUY - SEARCH FOR PIGGYBACK ELEMENT IF NEEDED                          
*=======================================================                        
         MVC   T#.ORDAPRD,BUYKPRD  SET PRD CODE                                 
         CLI   BDTIME,0            TEST PIGGYBACK                               
         BE    BUY100              NO                                           
*                                                                               
         LA    R2,BDELEM                                                        
         MVI   ELCODE,PBCODEQ      X'04'-PIGGYBACK ELEM                         
         BAS   RE,NEXTEL                                                        
         JNE   *+2                                                              
         MVC   T#.ORDAPRD2,2(R2)      SET PARTNER CODE                          
*                                                                               
         MVC   DUB(1),T#.ORDAPRD                                                
         LA    R1,DUB                                                           
         BAS   RE,FINDPRD                                                       
         MVC   DUB+4(1),T#.ORDAPRD2                                             
         LA    R1,DUB+4                                                         
         BAS   RE,FINDPRD                                                       
         CLC   DUB+1(3),DUB+5      COMPARE ALPHA CODES                          
         BL    BUY100               ** AND SWITCH IF NEEDED                     
         MVC   HALF,T#.ORDAPRD                                                  
         MVC   T#.ORDAPRD,HALF+1   PRD2                                         
         MVC   T#.ORDAPRD2,HALF    PRD1                                         
*                                                                               
*===============================================                                
* CHECK IF SPOTS ARE PRESENT TO BUILD SCHEDULE                                  
*===============================================                                
BUY100   L     R2,SVRECADR         POINT TO BUYREC                              
         LA    R2,BDELEM-BUYREC(R2)                                             
BUY110   SR    RF,RF               BUMP TO NEXT BUY ELEMENT                     
         ICM   RF,1,1(R2)                                                       
         LA    R2,0(RF,R2)                                                      
         JZ    *+2                 DIE ON ELEMENT LENGTH ZERO                   
         CLI   0(R2),0             END OF RECORD?                               
         BE    BUY280               YES                                         
         CLI   0(R2),RCORGQ        HAVE X'06'-ORIG/X'07'-OTO                    
         BL    BUY110                                                           
         CLI   0(R2),RCPOTOQ       OR X'0B'-POL ORIG/X'0C'-POL OTO              
         BH    BUY110                                                           
*                                                                               
         USING REGELEM,R2                                                       
         TM    RSTATUS,RSMINUSQ+RSMINSDQ  MINUS OR MINUSED SPOT?                
         BNZ   BUY110                      YES, SKIP THE SPOT                   
*                                                                               
BUY120   CLI   RCODE,RCPOLOQ       TEST POL ELEMENT                             
         BL    BUY140               NO                                          
*                                                                               
*================================================                               
* POL BUY - SET PRODUCT/PIGGYBACK/COST OVERRIDE                                 
*================================================                               
         CLI   RLEN,RPALLOC-RCODE  TEST UNALLOCATED                             
         BNH   BUY110                YES, SKIP THE SPOT                         
*                                                                               
         MVC   T#.ORDACOST,ORDTCOST SET BUYLINE COST                            
         TM    RSTATUS,RSRATOVQ     HAVE COST OVERRIDE?                         
         BZ    BUY130                                                           
         MVC   T#.ORDACOST,RPCOST                                               
*                                                                               
BUY130   MVC   T#.ORDAPRD,RPPRD    MOVE PRD CODE                                
*                                                                               
         MVI   T#.ORDAPRD2,0       INIT PIGGYBACK                               
         CLI   RLEN,RLPOL1LQ       TEST PIGGYBACK                               
         BNH   BUY140               NO, PIGGYBACK                               
         MVC   T#.ORDAPRD2,14(R2)   YES, MOVE PARTNER PRD CODE                  
*                                                                               
*======================================                                         
* GET PRODUCT CODES IN ALPHA SEQUENCE                                           
*======================================                                         
         MVC   DUB(1),T#.ORDAPRD                                                
         LA    R1,DUB                                                           
         BAS   RE,FINDPRD                                                       
         MVC   DUB+4(1),T#.ORDAPRD2                                             
         LA    R1,DUB+4                                                         
         BAS   RE,FINDPRD                                                       
         CLC   DUB+1(3),DUB+5      COMPARE ALPHA CODES                          
         BL    BUY140               ** AND SWITCH IF NEEDED                     
         MVC   HALF,T#.ORDAPRD                                                  
         MVC   T#.ORDAPRD,HALF+1   PRD1                                         
         MVC   T#.ORDAPRD2,HALF    PRD2                                         
*                                                                               
*=================================                                              
* CALCULATE THE FLIGHT #, IF ANY                                                
*=================================                                              
BUY140   MVI   T#.ORDAFLT,0        INIT FLIGHT                                  
*                                                                               
         L     R4,ORDAFTAB                                                      
         OC    0(16*4+2,R4),0(R4)  HAVE FLIGHTS?                                
         BZ    BUY200               NO                                          
*                                                                               
         MVI   T#.ORDAFLT,C'0'    SET 0 FLIGHT AS C'0'                          
         CLC   RDATE,0(R4)        BEFORE FLIGHT 0?                              
         BNH   BUY200                                                           
*                                                                               
         LA    R0,16               16 FLIGHT ENTRIES                            
         LA    R4,2(R4)                                                         
BUY150   OC    0(4,R4),0(R4)       ANY MORE FLT ENTRIES?                        
         BZ    BUY110               NO, SKIP THE SPOT                           
*                                                                               
         CLC   RDATE,0(R4)         BEFORE FLT'S START DATE?                     
         BL    BUY110               YES, SKIP THE SPOT                          
         CLC   RDATE,2(R4)         BEFORE FLT'S END DATE?                       
         BNH   BUY160                 YES, FOUND FLIGHT MATCH                   
         LA    R4,4(R4)            BUMP TO NEXT FLIGHT                          
         BCT   R0,BUY150                                                        
         B     BUY110              NO MATCH ON 16 FLTS, SKIP SPOTS              
*                                                                               
BUY160   L     RF,ORDAFTAB                                                      
         LAY   RF,-2(RF)                                                        
         SR    R4,RF               GIVES DISPLACEMENT IN FLIGHT TABLE           
         SRL   R4,2                GIVES FLIGHT                                 
         STC   R4,T#.ORDAFLT                                                    
*                                                                               
*==============================================                                 
* CALCULATE THE WEEK OFFSET AND BUMP #OFSPOTS                                   
*==============================================                                 
*                                                                               
BUY200   XC    T#.ORDASCHD,T#.ORDASCHD CLEAR SHEDULE DATA AREA                  
*                                                                               
         L     R4,ORDAETAB                                                      
         CLC   RDATE,0(R4)         BEFORE EST-START?                            
         BL    BUY110               YES, SKIP AND PROCESS NEXT SPOT             
*                                                                               
BUY210   CLI   2(R4),0             TEST END OF DATE LIST                        
         JE    *+2                  SHOULDN'T GET HERE, SOMETHING WRONG         
*                                                                               
         CLC   RDATE,2(R4)         COMPARE TO NEXT TABLE DATE                   
         BL    BUY220              IF LOW, POST HERE                            
*                                                                               
         CLI   4(R4),0             ONLY HAVE 2 MORE DATES?                      
         BE    BUY215               YES, THEN LAST DATE IS EST-END              
         LA    R4,2(R4)             NO, NEXT TABLE ENTRY                        
         B     BUY210                                                           
*                                                                               
BUY215   CLC   RDATE,2(R4)          IS SPOT PAST EST-END?                       
         BH    BUY110                YES, SKIP AND PROCESS NEXT SPOT            
*                                                                               
BUY220   L     RF,ORDAETAB                                                      
         SR    R4,RF               R4=DISPLACEMENT INTO TABLE                   
         LLH   RE,T#.ORDASCHD(R4)  GET HALFWORD SPOT COUNT                      
         LA    RE,1(RE)            AND BUMP                                     
         STH   RE,T#.ORDASCHD(R4)  AND PUT HALFWORD BACK                        
         DROP  R2                                                               
*                                                                               
*======================================================                         
* FIND MATCH ENTRY IN USER/WORK TABLE AND MERGE DATATAB                         
* OTHERWISE, IF NO MATCH, ADD TO END OF USER/WORK TABLE                         
*======================================================                         
         MVI   ORDAERR,0           RESET 'NO-ACTIVITY' FLAG                     
*                                                                               
         LA    R5,ORDADATA         POINT TO TABLE                               
         LH    R0,ORDALEN          TABLE LENGTH                                 
         CLI   ORDAACTN,ORDACHG    TEST 'CHANGE' POSTING                        
         BNE   BUY230                                                           
         LA    R5,WORKTAB                                                       
         LAY   R0,WORKTABX-WORKTAB                                              
*                                                                               
BUY230   AR    R0,R5               POINT TO END OF TABLE                        
         BCTR  R0,0                                                             
*                                                                               
BUY240   CLI   0(R5),0             TEST LAST ENTRY                              
         BE    BUY270                                                           
         CLC   0(ORDADLNQ,R5),T#.ORDADATA  MATCH ENTRY?                         
         BE    BUY250              A                                            
         LA    R5,ORDADLN2(R5)     NEXT ENTRY                                   
         CR    R5,R0               TEST PAST TABLE END                          
         BL    BUY240                                                           
         MVI   ORDAERR,ORDATABZ    SET 'TABLE TOO SMALL'                        
         B     EXIT                                                             
*                                                                               
* FOR 53 WEEK/DAY SCHEDULE, WORKING BACKWARDS (FROM END TO START)               
* ADDING THE # OF SPOTS IN THE 'TO' TABLE TO THE 'FROM' TABLE                   
*                                                                               
BUY250   LA    R3,53               53 WEEKS                                     
         LA    RE,ORDADLNQ-2(R5)   RE=A('TO' TABLE)                             
         LA    RF,T#.ORDASCHD-2    RF=A('FROM' ENTRY)                           
         SR    R6,R6                                                            
         SR    R7,R7                                                            
*                                                                               
BUY260   SLL   R3,1                *2 (ADJUST FOR 2-BYTE SPOT COUNT)            
         LLH   R6,0(R3,RF)         GET # SPOTS 'FROM' TABLE                     
         LLH   R7,0(R3,RE)         GET # SPOTS 'TO' TABLE                       
         AR    R6,R7               ADD THEM TOGETHER                            
         STH   R6,0(R3,RE)         PUT # SPOTS 'TO' TABLE                       
         SRL   R3,1                /2 (RE-ADJUST BACK FOR BCT)                  
         BCT   R3,BUY260           DO THIS 53 TIMES?                            
         B     BUY110               YES, PROCESS NEXT SPOT                      
*                                                                               
BUY270   MVC   0(ORDADLN2,R5),T#.ORDADATA  MOVE INTO TABLE                      
         B     BUY110              PROCESS NEXT SPOT                            
*                                                                               
*=================================                                              
* FINISHED BUY RECORD PROCESSING                                                
*=================================                                              
*                                                                               
BUY280   CLI   ORDAACTN,ORDACHG    TEST PROCESSING 'CHANGE' REC                 
         BE    BUY400                                                           
         L     RF,ORDAATRK             FOR COPY/ADD RECORD PROCESSING,          
         MVC   0(ORDTLNQ,RF),FLDTRACK  MOVE TRACK FIELDS TO ORDA                
         B     EXIT                                                             
*                                                                               
*==============================================================                 
* FOR CHANGE RECORD PROCESSING, MERGE WORKTAB WITH USER TABLE                   
*==============================================================                 
BUY400   LA    R6,WORKTAB          R6=A(WORKTAB)                                
*                                                                               
*==============================                                                 
* FIND TABLE ENTRY OR ADD NEW                                                   
*==============================                                                 
BUY405   LA    R5,ORDADATA         R5=A(USER TABLE)                             
         LH    R0,ORDALEN                                                       
         AR    R0,R5                                                            
         BCTR  R0,0                POINT TO END OF TABLE                        
*                                                                               
BUY410   CLI   0(R5),0             TEST LAST ENTRY                              
         BE    BUY420                                                           
         CLC   0(ORDADLNQ,R5),0(R6) MATCH ENTRY?                                
         BE    BUY420                                                           
         LA    R5,ORDADLN2(R5)                                                  
         CR    R5,R0               TEST PAST TABLE END                          
         BL    BUY410                                                           
         MVI   ORDAERR,ORDATABZ                                                 
         B     BUY490                                                           
*                                                                               
BUY420   MVC   0(ORDADLNQ,R5),0(R6)  MOVE IN ENTRY                              
*                                                                               
         L     RF,ORDAATRK                                                      
         OC    0(ORDTLNQ,RF),0(RF) HAVE FIELDS TRACKED?                         
         BZ    BUY430                NO                                         
         CLC   FLDTRACK,0(RF)      HAVE TRACKED FIELDS CHANGED?                 
         BE    BUY440                NO                                         
*                                                                               
BUY430   OC    ORDADLNQ(L'ORDASCHD,R5),ORDADLNQ(R6)  FORCE ALL ON               
         B     BUY450                                                           
*                                                                               
BUY440   DS    0H                  LEAVE ONLY NEW/DELETED ACTIVITY              
         XC    ORDADLNQ(L'ORDASCHD,R5),ORDADLNQ(R6)                             
         OC    ORDADLNQ(L'ORDASCHD,R5),ORDADLNQ(R5)                             
         BZ    BUY450                                                           
         MVI   ORDAERR,0           RESET 'NO-ACTIVITY' FLAG                     
*                                                                               
BUY450   LA    R6,ORDADLN2(R6)                                                  
BUY460   CLI   0(R6),0                                                          
         BNE   BUY405                                                           
*                                                                               
*===================================                                            
* TEST FOR ACTIVE ENTRIES IN TABLE                                              
*===================================                                            
         MVI   ORDAERR,0           CLEAR ERROR FLAG                             
*                                                                               
         LA    R5,ORDADATA                                                      
         LH    R0,ORDALEN                                                       
         AR    R0,R5                                                            
         BCTR  R0,0                                                             
*                                                                               
BUY470   CLI   0(R5),0             TEST LAST ENTRY                              
         BE    BUY480                                                           
         OC    ORDADLNQ(L'ORDASCHD,R5),ORDADLNQ(R5) TEST ACTIVE ENTRY           
         BNZ   BUY490                                                           
         LA    R5,ORDADLN2(R5)                                                  
         CR    R5,R0                                                            
         BL    BUY470                                                           
*                                                                               
BUY480   MVI   ORDAERR,ORDANOAC                                                 
*                                                                               
BUY490   L     RF,ORDAATRK                                                      
         XC    0(ORDTLNQ,RF),0(RF)   CLEAR TRACKED FIELD                        
         B     EXIT                                                             
*                                                                               
*==================                                                             
* FIND ALPHA PROD                                                               
*==================                                                             
FINDPRD  L     RF,ORDAAPRD         GET PRODUCT LIST ADDRESS                     
*                                                                               
FINDPRD2 CLC   0(1,R1),3(RF)                                                    
         BE    FINDPRD4                                                         
         LA    RF,4(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   FINDPRD2                                                         
         LA    RF,=C'***'                                                       
*                                                                               
FINDPRD4 MVC   1(3,R1),0(RF)                                                    
         BR    RE                                                               
*                                                                               
*=========                                                                      
* NEXTEL                                                                        
*=========                                                                      
         EJECT                                                                  
NEXTEL   SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
*                                                                               
NEXTEL2  CLI   0(R2),0                                                          
         BE    NEXTELX                                                          
         CLC   0(1,R2),ELCODE                                                   
         BER   RE                                                               
         CLI   0(R2),X'F1'         INSERT BEFORE ACTIVITY ELEM                  
         BNE   NEXTEL                                                           
*                                                                               
NEXTELX  LTR   RE,RE               SET CC NOT EQ                                
         BR    RE                                                               
*                                                                               
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
ELCODE   DS    X                                                                
*                                                                               
ORDTYPE  DS    C                                                                
ORDCASH  EQU   C'C'                                                             
ORDTRADE EQU   C'T'                                                             
*                                                                               
         DS    0A                                                               
SVPARS   DS    0XL8                                                             
SVTABADR DS    A                                                                
SVRECADR DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
*                                                                               
FLDTRACK DS    XL(ORDTLNQ)         TRACKED BUY FIELDS                           
*                                                                               
         DS    0D                                                               
         DC    C'**DATA**'                                                      
DATATAB  DS    CL(ORDADLN2)                                                     
*                                                                               
         DS    0D                                                               
         DC    C'*WORKTAB'                                                      
WORKTAB  DS    XL(WORKTABMAX*ORDADLN2)                                          
WORKTABX DS    0C                                                               
*                                                                               
WORKTABMAX EQU 100                 WORKTAB 100 MAX ENTRIES                      
*                                                                               
WORKX    EQU   *                                                                
         SPACE                                                                  
*                                                                               
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         SPACE 2                                                                
       ++INCLUDE SPORDATABD                                                     
         SPACE 2                                                                
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002SPORDAMNT 10/23/19'                                      
         END                                                                    
