*          DATA SET MPSRFCOMP  AT LEVEL 019 AS OF 05/01/02                      
*CATALP SRFCOMP                                                                 
         TITLE 'MEDIA PLANNING - SINGLE VEHICLE REACH AND FREQ'                 
         PRINT NOGEN                                                            
SRFCOMP  CSECT                                                                  
         NMOD1 10,**SRF                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,0(R1)                                                         
         USING SRFD,RA                                                          
*                                                                               
         L     RF,SRFPF1           SET PRECISION FACTOR (TEST ONLY)             
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         L     RF,=F'100000'                                                    
         ST    RF,PF1                                                           
*                                                                               
         L     RF,SRFPF2           SET PRECISION FACTOR (TEST ONLY)             
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         LA    RF,1                                                             
         ST    RF,PF2                                                           
*                                                                               
         MVC   WRFC1(12),SRFC1     C1,C2,POP IN WORK                            
         CLC   PF2,=F'1'                                                        
         BE    PR4                                                              
         L     R1,SRFC1                                                         
         M     R0,PF2                                                           
         ST    R1,WRFC1                                                         
         L     R1,SRFC2                                                         
         M     R0,PF2                                                           
         ST    R1,WRFC2                                                         
         L     R1,SRFPOP                                                        
         M     R0,PF2                                                           
         ST    R1,WRFPOP                                                        
*                                  CALCULATE K                                  
PR4      DS    0H                                                               
         L     R1,WRFC1                                                         
         MR    R0,R1                                                            
         L     RF,WRFPOP                                                        
         BAS   RE,DIV                                                           
*                                                                               
         L     RF,WRFC1                                                         
         SLL   RF,1                                                             
         S     RF,WRFC2                                                         
         SR    RF,R1                                                            
*                                                                               
         L     R1,WRFC2                                                         
         S     R1,WRFC1                                                         
         M     R0,PF1                                                           
         BAS   RE,DIV                                                           
         ST    R1,K                                                             
*                                  CALCULATE A                                  
         M     R0,WRFC1                                                         
         L     RF,WRFPOP                                                        
         BAS   RE,DIV                                                           
         ST    R1,A                                                             
*                                  CALCULATE B                                  
         S     R1,K                                                             
         LCR   R1,R1                                                            
         ST    R1,B                                                             
         EJECT                                                                  
*                                  REACH COMPUTATION                            
         SPACE 2                                                                
RCHCOMP  DS    0H                                                               
         L     R8,SRFRT            START OF REACH TABLE                         
         MVC   0(8,R8),WRFC1       SET C1 AND C2                                
         LA    R8,8(R8)            POINT TO C3 SLOT                             
         L     R3,SRFISS           COUNT                                        
         SH    R3,=H'2'                                                         
         BNP   RCH20               DONE IF COUNT LESS THAN 3                    
*                                                                               
         L     R7,WRFC2            START FOR CUME                               
*                                                                               
         L     R1,WRFC2            C2                                           
         S     R1,WRFC1            MINUS C1 = INITITIAL INCREMENT               
*                                                                               
         L     R5,B                R5 USED FOR (B+T-2)                          
         A     R5,PF1                                                           
*                                                                               
         L     RF,K                RF USED FOR (K+T-1)                          
         A     RF,PF1                                                           
         A     RF,PF1                                                           
*                                                                               
RCH4     DS    0H                                                               
         MR    R0,R5               INC X (B+T-2)                                
         BAS   RE,DIV              / (K+T-1)                                    
*                                                                               
         AR    R7,R1               ADD NEW INC TO PREV CUME                     
         ST    R7,0(R8)            PLACE IN OUTPUT                              
         LA    R8,4(R8)            NEXT OUTPUT SLOT                             
         A     R5,PF1              BUMP (B+T-2)                                 
         A     RF,PF1              BUMP (K+T-1)                                 
*                                                                               
         BCT   R3,RCH4             NEXT COUNT                                   
*                                                                               
RCH20    DS    0H                  DONE WITH REACH COMP                         
         EJECT                                                                  
*        FREQUENCY DISTRIBUTION COMPUTATION                                     
         SPACE 2                                                                
FQCOMP   DS    0H                                                               
         OC    SRFFT,SRFFT         TEST FREQ DIST WANTED                        
         BZ    FQ20                NO                                           
*                                                                               
         SH    R8,=H'8'            BACK UP TO C(N-1)                            
         L     R1,4(R8)            C(N)                                         
         S     R1,0(R8)                                                         
         M     R0,SRFISS           R1= N(C(N) - C(N-1))                         
*                                  = READ EXACTLY 1 = F(1)                      
*                                                                               
         L     R0,WRFPOP           POPULATION                                   
         S     R0,4(R8)            LESS C(N) = READ ZERO = F(0)                 
         L     R8,SRFFT            POINT TO OUTPUT AREA                         
         STM   R0,R1,0(R8)         SET F(0) AND F(1)                            
         LA    R8,8(R8)            POINT TO F(2) SLOT                           
*                                                                               
         L     R3,SRFISS           R3 FOR (N-T+1)                               
         BCTR  R3,R0                                                            
*                                                                               
         LA    R4,2                R4 FOR T                                     
*                                                                               
         L     R5,A                R5 FOR (A+T-1)                               
         A     R5,PF1                                                           
*                                                                               
         L     R7,PF1                                                           
         LR    R6,R3                                                            
         BCTR  R6,R0               ISSUES MINUS 2                               
         MR    R6,R6                                                            
         L     R6,B                                                             
         AR    R6,R7               R6 FOR (B+N-T)                               
*                                                                               
FQ4      DS    0H                                                               
         MR    R0,R3               F(T-1) X (N-T+1)                             
         MR    R0,R5               X (A+T-1)                                    
*                                                                               
         LR    RF,R6               / (B+N-T)                                    
         BAS   RE,DIV                                                           
         SR    R0,R0                                                            
         LR    RF,R4               / T                                          
         BAS   RE,DIV                                                           
*                                                                               
         ST    R1,0(R8)            PLACE IN OUTPUT                              
         LA    R8,4(R8)            NEXT OUTPUT SLOT                             
         LA    R4,1(R4)            BUMP T                                       
         A     R5,PF1              BUMP (A+T-1)                                 
         S     R6,PF1              DECR (B+N-T)                                 
         BCT   R3,FQ4              DECR (N-T+1) - DONE WHEN ZERO                
*                                                                               
FQ20     DS    0H                                                               
         EJECT                                                                  
*        END PROCEDURES                                                         
         SPACE 2                                                                
EPROC    DS    0H                                                               
         CLC   PF2,=F'1'                                                        
         BE    EP4                                                              
*                                                                               
         L     R3,SRFRT            ADJUST REACH TABLE                           
         L     R4,SRFISS                                                        
*                                                                               
EP2      DS    0H                                                               
         L     R1,0(R3)                                                         
         SR    R0,R0                                                            
         D     R0,PF2                                                           
         ST    R1,0(R3)                                                         
*                                                                               
         LA    R3,4(R3)                                                         
         BCT   R4,EP2                                                           
*                                                                               
         L     R3,SRFFT            AND FREQ TABLE                               
         L     R4,SRFISS                                                        
*                                                                               
EP3      DS    0H                                                               
         L     R1,0(R3)                                                         
         SR    R0,R0                                                            
         D     R0,PF2                                                           
         ST    R1,0(R3)                                                         
*                                                                               
         LA    R3,4(R3)                                                         
         BCT   R4,EP3                                                           
*                                                                               
EP4      DS    0H                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
DIV      DS    0H                                                               
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         SR    R1,R1                                                            
         BR    RE                                                               
*                                                                               
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT WORK AREA DSECT                                                  
K        DS    F                                                                
A        DS    F                                                                
B        DS    F                                                                
PF1      DS    F                                                                
PF2      DS    F                                                                
WRFC1    DS    F                                                                
WRFC2    DS    F                                                                
WRFPOP   DS    F                                                                
*                                                                               
         SPACE 3                                                                
       ++INCLUDE MPSRFD                                                         
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019MPSRFCOMP 05/01/02'                                      
         END                                                                    
