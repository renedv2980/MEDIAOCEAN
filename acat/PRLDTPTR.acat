*          DATA SET PRLDTPTR   AT LEVEL 024 AS OF 09/24/14                      
*CATALP PRLDTPTR                                                                
         TITLE 'LDTPTR - PRT - ACTIVE/PASSIVE POINTER TEST'                     
*                                                                               
*        PARAMS VIA R1                                                          
*        XL1   X'00'     RETURN  X'00'=ACTIVE  OR  X'FF'=PASSIVE                
*        AL3   A(RECORD)                                                        
*        XL1   FILE NUM  RETURN  X'FF'=SKIP KEY REQUIRED                        
*        AL3   A(SKIP KEY)                                                      
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*  TZIH  09/24/14 KEYS STARTING WITH X'F5'-X'F9' ARE AGENCY CODES, NOT          
*                 PASSIVE POINTERS.                                             
*                                                                               
*  KWAN  10/19/11 ADD INVOICE CREATION DATE PASSIVE COUNTER                     
*                                                                               
*  KWAN  01/11/11 CORRECT PARAMETER CHECK (PLIST+4) FOR X'41'                   
*                                                                               
*  RCRI  03/02/10 TEST PUBDIR PASSED IN PLIST+4                                 
*                                                                               
*  SMYE  04/25/07 X'A8' BUY PASSIVE POINTER FOR VARIOUS BUY DATES               
*                                                                               
*  KWAN  09/21/05 ADDED X'A0' PASSIVE FOR MAT= COMMENTS REPEAT                  
*                                                                               
*  SMYE  07/26/05 ADDED TWO ESR RECORD (X'75') PASSIVE POINTERS                 
*                 (X'B5' & X'BD') AND TWO SETUP RECORD (X'72') PASSIVE          
*                 POINTERS (X'6202' & X'6203')                                  
*                                                                               
*  SMYE  05/10/05 ADDED JOB (ADCODE) RECORD "AD-ID ONLY" PASSIVE X'C2'          
*                                                                               
*  SMYE  01/24/05 ADDED JOB (ADCODE) RECORD AD-ID PASSIVE X'C1'                 
*                                                                               
*  SMYE  06/29/04 ADDED NEW WEBIO RECORD PASSIVES (B4,BE)                       
*                                                                               
*  SMYE  12/01/03 ADDED CUSTOM COLUMN RECORD PASSIVE X'D1'                      
*                                                                               
*  SMYE  07/17/03 ADDED NEW INVOICE RECORD PASSIVES (B1,B2,B3,BF)               
*                                                                               
*  SMYE  02/01/01 ADDED PASSIVE BUY SERIAL # POINTER X'99'                      
*                                                                               
*  SMYE  12/26/00 ADDED PASSIVE PRODUCT POINTERS X'A3' & X'A4'                  
*                                                                               
         PRINT NOGEN                                                            
LDTPTR   CSECT                                                                  
         NMOD1 0,PRLDTPTR                                                       
         SPACE 2                                                                
         L     R2,0(R1)            R2 = A(POINTER)                              
         USING PBUYREC,(R2)                                                     
         MVI   0(R1),0                                                          
*                                                                               
LDTP1    CLI   4(R1),X'41'         TEST PUBDIR PASSED                           
         BNE   LDTP2                                                            
         TM    25(R2),X'01'        CONTROL BYTE HAS ACTIVE/PASSIVE              
         BO    PASSIV                                                           
         B     ACTIV                                                            
*                                                                               
LDTP2    DS    0H                                                               
         CLI   PBUYKEY,X'F5'       KEYS F5-F9 ARE AGENCY CODES                  
         BL    *+12                                                             
         CLI   PBUYKEY,X'F9'                                                    
         BNH   ACTIV                                                            
         CLI   PBUYKEY,X'F1'       ALL F1+'S (IN FIRST BYTE) ARE PSSV           
         BNL   PASSIV              YES - PASSIVE                                
         CLI   PBUYKEY+3,X'88'     TEST NEW MANUAL BILL POINTER                 
         BE    PASSIV              YES - PASSIVE                                
         CLI   PBUYKEY+3,X'99'     TEST BUY SERIAL # PASSIVE POINTER            
         BE    PASSIV              YES - PASSIVE                                
         CLI   PBUYKEY+3,X'21'     TEST CLT/PUB POINTER                         
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'23'     TEST INSERTION ORDER POINTER                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A0'     TEST MAT= COMMENT REPEAT PASSIVE             
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A1'     TEST TRAF OFF/CLT PASSIVE POINTER            
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A2'     TEST OFF/CLT PASSIVE POINTER                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A3'     TEST OFF/PRD PASSIVE POINTER                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A4'     TEST TRA/PRD PASSIVE POINTER                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A6'     TEST ADJ/PRD PASSIVE POINTER                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'A8'     TEST BUY REC DATE(S) PASSIVE                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B1'     TEST INVOICE CLT/PUB/SER# PASSIVE            
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B2'     TEST INVOICE PERIOD-BASED PASSIVE            
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B3'     TEST INVOICE INVOICE NUMBER PASSIVE          
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'BF'     TEST INVOICE SERIAL NUMBER PASSIVE           
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B4'     TEST WEBIO PASSIVE BY PERIOD                 
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'BE'     TEST WEBIO SERIAL NUMBER PASSIVE             
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B5'     TEST ESR REC PASSIVE BY PERIOD               
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'B6'     TEST INVOICE CREATION DATE PASSIVE           
         BE    PASSIV              YES PASSIVE (PNV6RCDQ)                       
         CLI   PBUYKEY+3,X'BD'     TEST ESR REC SERIAL NUMBER PASSIVE           
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'C1'     TEST JOB RECORD AD ID PASSIVE                
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'C2'     TEST JOB RECORD "AD ID ONLY" PASSIVE         
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYKEY+3,X'D1'     TEST CUSTOM COLUMN SEQUENCE# PASSIVE         
         BE    PASSIV              YES PASSIVE                                  
         CLC   =X'6202',PBUYKEY+3  TEST SETUP REC EIO PASSIVE POINTER           
         BE    PASSIV              YES PASSIVE                                  
         CLC   =X'6203',PBUYKEY+3  TEST SETUP REC ESR PASSIVE POINTER           
         BE    PASSIV              YES PASSIVE                                  
         CLI   PBUYREC+25,X'FF'    TEST FF DELETE                               
         BE    PASSIV                                                           
         CLI   PBUYKEY+3,X'5A'     STANDARD SPACE RECORD                        
         BE    PASSIV              DIR ONLY - TREAT AS PASSIVE                  
         CLI   PBUYREC+3,X'3A'     TEST FOR DIRECTORY ONLY                      
         BL    LDTP5               GROUP RECORDS X'3A' - X'3F'                  
         CLI   PBUYREC+3,X'3F'                                                  
         BH    LDTP5                                                            
         B     PASSIV              TREAT AS PASSIVE                             
*                                                                               
LDTP5    DS    0H                                                               
         CLI   PBUYKEY+3,X'20'                                                  
         BNE   ACTIV                                                            
         OC    PBUYKACT,PBUYKACT   TEST ACTIVE                                  
         BZ    ACTIV                                                            
         CLI   PBUYREC,X'FF'       TRAILER POINTER                              
         BE    ACTIV                                                            
PASSIV   EQU   *                                                                
         MVI   0(R1),X'FF'         PASSIVE                                      
ACTIV    EQU   *                                                                
         SPACE 2                                                                
         XMOD1 1                                                                
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
         EJECT                                                                  
*                   BUY REC DSECT                                               
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024PRLDTPTR  09/24/14'                                      
         END                                                                    
