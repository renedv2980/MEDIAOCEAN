*          DATA SET ACGETCAP   AT LEVEL 014 AS OF 07/07/20                      
*CATALP GETCAP                                                                  
         TITLE 'GETCAP - COST ALLOCATION PROFILE HANDLER'                       
GETCAP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CODX-COD,*GETCAP*,RA,RR=R2,CLEAR=YES                             
         USING COD,RC                                                           
         L     R9,0(R1)            PARAMETER 1 =A(COBLOCK)                      
         USING COBLOCK,R9                                                       
*                                                                               
         ST    R2,RELO             INITIALIZE                                   
         L     R2,COABUFF                                                       
         A     R2,COLBUFF                                                       
         BCTR  R2,0                                                             
         ST    R2,AENDBUFF          END OF OPTIMIZATION BUFFER                  
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVC   BINSRCH,COABINSR    GET A(BINSRCH) FROM BLOCK FIRST              
         OC    BINSRCH,BINSRCH     TEST IF PASSED                               
         BNZ   MAIN                YES                                          
         L     R2,=V(BINSRCH)                                                   
         LTR   R2,R2                                                            
         BZ    *+8                                                              
         A     R2,RELO                                                          
         ST    R2,BINSRCH                                                       
         EJECT                                                                  
*************************************************************                   
*              MAIN LOGIC CONTROL                                               
*************************************************************                   
*                                                                               
*                                                                               
MAIN     LA    RE,COPTIONS         PRECLEAR USERS OPTIONS AREA                  
         LA    RF,COPTIONL                                                      
         XCEFL                                                                  
*                                                                               
         MVC   COFLAG,=C'**COBL**' DUMP IDENTIFIER                              
         MVI   ANYIO,C'N'          SET I/O CHECKER                              
         EJECT                                                                  
*************************************************************                   
*              CHECK IF REQUIRED DATA HAS BEEN PASSED BY USER                   
*************************************************************                   
*                                                                               
*                                                                               
OKBLOK   DS    0H                                                               
         XC    COSTATUS,COSTATUS   CLEAR COBLOCK STATUS                         
         OC    COADM,COADM         DID USER PASS ADDR OF DATAMGR                
         BNZ   *+8                                                              
         OI    COSTATUS,CODATMIS   SET DATMGR ADDR MISSING                      
*                                                                               
         OC    COACOVL,COACOVL     ADDR OF COVAIL PASSED?                       
         BZ    OKBL02              NO -                                         
         OC    COABINSR,COABINSR   ADDR OF BINSRCH MUST ALSO BE PASSED          
         BNZ   OKBL02                                                           
         OI    COSTATUS,COBINMIS   SET COVAIL/BINSR MISSING                     
OKBL02   OC    COABUFF,COABUFF     OPTIMIZATION BUFFER PASSED?                  
         BZ    OKBL03              NO -                                         
         OC    COLBUFF,COLBUFF     L'OPTIMIZATION BUFFER ALSO PASSED?           
         BNZ   OKBL03                                                           
         OI    COSTATUS,COBUFERR   OPTIMIZATION BUFFER ERROR                    
*                                                                               
OKBL03   CLC   COKCPY,SPACES       COMPANY AND METHOD ARE REQUIRED              
         BNH   OKBL99                                                           
         CLC   COKMTHD,SPACES      (A BLANK METHOD IS ACCEPTABLE)               
         BL    OKBL99                                                           
*                                                                               
         CLC   COKOFCG,SPACES      IS OFFICE GROUP PRESENT?                     
         BNH   OKBL04              NO - CONTINUE                                
*                                  YES- OTHER KEY FIELDS MUST BE BLANK          
         CLC   COKOFC(L'COKOFC+L'COKDPT+L'COKSDT+L'COKPER),SPACES               
         BH    OKBL99                                                           
         B     OKBLXX                                                           
*                                                                               
OKBL04   CLC   COKOFC,SPACES       IS OFFICE PRESENT?                           
         BH    OKBL06              YES- CONTINUE                                
*                                  NO - OTHER KEY FIELDS MUST BE BLANK          
         CLC   COKDPT(L'COKDPT+L'COKSDT+L'COKPER),SPACES                        
         BH    OKBL99                                                           
         B     OKBLXX                                                           
*                                                                               
*                                                                               
OKBL06   CLC   COKDPT,SPACES       IS DEPT PRESENT?                             
         BH    OKBL08              YES - CONTINUE                               
         CLC   COKSDT(L'COKSDT+L'COKPER),SPACES                                 
         BH    OKBL99                                                           
         B     OKBLXX                                                           
*                                                                               
OKBL08   CLC   COKSDT,SPACES       IS SUB DEPT PRESENT?                         
         BH    OKBLXX              YES - ALL IS WELL                            
         CLC   COKPER,SPACES                                                    
         BNH   OKBLXX                                                           
*                                                                               
OKBL99   OI    COSTATUS,COLEVMIS   LEVELS OF KEY MISSING                        
*                                                                               
OKBLXX   OC    COSTATUS,COSTATUS                                                
         BNZ   MAINEND             ERROR STATUS SET TO INFORM USER              
         EJECT                                                                  
*************************************************************                   
*              BUILD OPTION KEY BUFFER                                          
*************************************************************                   
*                                                                               
*                                                                               
OKSET    DS    0H                                                               
         CLI   OKSTAT,0            ONLY NEED TO DO THIS ONCE                    
         BNE   OKSETX                                                           
         MVI   OKSTAT,2            2 MEANS BUFFER NOT AVAILABLE                 
         MVC   COVAIL,COACOVL      GET A(COVAIL) FROM BLOCK FIRST               
         OC    COVAIL,COVAIL       TEST IF COVAIL PASSED                        
         BZ    OKSETX              NO - DONT ATTEMPT THE CALL                   
         OC    AOKBUFF,AOKBUFF     DID WE ALREADY ACQUIRE THE BUFFER            
         BZ    OKSET0                                                           
         L     R2,AOKBUFF                                                       
         L     R3,LOKBUFF                                                       
         LTR   R3,R3                                                            
         BP    OKSET1                                                           
         DC    H'0'                DIE FOR NOW                                  
*                                                                               
OKSET0   OC    BINSRCH,BINSRCH     HAVE BINSRCH TO READ IT                      
         BZ    OKSETX                                                           
         GOTO1 COVAIL,DMCB,C'LOOK'                                              
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ERROR                               
         BNZ   *+6                 YES-DIE FOR NOW                              
         DC    H'0'                                                             
         S     R3,=F'1000000'      LEAVE AT LEAST A MEG FOR OTHERS              
*        BM    OKSETX                                                           
         BNM   *+6                 DIE FOR NOW                                  
         DC    H'0'                                                             
         L     R2,MINALLOC                                                      
         CR    R3,R2               TEST MINIMUM ALLOCATION AVAILABLE            
*        BL    OKSETX              YES                                          
         BNL   *+6                 DIE FOR NOW                                  
         DC    H'0'                                                             
*                                                                               
         GOTO1 COVAIL,DMCB,C'GET ',(R2),(R3)                                    
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ALLOCATION ERROR                    
*        BZ    OKSETX              YES                                          
         BNZ   *+6                 YES-DIE FOR NOW                              
         DC    H'0'                                                             
*                                                                               
OKSET1   ST    R2,AOKBUFF          BEGINNING OF KEY PORTION OF BUFFER           
         ST    R3,LOKBUFF          LENGTH OF ACQUIRED BUFFER                    
         MVI   OKSTAT,1            BUFFER ATTAINED                              
         SRL   R3,1                DIVIDE BUFFER BETWEEN KEYS/DATA              
         LA    R5,0(R3,R2)                                                      
         ST    R5,AOKDATA          BEGINNING OF DATA PORTION                    
         LA    RE,0(R3,R5)                                                      
         BCTR  RE,0                                                             
         ST    RE,AOKDATAX         SET A(END OF DATA BUFFER)                    
*                                                                               
         SR    R2,R2                                                            
         D     R2,=A(COVLNTH)      HOW MANY KEYS I CAN HOLD IN R3               
         BCTR  R3,0                                                             
         USING COVTABD,R2                                                       
         L     R2,AOKBUFF                                                       
         MVC   COVKEY(COVKLNTH),XFF   FIRST ENTRY IS HIGHEST KEY FOUND          
         XC    COVPOINT,COVPOINT      CLEAR POINTER TO RECORD AREA              
         LA    R2,COVLNTH(R2)                                                   
         SR    R4,R4                                                            
*                                  R3-MAX KEYS THAT WILL FIT                    
*                                  R4-COUNTER FOR NUMBER OF KEYS                
*                                  R2-KEY PORTION OF COVAIL BUFFER              
*                                  R5-DATA PORTION OF COVAIL BUFFER             
*                                                                               
         LA    R6,KEY              SET UP TO READ OPTIONS                       
         USING CAPRECD,R6                                                       
         MVC   CAPKEY,SPACES                                                    
         MVI   CAPKTYP,CAPKTYPQ    X'3E'                                        
         MVI   CAPKSUB,CAPKSUBQ    X'09'                                        
         MVC   CAPKCPY,COKCPY      COMPANY                                      
         MVC   CAPKMTHD,COKMTHD    METHOD                                       
         BAS   RE,HIGH                                                          
         B     *+8                                                              
OKSET2   BAS   RE,SEQ                                                           
         CLC   CAPKEY(CAPKMIN),KEYSAVE    SAME COMPANY AND METHOD?              
         BNE   OKSET6                     IF NOT WE ARE DONE                    
*                                                                               
         MVC   COVKEY(COVKLNTH),CAPKMTHD  SAVE KEY(MET/OFF/DPT/SUB/PER)         
         XC    COVPOINT,COVPOINT          CLEAR POINTER TO RECORD AREA          
         SR    RE,RE                                                            
         ICM   RE,3,CAPRLEN               GET RECORD LENGTH                     
         SH    RE,DATADISP                DEDUCT DISP TO FIRST ELEMENT          
         LA    RE,1(RE,R5)                + 2 FOR HEADER - 1 FOR EOR=1          
         C     RE,AOKDATAX                TEST ROOM FOR DATA                    
         BH    OKSET4                     NO ROOM FOR IT                        
*                                                                               
         STCM  R5,15,COVPOINT      SET POINTER TO DATA                          
         SR    RF,RF                                                            
         ICM   RF,3,CAPRLEN                                                     
         SH    RF,DATADISP                                                      
         LA    RF,1(RF)            RF=L'DATA BUFFER ENTRY                       
         STCM  RF,3,0(R5)          STORE LENGTH OF DATA ENTRY IN HEADER         
         LA    RE,2(R5)            BUMP PAST HEADER   RE=A(DESTINATION)         
         BCTR  RF,0                ADJUST FOR HEADER LENGTH                     
         BCTR  RF,0                                                             
         LR    R0,R6                                                            
         AH    R0,DATADISP         R0=A(SOURCE) DATA PORTION OF RECORD          
         LR    R1,RF               R1=SOURCE LENGTH=DESTINATION LENGTH          
         MVCL  RE,R0               ATTACH RECORD DATA TO HEADER                 
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,0(R5)          PUT LENGTH OF DATA RECORD IN RE              
         LA    R5,0(RE,R5)         BUMP R5 TO NEXT DATA AREA                    
*                                                                               
OKSET4   LA    R2,COVLNTH(R2)      BUMP R2 TO NEXT KEY ENTRY                    
         LA    R4,1(R4)            INCREMENT KEY COUNT                          
         CR    R4,R3               TEST IF KEY BUFFER FILLED                    
         BL    OKSET2              NO                                           
*                                                                               
OKSET5   S     R2,=A(COVLNTH)      BACK UP TO LAST ENTRY                        
         L     R1,AOKBUFF                                                       
         MVC   0(COVKLNTH,R1),0(R2) SET HIGHEST KEY IN BUFFER                   
*                                                                               
OKSET6   ST    R4,NOPTKEYS         SET N'OPTION KEYS                            
*                                                                               
OKSETX   DS    0H                                                               
         DROP  R2                                                               
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT DEFAULT LEVEL                                  
*************************************************************                   
*                                                                               
*                                                                               
DEFLEV   DS    0H                                                               
         MVI   FROM,COSLVDFT       "FROM" DEFAULT LEVEL                         
         LA    R6,DEFREC           ADDRESS A DUMMY DEFAULT RECORD               
         BAS   RE,APPLY                                                         
DEFLEVX  CLI   COSELLEV,COSLVDFT   IF ONLY ASKED FOR DEFAULT--DONE              
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT AGENCY LEVEL               *                   
*************************************************************                   
*                                                                               
AGLEV    DS    0H                                                               
         CLC   COKMTHD,SPACES      IS THIS ABOVE THE METHOD LEVEL?              
         BH    AGLXX               NO - FORGET IT                               
         MVI   FROM,COSLVAGY       MARK "FROM" AGENCY                           
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD                         
         BNE   AG3                 YES                                          
*        CLC   COKOFCG,MYOG        TEST IF OFFICE GROUP CHANGED                 
*        BNE   OG3                 YES                                          
*                                                                               
         OC    AAGYOPTS,AAGYOPTS   IS AGENCY RECORD SAVED?                      
         BZ    AG3                                                              
         L     R6,AAGYOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN+L'CAPKOFC),MYCPY                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     AGLXX                                                            
*                                                                               
AG3      BAS   RE,LOOKUP           LOOKUP RECORD (COVAIL BUFF OR FILE)          
         MVC   AAGYOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    AAGYOPTS+L'AAGYOPTS(AAGYOPLN),AAGYOPTS+L'AAGYOPTS                
*                                                                               
AGLXX    CLI   COSELLEV,COSLVAGY   TEST FOR AGENCY LEVEL REQUEST                
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT METHOD LEVEL                                   
*************************************************************                   
*                                                                               
*                                                                               
METHLEV  DS    0H                                                               
         CLC   COKMTHD,SPACES          DID USER PASS A METHOD?                  
         BNH   METLXX                  NO - FORGET IT                           
         MVI   FROM,COSLVMET          "FROM" METHOD LEVEL                       
         CLC   MYCPY(MYMIN),COKCPY     SAME COMP AND METHOD                     
         BNE   METL02                  NO - START AGAIN                         
         OC    AMETOPTS,AMETOPTS       IS OPTIONS RECORD SAVED?                 
         BZ    METL02                                                           
         L     R6,AMETOPTS                                                      
         CLC   CAPKCPY(MYMIN),MYCPY    MAKE SURE ITS THE METHOD PROFILE         
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY                PUT THESE TO GO BLOCK                    
         B     METLXX                  AND THIS LEVEL IS DONE                   
*                                                                               
METL02   MVC   MYSAV(MYSAVLN),SPACES      CLEAR MY SAVE AREA                    
         XC    BUFFNOW(BUFOPTLN),BUFFNOW  AND OPTIMIZATION ADDRESSES            
         OC    COABUFF,COABUFF         DID USER SUPPLY OPTIMI BUFFER            
         BZ    METL04                                                           
         MVC   BUFFNOW,COABUFF         SET CURRENT BUFFER ADDR TO               
*                                      CLEAR LOWER LEVELS                       
METL04   BAS   RE,LOOKUP                                                        
         MVC   AMETOPTS,AWRKADDR       ADDR WITHIN BUFFER                       
*                                      CLEAR LOWER LEVEL ADDRESSES              
         XC    AMETOPTS+L'AMETOPTS(AMETOPLN),AMETOPTS+L'AMETOPTS                
*                                                                               
METLXX   CLI   COSELLEV,COSLVMET       TEST FOR METHOD REQUEST                  
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT OFFICE GROUP LEVEL                             
*************************************************************                   
*                                                                               
*                                                                               
OGLEV    DS    0H                                                               
         CLC   COKOFCG,SPACES      DID USER PASS AN OFFICE GROUP                
         BNH   OGLXX               NO - FORGET IT                               
         MVI   FROM,COSLVOGR       MARK "FROM" OFFICE GROUP                     
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD                         
         BNE   OG3                 YES                                          
         CLC   COKOFCG,MYOG        TEST IF OFFICE GROUP CHANGED                 
         BNE   OG3                 YES                                          
*                                                                               
         OC    AOGROPTS,AOGROPTS   IS OFC GRP RECORD SAVED?                     
         BZ    OG3                                                              
         L     R6,AOGROPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN+L'CAPKOFC),MYCPY                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     OGLXX                                                            
*                                                                               
OG3      BAS   RE,LOOKUP           LOOKUP RECORD (COVAIL BUFF OR FILE)          
         MVC   AOGROPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    AOGROPTS+L'AOGROPTS(AOGROPLN),AOGROPTS+L'AOGROPTS                
*                                                                               
OGLXX    CLI   COSELLEV,COSLVOGR   TEST FOR OFFICE GROUP REQUEST                
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT OFFICE LEVEL                                   
*************************************************************                   
*                                                                               
*                                                                               
OFFLEV   DS    0H                                                               
         CLC   COKOFC,SPACES       DID USER PASS AN OFFICE                      
         BNH   OFFLXX                                                           
         MVI   FROM,COSLVOFF       "FROM" OFFICE LEVEL                          
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD?                        
         BNE   OF3                 NO -                                         
         CLC   MYOFC,COKOFC        SAME OFFICE CODE                             
         BNE   OF3                                                              
*                                                                               
         OC    AOFFOPTS,AOFFOPTS   DO WE HAVE A BUFFER ADDRESS?                 
         BZ    OF3                                                              
         L     R6,AOFFOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   CAPKOFC,MYOFC                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     OFFLXX                                                           
*                                                                               
OF3      MVC   MYDPT,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
         MVC   MYSDT,SPACES        LOWER LEVEL OPTIONS                          
         MVC   MYPER,SPACES                                                     
         BAS   RE,LOOKUP                                                        
         MVC   AOFFOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    AOFFOPTS+L'AOFFOPTS(AOFFOPLN),AOFFOPTS+L'AOFFOPTS                
*                                                                               
OFFLXX   CLI   COSELLEV,COSLVOFF   TEST FOR OFFICE REQUEST                      
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT DEPT LEVEL                                     
*************************************************************                   
*                                                                               
*                                                                               
DPTLEV   DS    0H                                                               
         CLC   COKDPT,SPACES       DID USER PASS A DEPARTMENT                   
         BNH   DPTLXX                                                           
         MVI   FROM,COSLVDPT       "FROM" DEPT LEVEL                            
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD?                        
         BNE   DP3                 NO -                                         
         CLC   MYOFC(L'MYOFC+L'MYDPT),COKOFC   SAME OFFICE/DEPT CODE            
         BNE   DP3                                                              
*                                                                               
         OC    ADEPOPTS,ADEPOPTS                                                
         BZ    DP3                                                              
         L     R6,ADEPOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT),CAPKOFC  SAME OFFICE/DEPT CODE            
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     DPTLXX                                                           
*                                                                               
DP3      MVC   MYSDT,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
         MVC   MYPER,SPACES        LOWER LEVEL OPTIONS                          
         BAS   RE,LOOKUP                                                        
         MVC   ADEPOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    ADEPOPTS+L'ADEPOPTS(ADEPOPLN),ADEPOPTS+L'ADEPOPTS                
*                                                                               
DPTLXX   CLI   COSELLEV,COSLVDPT                                                
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT SUBDEPT LEVEL                                  
*************************************************************                   
*                                                                               
*                                                                               
SDTLEV   DS    0H                                                               
         CLC   COKSDT,SPACES       DID USER PASS A SUB DEPARTMENT               
         BNH   SDTLXX                                                           
         MVI   FROM,COSLVSDT       "FROM" SUBDEPT LEVEL                         
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD?                        
         BNE   SD3                 NO -                                         
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT),COKOFC  SAME OFF/DPT/SUB          
         BNE   SD3                                                              
*                                                                               
         OC    ASDTOPTS,ASDTOPTS                                                
         BZ    SD3                                                              
         L     R6,ASDTOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT),CAPKOFC                           
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     SDTLXX                                                           
*                                                                               
SD3      MVC   MYPER,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
*                                  LOWER LEVEL OPTIONS                          
         BAS   RE,LOOKUP                                                        
         MVC   ASDTOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    ASDTOPTS+L'ASDTOPTS(ASDTOPLN),ASDTOPTS+L'ASDTOPTS                
*                                                                               
SDTLXX   CLI   COSELLEV,COSLVSDT   SUB DEPT                                     
         BE    MAINEND                                                          
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT PERSON LEVEL                                   
*************************************************************                   
*                                                                               
*                                                                               
PERLEV   DS    0H                                                               
         CLC   COKPER,SPACES       DID USER PASS A PERSON                       
         BNH   PERLXX                                                           
         MVI   FROM,COSLVPER       "FROM" PERSON LEVEL                          
         CLC   MYCPY(MYMIN),COKCPY SAME COMP AND METHOD?                        
         BNE   PE3                 NO -                                         
*                                  SAME OFFICE/DPT/SUB/PERSON?                  
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER),COKOFC                    
         BNE   PE3                 NO - CONTINUE                                
*                                                                               
         OC    APEROPTS,APEROPTS   SEE IF RECORD SAVED IN BUFFER                
         BZ    PE3                                                              
         L     R6,APEROPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CAPKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER),CAPKOFC                   
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLY            APPLY TO COBLOCK                             
         B     PERLXX                                                           
*                                                                               
PE3      BAS   RE,LOOKUP                                                        
         MVC   APEROPTS,AWRKADDR   ADDRESS INTO BUFFER                          
PERLXX   DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*************************************************************                   
*              END OF MAIN ROUTINES                                             
*************************************************************                   
*                                                                               
*                                                                               
MAINEND  DS    0H                                                               
         MVC   MYCPY,COKCPY        SAVE WHAT WE DID THIS TIME                   
         MVC   MYMET,COKMTHD                                                    
         MVC   MYOG,COKOFCG                                                     
         MVC   MYOFC,COKOFC                                                     
         MVC   MYDPT,COKDPT                                                     
         MVC   MYSDT,COKSDT                                                     
         MVC   MYPER,COKPER                                                     
*                                                                               
MAINEND2 CLI   ANYIO,C'N'          TEST ANY I/O DONE BY GETCAP                  
         BE    MAINENDX            NO                                           
*                                                                               
         ICM   R1,15,COAKEY        RESET READS FOR USER                         
         BZ    MAINENDX            USER HAS NOT PASSED A KEY                    
         MVC   KEY,0(R1)                                                        
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MAINENDX B     XIT                                                              
         EJECT                                                                  
*************************************************************                   
*              ROUTINE CONTROLS I/O OF OPTION RECORDS                           
*************************************************************                   
*                                                                               
*                                                                               
LOOKUP   NTR1  ,                                                                
         LA    R6,KEY                                                           
         USING CAPRECD,R6                                                       
         MVC   CAPKEY,SPACES                                                    
         MVI   CAPKTYP,CAPKTYPQ    X'3E'                                        
         MVI   CAPKSUB,CAPKSUBQ    X'09'                                        
         MVC   CAPKCPY,COKCPY      COMPANY CODE                                 
         MVC   CAPKMTHD,COKMTHD    METHOD                                       
         CLI   FROM,COSLVAGY       AGENCY LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         CLI   FROM,COSLVMET       METHOD LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         CLI   FROM,COSLVOGR       OFFICE GROUP LEVEL LOOKUP?                   
         BNE   *+14                YES - GO DO IT                               
         MVC   CAPKOFC,COKOFCG     MOVE OFFICE GROUP IN                         
         B     LOOKUP06            YES - GO DO IT                               
         MVC   CAPKOFC,COKOFC      MOVE OFFICE IN                               
         CLI   FROM,COSLVOFF       OFFICE LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CAPKDPT,COKDPT      MOVE DEPT IN                                 
         CLI   FROM,COSLVDPT       DEPT LEVEL LOOKUP?                           
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CAPKSDT,COKSDT      MOVE SUBDEPT IN                              
         CLI   FROM,COSLVSDT       SUBDEPT LEVEL LOOKUP?                        
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CAPKPER,COKPER      MOVE PERSON IN                               
         CLI   FROM,COSLVPER       PERSON LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         DC    H'0'                                                             
*                                                                               
LOOKUP06 CLI   OKSTAT,1            DO WE HAVE A OPTION KEY BUFFER?              
         BNE   LOOKUP12                                                         
         L     R2,AOKBUFF                                                       
         USING COVTABD,R2          1ST KEY IN BUFFER IS HIGHEST KEY             
         CLC   CAPKMTHD(COVKLNTH),COVKEY     CHECK KEY NOT PAST EOB             
         BH    LOOKUP12                                                         
         LA    R2,COVLNTH(R2)      SEE IF KEY WAS IN BUFFER                     
         L     R3,NOPTKEYS                                                      
         LTR   R3,R3                                                            
         BZ    LOOKUP99            (NO KEYS AT ALL)                             
         GOTO1 BINSRCH,DMCB,(X'02',CAPKMTHD),(R2),(R3),COVLNTH,        X        
               (0,COVKLNTH),(R3)                                                
         CLI   DMCB,X'01'                                                       
         BE    LOOKUP99            NO KEY SO SKIP IO                            
         L     R2,0(R1)                                                         
         CLC   CAPKMTHD(COVKLNTH),COVKEY     TEST SAME LEVEL                    
         BNE   LOOKUP99            NO-NO OPTIONS                                
*                                                                               
         MH    R3,=Y(COVLNTH)      COMPUTE END OF OPTION KEYS                   
         A     R3,AOKBUFF                                                       
         LA    R3,COVLNTH-1(R3)                                                 
*                                                                               
         MVC   CAPKMTHD(COVKLNTH),0(R2) SET RETURNED KEY                        
         ICM   R5,15,COVPOINT      GET A(DATA)                                  
         BZ    LOOKUP12            NOT THERE - READ THE FILE                    
*                                                                               
         LR    RE,R6               REBUILD RECORD FROM KEY AND DATA             
         AH    RE,DATADISP         RE=A(MOVE DESTINATION)                       
         SR    R1,R1               R1=L'SOURCE DATA                             
         ICM   R1,3,0(R5)          GET LENGTH OF DATA + HEADER                  
         BCTR  R1,0                ADJUST FOR HEADER                            
         BCTR  R1,0                                                             
         LA    RF,1(R1)            ADD ONE BACK IN FOR EOR                      
         LR    R4,RF               SAVE DATA LEN IN R4                          
         LA    R0,2(R5)            R0=A(MOVE SOURCE)                            
         MVCL  RE,R0               ATTACH DATA TO RECORD                        
         AH    R4,DATADISP         COMPUTE FULL REC LEN                         
         STCM  R4,3,CAPRLEN                                                     
         B     LOOKUP14                                                         
*                                                                               
LOOKUP12 BAS   RE,HIGH                                                          
         CLC   CAPKEY,KEYSAVE                                                   
         BNE   LOOKUP99                                                         
*                                                                               
LOOKUP14 DS    0H                  GO PUT TO OPTIMIZE BUFFER                    
         XC    AWRKADDR,AWRKADDR   CLEAR WORK ADDRESS                           
         OC    COABUFF,COABUFF     WAS A BUFFER PASSED                          
         BNZ   *+14                YES - CONTINUE                               
         XC    BUFFNOW,BUFFNOW     NO -  MAKER SURE BUFFNOW IS CLEAR            
         B     LOOKUP20                                                         
*                                                                               
         OC    BUFFNOW,BUFFNOW     FORGET IT IF BUFFNOW IS NOT SET              
         BZ    LOOKUP20                                                         
         ICM   R3,3,CAPRLEN                                                     
         LA    RE,BUFFNOW          RE=A(RECEIVING)                              
         LA    R1,0(R3,RE)                                                      
         C     R1,AENDBUFF         WILL RECORD FIT IN BUFFER                    
         BNH   LOOKUP18            YES - THEN PUT IT THERE                      
         XC    BUFFNOW,BUFFNOW     NO -  WE RAN OUT OF SPACE                    
         B     LOOKUP20                                                         
*                                                                               
LOOKUP18 LR    RF,R3               R3=LENGTH OF DATA RECORD                     
         LR    R1,RF               RF=LEN OF RECV  R1=LEN OF SEND               
         LR    R0,R6               R0=A(SENDING)                                
         MVCL  RE,R0               SAVE DATA RECORD IN OPTIMIZED BUFFER         
         LA    RE,BUFFNOW          MARK THE END OF THE RECORD                   
         ST    RE,AWRKADDR         PASS ADDR OF RECORD BACK TO CALLER           
         AR    RE,R3                                                            
         ST    RE,BUFFNOW          UPDATE BUFFNOW ADDRESS AS TO WHERE           
         B     LOOKUP20            WE ARE IN THE BUFFER                         
LOOKUP20 BAS   RE,APPLY            GO PUT THE OPTIONS TO COBLOCK                
*                                                                               
LOOKUP99 B     XIT                                                              
         EJECT                                                                  
*************************************************************                   
*              ROUTINE APPLIES PROFILE ELEMENTS TO COBLOCK                      
*************************************************************                   
*                                                                               
*        INPUT                     R6=A(RECORD)                                 
*                                                                               
         USING CAPRECD,R6          COST PROFILE RECORD                          
         USING OPDELD,R4           OPTION ELEMENTS                              
         USING CODSTABD,R5         OPTION DISPLACEMENT TABLE                    
APPLY    CLI   COSELLEV,0          APPLY IF PROCESSING ALL LEVELS               
         BE    *+12                                                             
         CLC   COSELLEV,FROM       OR IF THIS ONE REQUESTED                     
         BNER  RE                                                               
APPLYN   NTR1  ,                                                                
         LR    R4,R6                                                            
         MVI   ELCODE,OPDELQ       X'A4'                                        
         BAS   RE,GETEL                                                         
         B     *+8                                                              
APL02    BAS   RE,NEXTEL           NEXT OPTION ELEMENT                          
         BNE   APL99                                                            
         LA    R5,CDISTAB          RESET OPTIONS DISPLACEMENT TABLE             
APL04    CLI   CODSNUM,0           COBLOCK DISPLACEMENT NOT FOUND               
         BE    APL02               GET NEXT ELEMENT                             
         CLC   OPDNUM,CODSNUM      MATCH ON OPTION NUMBER                       
         BE    APL06                                                            
         LA    R5,CODSLEN(R5)      NEXT DISPLACEMENT TABLE ENTRY                
         B     APL04                                                            
*                                                                               
APL06    LA    R2,COPTIONS                                                      
         MVC   HALF,CODSDISP                                                    
         AH    R2,HALF             DISPLACE INTO OPTIONS                        
         SH    R2,=Y(COHEDLN)      BACK UP SET R2 TO OPTION HEADER              
         USING COPTION,R2                                                       
         SR    R1,R1                                                            
         IC    R1,CODSFLEN         PICK UP LENGTH                               
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         XC    COSET(0),COSET      PRECLEAR OPTION SETTING IN COBLOCK           
         SR    R1,R1                                                            
         IC    R1,OPDLN            PICK UP ELEMENT LENGTH                       
         SH    R1,=Y(OPDDATA-OPDELD+1)                                          
         EX    R1,*+4                                                           
         MVC   COSET(0),OPDDATA    MOVE DATA TO COBLOCK                         
         MVC   COLEVEL,FROM        FROM LEVEL                                   
         MVC   CODAT,OPDLAST       DATE OPTION WAS CHANGED                      
         MVC   COWHO,OPDPERS       WHO LAST CHANGED IT                          
         B     APL02                                                            
*                                                                               
APL99    B     XIT                                                              
*                                                                               
         DROP  R2,R4,R5                                                         
         EJECT                                                                  
*************************************************************                   
*              I/O AND ODDMENTS                                                 
*************************************************************                   
*                                                                               
*                                                                               
READ     MVC   COMMAND,DMREAD                                                   
         B     ACCALL                                                           
*                                                                               
HIGH     MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     ACCALL                                                           
*                                                                               
SEQ      MVC   COMMAND,DMRSEQ                                                   
*                                                                               
ACCALL   NTR1                                                                   
         MVI   ANYIO,C'Y'                                                       
         GOTO1 COADM,DMCB,(0,COMMAND),(0,ACCOUNT),KEY,IO                        
         TM    DMCB+8,X'10'                                                     
         BO    NO                                                               
         CLI   DMCB+8,0                                                         
         BE    YES                                                              
         DC    H'0'                                                             
*                                                                               
NO       LA    R1,1                                                             
         B     *+6                                                              
*                                                                               
YES      SR    R1,R1                                                            
         LTR   R1,R1                                                            
*                                                                               
XIT      XIT1                                                                   
*                                                                               
GETELIO  LA    R4,IO                                                            
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*************************************************************                   
*              CONSTANTS, TABLES                                                
*************************************************************                   
*                                                                               
*                                                                               
MINALLOC DC    F'10000'            MINIMUM BUFFER STORAGE ALLOCATION            
DATADISP DC    H'49'                                                            
XFF      DC    (L'CAPKEY)X'FF'                                                  
*                                                                               
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
ACCOUNT  DC    C'ACCOUNT'                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
         EJECT                                                                  
* ACCAPDFREC                                                                    
* ACCAPDISTB                                                                    
       ++INCLUDE ACCAPDFREC                                                     
       ++INCLUDE ACCAPDISTB                                                     
         EJECT                                                                  
*              LTORG FOR GETCAP                                                 
*                                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              WORKING STORAGE FOR GETCAP                                       
*                                                                               
*                                                                               
COD      DSECT                                                                  
RELO     DS    A                                                                
WORK     DS    CL64                                                             
DUB      DS    D                                                                
DMCB     DS    CL24                                                             
AENDBUFF DS    A                                                                
COVAIL   DS    V                                                                
BINSRCH  DS    V                                                                
         SPACE 1                                                                
HALF     DS    H                                                                
KEYSAVE  DS    CL42                I/O AREAS                                    
**********                                                                      
KEY      DS    CL42                                                             
LENGTH   DS    H                                                                
STATUS   DS    CL1                                                              
         DS    CL4                                                              
ELEMENTS DS    0C                                                               
         ORG   KEY                                                              
IO       DS    2000C                                                            
**********                                                                      
COMMAND  DS    CL8                                                              
ELCODE   DS    CL1                                                              
         SPACE 1                                                                
SPACES   DS    CL132                                                            
ANYIO    DS    CL1                                                              
FROM     DS    CL1                                                              
CODX     EQU   *                                                                
         SPACE 1                                                                
COBLOCKD DSECT                                                                  
*                                                                               
       ++INCLUDE ACCAPBLOCK                                                     
         EJECT                                                                  
*              GETCAP SAVE STORAGE IN COBLOCK                                   
*                                                                               
         ORG   COBLOCK                                                          
COSTORE  DS    0C                  BEGINNING OF STORAGE                         
COFLAG   DS    CL8                 **COBL**                                     
AOKBUFF  DS    A                   A(OPTION KEY BUFFER)                         
LOKBUFF  DS    F                   LENGTH OF ACQUIRED BUFFER                    
AOKDATA  DS    A                   A(OPTION DATA BUFFER)                        
AOKDATAX DS    A                   A(OPTION DATA BUFFER END)                    
NOPTKEYS DS    F                   N'OPTION KEYS IN BUFFER                      
OKSTAT   DS    X                                                                
MYSAV    DS    0C                                                               
MYCPY    DS    CL(L'CAPKCPY)       COMPANY                                      
MYMET    DS    CL(L'CAPKMTHD)      METHOD                                       
MYMIN    EQU   *-MYSAV             MINIMUM KEY REQUIRED                         
MYOG     DS    CL(L'CAPKOFC)       OFFICE GROUP                                 
MYOFC    DS    CL(L'CAPKOFC)       OFFICE                                       
MYDPT    DS    CL(L'CAPKDPT)       DEPT                                         
MYSDT    DS    CL(L'CAPKSDT)       SUB DEPT                                     
MYPER    DS    CL(L'CAPKPER)       PERSON                                       
MYSAVLN  EQU   *-MYSAV                                                          
*                                                                               
*                                  ADDRESSES IN OPTIMIZATION BUFFER             
BUFFNOW  DS    A                   PRESENT BUFFER ADDRESS                       
AAGYOPTS DS    A                   A(AGENCY OPTION RECORD)                      
AMETOPTS DS    A                   A(METHOD OPTION RECORD)                      
AOGROPTS DS    A                   A(OFFICE GROUP OPTION RECORD)                
AOFFOPTS DS    A                   A(OFFICE OPTION RECORD)                      
ADEPOPTS DS    A                   A(DEPT OPTION RECORD)                        
ASDTOPTS DS    A                   A(SUB DPT OPTION RECORD)                     
APEROPTS DS    A                   A(PERSON OPTION RECORD)                      
AAGYOPLN EQU   (*-AAGYOPTS)-L'AAGYOPTS                                          
AMETOPLN EQU   (*-AMETOPTS)-L'AMETOPTS LENGTH OF LOWER LEVELS TO CLEAR          
AOGROPLN EQU   (*-AOGROPTS)-L'AOGROPTS                                          
AOFFOPLN EQU   (*-AOFFOPTS)-L'AOFFOPTS                                          
ADEPOPLN EQU   (*-ADEPOPTS)-L'ADEPOPTS                                          
ASDTOPLN EQU   (*-ASDTOPTS)-L'ASDTOPTS                                          
AWRKADDR DS    A                   A(GENERAL AREA TO SAVE/PASS AN ADDR)         
BUFOPTLN EQU   *-BUFFNOW           LENGTH OF BUFFER SAVE ADDRESSES              
COSPARER EQU   (COBRESLN)-(*-COBLOK) AMOUNT OF RESERVED LEFT IN COBLOCK         
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*              DSECT TO COVER CDISTAB (ACCAPDISTB) WHICH IS INCLUDED            
*                                                                               
CODSTABD DSECT                                                                  
CODSNUM  DS    X                     PROFILE NUMBER                             
CODSFLEN DS    X                     LENGTH OF FIELD IN COBLOCK                 
CODSDISP DS    Y                     DISP INTO COBLOCK                          
CODSLEN  EQU   *-CODSTABD                                                       
*                                                                               
*                                                                               
*                                                                               
*              DSECT TO COVER COVAIL TABLE OF RECORDS                           
*                                                                               
COVTABD  DSECT                                                                  
COVKEY   DS    0C                    KEY                                        
COVMETH  DS    CL(L'CAPKMTHD)        METHOD                                     
COVOFFC  DS    CL(L'CAPKOFC)         OFFICE                                     
COVDPT   DS    CL(L'CAPKDPT)         DEPT                                       
COVSDT   DS    CL(L'CAPKSDT)         SUB DPT                                    
COVPER   DS    CL(L'CAPKPER)         PERSON                                     
COVKLNTH EQU   *-COVKEY                                                         
COVPOINT DS    XL4                 POINTER TO RECORD AREA                       
COVLNTH  EQU   *-COVTABD                                                        
*                                                                               
*                                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014ACGETCAP  07/07/20'                                      
         END                                                                    
