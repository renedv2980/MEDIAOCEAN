*          DATA SET MPLDDCNT   AT LEVEL 002 AS OF 06/21/85                      
*CATALP MPLDDCNT                                                                
         TITLE 'LDCOUNT - MEDIA PLANNING - DIR REC TYPE COUNT/PRINT'            
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'81'=COUNT DELETED PASSIVE            
*                           X'FF'=PRINT                                         
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 WRKX-WRKD,MPLDDCNT                                               
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
         CLI   0(R1),X'81'                                                      
         BE    EXIT                                                             
         SPACE 2                                                                
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,RECTBL           R3=A(RECORD TYPE TABLE)                      
         SR    R4,R4                                                            
COUNT1   CLI   0(R3),0             TEST END OF TABLE                            
         BE    COUNT3                                                           
         CLC   0(2,R3),0(R2)       COMPARE AGENCY/LISTNUM                       
         BNE   COUNT2                                                           
         LTR   R4,R4               SAVE A(FIRST AGENCY/LISTNUM)                 
         BNZ   *+6                                                              
         LR    R4,R3                                                            
         CLC   2(1,R3),2(R2)       COMPARE RECORD TYPE                          
         BNE   COUNT2                                                           
         SR    R4,R4               SET AGENCY/LISTNUM/RECTYPE FOUND             
         B     COUNT3                                                           
COUNT2   LA    R3,L'RECTBL(R3)                                                  
         B     COUNT1                                                           
*                                                                               
COUNT3   LTR   R4,R4               TEST IF ONLY FIRST FOUND                     
         BZ    *+6                                                              
         LR    R3,R4                                                            
         L     RF,20(R3)           BUMP TOTAL RECORD COUNT                      
         LA    RF,1(RF)                                                         
         ST    RF,20(R3)                                                        
         LA    R4,24(R3)                                                        
         TM    32(R2),X'C0'        TEST ACTIVE                                  
         BZ    COUNT3A                                                          
         LA    R4,32(R3)                                                        
         TM    36(R2),X'80'        TEST DELETED                                 
         BO    COUNT3A                                                          
         LA    R4,28(R3)                                                        
COUNT3A  L     RF,0(R4)            BUMP ACTIVE/LAPSED/DELETED COUNT             
         LA    RF,1(RF)                                                         
         ST    RF,0(R4)                                                         
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   MID1,HDRL1                                                       
         MVC   MID2,ULINE                                                       
         LA    R3,RECTBL           R3=A(RECORD TABLE ENTRY)                     
*                                                                               
PRINT1   L     RF,20(R3)           TEST TOTAL RECORD COUNTER                    
         LTR   RF,RF                                                            
         BZ    PRINT3              DONT PRINT IF ZERO                           
         MVC   PLNAME,4(R3)                                                     
         CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   PLRTOT,DUB1                                                      
         LA    R4,24(R3)           R4=A(RECORD COUNTER)                         
         LA    R5,PLRACT           R5=A(PLINE DISPLAY FIELD)                    
         LA    R0,3                                                             
*                                                                               
PRINT2   L     RF,0(R4)            TEST RECORD COUNTER                          
         LTR   RF,RF                                                            
         BNZ   PRINT2A                                                          
         MVC   0(8,R5),=8C'.'      SET DOTS IF ZERO                             
         B     PRINT2B                                                          
PRINT2A  CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   0(8,R5),DUB1                                                     
PRINT2B  LA    R4,4(R4)            BUMP TO NEXT RECORD COUNTER                  
         LA    R5,9(R5)                                                         
         BCT   R0,PRINT2                                                        
PRINT2C  MVC   P,PLINE                                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRINT3   CLI   0(R3),X'00'         EXIT IF END OF TABLE                         
         BE    PRINTX                                                           
         LA    R3,L'RECTBL(R3)                                                  
         B     PRINT1                                                           
*                                                                               
PRINTX   B     EXIT                                                             
         EJECT                                                                  
HDRL1    DC   CL132'RECORD TYPE      TOT RECS  ACTIVE   LAPSED   DELETE*        
               D'                                                               
ULINE    DC   CL132'---------------- -------- -------- -------- -------*        
               -'                                                               
         SPACE 2                                                                
PLINE    DC    CL132' '                                                         
         ORG   PLINE                                                            
PLNAME   DS    CL16                                                             
         DS    CL1                                                              
PLRTOT   DS    CL8                                                              
         DS    CL1                                                              
PLRACT   DS    CL8                                                              
         DS    CL1                                                              
PLRLAP   DS    CL8                                                              
         DS    CL1                                                              
PLRDEL   DS    CL8                                                              
         ORG                                                                    
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        XL3   AGENCY/LISTNUM/RECTYPE                                           
*        XL1   FLAGS                                                            
*        CL16  LIST AND RECORD TYPE                                             
*        4XL4 COUNTS TOTAL/ACTIVE/LAPSED/DELETED                                
*                                                                               
         DS    0F                                                               
RECTBL   DS    0XL36                                                            
         DC    X'010100',X'00',CL16'DDS TEST    HDR ',4F'0'                     
         DC    X'010101',X'00',CL16'DDS TEST    LBL ',4F'0'                     
         DC    X'010102',X'00',CL16'DDS TEST    FLD ',4F'0'                     
         DC    X'010120',X'00',CL16'DDS TEST    MEM ',4F'0'                     
         DC    X'010121',X'00',CL16'DDS TEST    S/N*',4F'0'                     
         DC    X'010122',X'00',CL16'DDS TEST    P/C*',4F'0'                     
         DC    X'0101F0',X'00',CL16'DDS TEST    STA ',4F'0'                     
*                                                                               
RECTBLX  DC    X'000000',X'00',CL16'MISCELLANEOUS   ',4F'0'                     
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002MPLDDCNT  06/21/85'                                      
         END                                                                    
