*          DATA SET DDDEJAVU   AT LEVEL 002 AS OF 04/23/02                      
*CATALP DEJAVU                                                                  
         TITLE 'MODULE TO PROCESS DAY EXPRESSIONS'                              
*                                                                               
*        P1    XL1      (L'INPUT/OUTPUT) TRUE O/P LEN RETURNED HERE             
*        P1    AL3      A(INPUT)                                                
*        P2    XL1      FLAGS                                                   
*        P2    AL3      A(OUTPUT)                                               
*        P3    XL1      FLAGS2     (NOT USED)                                   
*        P3    AL3      A(1 BYTE START END DAY O/P) OR ZERO                     
*                                                                               
*        FLAGS X'10'    VALIDATE INPUT STRING                                   
*              X'20'    START DAY IS IN BITS 08-01 - 0 DEFAULTS TO MON          
*                          TURN ON TO FORCE MONDAY AS START OF WEEK             
*              X'40'    FORCE MTWTFSS OUTPUT                                    
*              X'80'    SPARE                                                   
*        FLAGS RETURN   X'FF'      INVALID INPUT                                
*                                                                               
DEJAVU   CSECT                     1 GENERAL DATE PROCESSING                    
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**DEJA**,RR=RE,CLEAR=YES                             
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
*                                                                               
         MVC   DPARMS,0(R1)        SAVE DEJAVU PARMS                            
         ST    R1,DAPARMS          SAVE PARAMETER LIST ADDRESS                  
         CTRY  ,                                                                
         STC   R0,LANG                                                          
         SR    R2,R2                                                            
         ICM   R2,7,INADDR         EXTRACT ADDRESSES FROM PARMS                 
         SR    R3,R3                                                            
         ICM   R3,7,OUTADDR                                                     
         SR    R5,R5                                                            
         ICM   R5,7,DAYSOUT                                                     
         LA    RF,DAYTAB                                                        
         CLI   LANG,3              LANG 0,1,2 = ENGLISH                         
         BL    DEJAVU1                                                          
         SR    RE,RE                                                            
         IC    RE,LANG             ALL OTHERS CALCULATE OFFSET                  
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         SLL   RE,5                *32                                          
         LA    RF,0(RE,RF)                                                      
DEJAVU1  ST    RF,TABLE            SAVE A(MY TABLE)                             
         TM    FLAGS,X'10'                                                      
         BO    DAYV00              VALIDATE INPUT                               
         B     DAYO                OR OUTPUT DAYS                               
         EJECT                                                                  
******************************************                                      
*      VALIDATE INPUT STRING             *                                      
******************************************                                      
         SPACE 1                                                                
DAYV00   LTR   R5,R5                                                            
         BZ    *+8                                                              
         MVI   0(R5),0             ZERO DAYSOUT IF REQUIRED                     
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         SR    R1,R1               GET INPUT STRING INTO WORK                   
         ICM   R1,1,LENGTH                                                      
         BZ    DAYERR                                                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R2)       INPUT STRING IS AT 0(R2)                     
*                                                                               
         LA    R4,WORK             POINT TO START OF INPUT                      
*                                                                               
         CH    R1,=H'6'            INPUT MUST BE 7                              
         BNE   DAYV02              R1 IS LENGTH LESS ONE                        
         SR    RF,RF                                                            
         IC    RF,LANG                                                          
         SLL   RF,3                USE LANG * 8 FOR INDEX                       
         LA    RE,DAYTAB2(RF)                                                   
         CLC   WORK(2),0(RE)                                                    
         BE    DYPK1               MUST START WITH MT (OR WHATEVER!!)           
         CLI   WORK,C'.'           OR HAVE AT LEAST ONE '.'                     
         BE    DYPK1                                                            
         CLI   WORK+1,C'.'                                                      
         BE    DYPK1                                                            
         B     DAYV02                                                           
*                                                                               
DYPK1    DS    0H                  INPUT IS MTWTFSS FORMAT                      
         MVI   DAYBITS,0                                                        
         MVI   BYTE,X'40'                                                       
DYPK2    CLI   0(R4),C'.'                                                       
         BE    DYPK3                                                            
         OC    DAYBITS,BYTE        SET BIT ON                                   
         CLC   0(1,R4),0(RE)       TEST DAY VALUE IS CORRECT                    
         BNE   DAYERR                                                           
DYPK3    LA    R4,1(R4)            BUMP DAY POSITION                            
         LA    RE,1(RE)                                                         
         ZIC   R0,BYTE                                                          
         SRL   R0,1                SHIFT BIT VALUE                              
         STC   R0,BYTE                                                          
         CLI   0(RE),C'*'          HAVE WE FINISHED                             
         BNE   DYPK2                                                            
         MVC   0(1,R3),DAYBITS                                                  
         B     DAYX                OUTPUT DAYBITS                               
         EJECT                                                                  
DAYV02   ST    R4,LASTA            FIND DELIMITER                               
DAYV02A  CLI   0(R4),C' '                                                       
         BE    DAYV03                                                           
         CLI   0(R4),C'/'                                                       
         BE    DAYV03                                                           
         CLI   0(R4),C','                                                       
         BE    DAYV03                                                           
         CLI   0(R4),C'-'                                                       
         BE    DAYV03                                                           
         LA    R4,1(R4)                                                         
         B     DAYV02A                                                          
*                                                                               
DAYV03   ST    R4,THISA                                                         
         L     R0,LASTA                                                         
         SR    R4,R0               R4=L'1ST DAY EXPRESSION                      
         STC   R4,DAYLEN                                                        
*                                                                               
DAYV04   L     RF,TABLE                                                         
         L     RE,LASTA                                                         
         LA    R0,X'40'            R0 HOLDS BIT MASK (MON=X'40')                
         LA    R6,1                R6 HOLDS DAY VALUE                           
         BCTR  R4,0                                                             
DAYV05   CLC   DAYLEN,3(RF)        DON'T COMPARE SHORTER DAYS                   
         BL    *+8                                                              
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),0(RE)                                                    
         BE    DAYV06                                                           
         LA    RF,4(RF)            TRY NEXT DAY                                 
         SRL   R0,1                                                             
         LA    R6,1(R6)                                                         
         CLC   0(3,RF),=C'END'     IF END REACHED                               
         BNE   DAYV05                                                           
         B     DAYERR              NO MATCH ON DAY                              
*                                                                               
DAYV06   STC   R0,BYTE                                                          
         OC    DAYBITS,BYTE        TURN ON BIT                                  
         LTR   R5,R5                                                            
         BZ    DAYV06A             DO WE WANT START & END DAYS                  
         CLI   0(R5),0                                                          
         BNE   *+12                IF 0(R5) NOT ZERO WE HAVE START DAY          
         SLL   R6,4                                                             
         STC   R6,0(R5)            STORE FIRST DAY (ONCE ONLY)                  
         ST    R6,FULL                                                          
         NI    0(R5),X'F0'         MASK IN SUBSEQUENT DAYS TO GET LAST          
         OC    0(1,R5),FULL+3                                                   
DAYV06A  CLI   RFLAG,C'Y'          DO WE A WANT RANGE                           
         BNE   DAYV07                                                           
*                                                                               
DAYV06B  ZIC   R0,BYTE                                                          
         SLL   R0,1                SHIFT BACK ONE DAY                           
         STC   R0,BYTE                                                          
         CLI   BYTE,B'10000000'    TEST FOR WRAP ROUND                          
         BNE   *+8                                                              
         MVI   BYTE,B'00000001'                                                 
         IC    R0,BYTE                                                          
         NC    BYTE,DAYBITS        IS THIS BIT ON                               
         BNZ   DAYV07                                                           
         STC   R0,BYTE                                                          
         OC    DAYBITS,BYTE        TURN ON BIT                                  
         B     DAYV06B                                                          
*                                                                               
DAYV07   L     R4,THISA                                                         
         MVI   RFLAG,C'N'                                                       
         CLI   0(R4),C'-'          ARE WE AFTER A RANGE OF DAYS                 
         BNE   *+8                                                              
         MVI   RFLAG,C'Y'          SET FLAG FOR NEXT TIME                       
         LA    R4,1(R4)                                                         
         CLI   0(R4),C' '          ZERO IS END OF INPUT                         
         BNE   DAYV02                                                           
         MVC   0(1,R3),DAYBITS                                                  
         B     DAYX                                                             
         EJECT                                                                  
******************************************                                      
*      OUTPUT DAYS EXPRESSION            *                                      
******************************************                                      
         SPACE 1                                                                
DAYO     TM    FLAGS,X'40'         TEST FOR MTWTFSS FORMAT                      
         BNZ   UNPK1                                                            
         L     RF,TABLE            SET INITIALLY TO MONDAY                      
         MVI   BYTE,X'40'                                                       
         TM    FLAGS,X'20'         TEST FOR START DAY                           
         BZ    DAYO2                                                            
         TM    FLAGS,X'0F'         START DAY ZERO = MONDAY                      
         BZ    DAYO00                                                           
         MVC   DAYBITS,FLAGS       COPY FLAG TO DAYBITS                         
         NI    DAYBITS,X'0F'       LOSE FLAG BITS                               
         CLI   DAYBITS,X'07'                                                    
         BNH   *+6                                                              
         DC    H'0'                INVALID START DAY                            
         SR    R1,R1                                                            
         IC    R1,DAYBITS                                                       
DAYO1    BCTR  R1,0                FIND START DAY                               
         LTR   R1,R1                                                            
         BZ    DAYO00                                                           
         LA    RF,4(RF)            BUMP TABLE POINTER                           
         ZIC   R0,BYTE                                                          
         SRL   R0,1                SHIFT START DAY BIT                          
         STC   R0,BYTE                                                          
         B     DAYO1                                                            
*                                                                               
DAYO2    MVC   DAYBITS,0(R2)       FIND A SUITABLE START DAY                    
         MVI   RFLAG,C'N'                                                       
         ZIC   R0,BYTE                                                          
         TM    DAYBITS,X'41'       IF SUN.MON NOT ON START MONDAY               
         BNO   DAYO00                                                           
         TM    DAYBITS,X'7F'       IF MON-SUN START MONDAY                      
         BO    DAYO00                                                           
DAYO3    NC    BYTE,DAYBITS        LOOK FOR A DAY ON                            
         BNZ   DAYO5                                                            
         MVI   RFLAG,C'Y'          SET GAP FOUND                                
DAYO4    LA    RF,4(RF)            BUMP TABLE POINTER                           
         SRL   R0,1                SHIFT START DAY BIT                          
         STC   R0,BYTE                                                          
         B     DAYO3                                                            
DAYO5    CLI   RFLAG,C'Y'          HAVE I FOUND A  GAP YET                      
         BNE   DAYO4                                                            
         STC   R0,BYTE             START HERE                                   
*                                                                               
DAYO00   MVI   RFLAG,C'N'                                                       
         ZIC   R0,BYTE             ENDDAY IS START DAY SHIFTED SLL 1            
         SLL   R0,1                                                             
         STC   R0,ENDDAY                                                        
         CLI   ENDDAY,X'80'        IF START WAS SUN THEN ENDAY IS MON           
         BNE   *+8                                                              
         MVI   ENDDAY,X'01'                                                     
         MVC   STRDAY,0(RF)        RF POINTS TO FIRST DAY                       
         MVC   DAYBITS,0(R2)                                                    
         MVI   WORK,C' '           FILL OUTPUT WITH SPACES                      
         MVC   WORK+1(L'WORK-1),WORK                                            
         LA    R4,WORK                                                          
DAYO01   ZIC   R0,BYTE             SAVE DAY BIT                                 
         NC    BYTE,DAYBITS                                                     
         BZ    DAYO02                                                           
         MVC   THISDAY,0(RF)       SAVE THIS DAY VALUE                          
         STC   R0,SAVEOP           SAVE BIT VALUE ALSO                          
         CLI   RFLAG,C'Y'                                                       
         BNE   DAYO01A                                                          
         CLC   SAVEOP,ENDDAY       ENDDAY TERMINATES RANGE                      
         BE    DAYO02                                                           
         BNE   DAYO04                                                           
DAYO01A  MVC   0(3,R4),0(RF)       MOVE TABLE ENTRY TO WORK                     
         STC   R0,LASTOP           SAVE BIT VALUE                               
         LA    R4,3(R4)                                                         
         MVI   RFLAG,C'Y'                                                       
         B     DAYO04                                                           
DAYO02   CLI   RFLAG,C'Y'          WAS PREVIOUS DAY ON                          
         BNE   DAYO04                                                           
         MVI   RFLAG,C'N'                                                       
         CLC   LASTOP,SAVEOP       IS SAVED DAY SAME AS PREV                    
         BNE   DAYO03                                                           
         MVI   0(R4),C'/'          IF SO JUST INSERT A '/'                      
         LA    R4,1(R4)                                                         
         B     DAYO04                                                           
DAYO03   MVI   0(R4),C'-'          ELSE THIS IS A RANGE                         
         MVC   1(3,R4),THISDAY                                                  
         MVI   4(R4),C'/'                                                       
         LA    R4,5(R4)                                                         
DAYO04   LA    RF,4(RF)            BUMP TO NEXT TABLE ENTRY                     
         SRL   R0,1                SHIFT DAY BIT                                
         STC   R0,BYTE                                                          
         CLI   BYTE,0              TEST FOR WRAP ROUND                          
         BNE   *+12                                                             
         MVI   BYTE,B'01000000'    MOVE MONDAY TO BYTES                         
         SH    RF,=H'28'           POINT BACK TO MON                            
         ZIC   R0,BYTE                                                          
         CLC   0(3,RF),STRDAY      HAVE WE REACHED LAST DAY                     
         BNE   DAYO01                                                           
         BCTR  R4,0                                                             
         CLI   0(R4),C'A'          REMOVE END ',' OR '/'                        
         BNL   *+10                                                             
         MVI   0(R4),C' '                                                       
         BCTR  R4,0                                                             
         LA    R1,WORK                                                          
         SR    R4,R1                                                            
         LA    R4,1(R4)                                                         
         STC   R4,DAYLEN                                                        
*                                                                               
         MVC   WORK1,WORK                                                       
         CLI   LENGTH,0            IF NO LENGTH GIVEN THEN ASSUME ANY           
         BE    COMPX                  WILL DO                                   
         CLC   DAYLEN,LENGTH       WILL FULL EXPRESSION FIT                     
         BNH   COMPX                                                            
*                                  IF NOT COMPACT EXPRESSION                    
         LA    R6,14               COULD TAKE 14 PASSES                         
         XC    WORK1,WORK1                                                      
         L     RF,TABLE                                                         
         LA    R4,WORK                                                          
         LA    RE,WORK1                                                         
COMP01   CLC   0(3,R4),=C'   '     HAVE WE FINISHED                             
         BE    COMP03                                                           
         CLC   0(3,R4),0(RF)       FIND THIS DAY IN TABLE                       
         BNE   COMP02                                                           
         SR    R1,R1                                                            
         IC    R1,3(RF)            R1=MIN LEN OF DAY                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)       MOVE MINIMUM DAY CHRS IN                     
         LA    RE,1(RE,R1)         SHIFT WORK1 POINTER                          
         MVC   0(1,RE),3(R4)       MOVE IN SINGLE DELIMITER CHR                 
         LA    RE,1(RE)            SHIFT WORK1 POINTER                          
         LA    R4,4(R4)            GOTO NEXT EXPRESSION                         
COMP02   LA    RF,4(RF)            GOTO NEXT TABLE ENTRY                        
         CLC   0(3,RF),=C'END'                                                  
         BNE   *+8                                                              
         SH    RF,=H'28'           POINT BACK TO MON                            
         BCT   R6,COMP01           CONTINUE                                     
COMP03   LR    R4,RE                                                            
         LA    R1,WORK1                                                         
         SR    R4,R1                                                            
         BCTR  R4,0                                                             
         STC   R4,DAYLEN                                                        
         CLC   DAYLEN,LENGTH       WILL IT FIT NOW ??                           
         BH    UNPK1               OH SHIT BETTER PUT OUT MTWTFSS               
COMPX    IC    R1,DAYLEN                                                        
         STC   R1,LENGTH                                                        
         BCTR  R1,0                                                             
         EX    R1,*+8              MOVE EXPRESSION TO O/P AREA                  
         B     *+10                                                             
         MVC   0(0,R3),WORK1                                                    
         B     DAYX                                                             
         EJECT                                                                  
UNPK1    SR    RF,RF               UNPK MTWTFSS FORMAT                          
         IC    RF,LANG                                                          
         SLL   RF,3                USE LANG * 8 FOR INDEX                       
         LA    RE,DAYTAB2(RF)                                                   
         MVC   0(7,R3),0(RE)       MOVE IN ALL DAYS                             
         LA    RF,0(R3)                                                         
         MVC   DAYBITS,0(R2)                                                    
         MVI   BYTE,X'40'          START ON MONDAY                              
         SR    R0,R0                                                            
         IC    R0,BYTE                                                          
UNPK2    NC    BYTE,DAYBITS        IF THE BIT IS OFF                            
         BNZ   *+8                                                              
         MVI   0(RF),C'.'          REMOVE THAT DAY                              
         LA    RF,1(RF)                                                         
         SRL   R0,1                SHIFT TO NEXT DAY                            
         STC   R0,BYTE                                                          
         CLI   BYTE,0              UNTIL ALL DAYS DONE                          
         BNE   UNPK2                                                            
         MVI   LENGTH,7            LENGTH MUST BE 7                             
         B     DAYX                                                             
         EJECT                                                                  
DAYERR   MVI   FLAGS,X'FF'                                                      
         B     DAYXX                                                            
DAYX     MVI   FLAGS,0                                                          
DAYXX    DS    0H                                                               
         L     R1,DAPARMS          POINT TO PARAMETER LIST                      
         MVC   0(1,R1),LENGTH      RETURN OUTPUT LENGTH                         
         MVC   4(1,R1),FLAGS       RETURN COMPLETION STATUS                     
         XMOD1                                                                  
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLERS ADDRESSING MODE              
         EJECT                                                                  
*              TABLE OF VALUES  (ENG,GER,FRE,SPA,ITA,DUT)                       
         CNOP  0,4                                                              
         SPACE 2                                                                
DAYTAB   DC    C'MON',X'01',C'TUE',X'02',C'WED',X'01',C'THU',X'02'              
         DC    C'FRI',X'01',C'SAT',X'02',C'SUN',X'02',C'END',X'01'              
*                                                                               
         DC    C'MON',X'02',C'DIE',X'02',C'MIT',X'02',C'DON',X'02'              
         DC    C'FRE',X'01',C'SAM',X'02',C'SON',X'02',C'END',X'01'              
*                                                                               
         DC    C'LUN',X'01',C'MAR',X'02',C'MER',X'02',C'JEU',X'01'              
         DC    C'VEN',X'01',C'SAM',X'01',C'DIM',X'01',C'END',X'01'              
*                                                                               
         DC    C'MON',X'01',C'TUE',X'02',C'WED',X'01',C'THU',X'02'              
         DC    C'FRI',X'01',C'SAT',X'02',C'SUN',X'02',C'END',X'01'              
*                                                                               
         DC    C'MON',X'01',C'TUE',X'02',C'WED',X'01',C'THU',X'02'              
         DC    C'FRI',X'01',C'SAT',X'02',C'SUN',X'02',C'END',X'01'              
*                                                                               
         DC    C'MAA',X'01',C'DIN',X'02',C'WOE',X'01',C'DON',X'02'              
         DC    C'VRI',X'01',C'ZAT',X'02',C'ZON',X'02',C'END',X'01'              
*                                                                               
DAYTAB2  DC    C'MTWTFSS*'         ENGLISH                                      
         DC    C'MTWTFSS*'         ENGLISH                                      
         DC    C'MTWTFSS*'         ENGLISH                                      
         DC    C'MDMDFSS*'         GERMAN                                       
         DC    C'LMMJVSD*'         FRENCH                                       
         DC    C'MTWTFSS*'         SPANISH                                      
         DC    C'MTWTFSS*'         ITALIAN                                      
         DC    C'MDWDVZZ*'         DUTCH                                        
         EJECT                                                                  
*              DSECT FOR DEJAVU                                                 
         SPACE 3                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
DAPARMS  DS    A                   PARAMETER LIST ADDRESS                       
*                                                                               
DPARMS   DS    0CL12               DEJAVU PARMS                                 
LENGTH   DS    XL1                                                              
INADDR   DS    XL3                                                              
FLAGS    DS    XL1                                                              
OUTADDR  DS    XL3                                                              
FLAGS2   DS    XL1                                                              
DAYSOUT  DS    XL3                                                              
*                                                                               
LANG     DS    X                                                                
DAYLEN   DS    X                                                                
DAYBITS  DS    X                                                                
BYTE     DS    X                                                                
RFLAG    DS    X                                                                
WORK     DS    CL32                                                             
WORK1    DS    CL32                                                             
LASTA    DS    F                                                                
THISA    DS    F                                                                
TABLE    DS    F                                                                
RELO     DS    F                                                                
LASTOP   DS    CL1                                                              
SAVEOP   DS    CL1                                                              
ENDDAY   DS    CL1                                                              
THISDAY  DS    CL3                                                              
STRDAY   DS    CL3                                                              
WORKX    EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDDEJAVU  04/23/02'                                      
         END                                                                    
