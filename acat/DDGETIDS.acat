*          DATA SET DDGETIDS   AT LEVEL 008 AS OF 06/09/04                      
*CATALP GETIDS                                                                  
         TITLE 'GENERATE A LIST OF USER IDS FROM AN ID RECORD'                  
                                                                                
***********************************************************************         
* THIS MODULE IS AVAILABLE AS A CORE RESIDENT PHASE                   *         
* LINK WITH LMGETIDS - T00AF9 (UK) - T00AFA (US)                      *         
***********************************************************************         
                                                                                
GETIDS   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETIDS                                             
         USING WORKD,RC            RC=A(W/S)                                    
         LA    RE,*+6                                                           
         BSM   0,RE                SET IN 24-BIT MODE                           
         LR    R2,R1                                                            
         USING PLISTD,R2           R2=A(PARM LIST)                              
*                                                                               
GETID1   MVI   VALOPT,0            TEST VALIDATE OPTION SET IN P3               
         MVI   VALOPT1,0                                                        
         MVI   MATCH,0                                                          
         MVI   IDNUMX,X'FF'                                                     
         CLI   PVALOPT,C'N'                                                     
         BNE   *+12                                                             
         MVI   VALOPT,C'N'         SET ID NUMBER PASSED IN P4                   
         B     GETID1A                                                          
         CLI   PVALOPT,C'A'                                                     
         BNE   *+12                                                             
         MVI   VALOPT,C'A'         SET ID ALPHA PASSED IN P4                    
         B     GETID1A                                                          
         CLI   PVALOPT,C'S'                                                     
         BNE   GETID1A                                                          
         MVI   VALOPT,0            SET ID IN P4 AND SYNONYM IN P5               
         MVI   VALOPT1,C'S'                                                     
GETID1A  CLI   PVALOPT,C'W'        TEST RETURN WILD CARD OPTION                 
         BNE   GETID1AA                                                         
         MVI   VALOPT,0                                                         
         MVI   VALOPT1,C'W'                                                     
GETID1AA MVI   PVALOPT,0           RESET VALIDATE OPTION BYTE                   
*                                                                               
GETID1B  SR    R3,R3               R3=A(ID RECORD)                              
         ICM   R3,7,PIDREC                                                      
         LA    R8,3000             R8=MAX NUMBER OF IDS TO PROCESS              
         SR    R4,R4                                                            
         ICM   R4,7,PBLOCK         R4=A(OUTPUT BLOCK) MUST BE LONG ENUF         
         BNZ   GETID1C                                                          
         CLI   VALOPT,0            TEST VALIDATE MODE ONLY                      
         BNE   GETID1C             YES                                          
         LR    R4,RD               NO POINT TO BLOCK IN MY W/S                  
         L     RE,4(RD)                                                         
         A     RD,=A(WBLOCKX-WBLOCK)                                            
         ST    RE,4(RD)            SET POINTERS FOR LONGER W/S AREA             
         ST    RD,8(RE)                                                         
         LA    R8,WBLOCKNQ         R8=MAX IDS TO FIT IN MY W/S BUFFER           
GETID1C  ST    R4,PBLOCK-1         SET A(BLOCK) IN PLIST                        
*                                                                               
GETID1E  SR    R5,R5               R5=NUMBER OF LIST ENTRIES                    
         LA    R3,28(R3)                                                        
         MVI   LISTMODE,C'N'       SET REQD ELEMENT CODE                        
         MVC   FILLER,SPACES                                                    
         MVI   ELCODE,X'20'                                                     
         CLI   PIDTYPE,C'C'        C FOR COMPATIBLE IDS                         
         BE    GETI2                                                            
         CLI   PIDTYPE,C'L'        L FOR COMPATIBLE IDS NEW STYLE               
         BNE   *+14                                                             
         XC    FILLER,FILLER                                                    
         B     GETI2                                                            
         MVI   ELCODE,X'34'                                                     
         CLI   PIDTYPE,C'D'        D FOR DESTINATION IDS                        
         BE    GETI2                                                            
         CLI   PIDTYPE,C'M'        M FOR DESTINATION IDS NEW STYLE              
         BNE   *+14                                                             
         XC    FILLER,FILLER                                                    
         B     GETI2                                                            
*                                                                               
GETIERR1 OI    PERRNUM,X'01'       SET ERROR INVALID PARAMETER LIST             
         MVI   PIDTYPE,X'FF'       SET SEVERE ERROR                             
         B     GETXIT                                                           
*                                                                               
GETIERR2 OI    PERRNUM,X'02'       SET DELETED INCLUDE LIST                     
         B     GETI4               AND PLOUGH ON REGARDLESS                     
*                                                                               
GETIERR4 OI    PERRNUM,X'04'       SET ERROR MISSING INCLUDE LIST               
         B     GETI4               AND PLOUGH ON REGARDLESS                     
*                                                                               
GETIERR8 OI    PERRNUM,X'08'       SET ERROR OUTPUT BLOCK FULL                  
*NOP*    MVI   PIDTYPE,X'FF'       NOT A SEVERE ERROR ANY MORE                  
         BCTR  R5,0                                                             
         CLI   VALOPT,0            TEST VALIDATE ONLY                           
         BNE   *+14                                                             
         SH    R4,=H'12'           POINT TO LAST ENTRY IN OUTPUT BLOCK          
         XC    0(12,R4),0(R4)                                                   
         B     GETIX                                                            
         EJECT                                                                  
GETI2    CLI   0(R3),0             PROCESS ID RECORD                            
         BE    GETIX                                                            
         CLC   0(1,R3),ELCODE                                                   
         BE    GETI5                                                            
GETI4    ZIC   RE,1(R3)                                                         
         AR    R3,RE                                                            
         B     GETI2                                                            
*                                                                               
GETI5    LA    R7,NWIDEL           R7=A(COPY OF ID ELEMENT)                     
         USING NWIDEL,R7                                                        
         MVC   NWIDEL,0(R3)        BUILD NEW TYPE ID ELEMENT IN W/S             
         MVI   NWIDLEN,11                                                       
         MVI   NWIDTYP,0                                                        
         MVC   NWIDNAM,SPACES                                                   
         XC    NWIDNUM,NWIDNUM                                                  
         CLC   2(2,R3),=X'0001'    TEST FOR INCLUDE LIST OR AGY ALPHA           
         BL    GETI5C              INCLUDE LIST                                 
         BE    GETI5D              AGENCY ALPHA                                 
         TM    2(R3),NWIDNEQ                                                    
         BNZ   GETI5B                                                           
*                                                                               
GETI5A   MVC   NWIDTYP,2(R3)       NEW ELEMENT FORMAT                           
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
GETI5A1  TM    NWIDTYP,NWIDNUQ     TEST IF USERID NUMBER AT END                 
         BZ    GETI5A2                                                          
         AHI   RF,-2                                                            
         BM    GETI5X                                                           
         LA    RE,0(R3,RF)                                                      
         MVC   NWIDNUM,0(RE)       EXTRACT USERID NUMBER                        
         MVI   NWIDLEN,13                                                       
GETI5A2  AHI   RF,-4                                                            
         BM    GETI5X                                                           
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NWIDNAM(0),3(R3)    EXTRACT USERID NAME                          
         B     GETI5X                                                           
*                                                                               
GETI5B   MVC   NWIDNAM,2(R3)       OLD ELEMENT CONTAINS USERID NAME             
         CLI   1(R3),12            TEST IF ONLY ALPHA USER ID IN EL             
         BNH   *+10                                                             
         MVC   NWIDNUM,12(R3)      MOVE USER ID NUM TO LIST                     
         B     GETI5X                                                           
*                                                                               
GETI5C   OI    NWIDTYP,NWIDLIQ     OLD ELEMENT CONTAINS USERID LIST             
         MVC   NWIDNAM,4(R3)                                                    
         B     GETI5X                                                           
*                                                                               
GETI5D   OI    NWIDTYP,NWIDAGQ     OLD ELEMENT CONTAINS AGENCY NAME             
         MVC   NWIDNAM(2),4(R3)                                                 
*                                                                               
GETI5X   CLI   FILLER,0            RETURN LIST TYPE IN NEW FILLER               
         BNE   *+10                                                             
         MVC   FILLER+1(1),NWIDTYP                                              
         TM    NWIDTYP,NWIDAGQ     TEST AGENCY NAME                             
         BO    GETI15                                                           
         TM    NWIDTYP,NWIDLIQ     TEST USERID LIST                             
         BO    GETI10                                                           
*                                                                               
GETI6    CLI   LISTMODE,C'Y'       WAS LIST USERID ELEMENT FOUND                
         BE    GETI8                                                            
GETI6A   LA    RF,NWIDNAM+7        TEST FOR WILD CARD XX* USERID                
         LA    RE,8                                                             
GETI6AA  CLI   0(RF),C'*'                                                       
         BNE   GETI6AB                                                          
         BCTR  RE,0                SAVE LENGTH OF WILD CARD CHARACTERS          
         STC   RE,WILDCLEN                                                      
         B     GETI20              PROCESS WILD CARD ID                         
GETI6AB  CLI   0(RF),C' '                                                       
         BNE   GETI6AC             PROCESS SINGLE USERID                        
         BCTR  RF,0                                                             
         BCT   RE,GETI6AA                                                       
GETI6AC  CLI   VALOPT,0            TEST VALIDATE ONLY OPTION                    
         BE    GETI6F                                                           
         ICM   R1,7,PIDFLD         R1=A(USER ID VALUE)                          
         BZ    GETIERR1                                                         
GETI6B   CLI   VALOPT,C'N'                                                      
         BE    GETI6D                                                           
         CLC   NWIDNAM,0(R1)       MATCH ON ID ALPHA                            
         BNE   GETI6BA                                                          
         OI    MATCH,X'01'         SET EXACT MATCH                              
         MVC   IDNAME,NWIDNAM                                                   
         MVC   IDNAME+8(2),FILLER                                               
         MVC   IDNUM,NWIDNUM                                                    
         LA    R3,IDNAME                                                        
         ST    R3,PBLOCK-1                                                      
         B     GETIX                                                            
*                                                                               
GETI6BA  CLC   NWIDNAM,=CL8'ALL'                                                
         BNE   *+12                                                             
         OI    MATCH,X'02'         SET MATCH BECAUSE ALL IDS IS IN LIST         
         B     GETIX                                                            
         B     GETI6G                                                           
GETI6D   CLC   NWIDNUM,0(R1)       MATCH ON ID NUMBER                           
         BNE   *+12                                                             
         OI    MATCH,X'01'                                                      
         B     GETIX                                                            
         CLC   NWIDNAM,=CL8'ALL'                                                
         BNE   GETI6G                                                           
         OI    MATCH,X'02'                                                      
         B     GETIX                                                            
GETI6F   MVC   00(8,R4),NWIDNAM    MOVE USER ID NAME TO LIST                    
         MVC   08(2,R4),FILLER                                                  
         MVC   10(2,R4),NWIDNUM    MOVE USER ID NUM TO LIST                     
         LA    R4,12(R4)                                                        
GETI6G   LA    R5,1(R5)                                                         
         BCT   R8,GETI4                                                         
         B     GETIERR8                                                         
*                                                                               
GETI8    SR    R1,R1               DELETE AN ENTRY FROM LIST                    
         ICM   R1,7,PBLOCK                                                      
         BZ    GETI6A                                                           
         LTR   RE,R5                                                            
         BZ    GETI6A                                                           
GETI8A   CLC   0(8,R1),NWIDNAM                                                  
         BE    *+16                                                             
         LA    R1,12(R1)                                                        
         BCT   RE,GETI8A                                                        
         B     GETI6A              IF N/F IN LIST-ADD TO LIST                   
         SH    RE,=H'1'                                                         
         BZ    GETI8C                                                           
         MVC   0(12,R1),12(R1)     ELSE DELETE ENTRY                            
         LA    R1,12(R1)                                                        
         BCT   RE,*-10                                                          
GETI8C   LR    R4,R1               AND RESET POINTERS                           
         BCTR  R5,0                                                             
         LA    R8,1(R8)                                                         
         B     GETI4                                                            
         EJECT                                                                  
GETI10   XC    KEY,KEY             GET AN INCLUDE LIST                          
         MVI   KEY,C'W'                                                         
         MVI   KEY+17,C'I'                                                      
         MVC   KEY+18(6),NWIDNAM                                                
         SR    RF,RF                                                            
         ICM   RF,7,PDATAMGR                                                    
         GOTO1 (RF),DMCB,(X'08',=C'DMREAD'),=C'CTFILE',KEY,KEY                  
         CLI   DMCB+8,0                                                         
         BE    GETI10A                                                          
         TM    DMCB+8,X'02'        TEST DELETED RECORD                          
         BO    GETIERR2                                                         
         B     GETIERR4                                                         
GETI10A  LA    R6,KEY+28                                                        
GETI10B  CLI   0(R6),0                                                          
         BE    GETI10H                                                          
         CLI   0(R6),X'A4'                                                      
         BE    GETI10E                                                          
GETI10C  ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETI10B                                                          
*                                                                               
GETI10E  CLI   VALOPT,0            TEST VALIDATE ONLY OPTION                    
         BE    GETI10F                                                          
         ICM   R1,7,PIDFLD         R1=A(USER ID VALUE)                          
         BZ    GETIERR1                                                         
GETI10E1 CLI   VALOPT,C'N'                                                      
         BE    GETI10E3                                                         
         CLC   3(8,R6),0(R1)       MATCH ON ID ALPHA                            
         BNE   GETI10E2                                                         
         OI    MATCH,X'01'         SET EXACT MATCH                              
         MVC   IDNAME(8),3(R6)                                                  
         MVC   IDNAME+8(2),FILLER                                               
         MVC   IDNUM,13(R6)                                                     
         LA    R6,IDNAME                                                        
         ST    R6,PBLOCK-1                                                      
         B     GETIX                                                            
*                                                                               
GETI10E2 CLC   3(8,R6),=CL8'ALL'                                                
         BNE   GETI10G                                                          
         OI    MATCH,X'02'                                                      
         B     GETIX                                                            
GETI10E3 CLC   13(2,R6),0(R1)      MATCH ON ID NUMBER                           
         BNE   *+12                                                             
         OI    MATCH,X'01'                                                      
         B     GETIX                                                            
         CLC   3(8,R6),=CL8'ALL'                                                
         BNE   *+12                                                             
         OI    MATCH,X'02'                                                      
         B     GETIX                                                            
         B     GETI10G                                                          
*                                                                               
GETI10F  MVC   0(12,R4),3(R6)      ADD ENTRY TO LIST                            
         LA    R4,12(R4)                                                        
GETI10G  LA    R5,1(R5)            BUMP NUMBER OF ITEMS IN LIST                 
         BCT   R8,GETI10C                                                       
         B     GETIERR8                                                         
GETI10H  MVI   LISTMODE,C'Y'       SET IN LIST MODE                             
         B     GETI4                                                            
         EJECT                                                                  
GETI15   XC    KEY,KEY             GET AN AGENCY USER ID RECORD                 
         MVI   KEY,X'9B'                                                        
         MVI   KEY+1,X'01'                                                      
         MVC   KEY+2(2),NWIDNAM    SET AGENCY ALPHA                             
         MVC   SAVEKEY,KEY                                                      
         SR    RF,RF                                                            
         ICM   RF,7,PDATAMGR                                                    
         LA    R0,=C'DMRDHI'                                                    
GETI15A  GOTO1 (RF),DMCB,(X'08',(R0)),=C'CTFILE',KEY,KEY                        
         CLC   KEY(4),SAVEKEY                                                   
         BNE   GETI15J                                                          
         CLI   DMCB+8,0                                                         
         BE    GETI15B                                                          
         TM    DMCB+8,X'02'        IGNORE DELETED RECORDS                       
         BO    GETI15H                                                          
         B     GETIERR4                                                         
GETI15B  LA    R6,KEY+28                                                        
GETI15C  CLI   0(R6),0                                                          
         BE    GETI15H                                                          
         CLI   0(R6),X'02'         DESCRIPTION ELEMENT HAS USER ID NAME         
         BE    GETI15E                                                          
GETI15D  ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETI15C                                                          
*                                                                               
GETI15E  CLI   VALOPT,0            TEST VALIDATE ONLY OPTION                    
         BE    GETI15F                                                          
         ICM   R1,7,PIDFLD         R1=A(USER ID VALUE)                          
         BZ    GETIERR1                                                         
GETI15E1 CLI   VALOPT,C'N'                                                      
         BE    GETI15E3                                                         
         CLC   2(8,R6),0(R1)       MATCH ON ID ALPHA                            
         BNE   GETI15G                                                          
GETI15E2 OI    MATCH,X'01'         SET EXACT MATCH                              
         MVC   IDNAME,2(R6)                                                     
         MVC   IDNUM,KEY+23                                                     
         LA    R6,IDNAME                                                        
         ST    R6,PBLOCK-1                                                      
         B     GETIX                                                            
GETI15E3 CLC   KEY+23(2),0(R1)     MATCH ON ID NUMBER                           
         BNE   GETI15G                                                          
         B     GETI15E2                                                         
*                                                                               
GETI15F  MVC   00(8,R4),2(R6)      ADD ENTRY TO LIST                            
         MVC   08(2,R4),FILLER                                                  
         MVC   10(2,R4),KEY+23                                                  
         LA    R4,12(R4)                                                        
GETI15G  LA    R5,1(R5)            BUMP NUMBER OF ITEMS IN LIST                 
         BCT   R8,GETI15H                                                       
         B     GETIERR8                                                         
GETI15H  LA    R0,=C'DMRSEQ'       SET TO READ NEXT AGENCY USERID               
         B     GETI15A                                                          
GETI15J  MVI   LISTMODE,C'Y'       SET IN LIST/AGYALPH MODE                     
         B     GETI4                                                            
         EJECT                                                                  
GETI20   CLI   VALOPT1,C'S'        WILD CARD - TEST SYNONMY PASSED              
         BNE   GETI30                                                           
         SR    RF,RF               GET WILD CARD LENGTH                         
         IC    RF,WILDCLEN                                                      
         AHI   RF,-1                                                            
         BM    GETI4               IGNORE INVALID WILD CARDS                    
         SR    RE,RE                                                            
         ICM   RE,7,PIDFLD         GET A(USERID FIELD)                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),NWIDNAM                                                  
         BE    GETI20A                                                          
         SR    RE,RE                                                            
         ICM   RE,7,PSYNFLD        GET A(USERID SYNONYM FIELD)                  
         BZ    GETI4                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),NWIDNAM                                                  
         BE    GETI20A                                                          
         B     GETI4               BACK FOR NEXT ID IN LIST                     
GETI20A  OI    MATCH,X'04'         SET MATCHED ON WILD CARD COMPARE             
         B     GETIX               EXIT ON WILD CARD MATCH                      
         EJECT                                                                  
*READ ALL IDS THAT MATCH WILD CARD UNLESS PVALOPT 'W' WHEN JUST RETURN          
*SYNONYMS AND WILD CARD ITSELF                                                  
*                                                                               
GETI30   XC    KEY,KEY                                                          
         MVI   KEY,C'I'                                                         
         MVC   KEY+15(8),NWIDNAM                                                
         MVC   KEY+23(2),SPACES                                                 
         SR    RF,RF                                                            
         ICM   RF,7,PDATAMGR                                                    
         GOTO1 (RF),DMCB,(X'08',=C'DMRDHI'),=C'CTFILE',KEY,KEY                  
         B     GETI30B                                                          
GETI30A  SR    RF,RF                                                            
         ICM   RF,7,PDATAMGR                                                    
         GOTO1 (RF),DMCB,(X'08',=C'DMRSEQ'),=C'CTFILE',KEY,KEY                  
GETI30B  TM    DMCB+8,X'FF'-X'02'                                               
         BNZ   GETI30X                                                          
         TM    DMCB+8,X'02'        TEST DELETED RECORD                          
         BNZ   GETI30A                                                          
         CLI   KEY,C'I'                                                         
         BNE   GETI30X                                                          
         SR    RF,RF                                                            
         ICM   RF,1,WILDCLEN                                                    
         BZ    GETI30X                                                          
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   NWIDNAM(0),KEY+15                                                
         BNE   GETI30X                                                          
         XC    IDNUM,IDNUM                                                      
         MVC   IDNAME,KEY+15                                                    
         LA    R6,KEY+28                                                        
         SR    R1,R1                                                            
GETI30B1 CLI   0(R6),0                                                          
         BE    GETI30B5                                                         
         CLI   0(R6),X'02'                                                      
         BE    GETI30B3                                                         
         CLI   0(R6),X'07'                                                      
         BE    GETI30B4                                                         
GETI30B2 IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETI30B1                                                         
*                                                                               
GETI30B3 MVC   IDNUM,2(R6)         SAVE ID NUMBER                               
         B     GETI30B2                                                         
*                                                                               
GETI30B4 EQU   *                   PROCESS ID OPTIONS ELEMENT                   
         USING CTIDOD,R6                                                        
         TM    CTIDOFL1,CTIDOFSN   TEST FOR SYNONMYM ID NAME/NUMBER             
         BZ    GETI30B5                                                         
         MVC   IDNUM,CTIDOSNU                                                   
         MVC   IDNAME,CTIDOSID                                                  
         OC    IDNUM,IDNUM                                                      
         BZ    GETI30X                                                          
         B     GETI30C                                                          
*                                                                               
GETI30B5 EQU   *                   PROCESS ID OPTIONS ELEMENT                   
         CLI   VALOPT1,C'W'        'W' OPTION - ONLY RETURN SYNONYMS            
         BE    GETI30A                                                          
         TM    CTIDOFL1,CTIDOFWC   TEST FOR WILD CARDING ALLOWED                
         BZ    GETI30A                                                          
         OC    IDNUM,IDNUM                                                      
         BZ    GETI30X                                                          
         B     GETI30C                                                          
         DROP  R6                                                               
*                                                                               
GETI30C  CLI   VALOPT,0            TEST VALIDATE ONLY OPTION                    
         BE    GETI30F                                                          
         ICM   R1,7,PIDFLD         R1=A(USER ID VALUE)                          
         BZ    GETIERR1                                                         
GETI30C1 CLI   VALOPT,C'N'                                                      
         BE    GETI30C2                                                         
         CLC   IDNAME,0(R1)        MATCH ON ID ALPHA                            
         BNE   GETI30G                                                          
         OI    MATCH,X'01'                                                      
         B     GETI30C3                                                         
GETI30C2 CLC   IDNUM,0(R1)         MATCH ON ID NUMVBER                          
         BNE   GETI30G                                                          
GETI30C3 LA    R4,IDNAME           RETURN A(IDNAME/IDNUM)                       
         ST    R4,PBLOCK-1                                                      
         OI    MATCH,X'01'                                                      
         B     GETIX                                                            
*                                                                               
GETI30F  EQU   *                                                                
         SR    R1,R1               CHECK ENTRY NOT ALREADY IN LIST              
         ICM   R1,7,PBLOCK                                                      
         BZ    GETI30FB                                                         
         LTR   RE,R5                                                            
         BZ    GETI30FB                                                         
GETI30FA CLC   0(8,R1),IDNAME                                                   
         BE    GETI30A                                                          
         LA    R1,12(R1)                                                        
         BCT   RE,GETI30FA                                                      
*                                                                               
GETI30FB MVC   00(8,R4),IDNAME     ADD ENTRY TO LIST                            
         MVC   08(2,R4),FILLER                                                  
         MVC   10(2,R4),IDNUM                                                   
         LA    R4,12(R4)                                                        
GETI30G  LA    R5,1(R5)            BUMP NUMBER OF ITEMS IN LIST                 
         BCT   R8,GETI30A                                                       
         B     GETIERR8                                                         
*                                                                               
GETI30X  EQU   *                                                                
         CLI   VALOPT1,C'W'        'W' OPTION - RETURN WILD CARD                
         BNE   GETI4               RETURN TO PROCESS NEXT ID IN LIST            
         MVC   00(8,R4),NWIDNAM    ADD ENTRY TO LIST                            
         MVC   08(2,R4),FILLER                                                  
         XC    10(2,R4),10(R4)                                                  
         LA    R4,12(R4)                                                        
         LA    R5,1(R5)            BUMP NUMBER OF ITEMS IN LIST                 
         BCT   R8,GETI4            RETURN TO PROCESS NEXT ID IN LIST            
         B     GETIERR8                                                         
         EJECT                                                                  
GETIX    STCM  R5,1,PIDTYPE        RETURN NUMBER OF ENTRIES IN LIST             
         STCM  R5,2,PVALOPT        SET FOR >256 VALUES                          
         CLI   PIDTYPE,X'FF'       TRY NOT TO RETURN AN ERROR                   
         BNE   *+8                                                              
         MVI   PIDTYPE,X'FE'                                                    
GETIX1   CLI   VALOPT1,0           TEST SPECIAL CALL ("S" OR "W")               
         BE    GETIX2                                                           
         CLI   VALOPT1,C'W'                                                     
         BE    GETIX2                                                           
         MVC   PMATCH,MATCH        RETURN MATCHED BYTE FOR "S" CALL             
         CLI   FILLER,0                                                         
         BNE   GETIXX                                                           
         TM    FILLER+1,NWIDROQ    TEST IF MATCHED ON READ-ONLY ELEMENT         
         BZ    GETIXX                                                           
         OI    PMATCH,X'08'        SET MATCHED READ-ONLY WILD CARD              
         B     GETIXX              SET END OF LIST FLAG                         
*                                                                               
GETIX2   CLI   VALOPT,0            TEST VALIDATE CALL ("A" OR "N")              
         BE    GETIXX                                                           
         MVC   PMATCH,MATCH        RETURN MATCHED BYTE                          
         CLI   FILLER,0                                                         
         BNE   GETXIT                                                           
         TM    FILLER+1,NWIDROQ    TEST IF MATCHED ON READ-ONLY ELEMENT         
         BZ    GETXIT                                                           
         OI    PMATCH,X'08'        SET MATCHED READ-ONLY                        
         B     GETXIT                                                           
*                                                                               
GETIXX   MVI   0(R4),X'FF'         SET END OF LIST FLAG                         
*                                                                               
GETXIT   XMOD1 1                                                                
         EJECT                                                                  
SPACES   DC    CL10' '                                                          
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
ELCODE   DS    X                                                                
LISTMODE DS    C                                                                
VALOPT   DS    X                                                                
VALOPT1  DS    X                                                                
MATCH    DS    X                                                                
WILDCLEN DS    X                                                                
FILLER   DS    CL2                 LAST TWO CHRS OF 10 BYTE LIST ENTRY          
*                                                                               
NWIDEL   DS    XL1                 ELEMENT CODE X'20'                           
NWIDLEN  DS    XL1                 ELEMENT LENGTH                               
NWIDTYP  DS    XL1                 ELEMENT TYPE                                 
NWIDNEQ  EQU   X'80'               NEW ELEMENT - FIRST BIT MUST BE ZERO         
NWIDROQ  EQU   X'40'               READ ONLY ITEM                               
NWIDNUQ  EQU   X'20'               LAST TWO BYTES OF EL IS AN ID NUMBER         
NWIDAGQ  EQU   X'02'               NAME IS AN AGENCY                            
NWIDLIQ  EQU   X'01'               NAME IS A LIST                               
NWIDNAM  DS    CL8                 NAME (DEPENDS ON TYPE)                       
NWIDNUM  DS    XL2                 ID NUMBER                                    
*                                                                               
IDNAME   DS    CL10                MINI LIST FOR VALIDATE CALL                  
IDNUM    DS    XL2                                                              
IDNUMX   DS    X                                                                
*                                                                               
SAVEKEY  DS    CL25                                                             
KEY      DS    CL25                                                             
IO       DS    1000C                                                            
*                                                                               
WORKX    DS    0C                                                               
*                                                                               
WBLOCK   DS    250CL12                                                          
WBLOCKNQ EQU   (*-WBLOCK)/12                                                    
WBLOCKX  DS    0C                                                               
         SPACE 2                                                                
PLISTD   DSECT                                                                  
PIDTYPE  DS    C                                                                
PIDREC   DS    AL3                                                              
*                                                                               
PERRNUM  DS    X                                                                
PBLOCK   DS    AL3                                                              
*                                                                               
PVALOPT  DS    C                   N=NUMBER,A=ALPHA,S=SYNONYM                   
PDATAMGR DS    AL3                                                              
*                                                                               
PMATCH   DS    X                   RETURN MATCHED VALUE                         
PIDFLD   DS    AL3                 ID NUMBER OR ID ALPHA                        
*                                                                               
         DS    X                                                                
PSYNFLD  DS    AL3                 SYNONYM ALPHA                                
         EJECT                                                                  
*CTGENIDO - ID OPTIONS ELEMENT                                                  
       ++INCLUDE CTGENIDO                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDGETIDS  06/09/04'                                      
         END                                                                    
