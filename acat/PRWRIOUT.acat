*          DATA SET PRWRIOUT   AT LEVEL 086 AS OF 01/15/09                      
*CATALP PRWRIOUT                                                                
         TITLE 'PRWRIOUT - OUTLET FILTER - ENTRY POINTS'                        
         ENTRY HOUTLET                                                          
         ENTRY IOUTMKT                                                          
         ENTRY OOUTMKT                                                          
         ENTRY OUTFLT                                                           
PRWRIOUT CSECT                                                                  
         TITLE 'PRWRIOUT - CHANGE LOG'                                          
         TITLE 'PRWRIOUT - COMMONALITIES '                                      
***********************************************************************         
*                                                                     *         
*        HOUTLET   ADD OUTLET PERCENT TO COLUMN HEADLINE              *         
*        IOUTMKT   KEYWORD FOR OUTLET MARKET CODE - INPUT             *         
*        IOUTMKT   KEYWORD FOR OUTLET MARKET CODE - OUTPUT            *         
*        OUTFLT    FIND OUTLET PERCENTAGE AND APPLY                   *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT  AREA                                    *         
*        R3==>  DRIVER OUTPUT AREA                                    *         
*        R4==>  COMMON AREA                                           *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
***********************************************************************         
         TITLE 'PRWRIOUT - OUTLET MARKET ID - IOUTMKT'                          
***********************************************************************         
*                                                                     *         
*        OUTLET MARKET ID                                             *         
*        INPUT ROUTINE                                                *         
*        CONSTRUCTED FROM REGION AND DISTRICT CODES                   *         
*        BASED ON FORMULA IN B9 PROFILE                               *         
*        NAME IS FOUND IN ACCFILE RECORD                              *         
*                                                                     *         
*EXIT    R2==> CL6(CODE)                                              *         
*              OR CL20(NAME),CL6(CODE)                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         TITLE 'PRWRIOUT - CONSTRUCT OUTLET MARKET CODE IOUTMKT'                
         REQUS                                                                  
         DS    0D                                                               
IOUTMKT  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT IN TABLE                
         BZ    IOMGET10            TABLE IS EMPTY                               
*                                                                               
         USING MKTTBLD,R5          ESTABLISH ENTRY IN MKT TABLE                 
*                                                                               
         CLC   PBRGN,MTBRGN        SKIP IF NO CHANGE IN REGION                  
         BNE   *+10                                                             
         CLC   PBDST,MTBDST        OR DISTRICT                                  
         BE    IOMGETX                                                          
*                                                                               
IOMGET10 DS    0H                                                               
*                                                                               
         BRAS  RE,MKTGET           FILL IN MARKET FIELDS                        
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT IN TABLE                
         BZ    IOUTMKTX            NOTHING FOUND                                
*                                                                               
IOMGETX  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'A'       IF NOT SORTING ON NAME                       
         BE    *+14                                                             
         MVC   0(L'MTBMKTCD,R2),MTBMKTCD   RETURN MARKET CODE                   
         B     IOUTMKTX                                                         
*                                  ELSE                                         
         MVC   0(L'MTBMKTNM,R2),MTBMKTNM           RETURN MARKET NAME           
         MVC   L'MTBMKTNM(L'MTBMKTCD,R2),MTBMKTCD   ADD ON MKT CODE             
*                                                                               
IOUTMKTX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIOUT - OUTLET MARKET ID - OOUTMKT'                          
***********************************************************************         
*                                                                     *         
*        OUTLET MARKET ID                                             *         
*        OUTPUT ROUTINE                                               *         
*        SAVE LATEST MARKET CODE AND PERCENTAGES FOR HEADLINE ROUTINE *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         DS    0D                                                               
OOUTMKT  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         LA    R1,0(R2)            ASSUME MKT CODE FIRST                        
*                                                                               
         CLI   GLARGS+1,C'A'       IF SORTING BY NAME                           
         BNE   *+8                                                              
         LA    R1,L'MTBMKTNM(R2)      MKT CODE AFTER NAME                       
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT TABLE ENTRY             
         BZ    OOMFND                 FIRST TIME                                
*                                                                               
         USING MKTTBLD,R5          ESTABLISH ENTRY IN MKT TABLE                 
*                                                                               
         CLC   MTBMKTCD,0(R1)      SKIP IF MARKET CODE UNCHANGED                
         BE    OOMFNDX                                                          
*                                                                               
OOMFND   DS    0H                                                               
*                                                                               
         MVC   CMMKTCD,0(R1)       SAVE MARKET CODE                             
*                                                                               
         BRAS  RE,MKTFIND          FIND MARKET IN TABLE                         
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT TABLE ENTRY             
         BZ    OOUTMKTX            NONE FOUND                                   
*                                                                               
OOMFNDX  DS    0H                                                               
*                                                                               
         MVC   CODEAREA,SPACES     INIT CODE AREA                               
         MVC   NAMEAREA,SPACES     INIT NAMEAREA                                
*                                                                               
         CLI   GLARGS+1,C'B'       IF CODE WANTED                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'C'                                                    
         BNE   *+10                                                             
         MVC   CODEAREA(L'MTBMKTCD),MTBMKTCD  RETURN MKT CODE                   
*                                                                               
         CLI   GLARGS+1,C'C'       SKIP IF CODE                                 
         BE    *+10                                                             
         CLI   GLARGS+1,C'X'       SKIP IF NO PRINT                             
         BE    *+10                                                             
         MVC   NAMEAREA(L'MTBMKTNM),MTBMKTNM  RETURN MKT NAME                   
*                                                                               
         GOTOR GENOUTA             GENERAL EXIT                                 
*                                                                               
OOUTMKTX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIOUT - GET MARKET DATA - MKTGET'                            
***********************************************************************         
*                                                                     *         
*        GET MARKET DATA FROM ACCFILE                                 *         
*        MARKET ID IS COMBO OF REGIO & DISTRICT CODES                 *         
*           BASED ON FORMULA IN B9 PROFILE                            *         
*        ID IS USED TO READ ACCFILE RECORD FOR MARKET NAME            *         
*           AND OUTLET PERCENTAGES                                    *         
*        ALL DATA IS SAVED IN A TABLE FOR OUTPUT                      *         
*                                                                     *         
*        R2==> RECORDS                                                *         
*        R3==> ELEMENTS IN RECORDS                                    *         
*        R4==> COMMON AREA                                            *         
*        R5==> MKTTBLD                                                *         
*        R6==> MKBOUT                                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
MKTGET   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         L     RF,PBUTLA           SAVE CURRENT SE NUMBER                       
         MVC   CMSESV,4(RF)                                                     
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        IF FIRST TIME                                                *         
*           INIT BINSRCH PARAMETERS                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MKGBSRP  DS    0H                                                               
*                                                                               
         OC    CMBSRP,CMBSRP       SKIP IF PARMS ALREADY INITIALIZED            
         BNZ   MKGBSRPX                                                         
*                                                                               
         L     RF,=A(CMMKTTBC)     SET TABLE START - RESERVE 1ST SPOT           
         ST    RF,BSPATAB-BSPPRMS+CMBSRP                                        
*                                                                               
         LA    RF,MTBENTLQ         SET ENTRY LENGTH                             
         ST    RF,BSPLENR-BSPPRMS+CMBSRP                                        
*                                                                               
         LA    RF,L'MTBMKTCD       SET KEY LENGTH                               
         ST    RF,BSPLENK-BSPPRMS+CMBSRP                                        
*                                                                               
         SR    RF,RF                                                            
         LHI   RF,MTBMAX#Q         SET # OF AVAILABLE ENTRIES TO MAX            
         ST    RF,BSPMAX-BSPPRMS+CMBSRP                                         
*                                                                               
MKGBSRPX DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        READ PROFILES                                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MKGB9    DS    0H                                                               
*                                                                               
*        READ B9 PROFILE                                                        
*                                                                               
         XC    CMB9KY,CMB9KY       INIT PROFILE KEY                             
*                                                                               
         MVI   CMB9SYS,C'P'         SET SYSTEM                                  
         MVI   CMB9SYS+1,C'0'                                                   
         MVC   CMB9PID,=C'B9'       B9 - PROFILE                                
         MVC   CMB9AGY,PBAGY        AGENCY                                      
         MVC   CMB9MED,PBMED        MEDIA                                       
         MVC   CMB9CLT,PBCLT        CLIENT                                      
*                                                                               
*        CHECK IF OFFICE KNOWN                                                  
*                                                                               
         CLI   PBQCLT,C'*'         IF REQUESTED BY OFFICE                       
         BNE   *+14                                                             
         MVC   CMB9OFC,PBQCLT+1       USE IT                                    
         B     MKGB9OF9                                                         
*                                                                               
         CLI   PBCOFF,C' '        IF CLIENT OFFICE KNOWN USE IT                 
         BNH   *+10                                                             
         MVC   CMB9OFC,PBCOFF       CLIENT OFFICE CODE                          
*                                                                               
         CLI   PBQSLAVE,C'Y'        IF DOING SLAVES                             
         BNE   MKGB9SLX                                                         
*                                                                               
         ICM   RF,15,PBCLTADR     POINT TO CLIENT RECORD                        
         BZ    MKGB9SLX              NOT KNOWN                                  
*                                                                               
         USING PCLTRECD,RF                                                      
*                                                                               
         CLI   PCLTOFF,C' '       IF SLAVE CLIENT OFFICE KNOWN                  
         BNH   *+10                  USE IT                                     
         MVC   CMB9OFC,PCLTOFF       SLAVE OFFICE CODE                          
*                                                                               
         DROP  RF                                                               
*                                                                               
MKGB9SLX DS    0H                                                               
*                                                                               
MKGB9OF9 DS    0H                                                               
*                                                                               
         CLI   CMB9OFC,C' '       IF OFFICE PRESENT                             
         BNH   *+8                                                              
         MVI   CMB9STAR,C'*'         INDICATE IN PARAMETERS                     
*                                                                               
MKGB9OFX DS    0H                                                               
*                                                                               
         CLC   CMB9KYS,CMB9KY      IF NOT NEW KEY ALL OF SETUP DONE             
         BE    MKGOPENX                                                         
*                                                                               
         MVC   CMB9KYS,CMB9KY         SAVE NEW KEY                              
*                                                                               
         XC    CMB9PRF,CMB9PRF        INIT PROFILE AREA                         
*                                                                               
         GOTO1 GETPROF,DMCB,CMB9KY,CMB9PRF,DATAMGR  GET PROFILE                 
*                                                                               
         MVC   CMACCCD,CMB9PRF+0   SAVE COMPANY CODE                            
         MVC   CMACCUNT,CMB9PRF+1  SAVE UNIT                                    
         MVC   CMACCLDG,CMB9PRF+2  SAVE ADVERTISER                              
*                                                                               
MKGB9X   DS    0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
***********************************************************************         
*                                                                     *         
*        READ A0 PROFILE FOR AGENCY,MEDIA                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MKGA0    DS    0H                                                               
*                                                                               
         XC    CMA0KY,CMA0KY       INIT A0 KEY                                  
*                                                                               
         MVI   CMA0SYS,C'P'         SET SYSTEM                                  
         MVI   CMA0SYS+1,C'0'                                                   
         MVC   CMA0PID,=C'A0'       A0 - PROFILE                                
         MVC   CMA0AGY,PBAGY        AGENCY                                      
         MVC   CMA0MED,PBMED        MEDIA                                       
         MVC   CMA0CLT,PBCLT        CLIENT                                      
         MVC   CMA0STAR,CMB9STAR    COPY OFFICE OPTION FROM B9 KEY              
         MVC   CMA0OFC,CMB9OFC                                                  
*                                                                               
         CLC   CMA0KYS,CMA0KY      SKIP IF PROFILE ALREADY RETRIEVED            
         BE    MKGA0X                                                           
*                                                                               
*        GET A0 PROFILE                                                         
*                                                                               
         MVC   CMA0KYS,CMA0KY      SAVE PROFILE ID                              
         XC    CMA0PRF,CMA0PRF     INIT A0 PROF                                 
*                                                                               
         GOTO1 GETPROF,DMCB,(X'C0',CMA0KY),CMA0PRF,DATAMGR GET PROF             
*                                                                               
MKGA0X   DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        GET ACCPAK SYSTEM NUMBER AND COMPANY CODE                    *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MKGAUTH  DS    0H                                                               
*                                                                               
         XC    CMACCAGY,CMACCAGY   INIT ACC AGENCY CODE                         
*                                                                               
         USING PCLTRECD,RF                                                      
*                                                                               
         ICM   RF,15,PBCLTADR     POINT TO CLIENT RECORD                        
         BZ    *+10                  NOT KNOWN                                  
         MVC   CMACCAGY,PCLTACCA   SAVE ACC AGENCY ID                           
*                                                                               
         DROP  RF                                                               
*                                                                               
         CLC   PBAGY,=C'H7'       IF MINDSHARE                                  
         BNE   MKGAUTH1                                                         
*                                                                               
         USING PCLTRECD,RF                                                      
*                                                                               
         ICM   RF,15,PBCLTADR     POINT TO CLIENT RECORD                        
         BZ    MKGAUTHX               NOT KNOWN                                 
*                                                                               
         CLI   PCLTOFF,C'O'        AND IF OM ACC FILE                           
         BE    *+8                                                              
         CLI   PCLTOFF,C'A'        AND IF OM ACC FILE                           
         BE    *+8                                                              
         CLI   PCLTOFF,C'G'        AND IF OM ACC FILE                           
         BNE   *+10                                                             
         MVC   CMACCAGY,=C'OA'     SET TO ACC AGENCY OA                         
*                                                                               
         CLI   PCLTOFF,C'J'        AND IF JW ACC FILE                           
         BE    *+8                                                              
         CLI   PCLTOFF,C'T'        AND IF JW ACC FILE                           
         BNE   *+10                                                             
         MVC   CMACCAGY,=C'JW'     SET TO ACC AGENCY OA                         
*                                                                               
         DROP  RF                                                               
*                                                                               
MKGAUTH1 DS    0H                                                               
*                                                                               
         L     RF,PBUTLA           POINT TO UTL                                 
         MVI   4(RF),X'0A'         SET TO THE CONTROL SYSTEM                    
*                                                                               
         XC    CMKEY,CMKEY         INIT KEY AREA                                
         LA    R2,CMKEY            ESTABLISH CT5REC (SYSTEM ACCESS REC)         
         USING CT5REC,R2                                                        
*                                                                               
         MVI   CT5KTYP,CT5KTYPQ    RECORD ID                                    
         MVC   CT5KALPH,PBAGY      SET AGENCY CODE                              
*                                                                               
         CLC   CMA0PRF+14(2),=C'00' IF ALTERNATE AGENCY IN PROFILE              
         BE    *+20                                                             
         CLC   CMA0PRF+14(2),=C'  ' IF ALTERNATE AGENCY IN PROFILE              
         BNH   *+10                                                             
         MVC   CT5KALPH,CMA0PRF+14    USE IT                                    
*                                                                               
         CLC   CMACCAGY,=C'  '     IF ACC AGENCY OVERRIDE EXISTS                
         BNH   *+10                                                             
         MVC   CT5KALPH,CMACCAGY      USE IT                                    
*                                                                               
         CLC   CT5KEYSV,CMKEY      SKIP IF DATA ALEADY RETRIEVED                
         BE    MKGOPENX                                                         
*                                                                               
         MVC   CT5KEYSV,CMKEY      SAVE KEY                                     
*                                                                               
         GOTO1 DATAMGR,DMCB,=CL8'DMREAD',=CL8'CTFILE',CMKEY,CMIO1,0             
         CLI   8(R1),0             NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,CMIO1            POINT TO FOUND RECORD                        
*                                                                               
         LA    R3,CT5DATA          POINT TO FIRST ELEMENT IN RECORD             
         SR    R0,R0                                                            
*                                                                               
MKGAUTHL DS    0H                                                               
*                                                                               
         USING CTSYSD,R3           ESTABLISH SYS AUTH ELEMENT                   
*                                                                               
         CLI   CTSYSEL,0           MUST FIND AUTH ELEMENT                       
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   CTSYSEL,CTSYSELQ    FIND SYSTEM AUTH ELEMENT                     
         BNE   MKGAUTHC                                                         
*                                                                               
         CLI   CTSYSNUM,6          MUST BE FOR ACC SYSTEM                       
         BE    MKGAUTHF                                                         
*                                                                               
MKGAUTHC DS    0H                                                               
*                                                                               
         IC    R0,CTSYSLEN         BUMP TO NEXT ELEMENT                         
         AR    R3,R0                                                            
         B     MKGAUTHL                                                         
*                                                                               
MKGAUTHF DS    0H                                                               
*                                                                               
         CLC   CMACCSE,CTSYSSE     SKIP OPEN IF SAME SE NUMBER                  
         BE    MKGOPENX                                                         
*                                                                               
         MVC   CMACCSE,CTSYSSE     SAVE SE NUMBER                               
*                                                                               
MKGAUTHX DS    0H                                                               
*                                                                               
         L     RF,PBUTLA                                                        
         MVC   4(1,RF),CMACCSE     SET ACCOUNTING SE NUMBER                     
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        OPEN ACCFILES FOR AGENCY                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMOPEN',=C'ACCOUNT',ACCFILES,DMWRK               
*                                                                               
MKGOPENX DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        BUILD MARKET CODE FROM REGION AND DISTRICT CODES             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
*                                                                               
         MVC   CMMKTCD,SPACES      INIT MARKET CODE                             
*                                                                               
         LA    R1,CMMKTCD          POINT TO START OF MARKET CODE                
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,CMB9PRF+3      AMOUNT OF REGION CODE IN MKT CODE            
         BZ    MKGRGNX             NONE                                         
*                                                                               
         LA    RE,L'PBRGN          MAX REGION CODE LENGTH                       
         SR    RE,RF               DISPLACEMENT OF START INTO REG CD            
         LA    RE,PBRGN(RE)        POINT TO START OF REGION PORTION             
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(RE)       ACCUMULATE REGION PART OF CODE               
*                                                                               
         LA    R1,1(RF,R1)         POINT TO NEXT PART OF CODE                   
*                                                                               
MKGRGNX  DS    0H                                                               
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,CMB9PRF+4      AMOUNT OF DISTRICT CODE IN MKT CODE          
         BZ    MKGDSTX             NONE                                         
*                                                                               
         LA    RE,L'PBDST          MAX DISTRICT CODE LENGTH                     
         SR    RE,RF               DISPLACEMENT OF START INTO DST CD            
         LA    RE,PBDST(RE)        POINT TO START OF DISTRICT PORTION           
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(RE)       ACCUMULATE REGION PART OF CODE               
*                                                                               
         LA    R1,1(RF,R1)         POINT TO NEXT PART OF CODE                   
*                                                                               
MKGDSTX  DS    0H                                                               
*                                                                               
*        CHECK IF MARKET ALREADY IN TABLE                                       
*                                                                               
         GOTO1 PBBSRCH,CMBSRP,('BSPFIND',CMMKTCD)   FIND IN TABLE               
*                                                                               
         CLI   BSPCNTL-BSPPRMS+CMBSRP,BSPNF IF NOT FOUND                        
         BE    MKGMKTNF               THEN NOT IN TABLE                         
*                                                                               
         ICM   R5,15,BSPAREC-BSPPRMS+CMBSRP  POINT TO FOUND ENTRY               
         BZ    MKGMKTNF            NONE FOUND                                   
*                                  MARKET ALREADY IN TABLE                      
         ST    R5,CMMKTCUR         SET AS CURRENT MARKET ENTRY                  
*                                                                               
         B     MKTGETX             ALL DONE                                     
*                                                                               
MKGMKTNF DS    0H                  MARKET NOT IN TABLE YET                      
*                                                                               
*        ADD MARKET TO TABLE                                                    
*              FIRST ADD KEY ONLY                                               
*                                                                               
         GOTO1 PBBSRCH,CMBSRP,('BSPADD',CMMKTCD)   ADD TO TABLE                 
*                                                                               
         ICM   R5,15,BSPAREC-BSPPRMS+CMBSRP  POINT TO NEW ENTRY                 
         BNZ   *+6                                                              
         DC    H'0'                MUST GET INTO TABLE                          
*                                                                               
         ST    R5,CMMKTCUR         SAVE ADDRESS                                 
         USING MKTTBLD,R5          ESTABLISH MARKET TABLE                       
*                                                                               
         XC    MTBENTRY(MTBENTLQ),MTBENTRY INIT TABLE ENTRY                     
         MVC   MTBMKTCD,CMMKTCD    SET MARKET CODE                              
*                                                                               
*        READ ACCOUNT RECORDS FOR MARKET NAME AND OUTLET PERCENTAGES            
*                                                                               
         MVC   CMKEY,SPACES        INIT KEY                                     
         LA    R2,CMKEY            ESTABLISH AS ACCOUNT RECORD KEY              
         USING ACTRECD,R2                                                       
*                                                                               
         MVC   ACTKCPY,CMACCCD     COMPANY CODE                                 
         MVC   ACTKUNT,CMACCUNT    UNIT CODE                                    
         MVC   ACTKLDG,CMACCLDG    LEDGER                                       
*                                                                               
         MVC   ACTKACT(4),CMMKTCD  SET MARKET CODE IN KEY                       
*                                                                               
         MVC   CMKEYSV,CMKEY       SAVE KEY                                     
*                                                                               
*        READ FOR RECORD                                                        
*                                                                               
         L     RF,PBUTLA                                                        
         MVC   4(1,RF),CMACCSE     SET ACCOUNTING SE NUMBER                     
*                                                                               
         GOTO1 DATAMGR,DMCB,=CL8'DMRDHI',=CL8'ACCDIR',CMKEY,CMKEY,0             
*                                                                               
         CLC   ACTKEY,CMKEYSV      SKIP IF NOT FOUND                            
         BNE   MKGACT1X                                                         
*                                                                               
         CLI   8(R1),X'02'         SKIP IF DELETED                              
         BE    MKGACT1X                                                         
*                                                                               
         CLI   8(R1),0             NO OTHER ERRORS ALLOWED                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=CL8'GETREC',=CL8'ACCMST',CMKEY+50,CMIO1,  X        
               DMWRK               READ IN RECORD                               
*                                                                               
         CLI   8(R1),0             NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,CMIO1            SWITCH POINTER                               
*                                                                               
         LA    R3,ACTRFST          POINT TO FIRST ELEMENT                       
         USING NAMELD,R3           ESTABLISH AS NAME ELEMENT                    
*                                                                               
         SR    RF,RF                                                            
         BASR  RE,0                SET TOP OF LOOP                              
*                                                                               
         CLI   NAMEL,0             CHECK FOR END OF RECORD                      
         BE    MKGACT1X                                                         
         CLI   NAMEL,NAMELQ        LOOK FOR NAME ELEMENT IN REC                 
         BE    *+14                                                             
         IC    RF,NAMLN            ELEMENT LENGTH                               
         LA    R3,NAMEL(RF)        BUMP TO NEXT ELEMENT                         
         BR    RE                  PROCESS NEXT ELEMENT                         
*                                                                               
         MVC   MTBMKTNM,SPACES     INIT MARKET NAME                             
*                                                                               
         IC    RF,NAMLN            GET ELEMENT LENGTH                           
         AHI   RF,-(NAMEREC-NAMELD)   LENGTH OF NAME                            
*                                                                               
         CHI   RF,L'MTBMKTNM       MAKE SURE WE CAN HOLD NAME                   
         BNH   *+8                                                              
         LHI   RF,L'MTBMKTNM                                                    
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   MTBMKTNM(0),NAMEREC  GET NAME OF MARKET                          
*                                                                               
MKGACT1X DS    0H                                                               
*                                                                               
*        READ FOLLOWING RECORDS FOR OUTLET PERCENTAGES                          
*                                                                               
         LA    R6,MTBOUT           POINT TO FIRST OUTLET AREA                   
         USING MTBOUT,R6           ESTABLISH OUTLET AREA IN TABLE               
*                                                                               
         ZAP   MTBOUTPC,=P'0'      INIT OUTLET PERCENT                          
*                                                                               
MKGACTLP DS    0H                                                               
*                                                                               
         LA    R2,CMKEY            RE-POINT TO KEY AREA                         
*                                  READ NEXT SEQUENTIAL RECORD                  
         GOTO1 DATAMGR,DMCB,=CL8'DMRSEQ',=CL8'ACCDIR',CMKEY,CMKEY,0             
*                                                                               
         CLC   ACTKEY(ACTKACT+4-ACTKEY),CMKEYSV  MATCH ON MARKET CODE           
         BNE   MKGACTDN                                                         
*                                                                               
         CLI   8(R1),X'02'         SKIP IF DELETED                              
         BE    MKGACTC1                                                         
*                                                                               
         CLI   8(R1),0             NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=CL8'GETREC',=CL8'ACCMST',CMKEY+50,CMIO1,  X        
               DMWRK               READ IN RECORD                               
*                                                                               
         CLI   8(R1),0             NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,CMIO1            SWITCH POINTER                               
*                                                                               
         LA    R3,ACTRFST          POINT TO FIRST ELEMENT                       
         USING DSCELD,R3           ESTABLISH DISTRIBUTION SCHEME ELEM           
*                                                                               
*        FIND CORRECT DISTRIBUTION SCHEME ELEMENT                               
*                                                                               
         SR    RF,RF                                                            
         L     RE,PBESTADR         ESTABLISH ESTIMATE RECORD                    
         USING PESTREC,RE                                                       
*                                                                               
MKGDSCLP DS    0H                                                               
*                                                                               
         CLI   DSCEL,0             CHECK FOR END OF RECORD                      
         BE    MKGDSCDN                                                         
*                                                                               
         CLI   DSCEL,DSCELQ        LOOK FOR DISTRIB ELEMENT IN REC              
         BNE   MKGDSCCN                                                         
*                                                                               
         CLC   DSCCODE,PESTRSCH    MATCH ON SCHEME CODE                         
         BE    MKGDSCFD                                                         
*                                                                               
MKGDSCCN DS    0H                                                               
*                                                                               
         IC    RF,DSCLN            ELEMENT LENGTH                               
         LA    R3,DSCEL(RF)        BUMP TO NEXT ELEMENT                         
         B     MKGDSCLP            PROCESS NEXT ELEMENT                         
*                                                                               
MKGDSCFD DS    0H                                                               
*                                                                               
         PACK  DUB,ACTKACT+4(4)    OUTLET NUMBER                                
         CVB   RF,DUB                                                           
         STC   RF,MTBOUT           SET OUTLET NUMBER                            
*                                                                               
         ZAP   MTBOUTPC,DSCVAL     GET OUTLET PERCENTAGE                        
*                                                                               
MKGDSCDN DS    0H                                                               
*                                                                               
         DROP  RE                                                               
*                                                                               
MKGACTCN DS    0H                                                               
*                                                                               
         LA    R6,MTBOUTLQ(R6)     BUMP TO NEXT OUTLET AREA                     
*                                                                               
MKGACTC1 DS    0H                                                               
*                                                                               
         B     MKGACTLP                                                         
*                                                                               
MKGACTDN DS    0H                                                               
*                                                                               
MKTACTX  DS    0H                                                               
*                                                                               
MKTGETX  DS    0H                                                               
*                                                                               
*        MAKE SURE WE ARE POINTING TO PRINT FILES                               
*                                                                               
         CLC   PBRGN,MTBMKTCD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,PBUTLA                                                        
         MVC   4(1,RF),CMSESV      SET PRINT SE NUMBER                          
*                                                                               
         XIT1                                                                   
*                                                                               
ACCFILES DS    0C                  LIST OF ACC SYSTEM FILES TO OPEN             
         DC    CL8' ACCDIR'                                                     
         DC    CL8' ACCMST'                                                     
         DC    CL8'X'              EOL                                          
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
*                                                                               
         TITLE 'PRWRIOUT - FIND MARKET IN TABLE - MKTFIND'                      
***********************************************************************         
*                                                                     *         
*        FIND MARKET IN MARKET TABLE                                  *         
*                                                                     *         
*NTRY    CMMKTCD  MARKET CODE                                         *         
*                                                                     *         
*EXIT    CMMKTCUR   A(ENTRY IN MARKET TABLE)                                    
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
MKTFIND  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         XC    CMMKTCUR,CMMKTCUR   INIT A(TABLE ENTRY)                          
*                                                                               
         GOTO1 PBBSRCH,CMBSRP,('BSPFIND',CMMKTCD)   FIND IN TABLE               
*                                                                               
         CLI   BSPCNTL-BSPPRMS+CMBSRP,BSPNF IF NOT FOUND                        
         BE    MKTFINDX               THEN NOT IN TABLE                         
*                                                                               
         ICM   R5,15,BSPAREC-BSPPRMS+CMBSRP  POINT TO FOUND ENTRY               
         BZ    MKTFINDX            NONE FOUND                                   
*                                                                               
         ST    R5,CMMKTCUR         SET AS CURRENT MARKET ENTRY                  
*                                                                               
MKTFINDX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIOUT - OUTLET HEADLINE ROUTINE - HOUTLET'                   
***********************************************************************         
*                                                                     *         
*        OUTLET HEADLINE ROUTINE                                      *         
*              FIND CURRENT VALUE FOR OUTLET OF COLUMN                *         
*              MUST READ ACC FILE                                     *         
*                                                                     *         
*NTRY    R3 ==>  HEADLINE                                             *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
HOUTLET  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT ENTRY                   
         BZ    HOUTLETX            NONE AVAILABLE                               
         USING MKTTBLD,R5          ESTABLISH TABLE ENTRY                        
*                                                                               
*        FIND OUTLET NUMBER FOR COLUMN                                          
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQCFOUT),0,0                                 
         BNE   HOUTLETX                                                         
*                                                                               
         ICM   R6,15,8(R1)         POINT TO RETURNED FILTER                     
         BZ    HOUTLETX                                                         
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,FLTFVAL-FLTCTL(R6) GET LINE NUMBER                          
         BZ    HOUTLETX                                                         
*                                                                               
*        MATCH OUTLET NUMBER TO ONE IN TABLE                                    
*                                                                               
         LA    R6,MTBOUT           POINT TO FIRST OUTLET AREA                   
         LA    R0,MTBOUT#Q         MAX NUMBER OF OUTLETS                        
*                                                                               
HOUTLOOP DS    0H                                                               
*                                                                               
         USING MTBOUT,R6                                                        
*                                                                               
         CLM   RF,1,MTBOUT         MATCH OUTLET NUMBER                          
         BE    HOUTFND                                                          
*                                                                               
HOUTCONT DS    0H                                                               
         LA    R6,MTBOUTLQ(R6)     BUMP TO NEXT OUTLET                          
         BCT   R0,HOUTLOOP                                                      
*                                                                               
HOUTDONE DS    0H                                                               
         B     HOUTLETX            NO MATCH                                     
*                                                                               
HOUTFND  DS    0H                                                               
*                                                                               
         EDIT  (P3,MTBOUTPC),(6,0(R3)),2,BRACKET=YES PRINT PERCENT              
*                                                                               
HOUTLETX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIOUT - OUTLET FILTER ROUTINE - OUTFLT'                      
***********************************************************************         
*                                                                     *         
*        OUTLET FILTER   ROUTINE                                      *         
*              APPLY OUTLET PERCENT TO AMOUNT AT 0(R2)                *         
*                                                                     *         
*NTRY    R2 ==>  AMOUNT                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
OUTFLT   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,=A(COMMON)       ESTABLISH COMMON AREA                        
         USING COMMON,R4                                                        
*                                                                               
         ICM   R5,15,CMMKTCUR      POINT TO CURRENT MKT ENTRY                   
         BZ    OUTFLTX             NONE AVAILABLE                               
         USING MKTTBLD,R5          ESTABLISH TABLE ENTRY                        
*                                                                               
*        FIND OUTLET NUMBER FOR COLUMN                                          
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQCFOUT),0,0                                 
         BNE   OUTFLTX                                                          
*                                                                               
         ICM   R6,15,8(R1)         POINT TO RETURNED FILTER                     
         BZ    OUTFLTX                                                          
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,FLTFVAL-FLTCTL(R6) GET LINE NUMBER                          
         BZ    OUTFLTX                                                          
*                                                                               
*        MATCH OUTLET NUMBER TO ONE IN TABLE                                    
*                                                                               
         LA    R6,MTBOUT           POINT TO FIRST OUTLET AREA                   
         LA    R0,MTBOUT#Q         NUMBER OF OUTLETS                            
*                                                                               
OUTFLOOP DS    0H                                                               
*                                                                               
         USING MTBOUT,R6                                                        
*                                                                               
         CLM   RF,1,MTBOUT         MATCH OUTLET NUMBER                          
         BE    OUTFFND                                                          
*                                                                               
OUTFCONT DS    0H                                                               
         LA    R6,MTBOUTLQ(R6)     BUMP TO NEXT OUTLET                          
         BCT   R0,OUTFLOOP                                                      
*                                                                               
OUTFDONE DS    0H                                                               
         B     OUTFLTX             NO MATCH                                     
*                                                                               
OUTFFND  DS    0H                                                               
*                                                                               
         ZAP   CMPKWORK,0(8,R2)    COPY INPUT AMOUNT                            
         MP    CMPKWORK,MTBOUTPC   MULTIPLY BY OUTLET %                         
         SRP   CMPKWORK,64-4,5     ROUND TO 2 DECIMALS                          
         ZAP   0(8,R2),CMPKWORK    RETURN ALTERED AMOUNT                        
*                                                                               
OUTFLTX DS     0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIOUT - COMMON AREA - COMMON'                                
***********************************************************************         
*                                                                     *         
*        DATA COMMON TO ALL ROUTINES                                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
COMMON   DS    0D                  COMMON AREA                                  
*                                                                               
*        KEY AREAS                                                              
*                                                                               
CMKEY    DS    XL64                KEY BUILD AREA                               
CMKEYSV  DS    XL64                KEY SAVEAREA                                 
*                                                                               
DMWRK    DS    XL64                DATAMGR WORKAREA                             
*                                                                               
*        KEY FOR GETTING B9 PROFILE                                             
*                                                                               
CMB9KY   DS    XL16                PROFILE KEY                                  
         ORG   CMB9KY                                                           
CMB9SYS  DS    CL1                 SYSTEM                                       
         DS    XL1                 NOT USED                                     
CMB9PID  DS    CL2                 PROFILE ID                                   
CMB9AGY  DS    CL2                 AGENCY                                       
CMB9MED  DS    CL1                 MEDIA                                        
CMB9CLT  DS    CL3                 CLIENT                                       
CMB9STAR DS    CL1                 OFFICE INDICATOR                             
CMB9OFC  DS    CL1                 OFFICE CODE                                  
         ORG                                                                    
*                                                                               
CMB9KYS DS     XL(L'CMB9KY)        PROFILE KEY SAVEAREA                         
*                                                                               
CMB9PRF   DS   XL16                B9 PROFILE                                   
*                                                                               
*        KEY FOR GETTING A0 PROFILE                                             
*                                                                               
CMA0KY   DS    XL16                PROFILE KEY                                  
         ORG   CMA0KY                                                           
CMA0SYS DS     CL1                 SYSTEM                                       
         DS    XL1                 NOT USED                                     
CMA0PID DS     CL2                 PROFILE ID                                   
CMA0AGY DS     CL2                 AGENCY                                       
CMA0MED DS     CL1                 MEDIA                                        
CMA0CLT DS     CL3                 CLIENT                                       
CMA0STAR DS    CL1                 OFFICE INDICATOR                             
CMA0OFC DS     CL1                 OFFICE CODE                                  
*                                                                               
CMA0KYS DS     XL(L'CMA0KY)        PROFILE KEY SAVEAREA                         
*                                                                               
CMA0PRF  DS    XL16                A0  PROFILE                                  
*                                                                               
CMSESV   DS    XL1                 SE NUMBER SAVEAREA                           
*                                                                               
CMMKTCD  DS    XL6                 CURRENT MARKET CODE                          
*                                                                               
CMMKTCUR DS    A                   A(CURRENT ENTRY IN MARKET TABLE)             
*                                                                               
CMPKWORK DS    PL16                PACKED NUMBER WORKAREA                       
*        ACCFILE DATA                                                           
*                                                                               
CMACCSE  DS    XL1                 ACCFILE SE NUMBER                            
*                                                                               
CMACCCD  DS    XL1                 COMPANY CODE                                 
CMACCLDG DS    CL1                 ADVERTISER CODE                              
CMACCAGY DS    CL2                 ACC AGENCY CODE                              
CMACCUNT DS    CL1                 UNI CODE                                     
*                                                                               
         DS    0D                  ALIGNMENT                                    
CT5KEYSV DS    CL32                                                             
*                                                                               
*        BINSRCH PARMS FOR MARKET TABLE                                         
*                                                                               
         DS    0D                  ALIGNMENT                                    
CMBSRP   DC    XL(BSPPRML)'00'     PARMS FOR MKT TABLE                          
*                                                                               
CMIO1    DS    0D                                                               
         DS    XL4096              I/O AREA                                     
*                                                                               
CMMKTTBC DS    XL(MTBMAX#Q*MTBENTLQ) MARKET TABLE                               
*                                                                               
         TITLE 'PRWRIOUT - COMMON AREA - MKTTBLD'                               
***********************************************************************         
*                                                                     *         
*        TABLE FOR AMRKET INFO                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MKTTBLD  DSECT                                                                  
MTBENTRY DS    0X                  ENTRY IN MARKET TABLE                        
MTBMKTCD DS    CL6                 MARKET CODE                                  
MTBMKTNM DS    CL20                MARKET NAME                                  
MTBRGN   DS    CL3                 REGION CODE                                  
MTBDST   DS    CL3                 DISTRICT CODE                                
*                                                                               
MTBOUT   DS    AL1                 OUTLET NUMBER                                
MTBOUTPC DS    PL3                 OUTLET PERCENTAGE                            
MTBOUTLQ EQU   *-MTBOUT            OUTLET AREA LENGTH                           
         DS    2XL(MTBOUTLQ)       REMAINING 2 OUTLETS                          
MTBOUT#Q EQU   (*-MTBOUT)/MTBOUTLQ   # OF OUTLETS                               
MTBENTLQ EQU   *-MKTTBLD           LENGTH OF ENTRY IN TABLE                     
*                                                                               
MTBMAX#Q EQU   300                 MAX ENTRIES IN TABLE                         
*                                                                               
         TITLE   'PRWRIOUT DSECTS / STORAGE'                                    
* PRWRIEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRWRIEQUS                                                      
         PRINT ON                                                               
* DDBSRPRMD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDBSRPRMD                                                      
         PRINT ON                                                               
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         PRINT ON                                                               
* DRGLOBAL                                                                      
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
         PRINT ON                                                               
* PRVALTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRVALTABD                                                      
         PRINT ON                                                               
* DRIVETABLE                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
* DRINTRECD2                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRINTRECD2                                                     
         PRINT ON                                                               
* DDSPOOLD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
         PRINT ON                                                               
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT   ON                                                             
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'086PRWRIOUT  01/15/09'                                      
         END                                                                    
