*          DATA SET OUTER      AT LEVEL 012 AS OF 05/01/02                      
*CATALP OUTER                                                                   
         TITLE 'OUTER  OUTDOOR PRINT ROUTINE'                                   
*                                                                               
*        PARAMETERS VIA R1                                                      
*                                                                               
*        CL1        X'00'=BUYREC,X'01'=17 BYTE SPACE DESC                       
*        CL3        A(INPUT)                                                    
*        CL1        OUTPUT FORMAT DESIRED     X'01'= SSS-RRR--IIII              
*                                             X'00' = 1 OR 2 LINES              
*        CL3        A(OUTPUT LINE 1)      MAX 20 CHARS                          
*        CL1                                                                    
*        CL3        A(OUTPUT LINE 2)                                            
*                                                                               
*                   IF PARAMETER 1 IS A(BUYREC) AND PDBSPACE IS                 
*                   X'00'S AND THE FIRST COMMENT IS 17 OR LESS                  
*                   CHARS THEN IT WILL BE RETURNED IN LINE 1                    
*                   AND MARKED WITH AN X'FF'                                    
*                   SO IT WON'T PRINT ELSEWHERE                                 
*                ***(BEWARE IF WRITING BUYREC TO FILE)***                       
*                                                                               
         EJECT                                                                  
OUTER    CSECT                                                                  
         NMOD1 90,*OUTER*                                                       
         LM    R2,R4,0(R1)          SET R2,R3,R4                                
         MVC   0(20,R3),SPACES                                                  
         CLI   4(R1),0                                                          
         BNE   *+10                                                             
         MVC   0(20,R4),SPACES                                                  
         CLI   0(R1),X'01'                                                      
         BNE   OUT1                                                             
         LR    R8,R2           R2 IS A(SPACE DESC)                              
         BAS   R9,CKSPACE                                                       
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
         USING PBUYRECD,R2                                                      
OUT1     LA    R8,PBDSPACE                                                      
         BAS   R9,CKSPACE                                                       
         LR    R7,R3                                                            
         CLC   0(20,R3),SPACES     SEE IF ANYTHING DONE                         
         BE    OUT1B                                                            
         CLI   PBDSPACE,X'FF'                                                   
         BNE   EXIT                                                             
         LR    R7,R4                                                            
         CLC   0(20,R4),SPACES     CHECK SECOND LINE                            
         BNZ   EXIT                                                             
*                                                                               
*                   NOTHING - SO GO FIND 1ST COMMENT AND PUT IT IN              
*                   LINE 1 IF IT IS LESS THAN 17 CHARS                          
OUT1B    DS    0H                                                               
         LA    R5,PBDELEM                                                       
OUT2     CLI   0(R5),0                                                          
         BE    EXIT                                                             
         CLI   0(R5),X'66'                                                      
         BNE   NEXTEL                                                           
         CLI   1(R5),19                                                         
         BH    EXIT                                                             
*                                                                               
         CLI   1(R5),2                                                          
         BE    EXIT         NO DATA                                             
         SR    R6,R6                                                            
         IC    R6,1(R5)                                                         
         BCTR  R6,0                                                             
         BCTR  R6,0                                                             
         BCTR  R6,0                                                             
         EX    R6,MVCOM                                                         
         MVI   0(R5),X'FF'         MARK COMMENT ELEM CODE FF                    
         B     EXIT                                                             
*                                                                               
MVCOM    MVC   0(0,R7),2(R5)                                                    
*                                                                               
NEXTEL   SR    R0,R0                                                            
         IC    R0,1(R5)                                                         
         AR    R5,R0                                                            
         B     OUT2                                                             
*                                                                               
         EJECT                                                                  
CKSPACE  CLI   0(R8),X'FF'                                                      
         BNE   CKSP8                                                            
         USING SPACED,R8                                                        
         CP    SPSHOW,=P'0'                                                     
         BNE   CKSP                                                             
         CP    SPREG,=P'0'                                                      
         BNE   CKSP                                                             
         CP    SPILLUM,=P'0'                                                    
         BNE   CKSP                                                             
         B     CKSPX          ALL ZERO SO EXIT                                  
CKSP     CLI   4(R1),X'01'             CHECK OUTPUT FORMAT                      
         BE    CKSP6                                                            
         CP    SPSHOW,=P'0'                                                     
         BE    CKSP1                                                            
         LR    R5,R3                                                            
         CP    SPSHOW,=P'99999'       SPECIAL CODE                              
         BNE   CKSP0                                                            
         MVC   0(3,R3),=C'SPC'                                                  
         LA    R0,3                                                             
         B     CKSP0X                                                           
*                                                                               
CKSP0    EDIT  SPSHOW,(4,0(R3)),0,ALIGN=LEFT                                    
CKSP0X   AR    R5,R0                                                            
         MVC   1(7,R5),=C'SHOWING'                                              
*                                                                               
CKSP1    LR    R5,R3                                                            
         CLC   0(20,R5),SPACES     SEE IF 1ST LINE USED                         
         BZ    *+6                                                              
         LR    R5,R4         SET R5 TO SECOND LINE                              
         CP    SPREG,=P'0'                                                      
         BNE   CKSP1B                                                           
         CP    SPILLUM,=P'0'                                                    
         BE    CKSPX                                                            
         LA    R0,1                                                             
         MVI   0(R5),C'0'                                                       
         B     CKSP1D                                                           
CKSP1B   DS    0H                                                               
         EDIT  SPREG,(4,0(R5)),0,ALIGN=LEFT                                     
*                                                                               
CKSP1D   DS    0H                                                               
         AR    R5,R0                                                            
         MVC   1(3,R5),=C'REG'                                                  
         MVI   4(R5),C','                                                       
         LA    R5,6(R5)                                                         
         LA    R0,1                                                             
         MVI   0(R5),C'0'                                                       
         CP    SPILLUM,=P'0'                                                    
         BE    CKSP4                                                            
         EDIT  SPILLUM,(4,0(R5)),0,ALIGN=LEFT                                   
CKSP4    DS    0H                                                               
         AR    R5,R0                                                            
         MVC   1(5,R5),=C'ILLUM'                                                
         B     CKSPX                                                            
*                                                                               
CKSP6    EDIT  SPSHOW,(4,0(R3)),0  OUTPUT DESIRED IS SSSS RRRR  IIII            
         CP    SPSHOW,=P'99999'                                                 
         BNE   *+10                                                             
         MVC   0(4,R3),=C' SPC'                                                 
         EDIT  SPREG,(4,5(R3)),0                                                
         OI    8(R3),C'0'                                                       
         EDIT  SPILLUM,(4,11(R3)),0                                             
         OI    14(R3),C'0'                                                      
         B     CKSPX                                                            
*                                                                               
CKSP8    MVC   0(17,R3),0(R8)      MOVE SPACE DESC TO FIRST LINE                
CKSPX    BR    R9       RETURN                                                  
         EJECT                                                                  
EXIT     XIT1                                                                   
*                                                                               
DUB      DS    D                                                                
WORK     DS    CL30                                                             
SPACES   DC    20C' '                                                           
*                                                                               
         LTORG                                                                  
*                                                                               
SPACED   DSECT                                                                  
         DS    CL1                                                              
SPSHOW   DS    PL3                                                              
SPREG    DS    PL3                                                              
SPILLUM  DS    PL3                                                              
         SPACE 2                                                                
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012OUTER     05/01/02'                                      
         END                                                                    
