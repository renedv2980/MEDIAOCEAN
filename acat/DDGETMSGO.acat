*          DATA SET DDGETMSGO  AT LEVEL 002 AS OF 05/01/02                      
*       UK DATA SET DDGETMSGO  AT LEVEL 002 AS OF 10/11/83                      
*                              TRANSFERRED TO US  16/12/86 TCLEDDLO             
*CATALP GETMSGO                                                                 
         TITLE 'DDGETMSGO - GET SYSTEM MESSAGES FROM CTR FILE OFF LINE'         
* PARAM 1 - XL1'MSGNUM',AL3(OUTPUT)            (SET MSGNUM TO ZERO TO           
*                                              ANALYZE DATAMGR ERRORS)          
*       2 - XL1'FLDNDX',AL3(DMCB)              (FLDNDX IS ONLY USED             
*                                              WHEN P3+1=AL3(0))                
*       3 - XL1'SYSNUM',AL3(DATAMGR)                                            
*       3 - AL4(0)                                                              
*                                                                               
* LENGTH OF OUTPUT MESSAGE IS RETURNED IN P1 BYTE 0                             
*                                                                               
GETMSG   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MSGWORKX-MSGWORK,**GMSG**,RR=R3                                  
         USING MSGWORK,RC          RC=A(W/S)                                    
         ST    R3,RELO                                                          
         LR    R2,R1                                                            
         USING MSGPARM,R2          R2=A(PARMS)                                  
         L     R9,=V(DATAMGR)                                                   
         A     R9,RELO                                                          
         ST    R9,VDATAMGR                                                      
         MVI   MSGINDX,0                                                        
         MVC   MSGNUM,MSGP1        SET MSGNUM                                   
         MVC   MSGSYS,MSGP3        AND SYSTEM                                   
         CLC   MSGP3+1(3),VDATAMGR+1                                            
         BE    *+10                                                             
         MVC   MSGINDX,MSGP2       SET FIELD INDEX                              
         EJECT                                                                  
*                                                                               
GET2     L     R3,MSGP1            R3=A(OUTPUT)                                 
         MVI   0(R3),C' '                                                       
         MVC   1(59,R3),0(R3)                                                   
*                                                                               
         CLI   MSGNUM,0            TEST DATAMGR ERROR TO BE ANALYSED            
         BNE   *+8                                                              
         BAS   RE,GETDMER          YES - GET DATAMGR ERROR NUMBER               
*                                                                               
GET4     XC    MSGKEY,MSGKEY       BUILD CONTROL FILE ERROR RECORD KEY          
         MVI   MSGKEY,C'E'                                                      
         MVC   MSGKEY+23(1),MSGSYS                                              
         MVC   MSGKEY+24(1),MSGNUM                                              
         GOTO1 VDATAMGR,DMCB,(1,=C'DMREAD'),=C'CTFILE',MSGKEY,MSGKEY            
         LA    R4,MSGKEY+28                                                     
         CLI   DMCB+8,0                                                         
         BE    GET6                                                             
*                                                                               
         CLI   MSGSYS,0                                                         
         BE    GETERR                                                           
         MVI   MSGSYS,0            TRY TO READ SYSTEM DEFAULT MESSAGE           
         B     GET4                                                             
         SPACE 1                                                                
* BUILD MESSAGE - R4=A(MESSAGE ELEMENT),R3=A(OUTPUT)                            
*                                                                               
GET6     SR    R5,R5                                                            
         IC    R5,1(R4)                                                         
         SH    R5,=H'3'                                                         
         BM    GETERR                                                           
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(R4)                                                    
         LA    R3,1(R5,R3)                                                      
         CH    R5,=H'47'           TEST ENOUGH ROOM FOR FIELD INDEX             
         BH    GETX                                                             
         CLI   MSGINDX,0                                                        
         BE    GETX                                                             
         LA    R5,12(R5)           BUMP LENGTH                                  
         MVC   0(12,R3),=C' - FIELD# NN'                                        
         EDIT  (B1,MSGINDX),(2,10(R3)),ALIGN=LEFT                               
*                                                                               
GETX     LA    R5,1(R5)                                                         
         STC   R5,MSGP1            RETURN MSG LENGTH                            
         XMOD1 1                                                                
         EJECT                                                                  
* ANALYSE DMCB ERRORS - MSGNUM=0,P2=A(DMCB)                                     
*                                                                               
GETDMER  L     R5,MSGP2            R5=A(DMCB)                                   
         IC    R5,8(R5)            R5=ERROR BITS (X'80'-X'01')                  
         SR    R4,R4                                                            
         SLL   R5,24                                                            
         LA    R6,8                                                             
*                                  CONVERT BIT TO BIT NUMBER                    
GETDMER2 SLDL  R4,1                                                             
         LTR   R4,R4                                                            
         BNZ   *+8                                                              
         BCT   R6,GETDMER2                                                      
         LA    R4,58                                                            
         SR    R4,R6                                                            
         STC   R4,MSGNUM           SET MESSAGE NUMBER                           
         BR    RE                                                               
         SPACE 1                                                                
* BUILD 'ERROR NNN UNKNOWN' IF MESSAGE NOT AVAILABLE ON CONTROL FILE            
*                                                                               
GETERR   MVC   0(2,R4),=X'0213'                                                 
         MVC   2(17,R4),=C'ERROR NNN UNKNOWN'                                   
         SR    R5,R5                                                            
         IC    R5,MSGNUM                                                        
         CVD   R5,DUB                                                           
         UNPK  8(3,R4),DUB                                                      
         OI    10(R4),X'F0'                                                     
         B     GET6                                                             
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
VSYSFAC  DC    V(SYSFAC)                                                        
         SPACE 1                                                                
* DSECT TO COVER W/S                                                            
*                                                                               
MSGWORK  DSECT                                                                  
DUB      DS    D                                                                
RELO     DS    A                                                                
WORK     DS    5F                                                               
DMCB     DS    6F                                                               
VDATAMGR DS    V                                                                
MSGINDX  DS    C                                                                
MSGSYS   DS    C                                                                
MSGNUM   DS    C                                                                
MSGKEY   DS    CL25                                                             
         DS    200F                                                             
MSGWORKX EQU   *                                                                
         SPACE 1                                                                
* DSECT TO COVER CALLING PARMS                                                  
*                                                                               
MSGPARM  DSECT                                                                  
MSGP1    DS    F                                                                
MSGP2    DS    F                                                                
MSGP3    DS    F                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDGETMSGO 05/01/02'                                      
         END                                                                    
