*          DATA SET SPMEDAPRNT AT LEVEL 039 AS OF 01/07/14                      
*CATALP MEDAPRNT                                                                
         TITLE 'SAVE AND PRINT ACTUAL BOOKS'                                    
VMDAPRNT CSECT                                                                  
         NMOD1 0,MEDAPRNT                                                       
         PRINT NOGEN                                                            
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         CLC   QBOOK1(3),=C'ACT'                                                
         BNE   EXIT                                                             
         ST    R1,SAVE1                                                         
         CLI   FRSTSW,1                                                         
         BNE   PB1                                                              
         LA    RE,ACTSTOR                                                       
         L     RF,=A(LNACTSTOR)                                                 
         XCEF                                                                   
         LA    RE,CALLSTOR                                                      
         L     RF,=A(LNCALLSTOR)                                                
         XCEF                                                                   
         L     R1,SAVE1                                                         
         MVI   FRSTSW,0                                                         
PB1      CLC   8(4,R1),=F'1'                                                    
         BNE   STORBOOK                                                         
         LA    R4,ACTSTOR          SET DATE POINTER                             
         LR    R5,R4               SAVE STATION POINTER                         
         LR    R6,R4               SAVE SLOT POINTER                            
         CLI   0(R4),0                                                          
         BE    EXIT                                                             
         MVC   P(19),=C'*** BOOKS USED FOR '                                    
         CLI   MODE,STALAST                                                     
         BNE   PRNTBK1                                                          
         MVC   P+20(7),BIGSTA                                                   
         CLI   BIGSTA,C'0'         TEST CABLE                                   
         BL    *+10                                                             
         MVC   P+20(8),BIGSTA                                                   
         MVC   P+29(3),=C'***'                                                  
PRNTBK1  CLI   MODE,MKTLAST                                                     
         BNE   PRNTBK1A                                                         
         MVC   P(19),=C'*** BOOKS USED FOR '                                    
         MVC   P+20(24),MKTNM                                                   
         LA    R3,P+42                                                          
         CLI   0(R3),C' '                                                       
         BNE   *+8                                                              
         BCT   R3,*-8                                                           
         CLI   0(R3),0                                                          
         BNE   *+8                                                              
         BCT   R3,*-8                                                           
         MVC   2(3,R3),=C'***'                                                  
PRNTSTA  LA    R3,6(R3)                                                         
         SR    R1,R1                                                            
PRNTSTA1 MVC   0(7,R3),0(R5)       MOVE STATION                                 
         CLI   0(R5),C'0'          TEST CABLE                                   
         BL    *+10                                                             
         MVC   0(8,R3),0(R5)                                                    
         AHI   R3,9                                                             
         AHI   R5,7                                                             
         AHI   R1,1                                                             
         CHI   R1,8                                                             
         BH    PRNTBK1A                                                         
         CLI   0(R5),0                                                          
         BNE   PRNTSTA1                                                         
PRNTBK1A LA    R3,P2+16                                                         
         LA    R4,98(R6)                                                        
         ST    R3,APL                                                           
         LA    R2,4                                                             
PRNTBK2  GOTO1 DATCON,DMCB,(X'02',0(R4)),(X'05',0(R3))                          
         MVI   8(R3),C'-'                                                       
SQUASH   CLC   4(2,R4),4+LNACTBK(R4)  CHECK THE NEXT MONTH                      
         BNE   SQUASHX                                                          
         CLC   8(1,R4),8+LNACTBK(R4)  AND NEXT TYPE OF BOOK                     
         BNE   SQUASHX                                                          
* TRANSITION FROM NO WEEKLY TO WEEKLY                                           
         CLI   6(R4),0                IF NO WEEK PRESENT                        
         BNE   *+12                                                             
         CLI   6+LNACTBK(R4),0        BUT A FOLLOWING WEEK                      
         BNE   SQUASHX                 STOP THE SQUASH                          
* TRANSITION FROM WEEKLY TO NO WEEKLY                                           
         CLI   6(R4),0                IF WEEK IS PRESENT                        
         BE    *+12                                                             
         CLI   6+LNACTBK(R4),0        BUT NO FOLLOWING WEEK                     
         BE    SQUASHX                 STOP THE SQUASH                          
*                                                                               
         CLI   LNACTBK(R4),0          OR EOT                                    
         BE    SQUASHX                                                          
         LA    R4,LNACTBK(R4)                                                   
         B     SQUASH                                                           
SQUASHX  DS    0C                                                               
*                                                                               
         GOTO1 DATCON,DMCB,(X'02',2(R4)),(X'05',9(R3))                          
         NI    5(R4),X'7F'         KILL SCANAM BIT                              
         GOTO1 DATCON,DMCB,(X'03',4(R4)),(X'06',18(R3))                         
*                                                                               
         OC    6(2,R4),6(R4)     IF WEEKS SET                                   
         BZ    PRNTBK2A                                                         
         MVC   18(6,R3),=C'WEEKLY'                                              
*        MVI   24(R3),C'W'       APPEND W                                       
         TM    8(R4),X'80'       IF OVERNITE SET                                
         JZ    PRNTBK2B                                                         
*        MVI   24(R3),C'O'       APPEND O                                       
         MVC   18(6,R3),=C'OVNITE'                                              
         J     PRNTBK2B                                                         
*                                                                               
PRNTBK2A CLI   4(R4),0                                                          
         BNE   *+10                                                             
         MVC   18(6,R3),=C'BUYER '                                              
PRNTBK2B LA    R3,28(R3)                                                        
         LA    R4,LNACTBK(R4)                                                   
         CLI   0(R4),0                                                          
         BE    PRNTBK3                                                          
         BCT   R2,PRNTBK2                                                       
         L     R3,APL                                                           
         LA    R3,132(R3)                                                       
         ST    R3,APL                                                           
         LA    R2,4                                                             
         B     PRNTBK2                                                          
PRNTBK3  L     R3,APL                                                           
         LA    R3,132(R3)                                                       
         MVI   0(R3),0                                                          
         GOTO1 REPORT                                                           
         LA    R6,LNSTASTOR(R6)                                                 
         LR    R4,R6                                                            
         LR    R5,R6                                                            
         CLI   0(R6),0                                                          
         BNE   PRNTBK1                                                          
         LA    RE,ACTSTOR                                                       
         L     RF,=A(LNACTSTOR)                                                 
         XCEF                                                                   
         LA    RE,CALLSTOR                                                      
         L     RF,=A(LNCALLSTOR)                                                
         XCEF                                                                   
         XC    SVBMKT,SVBMKT                                                    
EXIT     XMOD1 1                                                                
         EJECT                                                                  
STORBOOK L     R4,4(R1)                                                         
         CLC   BMKT,SVBMKT         IS THIS THE SAME AS BUFFER                   
         B     STORBOK                                                          
         LA    RE,ACTSTOR          NO - CLEAR BUFFER                            
         L     RF,=A(LNACTSTOR)                                                 
         XCEF                                                                   
STORBOK  DS    0C                                                               
*                      ELIMINATE DUPLICATE STATIONS                             
         LA    R5,CALLSTOR                                                      
         MVC   SVCALL,BIGSTA     SAVE THE CALL LETTERS                          
         CLI   BIGSTA,C'0'         IF NOT NUMERIC - DON'T DO THIS !|            
         BL    STBST                                                            
         CLI   BIGSTA+4,C'/'       CONDENSE THE SYSCODES IF FUSION              
         BNE   *+10                                                             
         MVC   SVCALL,BIGSTA+5   USE ONLY CALL LETTERS                          
STBST    CLI   0(R5),0           ONLY DO ONE PER CALL LETTER                    
         BE    STBSTX                                                           
         CLC   SVCALL,0(R5)                                                     
         BE    EXIT                                                             
         LA    R5,L'SVCALL(R5)                                                  
         B     STBST                                                            
STBSTX   DS    0C                                                               
*                                                                               
         MVC   0(4,R5),SVCALL                                                   
         MVC   SVBMKT,BMKT         SET BUFFER MARKET NUMBER                     
         LA    R5,ACTSTOR                                                       
         CLI   0(R5),0                                                          
         BNE   STORBK6                                                          
STORBK2  MVC   0(7,R5),BIGSTA      SAVE STATION AND BOOKS                       
         CLI   BIGSTA,C'0'         TEST NUMERIC                                 
         BL    STORBK4             IF LOW, NOT A SYSCODE                        
         CLI   BIGSTA+4,C'/'       CONDENSE THE SYSCODES                        
         BNE   STORBK4                                                          
         MVC   0(4,R5),BIGSTA+5                                                 
         MVC   4(3,R5),=C'   '                                                  
***********************************                                             
STORBK4  STM   RE,R1,STRER1                                                     
         LR    RE,R5                                                            
         LA    RE,98(RE)                                                        
         LR    R0,R4                                                            
         LA    R1,LNBKSTOR                                                      
         LA    RF,LNBKSTOR                                                      
         MVCL  RE,R0                                                            
         LM    RE,R1,STRER1                                                     
         B     EXIT                                                             
*                                                                               
STORBK6 CLI    0(R5),0             SCAN BUFFER FOR STATION                      
         BE    STORBK2                                                          
         CLI   92(R5),0                                                         
         BNE   STORBK8                                                          
******************************************                                      
         STM   RE,R1,STRER1                                                     
         LR    RE,R5                                                            
         LA    RE,98(RE)                                                        
         LR    R0,R4                                                            
         LA    R1,LNBKSTOR                                                      
         LA    RF,LNBKSTOR                                                      
*        CLC   98(LNBKSTOR,R5),0(R4)                                            
         CLCL  RE,R0                                                            
         BE    STORBK10                                                         
         LM    RE,R1,STRER1                                                     
STORBK8 LA     R5,LNSTASTOR(R5)                                                 
         B     STORBK6                                                          
         SPACE 2                                                                
STORBK10 LA    R5,7(R5)                                                         
         LM    RE,R1,STRER1                                                     
         CLI   0(R5),0                                                          
         BNE   STORBK10                                                         
         MVC   0(7,R5),BIGSTA                                                   
         CLI   BIGSTA,C'0'                                                      
         BL    STORBKX                                                          
         CLI   BIGSTA+4,C'/'       CONDENSE THE SYSCODES                        
         BNE   STORBKX                                                          
         MVC   0(4,R5),BIGSTA+5                                                 
         MVC   4(3,R5),=C'   '                                                  
STORBKX  B     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
LNACTBK  EQU   9                                                                
LNBKSTOR EQU   67*LNACTBK     67 DATES TIME 9 BYTES                             
LNSTASTOR EQU  98+LNBKSTOR+10  ALLOW EXTRA 10 BYTE FOR TERMINATION              
APL      DS    F                                                                
APSTART  DS    CL2                                                              
APEND    DS    CL2                                                              
FRSTSW   DC    X'01'                                                            
SVBMKT   DC    XL2'00'                                                          
SVCALL   DS    CL4                                                              
STRER1   DS    4F                                                               
SAVE1    DS    F                                                                
CALLSTOR DS    CL1000                                                           
LNCALLSTOR EQU 1000                                                             
ACTSTOR  DS    14000C                                                           
ACTSTORX EQU   *                                                                
LNACTSTOR EQU  ACTSTORX-ACTSTOR                                                 
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE SPMEDBLOCK                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'039SPMEDAPRNT01/07/14'                                      
         END                                                                    
