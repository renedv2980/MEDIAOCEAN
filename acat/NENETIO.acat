*          DATA SET NENETIO    AT LEVEL 066 AS OF 06/02/20                      
*CATALP NETIO                                                                   
         TITLE 'NETIO - I/O CONTROLLER FOR NETWORK'                             
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-7982   11/28/16 SAVE OFF & RESTORE NBHUNOPT               *         
***********************************************************************         
                                                                                
*********************************************************                       
***  OLD NETIO IS IN PZIR LIBRARY AS NETIOS *************                       
*********************************************************                       
NETIO    CSECT                                                                  
         DS    9000C                                                            
         ORG   *-9000                                                           
         PRINT NOGEN                                                            
         NMOD1 NETWRKX-NETWRK,**NTIO**,R9,R8,RR=RA                              
         USING NETIOD,RC                                                        
         ST    RA,RELO                                                          
         L     RA,0(R1)            USER PASSES A(NEBLOCK)                       
         USING NEBLOCKD,RA                                                      
         MVC   NBCALLRD,4(RD)      SAVE W/S BACK LINK                           
         XC    ADROFFCR,ADROFFCR                                                
         SPACE 1                                                                
NTIOSTRT MVI   NBERROR,NBGOOD      CHECK FOR INITIALIZATION                     
         BAS   RE,MEDINIT          MAY NEED TO BUILD MEDIA BUFFER               
         BRAS  RE,INIT                                                          
         CLI   NBQINIT,0                                                        
         BNE   *+8                                                              
         BAS   RE,QINIT                                                         
         BAS   RE,NTIINIT          MAY NEED TO BUILD NTI BUFFER                 
         SPACE 1                                                                
RESTTEST CLI   NBFUNCT,NBFRESTR    RESTORE FUNCION                              
         BNE   SPLITEST                                                         
         BAS   RE,HIGH                                                          
         BAS   RE,GETREC                                                        
         MVI   NBFUNCT,0                                                        
         SPACE 1                                                                
SPLITEST OC    NBSELPRD,NBSELPRD   SET DEFAULT PRD TO POL IF NEEDED             
         BNZ   *+10                                                             
         MVC   NBSELPRD,=C'POL'                                                 
         TM    NBSPLOPT,X'80'      IS SPLIT OPTION IN ACTION                    
         BNO   SPEC2                                                            
         TM    NBSPLOPT,X'40'      OPTION TO SPLIT EVEN IF POL                  
         BO    SPLITST1                                                         
         CLC   NBSELPRD,=C'POL'    OTHERWISE NOT APPLICABLE FOR POL             
         BNE   *+12                                                             
         NI    NBSPLOPT,X'7F'      SO TURN OFF                                  
         B     SPEC2                                                            
         SPACE 1                                                                
SPLITST1 CLI   NBSPLPRN,0          YES - ARE WE IN THE MIDDLE?                  
         BE    SPEC2                                                            
         LM    R0,R7,NBGOR07                                                    
         SPACE 1                                                                
SPLITST2 BAS   RE,GETVALS          YES - SO VALUE AGAIN FOR NEXT                
         CLI   NBSPLPRN,0          IF THERE IS ANOTHER PRODUCT                  
         BE    SPEC2                  GO BACK FOR USER TO HANDLE                
         STM   R0,R7,NBGOR07                                                    
**       ZIC   R1,NBSPLPRN         MAY NEED TO FILTER THIS PRODUCT              
**       LA    R2,NBPRDMSK                                                      
**       BAS   RE,TESTMASK                                                      
**       BNE   SPLITST2                                                         
         CLI   NBSELPGR,0          PRODUCT GROUP                                
         BE    SPLITST3            NO                                           
         XC    WORK(3),WORK        YES                                          
         MVC   WORK(1),NBSPLPRN                                                 
***  CODE BELOW REMOVED SINCE NETVALUE PASES BACK AN X'FF' TO                   
***  INDICATE END OF PRODS FOR UNIT.  NBSPLPR3 IS NOT FILLED IN                 
***  SINCE THIS (POL) IS NOT A PROD ON UNIT ALTHOUGH IT IS                      
***  VALID PROD AND THUS IN LIST.                                               
***      CLI   NBSPLPR3,0          IF WE HAVE 3 CHARACTER CODE                  
***      BNE   *+6                                                              
***      DC    H'0'                WHY DON'T WE HAVE IT ?                       
         MVC   WORK(3),NBSPLPR3    USE THAT                                     
         CLI   NBSPLPR3,0          IF UNALLOCATED                               
         BNE   *+10                                                             
         MVC   WORK(3),=C'POL'     MAKE IT POL                                  
         BAS   RE,TSTOPMSK                                                      
         BNE   SPLITST2            REJECT                                       
         B     SPEC1               ACCEPT                                       
SPLITST3 CLI   NBPRDALL,X'FF'      ALL PRODS ?                                  
         BE    SPEC1               YES                                          
         CLI   NBSPLPR3,0          IF WE HAVE 3 CHARACTER PROD                  
         BE    SPLITST4                                                         
         CLC   NBSPLPR3,NBSELPRD   USE THAT                                     
         BNE   SPLITST2                                                         
         B     SPEC1                                                            
SPLITST4 CLC   NBSPLPRN,NBSELPNM                                                
         BNE   SPLITST2                                                         
*                                                                               
SPEC1    CLI   NBSELLEN,0          ARE WE FILTERING LENGTH                      
         BE    XIT                                                              
         CLC   NBSELLEN,NBLEN      THEN CHECK MATCH NOW                         
         BE    XIT                                                              
         B     SPLITST2                                                         
         EJECT                                                                  
*              SPECIAL FUNCTIONS                                                
         SPACE 3                                                                
SPEC2    CLI   NBFUNCT,NBFGET      CHECK FOR SPECIAL FUNCTIONS                  
         BE    FUNCGET             TO GET A RECORD. ALREADY HAVE KEY            
*                                                                               
         CLI   NBFUNCT,NBFNXCLT    SKIP TO NEXT CLIENT                          
         BNE   *+16                                                             
         MVI   NBFUNCT,0                                                        
         MVI   NBMODE,NBVALCLI                                                  
         B     NXTCLI                                                           
         SPACE 1                                                                
         CLI   NBFUNCT,NBFVAL                                                   
         BE    FUNCVAL             TO VALUE THE RECORD IN NBAIO                 
         CLI   NBFUNCT,NBFRDHI     DO A READHI BEFORE CONTINUING                
         BNE   SPEC4                ALLOWS I/O IN BETWEEN NETIO CALLS           
         BAS   RE,HIGH                                                          
         SPACE 1                                                                
SPEC4    CLI   NBDIRECT,C'Y'       WHEN ONLY KEY CHNGS ARE DAT,SUB,PRG          
         BE    QVALDAT                                                          
         CLI   NBRESUME,NBPROCPK   TO RESUME READING PACKAGES                   
         BNE   SPEC4B                                                           
         MVI   NBRESUME,0          RESET NBRESUME                               
         B     DOPKG               AND START AT PACKAGE STUFF                   
SPEC4B   CLI   NBRESUME,NBPROCUN   TO RESUME READING UNITS                      
         BNE   SPEC6               ** EXPECTS UNIT KEY IN KEY                   
         MVI   NBRESUME,0          ** DOES HIGH FIRST                           
         B     UD1                 **THEN GETREC                                
         SPACE 1                                                                
SPEC6    CLI   NBUPUNIT,C'Y'       IF SHOULD WRITE RECODR FIRST                 
         BNE   SPEC8                                                            
         BAS   RE,GETVALS                                                       
         MVI   NBUPUNIT,C'0'       RESET UPUNIT                                 
         SPACE 1                                                                
SPEC8    SR    RE,RE                                                            
         IC    RE,NBRETURN                                                      
         LTR   RE,RE               MAKE SURE RETURN VALUE IS LEGAL              
         BZ    BRERR                                                            
         LA    R1,BRCOUNT                                                       
         CR    RE,R1                                                            
         BH    BRERR                                                            
         LM    R0,R7,NBGOR07       RESTORE GENERAL REGISTERS                    
         SLL   RE,2                GET OFFSET INTO JUMP TABLE                   
         B     JUMPTBL(RE)                                                      
         EJECT                                                                  
*              JUMPTABLE                                                        
         SPACE 3                                                                
*     JUMPTABLE AND NBRETURN ARE USED TO ALLOW NETVALUE TO BE RELOCATED         
*     BETWEEN SUBSEQUENT CALLS. BE CAREFUL TO INSURE THAT R0-R7 WHICH           
*     ARE RESTORED CONTAIN NO ADDRESSES WHICH COULD HAVE CHANGED.               
*     THE BM EQUATES USED BELOW MUST MATCH THE NUMBERS IN THE JUMP              
*     TABLE. A MISTAKEN NUMBER CAUSES AWFUL PROBLEMS. BE CAREFUL!!!             
         SPACE 3                                                                
JUMPTBL  B     BRERR               0 - ERROR                                    
         B     BRSTART             1                                            
         B     BRAGYFIN            2                                            
         B     BRCLIFIN            3                                            
         B     BRPRDFIN            4                                            
         B     BRESTFIN            5                                            
         B     BRFSTPAK            6                                            
         B     BRNXTPAK            7                                            
         B     BRDATFIN            8                                            
         B     BRUBDNXT            9                                            
         B     BRUBPNXT            10                                           
         B     BRENDIT             11                                           
         B     BRFGTNXT            12                                           
         B     BRFVLNXT            13                                           
         B     BRUDTNXT            14                                           
BRCOUNT  EQU   (*-JUMPTBL)/4       NUMBER OF ENTRIES                            
         SPACE 1                                                                
BRERR    DC    H'0'                GETS HERE ON BAD RETURN VALUE                
         SPACE 1                                                                
BMSTART  EQU   1                                                                
BMAGYFIN EQU   2                                                                
BMCLIFIN EQU   3                                                                
BMPRDFIN EQU   4                                                                
BMESTFIN EQU   5                                                                
BMFSTPAK EQU   6                                                                
BMNXTPAK EQU   7                                                                
BMDATFIN EQU   8                                                                
BMUBDNXT EQU   9                                                                
BMUBPNXT EQU   10                                                               
BMENDIT  EQU   11                                                               
BMFGTNXT EQU   12                                                               
BMFVLNXT EQU   13                                                               
BMUDTNXT EQU   14                                                               
         SPACE 1                                                                
         EJECT                                                                  
*              INITIALIZE FOR REQUEST - AGENCY DETAILS                          
         SPACE 3                                                                
QINIT    NTR1                                                                   
         MVI   NBQINIT,1           IF GET HERE, ALL ARE VALIDATED               
         MVI   BRNCHNUM,BMSTART                                                 
         MVI   NBRETURN,BMSTART    INITIALIZE RETURN VALUES                     
         XIT1                                                                   
         SPACE 1                                                                
BRSTART  DS    0H                                                               
         EJECT                                                                  
*              VALIDATE AGENCY                                                  
         SPACE 3                                                                
*              OUTPUT: NBACTAM - ACTUAL AGENCY/MEDIA                            
*              IF NBSELAM NOT SET:                                              
*                      NBAGYNAM,NBAGYADD -                                      
*                      NBSELMED - DEFAULTS TO N IF NOT SET                      
*                      NBEFFAGY - THE CURRENT AGY                               
         SPACE 1                                                                
QVALAGY  MVI   NBMODE,NBVALAGY                                                  
         MVI   NBFILE,C'S'                                                      
         MVC   NBACTAM,NBSELAM                                                  
         CLI   NBSELAM,0           IF SET, ASSUME ALREADY VALID                 
         BNE   AGYOK               UNTIL NEW CONTROLLER                         
         OC    NBSELAGY,NBSELAGY   IF NO SELAGY THEN DO ALL                     
         BNZ   QVA2                                                             
         SPACE 1                                                                
NXTAGY   LH    R1,NBEFFAGY         PROCESS AGY=ALL                              
         LA    R1,1(R1)                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING AGYKEY,R4                                                        
         MVI   AGYKTYPE,X'06'                                                   
         STCM  R1,3,AGYKAGY                                                     
         BAS   RE,HIGH                                                          
         CLI   AGYKTYPE,X'06'      SEE IF LAST AGY                              
         BNE   QE6                                                              
         MVC   NBEFFAGY,AGYKAGY    SAVE NEW AGY                                 
         BAS   RE,GETREC                                                        
         MVI   ELCODE,X'01'                                                     
         L     R6,NBAIO                                                         
         BAS   RE,GETEL                                                         
         USING AGYEL,R6                                                         
         TM    AGYFLAG2,AGYFLAG2_XPR USES EXPANDED PRODUCTS                     
         BNO   *+8                                                              
         OI    NBINDS7,NBI7XFL     SET FLAG                                     
         MVI   ELCODE,X'02'                                                     
         CLI   NBSELMED,0                                                       
         BNE   VAGY1                                                            
         MVI   NBSELMED,C'N'                                                    
         SPACE 1                                                                
VAGY1    BAS   RE,NEXTEL                                                        
         BNE   NXTAGY                                                           
         CLC   2(1,R6),NBSELMED                                                 
         BNE   VAGY1                                                            
         MVC   NBACTAM,3(R6)                                                    
         B     AGYOK                                                            
         SPACE 1                                                                
QVA2     XC    KEY,KEY             SINGLE AGENCY                                
         LA    R4,KEY                                                           
         MVC   NBEFFAGY,NBSELAGY   PUT SEL IN EFF                               
         USING AGYKEY,R4                                                        
         MVI   AGYKTYPE,X'06'                                                   
         MVC   AGYKAGY,NBSELAGY                                                 
         BAS   RE,READ                                                          
         BNE   BADAGY                                                           
         BAS   RE,GETREC                                                        
         MVI   ELCODE,X'01'                                                     
         L     R6,NBAIO                                                         
         BAS   RE,GETEL                                                         
         USING AGYEL,R6                                                         
         TM    AGYFLAG2,AGYFLAG2_XPR USES EXPANDED PRODUCTS                     
         BNO   *+8                                                              
         OI    NBINDS7,NBI7XFL     SET FLAG                                     
         MVI   ELCODE,X'02'                                                     
         CLI   NBSELMED,0                                                       
         BNE   VAGY2                                                            
         MVI   NBSELMED,C'N'                                                    
         SPACE 1                                                                
VAGY2    BAS   RE,NEXTEL                                                        
         BNE   BADAGY                                                           
         CLC   2(1,R6),NBSELMED                                                 
         BNE   VAGY2                                                            
         MVC   NBACTAM,3(R6)                                                    
         B     AGYOK                                                            
         SPACE 1                                                                
BADAGY   MVI   NBERROR,NBINVAGY                                                 
         BAS   RE,GOERR                                                         
         SPACE 1                                                                
AGYOK    OC    NBAAGY,NBAAGY       USER OPTION TO GET BACK AGY                  
         BZ    VAGY4                                                            
         L     R4,NBAIO                                                         
         L     R3,NBAAGY                                                        
         MVC   DUB(2),AGYLEN                                                    
         LH    R5,DUB                                                           
         MOVE  ((R3),(R5)),(R4)                                                 
         SPACE 1                                                                
VAGY4    MVI   BRNCHNUM,BMAGYFIN                                                
         BAS   RE,GO                                                            
BRAGYFIN DS    0H                                                               
         EJECT                                                                  
*              VALIDATE CLIENTS                                                 
         SPACE 3                                                                
*              SPECIAL VALUES     NBSELCLI=ALL                                  
*                                                                               
*              INPUT:  NBACTAM    MUST ALREADY BE VALIDATED                     
*                                                                               
*              OUTPUT: NBACTCLI   ACTUAL CLIENT (2-BYTE CODE)                   
*                      NBACLI     IF NON-ZERO CLIENT RECORD IS PUT HERE         
*                      NBCLICOD   3-BYTE PRINTABLE CLIENT CODE                  
*                   IF NBSELCLI = ALL:                                          
*                      NBEFFCLI   EFFECTIVE CLIENT                              
*                   IF NBUSER = ZEROES: (ASSUMES NBEFFAGY IS SET)               
*                      NBUSER     USER PROFILE.                                 
         SPACE 1                                                                
QVALCLI  MVI   NBMODE,NBVALCLI                                                  
         CLC   NBSELCLI,=C'ALL'                                                 
         BNE   GETCLI                                                           
         SPACE 1                                                                
NXTCLI   LH    R1,NBEFFCLI         PROCESS CLIENT = ALL                         
         LA    R1,1(R1)            NBEFFCLI IS CURRENT CLIENT                   
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING CLTHDR,R4                                                        
         MVC   CKEYAM,NBACTAM                                                   
         STH   R1,CKEYCLT                                                       
         BAS   RE,HIGH             GET NEXT CLIENT RECORD                       
         CLC   KEY(2),KEYSAVE                                                   
         BNE   QE4                                                              
         XC    NBUSER,NBUSER       ENSURE PROFILES AGAIN                        
         NI    NBUNTSW,X'FF'-X'01' CLEAR MIDAS INDICATOR                        
         MVC   NBEFFCLI,CKEYCLT                                                 
         MVC   NBACTCLI,NBEFFCLI                                                
         BAS   RE,GETREC           GET CLIENT RECORD                            
         L     R4,NBAIO                                                         
*                                                                               
         TM    NBUNTSW,X'20'      SKIP OFFICE CHECKS                            
         BO    VCL2                                                             
         MVC   NBEFFOFF,COFFICE    DEFAULT = MEDIA OFFICE                       
         TM    NBINDS4,NBI4TRAF    LOOKING AT CLIENT TRAFFIC OFFICE?            
         BZ    VCL2                                                             
         CLI   CTRAFOFC,C' '       WAS CLIENT TRAFFIC OFFICE SET?               
         BNH   VCL2                                                             
         MVC   NBEFFOFF,CTRAFOFC   YES, USE THAT INSTEAD                        
*                                                                               
* - CHECK CLIENT ACC OFFICE                                                     
VCL2     OC    NBACCOFF,NBACCOFF   CLIENT ACC OFFICE?                           
         BZ    VCL3                                                             
         CLC   NBACCOFF,CACCOFC    MATCH?                                       
         BNE   NXTCLI                                                           
* - CHECK CLIENT GROUP FILTERING                                                
VCL3     CLI   NBSELCGR,0                                                       
         BE    VCL9                                                             
         LA    R0,10                                                            
         LA    RF,CGRP1            1ST GROUP OF 5                               
VCL3B    CLC   NBSELCGR(1),0(RF)   CHECK SCHEME LETTER                          
         BE    VCL5                                                             
VCL4     LA    RF,3(RF)                                                         
         C     R0,=F'6'            TRY NEXT 5                                   
         BNE   VCL4B                                                            
         LA    RF,CGRP6             2ND GROUP OF 5                              
VCL4B    BCT   R0,VCL3B                                                         
         B     NXTCLI                                                           
*                                                                               
VCL5     UNPK  DUB(5),1(3,RF)      UNPK PWOS                                    
         LA    R3,DUB                                                           
         LA    RE,NBSELCGR+1                                                    
         LA    R1,4                                                             
VCL6     CLI   0(RE),X'C1'         IF LETTER OR NUMBER                          
         BL    VCL7                                                             
         CLC   0(1,RE),0(R3)       MUST MATCH                                   
         BNE   VCL4                IF NO MATCH,TEST AGAINST NXT CGRP            
VCL7     LA    RE,1(RE)                                                         
         LA    R3,1(R3)                                                         
         BCT   R1,VCL6                                                          
         MVC   NBACTCGR(2),1(RF)   SET CLIENT GROUP CODE  PWOS                  
         SPACE 1                                                                
* ENSURE THAT EACH CLIENT IS AUTHORIZED FOR AGENCY WHEN NBSELCLI=ALL            
VCL9     DS    0H                                                               
         CLI   NBSELOFF,0          IF NBSELOFF SET,THIS IS NOT A                
         BNE   VCL10               CLI=ALL REQ (*/$N REQUEST)                   
         OC    NBTWAACC(2),NBTWAACC  LOCKOUT CHECKS                             
         BZ    VCL10                                                            
         CLC   =C'ALL',NBSELCLI                                                 
         BNE   VCL10                                                            
         TM    NBUNTSW,X'20'      SKIP OFFICE CHECKS                            
         BO    VCL10                                                            
         CLI   NBTWAACC,C'*'        IF *                                        
         BNE   VCL9B                                                            
*                                   IF *AN THEN CLIENT GROUP LIMIT              
*                                   ACCESS AND NOT OFFICE LIMIT                 
                                                                                
         CLI   NBTWAACC+1,C'A'        CHECK IF ALPHA                            
         BL    VCL9A                                                            
         CLI   NBTWAACC+1,C'Z'                                                  
         BH    VCL9A                                                            
         CLI   NBTWAACC+2,C'0'         CHECK IF NUMERIC                         
         BL    VCL9A                                                            
         CLI   NBTWAACC+2,C'9'                                                  
         BH    VCL9A                                                            
         B     VCL10                PASSED TESTS SO ITS CLIENT GROUP            
*                                   LIMIT ACCESS / HANDLED IN NEMEDGEN          
                                                                                
*                                                                               
VCL9A    MVC   NBSELOFF,NBTWAACC+1     SET OFFICE                               
         B     VCL10                                                            
*                                                                               
VCL9B    CLI   NBTWAACC,C'$'        IF $                                        
         BNE   VCL9D                                                            
         MVC   NBSELOFF,NBTWAACC+1                                              
         OI    NBOFFTYP,X'80'                                                   
         B     VCL10                                                            
*                                                                               
VCL9D    CLI   NBTWAACC,C'-'        IF NOT * OR - THEN CLIENT LOCKOUT           
         BE    VCL9F                                                            
         CLC   NBACTCLI,NBTWAACC    CLIENT LOCKOUT?                             
         BE    PROCCLI                 OK                                       
         OC    NBSECSV,NBSECSV         NOT OK- NEW SECURITY?                    
         BZ    NXTCLI                  NO-SKIP THIS CLIENT                      
         B     VCL10                   YES-LET NEW OFFICER HANDLE IT            
*                                                                               
VCL9F    CLI   NBTWAACC+1,C'*'      -* MEANS ALL BUT THIS OFFICE                
         BNE   VCL9H                                                            
         CLC   NBTWAACC+2(1),NBEFFOFF                                           
         BE    NXTCLI                                                           
         B     VCL10                                                            
*                                                                               
VCL9H    CLC   NBTWAACC+1(2),NBACTCLI   ALL BUT THIS CLIENT                     
         BE    NXTCLI                                                           
         B     VCL10                                                            
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
VCL10    TM    NBUNTSW,X'20'       SKIP OFFICE CHECKS                           
         BO    PROCCLI             YES                                          
         MVI   BYTE,0              CLEAR TO USE AS FLAG                         
         CLI   NBSELOFF,0          OFFICE FILTERING?                            
         BNE   VCL11               YES                                          
***      OC    NBSECSV,NBSECSV     NO/ NEW SECURITY SET?                        
***      BZ    PROCCLI                 NO-SO THAT'S ALL                         
***      B     NEWSEC                  YES-CHECK CLIENT AGAINST NEW SEC         
         B     PROCCLI                                                          
*                                                                               
* NOT OFFICE FILTERING BUT NEW SECURITY SET MEANS THIS IS A TRUE                
* CLIENT = ALL REQUEST SO WE MUST CHECK CLIENT AGAINST NEW                      
* SEC FOR CLIENT STRING SECURITY                                                
VCL11    TM    NBOFFTYP,X'80'      CHECK OFFICE LIST                            
         BO    OFFLIST                                                          
         TM    NBOFFTYP,X'40'      CHECK NEGATIVE FILTER                        
         BO    NEGOFF                                                           
* ONLY GETS HERE ON *N REQUESTS                                                 
         CLC   NBEFFOFF,NBSELOFF   FILTER OFFICE CODE                           
         BNE   NXTCLI                                                           
* PASSED OFFICE CODE FILTER, CHECK TO SEE IF ALSO OFFICE LIST FILTER            
*                                                                               
* IF IT GOT HERE AND NBSELCLI=ALL AND NBTWAACC=$N THEN IT IS NOT                
* A CLI=ALL REQUEST BUT A CLI=*N REQUEST SO WE WILL NEED                        
* TO FILTER FURTHER TO ENSURE THAT THE OFFICE IS IN THE $ OFFICELIST            
* PREVIOUSLY IF CLIENT REQUESTED *N AND THERE WAS $OFFICE LIMIT                 
* THE OFFICE LIST LIMIT WAS NOT CHECKED BECAUSE                                 
* MEDGEN USES NBSELOFF FIELD FOR BOTH OFFICE AND OFFICE LIST                    
* SO NETIO COULD NOT HANDLE CLI=*N REQUEST + $N OFFICE LIST                     
VCL12    CLC   NBSELCLI,=C'ALL'     IS IT CLI=ALL?                              
         BNE   PROCCLI             NO- THEN NO MORE FILTERING'                  
         CLI   NBTWAACC,C'$'       IS IT OFFICLE LIMIT ACCESS?                  
         BNE   PROCCLI             NO-SO NO MORE FILTERING                      
* NOT SURE IF NEXT TWO INSTRUCTINS ARE REDUNDANT                                
         MVC   BYTE,NBSELOFF       SAVE OFFICE CODE                             
         MVC   NBSELOFF,NBTWAACC+1 SET OFFICE LIST                              
         SPACE 1                                                                
OFFLIST  ICM   R3,15,NBSECSV         DO WE HAVE NEW SECURITY BLOCK?             
         BNZ   NEWSEC                                                           
         B     NEWSEC                                                           
         SPACE 1                                                                
NEWSEC   DS    0H                                                               
         ICM   R3,15,NBSECSV                                                    
         BNZ   *+6                                                              
         DC    H'0'               SHOULD HAVE BEEN CAUGHT EARLIER               
         USING NEWSECD,R3                                                       
         LA    R2,NEWOFFB                                                       
         USING OFFICED,R2                                                       
         XC    NEWOFFB,NEWOFFB                                                  
*                                                                               
         MVI   OFCSYS,C'S'                                                      
         MVC   OFCAUTH,NBTWAACC                                                 
*                                                                               
         CLI   NBSELOFF,0              FILTERING ON OFFICE?                     
         BE    NEWSEC10                                                         
         MVC   OFCAUTH+1(1),NBSELOFF   YES-SET IT                               
         MVI   OFCAUTH,C'$'        ASSUME ALL OTHERS ALREADY FILTERED           
*****                              HAVE ANDREW TEST DON'T LIMIT                 
*****                              FUTURE OPTIONS                               
                                                                                
*                                                                               
NEWSEC10 MVC   OFCAGY,NBEFFAGY                                                  
         MVC   OFCOFC,NBEFFOFF                                                  
         GOTO1 NBCLUNPK,DMCB,(CPROF+6,NBACTCLI),OFCCLT                          
         OC    OFCCLT,=X'404040'                                                
         MVC   OFCSAGMD,NBACTAM                                                 
         MVC   OFCLMT(4),NBTWAACC                                               
         CLI   NBSELOFF,0              FILTERING ON OFFICE?                     
         BE    NEWSEC20                                                         
         MVC   OFCLMT+1(1),NBSELOFF    YES-SET IT                               
         MVI   OFCLMT,C'$'            THIS IS IN NEW NETIO - LET'S NOT          
*****                                 DO THIS AND HAVE ANDREW TEST              
*****                                 DON'T LIMIT FUTURE OPTIONS                
                                                                                
NEWSEC20 MVC   OFCACCSC(3),CACCESS    ACCESS LIST FROM CLTHDR                   
         LA    RF,NEWSECA                                                       
         ST    RF,OFCSECD                                                       
*                                                                               
         XC    DMCB(8),DMCB                                                     
         MVC   DMCB+4(4),=X'D9000A38'    GET OFFICER ADDRESS                    
         GOTO1 NBCALLOV,DMCB                                                    
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
***->    MVI   NBERROR,55          SECURITY LOVKOUT                             
* DON'T NEED TO SET ERROR SINCE NETIO ONLY FILTERS                              
* CLIENT VALIDATION IN NEMEDGEN RETURNS SECURITY ERROR                          
*                                                                               
         XC    DMCB+4(12),DMCB+4                                                
         L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,(C'N',NEWOFFB),NBACOM                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
         TM    NBOFFTYP,X'40'      MAY BE NEGATIVE LIST                         
         BO    NEGLIST                                                          
         CLI   0(R1),0             IS OFFICE IN LIST                            
         BNE   NXTCLI                                                           
         CLI   BYTE,0              NEED TO RESTORE?                             
         BE    *+10                                                             
         MVC   NBSELOFF,BYTE       YES                                          
         B     PROCCLI                                                          
         SPACE 1                                                                
NEGLIST  CLI   0(R1),0             CHECK FOR NEGATIVE LIST                      
         BE    NXTCLI                                                           
         CLI   BYTE,0              NEED TO RESTORE?                             
         BE    *+10                                                             
         MVC   NBSELOFF,BYTE       YES                                          
         B     PROCCLI                                                          
         SPACE 1                                                                
NEGOFF   CLC   NBEFFOFF,NBSELOFF                                                
         BE    NXTCLI                                                           
         B     VCL12              CHECK ON CLI=*N TYPE REQUEST                  
***      B     PROCCLI                                                          
         SPACE 1                                                                
GETCLI   MVC   NBACTCLI,NBSELCL2                                                
         OC    NBSELCL2,NBSELCL2   GET 2-BYTE CLIENT CODE IN NBACTCLI           
         BNZ   QI8                                                              
         GOTO1 NBCLPACK,DMCB,NBSELCLI,NBACTCLI                                  
         CLI   0(R1),X'FF'         CK FOR VALID CLIENT                          
         BE    BADCLI                                                           
         SPACE 1                                                                
QI8      LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING CLTHDR,R4                                                        
         MVC   CKEYAM,NBACTAM                                                   
         MVC   CKEYCLT,NBACTCLI                                                 
         BAS   RE,READ                                                          
         BNE   BADCLI                                                           
         BAS   RE,GETREC           GET CLIENT RECORD                            
         L     R4,NBAIO                                                         
         MVC   NBEFFCLI,NBACTCLI                                                
         MVC   NBEFFOFF,COFFICE    NOTE OFFICE                                  
         TM    NBINDS4,NBI4TRAF    LOOKING AT CLIENT TRAFFIC OFFICE?            
         BZ    PROCCLI                                                          
         CLI   CTRAFOFC,C' '                                                    
         BNH   *+10                                                             
         MVC   NBEFFOFF,CTRAFOFC   USE CLIENT TRAFFIC OFFICE                    
         SPACE 1                                                                
PROCCLI  DS    0H                                                               
*          DATA SET NENETIO    AT LEVEL 041 AS OF 01/25/07                      
*************************************************************                   
* CHECK CLIENT STRING SECURITY                                                  
         ICM   R3,15,NBSECSV                                                    
         BZ    PRCLIOK                                                          
         USING NEWSECD,R3                                                       
         LA    R2,NEWOFFB                                                       
         USING OFFICED,R2                                                       
         XC    NEWOFFB,NEWOFFB                                                  
*                                                                               
         MVI   OFCSYS,C'S'                                                      
         MVC   OFCAUTH,NBTWAACC                                                 
         MVC   OFCAGY,NBEFFAGY                                                  
         MVC   OFCOFC,NBEFFOFF                                                  
         GOTO1 NBCLUNPK,DMCB,(CPROF+6,NBACTCLI),OFCCLT                          
         OC    OFCCLT,=X'404040'                                                
         MVC   OFCSAGMD,NBACTAM                                                 
         MVC   OFCLMT(4),NBTWAACC                                               
         MVC   OFCACCSC(3),CACCESS    ACCESS LIST FROM CLTHDR                   
         LA    RF,NEWSECA                                                       
         ST    RF,OFCSECD                                                       
*                                                                               
         XC    DMCB(8),DMCB                                                     
         MVC   DMCB+4(4),=X'D9000A38'    GET OFFICER ADDRESS                    
         GOTO1 NBCALLOV,DMCB                                                    
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         XC    DMCB+4(12),DMCB+4                                                
         L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,(C'N',NEWOFFB),NBACOM                                  
**       CLI   0(R1),0             IS OFFICE IN LIST                            
**       BNE   NXTCLI                                                           
         CLI   0(R1),0                                                          
         BE    PRCLIOK             OK - PROCESS                                 
         CLC   =C'ALL',NBSELCLI    NO - IS CLIENT = ALL                         
         BE    NXTCLI                   YES - CONTINUE                          
         BNE   BADCLI                   NO - REJECT                             
PRCLIOK  DS    0H                                                               
         DROP  R2,R3                                                            
*************************************************************                   
* PRODUCT LEVEL SECURITY CHECK                                                  
         CLI   NBTRAPSC,0          TRAFFIC CLIENT LIMIT ACCESS                  
         BE    ENDPSEC             NO                                           
         NI    NBINDS5,X'FF'-NBI5PSEC      CLEAR CLT LEV                        
         TM    COPT3,COP3PSEC              CLIENT PLEVEL SEC ?                  
         BNO   *+8                                                              
         OI    NBINDS5,NBI5PSEC    YES                                          
ENDPSEC  DS    0H                                                               
*                                                                               
         TM    NBINDS4,NBI4STWY    INCLUDE STEWARD ESTIMATES?                   
         BO    ENDSTWRD            YES-SKIP STEWARD EST TABLE                   
         ICM   R3,15,NBASTWRD         STEWARD ESTIMATE ADDRESS                  
         BZ    ENDSTWRD                                                         
         MVC   WORK(13),KEY        SAVE CLIENT KEY                              
         XC    0(255,R3),0(R3)                                                  
*                                                                               
         LA    R4,KEY                                                           
         USING EKEY,R4                                                          
         MVI   NBFILE,C'S'         SET TO READ SPOT                             
         XC    KEY,KEY                                                          
         MVC   EKEYAM,NBACTAM                                                   
         MVC   EKEYCLT,NBACTCLI                                                 
         MVC   EKEYPRD,=C'POL'                                                  
         MVC   KEYSAVE,KEY                                                      
STWRD05  GOTO1 HIGH                                                             
         B     STWRD05C                                                         
*                                                                               
STWRD05B GOTO1 SEQ                                                              
*                                                                               
STWRD05C CLC   KEY(4),KEYSAVE      A/M,CLIENT                                   
         BNE   STWRD10                                                          
         CLC   =C'POL',EKEYPRD                                                  
         BNE   STWRD05B                                                         
         CLI   EKEYEST,0           MAKE SURE IT'S EST REC                       
         BE    STWRD05B                                                         
         CLI   EKEYEST+1,0         MAKE SURE IT'S EST                           
         BNE   STWRD05B                                                         
         GOTO1 GETREC                                                           
         L     R4,NBAIO                                                         
         CLI   ETYPE,C'S'          STEWARD ESTIMATE                             
         BNE   STWRD07                                                          
         MVC   0(1,R3),EKEYEST     SET ESTIMATE                                 
         MVI   1(R3),0             CLEAR NEXT BYTE                              
         LA    R3,1(R3)            BUMP TABLE                                   
STWRD07  ZIC   R1,EKEYEST          BUMP EST                                     
         CLI   EKEYEST,X'FF'       LAST EST?                                    
         BE    STWRD10             YES-THAT'S ALL                               
         LA    R1,1(R1)                                                         
         STC   R1,KEY+7                                                         
         MVI   KEY+8,0                                                          
         B     STWRD05                                                          
                                                                                
STWRD10  DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(13),WORK        RESOTRE CLIENT KEY                           
         BAS   RE,READ                                                          
         BNE   BADCLI                                                           
         BAS   RE,GETREC                                                        
         L     R4,NBAIO                                                         
         USING CLTHDR,R4                                                        
*                                                                               
ENDSTWRD DS    0H                                                               
*                                                                               
         GOTO1 NBCLUNPK,DMCB,(CPROF+6,NBACTCLI),NBCLICOD                        
         OC    NBACLI,NBACLI       USER OPTION TO GET BACK CLIENT               
         BZ    CLIPROF                                                          
         L     R4,NBAIO                                                         
         L     R3,NBACLI                                                        
         MVC   DUB(2),CLEN                                                      
         LH    R5,DUB                                                           
         MOVE  ((R3),(R5)),(R4)                                                 
                                                                                
CLIPROF  DS    0H                                                               
         BRAS  RE,WBFLTS           FILL WBFLT TABLE                             
*                                                                               
         TM    COPT4,COP4MIDS      MIDAS CLIENT?                                
         BZ    *+8                                                              
         OI    NBUNTSW,X'10'       MIDAS CLIENT                                 
*                                                                               
                                                                                
*                                  GET USER PROFILES                            
         OC    NBUSER,NBUSER       CK IF NBUSER ALREADY FILLED                  
         BNZ   CLIOK                                                            
         XC    KEY,KEY             GET USER PROFILE INTO NBUSER                 
         MVC   KEY(4),=C'S0N0'                                                  
         MVC   KEY+4(2),NBEFFAGY                                                
         MVI   KEY+6,C'N'                                                       
         MVC   KEY+7(3),NBCLICOD                                                
         MVI   KEY+10,C'*'                                                      
         MVC   KEY+11(1),NBEFFOFF                                               
         GOTO1 NBGTPROF,DMCB,KEY,NBUSER,NBDM                                    
         MVI   KEY+3,C'1'          GET N1 PROFILE INTO NBUSER1                  
         GOTO1 NBGTPROF,DMCB,KEY,NBUSER1,NBDM                                   
         MVI   KEY+3,C'2'          GET N2 PROFILE INTO NBUSER2                  
         GOTO1 NBGTPROF,DMCB,KEY,NBUSER2,NBDM                                   
         MVI   KEY+3,C'3'          GET N2 PROFILE INTO NBUSER2                  
         GOTO1 NBGTPROF,DMCB,KEY,SVUSER3,NBDM                                   
*                                                                               
         NI    NBINDS9,X'FF'-NBI9SLDV    SHOW LOCKED DEMO VALUES?               
         CLI   SVUSER3,C'Y'                                                     
         JNE   *+8                                                              
         OI    NBINDS9,NBI9SLDV                                                 
*                                                                               
         BAS   RE,MEDINIT          MAY NEED TO BUILD MEDIA BUFFER               
         B     CLIOK                                                            
         SPACE 1                                                                
BADCLI   MVI   NBERROR,NBINVCLI                                                 
         BAS   RE,GOERR                                                         
         SPACE 1                                                                
CLIOK    MVI   BRNCHNUM,BMCLIFIN                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRCLIFIN DS    0H                                                               
         EJECT                                                                  
*              VALIDATE PRODUCTS                                                
         SPACE 3                                                                
*              SPECIAL VALUES: NBSELPRD = ALL                                   
*                                                                               
*              INPUT:  NBACTAM - MUST ALREADY BE VALIDATED                      
*                      NBACTCLI - MUST ALREADY BE VALIDATED                     
*                      NBSELPNM,NBSELPRD,NBSELPGR                               
*                                                                               
*              OUTPUT: NBPRDMSK - PRODUCT CODE MASK                             
*                   IF NBSELPNM = 0:                                            
*                      NBSELPRD - IF 0 THEN SET TO POL                          
*                      NBAPRD - IF NON-ZERO PRODUCT RECORD HERE                 
         SPACE 1                                                                
QVALPRD  MVI   NBMODE,NBVALPRD                                                  
         MVI   NBPRDALL,0                                                       
         L     RE,NBADRPRG         CLEAR PROD STORAGE AREA                      
         LTR   RE,RE                                                            
         BZ    QV00                                                             
         L     RF,=F'1600'                                                      
         XCEF                                                                   
QV00     DS    0H                                                               
*                                                                               
***      XC    NBPRDMSK,NBPRDMSK   INITIALIZE PRODUCT MASK                      
***      CLI   NBSELPNM,0                                                       
***      BE    QI14                                                             
***      ZIC   R1,NBSELPNM         PRODUCT CODE SPECIFIED                       
***      LA    R2,NBPRDMSK                                                      
***      BAS   RE,SETMASK                                                       
***      SR    R2,R2                                                            
***      BAS   RE,FILLPDET                                                      
***      MVC   NBEFFPNM,NBSELPNM    EFFECTIVE PROD NUMBER                       
***      B     PRDOK                                                            
*                                                                               
         CLI   NBSELPNM,0                                                       
         BNE   QI10                                                             
         OC    NBSELPN3,NBSELPN3                                                
         BZ    QI14                                                             
*                                  USER PASSED NBSELPN3                         
QI9      DS    0H                                                               
*****    MVC   NBSELPRD,NBSELPN3   SET IT IN NBSELPRD                           
         MVC   NBEFFPN3,NBSELPN3   AND 3 CHAR EFFECTIVE PROD                    
         MVC   NBEFFPNM,NBSELPNM                                                
         B     PRDOK                                                            
*                                  USER PASSED BINARY                           
QI10     OC    NBSELPN3,NBSELPN3   DO WE HAVE BINARY AND 3 CHAR ?               
         BNZ   QI9                 YES                                          
         MVC   NBEFFPNM,NBSELPNM   NO/SET BINARY                                
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING PLSTPSSV,R4                                                      
         MVC   PLSTTYPE(2),=X'0DF1'                                             
         MVC   PLSTAM(3),NBACTAM           A/M + CLIENT                         
         MVC   PLSTBPRD+1(1),NBSELPNM      BINARY PROD                          
         MVC   KEYSAVE,KEY                                                      
         BAS   RE,HIGH                                                          
QI10B    CLC   KEY(5),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   KEY+9(2),KEYSAVE+9  SAME BINARY CODE                             
         BE    QI12                                                             
         BAS   RE,SEQ                                                           
         B     QI10B                                                            
QI12     MVC   NBSELPRD,PLSTPRD                                                 
         MVC   NBEFFPN3,PLSTPRD                                                 
         B     PRDOK                                                            
         DROP  R4                                                               
************************************************************                    
QI14     B      QI15            !!!!!!!! DO NOT USE !!!!!!!                     
**       TM    NBINDS5,NBI5PSEC    BRAND LEVEL SEC?                             
**       BNO   QI15                                                             
**       LA    R4,KEY                                                           
**       XC    KEY,KEY                                                          
**       USING PRDHDR,R4                                                        
**       MVC   PKEYAM(3),NBACTAM      SET A/M AND CLIENT IN KEY                 
**       CLI   NBSELPGR,0          IF PROD GRPS                                 
**       BE    QI14C                                                            
**       CLC   NBSELPRD,=C'ALL'                                                 
**       BE    PRODGRP                                                          
**       CLC   NBSELPRD,=C'POL'                                                 
**       BE    PRODGRP                                                          
QI14C    CLC   NBSELPRD,=C'ALL'                                                 
**       BE    QI14D                                                            
**       CLC   NBSELPRD,=C'POL'                                                 
**       BNE   QI15                                                             
QI14D    DS    0H                 SET BRAND LEVEL PRODUCTS                      
***      BAS   RE,SETPSEC         SET BRAND LEVEL PRODUCTS                      
**       B     PRDOK                                                            
************************************************************                    
*                                                                               
QI15     LA    R4,KEY              NO PRODUCT NUMBER GIVEN                      
         XC    KEY,KEY                                                          
         USING PRDHDR,R4                                                        
         MVC   PKEYAM(3),NBACTAM      SET A/M AND CLIENT IN KEY                 
         CLC   NBSELPRD,=C'ALL'                                                 
         BE    PRODGRP                                                          
         MVC   PKEYPRD,NBSELPRD                                                 
         BAS   RE,READ             GET PRODUCT RECORD                           
         BNE   BADPRD                                                           
         BAS   RE,GETREC                                                        
         L     R4,NBAIO                                                         
         L     R3,NBAPRD           OPTION TO GET BACK PRODUCT                   
         LTR   R3,R3                                                            
         BZ    *+10                                                             
         MVC   0(240,R3),0(R4)                                                  
         SPACE 1                                                                
****     TM    NBINDS5,NBI5PSEC    TRAFFIC PROD LEVEL SECURITY?                 
****     BNO   QI19                                                             
****     TM    NBINDS5,NBI5PLST    OFFICE LIST ?                                
****     BNO   QI18                NO                                           
****     MVC   BYTE,PTRAFOFC                                                    
****     BAS   RE,PSECOFF          YES/GO TO OFFICER                            
****     BNE   BADPRD                                                           
****     B     QI19                                                             
**QI18     CLC   NBTRAPSC,PTRAFOFC                                              
**         BNE   BADPRD                                                         
QI19     LH    R1,PCODE                                                         
**       LA    R2,NBPRDMSK                                                      
**       BAS   RE,SETMASK                                                       
**       SR    R2,R2                                                            
**       BAS   RE,FILLPDET                                                      
         STC   R1,NBEFFPNM                                                      
QI19B    MVC   NBEFFPN3,PKEYPRD    SET 3 CHAR PROD                              
         CLC   NBSELPRD,=C'POL'    IF NOT POOL THEN ALL DONE                    
         BNE   PRDOK                                                            
         MVI   NBEFFPNM,0          RESET FOR POOL                               
         EJECT                                                                  
*              HANDLE PRODUCT GROUPS                                            
         SPACE 3                                                                
*                                  WHEN NBSELPRD = ALL OR POL                   
*                                       AND NBSELPGR IS SET                     
*                                       OR IF A(PBUFF) PROVIDED                 
         SPACE 1                                                                
PRODGRP  CLI   NBSELPGR,0          IF NO PRODUCT GROUP                          
         BNE   PG21                                                             
***      MVC   NBPRDMSK,FFS        SET PRDMSK AND EXIT                          
***      OC    NBAPBUFF,NBAPBUFF   UNLESS PBUFF PROVIDED                        
***      BZ    PRDOK                                                            
         MVI   NBPRDALL,X'FF'                                                   
         B     PRDOK                                                            
         SPACE 1                                                                
PG21     LA    R4,KEY                                                           
         XC    PKEYPRD,PKEYPRD                                                  
         SPACE 1                                                                
PG22     LA    R4,KEY              GET NEXT PRODUCT                             
         AI    PKEYPRD+2,1                                                      
         BAS   RE,HIGH                                                          
         CLC   KEY(4),KEYSAVE                                                   
         BE    PG23                                                             
         MVC   KEY,KEYSAVE         NEED TO RESORE POL                           
         CLC   NBSELPRD,=C'POL'                                                 
         BNE   PRDOK                                                            
         MVC   PKEYPRD,=C'POL'                                                  
         BAS   RE,HIGH                                                          
         BAS   RE,GETREC                                                        
         B     PRDOK                                                            
         SPACE 1                                                                
PG23     BAS   RE,GETREC                                                        
         L     R4,NBAIO                                                         
         TM    NBINDS5,NBI5PSEC    BRAND LEVEL SEC?                             
         BNO   QI2300              NO                                           
         TM    NBINDS5,NBI5PLST    TRAFFIC OFFICE LIST ?                        
         BNO   QI230                                                            
         MVC   BYTE,PTRAFOFC       CHECK AGAINST OFFICE LIST                    
         BAS   RE,PSECOFF                                                       
         BNE   PG22                                                             
         B     QI2300                                                           
QI230    CLC   NBTRAPSC,PTRAFOFC  YES-MATCHES TRAFFIC                           
         BNE   PG22                   -NO MATCH/GET NXT PRODUCT                 
QI2300   LA    R2,PGRP1            SELECT ASSIGNMENT (V,W,X)                    
         CLC   NBSELPGR(1),PGRP1                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
         BNO   QI23A                                                            
         MVC   BYTE,NBSELPGR                                                    
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP1                                                       
         BE    PG22                                                             
QI23A    LA    R2,PGRP2                                                         
         CLC   NBSELPGR(1),PGRP2                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
         BNO   QI23B                                                            
         MVC   BYTE,NBSELPGR                                                    
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP2                                                       
         BE    PG22                                                             
QI23B    LA    R2,PGRP3                                                         
         CLC   NBSELPGR(1),PGRP3                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
         BNO   QI23C                                                            
         MVC   BYTE,NBSELPGR                                                    
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP3                                                       
         BE    PG22                                                             
QI23C    LA    R2,PGRP4                                                         
         CLC   NBSELPGR(1),PGRP4                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
         BNO   QI23CC                                                           
         MVC   BYTE,NBSELPGR                                                    
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP4                                                       
         BE    PG22                                                             
QI23CC   LA    R2,PGRP5                                                         
         CLC   NBSELPGR(1),PGRP5                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
         BNO   QI23D                                                            
         MVC   BYTE,NBSELPGR                                                    
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP5                                                       
         BE    PG22                                                             
QI23D    LA    R2,PGRP6            FIELD = 5X3                                  
         LA    R0,5                                                             
QI23E    CLC   NBSELPGR(1),0(R2)                                                
         BE    QI24                                                             
         TM    NBSELPGR,X'40'      IS IT NEGATIVE FILTER                        
***      BO    PG22                NO / GET NEXT REC                            
         BZ    QI23G                                                            
         LA    R2,3(R2)            NO/BUMP TO NEXT SET                          
         BCT   R0,QI23E                                                         
         B     PG22                                                             
QI23G    MVC   BYTE,NBSELPGR       YES/CHECK IT                                 
         OI    BYTE,X'40'                                                       
         CLC   BYTE,PGRP6                                                       
         BE    PG22                                                             
         SPACE 1                                                                
QI24     UNPK  DUB(5),1(3,R2)      SEE IF THIS PRODUCT IS IN                    
         LA    RF,DUB                                                           
         LA    RE,NBSELPGR+1                                                    
         LA    R0,4                                                             
         SPACE 1                                                                
QI25     CLI   0(RE),C'*'          WILD CARD OK                                 
         BE    QI26                                                             
         CLI   0(RE),X'40'         BLANKS/ZERO OK                               
         BNH   QI26                                                             
         CLC   0(1,RE),0(RF)       ELSE IT MUST MATCH                           
         BE    QI26                                                             
         TM    0(RE),X'40'         IS IT NEGATIVE FILTER                        
         BO    PG22                NO                                           
         MVC   BYTE,0(RE)          YES/CHECK IT                                 
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(RF)                                                       
         BE    PG22                                                             
         SPACE 1                                                                
QI26     LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,QI25                                                          
         SPACE 1                                                                
         LH    R1,PCODE            PASSED THE TESTS SO...                       
**       BAS   RE,FILLPDET         FILL IN DETAILS                              
***      LA    R2,NBPRDMSK                                                      
***      BAS   RE,SETMASK          SET THIS PRODUCT AS OK                       
         MVC   WORK(3),PKEYPRD     PASS 3 CHAR CODE TO FILLPDET NOW             
         BAS   RE,SETOPMSK         SEET 3 CHAR IN PRODMASK                      
         BAS   RE,FILLPDET         FILL IN DETAILS                              
         B     PG22                                                             
         SPACE 1                                                                
BADPRD   CLC   NBSELCLI,=C'ALL'    IF ALL THEN NEXT CLI                         
         BE    QE2                                                              
         MVI   NBERROR,NBINVPRD                                                 
         BAS   RE,GOERR                                                         
         SPACE 1                                                                
PRDOK    MVI   BRNCHNUM,BMPRDFIN                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRPRDFIN B     QVALEST                                                          
         EJECT                                                                  
*                                                                               
* SET 3 CHAR PROD IN OVERFLOW PROD MASK AREA                                    
* INPUT  R1 -       BINARY PROD CODE                                            
* INPUT  WORK -     3 CHAR PROD CODE                                            
*                                                                               
SETOPMSK NTR1                                                                   
         SR    R5,R5                                                            
         CLI   NBSELPGR,0            IF NO PROD GROUP                           
         BE    STX                   NO NEED TO SET                             
         ICM   R2,15,NBADRPRG       USER PASSED ADDRESS OF PROD TABLE           
******   BZ    STX                                                              
         BNZ   ST19                YES                                          
         OC    NBACOM,NBACOM       NO - IF ON-LINE                              
         BNZ   STX                      OK                                      
         DC    H'0'                     OFF-LINE TAKE A HIT                     
ST19     LA    R5,500              FOR 500 PRODS                                
*                                                                               
ST20     OC    0(3,R2),0(R2)                                                    
         BZ    ST21                                                             
         CLC   1(3,R2),WORK        DO WE ALREADY HAVE IT?                       
         BE    STX                 YES                                          
         LA    R2,4(R2)                                                         
         BCT   R5,ST20                                                          
         DC    H'0'                EXPAND TABLE                                 
ST21     STC   R1,0(R2)            SET OVERFLOW BINARY PROD                     
         MVC   1(3,R2),WORK        SET OVERFLOW CL3 PRD                         
STX      XIT1  REGS=(R5)                                                        
*                                                                               
* TEST PROD CODE AGAINST OP PROD AREA                                           
* INPUT  WORK BYTE 1 = BINARY CODE                                              
* INPUT  WORK CL3 = 3 CHAR PROD CODE                                            
* PROD TABEL = (1 BINARY + 3 CHAR) X 200 PRODS                                  
TSTOPMSK NTR1                                                                   
         ICM   R2,15,NBADRPRG         ADDRESS OF PROGR GROUP?                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
***      LA    RE,200              HARD CODED FOR 500 OVERFLOW PRDMSK           
         LA    RE,500              HARD CODED FOR 500 OVERFLOW PRDMSK           
TS20     CLC   1(3,R2),WORK                                                     
         BE    TSX                                                              
         CLI   WORK+1,0            ARE WE DEALING WITH BINARY PROD              
         BNE   TS22                NO                                           
         CLC   0(1,R2),WORK        YES                                          
         BE    TSX                                                              
TS22     LA    R2,4(R2)                                                         
         OC    0(4,R2),0(R2)       IF UNALLOCATED-REJECT- (ONLY                 
         BZ    TSNO                PROD GRP REQUESTS SHOULD BE                  
         BCT   RE,TS20             CALLING THIS ROUTINE)                        
TSNO     LTR   R2,R2               SET CC -> NOT EQUAL                          
TSX      XIT1                                                                   
                                                                                
* CHECK BRAND AGAINST OFFICE LIST                                               
* WE ARE DOING OLD OFFICER CALL FOR NOW SINCE WE DON'T HAVE                     
* CLIENT ACCESS LIST AROUND -                                                   
PSECOFF  NTR1                                                                   
         OC    ADROFFCR,ADROFFCR                                                
         BNZ   PSECO10                                                          
*****->  BNZ   PSECO20                                                          
         XC    DMCB(8),DMCB                                                     
         MVC   DMCB+4(4),=X'D9000A38' GET OFFICER ADDRESS                       
         GOTO1 NBCALLOV,DMCB                                                    
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   ADROFFCR,DMCB                                                    
****->   B     PSECO20             NEW OFFICER CALL                             
PSECO10  LA    R1,DUB              OLD OFFICER CALL                             
         USING OFFICED,R1                                                       
         XC    DUB,DUB                                                          
         MVI   OFCSYS,C'S'                                                      
         MVI   OFCAUTH,C'$'                                                     
         MVC   OFCAUTH+1(1),NBTRAPSC                                            
         OI    OFCAUTH+1,X'C0'                                                  
         MVC   OFCAGY,NBSELAGY                                                  
         MVC   OFCOFC,BYTE                                                      
         DROP  R1                                                               
         GOTO1 ADROFFCR,DMCB,DUB,NBACOM                                         
         CLI   0(R1),0                                                          
         XIT1                                                                   
*                                                                               
PSECO20  ICM   R3,15,NBSECSV                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING NEWSECD,R3                                                       
         LA    R2,NEWOFFB                                                       
         USING OFFICED,R2                                                       
         XC    NEWOFFB,NEWOFFB                                                  
*                                                                               
         MVI   OFCSYS,C'S'                                                      
         MVI   OFCAUTH,C'$'                                                     
         MVC   OFCAUTH+1(1),NBTRAPSC                                            
         MVC   OFCAGY,NBSELAGY                                                  
         MVC   OFCOFC,BYTE             BYTE HAS OFFICE                          
         GOTO1 NBCLUNPK,DMCB,(CPROF+6,NBACTCLI),OFCCLT                          
         OC    OFCCLT,=X'404040'                                                
         MVC   OFCSAGMD,NBACTAM                                                 
         MVC   OFCLMT(4),NBTWAACC                                               
** WILL NEED ACCESS LIST BY READING CLIENT REC RO FROM TRAFFIC                  
** IF WE HAVE TO COME HERE FOR TRAFFIC BRAND LEVEL SECURITY                     
         MVC   OFCACCSC(3),CACCESS    ACCESS LIST FROM CLTHDR                   
         LA    RF,NEWSECA                                                       
         ST    RF,OFCSECD                                                       
         GOTO1 ADROFFCR,DMCB,DUB,NBACOM                                         
         CLI   0(R1),0             IS OFFICE IN LIST/SET CC CODE                
         XIT1                                                                   
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
         EJECT                                                                  
* READ PRODUCTS, THOSE THAT MATCH SECURITY SET IN PRODMASK                      
***SETPSEC  NTR1                                                                
         LA    R4,KEY                                                           
         XC    PKEYPRD,PKEYPRD                                                  
         SPACE 1                                                                
SETP22   LA    R4,KEY              GET NEXT PRODUCT                             
         AI    PKEYPRD+2,1                                                      
         BAS   RE,HIGH                                                          
         CLC   KEY(4),KEYSAVE                                                   
         BE    SETP23                                                           
         MVC   KEY,KEYSAVE         NEED TO RESORE POL                           
         CLC   NBSELPRD,=C'POL'                                                 
         BNE   SETPX                                                            
         MVC   PKEYPRD,=C'POL'                                                  
         BAS   RE,HIGH                                                          
         BAS   RE,GETREC                                                        
         B     SETPX                                                            
         SPACE 1                                                                
SETP23   BAS   RE,GETREC                                                        
         L     R4,NBAIO                                                         
         TM    NBINDS5,NBI5PLST    OFFICE LIST?                                 
         BNO   SETP28                                                           
         MVC   BYTE,PTRAFOFC                                                    
         BAS   RE,PSECOFF                                                       
         BNE   SETP22                                                           
         B     SETP28B                                                          
SETP28   CLC   NBTRAPSC,PTRAFOFC  YES-MATCHES TRAFFIC                           
         BNE   SETP22                 -NO MATCH/GET NXT PRODUCT                 
SETP28B  LH    R1,PCODE                                                         
***      LA    R2,NBPRDMSK                                                      
***      BAS   RE,SETMASK          SET THIS PRODUCT AS OK                       
SETP30   MVC   NBEFFPN3,PKEYPRD                                                 
         BAS   RE,SETOPMSK                                                      
         XC    NBEFFPN3,NBEFFPN3                                                
         B     SETP22                                                           
SETPX    XIT1                                                                   
         EJECT                                                                  
*              FILL IN PRODUCT DETAILS IF A(PBUFF) PROVIDED                     
         SPACE 3                                                                
*              INPUT   R2=A(PRODUCT GROUP) OR ZERO                              
*                      NBAIO=A(PRODUCT RECORD)                                  
*              OUTPUT  2 BYTE PRODUCT GROUP IN PBUFF                            
*              LOCALS  R3=A(BUFFER ENTRY)                                       
*                      R4=A(PRODUCT RECORD)                                     
*                      R5=INDEX COUNT OF PROD FROM SETOPMSK                     
         SPACE 1                                                                
FILLPDET NTR1                                                                   
         LTR   R2,R2               NEED A(PRODUCT GROUP)                        
         BZ    FILLXIT                                                          
         USING PBUFFD,R3                                                        
         USING PRDHDR,R4                                                        
         L     R3,NBAPBUFF         PICK UP A(BUFFER) IF AROUND                  
         LTR   R3,R3                                                            
         BZ    FILLXIT                                                          
         L     R4,NBAIO                                                         
****     LH    R1,PCODE                                                         
****     CLI   PCODE+1,255                                                      
****     BNE   *+8                                                              
****     LA    R1,220              POL IS IN SLOT 220                           
****     BCTR  R1,0                                                             
****     SLL   R1,1                EACH ENTRY IS 2 BYTES                        
****     AR    R3,R1               R3 NOW HAS A(ENTRY)                          
*        LA    R1,400              MAX NUMBER OF PRODS IN PRODGROUP             
         LA    R1,500              MAX NUMBER OF PRODS IN PRODGROUP             
         SR    R1,R5               R5 HAS INDEX FROM SETOPMSK                   
         SLL   R1,1                EACH ENTRY 2 BYTES                           
         AR    R3,R1                                                            
         MVC   PBUFFPGR,1(R2)      SAVE PRODUCT GROUP                           
         SPACE 1                                                                
FILLXIT  XIT1                                                                   
         DROP  R3                                                               
         DROP  R4                                                               
         EJECT                                                                  
*              VALIDATE ESTIMATES                                               
         SPACE 3                                                                
*              INPUT:  NBACTAM    MUST ALREADY BE VALIDATED                     
*                      NBACTCLI   MUST ALREADY BE VALIDATED                     
*                      NBSELPRD   MUST BE ALREADY VALIDATED                     
*                                 IF 0, POL IS USED                             
*                      NBSELEST,NBSELESE,NBSELSTR,NBSELEFL                      
*                                                                               
*              OUTPUT: NBESTMSK   ESTIMATE CODE MASK                            
*                      NBAPRD     IF NON-ZERO RECORD HERE                       
*                      NBEFFEST   EFFECTIVE ESTIMATE NUMBER                     
         SPACE 1                                                                
QVALEST  MVI   NBMODE,NBVALEST                                                  
         MVI   NBEFFEST,0          PRESET EFFECTIVE NUMBER                      
         XC    KEY,KEY             SET UP GENERAL KEY                           
         LA    R4,KEY                                                           
         USING ESTHDR,R4                                                        
         MVC   EKEYAM(3),NBACTAM   SET A/M AND CLIENT IN KEY                    
         MVC   EKEYPRD,NBSELPRD                                                 
         CLC   EKEYPRD,=C'ALL'     IF NBSELPRD=ALL USE POL IN KEY               
         BNE   *+10                                                             
         MVC   EKEYPRD,=C'POL'                                                  
         MVC   EKEYEST,NBSELEST                                                 
         CLI   NBSELESE,0          IF EST RANGE, GET FIRST                      
         BNE   QVEST2                                                           
         CLI   NBSELEST,0          IF ONE ESTIMATE SELECTED GOTO READ           
         BNE   GETEST                                                           
         MVI   EKEYEST,1           IF ALL, GET FIRST EST                        
         BAS   RE,HIGH                                                          
         CLI   EKEYEST+1,0         MAKE SURE THIS IS EST                        
         BNE   BADEST                                                           
         B     PROCEST                                                          
         SPACE 1                                                                
QVEST2   BAS   RE,HIGH             READ FIRST IF ALL OR RANGE                   
         CLC   KEY(7),KEYSAVE                                                   
         BE    PROCEST                                                          
         B     BADEST                                                           
         SPACE 1                                                                
GETEST   BAS   RE,READ                                                          
         BE    PROCEST                                                          
         B     BADEST                                                           
         SPACE 1                                                                
PROCEST  BAS   RE,GETREC                                                        
         L     R3,NBAEST           OPTION TO PASS BACK ESTIMATE                 
         LTR   R3,R3                                                            
         BZ    QI36                IF NBAEST SET PASS RECORD TO USER            
         L     R4,NBAIO                                                         
         MVC   DUB(2),ELEN                                                      
         LH    R5,DUB                                                           
         MOVE  ((R3),(R5)),(R4)                                                 
         SPACE 1                                                                
QI36     TM    NBINDS3,NBI3ESTM    USER SETS ESTIMATE MASK?                     
****     BO    ESTOK               YES/SO THAT'S ALL                            
         BO    ESTFILT                                                          
         MVC   NBESTMSK,FFS        SET UP ESTIMATE MASK                         
         CLI   NBSELEST,0          FOR NO/ALL USE FF'S                          
         BE    ESTFILT                                                          
         SPACE 1                                                                
         XC    NBESTMSK,NBESTMSK                                                
         ZIC   R1,NBSELEST                                                      
         LA    R2,NBESTMSK                                                      
         BAS   RE,SETMASK          FOR SINGLE ESTIMATE                          
         STC   R1,NBEFFEST                                                      
         ZIC   R3,NBSELESE                                                      
         LTR   R3,R3                                                            
         BZ    ESTFILT                                                          
         SPACE 1                                                                
QI38     LA    R1,1(R1)            SET MASK TO RANGE NBSELEST TO NBSELE         
         CR    R1,R3                                                            
         BH    ESTFILT                                                          
         BAS   RE,SETMASK          FOR ESTIMATE RANGE                           
         B     QI38                                                             
         SPACE 1                                                                
ESTFILT  L     R4,NBAIO                                                         
         OC    NBSELEFL,NBSELEFL   PROCESS FILTERS                              
         BZ    ESTOK                                                            
         TM    NBINDS3,NBI3ESTM       USER PASSED EST MASK?                     
         BNO   *+10                                                             
         MVC   WORK(32),NBESTMSK   SAVE EST MASK                                
         SPACE 1                                                                
         XC    NBESTMSK,NBESTMSK   REDO ESTMSK                                  
         LA    R4,KEY                                                           
         MVI   EKEYEST,0                                                        
         SPACE 1                                                                
QI42     LA    R4,KEY                                                           
         CLI   EKEYEST,255                                                      
         BE    ESTOK               LOOP EXIT                                    
         AI    EKEYEST,1           SKIP TO NEXT ESTIMATE                        
         XC    EKEYEST+1(5),EKEYEST+1                                           
         BAS   RE,HIGH                                                          
         CLC   KEY(7),KEYSAVE                                                   
         BNE   ESTOK               LOOP EXIT                                    
         CLI   NBSELESE,0          MAKE SURE ITS WITHIN A RANGE                 
         BE    QI44                                                             
         CLC   EKEYEST,NBSELEST                                                 
         BL    QI42                                                             
         CLC   EKEYEST,NBSELESE                                                 
         BH    QI42                                                             
         SPACE 1                                                                
QI44     L     R4,NBAIO            MEETS FILTER                                 
         BAS   RE,GETREC                                                        
         LA    R3,NBSELEFL                                                      
         LA    R5,EPROF                                                         
         LA    R0,3                                                             
         SPACE 1                                                                
QI46     CLI   0(R3),C'*'          WILD                                         
         BE    QI48                                                             
         CLI   0(R3),0                                                          
         BE    QI48                                                             
         TM    0(R3),X'40'                                                      
         BZ    QI47                                                             
         CLC   0(1,R3),0(R5)       FILTER                                       
         BNE   QI42                                                             
         B     QI48                                                             
         SPACE 1                                                                
QI47     MVC   HOLDEFLT,0(R5)                                                   
         NI    HOLDEFLT,X'FF'-X'40'   TURN OFF X'40' BIT                        
         CLC   0(1,R3),HOLDEFLT    NEGATIVE FILTER                              
         BE    QI42                                                             
         SPACE 1                                                                
QI48     LA    R3,1(R3)                                                         
         LA    R5,1(R5)                                                         
         BCT   R0,QI46                                                          
         ZIC   R1,EKEYEST          PASSED FILTERS                               
         LA    R2,NBESTMSK                                                      
         BAS   RE,SETMASK          SO SET MASK                                  
         CLI   NBEFFEST,0          SAVE NUMBER OF FIRST GOOD EST                
         BNE   QI42                                                             
         STC   R1,NBEFFEST                                                      
         L     R3,NBAEST           OPTION TO PASS BACK ESTIMATE                 
         LTR   R3,R3                                                            
         BZ    QI42                IF NBAEST SET PASS RECORD TO USER            
         L     R4,NBAIO                                                         
         MVC   DUB(2),ELEN                                                      
         LH    R5,DUB                                                           
         MOVE  ((R3),(R5)),(R4)                                                 
         B     QI42                                                             
         SPACE 1                                                                
BADEST   CLC   NBSELCLI,=C'ALL'    IF ALL CLIENTS THEN GET NEXT                 
         BE    QE2                                                              
         MVI   NBERROR,NBINVEST                                                 
         BAS   RE,GOERR                                                         
         SPACE 1                                                                
ESTOK    TM    NBINDS3,NBI3ESTM    ESTIMATE MASK ?                              
         BNO   ESTOKOK                                                          
         OC    NBSELEFL,NBSELEFL                                                
         BZ    ESTOKOK                                                          
         NC    NBESTMSK,WORK                                                    
ESTOKOK  MVI   BRNCHNUM,BMESTFIN                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRESTFIN DS    0H                                                               
         SPACE 1                                                                
         EJECT                                                                  
*              HANDLE PACKAGES                                                  
         SPACE 3                                                                
*              LOCALS              R3=LENGTH OF KEY FOR COMPARE                 
         SPACE 1                                                                
DOPKG    MVI   NBMODE,NBREQFST                                                  
         MVI   BRNCHNUM,BMFSTPAK                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRFSTPAK CLI   NBDATA,0            MAKE SURE PACKAGES ARE WANTED                
         BE    QE2                                                              
         MVI   NBFILE,C'U'                                                      
         CLI   NBDATA,C'U'         (U=UNITS ONLY)                               
         BE    ENDPKG                                                           
         LA    R4,KEY              SET UP KEY FOR READS                         
         XC    KEY,KEY                                                          
         USING NPRECD,R4                                                        
         MVI   NPKTYPE,X'02'                                                    
         MVC   NPKAM(3),NBACTAM                                                 
         LA    R3,13               R3=L'EFFECTIVE KEY -1                        
         CLI   NBSELNET,0                                                       
         BE    GET1STPK                                                         
         MVC   NPKNET,NBSELNET                                                  
         LA    R3,4(R3)                                                         
         CLI   NBSELEST,0                                                       
         BE    GET1STPK                                                         
         CLI   NBSELESE,0          IF AN ESTIMATE RANGE                         
         BNE   GET1STPK              DONT PUT SELEST IN KEY                     
         MVC   NPKEST,NBSELEST                                                  
         LA    R3,1(R3)                                                         
         CLI   NBSELPAK,0                                                       
         BE    GET1STPK                                                         
         MVC   NPKPACK,NBSELPAK                                                 
         LA    R3,1(R3)                                                         
         SPACE 1                                                                
GET1STPK BAS   RE,HIGH                                                          
         SPACE 1                                                                
CKPAKKEY EXCLC R3,KEY,KEYSAVE      CHECK MAIN C/B                               
         BNE   NOMORPK             NO MORE PACKAGES                             
         BAS   RE,PKCHECK                                                       
         BNE   NEXTPAK             NOT OK. TRY NEXT                             
         MVI   NBMODE,NBPROCPK                                                  
         MVI   BRNCHNUM,BMNXTPAK                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRNXTPAK DS    0H                                                               
         SPACE 1                                                                
NEXTPAK  DS    0H                                                               
****     L     R1,NBAIO                                                         
****     XC    0(200,R1),0(R1)     CLEAR IO AREA/PKG FILT PROB                  
         BAS   RE,SEQ                                                           
         B     CKPAKKEY                                                         
         SPACE 1                                                                
NOMORPK  B     ENDPKG                                                           
         EJECT                                                                  
*              CONTROL FILTERING OF PACKAGE RECORDS                             
         SPACE 3                                                                
PKCHECK  NTR1                                                                   
         LA    R4,KEY              NEED TO RESTORE R4 IN CASE IT CHNGED         
         ZIC   R1,NPKEST           CHECKS IF PACKAGE SHOULD BE SELECTED         
         BAS   RE,STWRDEST         TEST STEWARSHIP ESTIMATE                     
         BNE   NO                                                               
         LA    R2,NBESTMSK         TEST FOR ESTIMATE FILTERING                  
         BAS   RE,TESTMASK                                                      
         BNE   NO                                                               
         LA    R5,NPKNET           A(NETWORK)                                   
         BAS   RE,MEDFILT          FOR MEDIA FILTERING                          
         BNE   NO                                                               
         BAS   RE,GETREC                                                        
         DROP  R4                                                               
         L     R6,NBAIO                                                         
         USING NPRECD,R6                                                        
         TM    NPAKSTAT,X'01'      NO-SHOW PACKAGE                              
         BO    NO                                                               
         CLI   NBSELDP,0           DAYPART FILTERING                            
         BE    PK10                                                             
         CLC   NBSELDP,NPAKDP                                                   
         BNE   NO                                                               
         SPACE 1                                                                
PK10     CLI   NBSELPAK,0          PACKAGE FILTER                               
         BE    PK11                                                             
         CLC   NBSELPAK,NPKPACK                                                 
         BNE   NO                                                               
         SPACE 1                                                                
PK11     CLI   NBUPLD,0            ...UPLOAD FILTER                             
         BE    PK11B                                                            
         TM    NBUPLD,X'01'        ...UPLOADED DATA ONLY                        
         BNO   PK11A                                                            
         TM    NPAKCNTL,X'10'                                                   
         BNO   NO                                                               
         B     PK11B                                                            
PK11A    TM    NBUPLD,X'02'        ...SKIP UPLOADED DATA                        
         BNO   PK11B                                                            
         TM    NPAKCNTL,X'10'                                                   
         BO    NO                                                               
         SPACE 1                                                                
PK11B    CLI   NBSELPST,C'L'       OPTION TO SELECT LOCKED ONLY                 
         BNE   PK12                                                             
         TM    NPAKSTAT,X'20'                                                   
         BNO   NO                                                               
         B     PK14                                                             
         SPACE 1                                                                
PK12     CLI   NBSELPST,C'B'       DEFAULT IS TO IGNORE LOCKED                  
         BE    PK14                                                             
         TM    NPAKSTAT,X'20'                                                   
         BO    NO                                                               
         B     PK14                                                             
         SPACE 1                                                                
PK14     CLI   NBSELPRN,C'Y'       CHECK FOR NO PRINT                           
         BNE   PK16                                                             
         TM    NPAKSTAT,X'10'      IF BIT SET, DON'T PRINT                      
         BO    NO                                                               
         B     PK16                                                             
         SPACE 1                                                                
PK16     BAS   RE,PACKFILT                                                      
         BNE   NO                                                               
         LA    R5,NPKNET           A(NETWORK)                                   
         BAS   RE,NTIFILT                                                       
         B     YES                                                              
         SPACE 1                                                                
ENDPKG   DS    0H                                                               
         EJECT                                                                  
*              VALIDATE DATES                                                   
         SPACE 3                                                                
*              INPUT               NBSELSTR NBSELEND (EBCDIC)                   
*              OUTPUT              NBCMPSTR NBCMPEND (COMPRESSED)               
         SPACE 1                                                                
QVALDAT  MVI   NBMODE,NBVALDAT                                                  
         MVC   NBCMPSTR,MINDAT     SET DEFAULTS                                 
         MVC   NBCMPEND,MAXDAT     SET DEFAULTS                                 
         OC    NBSELSTR,NBSELSTR                                                
         BZ    QVENDDAT                                                         
         GOTO1 NBDATCON,DMCB,(0,NBSELSTR),(2,NBCMPSTR)                          
         SPACE 1                                                                
QVENDDAT OC    NBSELEND,NBSELEND                                                
         BZ    DATE2                                                            
         GOTO1 NBDATCON,DMCB,(0,NBSELEND),(2,NBCMPEND)                          
         SPACE 1                                                                
DATE2    CLC   NBCMPSTR,NBCMPEND   MAKE SURE END DATE AFTER START               
         BNH   DATOK                                                            
         MVI   NBERROR,NBEBEFST                                                 
         BAS   RE,GOERR                                                         
         SPACE 1                                                                
DATOK    MVI   NBDIRECT,0          CLEAR DIRECT FIELD                           
         MVI   BRNCHNUM,BMDATFIN                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRDATFIN DS    0H                                                               
         EJECT                                                                  
*              PROCESS UNITS                                                    
         SPACE 3                                                                
         CLI   NBSEQ,C'X'          FOR NET W/IN WEEK LIST                       
         BNE   DOU1                INITIALIZE CMPSTR,END                        
         MVI   NBDONTFD,C'N'                                                    
         L     R2,NBADTL                                                        
         LTR   R2,R2                                                            
         BZ    DOU1                                                             
         MVC   NBCMPSTR(4),0(R2)                                                
         SPACE 1                                                                
DOU1     MVI   NBFILE,C'U'                                                      
         CLI   NBDATA,C'P'         TEST IF UNITS DESIRED.                       
         BE    QE2                                                              
         OC    NBSEQ,NBSEQ         DEFAULT SEQ IS 'D'                           
         BNZ   DOU2                                                             
         MVI   NBSEQ,C'D'                                                       
         SPACE 1                                                                
DOU2     LA    R4,KEY              R4 POINTS TO UNITRECORD KEY                  
         USING NUKEY,R4                                                         
         SPACE 1                                                                
         MVC   NBEFFNET,NBSELNET   NBEFFNET IS CURRENT NETWORK                  
         CLI   NBSEQ,C'P'          IF P (DT) SEQUENCE. GO TO IT                 
         BE    UNBYDT                                                           
         CLI   NBSEQ,C'Q'          IF Q (OLD PROG) SEQUENCE.                    
         BE    UNBYPROG                                                         
         SPACE 1                                                                
         OC    NBSELNET,NBSELNET   IF NET SPECIFIED, SKIP LOGIC FOR ALL         
         BNZ   GETUNIT                                                          
         CLI   NBSEQ,C'D'          IF DATE SEQUENCE, SKIP LOGIC FOR ALL         
         BE    GETUNIT                                                          
         SPACE 1                   GET FIRST NETWORK IF ALL GIVEN               
FIRSTNET DS    0H                                                               
         SPACE 1                                                                
NEXTNET  XC    KEY,KEY                                                          
         MVI   NUKPTYPE,X'84'      SET UP PASSIVE KEY TO GET NEXT NET           
         MVC   NUKPAM,NBACTAM                                                   
         MVC   NUKPCLT,NBACTCLI                                                 
         MVC   NUKPNET,NBEFFNET                                                 
         ZIC   R1,NBEFFNET+3       INCREMENT EFFNET TO GET NEXT                 
         LA    R1,1(R1)            ONLY USE LAST BYTE BECAUSE IT WONT           
         STC   R1,NUKPNET+3          REQIRE A CARRY                             
         BAS   RE,HIGH                                                          
         CLC   KEY(4),KEYSAVE      IF NO MORE NETS EXIST                        
         BNE   QE2                   WE ARE DONE                                
         MVC   NBEFFNET,NUKPNET    OTHERWISE THIS IS NEW CURRENT NET            
         SPACE 1                                                                
GETUNIT  XC    KEY,KEY                                                          
         MVI   NUKTYPE,X'04'                                                    
         MVC   NUKAM,NBACTAM                                                    
         MVC   NUKCLT,NBACTCLI                                                  
         EJECT                                                                  
*              ROUTINES TO HANDLE UNITS USING ACTUAL KEYS                       
         SPACE 3                                                                
*              NBSEQ='D'   PROCESSES ALL NETWORKS IN ONE PASS                   
*              NBSEQ='N'   AND NBSELNET=0 - PROCESS NETWORKS                    
*                           ONE AT A TIME                                       
         SPACE 1                                                                
UNBYDATE MVC   NUKDATE,NBCMPSTR    WITH DATE                                    
UD1      BAS   RE,HIGH                                                          
         B     UD6                                                              
         SPACE 1                                                                
UD4      DS    0H                                                               
         BAS   RE,SEQ                                                           
         SPACE 1                                                                
UD6      CLC   KEY(4),KEYSAVE      CHECK C/B                                    
         BNE   NOMORUN                                                          
         CLI   NBDONTFD,C'Y'       POSSIBLY NOT FILTERING                       
         BE    UD7                                                              
         CLC   NUKDATE,NBCMPEND    CHECK WITHIN DATE RANGE                      
         BH    NOMORUN                                                          
         SPACE 1                                                                
UD7      CLI   NBEFFNET,0          IF PROCESS NETS ONE AT A TIME                
         BE    UD8                                                              
         CLC   NBEFFNET,NUKNET        CHECK MATCH                               
         BNE   UD4                                                              
         SPACE 1                                                                
UD8      ZIC   R1,NUKEST           POSSIBLE ESTIMATE FILTER                     
         BAS   RE,STWRDEST         STEWARDSHIP ESTIMATE?                        
         BNE   UD4                                                              
         LA    R2,NBESTMSK                                                      
         BAS   RE,TESTMASK                                                      
         BNE   UD4                                                              
         CLI   NBSELPRG,0          POSSIBLE PROGRAM FILTER                      
         BE    UD9                                                              
         CLC   NBSELPRG,NUKPROG                                                 
         BNE   UD4                                                              
         SPACE 1                                                                
UD9      CLI   NBSELDP,0           POSSIBLE DAYPART FILTER                      
         BE    UD10                                                             
         TM    NBINDS2,NBALLDPT    ALL DPT FILTERING?                           
         BO    UD10                YES-DON'T FILTER DPT HERE                    
         CLC   NBSELDP,NUKDP       MATCH MAIN DAYPART?                          
         BNE   UD4                 NO                                           
         SPACE 1                                                                
UD10     CLI   NBSELSUB,0          POSSIBLE SUB-CODE FILTER                     
         BE    UD11                                                             
         CLC   NBSELSUB,NUKSUB                                                  
         BNE   UD4                                                              
         SPACE 1                                                                
UD11     TM    NBUPLD,X'01'        CHECK FOR CABLE UPLOAD FILTER                
         BNO   UD12                                                             
         TM    NUKPROG+3,X'F0'                                                  
         BNO   UD4                                                              
         TM    NUKPROG+4,X'F0'                                                  
         BNO   UD4                                                              
         SPACE 1                                                                
UD12     CLI   NBSELTRF,C'B'       TRAFFIC FILTER. B= NO FILTER                 
         BE    UD16                                                             
         CLI   NBSELTRF,C' '       BLANK= ONLY IF SUBCODE < X'C1'               
         BE    UD13                                                             
         CLI   NBSELTRF,C'T'       T= ONLY IF SUBCODE GE X'C1'                  
         BE    UD14                                                             
         SPACE 1                                                                
UD13     CLI   NUKSUB,X'C1'        ONLY NON-TRAFFIC UNITS                       
         BNL   UD4                                                              
         B     UD16                                                             
         SPACE 1                                                                
UD14     CLI   NUKSUB,X'C1'        ONLY TRAFFIC UNITS                           
         BNL   UD16                                                             
         B     UD4                                                              
         SPACE 1                                                                
UD16     DS    0H                                                               
         CLI   NBSELNGR,0          STATION GROUP FILTERING?                     
         BE    UD18                YES                                          
         OC    NBNETGRP,NBNETGRP   STATION GROUP TABLE?                         
         BZ    UD18                NOT AROUND                                   
         MVI   BYTE,0                                                           
         MVC   FULL,NUKNET                                                      
         BAS   RE,STGRPCHK         STATION GROUP CHECK                          
         BNE   UD4                 REJECT                                       
UD18     DS    0H                                                               
         LA    R5,NUKNET                                                        
         BAS   RE,MEDFILT                                                       
         BNE   UD4                                                              
         B     UDGETREC                                                         
         SPACE 1                                                                
UDGETREC BAS   RE,GETREC                                                        
         BNE   UD4                                                              
         TM    NBINDS2,NBBILLRD    READ NEGENUBILLS?                            
         BNO   UNITFLT                                                          
         MVI   NBWHERE,C'I'                                                     
         GOTO1 =V(NETBLRDR),DMCB,NEBLOCKD,RR=RELO                               
UNITFLT  DS    0H                                                               
*****    GOTO1 =V(PRNTBL),DMCB,=C'GTREC',KEY,C'DUMP',20,=C'1D'                  
         BAS   RE,UNITFILT         FILTER STATUS & PRODUCT                      
         BNE   UD4                                                              
         MVI   NBMODE,NBPROCUN                                                  
         MVI   BRNCHNUM,BMUBDNXT                                                
         BAS   RE,GO                                                            
*                                                                               
         CLI   NBFUNCT,NBFNXEST                                                 
         BNE   BRUBDNXT                                                         
         MVI   NBFUNCT,0                                                        
         B     QE3                    THEN EXIT                                 
         SPACE 1                                                                
BRUBDNXT LA    R4,KEY              RESET R4 IN CASE BLOCK MOVED                 
         B     UD4                                                              
         SPACE 1                                                                
NOMORUN  OC    NBSELNET,NBSELNET   DONE IF NETWORK SELECTED                     
         BNZ   QE2                                                              
         CLI   NBSEQ,C'D'          DONE IF NETS PROCESSED TOGETHER              
         BE    QE2                                                              
         B     NEXTNET             OTHERWISE GET NEXT NETWORK                   
         EJECT                                                                  
*              ROUTINE TO HANDLE UNITS BY PROGRAM WITHIN NETWORK                
         SPACE 3                                                                
UNBYPROG XC    KEY,KEY                                                          
         LA    R4,KEY              RESTORE IN CASE CHANGED                      
         MVI   NUKPTYPE,X'84'      SET UP PASSIVE KEY                           
         MVC   NUKPAM,NBACTAM                                                   
         MVC   NUKPCLT,NBACTCLI                                                 
         MVC   NUKPNET,NBEFFNET                                                 
         LA    R3,7                R3=L'C/B -1                                  
         MVC   NUKPPROG,NBSELPRG                                                
         CLI   NBSELPRG,0                                                       
         BE    GET1STUP                                                         
         LA    R3,6(R3)                                                         
         MVC   NUKPDATE,NBCMPSTR    WHEN ALL KEY FIELDS ARE GIVEN,              
         MVC   NUKPSUB,NBSELSUB      THESE WILL SPEED IT UP                     
         SPACE 1                                                                
GET1STUP BAS   RE,HIGH             GET FIRST UNIT                               
         SPACE 1                                                                
CKUPKEY  EXCLC R3,KEY,KEYSAVE                                                   
         BE    UNP2                SAME NETWORK                                 
         OC    NBSELNET,NBSELNET   IF ONE NETWORK SELECTED THEN WE ARE          
         BNZ   QE2                      DONE                                    
         CLC   KEY(4),KEYSAVE      IS THIS A NEW NETWORK                        
         BNE   QE2                   IF NO THEN WE ARE DONE                     
         MVC   NBEFFNET,NUKPNET                                                 
UNP2     BAS   RE,UPCHECK                                                       
         BNE   GETNXTUP            NOT OK. TRY NEXT                             
PROCUP   MVI   NBMODE,NBPROCUN     PROCESS THE UNIT                             
         MVI   BRNCHNUM,BMUBPNXT                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRUBPNXT LA    R4,KEY              RESET R4 IN CASE KEY MOVED                   
         LA    R3,7                AND R3 FOR KEY COMPARISON                    
         CLI   NBSELPRG,0                                                       
         BE    *+8                                                              
         LA    R3,13                                                            
         SPACE 1                                                                
GETNXTUP BAS   RE,SEQ              GET NEXT UNIT                                
         B     CKUPKEY                                                          
         SPACE 1                   (USES PASSIVE KEY)                           
UPCHECK  NTR1                      CHECKS IF UNIT IS TO BE SELECTED             
         LA    R4,KEY              RESTORE IN CASE IT CHANGED                   
UP10     CLI   NBDONTFD,C'Y'                                                    
         BE    UP11                                                             
         CLC   NUKPDATE,NBCMPSTR   DATE FILTER                                  
         BL    NO                                                               
         CLI   NBENDOK,C'Y'        WE CAN HAVE SET TO IGNORE END                
         BE    UP11                DATE CHECKING FOR M/G OUT OF EST             
         CLC   NUKPDATE,NBCMPEND                                                
         BH    NO                                                               
         SPACE 1                                                                
UP11     ZIC   R1,NUKPEST          ESTIMATE FILTER                              
         BAS   RE,STWRDEST         STEWARDSHIP ESTIMATE                         
         BNE   NO                                                               
         LA    R2,NBESTMSK                                                      
         BAS   RE,TESTMASK                                                      
         BNE   NO                                                               
         CLI   NBSELDP,0           DAYPART FILTER                               
         BE    UP12                                                             
         CLC   NBSELDP,NUKPDP                                                   
         BNE   NO                                                               
UP12     CLI   NBSELSUB,0          SUB-CODE FILTER                              
         BE    UP14                                                             
         CLC   NBSELSUB,NUKPSUB                                                 
         BNE   NO                                                               
UP14     CLI   NBSELTRF,C'B'       TRAFFIC FILTER. B= NO FILTER                 
         BE    UP20                                                             
         CLI   NBSELTRF,C' '       BLANK= ONLY IF SUBCODE < X'C1'               
         BE    UP15                                                             
         CLI   NBSELTRF,C'T'       T= ONLY IF SUBCODE GE X'C1'                  
         BE    UP16                                                             
UP15     CLI   NUKPSUB,X'C1'        ONLY NON-TRAFFIC UNITS                      
         BNL   NO                                                               
         B     UP20                                                             
UP16     CLI   NUKPSUB,X'C1'        ONLY TRAFFIC UNITS                          
         BNL   UP20                                                             
         B     NO                                                               
UP20     DS    0H                                                               
         CLI   NBSELNGR,0          STA GROUP FILTER?                            
         BE    UP21                                                             
         OC    NBNETGRP,NBNETGRP   STATION GROUP TABLE?                         
         BZ    UP21                                                             
         MVC   FULL,NUKPNET                                                     
         MVI   BYTE,0                                                           
         BAS   RE,STGRPCHK         STATION GROUP CHECK                          
         BNE   NO                                                               
UP21     DS    0H                                                               
         B     UPGETREC                                                         
         SPACE 1                                                                
UPGETREC LA    R5,NUKPNET                                                       
         BAS   RE,MEDFILT                                                       
         BNE   NO                                                               
         BAS   RE,GETREC                                                        
         BNE   NO                                                               
         TM    NBINDS2,NBBILLRD    READ NEGENUBILLS?                            
         BNO   UNITFLTA                                                         
         MVI   NBWHERE,C'I'                                                     
         GOTO1 =V(NETBLRDR),DMCB,NEBLOCKD,RR=RELO                               
UNITFLTA DS    0H                                                               
         BAS   RE,UNITFILT         RECORD FILTERS                               
         BNE   NO                                                               
         B     YES                 PASSED ALL TESTS                             
         EJECT                                                                  
*              ROUTINE TO HANDLE UNITS BY DAY-TIME WITHIN NETWORK               
         SPACE 3                                                                
UNBYDT   XC    KEY,KEY                                                          
         LA    R4,KEY              RESTORE IN CASE CHANGED                      
         MVI   NUKDTYPE,X'94'      SET UP PASSIVE KEY                           
         MVC   NUKDAM,NBACTAM                                                   
         MVC   NUKDCLT,NBACTCLI                                                 
         LA    R3,3                R3=L'C/B -1                                  
         MVC   NUKDEST,NBSELEST                                                 
         CLI   NBSELEST,0                                                       
         BE    GET1STDT                                                         
         LA    R3,1(R3)                                                         
         MVC   NUKDNET,NBSELNET                                                 
         OC    NBSELNET,NBSELNET                                                
         BZ    GET1STDT                                                         
         LA    R3,4(R3)                                                         
         SPACE 1                                                                
GET1STDT BAS   RE,HIGH             GET FIRST UNIT                               
         SPACE 1                                                                
CKDTKEY  EXCLC R3,KEY,KEYSAVE                                                   
         BE    UNDT2                                                            
         CLI   NBSELESE,0          FOR EST RANGE, USE NEXT ESTIMATE             
         BE    QE2                                                              
         CLC   KEY(4),KEYSAVE                                                   
         BNE   QE2                                                              
         CLC   NUKDEST,NBSELESE    MAKE SURE STILL IN RANGE                     
         BH    QE2                                                              
UNDT2    BAS   RE,UDTCHECK                                                      
         BNE   GETNXTDT            NOT OK. TRY NEXT                             
PROCDT   MVI   NBMODE,NBPROCUN     PROCESS THE UNIT                             
         MVI   BRNCHNUM,BMUDTNXT                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRUDTNXT LA    R4,KEY              RESET R4 IN CASE BLOCK MOVED                 
         SPACE 1                                                                
GETNXTDT DS    0H                                                               
         CLI   NBFUNCT,NBFNXEST                                                 
         BNE   GTNXTDT                                                          
         MVI   NBFUNCT,0                                                        
         ZIC   R1,NUKDEST          GET ESTIMATE                                 
         CLI   NUKDEST,X'FF'        IF LAST EST                                 
         BE    QE2                    THEN EXIT                                 
         LA    R1,1(R1)            BUMP EST                                     
         STC   R1,NUKDEST                                                       
         MVC   NUKDNET,NBSELNET                                                 
         MVI   NUKDDAY,0           TO GET 1ST REC IN NEXT EST                   
         B     GET1STDT                                                         
         SPACE 1                                                                
GTNXTDT  BAS   RE,SEQ              GET NEXT UNIT                                
         B     CKDTKEY                                                          
         SPACE 1                   (USES DAY-TIME PASSIVE KEY)                  
UDTCHECK NTR1                      CHECKS IF UNIT IS TO BE SELECTED             
         LA    R4,KEY              RESTORE IN CASE IT CHANGED                   
         CLI   NBDONTFD,C'Y'                                                    
         BE    UDT13                                                            
         CLC   NUKDDATE,NBCMPSTR   DATE FILTER                                  
         BL    NO                                                               
         CLC   NUKDDATE,NBCMPEND                                                
         BH    NO                                                               
         SPACE                                                                  
UDT13    ZIC   R1,NUKDEST          ESTIMATE FILTER                              
         BAS   RE,STWRDEST         STEWARDSHIPESTIMATE?                         
         BNE   NO                                                               
         LA    R2,NBESTMSK                                                      
         BAS   RE,TESTMASK                                                      
         BNE   NO                                                               
         CLI   NBSELSUB,0          SUB-CODE FILTER                              
         BE    UDT14                                                            
         CLC   NBSELSUB,NUKDSUB                                                 
         BNE   NO                                                               
UDT14    DS    0H                                                               
         OC    NBSELPRG,NBSELPRG    PROG FILTER                                 
         BZ    UDT16                                                            
         CLC   NBSELPRG,NUKDPROG                                                
         BNE   NO                                                               
UDT16    DS    0H                                                               
         CLI   NBSELNGR,0          STA GROUP FILTER?                            
         BE    UDT18                                                            
         OC    NBNETGRP,NBNETGRP   STATION GROUP TABLE?                         
         BZ    UDT18                                                            
         MVC   FULL,NUKDNET                                                     
         MVI   BYTE,0                                                           
         BAS   RE,STGRPCHK         STATION GROUP CHECK                          
         BNE   NO                                                               
UDT18    DS    0H                                                               
         B     UDTGTREC                                                         
         SPACE 1                                                                
UDTGTREC LA    R5,NUKDNET                                                       
         BAS   RE,MEDFILT                                                       
         BNE   NO                                                               
         BAS   RE,GETREC                                                        
         BNE   NO                                                               
         CLI   NBSELDP,0           DAYPART FILTER                               
         BE    UDT50                                                            
         L     R6,NBAIO                                                         
         CLC   NBSELDP,NUKDP-NUKEY(R6)                                          
         BNE   NO                                                               
UDT50    CLI   NBSELTRF,C'B'       TRAFFIC FILTER. B= NO FILTER                 
         BE    UDT60                                                            
         CLI   NBSELTRF,C' '       BLANK= ONLY IF SUBCODE < X'C1'               
         BE    UDT51                                                            
         CLI   NBSELTRF,C'T'       T= ONLY IF SUBCODE GE X'C1'                  
         BE    UDT52                                                            
UDT51    CLI   NUKDSUB,X'C1'        ONLY NON-TRAFFIC UNITS                      
         BNL   NO                                                               
         B     UDT60                                                            
UDT52    CLI   NUKDSUB,X'C1'        ONLY TRAFFIC UNITS                          
         BNL   UDT60                                                            
         B     NO                                                               
UDT60    DS    0H                                                               
         TM    NBINDS2,NBBILLRD    READ NEGENUBILLS?                            
         BNO   UNITFLTB                                                         
         MVI   NBWHERE,C'I'                                                     
         GOTO1 =V(NETBLRDR),DMCB,NEBLOCKD,RR=RELO                               
UNITFLTB DS    0H                                                               
         BAS   RE,UNITFILT         RECORD FILTERS                               
         BNE   NO                                                               
         B     YES                 PASSED ALL TESTS                             
         EJECT                                                                  
*                                                                               
* INPUT : FULL HAS STATION                                                      
*         NBNETGRP+1  HAS 500 STATIONS X 4                                      
*         NBNETGRP(0) =C'-' MEANS NEGATIVE FILTER                               
*                                                                               
STGRPCHK NTR1  STATION GROUP FILTERING                                          
         L     R1,NBNETGRP                                                      
         CLI   0(R1),C'-'                                                       
         BNE   *+8                                                              
         MVI   BYTE,C'-'                                                        
         LA    R1,1(R1)                                                         
         LA    R2,500                                                           
STGRP10  CLC   FULL,0(R1)                                                       
         BE    STGRP25                                                          
         LA    R1,4(R1)                                                         
         BCT   R2,STGRP10                                                       
* NO MATCH                                                                      
         CLI   BYTE,C'-'                                                        
         BE    STGRPYES                                                         
         B     STGRPNO                                                          
* STATION MATCH                                                                 
STGRP25  CLI   BYTE,C'-'           NEGATIVE FILTERING                           
         BE    STGRPNO                                                          
         B     STGRPYES                                                         
STGRPYES SR    R1,R1                                                            
STGRPNO  LTR   R1,R1                                                            
         XIT1                                                                   
         EJECT                                                                  
*              FILTERS ON UNIT RECORDS                                          
         SPACE 3                                                                
UNITFILT NTR1                                                                   
*                                                                               
         BRAS  RE,DOWBFLT          GET WB FLIGHT REC                            
         BRAS  RE,FLTFLTER         FILTER ON FLIGHTS                            
         BNE   NO                                                               
                                                                                
*                                                                               
         TM    NBINDS7,NBI7DEAL    DEAL ONLY UNITS                              
         BZ    UNIF8                                                            
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'75'                                                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
UNIF8    TM    NBINDS8,NBI8NODL    NO DEAL UNITS ONLY                           
         BZ    UNIF10                                                           
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'75'                                                     
         BAS   RE,GETEL                                                         
         BE    NO                                                               
*                                                                               
UNIF10   TM    NBSPLOPT,X'01'      MXD FLAV -                                   
         BZ    ADUX                NO ADU/PFB/MISSED                            
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING NUMAINEL,R6                                                      
         TM    NUUNITST,X'02'+X'04' MISSED/PFB                                  
         BNZ   NO                                                               
         DROP  R6                                                               
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BNE   ADUX                                                             
         USING NUSDRD,R6                                                        
         TM    NUSDST3,X'02'       ADU UNIT ?                                   
         BO    NO                                                               
         DROP  R6                                                               
ADUX     DS    0H                                                               
*                                                                               
         OC    NBPOD,NBPOD        POD FILTER?                                   
         BZ    PODX                                                             
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'60',NBAIO),=C'Y'               
         CLI   DMCB+12,0                                                        
         BNE   NO                  NO POD - XIT                                 
         L     RE,DMCB+12                                                       
         USING NUOTH,RE                                                         
         LA    RE,NUOTHER                                                       
         LA    R1,NBPOD                                                         
         LA    R2,3                                                             
POD10    CLI   0(R1),C'*'                                                       
         BE    POD15                                                            
         CLC   0(1,R1),0(RE)                                                    
         BNE   NO                                                               
POD15    LA    R1,1(R1)                                                         
         LA    RE,1(RE)                                                         
         BCT   R2,POD10                                                         
         DROP  RE                                                               
PODX     EQU   *                                                                
*                                                                               
         L     R6,NBAIO                                                         
         TM    NBACCFLT,X'20'      STARCOM RESTORED UNITS?                      
         BNO   ENDRESTD                                                         
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BNE   NO                  REJECT                                       
         USING NUSDRD,R6                                                        
         TM    NUSDST4,X'02'    RESTORED UNIT?                                  
         BNO   NO                                                               
ENDRESTD EQU   *                                                                
         DROP  R6                                                               
*                                                                               
         L     R6,NBAIO                                                         
         TM    NBVARIND,X'02'      MULTI RUN UNITS ONLY ?                       
         BNO   DOUPLD                                                           
         MVI   ELCODE,X'60'                                                     
         BAS   RE,GETEL                                                         
         BNE   NO                  NOT MULTI - REJECT                           
         USING NUOTH,R6                                                         
MULTI5   CLI   NUOTTYP,C'L'        IS IT MULTI ?                                
         BE    DOUPLD              YES                                          
         BAS   RE,NEXTEL           TRY NEXT ELEM                                
         BE    MULTI5                                                           
         B     NO                                                               
         DROP  R6                                                               
DOUPLD   L     R6,NBAIO                                                         
         CLI   NBUPLD,0            UPLOAD FILTER                                
         BE    UF00                                                             
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BNE   UF00                                                             
         USING NUSDRD,R6                                                        
         TM    NBUPLD,X'01'        UPLOADED DATA ONLY                           
         BNO   UF01                                                             
         TM    NUSDST3,X'04'       IS IT UPLOADED UNIT                          
         BO    UF00                YES/OK                                       
         B     NO                                                               
UF01     TM    NBUPLD,X'02'        SKIP UPLOADED DATA                           
         BNO   UF00                                                             
         TM    NUSDST3,X'04'       IS IT UPLOADED UNIT                          
         BO    NO                  YES/SKIP IT                                  
*                                                                               
         DROP  R4                                                               
         USING NURECD,R6                                                        
UF00     L     R6,NBAIO                                                         
         LA    R5,NUKNET           A(NETWORK)                                   
*****    BAS   RE,MEDFILT          FOR MEDIA FILTERING                          
*****    BNE   NO                                                               
         BAS   RE,SETSURV          SET SURVEY TO N/C/X'00'                      
         USING NUMAINEL,R6                                                      
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         TM    NUPACKST,X'01'      NO-SHOW PACKAGE                              
         BO    NO                                                               
         CLI   NBSELPAK,0                                                       
         BNE   UF3                                                              
         CLI   NBSELPST,C'L'       FILTER LOCKED PACKAGES ONLY                  
         BNE   UF2                                                              
         TM    NUPACKST,X'20'                                                   
         BNO   NO                                                               
         B     UF2P5                                                            
         SPACE 1                                                                
UF2      CLI   NBSELPST,C'B'       FILTER UNLOCKED PACKAGES                     
         BE    UF2P5                                                            
         TM    NUPACKST,X'20'                                                   
         BO    NO                                                               
         B     UF2P5                                                            
         SPACE 1                                                                
UF2P5    CLI   NBSELPRN,C'Y'       CHECK FOR NO PRINT                           
         BNE   UF4                                                              
         TM    NUPACKST,X'10'      IF BIT SET, DON'T PRINT                      
         BO    NO                                                               
         B     UF4                                                              
         SPACE 1                                                                
UF3      CLC   NBSELPAK,NUPACK     FILTER SELECTED PACKAGE                      
         BNE   NO                                                               
         CLI   NBSELPST,C'B'       TEST FOR BOTH LCKED/UNLKD                    
         BE    UF3P5               YES-TAKE UNIT                                
         CLI   NBSELPST,C'L'       TEST FOR LOCKED ONLY                         
         BNE   UF3A                NO                                           
         TM    NUPACKST,X'20'      YES-NOW TEST FOR LOCKED PACKAGE              
         BNO   NO                                                               
         B     UF3P5                                                            
         SPACE 1                                                                
UF3A     TM    NUPACKST,X'20'      TEST FOR LOCKED PACKAGE                      
         BO    NO                  DEFAULT IS TO EXCLUDE LOCKED                 
         SPACE 1                                                                
UF3P5    CLI   NBSELPRN,C'Y'       CHECK FOR NO PRINT                           
         BNE   UF4                                                              
         TM    NUPACKST,X'10'      IF BIT SET, DON'T PRINT                      
         BO    NO                                                               
         B     UF4                                                              
         SPACE 1                                                                
UF4      DS    0H                                                               
         CLI   NBPIGOPT,0                                                       
         BE    *+12                                                             
         BRAS  RE,PIGGYOPT         PIGGY BACK OPTION                            
         BE    NO                                                               
******************************************************************              
* FIRST USE X'19' ELEM - THIS TAKES PRECEDENCE                                  
*                                                                               
         LA    R1,NUMAINEL         ...IF MULTIPLE PROD ELEM                     
UF4B     ZIC   R0,1(R1)                                                         
         LTR   R0,R0                                                            
         BZ    UF4C                                                             
         AR    R1,R0                                                            
         CLI   0(R1),X'19'         ..THIS HAS PRECEDENCE                        
         BL    UF4B                                                             
         BH    UF4C                                                             
         CLI   1(R1),17              .. IF MORE THAN 2 PRODS                    
         BH    UF6                   .. SKIP PIGGY FILTER      )                
         USING NUPDEEL,R1                                                       
         XC    WORK(9),WORK                                                     
         MVC   WORK+3(3),NUPDEPR                                                
         CLI   1(R1),10              MORE THAN 1 PROD                           
         BE    *+10                                                             
         MVC   WORK+6(3),NUPDEPR+7    GET SECOND PROD                           
         CLI   NBSELPGR,0            PRODGROUPS?                                
         BE    UF4BB                                                            
         MVC   WORK(3),WORK+3      1ST PROD                                     
         BAS   RE,TSTOPMSK          USES WORK TO TEST                           
         BE    UF4BC                                                            
*****    CLI   NBPR2CL3,0                                                       
*****    BE    NO                                                               
         CLI   WORK+6,0                                                         
         BE    NO                                                               
         MVC   WORK(3),WORK+6      2ND PRD                                      
         BAS   RE,TSTOPMSK                                                      
         BE    UF4BC                                                            
         B     NO                                                               
*                                                                               
UF4BB    CLI   NBPRDALL,X'FF'       ACCEPT ALL PRODS                            
         BE    UF4BC                                                            
         CLC   NBSELPRD,WORK+3      NO/CHECK                                    
         BE    UF4BC                                                            
         CLC   NBSELPRD,WORK+6                                                  
         BE    UF4BC                                                            
         B     NO                                                               
UF4BC    CLI   NBSELPG3,0          PIGGYBACK FILTERING                          
         BE    UF6                                                              
         CLC   NBSELPG3,WORK+3                                                  
         BE    UF6                                                              
         B     NO                                                               
         DROP  R1                                                               
*                                                                               
* NO X'19' - SO WE HAVE NO OVERFLOW PRODS                                       
*                                                                               
UF4C     DS   0H                                                                
         LA    R1,NUMAINEL         ...IF MULTIPLE PROD ELEM                     
UF4D     ZIC   R0,1(R1)                                                         
         LTR   R0,R0                                                            
         BZ    UF4E                                                             
         AR    R1,R0                                                            
         CLI   0(R1),X'14'                                                      
         BE    UF5                 ...SKIP PROD CHECK/GET LATER                 
         BL    UF4D                                                             
*                                                                               
UF4E     CLI   NBSELPGR,0          PRODUCT GROUPS?                              
         BE    UF4F                                                             
         XC    WORK,WORK                                                        
         MVC   WORK(1),NUPRD                                                    
         CLI   WORK,0              IF UNALLOCATED                               
         BNE   *+8                                                              
         MVI   WORK,X'FF'                                                       
         BAS   RE,TSTOPMSK        IF PRODGROUPS TEST AGAINTS PROD TABLE         
         BE    UF5                                                              
         CLI   NUPRD2,0                                                         
         BE    NO                                                               
         MVC   WORK(1),NUPRD2                                                   
         BAS   RE,TSTOPMSK                                                      
         BE    UF5                                                              
         B     NO                                                               
*                                                                               
UF4F     DS    0H                  ALLOCATED PRODUCT                            
         CLI   NBPRDALL,X'FF'       IF = ALL/POL                                
         BE    UF5                 ACCEPT ALL PRODS                             
**       ZIC   R1,NUPRD            ALLOCATED PRODUCT                            
**       LTR   R1,R1                                                            
***      BNZ   *+8                                                              
***      LA    R1,255                                                           
***      LA    R2,NBPRDMSK                                                      
***      BAS   RE,TESTMASK         MAY BE FILTERING                             
**       BE    UF5                                                              
**       CLI   NUPRD2,0            TRY SECOND PRODUCT                           
**       BE    NO                                                               
***      ZIC   R1,NUPRD2                                                        
***      BAS   RE,TESTMASK                                                      
**       BNE   NO                                                               
         CLC   NBEFFPNM,NUPRD                                                   
         BE    UF5                                                              
         CLC   NBEFFPNM,NUPRD2                                                  
         BNE   NO                                                               
                                                                                
         SPACE 1                                                                
UF5      CLI   NBSELPIG,0          WAS PIGGYBACK FILTERING REQUESTED            
         BE    UF6                                                              
         CLC   NBSELPIG,NUPRD      THEN IT MUST MATCH BOTH                      
         BNE   NO                                                               
         SPACE 1                                                                
UF6      LA    R2,NUPRFILT         PROGRAM TYPE FILTERS                         
         LA    R3,NBSELPFL                                                      
         LA    R0,3                                                             
         SPACE 1                                                                
UF8      CLI   0(R3),0             NOT SPECIFIED                                
         BE    UF12                                                             
         CLI   0(R3),C'*'          WILD CARD                                    
         BE    UF12                                                             
         TM    0(R3),X'40'                                                      
         BNO   UF10                                                             
         CLC   0(1,R2),0(R3)       FILTER                                       
         BE    UF12                                                             
         B     NO                                                               
         SPACE 1                                                                
UF10     CLC   0(1,R2),0(R3)       NEGATIVE FILTER                              
         BE    NO                                                               
         SPACE 1                                                                
UF12     LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,UF8                                                           
         SPACE 1                                                                
UF14     DS    0H                                                               
         CLI   NBUSER+13,C'Y'      FILTER PRE-EMPTS                             
         BNE   UF16                  IF PROFILE SET                             
         TM    NUUNITST,X'40'                                                   
         BO    NO                                                               
         SPACE 1                                                                
UF16     CLI   NBSELUOP,0          UNIT SELECTION OPTION                        
         BE    UF20                                                             
         CLI   NBSELUOP,C'E'       ONLY RETURN ESTIMATES                        
         BNE   UF18                NO GOOD IF A PFB OR MAKEGOOD                 
         TM    NUUNITST,X'05'                                                   
         BNZ   NO                                                               
*                                                                               
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BNE   UF18                                                             
         USING NUSDRD,R6                                                        
         TM    NUSDST3,X'02'       NO GOOD IF A ADU                             
         BZ    UF18                YES/OK                                       
         B     NO                                                               
         SPACE 1                                                                
UF18     L     R6,NBAIO                                                         
         USING NURECD,R6                                                        
         CLI   NBSELUOP,C'A'       ONLY RETURN ACTUALS                          
         BNE   UF20                NO GOOD IF A MISSED SPOT                     
         TM    NUUNITST,X'42'           OR PREEMPT                              
         BZ    UF20                                                             
         B     NO                                                               
         SPACE 1                                                                
UF20     CLC   NBSELPRD,=C'POL'    IF POL WAS REQUESTED                         
         BNE   UF22                                                             
*****    BNE   UF21                                                             
         TM    NBSPLOPT,X'40'      AND WE ARE NOT SPLITTING,                    
         BO    UF22                                                             
UF21     CLI   NBSELLEN,0          CHECK LENGTH IF REQUESTED                    
         BE    UF22                                                             
         CLC   NBSELLEN,NULEN                                                   
         BNE   NO                                                               
         SPACE 1                                                                
UF22     DS    0H                                                               
* ACCFILT INTERFERES WITH ACC FILTERS IN NEMEDGEN                               
* ACCFILT INTERFERES WITH ACC FILTERS IN NEMEDGEN                               
*****    BAS   RE,ACCFILT          ACCOUNTING FILTERS                           
*****    BNE   NO                                                               
         BAS   RE,BPFILT           BILLED PAID DATE FILTERS                     
         BNE   NO                                                               
         BAS   RE,ACTFILT          ACTIVITY DATE FILTERS                        
         BNE   NO                                                               
         BAS   RE,PACKFILT         CHECK PACKAGE FILTERING                      
         BNE   NO                                                               
         BAS   RE,BLBFILT          BILLBOARD FILTER                             
         BNE   NO                                                               
         SPACE 1                                                                
         TM    NBUPLD,X'04'        ONLY PROGS WITH NUMERIC IN 3/4               
         BNO   UF24                                                             
         L     R6,NBAIO                                                         
         USING NURECD,R6                                                        
         TM    NUKPROG+2,X'F0'                                                  
         BO    UFXIT                                                            
         TM    NUKPROG+3,X'F0'                                                  
         BNO   NO                                                               
         SPACE 1                                                                
UF24     DS    0H                                                               
         TM    NBACCFLT,X'02'      ALLOCATED UNITS ONLY                         
         BNO   UF26                                                             
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING NUMAINEL,R6                                                      
         CLI   NUPRD,0                                                          
         BE    NO                                                               
         SPACE 1                                                                
UF26     DS    0H                  SUBDAYPART FILTERING                         
         CLI   NBSELDP,0           ANY DPT FILTER?                              
         BE    UF28                NO                                           
         TM    NBINDS2,NBALLDPT    ALL DPT FILTERING?                           
         BNO   UF28                NO                                           
*                                                                               
*                                  YES - MATCH ON SUBDAYPART                    
*                                  IF NO SUB/MATCH ON DAYPART                   
*                                                                               
*                                      SUBDAYPART FIRST                         
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'60',NBAIO),=C'U'               
         CLI   DMCB+12,0                                                        
         BNE   UF27                NO SUBDAYPART- TRY DAYPART                   
         L     RE,DMCB+12                                                       
         USING NUOTH,RE                                                         
         MVC   WORK(2),NUOTHER                                                  
         OC    WORK(2),=X'4040'                                                 
         CLC   NBSELNDP,WORK       SUBDAYPART MATCH?                            
         BNE   NO                  NO-REJECT                                    
         B     UF28                YES-GO ON                                    
*                                                                               
         DROP  R6                                                               
UF27     L     R6,NBAIO            NO SUBDAYPART/MATCH ON DAYPART               
         USING NURECD,R6                                                        
         CLC   NBSELDP,NUKDP                                                    
         BNE   NO                  NO-REJECT                                    
         BE    UF28                YES                                          
         DROP  RE,R6                                                            
*                                                                               
UF28     DS    0H                                                               
         SPACE 1                                                                
UFXIT    L     R6,NBAIO                                                         
         USING NURECD,R6                                                        
         LA    R5,NUKNET           A(NETWORK)                                   
         BAS   RE,NTIFILT                                                       
         B     YES                                                              
         SPACE 1                                                                
         DROP  R6                                                               
         EJECT                                                                  
*              FILL MEDIA BUFFER FOR NEW CLIENT                                 
         SPACE 3                                                                
*              INPUT          (R2) BANBUFF A(NETWORK BUFFER)                   
*              OUTPUT              FILLED WITH CALL LETTERS AND MEDIA           
*                                  'MARKET' AND AFFILIATION                     
*              LOCALS              R4=A('STATION')                              
*                                  R0=COUNT                                     
         SPACE 1                                                                
MEDINIT  NTR1                                                                   
         L     R2,NBANBUFF                                                      
         LTR   R2,R2                                                            
         BZ    XIT                                                              
         CLI   NBCLICOD,0          DO WE HAVE A CLIENT YET                      
         BE    XIT                                                              
         OC    0(3,R2),0(R2)       DO IT ONLY ONCE!                             
         BNZ   XIT                                                              
***      CLC   NBCLICOD,0(R2)      HAVE WE FILLED BUFFER FOR THIS CLI           
***      BE    XIT                                                              
         MVC   0(3,R2),NBCLICOD    USE FIRST ENTRY FOR CLIENT CODE              
         LA    R2,L'NBUFFREC(R2)                                                
         USING NBUFFD,R2                                                        
         LA    R0,799              MAX ENTRIES IN STATION TABLE                 
         TM    NBINDS7,NBI7NTIO    EXPANDED BLOCK?                              
         BZ    *+8                                                              
         LA    R0,1199             YES                                          
         XC    KEY,KEY             SET UP KEY                                   
         LA    R4,KEY                                                           
         USING STAREC,R4                                                        
         MVI   STAKTYPE,C'S'                                                    
         MVI   STAKMED,C'N'                                                     
         MVI   NBFILE,C'X'                                                      
         BAS   RE,HIGH                                                          
         L     R4,NBAIO                                                         
         MVI   MEDSW,0                                                          
         B     FILLMED4                                                         
         SPACE 1                                                                
FILLMED2 BAS   RE,SEQ                                                           
         SPACE 1                                                                
FILLMED4 CLC   KEY(2),KEYSAVE                                                   
         BNE   FILLMED8                                                         
         CLC   STAKAGY,NBSELAGY    MUST MATCH FOR AGENCY                        
         BNE   FILLMED2                                                         
         CLI   STYPE,X'40'         SEE IF STATION TYPE INPUTTED                 
         BNH   FILLMED2            IF NOT BYPASS                                
         CLC   STAKCLT,=C'000'     ,,ONLY USED BY SPOT 5/1/03                   
         BNE   FILLMED2            SKIP EM -                                    
         SPACE 1                                                                
FILLMED6 MVC   NBUFFNET,STAKCALL   GOT ONE SO SAVE CALL LETTERS                 
         MVC   NBUFFMED,STYPE      TYPE (MEDIA)                                 
FILLMED7 LA    R2,L'NBUFFREC(R2)                                                
         BCT   R0,FILLMED2                                                      
         DC    H'0'                STATION TABLE OVERFLOW                       
         SPACE 1                                                                
FILLMED8 MVI   NBFILE,C'S'                                                      
         B     XIT                                                              
         EJECT                                                                  
*              FILL NTI STATION BUFFER                                          
         SPACE 3                                                                
*              INPUT          (R2) NBNTISTA A(NTI STATION BUFFER)               
*              OUTPUT              FILLED WITH CALL LETTERS AND MEDIA           
         SPACE 1                                                                
NTIINIT  NTR1                                                                   
         L     R2,NBCNVNTI                                                      
         LTR   R2,R2                                                            
         BZ    XIT                                                              
         CLC   2(1,R2),NBACTAM     FOR THE CORRECT AGENCY                       
         BNE   FILLNTI2                                                         
         CLC   0(2,R2),=XL2'0D75'  DO WE RECORD YET                             
         BE    XIT                                                              
*                                                                               
FILLNTI2 XC    0(4,R2),0(R2)                                                    
         XC    KEY,KEY                                                          
         MVC   KEY(2),=XL2'0D75'                                                
         MVC   KEY+2(1),NBACTAM                                                 
         MVC   0(3,R2),KEY                                                      
         MVI   NBFILE,C'S'                                                      
         USING SLSRECD,R3                                                       
         BAS   RE,HIGH                                                          
         CLC   KEY(3),KEYSAVE                                                   
         BNE   XIT                                                              
         LA    R2,3(R2)                                                         
         L     R3,NBAIO                                                         
         LA    R4,799              MAX ENTRIES IN STATION TABLE                 
         J     FILLNTI4                                                         
*                                                                               
FILLNTI3 BAS   RE,SEQ                                                           
         CLC   KEY(3),KEYSAVE                                                   
         JNE   XIT                                                              
*                                                                               
FILLNTI4 BAS   RE,GETREC                                                        
         CLI   SLSNTI,X'40'                                                     
         BH    FILLNTI5                                                         
         CLC   SLSLEN,=X'002B'     NEW STYLE W/ COMSCORE INFO?                  
         JNE   FILLNTI3                                                         
         CLI   SLSCSNN,X'40'                                                    
         BNH   FILLNTI3                                                         
*                                                                               
FILLNTI5 MVC   0(5,R2),SLSKSTA                                                  
         MVC   5(5,R2),SLSNTI                                                   
         CLC   SLSLEN,=X'002B'     NEW STYLE W/ COMSCORE INFO?                  
         JNE   *+10                                                             
         MVC   10(10,R2),SLSCSNN   COMSCORE NETWORK #                           
         MVI   20(R2),0                                                         
         LA    R2,20(R2)                                                        
*        MVI   10(R2),0                                                         
*        LA    R2,10(R2)                                                        
*                                                                               
         BCT   R4,FILLNTI3                                                      
         J     XIT                                                              
*                                                                               
* THIS WAS A BUG (SPEC-46163) - IT WAS ALWAYS DOING THE BCT LOOP                
* WHEN IT SHOULD ONLY BE DOING IT IF IT ENTERS SOMETHING INTO THE               
* TABLE.  FILLNTI6 WILL BE REMOVED AND FILLNTI3 IS ADDED. - SCHT                
*&&DO                                                                           
FILLNTI6 BAS   RE,SEQ                                                           
         CLC   KEY(3),KEYSAVE                                                   
         BNE   XIT                                                              
         BCT   R4,FILLNTI4                                                      
         B     XIT                                                              
*&&                                                                             
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE TO CHECK FOR MEDIA FILTERING                             
         SPACE 3                                                                
*              INPUTS              NBANBUFF OPTIONAL NETWORK BUFFER             
*                                  ROUTINES IGNORED IF BUFFER MISSING           
*                                  R5=A(NETWORK)                                
*                                  NBSELMFL (OPTIONAL MEDIA FILTER)             
*              OUTPUTS             NBEFFMED                                     
*              CONDITION CODE      NO=MEDIA FILTER FAILED                       
         SPACE 1                                                                
MEDFILT  NTR1                                                                   
         L     R2,NBANBUFF                                                      
         MVI   NBEFFMED,0                                                       
         LTR   R2,R2                                                            
         BZ    YES                                                              
         USING NBUFFREC,R2                                                      
         LA    R2,L'NBUFFREC(R2)   FIRST ENTRY HAS PRESENT CLIENT               
         LA    R0,799              MAX ENTRIES IN STATION TABLE                 
         TM    NBINDS7,NBI7NTIO    EXPANDED BLOCK?                              
         BZ    *+8                                                              
         LA    R0,1199             YES                                          
         SPACE 1                                                                
MEDF2    OC    NBUFFREC,NBUFFREC   LOOK UP IN NETWORK BUFFER                    
         BZ    MEDF6                                                            
         CLC   NBUFFNET,0(R5)      FOR A MATCH ON NETWORK                       
         BE    MEDF4                                                            
         LA    R2,L'NBUFFREC(R2)                                                
         BCT   R0,MEDF2                                                         
         B     MEDF6                                                            
         SPACE 1                                                                
MEDF4    MVC   NBEFFMED,NBUFFMED   GOT A HIT SO PICK UP MEDIA                   
         SPACE 1                                                                
MEDF6    CLI   NBSELMFL,0          ANY FILTERING ACTIVE                         
         BE    YES                 NO - SO THIS IS OK                           
         TM    NBSELMFL,X'40'                                                   
         BNO   MEDF8                                                            
         CLC   NBSELMFL,NBEFFMED                                                
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
MEDF8    MVC   BYTE,NBSELMFL       NEGATIVE FILTER                              
         OI    BYTE,X'40'                                                       
         CLC   BYTE,NBEFFMED                                                    
         BE    NO                                                               
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK FOR NTI CONVERSION                              
         SPACE 3                                                                
*              INPUTS              NBNTISTA OPTIONAL NETWORK BUFFER             
*                                  ROUTINES IGNORED IF BUFFER MISSING           
*                                  R5=A(NETWORK)                                
*              OUTPUTS             NBNTISTA                                     
*                                  NBSTATYP                                     
         SPACE 1                                                                
NTIFILT  NTR1                                                                   
         L     R2,NBCNVNTI                                                      
         LTR   R2,R2                                                            
         BZ    XIT                                                              
         XC    NBNTISTA,NBNTISTA                                                
         CLC   0(2,R2),=XL2'0D75'                                               
         BNE   XIT                                                              
         LA    R2,3(R2)            POINT TO TABLE                               
*                                                                               
NTIF2    CLI   0(R2),0                                                          
         BE    XIT                                                              
         CLC   0(4,R5),0(R2)       CHECK FOR MATCH ON NETWORK                   
         BE    NTIF4                                                            
         LA    R2,20(R2)                                                        
         B     NTIF2                                                            
*                                                                               
NTIF4    MVC   NBNTISTA,5(R2)      MOVE NTI STATION TO BLOCK                    
         MVC   NBSTATYP,9(R2)      MOVE STATION TYPE TO BLOCK                   
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SET SURVEY FROM NETWORK                               
         SPACE 3                                                                
*              INPUT               R5=A(NETWORK)                                
*                                  NBSTATYP SET TO O FOR OTHER,                 
*                                  N FOR NET, C FOR CABLE, S FOR SYND           
*              OUTPUT              NBSURVEY SET TO ....                         
*                                  N FOR NETWORK, C FOR CABLE OR X'00'          
         SPACE 1                                                                
SETSURV  NTR1                                                                   
         MVI   ELCODE,X'02'        SET UP FOR ACTIVITY ELEMENT                  
         L     R6,NBAIO                                                         
         USING NUSDRD,R6                                                        
         BAS   RE,GETEL                                                         
         BNE   SETSURV1                                                         
         MVC   NBSURVEY,NUPOSTYP        PRECISION TYPE                          
         CLI   NUBKTYP,X'40'                                                    
         BNH   *+10                                                             
         MVC   NBSURVEY,NUBKTYP          BOOK TYPE                              
         B     XIT                                                              
*                                                                               
SETSURV1 MVI   NBSURVEY,0                                                       
         LA    R1,SURVLIST                                                      
         SPACE 1                                                                
SETSURV2 CLI   0(R1),X'FF'                                                      
         BE    XIT                                                              
         CLC   0(4,R1),0(R5)       MATCH ON NETWORK                             
         BE    SETSURV4                                                         
         LA    R1,5(R1)                                                         
         B     SETSURV2                                                         
         SPACE 1                                                                
SETSURV4 MVC   NBSURVEY,4(R1)      PASS BACK SURVEY VALUE                       
         B     XIT                                                              
         DROP  R6                                                               
         SPACE 1                                                                
SURVLIST DC    C'ABC ',C'N'                                                     
         DC    C'CBS ',C'N'                                                     
         DC    C'NBC ',C'N'                                                     
         DC    C'FOX ',C'N'                                                     
         DC    C'TCF ',C'N'                                                     
         DC    C'FBC ',C'N'                                                     
         DC    C'CNN ',C'C'                                                     
         DC    C'WTBS',C'C'                                                     
         DC    C'TBS ',C'C'                                                     
         DC    C'USA ',C'C'                                                     
         DC    C'CBN ',C'C'                                                     
         DC    C'ESPN',C'C'                                                     
         DC    C'MTV ',C'C'                                                     
         DC    C'NICK',C'C'                                                     
         DC    C'NIK ',C'C'                                                     
         DC    C'AEN ',C'C'                                                     
         DC    C'LFTM',C'C'                                                     
         DC    C'FF'                                                            
         EJECT                                                                  
*              ACCOUNTING FILTERS                                               
         SPACE 3                                                                
ACCFILT  NTR1                                                                   
         CLI   NBSELFLT,0          ACCOUNTING FILTERS                           
         BE    YES                                                              
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'01'        ACCFILTERS EXPECT X'01'                      
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING NUMAINEL,R6                                                      
         CLI   NBSELFLT,1          1=ONLY RETURN PAID                           
         BE    UFPAID                                                           
         CLI   NBSELFLT,2          2=ONLY RETUN UNPAID                          
         BE    UFUNPAID                                                         
         CLI   NBSELFLT,3          3=ONLY RETURN BILLED                         
         BE    UFBILL                                                           
         CLI   NBSELFLT,4          4=ONLY RETURN UNBILLED                       
         BE    UFUNBILL                                                         
         CLI   NBSELFLT,5          5=ONLY RETURN NO ACTUAL COSTS                
         BE    UFNOACT                                                          
         CLI   NBSELFLT,6          6=ONLY RETURN UNALLOCATED                    
         BE    UFUNALL                                                          
         DC    H'0'                BAD SELFLT FIELD                             
         SPACE 1                                                                
UFPAID   MVI   ELCODE,X'12'        PAY ELEMENT                                  
         BAS   RE,NEXTEL           ONLY ACCEPT UNIT IF HAS A PAY ELEM           
         BE    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFUNPAID TM    NUUNITST,X'42'      IF A PRE-EMPT OR MISSED                      
         BZ    UFUNP2                IF PAY ELEMENTS EXIST, ACCEPT IT           
         MVI   ELCODE,X'12'                                                     
         BAS   RE,NEXTEL                                                        
         BE    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFUNP2   MVI   ELCODE,X'12'        ELSE, REJECT IF NO PAY ELEMENTS              
         BAS   RE,NEXTEL                                                        
         BNE   UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFBILL   MVI   ELCODE,X'10'        BILL ELEMENT                                 
         BAS   RE,NEXTEL           ONLY ACCEPT IF UNIT HAS A BILL ELEM          
         BE    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFUNBILL TM    NUUNITST,X'42'      IF A PRE-EMPT OR MISSED                      
         BZ    UFBPOK                IF BIL ELEMENTS EXIST, ACCEPT IT           
         MVI   ELCODE,X'10'                                                     
         BAS   RE,NEXTEL                                                        
         BE    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFNOACT  TM    NUUNITST,X'20'      CHECK IF ACTUAL COST                         
         BZ    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFUNALL  CLI   NUPRD,0             UNALLOCATED                                  
         BE    UFBPOK                                                           
         B     NO                                                               
         SPACE 1                                                                
UFBPOK   MVI   ELCODE,0            RESET ELCODE                                 
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK FOR ACTIVITY DATE FILTERS                       
         SPACE 3                                                                
ACTFILT  NTR1                                                                   
         OC    NBACTSTR(4),NBACTSTR ANY ACTIVITY REQUESTS?                      
         BZ    YES                                                              
         MVI   ELCODE,X'99'        SET UP FOR ACTIVITY ELEMENT                  
         L     R6,NBAIO                                                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING NUACTD,R6                                                        
         LA    R3,NUACTCDT                                                      
         CLI   0(R3),0                                                          
         BNE   *+8                                                              
         LA    R3,NUACTADT                                                      
         GOTO1 NBDATCON,DMCB,(3,(R3)),(2,WORK)                                  
         CLC   WORK(2),NBACTSTR    CHECK ACTIVITY FALLS BETWEEN START           
         BL    NO                                                               
         CLC   WORK(2),NBACTEND    AND END DATE                                 
         BH    NO                                                               
         B     YES                 OK SO WE GOT A HIT                           
         EJECT                                                                  
*              ROUTINE TO CHECK FOR BILLED/PAID DATE FILTERS                    
         SPACE 3                                                                
BPFILT   NTR1                                                                   
* IF NBBILSTR SET, THERE ARE TWO OPTIONS                                        
* ONE OPTION EXCLUDES UNITS THAT DO NOT MATCH FILTER (BHRDATE)                  
* OTHER OPTION (BHRDATEI) EXCLUDES ONLY BILL ELEMS THAT DON'T MATCH             
* THE FIRST OPTION IS HANDLED IN THIS ROUTINE                                   
         OC    NBBILSTR(4),NBBILSTR ANY BILLED REQUESTS?                        
         BZ    BPFILT2                                                          
         TM    NBVARIND,X'01'      BHRDATEI FILTER?                             
         BO    BPFILT2             YES/SKIP TEST HERE                           
         MVI   ELCODE,X'10'        SET UP FOR BILLING ELEMENTS                  
         MVC   WORK(4),NBBILSTR    PASS DATES                                   
         BAS   RE,BPFILT4                                                       
         BNE   NO                                                               
         SPACE 1                                                                
BPFILT2  OC    NBPAYSTR(4),NBPAYSTR ANY PAID REQUESTS?                          
         BZ    YES                                                              
         MVI   ELCODE,X'12'        SET UP FOR PAYING ELEMENTS                   
         MVC   WORK(4),NBPAYSTR    PASS DATES                                   
         BAS   RE,BPFILT4                                                       
         BNE   NO                                                               
         B     YES                                                              
         SPACE 1                                                                
BPFILT4  NTR1                                                                   
         L     R6,NBAIO                                                         
         OC    WORK+2(2),WORK+2    SET HIGH END DATE IF NOT REQUESTED           
         BNZ   *+10                                                             
         MVC   WORK+2(2),=X'FFFF'                                               
         BAS   RE,GETEL                                                         
         B     BPFILT8                                                          
         SPACE 1                                                                
BPFILT6  BAS   RE,NEXTEL                                                        
         SPACE 1                                                                
BPFILT8  BNE   NO                                                               
*                                  USE BILLING ELEMENT FOR PAYING ALSO          
         USING NUBILD,R6                                                        
         TM    NBACCFLT,X'01'      CHECK INTEG ONLY                             
         BNO   BPFILT9                                                          
         CLI   NUBILTYP,C'I'                                                    
         BNE   BPFILT6                                                          
BPFILT9  DS    0H                                                               
         CLC   NUBILDAT,WORK       CHECK ACTIVITY FALLS BETWEEN START           
         BL    BPFILT6                                                          
         CLC   NUBILDAT,WORK+2     AND END DATE                                 
         BH    BPFILT6                                                          
         B     YES                 OK SO WE GOT A HIT                           
         EJECT                                                                  
*              ROUTINE TO CHECK FOR PACKAGE FILTERING                           
         SPACE 3                                                                
PACKFILT NTR1                                                                   
         XC    DUMEL,DUMEL                                                      
         MVC   NBPKFILT,=C'******' PRESET PACKAGE FILTERS                       
         MVI   ELCODE,X'08'                                                     
         L     R6,NBAIO                                                         
         USING NUFILD,R6                                                        
         BAS   RE,GETEL                                                         
         BE    *+8                                                              
         LA    R6,DUMEL                                                         
         B     PKF4                                                             
         SPACE 1                                                                
PKF2     BAS   RE,NEXTEL                                                        
         SPACE 1                                                                
PKF4     BE    PKF4B               NEED TO FIND A FILTER ELEMENT                
         LA    R6,DUMEL            NEED TO FIND A FILTER ELEMENT                
         B     PKF5                                                             
PKF4B    CLI   NUFILSCM,C'K'       WITH SCHEME CODE K                           
         BNE   PKF2                                                             
         MVC   NBPKFILT,NUFILTER                                                
         SPACE 1                                                                
PKF5     OC    NBSELPKF,NBSELPKF   WERE ANY FILTERS SPECIFIED?                  
         BZ    YES                 NO - EXIT                                    
         LA    RE,NBSELPKF                                                      
         LA    RF,NUFILTER                                                      
         LA    R0,6                                                             
         SPACE 1                                                                
PKF6     CLI   0(RE),0                                                          
         BE    YES                                                              
         CLI   0(RE),C' '                                                       
         BE    YES                                                              
         CLI   0(RE),C'*'          WILD CARD                                    
         BE    PKFEND                                                           
         CLI   0(RE),C'-'          DASH IS ALSO WILD                            
         BE    PKFEND                                                           
         TM    0(RE),X'40'                                                      
         BNO   PKF8                                                             
         CLC   0(1,RE),0(RF)       TEST CHARACTER                               
         BE    PKFEND                                                           
         B     NO                                                               
         SPACE 1                                                                
PKF8     MVC   BYTE,0(RE)          NEGATIVE TEST                                
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(RF)                                                       
         BE    NO                                                               
         SPACE 1                                                                
PKFEND   LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,PKF6             CHECK ALL 6 CHARACTERS                       
         B     YES                 AND THEN WE'RE DONE                          
         EJECT                                                                  
*              REQUEST END                                                      
         SPACE 3                                                                
QE2      CLI   NBSEQ,C'X'           FOR X SEQUENCE                              
         BNE   QE3                                                              
         OC    NBADTL,NBADTL                                                    
         BZ    QE3                                                              
         L     R2,NBADTL            FIND CURRENT IN DATELIST                    
         SPACE 1                                                                
QDL1     CLC   0(4,R2),NBCMPSTR                                                 
         BE    QDL2                                                             
         LA    R2,4(R2)                                                         
         B     QDL1                                                             
         SPACE 1                                                                
QDL2     LA    R2,4(R2)            GET DATE SET AFTER THIS ONE                  
         CLI   0(R2),0                                                          
         BE    QE3                                                              
         MVC   NBCMPSTR(4),0(R2)                                                
         B     DOU1                                                             
         SPACE 1                                                                
QE3      MVI   NBFILE,C'S'                                                      
         CLC   NBSELCLI,=C'ALL'    DO WE NEED TO HANDLE ANOTHER CLIENT          
         BE    QVALCLI             YES GO BACK FOR NEXT. REVALIDATE             
         SPACE 1                                                                
QE4      OC    NBSELAGY,NBSELAGY   DO WE NEED TO HANDLE ANOTHER AGY             
         BNZ   QE6                                                              
         XC    NBEFFCLI,NBEFFCLI   IF SO, START EFFCLI AT 0                     
         B     QVALAGY                                                          
QE6      MVI   NBMODE,NBREQLST                                                  
         MVI   BRNCHNUM,BMENDIT                                                 
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRENDIT  MVI   NBQINIT,0                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES  TO CONTROL GOING TO MODULE                             
*              GOERR - NBRETURN IS STILL SET TO LAST ENTRY                      
         SPACE 3                                                                
GO       MVC   NBRETURN,BRNCHNUM   SAVE RETURN ADDRESS                          
         CLI   NBMODE,NBPROCUN     IF WE HAVE A UNIT AND...                     
         BNE   GOERR                                                            
         TM    NBSPLOPT,X'80'      TEST OPTION ON                               
         BO    SPLITST2            DEAL WITH GETVALS AND FILTERING              
*                                  UP AT THE BEGINNING                          
         SPACE 1                                                                
GOERR    STM   R0,R7,NBGOR07       SAVE GENERAL REGS                            
         SPACE 1                                                                
GO1      BAS   RE,GETVALS                                                       
         CLI   NBTRCOPT,C'Y'       OPTION TO TRACE                              
         BNE   XITGO                                                            
         LA    R1,MODETAB                                                       
         SPACE 1                                                                
GO2      CLC   0(1,R1),NBMODE                                                   
         BE    GO4                                                              
         LA    R1,16(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   GO2                                                              
         SPACE 1                                                                
GO4      MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         MVC   P(18),=C'GO TO USER MODE = '                                     
         MVC   P+18(15),1(R1)                                                   
         GOTO1 NBPRINT,DMCB,PFILL,=C'BL02'                                      
         SPACE 1                                                                
XITGO    CLI   NBSELMOD,0          CK TRAP MODE                                 
         BE    XIT                                                              
         CLI   NBERROR,0           EXIT NO MATTER WHAT IF ERROR                 
         BNE   XIT                                                              
         CLI   NBMODE,NBREQLST     EXIT IF LAST ONE                             
         BE    XIT                                                              
         CLC   NBMODE,NBSELMOD                                                  
         BNE   NTIOSTRT            LOOP TIL NBMODE=NBSELMOD                     
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CONTROL DATA EXTRACTION (KEYS)                        
         SPACE 3                                                                
GETVALS  NTR1                                                                   
         CLI   NBMODE,NBPROCUN     ONLY CALL NETVALUE                           
         BNE   GETV10                                                           
                                                                                
***      TM    NBINDS2,NBBILLRD    READ NEGENUBILLS?                            
***      BNO   GVNETVAL                                                         
***      MVI   NBWHERE,C'I'                                                     
***      GOTO1 =V(NETBLRDR),DMCB,NEBLOCKD,RR=RELO                               
         B     GVNETVAL                                                         
GETV10   CLI   NBMODE,NBPROCPK                                                  
         BE    GVNETVAL                                                         
         B     GVXIT                                                            
*                                                                               
GVNETVAL MVI   NBWHERE,C'I'                                                     
         MVC   SVHUNOPT,NBHUNOPT   SAVE OFF NBHUNOPT NETVAL CAN MODIFY          
         GOTO1 NBNETVAL,DMCB,(RA)                                               
         MVC   NBHUNOPT,SVHUNOPT   RESTORE NBHUNOPT                             
         MVI   NBWHERE,0                                                        
         OC    NBHOOK,NBHOOK       IF A HOOK ROUTINE                            
         BZ    GVNET2                                                           
         LA    RE,GVNET2           SET RETURN TO EXIT                           
         SPACE 1                                                                
HOOK     NTR1                      SAVE OWN REGS IN W/S                         
         L     RF,NBHOOK                                                        
         L     RE,NBCALLRD         RESTORE CALLING ROUTINES REGISTERS           
         LR    R0,RE               PASS R0 SO CALLER CAN EXIT ON OWN            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GOTO USER HOOK                               
         SPACE 1                                                                
GVHOOKX  XIT1                                                                   
         SPACE 1                                                                
GVNET2   CLI   NBUPUNIT,C'Y'       SEE IF RECORD HAS BEEN UPDATED               
         BNE   GVXIT                                                            
         CLI   NBNOWRIT,C'N'       SEE IF WRITE INHIBIT IS SET                  
         BE    GVXIT                                                            
         L     R2,NBADMWRK         DID USER LET US SAVE DMWORK                  
         LTR   R2,R2                                                            
         BZ    GVNET4                                                           
         MVC   DMWORK(96),0(R2)    THEN RESTORE VALUES FROM GETREC              
         SPACE 1                                                                
GVNET4   DS    0H                  WRITE IT                                     
         TM    NBINDS2,NBBILLRD    **IF READING NEW BILL RECORDS                
         BNO   GVNET4C                                                          
         TM    NBINDS4,NBI4BLRD   **IF ONLY GETTING $$$                         
         BO    GVNET4C                OK                                        
         DC    H'0'                **ELSE SHOULD NOT WRITE BACK                 
GVNET4C  LA    R3,KEY+21                                                        
         L     R2,NBAIO                                                         
         GOTO1 NBDM,DMCB,=C'PUTREC',FILE,(R3),(R2),DMWORK                       
         SPACE 1                                                                
GVXIT    MVI   NBUPUNIT,0          RESET UP UNIT                                
         B     XIT                                                              
         EJECT                                                                  
*              MASK HANDLING                                                    
         SPACE 3                                                                
SETMASK  NTR1                                                                   
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SR    R0,R0               R1 HAS NUMBER                                
         SLDL  R0,29               GET BYTE NO. INTO R0                         
         SRL   R1,29                   BIT  NO. INTO R1                         
         AR    R2,R0               R2 HAS (MASK)                                
         LA    R1,BITLIST(R1)                                                   
         OC    0(1,R2),0(R1)                                                    
         B     XIT                                                              
         SPACE 1                                                                
TESTMASK NTR1                                                                   
         SR    R0,R0                                                            
         SLDL  R0,29                                                            
         SRL   R1,29                                                            
         AR    R2,R0                                                            
         LA    R1,BITLIST(R1)                                                   
         MVC   BITTEST,0(R2)                                                    
         NC    BITTEST,0(R1)                                                    
         CLI   BITTEST,0                                                        
         BE    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
BITLIST  DC    X'8040201008040201'                                              
*                                                                               
STWRDEST NTR1                                                                   
         TM    NBINDS4,NBI4STWY    ACCEPT STEWARD EST?                          
         BO    STWDOK                                                           
         ICM   R2,15,NBASTWRD                                                   
         BZ    STD30               NO ADDR=SAME AS NOT STEWARD EST              
         STC   R1,BYTE                                                          
         LA    R3,255                                                           
STD20    CLI   0(R2),0                                                          
         BE    STD30               NOT STEWARD EST                              
         CLC   BYTE,0(R2)                                                       
         BE    STD25               STEWARD ESTIMATE                             
         LA    R2,1(R2)                                                         
         BCT   R3,STD20                                                         
         B     STD30                                                            
*                                                                               
* STEWARD EST                                                                   
STD25    TM    NBINDS4,NBI4STWO    ONLY STEWAR EST                              
         BO    STWDOK              YES-ACCEPT                                   
         B     STWDNOK             REJECT                                       
*                                                                               
* NOT STEWARD EST                                                               
STD30    TM    NBINDS4,NBI4STWO            ONLY STEWARD ESTIMATES?              
         BO    STWDNOK                     YES-REJECT                           
         B     STWDOK                      NO-ACCEPT                            
STWDOK   SR    RE,RE                                                            
STWDNOK  LTR   RE,RE                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   FILTER ON BILLBOARD / NBBLBOPT=Y=ONLY BILLB UNITS, =N=REJECT THEM           
BLBFILT  NTR1                                                                   
         CLI   NBBLBOPT,0                                                       
         BE    BLBX                                                             
         L     R6,NBAIO                                                         
         MVI   ELCODE,X'21'                                                     
         USING NUCMLEL,R6                                                       
         BAS   RE,GETEL            .IS THERE A BILLBOARD ELEMENT                
         BNE   BLB7                                                             
         CLI   NUCMLBSL,0          IS BB LENGTH 0                               
         BNE   BLB5                                                             
         TM    NUCMLFLG,X'04'      BUY REQUIRED BB                              
         BNO   BLB7                                                             
BLB5     CLI   NBBLBOPT,C'Y'      .YES/ACCEPT ONLY BILLB UNITS                  
         BE    BLBX                                                             
         B     BLBNO                                                            
BLB7     CLI   NBBLBOPT,C'N'      .NO/DONT ACCEPT BB UNITS                      
         BE    BLBX                                                             
BLBNO    LA    R1,1                                                             
         B     *+6                                                              
BLBX     SR    R1,R1                                                            
         LTR   R1,R1                                                            
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              SPECIAL FUNCTIONS                                                
         SPACE 3                                                                
FUNCGET  DS    0H                  GETS THE RECORD NBKEY HAS KEY                
*                                  AND FILLS THE BLOCK ACCORDINGLY              
*                                  ONCE USED YOU CAN'T RETURN                   
*                                     TO SEQUENTIAL MODE                        
         SPACE 1                                                                
         MVI   NBSELMOD,0          NO SELMOD FOR SPECIAL MODES                  
         MVI   NBFILE,C'U'         UNIT FILE ONLY                               
         BAS   RE,GETREC           GET RECORD IN NBAIO                          
*                                                                               
         CLI   NBFUNCT,NBFGET                                                   
         BNE   FGET5                                                            
         L     R6,NBAIO            SETS NBSURVEY                                
         USING NURECD,R6                                                        
         LA    R5,NUKNET                                                        
         BAS   RE,SETSURV                                                       
         DROP  R6                                                               
*                                                                               
FGET5    MVI   NBMODE,NBPROCUN                                                  
         MVI   BRNCHNUM,BMFGTNXT                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRFGTNXT B     QE4                 IF GET HERE MEANS TRYING TO RESUME           
         SPACE 3                                                                
FUNCVAL  DS    0H                  FILLS NETBLOCK FOR REC IN NBAIO              
*                                  ONCE USED YOU CAN'T RETURN                   
*                                     TO SEQUENTIAL MODE                        
         SPACE 1                                                                
         TM    NBINDS2,NBBILLRD    READ NEGENUBILLS?                            
         BNO   FUNV10                                                           
         MVI   NBWHERE,C'I'                                                     
         GOTO1 =V(NETBLRDR),DMCB,NEBLOCKD,RR=RELO                               
FUNV10   MVI   NBSELMOD,0          NO SELMOD FOR SPECIAL MODES                  
         MVI   NBFILE,C'U'         UNIT FILE ONLY                               
         MVI   NBMODE,NBPROCUN                                                  
         MVI   BRNCHNUM,BMFVLNXT                                                
         BAS   RE,GO                                                            
         SPACE 1                                                                
BRFVLNXT B     QE4                 IF GET HERE MEANS TRYING TO RESUME           
*                                  TO SEQUENTIAL MODE.                          
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   NBKEYLST,NBKEY                                                   
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   NBKEYLST,NBKEY                                                   
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'UNTDIR  '         DIRECTORIES                         
         CLI   NBFILE,C'S'                                                      
         BNE   *+10                                                             
         MVC   FILE(8),=C'SPTDIR  '                                             
         CLI   NBFILE,C'X'                                                      
         BNE   *+10                                                             
         MVC   FILE(8),=C'STATION '                                             
         BAS   RE,DMLOCKCK                                                      
         L     R2,NBAIO                                                         
         ZIC   R4,NBDMGOPT         OPTIONAL DATAMGR BITS                        
         GOTO1 NBDM,DMCB,((R4),COMMAND),FILE,KEY,(R2),0                         
         BAS   RE,DMTRACE                                                       
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         L     R2,NBAIO                                                         
         ZIC   R4,NBDMGOPT         OPTIONAL DATAMGR BITS                        
         MVC   FILE(8),=C'SPTFILE '     FILE                                    
         MVC   NBDTADSP,=H'24'                                                  
         LA    R3,KEY+14                                                        
         CLI   NBFILE,C'S'              NO UPDATE FOR SPOT FILE                 
         BE    GETREC4                                                          
         MVC   FILE(8),=C'UNTFILE '     READ UNIT FILE FOR UPDATE               
         BAS   RE,DMLOCKCK                                                      
         MVC   NBDTADSP,=H'27'                                                  
         LA    R3,KEY+21                                                        
         CLI   NBNOWRIT,C'N'       SEE IF WRITE INHIBIT IS SET                  
         BE    GETREC4             IF SO, DONT READ FOR UPDATE                  
         LA    R4,128(R4)          (TURN ON X'80' BIT)                          
         SPACE 1                                                                
GETREC4  DS    0H                                                               
**       OC    NBA56K,NBA56K        ..FULL TRACK READ AREA?                     
**       BZ    GETREC5                                                          
**       CLI   NBNOWRIT,C'N'         ..FOR NO UPDATE ONLY                       
**       BNE   GETREC5                                                          
**       CLC   FILE(8),=C'UNTFILE '  ..FOR UNIT FILE ONLY                       
**       BNE   GETREC5                                                          
**       LA    R5,NBA56K                                                        
**       GOTO1 NBDM,DMCB,(X'FF',=C'GETREC'),FILE,(R3),(R2),(R5),(R5)            
**       CLI   8(R1),0             OK                                           
**       BE    YES                                                              
**       CLI   8(R1),2             RECORD IS DELETED                            
**       BE    NO                                                               
**       TM    8(R1),X'90'                                                      
**       BM    NO                                                               
**       DC    H'0'                                                             
*                                                                               
GETREC5  GOTO1 NBDM,DMCB,((R4),=C'GETREC'),FILE,(R3),(R2),DMWORK                
         BAS   RE,DMCHECK                                                       
         L     R2,NBADMWRK         DID USER LET US SAVE DMWORK                  
         LTR   R2,R2                                                            
         BZ    YES                                                              
**       BZ    XIT                                                              
         MVC   0(96,R2),DMWORK     THEN SAVE VALUES FOR FUTURE PUTREC           
         B     YES                                                              
**       B     XIT                                                              
*              LOCK TABLE CHECK                                                 
         SPACE 3                                                                
DMLOCKCK NTR1                                                                   
         CLI   NBNOWRIT,C'N'       ARE WE WRITING RECORDS BACK                  
         BE    DMLCKEX                                                          
         CLC   NBUNTCT,=H'250'     HAS IO COUNT EXCEEDED 250                    
         BNH   DMLCK100                                                         
         CLC   FILE(8),=C'UNTDIR  '  IS THIS THE UNIT DIR READ                  
         BNE   DMLCK100                                                         
*                                                                               
         GOTO1 NBDM,DMCB,(0,=C'DMUNLK'),=CL8'UNTFILE' FREE LOCK TAB             
         GOTO1 NBDM,DMCB,(0,=C'DMUNLK'),=CL8'UNTDIR'  FREE LOCK TAB             
         XC    NBUNTCT,NBUNTCT                                                  
*                                                                               
DMLCK100 CLC   FILE(8),=C'UNTDIR  '  IS THIS THE UNIT DIR READ                  
         BE    DMLCK120                                                         
         CLC   FILE(8),=C'UNTFILE '  IS THIS THE UNIT DIR READ                  
         BNE   XIT                                                              
* INCREMENT THE COUNTER                                                         
DMLCK120 SR    RE,RE                                                            
         ICM   RE,3,NBUNTCT                                                     
         LA    RE,1(RE)                                                         
         STCM  RE,3,NBUNTCT                                                     
*                                                                               
DMLCKEX  B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0             OK                                           
         BER   RE                                                               
         CLI   8(R1),2             RECORD IS DELETED OK TOO                     
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
DMTRACE  CLI   NBTRCOPT,C'Y'                                                    
         BNER  RE                                                               
         NTR1                                                                   
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         MVC   P(8),COMMAND                                                     
         MVC   P+8(8),FILE                                                      
         GOTO1 NBHEXOUT,PARAS,DMCB+8,P+16,1,=C'TOG'                             
         MVC   P+20(25),KEY                                                     
         GOTO1 NBHEXOUT,PARAS,KEY,P+50,25,=C'TOG'                               
         GOTO1 NBPRINT,PARAS,PFILL,=C'BL01'                                     
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         MVC   P+20(25),0(R2)                                                   
         GOTO1 NBHEXOUT,PARAS,0(R2),P+50,25,=C'TOG'                             
         GOTO1 NBPRINT,PARAS,PFILL,=C'BL02'                                     
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         B     XIT                                                              
         SPACE 1                                                                
         PRINT GEN                                                              
         GETEL (R6),NBDTADSP,ELCODE                                             
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
*                                                                               
         EJECT                                                                  
*              CONSTANTS & LTORG                                                
         SPACE 3                                                                
VTYPES   DS    0F                                                               
         DC    V(ADDAY)                                                         
         DC    A(0)                                                             
         DC    V(CLPACK)                                                        
         DC    V(CLUNPK)                                                        
         DC    V(CONFID)                                                        
         DC    V(DATAMGR)                                                       
         DC    V(DATCON)                                                        
         DC    V(DEMADDR)                                                       
         DC    V(DEMAINT)                                                       
         DC    V(DEMAND)                                                        
         DC    V(DEMEL)                                                         
         DC    V(DEMOMATH)                                                      
         DC    V(DEMOUT)                                                        
         DC    V(GETDAY)                                                        
         DC    V(HEXOUT)                                                        
         DC    V(HELLO)                                                         
         DC    V(NETVALUE)                                                      
         DC    V(GETPROF)                                                       
XVTYPES  DS    0C                  END OF VTYPES LIST                           
         SPACE 1                                                                
FFS      DC    32X'FF'                                                          
MINDAT   DC    XL2'0000'                                                        
MAXDAT   DC    XL2'FFFF'                                                        
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
MODETAB  DS    0F                                                               
         DC    AL1(31),CL15'VALIDATE AGENCY'                                    
         DC    AL1(41),CL15'VALIDATE CLIENT'                                    
         DC    AL1(51),CL15'VALIDATE PRODCT'                                    
         DC    AL1(61),CL15'VALIDATE ESTIM '                                    
         DC    AL1(71),CL15'VALIDATE PACKGE'                                    
         DC    AL1(81),CL15'VALIDATE DATE  '                                    
         DC    AL1(100),CL15'REQUEST FIRST'                                     
         DC    AL1(110),CL15'NETWORK FIRST'                                     
         DC    AL1(120),CL15'PACKAGE FIRST'                                     
         DC    AL1(130),CL15'PROGRAM FIRST'                                     
         DC    AL1(140),CL15'PROCESS PACKAGE'                                   
         DC    AL1(150),CL15'PROCESS UNIT '                                     
         DC    AL1(160),CL15'PROGRAM LAST '                                     
         DC    AL1(170),CL15'PACKAGE LAST '                                     
         DC    AL1(180),CL15'NETWORK LAST '                                     
         DC    AL1(190),CL15'REQUEST LAST'                                      
         DC    X'FF'                                                            
         EJECT                                                                  
* SET UP BINTABLE                                                               
WBFLTS   NTR1  LABEL=*,BASE=*                                                   
*                                                                               
* SET UP BINTABLE                                                               
         SR    R0,R0               A OF REC TO BE ADDED                         
*                                                                               
         L     R1,NBWBFLT          PASSED WBFLT BUFFER                          
         LTR   R1,R1                                                            
         BZ    WBEND                                                            
         SR    R0,R0               A OF REC TO BE ADDED                         
         LA    R1,BINDCBE(R1)      BUMP TO RECORD AREA                          
*                                  1ST 24 BYTES = BINDMCB                       
*                                  NEXT 4 BYTES ADDR OF MATCHED REC             
*                                  NEXT 10 BYTES FLT ID                         
                                                                                
         SR    R2,R2               NUM OF REC IN TBL,UPDATED BY BINSRCH         
         LA    R3,BINRCLNE         LENGTH OF REC                                
         LA    R4,BINKLNE          DISP OF KEY INTO REC                         
         LA    R5,BNALNE/BINRCLNE   BINAREA=34K (FOR STARTERS)                  
         L     RE,NBWBFLT          PASSED ADDR OF WORK AREA                     
         STM   R0,R5,0(RE)                                                      
*                                                                               
         LA    RE,BINDCBE(RE)      BUMP TO BIN REC AREA                         
         LR    RF,R5               R5 HAS BINTBL LENGTH                         
         XCEF                                                                   
*                                                                               
* LOAD BINTABLE                                                                 
         LA    R3,MYKEY                                                         
         USING WP1KEY,R3                                                        
         XC    MYKEY,MYKEY                                                      
         MVC   MYKEY(2),=X'0E8C'                                                
         MVC   MYKEY+2(1),NBACTAM                                               
         MVC   MYKEY+3(2),NBACTCLI                                              
         MVC   MYKEYSV,MYKEY                                                    
         GOTO1 NBDM,DMCB,(0,=CL8'DMRDHI'),=CL8'XSPDIR',MYKEY,MYKEY,0            
         B     WB10                                                             
*                                                                               
WBSEQ    GOTO1 NBDM,DMCB,(0,=CL8'DMRSEQ'),=CL8'XSPDIR',MYKEY,MYKEY,0            
*                                                                               
WB10     CLC   MYKEYSV(5),MYKEY                                                 
         BNE   WBEND                                                            
         TM    WFKCNTRL,WFKINACT   KEY INACTIVE?                                
         BO    WBSEQ                                                            
* AT THIS POINT NBCMPSTR/END NOT FILLED IN                                      
*****    CLC   WP1STD,NBCMPEND      CHECK WBFLT REC WITHIN                      
*****    BH    WBSEQ                REQUEST START/END DATES                     
*****    CLC   WP1END,NBCMPSTR                                                  
*****    BL    WBSEQ                                                            
*                                                                               
         L     R4,NBWBFLT                                                       
         USING BINDSCT,R4                                                       
         GOTO1 =V(BINSRCH),BINDMCB,(1,MYKEY+5),RR=RELO                          
         CLC   0(2,R1),=1X'00'     TABLE FULL ?                                 
         BNE   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         B     WBSEQ                                                            
WBEND    XIT1                                                                   
*                                                                               
         DROP  R3,R4                                                            
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
                                                                                
*                                                                               
* GET FLIGHT ID / RECORD FOR UNIT                                               
* RETURNS BYTE AND HALF AS INDICATORS                                           
*                                                                               
DOWBFLT  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVI   BYTE,0              CLEAR OVERRIDE FLAG                          
         MVI   HALF,0              CLEAR FLT TYPE FILTER                        
*                                                                               
         L     R4,NBWBFLT          WB FLIGHT BUFFER AREA PASSED ?               
         LTR   R4,R4                                                            
         BZ    DOWBFX              NO- EXIT                                     
         USING BINDSCT,R4                                                       
                                                                                
         L     R3,NBAIO                A(UNIT RECORD)                           
         USING NURECD,R3                                                        
*                                                                               
* CHECK IF FLIGHT OVERRIDE ON UNIT                                              
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'60',0(R3)),(1,=C'1')          
         CLI   12(R1),0                                                         
         BNE   GETFL100                                                         
         L     RE,12(R1)                                                        
         MVC   BWBFLTID,3(RE)       MOVE OVERRIDE FLIGHT NUMBER OUT             
         MVI   BWBFLTID+10,C'*'    OVERRIDE INDICATOR                           
         XC    BWBFLTA,BWBFLTA     CLEAR REC ADDR AREA                          
         MVI   BYTE,C'*'           SET OVERRIDE FLAG FOR FILTER                 
         B     DOWBFX                                                           
         DROP  R4                                                               
*                                                                               
*  BUILD FLIGHT KEY                                                             
*                                                                               
*  GET DAYPART INFORMATION                                                      
GETFL100 MVC   BYTE,NUKDP        SAVE UNIT DAYPART                              
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'60',0(R3)),(1,=C'U')          
         CLI   12(R1),0                                                         
         BNE   GETFL110                                                         
         L     R2,12(R1)                                                        
         USING NUOTH,R2                                                         
         MVC   HALF,NUOTHER        HALF->SUB-DAYPART                            
         DROP  R2                                                               
********************************************************************            
*  RETURN 1 CHARACTER EQUATE FOR SUBDAYPART                                     
         LA    R4,WORK                                                          
         USING NDPTHDR,R4                                                       
         XC    WORK,WORK                                                        
         MVI   NDPTKTYP,X'0D'                                                   
         MVI   NDPTKTYP+1,X'07'                                                 
         MVC   NDPTAGM,NBACTAM                                                  
         MVC   WORK+25(20),WORK                                                 
*                                                                               
HNDPT010 DS    0H                                                               
         GOTO1 NBDM,DMCB,(0,=CL8'DMRDHI'),=CL8'UNTDIR',WORK,WORK,0              
         B     HNDPT040                                                         
*                                                                               
HNDPT020 DS    0H                                                               
         GOTO1 NBDM,DMCB,(0,=CL8'DMRSEQ'),=CL8'UNTDIR',WORK,WORK,0              
                                                                                
HNDPT040 CLC   WORK(5),WORK+25      AGY LEVEL/CLIENT LEVEL                      
         BNE   HNDPT100            NO                                           
         CLC   NDPTDPTA,HALF       MATCH ON CODE?                               
         BNE   HNDPT020                                                         
         MVC   BYTE,NDPTDPTE       PASS BACK EQUATE                             
         B     HNDPTEX                                                          
*                                                                               
* CHECK KEY FOR AGENCY OR CLIENT LEVEL CHECK                                    
* IF AGENCY LEVEL-RESET KEY-MOVE CLIENT CODE IN-RESTART SEARCH                  
* IF CLIENT LEVEL-EXIT ROUTINE-DPT WAS INVALID                                  
*                                                                               
HNDPT100 OC    WORK+28(2),WORK+28  READING CLIENT LEVEL?                        
         BNZ   HNDPTEX             YES - THAT'S ALL                             
*                                                                               
         XC    WORK(20),WORK       NO- READ FOR CLIENT LEVEL                    
         MVC   WORK(3),WORK+25                                                  
         MVC   WORK+3(2),NBACTCLI                                               
         MVC   WORK+25(20),WORK                                                 
         B     HNDPT010                                                         
*                                                                               
HNDPTEX  DS    0H                                                               
* NEED TO RE-ESTABLISH UNTDIR POINTER                                           
         GOTO1 NBDM,DMCB,(0,=CL8'DMRDHI'),=CL8'UNTDIR',KEY,KEY,0                
         DROP  R4                                                               
********************************************************************            
*                                                                               
*  GET PRODUCT FROM 19 ELEMENT                                                  
GETFL110 GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'19',0(R3)),0                  
         CLI   12(R1),0                                                         
         BNE   GETFL350                                                         
         L     RE,12(R1)                                                        
         LA    R4,P                                                             
         XC    P,P                                                              
         MVC   0(1,R4),BYTE        DAYPART                                      
         MVC   1(3,R4),3(RE)       PRODUCT                                      
*                                                                               
*  GET FLIGHT TYPE                                                              
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'02',0(R3)),0                  
         CLI   12(R1),0                                                         
         BNE   GETFL350                                                         
         L     RE,12(R1)                                                        
         USING NUSDRD,RE                                                        
*                                                                               
*  IF D RADIO FLIGHT TYPE MUST BE "D"                                           
         MVI   4(R4),C'D'                                                       
         CLI   NUSTATYP,C'D'                                                    
         BE    GETFL150                                                         
*                                                                               
*  IF C CABLE FLIGHT DEFAULTS TO "C" UNLESS                                     
*  FIELD NUSDSBMD = "V", "B", "G", OR "T"                                       
         CLI   NUSTATYP,C'C'                                                    
         BNE   GETFL120                                                         
         MVC   4(1,R4),NUSDSBMD                                                 
         CLI   NUSDSBMD,C'T'                                                    
         BE    GETFL150                                                         
         CLI   NUSDSBMD,C'V'                                                    
         BE    GETFL150                                                         
         CLI   NUSDSBMD,C'B'                                                    
         BE    GETFL150                                                         
         CLI   NUSDSBMD,C'M'                                                    
         BE    GETFL150                                                         
         CLI   NUSDSBMD,C'G'                                                    
         BE    GETFL150                                                         
         MVI   4(R4),C'C'                                                       
         B     GETFL150                                                         
*                                                                               
*  IF N NETWORK FLIGHT DEFAULTS TO NULLS UNLESS                                 
*  FIELD NUSDSBMD = "I"                                                         
GETFL120 CLI   NUSTATYP,C'N'                                                    
         BNE   GETFL130                                                         
         MVC   4(1,R4),NUSDSBMD                                                 
         CLI   NUSDSBMD,C'I'                                                    
         BE    GETFL150                                                         
         CLI   NUSDSBMD,C'U'                                                    
         BE    GETFL150                                                         
         MVI   4(R4),0                                                          
         B     GETFL150                                                         
*                                                                               
*  IF MEDIA S/O FLIGHT TYPE IS SET TO NULLS                                     
GETFL130 MVI   4(R4),0                                                          
         CLI   NUSTATYP,C'S'                                                    
         BE    GETFL150                                                         
         CLI   NUSTATYP,C'O'                                                    
         BE    GETFL150                                                         
***      DCHO                    SOMETHING WRONG HERE                           
         B     GETFL350          CLEAR BIND AND EXIT                            
         DROP  RE                                                               
*                                                                               
*  BUILD THE BINSRCH KEY FOR DAYPART LEVEL FLIGHT RECORD                        
*  BUILD THE KEY IN WORK                                                        
GETFL150 DS    0H                                                               
         XC    WORK,WORK                                                        
         LA    R2,WORK                                                          
         USING BINRECD,R2                                                       
         MVC   BINPRD,1(R4)         PRODUCT                                     
         MVC   BINFLTP,4(R4)       FLIGHT TYPE                                  
         MVC   BINDPT,0(R4)         DAYPART                                     
         MVC   BINEND,NUKDATE       END DATE                                    
         L     R4,NBWBFLT                                                       
         USING BINDMCB,R4                                                       
*                                                                               
         GOTO1 =V(BINSRCH),BINDMCB,(2,(R2)),RR=RELO                             
         DROP  R4                                                               
         CLI   0(R1),X'00'                                                      
         BNE   GETFL200             REC NOT FOUND                               
         L     R4,0(R1)             POINT TO FOUND REC                          
         CLC   0(BINEND-BINPRD,R4),WORK   PRD/FLTYP/DPT                         
         BE    GETFL400                                                         
*                                                                               
*  BUILD THE KEY (THE DEFAULT RECORD)                                           
GETFL200 CLI   BINDPT,0            ALREADY CHECKED FOR DEFAULT?                 
         BE    GETFL350            YES- NO FLIGHT FOR UNIT                      
         MVI   BINDPT,0                                                         
         L     R4,NBWBFLT                                                       
         USING BINDMCB,R4                                                       
         GOTO1 =V(BINSRCH),BINDMCB,(2,(R2)),RR=RELO                             
         DROP  R4                                                               
         CLI   0(R1),X'00'                                                      
         BNE   GETFL350             REC NOT FOUND                               
         L     R4,0(R1)             POINT TO FOUND REC                          
         CLC   0(BINEND-BINPRD,R4),WORK   PRD/FLTYP/DPT                         
         BE    GETFL400                                                         
*  NO FLIGHT ATTACHED TO THIS UNIT                                              
GETFL350 DS    0H                                                               
         L     RE,NBWBFLT                                                       
         USING BINDSCT,RE                                                       
         XC    BWBFLTA(14),BWBFLTA  ADDR AND FLIGHT ID                          
         B     DOWBFX                                                           
*  CHECK IF UNIT FALL WITHIN KEY DATE RANGE                                     
         DROP   R2                                                              
GETFL400 DS     0H              R4->RETURNED FLIGHT REC                         
         USING  BINRECD,R4                                                      
         CLC    NUKDATE,BINSTRT                                                 
         BL     GETFL200                                                        
         CLC    NUKDATE,BINEND                                                  
         BH     GETFL200                                                        
         MVC   HALF(1),BINFLTP     PASS FLT TYPE FOR FILTER-                    
*  OUTPUT THE FLIGHT NUMBER                                                     
         L     RE,NBWBFLT                                                       
         USING BINDSCT,RE                                                       
         ST     R4,BWBFLTA         PASS ADDR OF REC                             
         MVC    BWBFLTID,BINWBID   BINWBID=CL10,BWBFLTID=CL11                   
         MVI    BWBFLTID+10,X'40'  CLEAR LAST BYTE                              
*                                                                               
DOWBFX   XIT1                                                                   
         DROP  R3,R4,RE                                                         
         LTORG                                                                  
                                                                                
         EJECT                                                                  
                                                                                
*FILTER FOR FLIGHT TYPES                                                        
FLTFLTER NTR1  BASE=*,LABEL=*                                                   
*                                                                               
* FLIGHT OVERRIDE FILTER - OVERRIDES ONLY OR SKIP OVERRIDES                     
                                                                                
         TM    NBWBFLT1,NBWBFOV+NBWBFNOV  FILTER OVERRIDE?                      
         BZ    FLTFILT1                                                         
         TM    NBWBFLT1,NBWBFOV    OVERRIDES ONLY?                              
         BZ    *+16                                                             
         CLI   BYTE,C'*'           OVERRIDE (SET IN DOWBFLT)                    
         BNE   NOFLT               NOT OVERRIDE-SKIP                            
         B     FLTFILT1            CONTINUE                                     
                                                                                
*                                  NO OVERRIDES                                 
         CLI   BYTE,C'*'           OVERRIDE ?                                   
         BE    NOFLT               YES-SKIP                                     
*                                                                               
* MEDIA  / SUB MEDIA FILTER                                                     
FLTFILT1 CLI   NBWBMEDF,0          MEDIA FILTERING ?                            
         BE    FLTFILTX            NO -                                         
*                                                                               
         TM    NBWBMEDF,X'01'      NETWORK?                                     
         BZ    *+16                                                             
         CLI   NBEFFMED,C'N'                                                    
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
         TM    NBWBMEDF,X'02'      SYNDICATION?                                 
         BZ    *+16                                                             
         CLI   NBEFFMED,C'S'                                                    
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
         TM    NBWBMEDF,X'04'      OTHER ?                                      
         BZ    *+16                                                             
         CLI   NBEFFMED,C'O'                                                    
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
         CLI   NBEFFMED,C'C'       SO MUST BE CABLE                             
         BNE   NOFLT                                                            
         TM    NBWBMEDF,X'08'      CC FILTER ?                                  
         BZ    *+16                                                             
         CLI   HALF,C'C'                                                        
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
         TM    NBWBMEDF,X'10'     CT FILTER ?                                   
         BZ    *+16                                                             
         CLI   HALF,C'T'                                                        
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
         TM    NBWBMEDF,X'20'      CV FILTER ?                                  
         BZ    *+16                                                             
         CLI   HALF,C'V'                                                        
         BNE   NOFLT                                                            
         B     FLTFILTX                                                         
*                                                                               
FLTFILTX L     R1,NBWBFLT                                                       
         LTR   R1,R1                                                            
         BZ    FLTFLTXX                                                         
         USING BINDSCT,R1                                                       
*                                                                               
         CLI   BWBFLEND,0          FLIGHT END RANGE                             
         BNE   FLTFLIT3                                                         
         CLI   BWBFLSTR,0           FLIGHT START RANGE                          
         BE    FLTFLTXX                                                         
* MATCH ON FLIGHT                                                               
         CLC   BWBFLSTR,BWBFLTID                                                
         BNE   NOFLT                                                            
         B     FLTFLTXX                                                         
* FLIGHT RANGE                                                                  
FLTFLIT3 CLC   BWBFLTID(10),BWBFLEND                                            
         BH    NOFLT                                                            
         CLC   BWBFLTID(10),BWBFLSTR                                            
         BL    NOFLT                                                            
         B     FLTFLTXX                                                         
*                                                                               
FLTFLTXX SR    RE,RE                                                            
NOFLT    LTR   RE,RE                                                            
         XIT1                                                                   
         DROP  R1                                                               
*                                                                               
         LTORG                                                                  
                                                                                
         EJECT                                                                  
*              INITIALIZATION ROUTINES                                          
         SPACE 3                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
***      MVI   NBINIT,1            INITIALIZE NETBLOCK                          
         MVC   NBSELID-8(4),=C'SELS'                                            
***->    MVC   NBDATA-8(8),=C'**OPTS**'                                         
***->    MVC   NBAIO-8(8),=C'**ADDS**'                                          
         MVC   NBKEY-8(4),=C'KEYS'                                              
***->    MVC   NBMODE-8(8),=C'**MODE**'                                         
***->    MVC   NBERROR-8(8),=C'**ERRS**'                                        
         MVC   NBACTAM-8(4),=C'AKEY'                                            
***->    MVC   NBMAINEL-8(8),=C'**MAIN**'                                       
***->    MVC   NBBILTGR-8(8),=C'**BILL**'                                       
***->    MVC   NBMGFPCD-8(8),=C'**MG/F**'                                       
***->    MVC   NBMGBPCD-8(8),=C'**MGBY**'                                       
***->    MVC   NBESTUN-8(8),=C'**ESTV**'                                        
***->    MVC   NBACTUN-8(8),=C'**ACTV**'                                        
         SPACE 1                                                                
         OC    NBACOM,NBACOM       IS A(COMFACS) AROUND                         
         BNZ   INIT2               NO - ASSUME WE'RE LINKED OFF LINE            
         MVC   NBXTRNS(XVTYPES-VTYPES),VTYPES     MOVE IN EXTRNS                
         OC    NBCONFID,NBCONFID                  CONFID LINKED                 
         BZ    INIT4                                                            
         LA    R2,WORK                                                          
         USING CND,R2                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNID,NBSELID                                                     
         OC    CNID,CNID           WAS AN ID PROVIDED                           
         BZ    INIT4                                                            
         GOTO1 NBCONFID,DMCB,(10,(R2)),DMCB+4                                   
         MVC   NBSELAGY,CNAGY      THEN WE CAN PROVIDE AGENCY CODE              
         CLI   NBOPNOPT,C'Y'       OPTION TO OPEN FILES                         
         BNE   INIT4                                                            
         L     R3,NBUTL                                                         
         MVC   4(1,R3),CNNSE                                                    
         GOTO1 NBDM,DMCB,=C'OPEN',=CL8'SPOT',FILLIST,NBAIO                      
         B     INIT4                                                            
         EJECT                                                                  
*              COMFACS AND CALLOV FUNCTIONS                                     
         SPACE 3                                                                
INIT2    L     R2,NBACOM           ON LINE - ADDRESSES FROM COMFACS             
         USING COMFACSD,R2                                                      
         MVC   NBDM,CDATAMGR                                                    
         MVC   NBCALLOV,CCALLOV                                                 
         MVC   NBDATCON,CDATCON                                                 
         MVC   NBGETDAY,CGETDAY                                                 
         MVC   NBADDAY,CADDAY                                                   
         MVC   NBHEXOUT,CHEXOUT                                                 
         MVC   NBHELLO,CHELLO                                                   
         MVC   NBDEMADR,CDEMADDR                                                
         MVC   NBDEMAIN,CDEMAINT                                                
         MVC   NBDEMAND,CDEMAND                                                 
         MVC   NBDEMEL,CDEMEL                                                   
         MVC   NBDEMMTH,CDEMOMTH                                                
         MVC   NBDEMOUT,CDEMOUT                                                 
         MVC   NBGTPROF,CGETPROF                                                
***      MVC   NBDEFINE,CDEFINE            NOT THERE                            
         SPACE 1                                                                
         L     R2,=V(CLPACK)                                                    
         A     R2,RELO                                                          
         ST    R2,NBCLPACK                                                      
         L     R2,=V(CLUNPK)                                                    
         A     R2,RELO                                                          
         ST    R2,NBCLUNPK                                                      
         SPACE 1                                                                
******   OC    NBNETVAL,NBNETVAL   IF NO ADDRESS FOR NETVALUE                   
******   BNZ   INIT4                                                            
         OC    NBCALLOV,NBCALLOV   IF CALL OVERLAY EXISTS                       
         BZ    INIT3                                                            
         GOTO1 NBCALLOV,DMCB,0,X'D9000A28'    GET NETVALUE ADDRESS              
         MVC   NBNETVAL,DMCB                                                    
         GOTO1 NBCALLOV,DMCB,0,X'D9000AE0'    GET DEMOCON ADDRESS               
         MVC   NBDEMCON,DMCB                                                    
         GOTO1 NBCALLOV,DMCB,0,X'D9000A26'    GET DEFINE  ADDRESS               
         MVC   NBDEFINE,DMCB                                                    
*                                                                               
         GOTO1 NBCALLOV,DMCB,0,X'D9000A5A'    GET COMINTER ADDRESS              
         OC    NBEXTEND,NBEXTEND                                                
         JZ    INIT4                                                            
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         MVC   NBXCOMIN,DMCB       A(COMINTER)                                  
         B     INIT4                                                            
*                                                                               
INIT3    GOTO1 NBLOADER,DMCB,=C'T00A28  ',0,0  OTHERWISE LOAD IT IN             
         MVC   NBNETVAL,DMCB+4                                                  
*                                                                               
         GOTO1 NBLOADER,DMCB,=C'T00A5A  ',0,0  OTHERWISE LOAD IT IN             
         OC    NBEXTEND,NBEXTEND                                                
         JZ    INIT4                                                            
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         MVC   NBXCOMIN,DMCB+4     A(COMINTER)                                  
         DROP  RF                                                               
*                                  CLEAR OPTIONAL BUFFERS                       
INIT4    CLI   NBINIT,1            ALREDY CLEARED ?                             
         BE    INIT6                                                            
         L     RE,NBAPBUFF         PRODUCT BUFFER                               
         LTR   RE,RE                                                            
         BZ    INIT6                                                            
         L     RF,=F'1000'                                                      
         XCEF                                                                   
         MVI   NBINIT,1            INITIALIZE NETBLOCK                          
INIT6    XIT1                                                                   
         LTORG                                                                  
*                                                                               
FILLIST  DS    0F                                                               
         DC    CL8'NSPTFILE'                                                    
         DC    CL8'NSPTDIR'                                                     
         DC    CL8'NSTAFILE'                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL8'NDEMDIRA'                                                    
         DC    CL8'NDEMDIRN'                                                    
**       DC    CL8'NDEMDIRO'                                                    
**       DC    CL8'NDEMFILA'                                                    
**       DC    CL8'NDEMFILO'                                                    
**       DC    CL8'NDEMFILN'                                                    
**       DC    CL8'NDEMFILP'                                                    
**       DC    CL8'NDEMFILQ'                                                    
**       DC    CL8'NDEMFILU'                                                    
**       DC    CL8'NDEMFILV'                                                    
**       DC    CL8'NHUTFL'                                                      
**       DC    CL8'NPAVFLA'                                                     
**       DC    CL8'NPAVFLN'                                                     
         DC    CL8'NPAVDIR'                                                     
**       DC    CL8'NPAVFIL'                                                     
         DC    CL8'NUNTFIL'                                                     
         DC    CL8'NUNTDIR'                                                     
         DC    CL8'NNTIDIR'                                                     
**       DC    CL8'NNTIFIL'                                                     
**       DC    CL8'NNTIFILN'                                                    
**       DC    CL8'NNTIFILO'                                                    
**       DC    CL8'NNTIFILP'                                                    
         DC    CL8'NL=NTIFL'                                                    
         DC    CL8'NL=PAVFL'                                                    
         DC    CL8'NL=DEMFA'                                                    
         DC    CL8'NL=DEMFN'                                                    
         DC    C'X'                                                             
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
*   TESTS IF UNIT HAS PIGGY BACKS                                               
*   NBPIGOPT=Y ACCEPTS ONLY PIGGY UNITS/ =N REJECTS PIGGY UNITS                 
*                                                                               
PIGGYOPT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'19',NBAIO),0                  
         CLI   12(R1),0                                                         
         BNE   PG10                                                             
         L     R6,12(R1)                                                        
         USING NUPDED,R6                                                        
         TM    NUPDEIND,NUPDCSP     CHECK FOR COPYSPLIT                         
         BO    PG100                NOT A PIGGYPACK                             
         CLI   NUPDELEN,17          CHECK FOR 2 PRODUCTS                        
         BNE   PG100                NOT A PIGGYPACK                             
         B     PG80                                                             
*                                                                               
PG10     GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFILE'),(X'14',NBAIO),0                  
         CLI   12(R1),0                                                         
         BE    PG100                YES REJECT, ONLY PIGGYBACKS                 
*                                                                               
         USING NUMAINEL,R6                                                      
         CLI   NUPRD,0                                                          
         BE    PG100                NOT A PIGGYBACK                             
         CLI   NUPRD2,0                                                         
         BE    PG100                NOT A PIGGYBACK                             
*                                                                               
* AT THIS POINT THE UNIT IS A PIGGYBACK                                         
PG80     CLI   NBPIGOPT,C'Y'       ACCEPT ONLY PIGGYS                           
         BE    YESPG                                                            
         B     NOPG                                                             
*                                                                               
* AT THIS POINT THE UNIT IS NOT A PIGGYBACK                                     
PG100    CLI   NBPIGOPT,C'Y'       ACCEPT ONLY PIGGYS                           
         BE    NOPG                                                             
         B     YESPG                                                            
***************************                                                     
***         USING NUMAINEL,R6                                                   
***         CLI   NBPIGOPT,C'Y'       ACCEPT ONLY PIGGYS                        
***         BNE   PG5                                                           
***         CLI   NUPRD,0                                                       
***         BE    NOPG                                                          
***         CLI   NUPRD2,0                                                      
***         BE    NOPG                                                          
***         B     YESPG                                                         
***PG5      CLI   NBPIGOPT,C'N'       REJECT PIGGYS                             
***         BE    *+6                                                           
***         DC    H'0'                                                          
***         CLI   NUPRD,0                                                       
***         BE    YESPG                                                         
***         CLI   NUPRD2,0                                                      
***         BE    YESPG                                                         
NOPG     SR    R1,R1                                                            
         B     *+8                                                              
YESPG    LA    R1,1                                                             
         LTR   R1,R1                                                            
         XIT1                                                                   
         DROP  R6                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
NETIOD   DSECT                                                                  
NETWRK   DS    0C                                                               
DUB      DS    D                                                                
WORK     DS    CL64                                                             
DMCB     DS    6F                                                               
DMWORK   DS    CL96                                                             
RELO     DS    A                                                                
BYTE     DS    CL1                                                              
FILE     DS    CL8                                                              
COMMAND  DS    CL8                                                              
PFILL    DS    C                                                                
P        DS    CL132                                                            
PARAS    DS    6F                                                               
FULL     DS    F                                                                
BRNCHNUM DS    CL1                 NUMBER OF PLACE TO RETURN TO.                
ELCODE   DS    CL1                                                              
BITTEST  DS    CL1                                                              
FIRSTSW  DS    CL1                                                              
PROGSW   DS    CL1                                                              
NETSW    DS    CL1                                                              
HOLDEFLT DS    CL1                 HOLDS EST FILTER FOR ANDING                  
MEDSW    DS    CL1                 MEDIA FILL SWITCH                            
HALF     DS    H                                                                
DUMEL    DS    CL10                                                             
ADROFFCR DS    A                                                                
* WB FLIGHT READ KEYS                                                           
MYKEY    DS    CL50                                                             
MYKEYSV  DS    CL50                                                             
NEWOFFB  DS    CL100                                                            
SVHUNOPT DS    CL1                                                              
SVUSER3  DS    CL16                                                             
         SPACE 1                                                                
*        DEMO BLOCK HERE                                                        
         PRINT OFF                                                              
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
NETWRKX  EQU   *                                                                
         SPACE 3                                                                
       ++INCLUDE NEWBFLTD                                                       
         SPACE 3                                                                
       ++INCLUDE NEGENPBUFF                                                     
         SPACE 3                                                                
       ++INCLUDE NEGENNBUFF                                                     
         SPACE 3                                                                
       ++INCLUDE NEGENTBUFF                                                     
*  INCLUDES    DDCNTRL                                                          
*  HERE...     DDCOMFACS                                                        
*              DDOFFICED                                                        
*              SPGENCLT                                                         
*              SPGENAGY                                                         
*              SPGENPRD                                                         
*              SPGENPRG                                                         
*              SPGENEST                                                         
*              SPGENSTA                                                         
*              NEGENPACK                                                        
*              NEGENUNIT                                                        
*              NETBLOCKD                                                        
       ++INCLUDE DDCNTRL                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDOFFICED                                                      
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENAGY                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENPRG                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPGENSLST                                                      
       ++INCLUDE NEGENPACK                                                      
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NETXBLKD                                                       
       ++INCLUDE SPGENWBFLT                                                     
       ++INCLUDE NEGENDPT                                                       
NEBLOCKD DSECT                                                                  
       ++INCLUDE NETBLOCKN                                                      
       ++INCLUDE NETBLKXTND                                                     
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FASECRETD                                                      
       ++INCLUDE NETSECD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
KEY      EQU   NBKEY                                                            
KEYSAVE  EQU   NBKEYLST                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'066NENETIO   06/02/20'                                      
         END                                                                    
