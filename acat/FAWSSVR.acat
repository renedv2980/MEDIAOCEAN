*          DATA SET FAWSSVR    AT LEVEL 015 AS OF 10/02/08                      
*CATALP WSSVR                                                                   
         TITLE 'FAWSSVR  - SAVE W/S AREA INTO XA'                               
         SPACE 1                                                                
WSSVR    CSECT                                                                  
         NMOD1 WORKL,**WSSVR*                                                   
         USING WORKD,RC                                                         
         LR    RA,R1                                                            
         USING FAWSSVRD,RA                                                      
*                                                                               
         ICM   RF,15,VPROTOFF                                                   
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         ICM   R2,15,VSSB                                                       
         BZ    XMOD                                                             
         USING SSBD,R2                                                          
         ICM   R3,15,SSBTKADR                                                   
         BZ    XMOD                                                             
         USING TCBD,R3                                                          
*                                                                               
         CLI   FAWSACTN,FAWSUSVE                                                
         BE    WSSV02                                                           
         CLI   FAWSACTN,FAWSURST                                                
         BE    WSSV02                                                           
         CLI   FAWSACTN,FAWSUDEL                                                
         BE    WSSV02                                                           
         CLI   FAWSACTN,FAWSUCLR                                                
         BE    WSSV02                                                           
         B     WSSV04                                                           
*                                                                               
WSSV02   MVC   MAXLEN,=AL4(XAPASSRL)                                            
         ICM   RF,15,TCBUTL                                                     
         ICM   R4,15,TUTLXADR-UTLD(RF)                                          
         BNZ   *+6                                                              
         DC    H'0'                WRONG FASTART/LCM                            
*                                                                               
         SAM31                                                                  
         ICM   R4,15,XAAPASSR-XAUTLD(R4)                                        
         MVC   0(4,R4),=CL4'AATK'  FAME AT LAST                                 
         B     WSSV06                                                           
*                                                                               
WSSV04   LH    RF,SSBWSSVR         SET MAXLEN FROM EQUATE IN SSB                
         MHI   RF,K                                                             
         ST    RF,MAXLEN                                                        
         SAM31                                                                  
         USING WSMAPD,R4                                                        
         ICM   R4,15,TCBAXAWS                                                   
         CLC   TCBSIN,WSSIN        ENSURE SAME SIN                              
         BE    WSSV06              YES - FINE                                   
*                                                                               
         LR    R0,R4               CLEAR AREA AND SET SIN                       
         L     R1,MAXLEN                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVC   WSSIN,TCBSIN                                                     
         DROP  R4                                                               
*                                                                               
WSSV06   MVC   ACTION,FAWSACTN     Save action (Used as return code)            
         MVI   FAWSRTN,FAWSROK                                                  
         CLI   ACTION,FAWSASVE     Save TOKEN                                   
         BE    SAVE                                                             
         CLI   ACTION,FAWSUSVE     Save TOKEN                                   
         BE    SAVE                                                             
         CLI   ACTION,FAWSGMXA     Get A(XA BLOCK) and/or obtain area           
         BE    GETXA                                                            
         CLI   ACTION,FAWSARST     Restore TOKEN                                
         BE    RESTORE                                                          
         CLI   ACTION,FAWSURST     Restore TOKEN                                
         BE    RESTORE                                                          
         CLI   ACTION,FAWSADEL     Delete TOKEN                                 
         BE    DELETE                                                           
         CLI   ACTION,FAWSUDEL     Delete TOKEN                                 
         BE    DELETE                                                           
         CLI   ACTION,FAWSUCLR     Clear OUT AREA                               
         BE    CLEAR                                                            
         DC    H'0'                                                             
         SPACE 1                                                                
***********************************************************************         
* STORAGE IS ARRANGED AS FOLLOWS IN THE XA BUFFER                     *         
* XL4  : SIN (TO ENSURE CLEANLINESS)                                  *         
* FOLLOWED BY REPEATED COPIES OF THE FOLLOWING:                       *         
* XL4  : TOKEN                                                        *         
* XL1  : N/D                                                          *         
* XL3  : LENGTH OF STORAGE ASSOCIATED WITH TOKEN                      *         
* XL?? : STORAGE ITSELF                                               *         
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
* SAVE TOKEN BLOCK INTO BUFFER                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING WSMAPD,R6                                                        
SAVE     LA    R6,L'TCBSIN(R4)                                                  
         LR    R0,R4                                                            
         A     R0,MAXLEN           R0=A(END OF BUFFER)                          
*                                                                               
         XR    RF,RF                                                            
SVE02    OC    WSTOKEN,WSTOKEN     END OF LINKED TOKEN LIST                     
         BZ    SVE04                                                            
         CLC   FAWSTOKN,WSTOKEN    ALREADY SAVED THIS TOKEN?                    
         BE    SVE06               YES                                          
         ICM   RF,7,WSLEN          LENGTH IS AT +5                              
         LA    R6,WSLNQ(RF,R6)     NEXT TOKEN                                   
         CR    R6,R0               AT END OF BUFFER?                            
         BL    SVE02               STILL WITHIN RANGE FOR THIS BUFFER           
         B     ERRNF                                                            
*                              *** NEW TOKEN TO ADD                             
SVE04    LR    RF,R6                                                            
         SR    RF,R4               RF=AMOUNT OF BUFFER USED                     
         L     R0,MAXLEN                                                        
         SR    R0,RF               R0=AMOUNT OF BUFFER LEFT                     
         SHI   R0,WSLNQ                                                         
         BNP   ERRFUL              NO SPACE                                     
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,FAWSLEN                                                     
         CR    R0,RF                                                            
         BL    ERRFUL              BLOCK TOO BIG TO FIT                         
*                                                                               
         MVC   WSTOKEN,FAWSTOKN    SAVE TOKEN AND LENGTH                        
         STCM  RF,7,WSLEN                                                       
*                                                                               
         LA    R0,WSDATA           MOVE IN DATA                                 
         XR    R1,R1                                                            
         ICM   R1,3,FAWSLEN                                                     
         L     RE,FAWSADR                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     XMOD                                                             
*                              *** TOKEN TO UPDATE                              
SVE06    CLC   FAWSLEN,WSLEN+1     LENGTH CHANGE?                               
         BNE   SVE08               YES                                          
*                                                                               
         LA    R0,WSDATA           MOVE IN DATA                                 
         XR    R1,R1                                                            
         ICM   R1,3,FAWSLEN                                                     
         L     RE,FAWSADR                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     XMOD                                                             
*                                                                               
SVE08    BRAS  RE,DELTOKN          DELETE THIS TOKEN                            
         B     SAVE                THEN GO BACK TO RE-ADD IT                    
         EJECT                                                                  
***********************************************************************         
* GET and/or obtain XA area and pass to user, issued by TOKEN         *         
***********************************************************************         
         SPACE 1                                                                
GETXA    LA    R6,L'TCBSIN(R4)                                                  
         LR    R0,R4                                                            
         A     R0,MAXLEN           R0=A(End of buffer)                          
         XC    FAWSADR,FAWSADR                                                  
         XC    FAWSEADR,FAWSEADR                                                
*                                                                               
         XR    RF,RF                                                            
GXA02    OC    WSTOKEN,WSTOKEN     End of linked TOKEN list                     
         BZ    GXA04                                                            
         CLC   FAWSTOKN,WSTOKEN    Already saved this TOKEN?                    
         BE    GXA06               Yes                                          
         ICM   RF,7,WSLEN          Length of entry                              
         LA    R6,WSLNQ(RF,R6)     Next TOKEN                                   
         CR    R6,R0               At end of buffer?                            
         BL    GXA02               Still within range for this buffer           
         B     ERRNF               Yes                                          
*                              *** New TOKEN to add                             
GXA04    LR    RF,R6                                                            
         SR    RF,R4               RF=Amount of buffer used                     
         L     R1,MAXLEN                                                        
         SR    R1,RF                                                            
         SHI   R1,WSLNQ            R1 = Amount of buffer left                   
         BNP   ERRFUL              No space                                     
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,FAWS#OFK                                                    
         MHI   RF,K                RF = Size of this buffer                     
         CR    R1,RF               Will it fit ?                                
         BL    ERRFUL              Block too big to fit                         
*                                                                               
         MVI   WSIND,WS_XAGET      Set as XA GET type?                          
         MVC   WSTOKEN,FAWSTOKN    Save TOKEN and length                        
         STCM  RF,7,WSLEN                                                       
         B     GXA18                                                            
*                              *** TOKEN to update                              
GXA06    XR    R1,R1                                                            
         ICM   R1,3,FAWS#OFK                                                    
         MHI   R1,K                                                             
         XR    RF,RF                                                            
         ICM   RF,7,WSLEN                                                       
         TM    WSIND,WS_XAGET      XA GET Type?                                 
         BZ    GXA18               No, so ignore passed length                  
         CR    RF,R1               Length change?                               
         BE    GXA18               Found and the same so pass info back         
         BL    GXA20               Yes, want less, so delete, re-add            
*                                                                               
GXA08    LR    RE,R6               Save R6                                      
         LA    R6,WSLNQ(RF,R6)     Next TOKEN                                   
         OC    WSTOKEN,WSTOKEN     At end?                                      
         BZ    GXA10               Yes, so extend buffer                        
         LR    R6,RE               Restore so can delete TOKEN                  
         B     GXA20               No, so give warning, delete, re-add          
*                                                                               
GXA10    LR    R6,RE               Restore TOKEN address                        
         AR    RE,R1               New end                                      
         CR    R0,RE               Block passed end of buff (R0)                
         BL    ERRFUL              Yes, block too big to fit                    
         LR    RF,R1                                                            
         STCM  RF,7,WSLEN          New size                                     
*                                                                               
GXA18    LA    R1,WSDATA           Return XA address to user                    
         ST    R1,FAWSADR          Start Address                                
         AR    R1,RF               RF=Size                                      
         ST    R1,FAWSEADR         End Address                                  
         B     XMOD                                                             
*                                                                               
GXA20    OI    FAWSRTN,FAWSRWA1                                                 
         BRAS  RE,DELTOKN          Delete this TOKEN                            
         B     GETXA               Then go back to re-add it                    
         EJECT                                                                  
***********************************************************************         
* RESTORE TOKEN BLOCK FROM BUFFER                                     *         
***********************************************************************         
         SPACE 1                                                                
RESTORE  LA    R6,L'TCBSIN(R4)                                                  
         LR    R0,R4                                                            
         A     R0,MAXLEN           R0=A(END OF BUFFER)                          
*                                                                               
         XR    RF,RF                                                            
RES02    OC    WSTOKEN,WSTOKEN     END OF LINKED TOKEN LIST                     
         BZ    ERRNF                                                            
         CLC   FAWSTOKN,WSTOKEN    FOUND TOKEN?                                 
         BE    RES04               YES                                          
         ICM   RF,7,WSLEN          LENGTH IS AT +4                              
         LA    R6,WSLNQ(RF,R6)     NEXT TOKEN                                   
*                                                                               
         CR    R6,R0               STILL WITHIN RANGE FOR THIS BUFFER           
         BL    RES02                                                            
         B     ERRNF               TOKEN NOT FOUND                              
*                                                                               
RES04    LA    R0,WSDATA           R0 = MOVE FROM ADDRESS                       
         L     RE,FAWSADR          RE = MOVE TO ADDRESS                         
         XR    R1,R1                                                            
         ICM   R1,7,WSLEN          R1=BLOCK LENGTH                              
         CLI   FAWSOVR,FAWSOLEN    LENGTH OVERRIDE BY CALLER?                   
         BNE   RES06               NO                                           
         CLM   R1,3,FAWSLEN                                                     
         BL    RES06               CAN'T ASK FOR MORE THAN YOU SAVED            
         ICM   R1,3,FAWSLEN                                                     
*                                                                               
RES06    STCM  R1,3,FAWSLEN        RETURN LENGTH                                
         LR    RF,R1                                                            
         MVCL  RE,R0               RESTORE BUFFER                               
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* DELETE TOKEN BLOCK FROM STORAGE                                     *         
***********************************************************************         
         SPACE 1                                                                
DELETE   LA    R6,L'TCBSIN(R4)                                                  
         LR    R0,R4                                                            
         A     R0,MAXLEN           R0=A(END OF BUFFER)                          
*                                                                               
         XR    RF,RF                                                            
DEL02    OC    WSTOKEN,WSTOKEN     END OF TOKEN LIST?                           
         BZ    ERRNF               YES - TOKEN NOT FOUND                        
         CLC   FAWSTOKN,WSTOKEN    MATCH TOKEN?                                 
         BE    DEL04               YES                                          
*                                                                               
         ICM   RF,7,WSLEN          LENGTH IS AT +4                              
         LA    R6,WSLNQ(RF,R6)     NEXT TOKEN                                   
         CR    R6,R0               STILL WITHIN RANGE FOR THIS BUFFER           
         BL    DEL02                                                            
         B     ERRNF               NOT FOUND IN BUFFER                          
*                                                                               
DEL04    BRAS  RE,DELTOKN          R0 = MOVE FROM ADDRESS                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* DELETE TOKEN FROM BUFFER                                            *         
* NTRY: R4 = A(BUFFER START)                                          *         
*       R6 = A(TOKEN)                                                 *         
***********************************************************************         
         SPACE 1                                                                
DELTOKN  NTR1  ,                                                                
         LR    R0,R6                                                            
         SR    R0,R4               R0=DISPLACEMENT FROM START                   
         L     R1,MAXLEN                                                        
         SR    R1,R0               R1=MOVE TO LENGTH                            
*                                                                               
         LR    RF,R1               RF=LENGTH REMAINING                          
         XR    R0,R0                                                            
         ICM   R0,7,WSLEN                                                       
         AHI   R0,WSLNQ            R0=LENGTH TO REMOVE                          
         SR    RF,R0               RF=MOVE FROM LENGTH                          
*                                                                               
         LR    RE,R0                                                            
         AR    RE,R6               RE=MOVE FROM ADDRESS                         
*                                                                               
         LR    R0,R6               R0=MOVE TO ADDRESS                           
         MVCL  R0,RE               MOVE AND ZERO FILL                           
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CLEAR OUT UTL REGION                                                *         
***********************************************************************         
         SPACE 1                                                                
CLEAR    LR    R0,R4                                                            
         ICM   R1,15,MAXLEN                                                     
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND HANDY ROUTINES                                      *         
***********************************************************************         
         SPACE 1                                                                
ERRNF    OI    FAWSRTN,FAWSRNF                                                  
         B     XMOD                                                             
*                                                                               
ERRFUL   OI    FAWSRTN,FAWSRFUL                                                 
         B     XMOD                                                             
*                                                                               
XMOD     SAM24                                                                  
         ICM   RF,15,VPROTON                                                    
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
VSSB     DC    V(SSB)                                                           
VPROTOFF DC    V(PROTOFF)                                                       
VPROTON  DC    V(PROTON)                                                        
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
K        EQU   1024                                                             
DUB      DS    D                                                                
MAXLEN   DS    F                                                                
ACTION   DS    X                                                                
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE MAPPING                                                       
***********************************************************************         
WSMAPD   DSECT                                                                  
WSSIN    DS    AL4                 SIN of transaction                           
         ORG   WSSIN                                                            
WSTOKEN  DS    CL4                 TOKEN                                        
WSIND    DS    XL1                 Indicator                                    
WS_XAGET EQU   X'02'               .  User controled 31-bit area                
WSLEN    DS    XL3                 Length of buffer                             
WSDATA   DS    0X                  USER DATA                                    
WSLNQ    EQU   *-WSMAPD                                                         
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE FAWSSVRD                                                       
         EJECT                                                                  
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015FAWSSVR   10/02/08'                                      
         END                                                                    
