*          DATA SET TALDCPTR   AT LEVEL 194 AS OF 10/30/19                      
*CATALP TALDCPTR                                                                
*                                                                               
***********************************************************************         
*                                                                     *         
*  YOU MUST RE-LINK LMTALD, TASYSVAL AND TALNK01 WHEN YOU CHANGE THIS *         
*                                                                     *         
*  MODULE. IT IS ABSOLUTELY NECESSARY THAT THEY ARE INSTALLED LIVE AT *         
*                                                                     *         
*  THE SAME TIME.  BE CAREFUL, AND DON'T FORGET!!!!                   *         
*                                                                     *         
***********************************************************************         
*                                                                               
         TITLE 'LDCPTR - TALENT - CREATE DIRECTORY POINTERS'                    
         PRINT NOGEN                                                            
*        PARA1 BYTE  0  =X'80' ONLINE                                           
*                       =X'40' PASSING PARM BLOCK IN P3                         
*                       =X'20' FORCE SEQ BASED ON TIME                          
*                       =X'10' DO NOT GEN STOP/PULL PTRS ON CHK REIS            
*                       =X'08' ONLY GENERATE TLCOECDQ POINTER                   
*              BYTES 1-3=A(RECORD)                                              
*        PARA2=A(AREA TO CREATE PASSIVE POINTERS)                               
*              LAST POINTER FOLLOWED BY X'00'                                   
*        PARA3=A(PARAMTER BLOCK - TALDCPTRD IF P1 BYTE 0 = X'80')               
*        PARA4=NOT USED                                                         
*        PARA5=A(RECORD'S DISK ADDRESS)                                         
*                                                                               
LDCPTR   CSECT                                                                  
         NMOD1 WRKX-WRKD,TALDCPTR,RA,R7,RR=R4,CLEAR=YES                         
         USING WRKD,RC                                                          
         ST    R4,RELO                                                          
         LM    R2,R4,0(R1)         R2=A(PRIME DATA RECORD)                      
         USING CPTRD,R4            R4=A(OPT. PARAM. BLOCK)                      
         MVC   P1BYTE0,0(R1)                                                    
         L     RF,16(R1)                                                        
         MVC   RDSKADD,0(RF)       RECORD'S DISK ADDRESS                        
*                                                                               
         TM    P1BYTE0,X'40'       TEST WE HAVE PARAM. BLOCK                    
         BZ    POINT0                                                           
         MVC   DATCON,CPDATCON     SAVE A(DATCON)                               
         MVC   ADDAY,CPADDAY            A(ADDAY)                                
         MVC   ACATTAB,CPCATTAB         A(CATEGORY TABLE)                       
         MVC   AYRSTAB,CPYRSTAB         A(YEARS TABLE)                          
         MVC   PROTOFF,CPPROTOF         A(PROTECTION OFF)                       
         MVC   PROTON,CPPROTON          A(PROTECTION ON)                        
         MVC   AUSETAB,CPUSETAB         A(USE TABLE)                            
         B     POINT1                                                           
*                                                                               
POINT0   MVC   DATCON,=V(DATCON)   ELSE THEY'D BETTER BE INCLUDED               
         MVC   ADDAY,=V(ADDAY)                                                  
         MVC   ACATTAB,=V(LDCATAB)                                              
         MVC   AYRSTAB,=V(LDYRTAB)                                              
         MVC   AUSETAB,=V(LDUSTAB)                                              
*                                                                               
POINT1   GOTO1 DATCON,DUB,(5,0),(1,TODAY)  SAVE TODAY'S DATE                    
         GOTO1 DATCON,DUB,(5,0),(0,WORK)        IN WORK CHAR FORMAT             
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+8,-2       2 YEARS AGO               
         GOTO1 DATCON,DUB,(0,WORK+8),(1,PRI2YR)       SAVE IN PACKED            
*                                                                               
         MVC   0(32,R3),0(R2)      SET ONE ACTIVE POINTER                       
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(5,R3),34(R3)                                                  
         LA    R3,38(R3)           R3=A(NEXT PASSIVE POINTER RECORD)            
         LA    R4,PPLIST                                                        
         SPACE 1                                                                
POINT2   CLC   0(1,R2),0(R4)       LOOK FOR MATCH ON RECORD TYPE                
         BNE   POINT4                                                           
         TM    P1BYTE0,X'08'       IF ONLY GENERATE TLCOECDQ POINTER            
         BZ    POINT2A                                                          
         CLI   1(R4),TLCOECDQ      SKIP ALL OTHERS                              
         BNE   POINT4                                                           
POINT2A  XC    0(32,R3),0(R3)      CREATING PASSIVE POINTER                     
         LR    R6,R2               PRESET R6 FOR GETELS                         
*                                                                               
POINT3   ST    R3,SAVER3                                                        
         L     RF,4(R4)            PICK UP CREATE ROUTINE ADDRESS               
         A     RF,RELO                                                          
         BASR  RE,RF               AND EXECUTE                                  
         BNE   POINT4              (NOT INTERESTED)                             
         L     R3,SAVER3                                                        
         MVC   0(1,R3),1(R4)       CODE IS IN TABLE                             
         MVC   32(1,R3),34(R2)     TAKE STATUS FROM PARENT                      
         CLI   0(R3),TLINBCDQ      UNLESS INV. STATUS/BILLING                   
         BE    POINT3D                                                          
         CLI   0(R3),TLDVOCDQ      OR ADVICE OFFICE                             
         BE    POINT3D                                                          
         CLI   0(R3),TLDVEDDQ      OR ADVICE EARLIEST DUE DATE                  
         BE    POINT3D                                                          
         CLI   0(R3),TLDVRDDQ      OR ADVICE REGULAR DUE DATE                   
         BE    POINT3D                                                          
         CLI   0(R3),TLDVMDDQ      OR ADVICE MUSIC DUE DATE                     
         BE    POINT3D                                                          
         MVC   33(1,R3),35(R2)     TAKE 2ND STATUS FROM PARENT                  
POINT3D  XC    34(5,R3),34(R3)     CLEAR D/A AND 1ST BYTE OF NEXT               
         LA    R3,38(R3)                                                        
         SPACE 1                                                                
POINT4   LA    R4,8(R4)                                                         
         CLI   0(R4),X'FF'                                                      
         BE    EXIT                                                             
         B     POINT2                                                           
         EJECT                                                                  
*              STAFF RECORDS                                                    
         SPACE 3                                                                
         USING TLSTD,R2                                                         
         USING TLSTPD,R3                                                        
*                                  STAFF NAME                                   
PASSSTN  NTR1                                                                   
         MVI   ELCODE,TASTELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TASTD,R6                                                         
         MVC   TLSTNLST,TASTLST                                                 
         MVC   TLSTNFST,TASTFST                                                 
         MVC   TLSTNSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  STAFF CLIENT GROUP                           
PASSSTC  NTR1                                                                   
         MVC   TLSTCUSR,TLSTUSER   USER ID NUMBER                               
         MVC   TLSTCSTA,TLSTSTAF   STAFF ID                                     
         MVC   TLSTCSEQ,TLSTSSEQ   SEQUENCE NUMBER                              
         SPACE 1                                                                
         MVI   ELCODE,TASTELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TASTD,R6                                                         
         OC    TASTCLG,TASTCLG                                                  
         BZ    NO                                                               
         MVC   TLSTCCGP,TASTCLG    CLIENT GROUP                                 
         B     YES                                                              
         EJECT                                                                  
*              AGENCY AND AGENCY GROUP                                          
         SPACE 3                                                                
         USING TLAYD,R2                                                         
         USING TLAYPD,R3                                                        
*                                  AGENCY NAME                                  
PASSAYN  NTR1                                                                   
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLAYNAME,WORK                                                    
         MVC   TLAYNSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  AGENCY GROUP FOR AGENCY                      
PASSAYG  NTR1                                                                   
         MVI   ELCODE,TAAYELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAAYD,R6                                                         
         MVC   TLAYGAGG,TAAYAGG                                                 
         MVC   TLAYGAGY,TLAYAGY                                                 
         B     YES                                                              
         SPACE 3                                                                
         USING TLAGD,R2                                                         
         USING TLAGPD,R3                                                        
*                                  AGENCY GROUP NAME                            
PASSAGN  NTR1                                                                   
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLAGNAME,WORK                                                    
         MVC   TLAGNSEQ,RDSKADD                                                 
         B     YES                                                              
         EJECT                                                                  
         USING TLAYD,R2                                                         
         USING TLBRPD,R3                                                        
*                                  AGENCY BRATE BY NAME                         
PASSAYB  NTR1                                                                   
         MVI   ELCODE,TARAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLBRNNAM,WORK                                                    
         MVC   TLBRNCOD,TLAYAGY                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  AGENCY BRATE BY CODE                         
PASSAYC  NTR1                                                                   
         MVI   ELCODE,TARAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLBRCAGY,TLAYAGY                                                 
         B     YES                                                              
         SPACE 1                                                                
*              CLIENT AND CLIENT GROUP                                          
         SPACE 3                                                                
         USING TLCLD,R2                                                         
         USING TLCLPD,R3                                                        
*                                  CLIENT NAME                                  
PASSCLN  NTR1                                                                   
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLCLNAME,WORK                                                    
         MVC   TLCLNAGY,TLCLAGY                                                 
         MVC   TLCLNSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  CLIENT GROUP                                 
PASSCLG  NTR1                                                                   
         MVC   TLCLGCLI,TLCLCLI                                                 
         MVC   TLCLGAGY,TLCLAGY                                                 
         SPACE 1                                                                
         MVI   ELCODE,TACIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TACID,R6                                                         
         CLI   TACICLG,C'A'                                                     
         BL    YES                                                              
         MVC   TLCLGCLG,TACICLG                                                 
         B     YES                                                              
         SPACE 3                                                                
         USING TLCGD,R2                                                         
         USING TLCGPD,R3                                                        
*                                  CLIENT GROUP NAME                            
PASSCGN  NTR1                                                                   
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLCGNAME,WORK                                                    
         MVC   TLCGNSEQ,RDSKADD                                                 
         B     YES                                                              
         EJECT                                                                  
         USING TLCLD,R2                                                         
         USING TLBRPD,R3                                                        
*                                  CLIENT BRATE BY NAME                         
PASSCLB  NTR1                                                                   
         MVI   ELCODE,TARAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLBRNAGY,HEXFFS                                                  
         OC    TLCLAGY,TLCLAGY                                                  
         BZ    *+10                                                             
         MVC   TLBRNAGY,TLCLAGY                                                 
         MVC   TLBRNNAM,WORK                                                    
         MVC   TLBRNCOD,TLCLCLI                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  CLIENT BRATE BY CODE                         
PASSCLC  NTR1                                                                   
         MVI   ELCODE,TARAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLBRCAGY,HEXFFS                                                  
         OC    TLCLAGY,TLCLAGY                                                  
         BZ    *+10                                                             
         MVC   TLBRCAGY,TLCLAGY                                                 
         MVC   TLBRCCLI,TLCLCLI                                                 
         B     YES                                                              
         SPACE 1                                                                
*              PRODUCT AND PRODUCT GROUP                                        
         SPACE 3                                                                
         USING TLPRD,R2                                                         
         USING TLPRPD,R3                                                        
*                                  PRODUCT NAME                                 
PASSPRN  NTR1                                                                   
         MVC   TLPRNCLI,TLPRCLI                                                 
         MVC   TLPRNAGY,TLPRAGY                                                 
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLPRNAME,WORK                                                    
         MVC   TLPRNSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  PRODUCT GROUP                                
PASSPRG  NTR1                                                                   
         MVI   ELCODE,TAPIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPID,R6                                                         
         OC    TAPIPRG,TAPIPRG     SKIP IF NO PRODUCT GROUP                     
         BZ    NO                                                               
         MVC   TLPRGPRG,TAPIPRG                                                 
         MVC   TLPRGAGY,TLPRAGY                                                 
         MVC   TLPRGCLI,TLPRCLI                                                 
         MVC   TLPRGPRD,TLPRPRD                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  PRODUCT TYPE                                 
PASSPRT  NTR1                                                                   
         MVI   ELCODE,TAPIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPID,R6                                                         
         CLI   TAPILEN,TAPILNQ2    ELEMENT MUST BE NEW LENGTH                   
         BNE   NO                                                               
         OC    TAPIPTYP,TAPIPTYP   SKIP IF NO PRODUCT TYPE                      
         BZ    NO                                                               
         MVC   TLPRTPRT,TAPIPTYP                                                
         MVC   TLPRTAGY,TLPRAGY                                                 
         MVC   TLPRTCLI,TLPRCLI                                                 
         MVC   TLPRTPRD,TLPRPRD                                                 
         B     YES                                                              
         EJECT                                                                  
*                                                                               
         USING TLPGD,R2                                                         
         USING TLPGPD,R3                                                        
*                                  PRODUCT GROUP NAME                           
PASSPGN  NTR1                                                                   
         MVC   TLPGNAGY,TLPGAGY                                                 
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLPGNAME,WORK                                                    
         MVC   TLPGNSEQ,RDSKADD                                                 
         B     YES                                                              
         EJECT                                                                  
*              COMMERCIAL                                                       
         SPACE 2                                                                
         USING TLCOD,R2                                                         
         USING TLCOPD,R3                                                        
*                                  COMMERCIAL NAME                              
PASSCON  NTR1                                                                   
         MVC   TLCONCLI,TLCOCLI                                                 
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLCONAME,WORK                                                    
         MVC   TLCONCOM,TLCOCOM                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  COMMERCIAL (AGENCY/CID)                      
PASSCOI  NTR1                                                                   
         MVC   TLCOIAGY,TLCOAGY                                                 
         MVC   TLCOICOM,TLCOCOM                                                 
         SPACE 1                                                                
         LR    R6,R2                                                            
         SPACE 1                                                                
         USING TAVRD,R6                                                         
         CLI   TLCOVER,TLCOV026    IF RECORD ONLY SAVES VERSIONS                
         BE    PASSCOI2                                                         
         MVI   ELCODE,TAVRELQ      RECORD MUST CONTAIN VERSIONS                 
         BAS   RE,GETEL            IN ORDER TO GENERATE POINTER                 
         BE    PASSCOI4                                                         
         B     NO                                                               
         SPACE 1                                                                
         USING TACOD,R6                                                         
PASSCOI2 MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLCOICID,TACOCID                                                 
         SPACE 1                                                                
         USING TAVRD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAVRELQ      SEE IF THERE ARE VERSIONS                    
         BAS   RE,GETEL                                                         
         B     PASSCOI6                                                         
PASSCOI4 MVC   TLCOICID,TAVRCID    VERSION ID                                   
         MVC   TLCOIVER,TAVRVERS   VERSION CODE                                 
         BAS   RE,NEXTEL                                                        
PASSCOI6 BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOICDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOI4                                                         
         SPACE 1                                                                
*                                  COMMERCIAL SEQ NO                            
PASSCOC  NTR1                                                                   
         CLI   TLCOVER,TLCOV026                                                 
         BNE   NO                                                               
         MVC   TLCOCCOM,TLCOCOM                                                 
         B     YES                                                              
         SPACE 1                                                                
*                                  COMMERCIAL (MUSIC)                           
PASSCOM  NTR1                                                                   
         MVC   TLCOMAGY,TLCOAGY                                                 
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         MVC   TLCOMCID,TACOCID                                                 
         LR    R6,R2                                                            
         MVI   ELCODE,TACPELQ      GET COMML PUB. MUSIC EL.                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACPD,R6                                                         
PASSCOM4 MVC   TLCOMMUS,TACPMUS    SET MUSIC CODE                               
         SPACE 1                                                                
         BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOMCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOM4                                                         
         SPACE 1                                                                
*                                  PRINT COMMERCIAL                             
PASSCOP  NTR1                                                                   
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLI   TACOMED,TACOMEDP    MUST BE MEDIA PRINT                          
         BNE   NO                                                               
         MVC   TLCOPAGY,TLCOAGY                                                 
         MVC   TLCOPCLI,TLCOCLI                                                 
         MVC   TLCOPCID,TLCOCID                                                 
         MVC   TLCOPCOM,TLCOCOM                                                 
         LR    R6,R2                                                            
         MVI   ELCODE,TAPRELQ      GET PRODUCT ELEMENTS                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPRD,R6                                                         
PASSCOP4 MVC   TLCOPPRD,TAPRPRD    SET PRODUCT CODE                             
         SPACE 1                                                                
         BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOPCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOP4                                                         
         SPACE 1                                                                
*                                  COMMERCIAL (AFM CONTRACTS)                   
PASSCOA  NTR1                                                                   
         TM    TLCOSTAT,X'40'      IGNORE COPIED FROM COMMERCIALS               
         BO    NO                                                               
                                                                                
         USING TAOCD,R6                                                         
         MVI   ELCODE,TAOCELQ      GET OLD AGENCY/CID ELEMENT                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
                                                                                
PASSCOA1 BAS   RE,NEXTEL                                                        
         BNE   PASSCOA2                                                         
         TM    TAOCSTAT,TAOCSTO    IGNORE IF 'TO' BIT ON IN ANY EL              
         BO    NO                                                               
         B     PASSCOA1                                                         
         DROP  R6                                                               
                                                                                
PASSCOA2 MVC   TLCOAAGY,TLCOAGY    SET AGENCY CODE                              
                                                                                
         USING TACOD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLCOACID,TACOCID    SET COMMERCIAL ID                            
         DROP  R6                                                               
                                                                                
         USING TAMCD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAMCELQ      GET MUSIC CONTRACT DETAILS ELEMENT           
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
                                                                                
         MVC   WORKEXT(L'TAMCCON),TAMCCON  INITIALIZE LIST OF                   
         MVI   WORKEXT+L'TAMCCON,X'FF'     ENCOUNTERED MUSIC CONTRACTS          
         TM    TAMCSTAT,TAMCSNEW                                                
         JZ    PASSCOA3                                                         
         MVC   WORKEXT(L'TAMCCON),TLCOACID                                      
                                                                                
PASSCOA3 MVC   TLCOACON,TAMCCON    SET CONTRACT NUMBER                          
         TM    TAMCSTAT,TAMCSNEW                                                
         JZ    PASSCOA4                                                         
         MVC   TLCOACON,TLCOACID                                                
PASSCOA4 BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
                                                                                
         LA    RE,WORKEXT                                                       
                                                                                
         LA    RF,TAMCCON                                                       
         TM    TAMCSTAT,TAMCSNEW                                                
         JZ    PASSCOA5                                                         
         LA    RF,TLCOACID                                                      
                                                                                
PASSCOA5 CLI   0(RE),X'FF'                                                      
         BE    PASSCOA6                                                         
         CLC   0(L'TAMCCON,RF),0(RE) IF MUSIC CONTRACT ALREADY                  
         BE    PASSCOA4              ENCOUNTERED, DON'T ADD AGAIN               
         LA    RE,L'TAMCCON(RE)                                                 
         B     PASSCOA5                                                         
                                                                                
PASSCOA6 MVC   0(L'TAMCCON,RE),0(RF)                                            
         MVI   L'TAMCCON(RE),X'FF'                                              
         DROP  R6                                                               
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOACDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE KEY                                     
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOA3                                                         
         EJECT                                                                  
*                                  COMMERCIAL (AKA CIDS)                        
PASSCOK  NTR1                                                                   
         TM    TLCOSTAT,X'40'      IGNORE COPIED FROM COMMERCIALS               
         BO    NO                                                               
         MVC   TLCOKAGY,TLCOAGY    AGENCY                                       
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
*                                  EXCLUDE LOCKED OR RELEASED                   
         TM    TACOSTAT,TACOSTLO+TACOSTRL                                       
         BNZ   NO                                                               
         MVC   TLCOKCID,TACOCID    TALENT CID                                   
*                                                                               
         LR    R6,R2               LOOK FOR LIFT DETAILS ELEMENT                
         MVI   ELCODE,TALFELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TALFD,R6                                                         
         MVC   TLCOKAKA,TALFLID    AKA CID (FROM LIFT ID)                       
         B     YES                                                              
         SPACE 2                                                                
*                                  COMMERCIAL ID CLIENT GROUP                   
PASSCOG  NTR1                                                                   
         TM    TLCOSTAT,X'40'      IGNORE COPIED FROM COMMERCIALS               
         BO    NO                                                               
         MVC   TLCOGAGY,TLCOAGY    AGENCY                                       
         MVC   TLCOGCOM,TLCOCOM    INTERNAL COMMERCIAL NUMBER                   
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
*                                                                               
         CLI   TACOMED,TACOMEDP    EXCLUDE PRINT COMMERCIALS                    
         BE    NO                                                               
         OC    TACOCLG,TACOCLG     IF CLIENT GROUP                              
         BZ    NO                                                               
         MVC   TLCOGCLG,TACOCLG    CLIENT GROUP                                 
         MVC   TLCOGCID,TACOCID    TALENT CID                                   
         B     YES                                                              
*                                  COMMERCIAL NAME CLIENT GROUP                 
PASSCOL  NTR1                                                                   
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         SPACE 1                                                                
         CLI   TACOMED,TACOMEDP    EXCLUDE PRINT COMMERCIALS                    
         BE    NO                                                               
         OC    TACOCLG,TACOCLG     IF CLIENT GROUP                              
         BZ    NO                                                               
         MVC   TLCOLCLG,TACOCLG    CLIENT GROUP                                 
         MVC   TLCOLCOM,TLCOCOM    INTERNAL COMMERCIAL NUMBER                   
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLCOLNAM,WORK                                                    
         B     YES                                                              
         EJECT                                                                  
*                                  COMMERCIAL (VERSION LETTER)                  
PASSCOV  NTR1                                                                   
         MVC   TLCOVAGY,TLCOAGY    AGENCY                                       
         MVC   TLCOVCLI,TLCOCLI    CLIENT                                       
         MVC   TLCOVPRD,TLCOPRD    PRODUCT                                      
         MVC   TLCOVCID,TLCOCID    COMM ID                                      
         MVC   TLCOVCOM,TLCOCOM    INTERNAL COMMERCIAL NUMBER                   
         SPACE 1                                                                
         USING TAVRD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAVRELQ      SEE IF THERE ARE VERSIONS                    
         SPACE 1                                                                
         CLI   TLCOVER,TLCOV026    FOR RECORDS WITH VERSIONS 27                 
         BE    PASSCOV3            AND GREATER, ONLY ADD A KEY                  
         BAS   RE,GETEL            FOR EACH VERSION                             
         BE    PASSCOV4                                                         
         B     NO                                                               
         SPACE 1                                                                
PASSCOV3 BAS   RE,GETEL                                                         
         B     PASSCOV6                                                         
PASSCOV4 MVC   TLCOVVER,TAVRVERS   VERSION CODE                                 
         BAS   RE,NEXTEL                                                        
PASSCOV6 BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOVRDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOV4                                                         
         EJECT                                                                  
*                                  COMMERCIAL ID COMM'L GROUP                   
PASSCGI  NTR1                                                                   
         TM    TLCOSTAT,X'40'      IGNORE COPIED FROM COMMERCIALS               
         BO    NO                                                               
         MVC   TLCOGIAY,TLCOAGY    AGENCY                                       
         MVC   TLCOGIIN,TLCOCOM    INTERNAL COMMERCIAL NUMBER                   
*                                                                               
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLI   TACOLEN,TACOLNQ     EXCLUDE OLD STYLE ELEMENTS                   
         BE    NO                                                               
         CLI   TACOMED,TACOMEDP    EXCLUDE PRINT COMMERCIALS                    
         BE    NO                                                               
         OC    TACOCGRP,TACOCGRP   IF COMMERCIAL GROUP                          
         BZ    NO                                                               
         MVC   TLCOGICG,TACOCGRP   COMMERCIAL GROUP                             
         MVC   TLCOGIID,TACOCID    TALENT CID                                   
         B     YES                                                              
*                                  COMMERCIAL NAME CLIENT GROUP                 
PASSCGA  NTR1                                                                   
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLI   TACOLEN,TACOLNQ     EXCLUDE OLD STYLE ELEMENTS                   
         BE    NO                                                               
         CLI   TACOMED,TACOMEDP    EXCLUDE PRINT COMMERCIALS                    
         BE    NO                                                               
         OC    TACOCGRP,TACOCGRP   IF COMMERCIAL GROUP                          
         BZ    NO                                                               
         MVC   TLCOGNCG,TACOCGRP   COMMERCIAL GROUP                             
         MVC   TLCOLNIN,TLCOCOM    INTERNAL COMMERCIAL NUMBER                   
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLCOLNAM,WORK                                                    
         B     YES                                                              
*                                  COMMERCIAL NAME BREAKDOWN                    
PASSCOB  NTR1                                                                   
         USING TLCOD,R2                                                         
         USING TLCOPD,R3                                                        
                                                                                
         USING TACOD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
                                                                                
         MVI   TLCOPCD,TLCOBCDQ    ALWAYS CREATE PASSIVE WITH                   
         MVC   TLCOBNAM,SPACES     COMMERCIAL ID                                
         MVC   TLCOBNAM(L'TACOCID),TACOCID                                      
         OI    TLCOBSTA,TLCOBSID                                                
         MVC   TLCOBCOM,TLCOCOM                                                 
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
******** MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
                                                                                
         BAS   RE,GETNAME          GET COMMERCIAL NAME INTO WORK                
         BNE   NO                                                               
         MVI   BRKBYTE,C'A'                                                     
         MVI   TLCOBSTA,0                                                       
                                                                                
PCOB10   MVC   TLCOBNAM,WORK       BUILD NEW KEY                                
         MVC   TLCOBCOM,TLCOCOM                                                 
                                                                                
PCOB20   CLC   WORK,SPACES         DONE IF ALL WORDS IN NAME HAVE               
         BE    YES                 BEEN PROCESSED                               
                                                                                
         CLI   WORK,C' '           ELSE TRIM OFF CURRENT WORD ...               
         BE    PCOB30                                                           
         CLI   BRKBYTE,C' '                                                     
         BE    PCOB40                                                           
PCOB30   MVC   BRKBYTE,WORK                                                     
         MVC   WORK,WORK+1                                                      
         MVI   WORK+L'WORK-1,C' '                                               
         B     PCOB20                                                           
                                                                                
PCOB40   MVC   BRKBYTE,WORK                                                     
         MVI   TLCOPCD,TLCOBCDQ    ... AND ADD THIS POINTER TO STACK            
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PCOB10                                                           
         SPACE 1                                                                
*                                  COMMERCIAL SEQ NO                            
*                                  CAST HOLDING FEE FROM COMMERCIAL             
PASSCOAH NTR1                                                                   
         USING TLCAPD,R3                                                        
         MVC   TLCAHAGY,TLCOAGY    AGENCY                                       
         MVC   TLCAHCOM,TLCOCOM    COMMERCIAL                                   
         MVC   TLCAHCLI,TLCOCLI    CLIENT                                       
         MVC   TLCAHPRD,TLCOPRD    PRODUCT                                      
         MVI   ELCODE,TACOELQ      GET COMMERCIAL DETAILS EL.                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLI   TACOLEN,TACOLNQ2    DO NOT GENERATE FOR COMMERCIALS              
         BL    PCOAH1              WITH "DO NOT GENERATE HOLDING                
         TM    TACOSTA3,TACOSNHF   FEE NOTICES" STATUS ON                       
         BO    NO                                                               
                                                                                
PCOAH1   CLI   TACOMED,TACOMEDT    ALWAYS GENERATE FOR TV                       
         BE    PCOAH4                                                           
         CLI   TACOMED,TACOMEDI    INTERNET                                     
         BE    PCOAH4                                                           
         CLI   TACOMED,TACOMEDN    AND NEW MEDIA COMMERCIALS                    
         BE    PCOAH4                                                           
                                                                                
         CLI   TACOMED,TACOMEDC    ALSO GENERATE FOR CABLE COMMERCIALS          
         BNE   NO                  WITH A FIRST FIXED CYCLE DATE OF             
         CLC   TACOFCYC,=X'A90401' APR01/09 OR LATER                            
         BL    NO                                                               
*                                  EXCLUDE LOCKED, RELEASED, CAN RATES          
PCOAH4   TM    TACOSTAT,TACOSTLO+TACOSTRL+TACOSCRT                              
         BNZ   NO                                                               
*                                                                               
         CLI   TACOLEN,TACOLNQ2                                                 
         BL    PCOAH4A                                                          
         TM    TACOSTA3,TACOSSMW   EXCLUDE SOCIAL MEDIA WAIVER                  
         BO    NO                                                               
*                                                                               
PCOAH4A  LA    RE,NAHLDTAB         RE=A(COMM'L TYPE TAB GET NO HFN)             
PCOAH5   CLI   0(RE),0             TEST END OF TABLE                            
         BE    PCOAH20                                                          
         CLC   TACOTYPE,0(RE)      TEST MATCH ON COMMERCIAL TYPE                
         BE    NO                  YES - EXCLUDE IT                             
         LA    RE,1(RE)            NO-BUMP TO NEXT COMML TYPE IN TAB            
         B     PCOAH5                                                           
PCOAH20  MVC   TLCAHDTE,TACOFCYC   SET COMMERCIAL 1ST FIXED CYCLE DATE          
*                                                                               
         CLI   TACOTYPE,CTYSEAS2   IF SEASONAL COMMERCIAL                       
         BNE   YES                                                              
         MVC   TLCAHDTE,TACOAIR    SET COMMERCIAL 1ST AIR DATE                  
         OC    TACOSAIR,TACOSAIR   OR, IF PRESENT, 2ND 1ST AIR DATE             
         BZ    PCOAH30                                                          
         MVC   TLCAHDTE,TACOSAIR                                                
PCOAH30  OC    TLCAHDTE,TLCAHDTE   IF NEITHER PRESENT                           
         BNZ   YES                 DO NOT GENERATE POINTER                      
         B     NO                                                               
*                                  COMMERCIAL/VERSION (MUSIC TRACK)             
PASSCOT  NTR1                                                                   
         USING TLCOD,R2                                                         
         USING TLCOPD,R3                                                        
         CLI   0(R2),TLCOCDQ       IF COMMERCIAL RECORD                         
         BNE   PASSCOT1                                                         
         MVC   TLCOTCOM,TLCOCOM    SET INTERNAL COMMERCIAL NUMBER               
         LR    R6,R2                                                            
         MVI   ELCODE,TAVRELQ      IF COMMERCIAL HAS VERSIONS                   
         BAS   RE,GETEL                                                         
         BNE   PASSCOT2                                                         
         MVI   TLCOTVER,1          SET VERSION 1                                
         B     PASSCOT2                                                         
         DROP  R2                                                               
         SPACE 1                                                                
         USING TLVRD,R2                                                         
PASSCOT1 MVC   TLCOTCOM,TLVRCOM    IF VERSION RECORD, SET INTERNAL              
         MVC   TLCOTVER,TLVRVER    COMMERCIAL NUMBER AND VERSION                
         DROP  R2                                                               
         SPACE 1                                                                
         USING TATRD,R6                                                         
PASSCOT2 LR    R6,R2                                                            
         MVI   ELCODE,TATRELQ      SEE IF THERE ARE MUSIC CONTRACT/             
         BAS   RE,GETEL            TRACK ELEMENTS                               
         BNE   NO                                                               
PASSCOT3 MVC   TLCOTMCO,TATRCOM    TYPE M INTERNAL COMMERCIAL NUMBER            
         MVC   TLCOTTRK,TATRTRK    TRACK                                        
         BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOTCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOT3                                                         
*                                  COMMERCIAL (WEB APP RECORD ID)               
PASSCOW  NTR1                                                                   
         MVI   ELCODE,TAFNELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PASSCOW2 BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TAFND,R6                                                         
         CLI   TAFNTYPE,TAFNTWRI                                                
         BNE   PASSCOW2                                                         
         ZIC   RF,TAFNLEN                                                       
         SHI   RF,4                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TLCOWWID(0),TAFNNAME                                             
         OC    TLCOWWID,SPACES                                                  
         DROP  R6                                                               
         SPACE 1                                                                
         USING TLCOD,R2                                                         
         MVC   TLCOWCOM,TLCOCOM                                                 
         B     YES                                                              
         DROP  R2                                                               
*                                  COMMERCIAL (MUSIC W/O AGENCY)                
         USING TLCOD,R2                                                         
PASSCOO  NTR1                                                                   
         MVC   TLCOOCOM,TLCOCOM                                                 
         B     PASSCOO2                                                         
         DROP  R2                                                               
         SPACE 1                                                                
         USING TLVRD,R2                                                         
PASSVOO  NTR1                                                                   
         MVC   TLCOOCOM,TLVRCOM                                                 
         MVC   TLCOOVER,TLVRVER                                                 
         DROP  R2                                                               
         SPACE 1                                                                
PASSCOO2 LR    R6,R2                                                            
         MVI   ELCODE,TACPELQ      GET COMML PUB. MUSIC EL.                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACPD,R6                                                         
PASSCOO4 MVC   TLCOOMUS,TACPMUS    SET MUSIC CODE                               
         DROP  R6                                                               
         SPACE 1                                                                
         BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCOPCD,TLCOOCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCOPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCOO4                                                         
*                                  ACTIVE COMMERCIALS                           
PASSCOE  NTR1                                                                   
         BRAS  RE,PCOE                                                          
         B     XIT                                                              
         EJECT                                                                  
*              AGENT NAME & AGENT NUMBER & AGENT NAME BREAKDOWN                 
         SPACE 3                                                                
*                                  AGENT NAME                                   
PASSANN  NTR1                                                                   
         USING TLAND,R2                                                         
         USING TLANPD,R3                                                        
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLANNAME,WORK                                                    
         MVC   TLANNSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  AGENT NUMBER (T & R)                         
PASSANC  NTR1                                                                   
         USING TLAND,R2                                                         
         USING TLANPD,R3                                                        
*                                                                               
         MVI   ELCODE,TAANELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PASSANC1                                                         
         USING TAAND,R6                                                         
         CLC   TAANTNR,SPACES                                                   
         BNH   PASSANC1                                                         
         CLC   TAANTNR,TLANAGT     T&R NUMBER = DPS NUMBER                      
         BE    PASSANC1                                                         
         MVI   0(R3),TLANCCDQ                                                   
         MVC   TLANCAGT,TAANTNR                                                 
*                                                                               
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3           ANOTHER POINTER SO BUMP STACK                
*                                                                               
PASSANC1 MVI   0(R3),TLANCCDQ                                                   
         MVC   TLANCAGT,TLANAGT                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  AGENT NAME BREAKDOWN                         
PASSANB  NTR1                                                                   
         USING TLAND,R2                                                         
         USING TLANPD,R3                                                        
         MVI   TLANPCD,TLANBCDQ    ALWAYS CREATE PASSIVE WITH                   
         MVC   TLANBNAM,SPACES     AGENT CODE                                   
         MVC   TLANBNAM(L'TLANAGT),TLANAGT                                      
         MVC   TLANBAGT,TLANAGT                                                 
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLANPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLANPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
                                                                                
         BAS   RE,GETNAME          GET AGENT NAME INTO WORK                     
         BNE   NO                                                               
         MVI   BRKBYTE,C'A'                                                     
                                                                                
PANB10   MVC   TLANBNAM,WORK       BUILD NEW KEY                                
         MVC   TLANBAGT,TLANAGT                                                 
         MVC   TLANBELQ,ELCODE                                                  
                                                                                
PANB20   CLC   WORK,SPACES         DONE IF ALL WORDS IN NAME HAVE               
         BE    PANB50              BEEN PROCESSED                               
                                                                                
         CLI   WORK,C' '           ELSE TRIM OFF CURRENT WORD ...               
         BE    PANB30                                                           
         CLI   BRKBYTE,C' '                                                     
         BE    PANB40                                                           
PANB30   MVC   BRKBYTE,WORK                                                     
         MVC   WORK,WORK+1                                                      
         MVI   WORK+L'WORK-1,C' '                                               
         B     PANB20                                                           
                                                                                
PANB40   MVC   BRKBYTE,WORK                                                     
         MVI   TLANPCD,TLANBCDQ    ... AND ADD THIS POINTER TO STACK            
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLANPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLANPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PANB10                                                           
                                                                                
PANB50   CLI   ELCODE,TASNELQ      DONE IF SHORT NAME HAS ALREADY               
         BE    YES                 BEEN PROCESSED                               
                                                                                
         USING TASND,R6                                                         
         LR    R6,R2               DOES AGENT HAVE SHORT NAME?                  
         MVI   ELCODE,TASNELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         MVC   WORK,SPACES         GET SHORT NAME INTO WORK                     
         MVC   WORK(16),TASNAME                                                 
         DROP  R6                                                               
                                                                                
         MVI   TLANPCD,TLANBCDQ    ADD LAST POINTER TO STACK                    
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLANPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLANPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
                                                                                
         MVI   BRKBYTE,C'A'        GO PROCESS SHORT NAME                        
         B     PANB10                                                           
         SPACE 3                                                                
*                                  AGENT SS NUMBER                              
PASSANS  NTR1                                                                   
         USING TLAND,R2                                                         
         USING TLANPD,R3                                                        
                                                                                
         MVI   ELCODE,TAANELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAAND,R6                                                         
         OC    TAANSSN,TAANSSN                                                  
         BZ    NO                                                               
         MVC   TLANSSSN,TAANSSN                                                 
         DROP  R6                                                               
                                                                                
         MVC   TLANSAGT,TLANAGT                                                 
         B     YES                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
*              W4 RECORDS                                                       
         SPACE 2                                                                
         USING TLW4D,R2                                                         
         USING TLW4PD,R3                                                        
*                                  W4 NAME                                      
PASSW4N  NTR1                                                                   
         MVI   ELCODE,TAW4ELQ                                                   
         MVC   TLW4NLST(10),=C'MISSING W4'                                      
         MVC   TLW4NSEQ,RDSKADD                                                 
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TAW4D,R6                                                         
         MVC   TLW4NLST,TAW4NAM2                                                
         MVC   TLW4NFST,TAW4NAM1                                                
         MVC   TLW4NSEQ,RDSKADD                                                 
         SPACE 1                                                                
         MVI   ELCODE,TAAKELQ      LOOK FOR AKA NAME                            
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLW4PCD,TLW4NCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         USING TAAKD,R6                                                         
         MVC   TLW4NLST,TAAKNAM2                                                
         MVC   TLW4NFST,TAAKNAM1                                                
         OI    TLW4NSTA,TLW4NSAK   INDICATE AKA NAME                            
         MVC   TLW4NSEQ,RDSKADD                                                 
         B     YES                                                              
         SPACE 2                                                                
*                                  CORPORATION SS#                              
PASSW4C  NTR1                                                                   
         XC    LASTKEY,LASTKEY                                                  
         SPACE 1                                                                
         USING TAW4D,R6                                                         
         MVI   ELCODE,TAW4ELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PASSW4C1                                                         
         MVC   SVW4TYPE,TAW4TYPE                                                
         DROP  R6                                                               
         SPACE 1                                                                
PASSW4C1 LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         SPACE 1                                                                
         USING TATID,R6                                                         
PASSW4C2 MVI   0(R3),TLW4CCDQ                                                   
         MVC   TLW4CCRP,TATIID                                                  
         MVC   TLW4CSSN,TLW4SSN                                                 
         MVC   TLW4CTYP,SVW4TYPE                                                
         BAS   RE,NEXTEL                                                        
         BNE   YES                 (I DO MEAN YES!)                             
         CLC   LASTKEY,TLW4PKEY    PROTECT AGAINST SAME CORP                    
         BE    PASSW4C2                                                         
         MVC   LASTKEY,TLW4PKEY                                                 
         SPACE 1                                                                
*                                  ANOTHER POINTER SO BUMP STACK                
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         B     PASSW4C2                                                         
         SPACE 2                                                                
*                                  OLD EMP/SSN PASSIVE                          
PASSW4O  NTR1                                                                   
         XC    LASTKEY,LASTKEY                                                  
         MVI   ELCODE,TACVELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACVD,R6                                                         
         MVC   TLW4OEMP,TACVDATA                                                
         MVC   TLW4OSSN,TACVDATA+3                                              
         B     YES                                                              
         SPACE 2                                                                
*                                  LLC OR LLP                                   
PASSW4L  NTR1                                                                   
         BRAS  RE,PASSW4LX                                                      
         B     XIT                                                              
         EJECT                                                                  
*              LIEN RECORDS                                                     
         SPACE 3                                                                
         USING TLLND,R2                                                         
         USING TLLNPD,R3                                                        
*                                  LIEN RANK                                    
PASSLNR  NTR1                                                                   
         MVI   ELCODE,TALNELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TALND,R6                                                         
         MVC   TLLNRSSN,TLLNSSN                                                 
         MVC   TLLNRRNK,TALNRNK                                                 
         MVC   TLLNRLIN,TLLNLIN                                                 
         B     YES                                                              
         EJECT                                                                  
*              CAST RECORDS                                                     
         SPACE 3                                                                
         USING TLCAD,R2                                                         
         USING TLCAPD,R3                                                        
*                                  EMPLOYEE'S COMMERCIALS                       
PASSCAC  NTR1                                                                   
         MVC   TLCACSSN,TLCASSN                                                 
         MVC   TLCACCOM,TLCACOM                                                 
         MVC   TLCACCAT,TLCACAT                                                 
         MVC   TLCACSEQ,TLCASEQ                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  AGENT                                        
PASSCAA  NTR1                                                                   
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLCAASSN,TLCASSN                                                 
         MVC   TLCAACOM,TLCACOM                                                 
         MVC   TLCAACAT,TLCACAT                                                 
         MVC   TLCAASEQ,TLCASEQ                                                 
         USING TACAD,R6                                                         
         OC    TACANCDE,TACANCDE   CONVERT AGENT NUMBER TO EBCDIC               
         BZ    NO                                                               
         GOTOR TRNSAGT,DMCB,(X'40',TACANCDE),TLCAAAGT                           
         B     YES                                                              
         SPACE 3                                                                
*                                  CAST GUARANTEES                              
PASSCAG  NTR1                                                                   
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         CLI   TACALEN,TACAGUA+L'TACAGUA-TACAD                                  
         BL    NO                                                               
         OC    TACAGUA,TACAGUA                                                  
         BZ    NO                                                               
         USING TACAD,R6                                                         
         MVC   TLCAGSSN,TLCASSN                                                 
         MVC   TLCAGGUA,TACAGUA                                                 
         MVC   TLCAGCOM,TLCACOM                                                 
         MVC   TLCAGCAT,TLCACAT                                                 
         MVC   TLCAGSEQ,TLCASEQ                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  CAST HOLDING FEE                             
PASSCAH  NTR1                                                                   
         MVI   ELCODE,TACAELQ      GET CAST DETAILS EL.                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACAD,R6            R6=A(CAST DETAILS EL.)                       
*11/22/96OC    TACALAST,TACALAST                                                
****     BNZ   NO            ****  REJECT IF LAST SERVICE DATE PRESENT          
         TM    TACASTA3,TACASEQY   REJECT IF EQUITY PERFORMER                   
         BO    NO                                                               
         SPACE 1                                                                
         L     R4,ACATTAB          R4=A(CATEGORY TABLE)                         
         USING CATTABD,R4                                                       
PASSCA02 CLI   0(R4),X'FF'         IF END OF TABLE THEN RETURN NEQ              
         BE    NO                                                               
         CLC   CATCDE,TLCACAT      MATCH ON CATEGORY CODE                       
         BE    PASSCA04                                                         
         ZIC   RE,CATLEN           BUMP TO NEXT CATEGORY                        
         AR    R4,RE                                                            
         B     PASSCA02                                                         
         SPACE 1                                                                
PASSCA04 CLI   CATUNI,0            EXCLUDE IF NO ALLOWABLE UNIONS               
         BE    NO                                                               
         TM    CATUNI,AFM          EXCLUDE IF ALLOWABLE UNION IS AFM            
         BO    NO                                                               
         TM    CATTYPE,NOHLD       TEST CATEGORY EXCLUDED FROM HLD              
         BO    NO                                                               
         CLC   TACAONOF,=C'OFF'    IF OFF-CAMERA PERF.                          
         BNE   PASSCA10                                                         
         TM    CATTYPE,NOHLDOFF    TEST OFF-CAM EXCLUSION BIT                   
         BO    NO                                                               
*                                                                               
         L     RE,AYRSTAB                                                       
         USING YRTABD,RE                                                        
PASSCA06 CLC   TACAYEAR(2),YRCDE   FIND IN CONTRACT YEAR TABLE                  
         BE    PASSCA08                                                         
         LA    RE,YRLNQ(RE)                                                     
         B     PASSCA06                                                         
*                                                                               
PASSCA08 CLI   YREQU,CN88          AS OF '88 CONTRACT                           
         BL    PASSCA10                                                         
         TM    CATTYPE,NHLDOF88    HAVE ANOTHER EXCLUSION BIT                   
         BO    NO                                                               
*                                                                               
PASSCA10 MVC   TLCAHFFC,TACAFCYC   SET CAST FIRST FIXED CYCLE DATE              
         MVC   TLCAHDTE,TACAFCYC   SET HERE IN CASE NO HLDS PAID YET            
         SPACE 1                                                                
         MVI   ELCODE,TAHFELQ      GET HOLDING FEE NOTIFICATION EL.             
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   PASSCA12                                                         
         USING TAHFD,R6                                                         
         MVC   TLCAHNXT,TAHFNXTS   SET NEXT CYCLE START                         
         MVC   TLCAHMRK,TAHFMDTE   AND DATE MARKED                              
         SPACE 1                                                                
         USING TACRD,R4                                                         
PASSCA12 MVI   ELCODE,TACRELQ      SCAN APPLIED CREDIT HISTORY ELS.             
         XR    R1,R1               CLEAR R1 IN CASE NO HLDS PAID YET            
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   PASSCA20                                                         
PASSCA14 LR    R4,R6               SAVE A(LAST ONE)                             
         TM    TACRSTAT,TACRSSEA   IGNORE PAYMENTS TO SEASONALS                 
         BO    PASSCA20                                                         
         CLC   TACRUSE,=C'HLD'     IF IT'S A HLD,                               
         BE    PASSCA16                                                         
         CLC   TACRUSE,=C'SHL'     SPANISH HOLD,                                
         BE    PASSCA16                                                         
         CLC   TACRUSE,=C'ADH'     ADDENDUM HOLD                                
         BE    PASSCA16                                                         
         CLC   TACRUSE,=C'SRE'     SPANISH REINSTATEMENT                        
         BE    PASSCA16                                                         
         CLC   TACRUSE,=C'REN'     OR REINSTATEMENT                             
         BE    PASSCA16                                                         
         CLC   TACRUSE,=C'ARN'     OR ADDENDUM REINSTATEMENT                    
         BNE   *+6                                                              
PASSCA16 LR    R1,R4               SAVE A(LAST HOLD TACR EL)                    
         BAS   RE,NEXTEL                                                        
         BE    PASSCA14                                                         
         CLC   TACRUSE,=C'ADT'     IF THIS IS ADDENDUM SESSION                  
         BNE   *+12                                                             
         CLI   TACRTYPE,UADT13W    THEN IF NOT 13W CYCLE                        
         BNE   NO                    DON'T ISSUE HFN                            
         CLC   TACRUSE,=C'ADC'     OR IF ADDENDUM SESSION/WSP COMBINED          
         BNE   *+12                                                             
         CLI   TACRTYPE,UADC13W    THEN IF NOT 13W CYCLE                        
         BNE   NO                    DON'T ISSUE HFN                            
         SPACE 1                                                                
         CLC   TACRUSE,=C'HLD'     IF IT'S A HLD, THEN NEXT ISN'T 1ST           
         BE    PASSCA18                                                         
         CLC   TACRUSE,=C'SHL'     ALSO SPANISH HOLD                            
         BE    PASSCA18                                                         
         CLC   TACRUSE,=C'ADH'     ALSO ADDENDUM HOLD                           
         BE    PASSCA18                                                         
         CLC   TACRUSE,=C'SRE'     ALSO SPANISH REINSTATEMENT                   
         BE    PASSCA18                                                         
         CLC   TACRUSE,=C'REN'     ALSO REINSTATEMENT                           
         BE    PASSCA18                                                         
         LTR   R1,R1               TEST HAVE A(LAST HLD TACR) IN CASE           
         BZ    PASSCA20                                                         
         LR    R4,R1               PAID SESS LATER. IF YES SET R4 TO R1         
PASSCA18 OC    TACREND,TACREND     PROTECT AGAINST BAD DATA                     
         BZ    NO                                                               
         GOTO1 DATCON,WORK,(1,TACREND),(0,DUB)   CVT END DATE TO EBCDIC         
         GOTO1 ADDAY,WORK,DUB,DUB,1              ADD 1 DAY                      
         GOTO1 DATCON,WORK,(0,DUB),(1,TLCAHDTE)  NOW HAVE NEXT START            
         SPACE 1                                                                
PASSCA20 DS    0H                                                               
         MVC   TLCAHCOM,TLCACOM    BUILD REST OF KEY                            
         MVC   TLCAHSRT,TLCASORT                                                
         MVC   TLCAHSSN,TLCASSN                                                 
         B     YES                                                              
         SPACE 3                                                                
*                                  CAST MUSIC TRACK                             
PASSCAT  NTR1                                                                   
         MVC   TLCATCOM,TLCACOM    SET INTERNAL COMMERCIAL NUMBER               
         MVC   TLCATSEQ,TLCASEQ    CAST SEQUENCE NUMBER                         
         MVC   TLCATSSN,TLCASSN    AND SOCIAL SECURITY NUMBE                    
         SPACE 1                                                                
         USING TATRD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TATRELQ      SEE IF THERE ARE MUSIC CONTRACT/             
         BAS   RE,GETEL            TRACK ELEMENTS                               
         BNE   NO                                                               
         SPACE 1                                                                
         MVC   WORKEXT(L'TATRCOM+L'TATRCSQ),TATRCOM                             
         MVI   WORKEXT+L'TATRCOM+L'TATRCSQ,X'FF'                                
         SPACE 1                                                                
PASSCAT1 MVC   TLCATMCO,TATRCOM    TYPE M INTERNAL COMMERCIAL NUMBER            
         MVC   TLCATMCS,TATRCSQ    TRACK                                        
PASSCAT2 BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
         SPACE 1                                                                
         LA    RE,WORKEXT                                                       
PASSCAT3 CLI   0(RE),X'FF'                                                      
         BE    PASSCAT4                                                         
         CLC   0(L'TATRCOM+L'TATRCSQ,RE),TATRCOM                                
         BE    PASSCAT2                                                         
         LA    RE,L'TATRCOM+L'TATRCSQ(RE)                                       
         B     PASSCAT3                                                         
         SPACE 1                                                                
PASSCAT4 MVC   0(L'TATRCOM+L'TATRCSQ,RE),TATRCOM                                
         MVI   L'TATRCOM+L'TATRCSQ(RE),X'FF'                                    
         DROP  R6                                                               
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCAPCD,TLCATCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCAPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCAPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCAT1                                                         
         EJECT                                                                  
*                                  NAME                                         
PASSCAN  NTR1                                                                   
         MVI   ELCODE,TANMELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLCANCOM,TLCACOM                                                 
         MVC   TLCANSEQ,TLCASEQ                                                 
         USING TANMD,R6                                                         
         MVC   TLCANNAM,TANMCRPN                                                
         B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - PAYMENT HISTORY                                
         SPACE 3                                                                
         USING TLIND,R2                                                         
         USING TLINPD,R3                                                        
*                                  PAYMENT HISTORY                              
PASSINH  NTR1                                                                   
         CLI   TLINCD,TLINCDQ                                                   
         BNE   PASSINH2                                                         
         MVI   ELCODE,TAINELQ      FROM INVOICE RECORD                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         TM    TAINSTA2,TAINSADJ   DON'T GENERATE IF PAYMENT ADJUSTMENT         
         BO    NO                                                               
         MVC   TLINHSEQ,TAINSEQ    PICK OUT SEQUENCE NUMBER                     
         XI    TLINHSEQ,X'FF'      AND COMPLEMENT IT                            
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         MVC   TLINHCOM,TAPDCOM                                                 
         TM    TAPDOPT3,TAPDOGRY                                                
         BNZ   NO                  DON'T GENERATE IF GREY                       
         MVC   TLINHINV,TLININV                                                 
         B     YES                                                              
         SPACE 1                                                                
         USING TLHCD,R2                                                         
PASSINH2 MVC   TLINHCOM,TLHCCOM    FROM HISTORY COMMENT                         
         MVC   TLINHINV,TLHCINV                                                 
         MVC   TLINHSEQ,TLHCSEQ                                                 
         B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - BILLING OPEN ITEM                              
         SPACE 3                                                                
*                                  STATUS/BILLING OPEN ITEM                     
         USING TLIND,R2                                                         
PASSINB  NTR1                                                                   
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         MVC   WORK(1),TAINSTAT                                                 
         MVC   WORK+1(1),TAINSTA2                                               
         TM    TAINSTA2,TAINSADJ   DON'T GENERATE IF PAYMENT ADJUSTMENT         
         BO    NO                                                               
         MVC   TLINBST2,TAINSTAT                                                
         TM    TAINSTAT,TAINSBIL   ALWAYS CREATE IF NOT BILLED                  
         BZ    PINB2                                                            
         TM    TAINSTAT,TAINSCHK+TAINSCAN+TAINSCIN DON'T CREATE IF CHKS         
         BNZ   NO                                  WRITTEN OR CANCELLED         
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDPST1,TAPDPBNP   DON'T GENERATE IF BNP                        
         BO    NO                                                               
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         SPACE 1                                                                
PINB2    LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PINB6                                                            
         TM    TAPDOPT3,TAPDODUM+TAPDOGRY                                       
         BNZ   NO                  DON'T GENERATE IF DUMMY OR GREY              
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BNO   PINB6                                                            
         TM    WORK,TAINSCIN       UNLESS CANCEL(LER) INVOICE                   
         BO    PINB6                                                            
         TM    WORK+1,TAINSHLR     OR UNLESS HOLD(COD) RELEASED                 
         BNO   NO                                                               
PINB6    MVC   TLINBAGY,TLINAGY                                                 
         MVC   TLINBINV,TLININV                                                 
         B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - CHECK OPEN ITEM                                
         SPACE 3                                                                
*                                  CHECK OPEN ITEM                              
PASSINC  NTR1                                                                   
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         TM    TAPDOPT3,TAPDODUM+TAPDOGRY                                       
         BNZ   NO                  DON'T GENERATE IF DUMMY OR GREY              
         SPACE 1                                                                
         MVI   TLINCSRT,X'08'      SET SORT BYTE                                
         CLC   TAPDUSE,=C'GRT'                                                  
         BE    *+12                                                             
         TM    TAPDSTA2,TAPDSPRI                                                
         BZ    *+8                                                              
         MVI   TLINCSRT,X'02'                                                   
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         TM    TAINSTAT,TAINSCAN+TAINSCIN  NOT IF CANCEL(LED) INV.              
         BNZ   NO                                                               
         TM    TAINSTAT,TAINSCHK   GO ON IF CHECKS NOT WRITTEN                  
         BZ    PINC1                                                            
         TM    TAINSTA2,TAINSFRC                 IF FORCE LASTYEAR ON           
         BZ    PINCA                                                            
         GOTO1 DATCON,DMCB,(1,TAINCKRN),(0,DUB)  USE CHECK RUN DATE             
         B     PINCB                                                            
PINCA    GOTO1 DATCON,DMCB,(1,TAINCDTE),(0,DUB)  ELSE, USE CHECK DATE           
         SPACE 1                                                                
PINCB    GOTO1 ADDAY,DMCB,DUB,DUB,2           ADD 2 DAYS TO DATE                
         GOTO1 DATCON,DMCB,(0,DUB),(1,WORK)                                     
         CLC   WORK(3),TODAY                  DON'T BOTHER IF OLDER             
         BL    NO                                                               
PINC1    MVC   TLINCAGY,TLINAGY    AGENCY                                       
         MVC   TLINCINV,TLININV    INVOICE                                      
         XC    TLINCINV,HEXFFS     (UNCOMPLEMENT)                               
         TM    TAINSTA2,TAINSADJ   PAYROLL ADJUSTMENT INVOICES HAVE DUE         
         BZ    *+14                    DATE OF X'000004'                        
         MVC   TLINCDUE,=X'000004'                                              
         B     YES                                                              
         TM    TAINSTA2,TAINSURG   URGENT INVOICES HAVE DUE DATE OF             
         BZ    *+14                    X'000008'                                
         MVC   TLINCDUE,=X'000008'                                              
         B     YES                                                              
         LR    R6,R2               ALL OTHER INVOICES NEED DUE DATE EL.         
         MVI   ELCODE,TADDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TADDD,R6                                                         
         MVC   TLINCDUE,TADDDATE                                                
         B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - BILL DATE POINTERS                             
         SPACE 1                                                                
PASSIND  NTR1                                                                   
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDOPT3,TAPDOGRY                                                
         BNZ   NO                  DON'T GENERATE FOR GREY                      
         MVC   SAVOPT3,TAPDOPT3                                                 
         LR    R6,R2                                                            
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         MVC   TLINDDTE,TAINBDTE   BILL DATE (PWOS)                             
         TM    SAVOPT3,TAPDODUM    OR, IF DUMMY,                                
         BNO   *+10                                                             
         MVC   TLINDDTE,TAINPDTE   USE PAID DATE (PWOS)                         
         OC    TLINDDTE,TLINDDTE   DON'T BOTHER IF NO 'BILL' DATE               
         BZ    NO                                                               
         MVC   TLINDAGY,TLINAGY    AGENCY                                       
         MVC   TLINDINV,TLININV    INVOICE NUMBER                               
         XC    TLINDINV,HEXFFS     (UNCOMPLEMENT)                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PASSIND4                                                         
         USING TAPDD,R6                                                         
         MVI   TLINDCUR,C'U'       CURRENCY                                     
         TM    TAPDSTAT,TAPDSCAN                                                
         BZ    *+8                                                              
         MVI   TLINDCUR,C'C'                                                    
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    *+8                                                              
         MVI   TLINDCUR,C'E'                                                    
         MVC   TLINDEMP,TAPDEMP    EMPLOYER                                     
         MVC   TLINDOFF,TAPDOFF    OFFICE                                       
         MVC   TLINDCLI,TAPDCLI    CLIENT                                       
         SPACE 1                                                                
PASSIND4 LR    R6,R2                                                            
         MVI   ELCODE,TAOIELQ      ADJUSTMENTS NEED ORIGINAL AGY/INV            
         BAS   RE,GETEL                                                         
         BNE   PASSINDX                                                         
         USING TAOID,R6                                                         
         OC    TAOIAGY,TAOIAGY     IGNORE DUMMY ELS.                            
         BZ    PASSINDX                                                         
         MVC   TLINDADJ,TLINDINV+2  MOVE ADJ. INVOICE+2(3) TO BOTTOM            
         MVC   TLINDAGY,TAOIAGY     USE ORIGINAL AGENCY                         
         MVC   TLINDINV,TAOIINV     AND ORIGINAL INVOICE NUMBER                 
PASSINDX B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - CHECK DATE POINTERS                            
         SPACE 1                                                                
PASSINK  NTR1                                                                   
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         LR    R4,R6               SAVE A(INV. STATUS EL.)                      
         MVC   TLINKDTE,TAINCDTE   CHECK DATE (PWOS)                            
         OC    TLINKDTE,TLINKDTE   OK IF WE HAVE CHECK DATE                     
         BNZ   PASSINK2                                                         
         MVC   TLINKDTE,TAINBDTE   MAY BE OK IF WE HAVE BILLED DATE             
         OC    TLINKDTE,TLINKDTE                                                
         BZ    NO                                                               
         SPACE 1                                                                
PASSINK2 MVC   TLINKAGY,TLINAGY    AGENCY                                       
         MVC   TLINKINV,TLININV    INVOICE NUMBER                               
         XC    TLINKINV,HEXFFS     (UNCOMPLEMENT)                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         TM    TAPDOPT3,TAPDODUM   DON'T GENERATE IF DUMMY                      
         BO    NO                                                               
         MVI   TLINKCUR,C'U'       CURRENCY                                     
         TM    TAPDSTAT,TAPDSCAN                                                
         BZ    *+8                                                              
         MVI   TLINKCUR,C'C'                                                    
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    *+8                                                              
         MVI   TLINKCUR,C'E'                                                    
         MVC   TLINKEMP,TAPDEMP    EMPLOYER                                     
         MVC   TLINKOFF,TAPDOFF    OFFICE                                       
         MVC   TLINKCLI,TAPDCLI    CLIENT                                       
         SPACE 1                                                                
         TM    TAPDPST1,TAPDPBNP   OK IF BNP                                    
         BO    PASSINK4                                                         
         LR    R6,R4               RESTORE A(INV. STATUS EL.)                   
         USING TAIND,R6                                                         
         OC    TAINCDTE,TAINCDTE   ELSE REJECT IF NO CHECK DATE                 
         BZ    NO                                                               
         SPACE 1                                                                
PASSINK4 LR    R6,R2                                                            
         MVI   ELCODE,TAOIELQ      ADJUSTMENTS NEED ORIGINAL AGY/INV            
         BAS   RE,GETEL                                                         
         BNE   PASSINKX                                                         
         USING TAOID,R6                                                         
         OC    TAOIAGY,TAOIAGY     IGNORE DUMMY ELS.                            
         BZ    PASSINKX                                                         
         MVC   TLINKADJ,TLINKINV+2  MOVE ADJ. INVOICE+2(3) TO BOTTOM            
         MVC   TLINKAGY,TAOIAGY     USE ORIGINAL AGENCY                         
         MVC   TLINKINV,TAOIINV     AND ORIGINAL INVOICE NUMBER                 
PASSINKX B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - PAY DATE POINTERS                              
         SPACE 1                                                                
PASSINP  NTR1                                                                   
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         LR    R4,R6               SAVE A(INV. STATUS EL.)                      
         MVC   TLINPDTE,TAINPDTE   CHECK DATE (PWOS)                            
         OC    TLINPDTE,TLINPDTE                                                
         BZ    NO                                                               
         SPACE 1                                                                
PASSINP2 MVC   TLINPAGY,TLINAGY    AGENCY                                       
         MVC   TLINPINV,TLININV    INVOICE NUMBER                               
         XC    TLINPINV,HEXFFS     (UNCOMPLEMENT)                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         MVI   TLINPCUR,C'U'       CURRENCY                                     
         TM    TAPDSTAT,TAPDSCAN                                                
         BZ    *+8                                                              
         MVI   TLINPCUR,C'C'                                                    
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    *+8                                                              
         MVI   TLINPCUR,C'E'                                                    
         MVC   TLINPEMP,TAPDEMP    EMPLOYER                                     
         MVC   TLINPOFF,TAPDOFF    OFFICE                                       
         MVC   TLINPCLI,TAPDCLI    CLIENT                                       
         SPACE 1                                                                
PASSINP4 LR    R6,R2                                                            
         MVI   ELCODE,TAOIELQ      ADJUSTMENTS NEED ORIGINAL AGY/INV            
         BAS   RE,GETEL                                                         
         BNE   PASSINKX                                                         
         USING TAOID,R6                                                         
         OC    TAOIAGY,TAOIAGY     IGNORE DUMMY ELS.                            
         BZ    PASSINKX                                                         
         MVC   TLINPADJ,TLINPINV+2  MOVE ADJ. INVOICE+2(3) TO BOTTOM            
         MVC   TLINPAGY,TAOIAGY     USE ORIGINAL AGENCY                         
         MVC   TLINPINV,TAOIINV     AND ORIGINAL INVOICE NUMBER                 
PASSINPX B     YES                                                              
         EJECT                                                                  
*              INVOICE RECORDS - WEB APPLICATION POINTERS                       
         SPACE 1                                                                
PASSINW  NTR1                                                                   
         MVI   ELCODE,TAFNELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PASSINW2 BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TAFND,R6                                                         
         CLI   TAFNTYPE,TAFNTWEB                                                
         BNE   PASSINW2                                                         
         ZIC   RF,TAFNLEN                                                       
         SHI   RF,4                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TLINWWID(0),TAFNNAME                                             
         OC    TLINWWID,SPACES                                                  
         DROP  R6                                                               
         SPACE 1                                                                
         USING TLIND,R2                                                         
         MVC   TLINWAGY,TLINAGY                                                 
         MVC   TLINWINV,TLININV                                                 
         XC    TLINWINV,HEXFFS                                                  
         B     YES                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              INVOICE RECORDS - BOVER POINTERS                                 
         SPACE 1                                                                
PASSINO  NTR1                                                                   
         BRAS  RE,PASSINO0                                                      
         B     XIT                                                              
         EJECT                                                                  
*              INVOICE RECORDS - ETIME POINTERS                                 
         SPACE 1                                                                
PASSIET  NTR1                                                                   
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         CLI   TAINLEN,TAINLNQ                                                  
         BL    NO                                                               
         OC    TAINPTPD,TAINPTPD                                                
         BZ    NO                                                               
         TM    TAINSTAT,TAINSCIN        DO NOT ADD IF CANCEL INVOICE            
         BO    NO                                                               
         MVC   TLINECOM,TAINTMCO                                                
         MVC   TLINETPS,TAINPTPS                                                
         XC    TLINETPS,HEXFFS                                                  
         DROP  R6                                                               
         SPACE 1                                                                
         USING TLIND,R2                                                         
         CLC   TLINAGY,=CL6'999999'     DO NOT ADD IF ADJUSTMENT                
         BE    NO                                                               
         MVC   TLINEAGY,TLINAGY                                                 
         MVC   TLINEINV,TLININV                                                 
         DROP  R2                                                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TAPDD,R6                                                         
         TM    TAPDPST1,TAPDPCRD                                                
         BZ    YES                                                              
         OI    TLINESTA,TLINESCR                                                
         B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              RETRO INVOICE RECORDS                                            
PASSINR  NTR1                                                                   
         BRAS  RE,PRETINV                                                       
         BNE   NO                                                               
         B     YES                                                              
                                                                                
*              CHECK RECORDS                                                    
         SPACE 3                                                                
         USING TLCKD,R2                                                         
         USING TLCKPD,R3                                                        
*                                  CHECKS                                       
PASSCKC  NTR1                                                                   
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         OC    TACDCHK,TACDCHK     IF THERE'S A CHECK NUMBER                    
         BZ    NO                                                               
         MVC   TLCKCBNK,TACDBNK                                                 
         MVC   TLCKCCHK,TACDCHK                                                 
         XC    TLCKCCHK,HEXFFS     (COMPLEMENT)                                 
         MVC   TLCKCSEQ,TLCKSEQ                                                 
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - CAST PAYMENT HISTORY                             
         SPACE 3                                                                
*                                  CAST PAYMENT HISTORY                         
PASSCKH  NTR1                                                                   
         MVC   TLCKHSSN,TLCKSSN                                                 
         MVC   TLCKHCAT,TLCKCAT                                                 
         MVC   TLCKHSEQ,TLCKSORT+4                                              
         MVC   TLCKHINV,TLCKINV                                                 
         XC    TLCKHINV,HEXFFS     (COMPLEMENT)                                 
         MVC   TLCKHCSQ,TLCKSEQ                                                 
         MVC   TLCKHDA,RDSKADD                                                  
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ      NEED INTERNAL COMMERCIAL NUMBER              
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDPST1,TAPDPBNP   DON'T GENERATE IF BNP                        
         BO    NO                                                               
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         TM    TAPDADJS,TAPDADTR+TAPDADTT+TAPDADST                              
         BNZ   NO                  DON'T GENERATE IF TAX/REF OR TRANS           
         TM    TAPDOPT3,TAPDODUM+TAPDOGRY                                       
         BNZ   NO                  DON'T GENERATE IF DUMMY OR GREY              
         MVC   TLCKHCOM,TAPDCOM                                                 
         LR    R6,R2                                                            
         MVI   ELCODE,TAOIELQ      ADJUSTMENTS NEED ORIGINAL INVOICE            
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TAOID,R6                                                         
         MVC   TLCKHINV,TAOIINV    OTHERWISE SAVE INVOICE IN KEY                
         XC    TLCKHINV,HEXFFS     (COMPLEMENT)                                 
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - EMPLOYEE'S CHECKS                                
         SPACE 3                                                                
*                                  EMPLOYEE'S CHECKS                            
PASSCKE  NTR1                                                                   
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDSTAT,TAPDSCNL   DON'T BOTHER IF INVOICE CANCELLED            
         BO    NO                                                               
         TM    TAPDPST1,TAPDPBNP   OR IF BNP INVOICE                            
         BO    NO                                                               
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         TM    TAPDOPT3,TAPDODUM                                                
         BO    NO                  DON'T GENERATE IF DUMMY                      
         MVC   TLCKESSN,TLCKSSN    S/S NUMBER                                   
         MVI   TLCKECUR,C'U'       CURRENCY                                     
         TM    TAPDSTAT,TAPDSCAN                                                
         BZ    *+8                                                              
         MVI   TLCKECUR,C'C'                                                    
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    *+8                                                              
         MVI   TLCKECUR,C'E'                                                    
         MVC   TLCKEEMP,TAPDEMP    EMPLOYER                                     
         MVC   TLCKEAGY,TLCKAGY    AGENCY                                       
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAOIELQ      ADJUSTMENTS NEED ORIGINAL AGENCY             
         BAS   RE,GETEL                                                         
         BNE   PASSCKE2                                                         
         USING TAOID,R6                                                         
         OC    TAOIAGY,TAOIAGY                                                  
         BZ    PASSCKE2                                                         
         MVC   TLCKEAGY,TAOIAGY                                                 
         SPACE 1                                                                
PASSCKE2 LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKESSN,TATIID                                                  
         SPACE 1                                                                
*                                  IN CASE CHECKS NOT WRITTEN YET,              
         MVC   TLCKEDTE+1(2),TLCKSORT+4  SET CAST INPUT SEQ NO. IN DATE         
         MVI   DUB+7,X'0C'               AND BINARY INVOICE NO. IN SEQ          
         MVO   DUB,TLCKINV(5)            USE FIRST 5 HOBS                       
         CVB   R1,DUB                                                           
         STCM  R1,15,TLCKESEQ                                                   
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PASSCKE5                                                         
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     TEST THERE'S A DATE                          
         BZ    PASSCKE5                                                         
         MVC   TLCKEDTE,TACDDTE    CHECK DATE                                   
         XC    TLCKEDTE,HEXFFS     (COMPLEMENT)                                 
         SPACE 1                                                                
         MVC   TLCKESEQ,TACDSEQ    SEQUENCE NUMBER                              
         OC    TLCKESEQ,TLCKESEQ   IF NOTHING IN SEQ YET                        
         BNZ   PASSCKE5                                                         
         MVC   TLCKESEQ,TACDCHK+4  USE CHECK NUMBER                             
PASSCKE5 XC    TLCKESEQ,HEXFFS     (COMPLEMENT SEQ)                             
         TM    TACDSTA2,TACDSMSC   TEST MULTI-CONTRACT SERVICE                  
         BZ    *+8                                                              
         OI    TLCKESTA,TLCKEMSC                                                
         TM    TACDSTAT,TACDSFRC   FORCED TO END OF LAST YEAR                   
         BZ    *+8                                                              
         OI    TLCKESTA,TLCKEFRC                                                
                                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACAELQ      NEED CAST DETAILS EL                         
         BAS   RE,GETEL                                                         
         BNE   PASSCKEX                                                         
         USING TACAD,R6                                                         
         OC    TACANCDE,TACANCDE                                                
         BZ    PASSCKEX                                                         
         GOTOR TRNSAGT,DMCB,(X'40',TACANCDE),TLCKEAGT                           
PASSCKEX B     YES                                                              
         EJECT                                                                  
*=====================================================================          
*              CHECK RECORDS - EMPLOYEE'S CHECKS 2                              
*=====================================================================          
*                                                                               
PASSCKK  NTR1                                                                   
*                                                                               
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     TEST THERE'S A CHECK DATE                    
         BZ    NO                                                               
         OC    TACDCHK,TACDCHK     TEST THERE'S A CHECK NUMBER                  
         BZ    NO                                                               
         CLI   TACDDTE,X'A7'       ONLY START WITH 2007                         
         BL    NO                                                               
         CLC   TACDDTE(1),PRI2YR   BEFORE 2 YEARS FROM THIS YEAR?               
         BL    NO                  DON'T WANT IT THEN                           
         MVC   TLCKKDTE,TACDDTE    CHECK DATE                                   
         MVC   TLCKKCKN,TACDCHK    CHECK NUMBER                                 
*                                                                               
         MVC   TLCKKSEQ,TACDSEQ    SEQUENCE NUMBER                              
         OC    TLCKKSEQ,TLCKKSEQ   IF NOTHING IN SEQ YET                        
         BNZ   PASSCKK1                                                         
         MVC   TLCKKSEQ,TACDCHK+4  USE CHECK NUMBER                             
*                                                                               
PASSCKK1 LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDSTAT,TAPDSCNL   DON'T BOTHER IF INVOICE CANCELLED            
         BO    NO                                                               
         TM    TAPDPST1,TAPDPBNP   OR IF BNP INVOICE                            
         BO    NO                                                               
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         TM    TAPDOPT3,TAPDODUM                                                
         BO    NO                  DON'T GENERATE IF DUMMY                      
         MVC   TLCKKSSN,TLCKSSN    S/S NUMBER                                   
         MVI   TLCKKCUR,C'U'       CURRENCY                                     
         TM    TAPDSTAT,TAPDSCAN                                                
         BZ    *+8                                                              
         MVI   TLCKKCUR,C'C'                                                    
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    *+8                                                              
         MVI   TLCKKCUR,C'E'                                                    
         MVC   TLCKKEMP,TAPDEMP    EMPLOYER                                     
*                                                                               
PASSCKK2 LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKKSSN,TATIID                                                  
*                                                                               
PASSCKKX B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - EMPLOYEE'S PAYROLL YTD POINTERS                  
         SPACE 3                                                                
PASSCKY  NTR1                                                                   
         MVI   CKYSTAT,0                                                        
         SPACE 1                                                                
         MVI   ELCODE,TAPDELQ      NEED EMPLOYER                                
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDSTAT,TAPDSCNL   DON'T BOTHER IF INVOICE CANCELLED            
         BO    NO                                                               
         TM    TAPDPST1,TAPDPBNP+TAPDPCRD  OR IF BNP OR CREDIT INVOICE          
         BNZ   NO                                                               
         TM    TAPDOPT3,TAPDODUM   DON'T GENERATE IF DUMMY                      
         BNZ   NO                                                               
         MVI   TLCKYCUR,C'U'       SET US$                                      
         TM    TAPDSTAT,TAPDSCAN   IF CAN$ PAYMENT                              
         BZ    *+8                                                              
         MVI   TLCKYCUR,C'C'       SET CAN$                                     
         TM    TAPDPST2,TAPDPEUR   IF EURO PAYMENT                              
         BZ    *+8                                                              
         MVI   TLCKYCUR,C'E'       SET EURO                                     
         MVC   TLCKYSSN,TLCKSSN    S/S NUMBER                                   
         MVC   TLCKYEMP,TAPDEMP    EMPLOYER                                     
         MVC   TLCKYW4T,TAPDW4TY   W4 TYPE                                      
         MVC   TLCKYCSQ,TLCKSEQ    SEQUENCE NUMBER                              
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKYSSN,TATIID                                                  
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     TEST THERE'S A DATE                          
         BZ    NO                                                               
         CLC   TACDDTE,=X'960101'  TEST 1996 OR LATER                           
         BL    NO                                                               
         TM    TACDSTAT,TACDSLIN   IGNORE LIEN CHECKS                           
         BO    NO                                                               
         TM    TACDSTAT,TACDSTRS   IGNORE TRUSTEE CHECKS                        
         BO    NO                                                               
*                                  TAX YEAR                                     
         GOTO1 DATCON,DMCB,(1,TACDDTE),(20,WORK)                                
         MVC   TLCKYEAR,WORK                                                    
         SPACE 1                                                                
         MVC   TLCKYDTE,TACDDTE    CHECK DATE                                   
         XC    TLCKYDTE,HEXFFS     (COMPLEMENT)                                 
         SPACE 1                                                                
         TM    TACDSTAT,TACDSFRC   IF FORCING LAST YEAR                         
         BZ    *+10                                                             
         MVC   TLCKYSEQ,TACDSEQ2   USE OVERRIDE SEQUENCE NUMBER                 
         OC    TLCKYSEQ,TLCKYSEQ   IF NOTHING IN SEQ YET                        
         BNZ   *+10                                                             
         MVC   TLCKYSEQ,TACDSEQ    SEQUENCE NUMBER                              
         OC    TLCKYSEQ,TLCKYSEQ   IF NOTHING IN SEQ YET                        
         BNZ   *+10                                                             
         MVC   TLCKYSEQ,TACDCHK+4  USE CHECK NUMBER                             
         XC    TLCKYSEQ,HEXFFS     (COMPLEMENT SEQ)                             
         SPACE 1                                                                
         XC    WORK,WORK                                                        
         LA    R4,WORK             R4=A(LIST OF UNITS WE'VE PROCESSED)          
                                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAATELQ      P+ CANADIAN                                  
         BAS   RE,GETEL                                                         
         BNE   PASSCKYA                                                         
         BRAS  RE,PKCAN                                                         
         TM    CKYSTAT,CKYSFED                                                  
         BO    YES                                                              
         L     R3,SAVER3                                                        
                                                                                
PASSCKYA LR    R6,R2                                                            
         MVI   ELCODE,TACWELQ      CHECK IF THERE IS A FEDERAL ELEMENT          
         BAS   RE,GETEL                                                         
         BNE   PASSCKY0                                                         
         USING TACWD,R6                                                         
         CLC   TACWUNIT,=C'FD '                                                 
         BE    PASSCKY1                                                         
         BAS   RE,NEXTEL                                                        
         BE    *-14                                                             
         SPACE 1                                                                
PASSCKY0 MVC   TLCKYTXU,=C'FD '    NO - GENERATE A FEDERAL                      
         CLI   TLCKYCUR,C'E'                                                    
         BNE   *+10                                                             
         MVC   TLCKYTXU,=C'EU '                                                 
         LR    R6,R2                                                            
         BAS   RE,GETEL            IF THERE ARE NO WITHHOLDING ELEMENTS         
         BNE   YES                 THEN RETURN YES TO CREATE FD POINTER         
         B     PASSCKY5            ELSE STACK FD POINTER & PROCESS CWEL         
         SPACE 1                                                                
PASSCKY1 LR    R6,R2               YES - START OVER FROM BEGINNING              
         BAS   RE,GETEL            (AT LEAST FD IS HERE)                        
         SPACE 1                                                                
PASSCKY2 MVC   TLCKYTXU,TACWUNIT   FILL IN TAX UNIT                             
         MVC   TLCKYSTS,TACWSTAT   WITHHOLDING STATUS                           
         MVC   0(3,R4),TACWUNIT    SAVE UNIT TO PREVENT DUPLICATES              
         LA    R4,3(R4)                                                         
         SPACE 1                                                                
PASSCKY3 BAS   RE,NEXTEL           LOOK FOR ANOTHER WITHHOLDING ELEMENT         
         BNE   YES                                                              
         OC    TACWUNIT,TACWUNIT   TAX UNIT MUST BE DEFINED                     
         BZ    PASSCKY3                                                         
         LA    R1,WORK             INSURE WE HAVEN'T SEEN THIS UNIT YET         
PASSCKY4 CLI   0(R1),0                                                          
         BE    PASSCKY5                                                         
         CLC   TACWUNIT,0(R1)                                                   
         BE    PASSCKY3                                                         
         LA    R1,3(R1)                                                         
         B     PASSCKY4                                                         
*                                  ANOTHER POINTER SO BUMP STACK                
PASSCKY5 MVI   TLCKPCD,TLCKYCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCKPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLCKPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCKY2                                                         
         EJECT                                                                  
*              CHECK RECORDS - EMPLOYEE'S BILLING YTD POINTERS                  
         SPACE 3                                                                
PASSCKB  NTR1                                                                   
         MVI   ELCODE,TABYELQ      NEED BILLING YTD ELEMENT                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TABYD,R6                                                         
         OC    TABYDATE,TABYDATE   DON'T BOTHER IF NO RUN DATE                  
         BZ    NO                                                               
         CLC   TABYDATE,=X'960101' TEST 1996 OR LATER                           
         BL    NO                                                               
         MVC   TLCKBDTE,TABYDATE   SET RUN DATE                                 
         XC    TLCKBDTE,HEXFFS     (COMPLEMENTED)                               
         MVC   TLCKBTIM,TABYTIME   SET RUN TIME                                 
         XC    TLCKBTIM,HEXFFS     (COMPLEMENTED)                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS EL.                      
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDSTA2,TAPDSSUB   DON'T GENERATE IF SUBSIDIARY                 
         BO    NO                                                               
         MVC   TLCKBEMP,TAPDEMP    EMPLOYER                                     
         MVI   TLCKBCUR,C'U'       SET US$                                      
         TM    TAPDSTAT,TAPDSCAN   IF CAN$ PAYMENT                              
         BZ    *+8                                                              
         MVI   TLCKBCUR,C'C'       SET CAN$                                     
         TM    TAPDPST2,TAPDPEUR   IF EURO PAYMENT                              
         BZ    *+8                                                              
         MVI   TLCKBCUR,C'E'       SET EURO                                     
         SPACE 1                                                                
         MVC   TLCKBSSN,TLCKSSN    S/S NUMBER                                   
         MVC   TLCKBAGY,TLCKAGY    AGENCY                                       
         MVC   TLCKBINV,TLCKINV+2  LAST 3 BYTES OF INVOICE NUMBER               
         MVC   TLCKBSEQ,TLCKSORT+4 SEQUENCE NUMBER                              
         XC    TLCKBSEQ,HEXFFS     (COMPLEMENTED)                               
         LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKBSSN,TATIID                                                  
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - DUE COMPANY TRACKING                             
         SPACE 3                                                                
PASSCKD  NTR1                                                                   
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     TEST THERE'S A DATE                          
         BZ    NO                                                               
         OC    TACDCHK,TACDCHK          AND A CHECK NUMBER                      
         BZ    NO                                                               
         MVC   TLCKDSSN,TLCKSSN                                                 
         MVC   TLCKDDTE,TACDDTE                                                 
         XC    TLCKDDTE,HEXFFS     (COMPLEMENT)                                 
         CLI   TACDLEN,52                                                       
         BL    *+10                                                             
         MVC   TLCKDSEQ,TACDSEQ    SEQUENCE NUMBER                              
         OC    TLCKDSEQ,TLCKDSEQ   IF NOTHING IN SEQ YET                        
         BNZ   *+10                                                             
         MVC   TLCKDSEQ,TACDCHK+4  USE CHECK NUMBER                             
         XC    TLCKDSEQ,HEXFFS     (COMPLEMENT SEQ)                             
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKDSSN,TATIID                                                  
         SPACE 1                                                                
         MVC   WORK,TLCKPKEY       SAVE WHAT WE HAVE OF KEY SO FAR              
         LR    R6,R2                                                            
         MVI   ELCODE,TADWELQ      LOOK FOR DUE COMPANY WITHHOLDING             
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TADWD,R6                                                         
         CLI   TADWDUC,0                                                        
         BE    NO                                                               
         MVC   TLCKDDUC,TADWDUC    DUE COMPANY CODE TO KEY                      
         SPACE 1                                                                
PASSCKD4 BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
         CLI   TADWDUC,0                                                        
         BE    PASSCKD4                                                         
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCKPCD,TLCKDCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLCKPKEY,WORK       PRE-SET TO SAVED KEY                         
         MVC   TLCKDDUC,TADWDUC    DUE COMPANY CODE TO KEY                      
         B     PASSCKD4                                                         
         EJECT                                                                  
*              CHECK RECORDS - LIEN TRACKING                                    
         SPACE 3                                                                
PASSCKL  NTR1                                                                   
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     TEST THERE'S A DATE                          
         BZ    NO                                                               
         OC    TACDCHK,TACDCHK          AND A CHECK NUMBER                      
         BZ    NO                                                               
         TM    TACDSTAT,TACDSLIN   AND NOT A LIEN PAYEE CHECK                   
         BO    NO                                                               
         MVC   TLCKLSSN,TLCKSSN                                                 
         MVC   TLCKLDTE,TACDDTE                                                 
         XC    TLCKLDTE,HEXFFS     (COMPLEMENT)                                 
         CLI   TACDLEN,52                                                       
         BL    *+10                                                             
         MVC   TLCKLSEQ,TACDSEQ    SEQUENCE NUMBER                              
         OC    TLCKLSEQ,TLCKLSEQ   IF NOTHING IN SEQ YET                        
         BNZ   *+10                                                             
         MVC   TLCKLSEQ,TACDCHK+4  USE CHECK NUMBER                             
         XC    TLCKLSEQ,HEXFFS     (COMPLEMENT SEQ)                             
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TATIELQ      MAY NEED CORPORATION TAX ID                  
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         USING TATID,R6                                                         
         MVC   TLCKLSSN,TATIID                                                  
         SPACE 1                                                                
         MVC   WORK,TLCKPKEY       SAVE WHAT WE HAVE OF KEY SO FAR              
         LR    R6,R2                                                            
         MVI   ELCODE,TALWELQ      LOOK FOR LIEN WITHHOLDING                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TALWD,R6                                                         
         CLI   TALWLIN,0                                                        
         BE    NO                                                               
         MVC   TLCKLLIN,TALWLIN    LIEN CODE TO KEY                             
         SPACE 1                                                                
PASSCKL4 BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
         CLI   TALWLIN,0                                                        
         BE    PASSCKL4                                                         
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCKPCD,TLCKLCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLCKPKEY,WORK       PRE-SET TO SAVED KEY                         
         MVC   TLCKLLIN,TALWLIN    LIEN CODE TO KEY                             
         B     PASSCKL4                                                         
         DROP  R2                                                               
         EJECT                                                                  
*              CHECK RECORDS - CHECK STOP/STOP DATE                             
         SPACE 3                                                                
PASSCSS  NTR1                                                                   
         TM    P1BYTE0,X'10'       DO NOT GENERATE FOR REISSUE                  
         BO    NO                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAKPELQ                                                   
         BAS   RE,GETEL            IF STOP ELEMENT EXISTS                       
         BNE   NO                                                               
         USING TAKPD,R6                                                         
         MVC   TLCKCSSD,TAKPSRDT   PUT STOP REQUEST DATE                        
         XC    TLCKCSSD,HEXFFS     COMPLEMENTED                                 
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         MVC   TLCKCSSC,TACDCHK    AND CHECK # INTO KEY                         
         SPACE 1                                                                
         MVI   TLCKCSSX,C'N'                                                    
         LR    R6,R2                                                            
         MVI   ELCODE,TACNELQ      INDICATE WHETHER CHECK STOP                  
         BAS   RE,GETEL            CANCEL ELEMENT EXISTS                        
         BNE   PCSS10                                                           
         USING TACND,R6                                                         
         CLI   TACNTYPE,TACNTYPS                                                
         BNE   PCSS10                                                           
         MVI   TLCKCSSX,C'Y'                                                    
         SPACE 1                                                                
         USING TLCKCD,R6                                                        
PCSS10   LR    R6,R2                                                            
         MVC   TLCKCSSI,TLCKINV    SAVE INVOICE NUMBER                          
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - CHECK STOP/CHECK DATE                            
         SPACE 3                                                                
PASSCSC  NTR1                                                                   
         TM    P1BYTE0,X'10'       DO NOT GENERATE FOR REISSUE                  
         BO    NO                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAKPELQ                                                   
         BAS   RE,GETEL            IF STOP ELEMENT EXISTS                       
         BNE   NO                                                               
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         MVC   TLCKCSCD,TACDDTE    PUT CHECK DATE                               
         XC    TLCKCSCD,HEXFFS     COMPLEMENTED                                 
         MVC   TLCKCSCC,TACDCHK    AND CHECK # INTO KEY                         
         SPACE 1                                                                
         MVI   TLCKCSCX,C'N'                                                    
         LR    R6,R2                                                            
         MVI   ELCODE,TACNELQ      INDICATE WHETHER CHECK STOP                  
         BAS   RE,GETEL            CANCEL ELEMENT EXISTS                        
         BNE   PCSC10                                                           
         USING TACND,R6                                                         
         CLI   TACNTYPE,TACNTYPS                                                
         BNE   PCSC10                                                           
         MVI   TLCKCSCX,C'Y'                                                    
         SPACE 1                                                                
         USING TLCKCD,R2                                                        
PCSC10   MVC   TLCKCSCI,TLCKINV    SAVE INVOICE NUMBER                          
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - CHECK PULL/PULL DATE                             
         SPACE 3                                                                
PASSCPS  NTR1                                                                   
         TM    P1BYTE0,X'10'       DO NOT GENERATE FOR REISSUE                  
         BO    NO                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAKLELQ                                                   
         BAS   RE,GETEL            IF PULL ELEMENT EXISTS                       
         BNE   NO                                                               
         USING TAKLD,R6                                                         
         MVC   TLCKCPSD,TAKLRQDT   PUT PULL REQUEST DATE                        
         XC    TLCKCPSD,HEXFFS     COMPLEMENTED                                 
         MVC   TLCKCPSR,TAKLSTAT   REASON FOR PULL                              
         SPACE 1                                                                
         MVC   TLCKCPSI,TLCKINV    INVOICE                                      
         MVC   TLCKCPSE,TLCKSEQ    CHECK SEQUENCE NUMBER                        
         MVC   TLCKCPSS,TLCKSORT+4 CAST SEQUENCE NUMBER                         
         MVC   TLCKCPSN,TLCKSSN    SOCIAL SECURITY NUMBER                       
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TACDD,R6                                                         
         MVC   TLCKCPSC,TACDCHK    AND CHECK # INTO KEY                         
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - CHECK PULL/CHECK DATE                            
         SPACE 3                                                                
PASSCPC  NTR1                                                                   
         TM    P1BYTE0,X'10'       DO NOT GENERATE FOR REISSUE                  
         BO    NO                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAKLELQ                                                   
         BAS   RE,GETEL            IF PULL ELEMENT EXISTS                       
         BNE   NO                                                               
         USING TAKLD,R6                                                         
         MVC   TLCKCPCR,TAKLSTAT   PUT REASON FOR CHECK PULL                    
         SPACE 1                                                                
         MVC   TLCKCPCI,TLCKINV    INVOICE                                      
         MVC   TLCKCPCE,TLCKSEQ    CHECK SEQUENCE NUMBER                        
         MVC   TLCKCPCS,TLCKSORT+4 CAST SEQUENCE NUMBER                         
         MVC   TLCKCPCN,TLCKSSN    SOCIAL SECURITY NUMBER                       
         MVC   TLCKCPDA,RDSKADD    DISK ADDRESS                                 
         SPACE 1                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TACDD,R6                                                         
         MVC   TLCKCPCD,TACDDTE    CHECK DATE                                   
         XC    TLCKCPCD,HEXFFS     COMPLEMENTED                                 
         XR    R1,R1               DEFAULT CHECK # IS ZEROS                     
         CLC   TACDCHK,=CL8' '     IF NO CHECK #, CHK # IS BIN ZEROS            
         BNH   PCPC200                                                          
         PACK  DUB,TACDCHK         CONVERT CHECK # TO BINARY                    
         CVB   R1,DUB                                                           
PCPC200  STCM  R1,15,TLCKCPCC      AND CHECK # INTO KEY                         
         B     YES                                                              
         EJECT                                                                  
*              CHECK RECORDS - CREDIT INVOICE                                   
         SPACE 3                                                                
PASSCKR  NTR1                                                                   
         CLI   TLCKSEQ,X'FF'                                                    
         BNE   NO                                                               
         CLC   TLCKSORT+4(2),=2X'FF'                                            
         BE    NO                                                               
                                                                                
         USING TANUD,R6                                                         
         MVI   ELCODE,TANUELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PCKR10   BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         CLI   TANUTYPE,TANUTINV                                                
         BNE   PCKR10                                                           
         MVC   TLCKRCIN,TANUMBER                                                
         DROP  R6                                                               
                                                                                
         USING TAPDD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLCKRCOM,TAPDCOM                                                 
         DROP  R6                                                               
                                                                                
         MVC   TLCKRAGY,TLCKAGY                                                 
         MVC   TLCKRCSQ,TLCKSORT+4                                              
         MVC   TLCKRINV,TLCKINV                                                 
                                                                                
         PACK  DUB,TLCKSSN      CONVERT SSN TO BINARY                           
         CVB   R1,DUB                                                           
         STCM  R1,15,TLCKRSSN                                                   
                                                                                
         B     YES                                                              
         EJECT                                                                  
*              ADVICE RECORDS                                                   
         SPACE 3                                                                
         USING TLDVD,R2                                                         
         USING TLDVPD,R3                                                        
*                                  ADVICE OFFICE/SEND-DATE                      
PASSDVO  NTR1                                                                   
         MVI   ELCODE,TADVELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TADVD,R6                                                         
         MVC   TLDVOOFF,TADVOFF    OFFICE                                       
         MVC   TLDVOAGY,TLDVAGY    AGENCY                                       
         MVC   TLDVODTE,TADVSDTE   SEND DATE                                    
         OC    TLDVODTE,TLDVODTE   IF NO SENT DATE                              
         BNZ   PASSDVO5                                                         
         OC    TADVPDTE,TADVPDTE   BUT PAID DATE DEFINED                        
         BZ    PASSDVO5                                                         
         MVC   TLDVODTE,TADVPDTE   USE IT                                       
         SPACE 1                                                                
PASSDVO5 MVC   TLDVOCID,TLDVCID    COMMERCIAL                                   
         MVC   TLDVOADV,TLDVADV    ADVICE                                       
         MVC   TLDVOSEQ,TLDVSEQ    SEQUENCE                                     
         MVC   TLDVOST2,TADVSTAT   STATUS                                       
         CLI   TADVOFF2,0          IF SECOND OFFICE CODE SPECIFIED              
         BE    YES                                                              
         CLC   TADVOFF,TADVOFF2    AND DIFFERENT FROM FIRST                     
         BE    YES                 GENERATE ANOTHER POINTER                     
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLDVPCD,TLDVOCDQ    RECORD TYPE                                  
         MVC   32(1,R3),34(R2)     TAKE ONLY FIRST STATUS FROM PARENT           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLDVPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLDVPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         MVC   TLDVOOFF,TADVOFF2   SET SECOND OFFICE CODE                       
         MVC   TLDVOST2,TADVSTAT   SET SECOND STATUS BYTE                       
         B     YES                                                              
         SPACE 2                                                                
*                                  ADVICE EARLIEST DUE DATE                     
PASSDVE  NTR1                                                                   
         MVI   TLDVPCD,TLDVEDDQ    SET RECORD CODE                              
         SPACE 1                                                                
         USING TADDD,R6                                                         
         MVI   ELCODE,TADDELQ      SET EARLIEST DUE DATE                        
         BAS   RE,GETEL                                                         
         BNE   PASSDVDU                                                         
         SPACE 1                                                                
         MVC   TLDVEDUE,TADDDATM   USE MUSIC DUE DATE                           
         OC    TADDDATE,TADDDATE   IF REGULAR DUE DATE NOT PRESENT              
         BZ    PASSDVDU                                                         
         SPACE 1                                                                
         MVC   TLDVEDUE,TADDDATE   USE REGULAR DUE DATE                         
         OC    TADDDATM,TADDDATM   IF MUSIC DUE DATE NOT PRESENT                
         BZ    PASSDVDU                                                         
         SPACE 1                                                                
         CLC   TLDVEDUE,TADDDATM   ELSE, USE EARLIEST DUE DATE                  
         BL    PASSDVDU                                                         
         MVC   TLDVEDUE,TADDDATM                                                
         B     PASSDVDU                                                         
         SPACE 2                                                                
*                                  ADVICE REGULAR DUE DATE                      
PASSDVR  NTR1                                                                   
         USING TAVUD,R6                                                         
         MVI   ELCODE,TAVUELQ      ONLY GENERATE IF ADVICE HAS                  
         BAS   RE,GETEL            A REGULAR CYCLE                              
         BNE   NO                                                               
         OC    TAVUCYC,TAVUCYC                                                  
         BZ    NO                                                               
         SPACE 1                                                                
         MVI   TLDVPCD,TLDVRDDQ    SET RECORD CODE                              
         SPACE 1                                                                
         USING TADDD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TADDELQ      SET REGULAR DUE DATE                         
         BAS   RE,GETEL                                                         
         BNE   PASSDVDU                                                         
         OC    TADDDATE,TADDDATE                                                
         BZ    NO                                                               
         MVC   TLDVRDUE,TADDDATE                                                
         B     PASSDVDU                                                         
         SPACE 2                                                                
*                                  ADVICE MUSIC DUE DATE                        
PASSDVM  NTR1                                                                   
         USING TAVUD,R6                                                         
         MVI   ELCODE,TAVUELQ      ONLY GENERATE IF ADVICE HAS                  
         BAS   RE,GETEL            A MUSIC CYCLE                                
         BNE   NO                                                               
         OC    TAVUMCYS,TAVUMCYS                                                
         BZ    NO                                                               
         SPACE 1                                                                
         MVI   TLDVPCD,TLDVMDDQ    SET RECORD CODE                              
         SPACE 1                                                                
         USING TADDD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TADDELQ      SET MUSIC DUE DATE                           
         BAS   RE,GETEL                                                         
         BNE   PASSDVDU                                                         
         OC    TADDDATM,TADDDATM                                                
         BZ    NO                                                               
         MVC   TLDVMDUE,TADDDATM                                                
         B     PASSDVDU                                                         
         DROP  R6                                                               
         SPACE 2                                                                
         USING TADVD,R6                                                         
PASSDVDU LR    R6,R2               DO NOT GENERATE FOR SESSIONS                 
         MVI   ELCODE,TADVELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         CLI   TADVTYPE,TADVTYPS                                                
         BE    NO                                                               
         SPACE 1                                                                
         LHI   RE,TLDVEIND         DEFAULT TO INCOMPLETE WITH DUE DATE          
         TM    TADVSTAT,TADVSCMP   IF COMPLETED                                 
         BZ    *+8                                                              
         LHI   RE,TLDVECMD         SET AS COMPLETE WITH DUE DATE                
         OC    TLDVEDUE,TLDVEDUE   IF NO DUE DATE                               
         BNZ   *+14                                                             
         MVC   TLDVEDUE,HEXFFS                                                  
         AHI   RE,2                ADD 2 TO THE STATUS                          
         STC   RE,TLDVESTA                                                      
         SPACE 1                                                                
         MVC   TLDVEOFF,TADVOFF    OFFICE                                       
         MVC   TLDVEAGY,TLDVAGY    AGENCY                                       
         SPACE 1                                                                
         MVC   TLDVECID,TLDVCID    COMMERCIAL                                   
         MVC   TLDVEADV,TLDVADV    ADVICE                                       
         MVC   TLDVESEQ,TLDVSEQ    SEQUENCE                                     
         MVC   TLDVOST2,TADVSTAT   STATUS                                       
         CLI   TADVOFF2,0          IF SECOND OFFICE CODE SPECIFIED              
         BE    YES                                                              
         CLC   TADVOFF,TADVOFF2    AND DIFFERENT FROM FIRST                     
         BE    YES                 GENERATE ANOTHER POINTER                     
*                                  ANOTHER POINTER SO BUMP STACK                
         MVC   32(1,R3),34(R2)     TAKE ONLY FIRST STATUS FROM PARENT           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLDVPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLDVPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         MVC   TLDVEOFF,TADVOFF2   SET SECOND OFFICE CODE                       
         MVC   TLDVOST2,TADVSTAT   SET SECOND STATUS BYTE                       
         B     YES                                                              
         EJECT                                                                  
*              NETWORK TRANSFER RECORDS                                         
         SPACE 3                                                                
         USING TLNXD,R2                                                         
         USING TLNXPD,R3                                                        
*                                  NETWORK TRANSFER BY NID NAME                 
PASSNXN  NTR1                                                                   
         CLI   TLNXSEQ,0           DON'T GENERATE IF NOT BASE RECORD            
         BNE   NO                                                               
         MVC   TLNXNAGY,TLNXAGY    AGENCY                                       
         MVC   TLNXNDTE,TLNXDATE   DATE ADDED                                   
         MVC   TLNXNSEQ,RDSKADD    SEQUENCE                                     
*                                                                               
         USING TAFND,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAFNELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PASSNXN5 BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         CLI   TAFNTYPE,TAFNTTTL                                                
         BNE   PASSNXN5                                                         
         MVC   TLNXNAME,SPACES                                                  
         LA    R1,L'TLNXNAME-1     MAX LENGTH OF NAME                           
         ZIC   RE,TAFNLEN                                                       
         SHI   RE,TAFNLNQ+1                                                     
         CR    R1,RE               COMPARE L'FIELD TO L'DATA                    
         BNH   *+6                                                              
         LR    R1,RE               FIELD IS BIGGER - MOVE FOR L'DATA            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TLNXNAME(0),TAFNNAME  NETWORK ID NAME                            
         B     YES                                                              
         SPACE 2                                                                
*                                  NETWORK TRANSFER BY 12-CHAR ID               
PASSNXC  NTR1                                                                   
         CLI   TLNXSEQ,0           DON'T GENERATE IF NOT BASE RECORD            
         BNE   NO                                                               
*                                                                               
         USING USETABD,RF                                                       
         L     RF,AUSETAB          RF=A(USE TABLE)                              
PASSNXC1 CLC   TLNXUSE,USECDE      FIND USE EQUATE IN USE TABLE                 
         BE    PASSNXC2                                                         
         LH    RE,USELEN                                                        
         AR    RF,RE                                                            
         CLI   0(RF),X'FF'                                                      
         BNE   PASSNXC1                                                         
         B     NO                                                               
*                                                                               
PASSNXC2 MVC   TLNXCAGY,TLNXAGY    AGENCY                                       
         MVC   TLNXCCID,SPACES     NETWORK ID                                   
         MVC   TLNXCCID(L'TLNXNID),TLNXNID                                      
         MVC   TLNXCCLI,TLNXNCLI   CLIENT                                       
         MVC   TLNXCPRD,TLNXNPRD   PRODUCT                                      
         MVC   TLNXCMED,TLNXMED    MEDIA                                        
         MVC   TLNXCUSE,USEEQU     USE EQUATE                                   
         MVC   TLNXCDAD,RDSKADD                                                 
         DROP  RF                                                               
*                                                                               
         USING TANXD,R6                                                         
         LR    R6,R2               IF NETWORK DETAILS ELEMENT                   
         MVI   ELCODE,TANXELQ      CONTAINS 12-CHARACTER AD-ID                  
         BAS   RE,GETEL            INSERT IT INTO KEY                           
         BNE   YES                                                              
         CLI   TANXLEN,TANXLN2Q                                                 
         BL    YES                                                              
         OC    TANXADID,TANXADID                                                
         BZ    YES                                                              
         CLC   TANXADID,SPACES                                                  
         BE    YES                                                              
         MVC   TLNXCCID,TANXADID                                                
         B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              DUE COMPANY TRACKING RECORDS                                     
         SPACE 3                                                                
         USING TLDTD,R2                                                         
         USING TLCKPD,R3                                                        
*                                  CHECK DUE COMPANY TRACKING                   
PASSDTC  NTR1                                                                   
         MVC   TLCKDSSN,TLDTSSN                                                 
         MVC   TLCKDDUC,TLDTDUC                                                 
         MVC   TLCKDDTE,TLDTDTE                                                 
         MVC   TLCKDSEQ,TLDTSEQ                                                 
         B     YES                                                              
         EJECT                                                                  
*              CAST EPISODE RECORDS                                             
         SPACE 3                                                                
         USING TLECD,R2                                                         
         USING TLECPD,R3                                                        
*                                                                               
PASSECC  NTR1                                                                   
         MVC   TLECCSSN,TLECSSN                                                 
         MVC   TLECCEPI,TLECEPI                                                 
         MVC   TLECCCOM,TLECCOM                                                 
         MVC   TLECCCAT,TLECCAT                                                 
         MVC   TLECCINV,TLECINV                                                 
         B     YES                                                              
         EJECT                                                                  
*              DUE COMPANY RECORDS                                              
         SPACE 3                                                                
         USING TLDUD,R2                                                         
         USING TLDUPD,R3                                                        
*                                                                               
PASSDCT  NTR1                                                                   
         MVI   TLDUPCD,TLDUPCDQ                                                 
         MVC   TLDUPSSN,TLDUSSN                                                 
         MVC   TLDUPDUC,TLDUDUC                                                 
         MVC   TLDUPST,TLDUSTAT                                                 
         MVI   ELCODE,TADUELQ                                                   
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TADUD,R6                                                         
         CLI   TADUTYPE,TADUTYDU                                                
         BNE   *+8                                                              
         OI    TLDUPTYP,TLDUPCMP                                                
         CLI   TADUTYPE,TADUTYAY                                                
         BNE   *+8                                                              
         OI    TLDUPTYP,TLDUPAGY                                                
         B     YES                                                              
         EJECT                                                                  
*              CONTRACT RECORDS                                                 
         SPACE 3                                                                
         USING TLCND,R2                                                         
         USING TLCNPD,R3                                                        
*                                                                               
PASSCNT  NTR1                                                                   
         MVC   TLCNPAGY,TLCNAGY    AGENCY                                       
         MVC   TLCNPCND,TLCNCNID   CONTRACT ID                                  
         MVC   TLCNPTRS,TLCNTRMS   TERM START DATE                              
         MVC   TLCNPTRE,TLCNTRME   TERM END DATE                                
         SPACE 1                                                                
         USING TAASD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAASELQ      SEE IF THERE ARE ASSIGNMENTS                 
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
PASSCNT4 MVC   TLCNPCOM,TAASCOM    INTERNAL COMMERCIAL CODE                     
         MVC   TLCNPVER,TAASVER    VERSION                                      
         BAS   RE,NEXTEL                                                        
         BNE   YES                                                              
*                                  ANOTHER POINTER SO BUMP STACK                
         MVI   TLCNPCD,TLCNPCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCNPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLCNPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PASSCNT4                                                         
         EJECT                                                                  
*              GENERAL LIST RECORDS                                             
         SPACE 3                                                                
         USING TLGLD,R2                                                         
         USING TLGLPD,R3                                                        
*                                                                               
PASSGLN  NTR1                                                                   
         MVC   TLGLNTYP,TLGLTYPE                                                
         MVC   TLGLNCOD,TLGLLST                                                 
         MVI   ELCODE,TANAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TANAD,R6                                                         
         ZIC   R1,TANALEN                                                       
         SHI   R1,2                                                             
         CHI   R1,22                                                            
         BNH   *+8                                                              
         LHI   R1,22                                                            
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     YES                                                              
         MVC   TLGLNNAM(0),TANANAME                                             
         EJECT                                                                  
         USING TLOGD,R2                                                         
         USING TLOGPD,R3                                                        
*                                  COMMERCIAL GROUP NAME                        
PASSCGR  NTR1                                                                   
         MVC   TLOGNAGY,TLOGAGY                                                 
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLOGNAME,WORK                                                    
         MVC   TLOGNSEQ,RDSKADD                                                 
         B     YES                                                              
         EJECT                                                                  
         USING TLPTD,R2                                                         
         USING TLPTPD,R3                                                        
*                                  PRODUCT TYPE NAME                            
PASSPTN  NTR1                                                                   
         MVC   TLPTNAGY,TLPTAGY                                                 
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLPTNAME,WORK                                                    
         MVC   TLPTNSEQ,RDSKADD                                                 
         B     YES                                                              
         EJECT                                                                  
         USING TLMTD,R2                                                         
         USING TLMTPD,R3                                                        
*                                  MARKET NAME                                  
PASSMNM  NTR1                                                                   
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLMTNMNM,WORK                                                    
         MVC   TLMTNMTY,TLMTTYPE                                                
         MVC   TLMTNMCD,TLMTCODE                                                
         B     YES                                                              
*                                  MARKET ALPHA CODE                            
PASSMAC  NTR1                                                                   
         CLI   TLMTTYPE,C'T'                                                    
         BNE   NO                                                               
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLMTALIN,TLMTINUM                                                
         MVC   TLMTALAL,WORK                                                    
*                                                                               
         LA    R6,TLMTALAL                                                      
         LHI   RE,L'TLMTALAL                                                    
PMAC10   CLI   0(R6),C' '                                                       
         BNE   *+8                                                              
         MVI   0(R6),0                                                          
         LA    R6,1(R6)                                                         
         BCT   RE,PMAC10                                                        
         B     YES                                                              
*                                  INTERNAL MARKET CODE                         
PASSMIC  NTR1                                                                   
         MVC   TLMTICIC,TLMTINUM                                                
         MVC   TLMTICCD,TLMTCODE                                                
         CLI   TLMTTYPE,C'T'                                                    
         BNE   YES                                                              
         BAS   RE,GETSHORT                                                      
         BNE   NO                                                               
         MVC   TLMTICCD,WORK                                                    
*                                                                               
         LA    R6,TLMTICCD                                                      
         LHI   RE,L'TLMTICCD                                                    
PMIC10   CLI   0(R6),C' '                                                       
         BNE   *+8                                                              
         MVI   0(R6),0                                                          
         LA    R6,1(R6)                                                         
         BCT   RE,PMIC10                                                        
         B     YES                                                              
         EJECT                                                                  
         USING TLMUD,R6                                                         
         USING TLMUPD,R3                                                        
*                                  PMUSIC PASSIVE                               
PASSPMU  NTR1                                                                   
         OC    TLMUAGY,TLMUAGY     OLD STYLE MUSIC RECORD?                      
         BNZ   NO                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLMUPCMP,WORK       COMPOSITION NAME                             
         MVC   TLMUPMUS,TLMUMUS    MUSIC #                                      
         B     YES                                                              
         EJECT                                                                  
         USING TLUHD,R6                                                         
         USING TLUHPD,R3                                                        
*                                  USAGE HISTORY CNET/MKT/CSYS                  
PASSUMT  NTR1                                                                   
         LA    R4,PUMTTAB          ONLY ADD THIS POINTER IF USE                 
PUMT10   CLI   0(R4),X'FF'         IS IN TABLE                                  
         BE    NO                                                               
         CLC   TLUHUSE,0(R4)                                                    
         BE    PUMT20                                                           
         LA    R4,L'PUMTTAB(R4)                                                 
         B     PUMT10                                                           
         SPACE 1                                                                
PUMT20   MVC   TLUHMCOM,TLUHCOM    COPY INTERNAL COMMERCIAL NUMBER              
         MVC   TLUHMSEQ,TLUHCSEQ   CAST INPUT SEQUENCE NUMBER                   
         MVC   TLUHMUSE,TLUHUSE    USE CODE                                     
         MVC   TLUHMINV,TLUHINV    AND INVOICE NUMBER                           
         SPACE 1                                                                
         USING TAUHD,R6                                                         
         MVI   ELCODE,TAUHELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   TLUHMTYP,TAUHTYPE   COPY USE TYPE                                
         MVC   TLUHMCCS,TAUHSTRT   COMMERCIAL CYCLE START                       
         SPACE 1                                                                
         ZIC   RE,3(R4)            COPY CREDIT STATUS                           
         AR    RE,R6                                                            
         MVC   TLUHMSTA,0(RE)                                                   
         SPACE 1                                                                
         USING TAVRD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAVRELQ      COPY VERSION LETTER                          
         BAS   RE,GETEL                                                         
         BNE   PUMT30                                                           
         MVC   TLUHMVER,TAVRVERS                                                
         SPACE 1                                                                
         USING TAMTD,R6                                                         
PUMT30   LR    R6,R2                                                            
         MVI   ELCODE,TAMTELQ      ONLY GENERATE IF CNET/MKT/CSYS               
         BAS   RE,GETEL            IS ON THIS INVOICE                           
         BNE   NO                                                               
         SPACE 1                                                                
PUMT40   MVC   TLUHMINM,TAMTINUM   INSERT CNET/MKT/CSYS INTERNAL CODE           
         MVC   TLUHMC1D,TAMTCYCS   FIRST USE DATE                               
         MVC   TLUHMCYE,TAMTCYCE   AND CNET/MKT/CSYS END DATE                   
         SPACE 1                                                                
         BAS   RE,NEXTEL           IF THERE'S ANOTHER CNET/MKT/CSYS             
         BNE   YES                 BUMP STACK                                   
         MVI   TLUHPCD,TLUHMTDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLUHPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLUHPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PUMT40                                                           
         SPACE 2                                                                
*                                  TABLE OF USES THAT REQUIRE                   
PUMTTAB  DC    0CL4                THE USAGE HISTORY PASSIVE POINTER            
         DC    CL3'CBL',AL1(TAUHCSTA-TAUHD)                                     
         DC    CL3'SCB',AL1(TAUHCSTA-TAUHD)                                     
         DC    CL3'WSP',AL1(TAUHSTAT-TAUHD)                                     
         DC    CL3'SWS',AL1(TAUHSTAT-TAUHD)                                     
         DC    CL3'WSC',AL1(TAUHSTAT-TAUHD)                                     
         DC    CL3'ADW',AL1(TAUHSTAT-TAUHD)                                     
         DC    CL3'LCB',AL1(TAUHLCST-TAUHD)                                     
         DC    X'FF'                                                            
         EJECT                                                                  
*              VERSION                                                          
         SPACE 2                                                                
         USING TLVRD,R2                                                         
         USING TLCOPD,R3                                                        
*                                  VERSION NAME                                 
PASSVRN  NTR1                                                                   
         USING TAFND,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAFNELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PASSVRN5 BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         CLI   TAFNTYPE,TAFNTCLI                                                
         BNE   PASSVRN5                                                         
         MVC   TLCONCLI,TAFNNAME                                                
         DROP  R6                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLCONAME,WORK                                                    
         MVC   TLCONVER,TLVRVER                                                 
         MVC   TLCONCOM,TLVRCOM                                                 
         B     YES                                                              
         EJECT                                                                  
*              VERSION MUSIC PASSIVE                                            
PASSVMU  NTR1                                                                   
         BRAS  RE,PASSVMU0                                                      
         B     XIT                                                              
*              TASK                                                             
         SPACE 2                                                                
         USING TLTKD,R2                                                         
         USING TLTKPD,R3                                                        
*                                  TASK NAME                                    
PASSTKN  NTR1                                                                   
         CLI   TLTKSCD,TLTKSCDQ                                                 
         BNE   NO                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLTKNAME,WORK                                                    
         MVC   TLTKNTSK,TLTKTASK                                                
         MVI   TLTKNSCD,TLTKSCDQ                                                
         B     YES                                                              
         EJECT                                                                  
*              PAYMENT TYPE                                                     
         SPACE 2                                                                
         USING TLPMD,R2                                                         
         USING TLPMPD,R3                                                        
*                                  PAYMENT TYPE NAME                            
PASSPMN  NTR1                                                                   
         CLI   TLPMSCD,TLPMSCDQ                                                 
         BNE   NO                                                               
         BAS   RE,GETNAME                                                       
         BNE   NO                                                               
         MVC   TLPMNAME,WORK                                                    
         MVC   TLPMNPTY,TLPMPTYP                                                
         MVI   TLPMNSCD,TLPMSCDQ                                                
         B     YES                                                              
         EJECT                                                                  
*              ODDMENTS, LTORG                                                  
         SPACE 3                                                                
GETSHORT NTR1                                                                   
         LR    R6,R2                                                            
         MVI   ELCODE,TASNELQ                                                   
         MVC   WORK,SPACES                                                      
         BAS   RE,GETEL                                                         
         BNE   GETNAME2                                                         
         USING TASND,R6                                                         
         MVC   WORK(16),TASNAME                                                 
         B     YES                                                              
         SPACE 1                                                                
GETNAME  NTR1                                                                   
GETNAME2 LR    R6,R2                                                            
         MVI   ELCODE,TANAELQ                                                   
         MVC   WORK,SPACES                                                      
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TANAD,R6                                                         
         ZIC   R1,TANALEN                                                       
         AHI   R1,-3                                                            
         EX    R1,*+8                                                           
         B     YES                                                              
         MVC   WORK(0),TANANAME                                                 
         EJECT                                                                  
         GETEL (R6),DATADISP,ELCODE                                             
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
DATADISP DC    H'40'                                                            
SPACES   DC    CL48' '                                                          
SEQ      DC    F'0'                                                             
HEXFFS   DC    8X'FF'                                                           
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 3                                                                
       ++INCLUDE TASYSNAHLD                                                     
         EJECT                                                                  
PPLIST   DS    0D                                                               
         DC    AL1(TLSTCDQ),AL1(TLSTNCDQ),X'0000',A(PASSSTN)                    
         DC    AL1(TLSTCDQ),AL1(TLSTCCDQ),X'0000',A(PASSSTC)                    
         DC    AL1(TLAYCDQ),AL1(TLAYNCDQ),X'0000',A(PASSAYN)                    
         DC    AL1(TLAYCDQ),AL1(TLAYGCDQ),X'0000',A(PASSAYG)                    
         DC    AL1(TLAYCDQ),AL1(TLBRCCDQ),X'0000',A(PASSAYC)                    
         DC    AL1(TLAYCDQ),AL1(TLBRNCDQ),X'0000',A(PASSAYB)                    
         DC    AL1(TLAGCDQ),AL1(TLAGNCDQ),X'0000',A(PASSAGN)                    
         DC    AL1(TLCLCDQ),AL1(TLCLNCDQ),X'0000',A(PASSCLN)                    
         DC    AL1(TLCLCDQ),AL1(TLCLGCDQ),X'0000',A(PASSCLG)                    
         DC    AL1(TLCLCDQ),AL1(TLBRCCDQ),X'0000',A(PASSCLC)                    
         DC    AL1(TLCLCDQ),AL1(TLBRNCDQ),X'0000',A(PASSCLB)                    
         DC    AL1(TLCGCDQ),AL1(TLCGNCDQ),X'0000',A(PASSCGN)                    
         DC    AL1(TLPRCDQ),AL1(TLPRNCDQ),X'0000',A(PASSPRN)                    
         DC    AL1(TLPRCDQ),AL1(TLPRGCDQ),X'0000',A(PASSPRG)                    
         DC    AL1(TLPRCDQ),AL1(TLPRTCDQ),X'0000',A(PASSPRT)                    
         DC    AL1(TLPGCDQ),AL1(TLPGNCDQ),X'0000',A(PASSPGN)                    
         DC    AL1(TLCOCDQ),AL1(TLCONCDQ),X'0000',A(PASSCON)                    
         DC    AL1(TLCOCDQ),AL1(TLCOICDQ),X'0000',A(PASSCOI)                    
         DC    AL1(TLCOCDQ),AL1(TLCOMCDQ),X'0000',A(PASSCOM)                    
         DC    AL1(TLCOCDQ),AL1(TLCOPCDQ),X'0000',A(PASSCOP)                    
         DC    AL1(TLCOCDQ),AL1(TLCAHCDQ),X'0000',A(PASSCOAH)                   
         DC    AL1(TLCOCDQ),AL1(TLCOCCDQ),X'0000',A(PASSCOC)                    
         DC    AL1(TLCOCDQ),AL1(TLCOGCDQ),X'0000',A(PASSCOG)                    
         DC    AL1(TLCOCDQ),AL1(TLCOLCDQ),X'0000',A(PASSCOL)                    
         DC    AL1(TLCOCDQ),AL1(TLCOACDQ),X'0000',A(PASSCOA)                    
         DC    AL1(TLCOCDQ),AL1(TLCOKCDQ),X'0000',A(PASSCOK)                    
         DC    AL1(TLCOCDQ),AL1(TLCOVRDQ),X'0000',A(PASSCOV)                    
         DC    AL1(TLCOCDQ),AL1(TLCOGIDQ),X'0000',A(PASSCGI)                    
         DC    AL1(TLCOCDQ),AL1(TLCOGNDQ),X'0000',A(PASSCGA)                    
         DC    AL1(TLCOCDQ),AL1(TLCOBCDQ),X'0000',A(PASSCOB)                    
         DC    AL1(TLCOCDQ),AL1(TLCOTCDQ),X'0000',A(PASSCOT)                    
         DC    AL1(TLCOCDQ),AL1(TLCOWCDQ),X'0000',A(PASSCOW)                    
         DC    AL1(TLCOCDQ),AL1(TLCOOCDQ),X'0000',A(PASSCOO)                    
         DC    AL1(TLCOCDQ),AL1(TLCOECDQ),X'0000',A(PASSCOE)                    
         DC    AL1(TLANCDQ),AL1(TLANNCDQ),X'0000',A(PASSANN)                    
         DC    AL1(TLANCDQ),AL1(TLANCCDQ),X'0000',A(PASSANC)                    
         DC    AL1(TLANCDQ),AL1(TLANBCDQ),X'0000',A(PASSANB)                    
         DC    AL1(TLANCDQ),AL1(TLANSCDQ),X'0000',A(PASSANS)                    
         DC    AL1(TLW4CDQ),AL1(TLW4NCDQ),X'0000',A(PASSW4N)                    
         DC    AL1(TLW4CDQ),AL1(TLW4CCDQ),X'0000',A(PASSW4C)                    
         DC    AL1(TLW4CDQ),AL1(TLW4OCDQ),X'0000',A(PASSW4O)                    
         DC    AL1(TLW4CDQ),AL1(TLW4LCDQ),X'0000',A(PASSW4L)                    
         DC    AL1(TLLNCDQ),AL1(TLLNRCDQ),X'0000',A(PASSLNR)                    
         DC    AL1(TLCACDQ),AL1(TLCACCDQ),X'0000',A(PASSCAC)                    
         DC    AL1(TLCACDQ),AL1(TLCAACDQ),X'0000',A(PASSCAA)                    
         DC    AL1(TLCACDQ),AL1(TLCAGCDQ),X'0000',A(PASSCAG)                    
         DC    AL1(TLCACDQ),AL1(TLCAHCDQ),X'0000',A(PASSCAH)                    
         DC    AL1(TLCACDQ),AL1(TLCATCDQ),X'0000',A(PASSCAT)                    
         DC    AL1(TLCACDQ),AL1(TLCANCDQ),X'0000',A(PASSCAN)                    
         DC    AL1(TLHCCDQ),AL1(TLINHCDQ),X'0000',A(PASSINH)                    
         DC    AL1(TLINCDQ),AL1(TLINHCDQ),X'0000',A(PASSINH)                    
         DC    AL1(TLINCDQ),AL1(TLINBCDQ),X'0000',A(PASSINB)                    
         DC    AL1(TLINCDQ),AL1(TLINCCDQ),X'0000',A(PASSINC)                    
         DC    AL1(TLINCDQ),AL1(TLINDCDQ),X'0000',A(PASSIND)                    
         DC    AL1(TLINCDQ),AL1(TLINKCDQ),X'0000',A(PASSINK)                    
         DC    AL1(TLINCDQ),AL1(TLINPCDQ),X'0000',A(PASSINP)                    
         DC    AL1(TLINCDQ),AL1(TLINWCDQ),X'0000',A(PASSINW)                    
         DC    AL1(TLINCDQ),AL1(TLINOCDQ),X'0000',A(PASSINO)                    
         DC    AL1(TLINCDQ),AL1(TLINECDQ),X'0000',A(PASSIET)                    
         DC    AL1(TLINCDQ),AL1(TLINRCDQ),X'0000',A(PASSINR)                    
         DC    AL1(TLCKCDQ),AL1(TLCKCCDQ),X'0000',A(PASSCKC)                    
         DC    AL1(TLCKCDQ),AL1(TLCKECDQ),X'0000',A(PASSCKE)                    
         DC    AL1(TLCKCDQ),AL1(TLCKKCDQ),X'0000',A(PASSCKK)                    
         DC    AL1(TLCKCDQ),AL1(TLCKYCDQ),X'0000',A(PASSCKY)                    
         DC    AL1(TLCKCDQ),AL1(TLCKBCDQ),X'0000',A(PASSCKB)                    
         DC    AL1(TLCKCDQ),AL1(TLCKHCDQ),X'0000',A(PASSCKH)                    
         DC    AL1(TLCKCDQ),AL1(TLCKDCDQ),X'0000',A(PASSCKD)                    
         DC    AL1(TLCKCDQ),AL1(TLCKLCDQ),X'0000',A(PASSCKL)                    
         DC    AL1(TLCKCDQ),AL1(TLCKCSSQ),X'0000',A(PASSCSS)                    
         DC    AL1(TLCKCDQ),AL1(TLCKCSCQ),X'0000',A(PASSCSC)                    
         DC    AL1(TLCKCDQ),AL1(TLCKCPSQ),X'0000',A(PASSCPS)                    
         DC    AL1(TLCKCDQ),AL1(TLCKCPCQ),X'0000',A(PASSCPC)                    
         DC    AL1(TLCKCDQ),AL1(TLCKRCDQ),X'0000',A(PASSCKR)                    
         DC    AL1(TLDVCDQ),AL1(TLDVOCDQ),X'0000',A(PASSDVO)                    
         DC    AL1(TLDVCDQ),AL1(TLDVEDDQ),X'0000',A(PASSDVE)                    
         DC    AL1(TLDVCDQ),AL1(TLDVRDDQ),X'0000',A(PASSDVR)                    
         DC    AL1(TLDVCDQ),AL1(TLDVMDDQ),X'0000',A(PASSDVM)                    
         DC    AL1(TLNXCDQ),AL1(TLNXNCDQ),X'0000',A(PASSNXN)                    
         DC    AL1(TLNXCDQ),AL1(TLNXCCDQ),X'0000',A(PASSNXC)                    
         DC    AL1(TLDTCDQ),AL1(TLCKDCDQ),X'0000',A(PASSDTC)                    
         DC    AL1(TLECCDQ),AL1(TLECCCDQ),X'0000',A(PASSECC)                    
         DC    AL1(TLGLCDQ),AL1(TLGLNCDQ),X'0000',A(PASSGLN)                    
         DC    AL1(TLDUCDQ),AL1(TLDUPCDQ),X'0000',A(PASSDCT)                    
         DC    AL1(TLCNCDQ),AL1(TLCNPCDQ),X'0000',A(PASSCNT)                    
         DC    AL1(TLOGCDQ),AL1(TLOGNCDQ),X'0000',A(PASSCGR)                    
         DC    AL1(TLPTCDQ),AL1(TLPTNCDQ),X'0000',A(PASSPTN)                    
         DC    AL1(TLMTCDQ),AL1(TLMTNMDQ),X'0000',A(PASSMNM)                    
         DC    AL1(TLMTCDQ),AL1(TLMTALDQ),X'0000',A(PASSMAC)                    
         DC    AL1(TLMTCDQ),AL1(TLMTICDQ),X'0000',A(PASSMIC)                    
         DC    AL1(TLMUCDQ),AL1(TLMUPCDQ),X'0000',A(PASSPMU)                    
         DC    AL1(TLUHCDQ),AL1(TLUHMTDQ),X'0000',A(PASSUMT)                    
         DC    AL1(TLVRCDQ),AL1(TLCONCDQ),X'0000',A(PASSVRN)                    
         DC    AL1(TLVRCDQ),AL1(TLCOTCDQ),X'0000',A(PASSCOT)                    
         DC    AL1(TLVRCDQ),AL1(TLCOMCDQ),X'0000',A(PASSVMU)                    
         DC    AL1(TLVRCDQ),AL1(TLCOOCDQ),X'0000',A(PASSVOO)                    
         DC    AL1(TLTKCDQ),AL1(TLTKNCDQ),X'0000',A(PASSTKN)                    
         DC    AL1(TLPMCDQ),AL1(TLPMNCDQ),X'0000',A(PASSPMN)                    
         DC    X'FF'                                                            
         EJECT                                                                  
*              VERSION MUSIC PASSIVE                                            
         SPACE 2                                                                
         USING TLVRD,R2                                                         
         USING TLCOPD,R3                                                        
*                                                                               
PASSVMU0 NTR1  BASE=*,LABEL=*                                                   
         USING TAFND,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TAFNELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PASSVMU2 BRAS  RE,NEXTEL                                                        
         JNE   NO                                                               
         CLI   TAFNTYPE,TAFNTAGY                                                
         JNE   PASSVMU2                                                         
         ZIC   RF,TAFNLEN                                                       
         SHI   RF,TAFNLNQ                                                       
         SHI   RF,1                                                             
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   TLCOMAGY(0),TAFNNAME   AGENCY                                    
         OC    TLCOMAGY,SPACES                                                  
         DROP  R6                                                               
*                                                                               
         USING TACOD,R6                                                         
         LR    R6,R2                                                            
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         MVC   TLCOMCID,TACOCID    COMMERCIAL CODE                              
         DROP  R6                                                               
*                                                                               
         MVC   TLCOMVER,TLVRVER    VERSION                                      
*                                                                               
         USING TACPD,R6                                                         
         MVC   WORK,TLCOPKEY                                                    
         LR    R6,R2                                                            
         MVI   ELCODE,TACPELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         MVC   TLCOMMUS,TACPMUS    MUSIC CODE                                   
*                                                                               
PASSVMU4 BRAS  RE,NEXTEL                                                        
         JNE   YES                                                              
*                                                                               
         MVI   TLCOPCD,TLCOMCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLCOPKEY,WORK       PRE-SET TO SAVED KEY                         
         MVC   TLCOMMUS,TACPMUS    MUSIC CODE                                   
         J     PASSVMU4                                                         
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
* GENERATE YTD PASSIVE KEYS FOR P+ CANADIANS                                    
*                                                                               
* ON ENTRY, R4 = A(LIST OF UNITS)                                               
*                                                                               
         USING TLCKD,R2                                                         
         USING TLCKPD,R3                                                        
                                                                                
PKCAN    NTR1  BASE=*,LABEL=*                                                   
         LR    R6,R2                                                            
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         USING TACAD,R6                                                         
         MVC   THREE,TACAUNIT      SAVE PERFORMER'S TAXED UNIT                  
                                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PKCANX                                                           
         USING TATUD,R6                                                         
                                                                                
PKCAN10  MVC   TLCKYTXU,TATUUNIT   FILL IN TAX UNIT                             
         CLC   TATUUNIT,=C'CN '                                                 
         BE    PKCAN15                                                          
         CLC   TATUUNIT,=C'FD '                                                 
         BE    PKCAN15                                                          
         MVC   TLCKYTXU,THREE      EVTIME UNIT DNE TAXED UNIT                   
PKCAN15  MVC   0(3,R4),TLCKYTXU    SAVE UNIT TO PREVENT DUPLICATES              
         AHI   R4,3                                                             
                                                                                
         CLC   TLCKYTXU,=C'FD '                                                 
         BNE   PKCAN20                                                          
         OI    CKYSTAT,CKYSFED                                                  
                                                                                
PKCAN20  BRAS  RE,NEXTEL                                                        
         BNE   PKCANX                                                           
         LA    R1,WORK             INSURE WE HAVEN'T SEEN THIS UNIT YET         
PKCAN25  CLI   0(R1),0                                                          
         BE    PKCAN30                                                          
         CLC   TATUUNIT,0(R1)                                                   
*        CLC   TLCKYTXU,0(R1)                                                   
         BE    PKCAN20                                                          
         LA    R1,3(R1)                                                         
         B     PKCAN25                                                          
                                                                                
PKCAN30  MVI   TLCKPCD,TLCKYCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLCKPKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)                                                        
         ST    R3,SAVER3                                                        
         MVC   TLCKPKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         B     PKCAN10                                                          
PKCANX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO TRANSLATE AGENT CODE                              *         
*        INCLUDED BY TASYSVAL AND TALNK01                             *         
*        ON ENTRY ... P1 BYTE 0 = X'80' CHARACTER TO NUMERIC          *         
*                                 X'40' NUMERIC TO CHARACTER          *         
*                     P1        = A(INCOMING AGENT CODE)              *         
*                     P2        = A(TRANSLATED AGENT CODE)            *         
***********************************************************************         
                                                                                
TRNSAGT  NTR1  BASE=*,LABEL=*                                                   
       ++INCLUDE TATRNSAGT                                                      
         EJECT                                                                  
***********************************************************************         
         USING TLIND,R2                                                         
         USING TLINPD,R3                                                        
PRETINV  NTR1  BASE=*,LABEL=*                                                   
         BAS   RE,PIRT                                                          
         JNE   NO                                                               
                                                                                
         MVC   TLINRAGY,TLINAGY                                                 
         MVC   TLINROIN,RET4INV                                                 
         MVC   TLINRRIN,TLININV                                                 
         XC    TLINRRIN,=6X'FF'                                                 
         J     YES                                                              
         DROP  R2,R3                                                            
                                                                                
PIRT     NTR1                                                                   
         LR    R6,R2                                                            
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TAPDD,R6                                                         
         TM    TAPDOPT3,TAPDORET   HAS TO BE A RETRO PAYMENT                    
         JZ    NO                                                               
         DROP  R6                                                               
                                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TAIND,R6                                                         
         TM    TAINSTAT,TAINSCAN   SKIP CANCELLED INVOICE                       
         JO    NO                                                               
         DROP  R6                                                               
                                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,TANUELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PIRT100  BAS   RE,NEXTEL                                                        
PIRT150  JNE   NO                                                               
         USING TANUD,R6                                                         
         CLI   TANUTYPE,TANURT4I   HAS TO HAVE RETRO FOR INVOICE                
         JNE   PIRT100                                                          
         MVC   RET4INV,TANUMBER    SAVE IT                                      
         J     YES                                                              
         DROP  R6                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
         USING TLW4D,R2                                                         
         USING TLW4PD,R3                                                        
PASSW4LX NTR1  BASE=*,LABEL=*                                                   
         MVI   ELCODE,TAFLELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   PASSW4L2                                                         
         USING TAFLD,R6                                                         
         CLI   TAFLFLT1,TA99TYB                                                 
         JE    PASSW4L1                                                         
         CLI   TAFLFLT1,TA99TYC                                                 
         JE    PASSW4L1                                                         
         CLI   TAFLFLT1,TA99TYF                                                 
         JE    PASSW4L1                                                         
         CLI   TAFLFLT1,TA99TYL                                                 
         JE    PASSW4L1                                                         
         CLI   TAFLFLT1,TA99TYP                                                 
         JNE   PASSW4L2                                                         
         SPACE 1                                                                
PASSW4L1 MVC   TLW4LFLT,TAFLFLT1                                                
         DROP  R6                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAW4ELQ                                                   
         LR    R6,R2                                                            
         BRAS  RE,GETEL                                                         
         JNE   PASSW4L2                                                         
         USING TAW4D,R6                                                         
         CLI   TAW4TYPE,TAW4TYCO                                                
         JNE   PASSW4L2                                                         
         DROP  R6                                                               
         SPACE 1                                                                
         MVC   TLW4LFID,TLW4FID                                                 
         J     YES                                                              
         SPACE 1                                                                
PASSW4L2 CLI   TLW4LFLT,TA99TYF                                                 
         JE    NO                                                               
         SPACE 1                                                                
         MVI   ELCODE,TAW4ELQ                                                   
         LR    R6,R2                                                            
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TAW4D,R6                                                         
         CLI   TAW4TYPE,TAW4TYES                                                
         JNE   NO                                                               
         DROP  R6                                                               
         SPACE 1                                                                
         MVI   ELCODE,TATIELQ                                                   
         LR    R6,R2                                                            
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TATID,R6                                                         
PASSW4L3 MVC   TLW4LFID,TATIID                                                  
         MVC   TLW4LEST,TLW4FID                                                 
         BRAS  RE,NEXTEL                                                        
         JNE   YES                                                              
         MVI   TLW4PCD,TLW4LCDQ    RECORD TYPE                                  
         MVC   32(2,R3),34(R2)     STATUS FROM PARENT                           
         XC    34(36,R3),34(R3)    CLEAR D/A AND NEXT KEY                       
         MVC   LASTKEY,TLW4PKEY    SAVE CURRENT KEY                             
         LA    R3,38(R3)           BUMP TO NEXT SLOT                            
         ST    R3,SAVER3                                                        
         MVC   TLW4PKEY,LASTKEY    PRE-SET TO SAVED KEY                         
         J     PASSW4L3                                                         
         DROP  R2,R3,R6                                                         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
         USING TLCOD,R2                                                         
         USING TLCOPD,R3                                                        
PCOE     NTR1  BASE=*,LABEL=*                                                   
         CLI   TLCOVER,TLCOV026                                                 
         JNE   NO                                                               
         LR    R6,R2                                                            
         MVI   ELCODE,TAACELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PCOE10   BRAS  RE,NEXTEL                                                        
         JNE   NO                                                               
         USING TAACD,R6                                                         
         CLC   TAACCDTE,=X'A40101'  START WITH 2004                             
         JL    PCOE10                                                           
         MVC   TLCOECOM,TLCOCOM                                                 
         J     YES                                                              
         DROP  R2,R3,R6                                                         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*              INVOICE RECORDS - BOVER POINTERS                                 
         SPACE 1                                                                
         USING TLINPD,R3                                                        
PASSINO0 NTR1  BASE=*,LABEL=*                                                   
         MVI   ELCODE,TARAELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TARAD,R6                                                         
         CLI   TARASTA2,0                                                       
         JE    NO                                                               
         DROP  R6                                                               
         SPACE 1                                                                
         USING TLIND,R2                                                         
         MVC   TLINOAGY,TLINAGY                                                 
         MVC   TLINOINV,TLININV                                                 
         XC    TLINOINV,HEXFFS                                                  
         J     YES                                                              
         DROP  R2,R3                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER LOCAL WORKING STORAGE                             
         SPACE 2                                                                
WRKD     DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
WORK     DS    CL48                                                             
         ORG   WORK                                                             
WORKEXT  DS    CL85                                                             
SAVER3   DS    F                                                                
RELO     DS    F                                                                
ELCODE   DS    CL1                                                              
P1BYTE0  DS    XL1                                                              
SAVOPT3  DS    XL1                                                              
LASTKEY  DS    CL32                                                             
TODAY    DS    PL3                                                              
PRI2YR   DS    PL3                                                              
DATCON   DS    V                                                                
ADDAY    DS    V                                                                
ACATTAB  DS    A                                                                
AYRSTAB  DS    A                                                                
PROTOFF  DS    V                                                                
PROTON   DS    V                                                                
SCANNER  DS    V                                                                
AUSETAB  DS    A                                                                
RDSKADD  DS    A                                                                
SVW4TYPE DS    CL1                                                              
BRKBYTE  DS    CL1                                                              
                                                                                
RET4INV  DS    XL6                 RETRO FOR INVOICE                            
THREE    DS    XL3                                                              
                                                                                
CKYSTAT  DS    X                   PAYROLL YTD STATUS                           
CKYSFED  EQU   X'80'               FD KEY ADDED                                 
WRKX     EQU   *                                                                
         SPACE 3                                                                
       ++INCLUDE TALDCPTRD                                                      
         EJECT                                                                  
* TAGENFILE                                                                     
* TASYSEQUS                                                                     
* TASYSDSECT                                                                    
         SPACE 2                                                                
         PRINT OFF                                                              
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TASYSDSECT                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'194TALDCPTR  10/30/19'                                      
         END                                                                    
