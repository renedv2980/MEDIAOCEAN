*          DATA SET TACKREPS   AT LEVEL 095 AS OF 05/01/02                      
*CATALP TACKREPA,*                                                              
         TITLE 'TACKREP - TALENT CHECK WRITING APPLICATION'                     
TACKREP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TACKREP,RA,R9,R8                                               
         L     RC,0(R1)            RC = A(GENCON WORKING STORAGE)               
         USING GEND,RC                                                          
         L     R7,ASUBSYSD         R7 = A(CONTROLLER WORKING STORAGE)           
         USING SUBSYSD,R7                                                       
         LA    R6,BUFF             R6 = A(LOCAL WORKING STORAGE)                
         LA    R6,8(R6)                                                         
         USING CHECKD,R6                                                        
         EJECT                                                                  
* MAIN DRIVER - THE WORK IS BROKEN UP INTO TWO PASSES.  THE FIRST PASS          
* READS AND PROCESSES ALL THE RECORDS FROM TALDIR/TALFIL THAT MUST BE           
* READ AND WRITES OUT SORTER RECORDS WITH THE INFORMATION IT EXTRACTED          
* FROM THE FILE.  ADDITIONALLY, IT CALLS ALLTAX TO GET THE TAX AMOUNTS          
* FOR EACH TAX UNIT, AND IT SAVES THOSE VALUES IN THE SORTER RECORD AS          
* WELL.  THE SECOND PASS READS BACK THE SORTER RECORDS AND FROM THE             
* INFORMATION THEY CONTAIN PRINTS CHECKS AND WRITES CHECK RECORDS.              
*                                                                               
MAIN     DS    0H                                                               
         GOTO1 INITIAL,DMCB,0      INITIALIZE SYSTEM WORKING STORAGE            
*                                                                               
         CLI   MODE,PRINTREP       DO ALL THROUGH MODE PRINTREP                 
         BNE   MX                                                               
         BAS   RE,INITADDR         INITIALIZE ADDRESSES                         
*                                  READ SYSTEM CONTROL RECORD                   
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
         BNE   ERRSYS                                                           
         L     R4,AIO                                                           
         MVI   ELCODE,TASYELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRSYS                                                           
         GOTO1 INITCTR             INITIALIZE CONTROL VARIABLES                 
*                                                                               
         GOTO1 =A(VINITABS)        INITIALIZE LCLTAB AND OFFTAB                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',CHECKD),SRTREC-CHECKD,=C'CHECKD'              
*                                                                               
         GOTO1 =V(ILBOSTP0)        INITIALIZE ALLTAX MODULE                     
*                                                                               
         LA    R2,SRTKEYL          INSERT SORT KEY LENGTH INTO SORTCARD         
         LA    R3,SORTCARD+15                                                   
         EDIT  (R2),(2,(R3))                                                    
*                                  SET L'SORT RECORD IN RECCARD                 
         LA    R2,SRTRECL                                                       
         CVD   R2,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RECCARD+21(4),DUB+5(3)                                           
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',SORTCARD),160,=C'SORTCARD'                    
*                                                                               
         GOTO1 VSORTER,DMCB,SORTCARD,RECCARD  OPEN SORTER FILE                  
*                                                                               
         XC    TGTHREE,TGTHREE     CLEAR START DATE                             
         MVC   END1,REQEND1        SET REQUESTED END DATE                       
         BAS   RE,CHKPASS1         CHECK RECORDS PASS 1 - PUT SORTRECS          
*                                                                               
         CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    M50                     R(ECALC) THEN SKIP                       
         CLI   RERUN,C'R'                                                       
         BE    M50                                                              
         OC    REQASOF,REQASOF     IF AS/OF DATE REQUESTED                      
         BZ    M50                                                              
         MVC   TGTHREE,REQASOF     SET TO START AT THAT DATE                    
         MVC   END1,XFFS           AND TO GO UNTIL END                          
         BAS   RE,CHKPASS1         CHECK RECORDS PASS 1 - PUT SORTRECS          
         MVC   END1,REQEND1        RESET REQ. END DATE                          
*                                                                               
M50      L     RF,NEXTERI          MARK END OF ERROR INVOICE TABLE              
         MVI   0(RF),X'FF'                                                      
*                                  CHECK RECORDS PASS 2 - GET SORTRECS          
         GOTO1 =V(TACKPRT),DMCB,(RC)                                            
MX       B     XIT                                                              
         EJECT                                                                  
* INITIALIZE ADDRESSES OF ROUTINES, TABLES, AND IOAREAS.                        
*                                                                               
INITADDR NTR1                                                                   
         L     RF,ATWA             RF = A(TWA)                                  
         USING T703FFD,RF                                                       
*                                                                               
         MVC   AMASTD,TWAMASTC     SAVE A(MASTCON DSECT)                        
         MVC   ORIGID,TWAORIG           SIGN-ON ID IN HEX                       
         MVC   AGYALPH,TWAAGY           SIGN-ON ID IN ALPHANMERIC               
*                                                                               
         L     RE,TWADCONS         SAVE ADDRESS OF TWADCON ROUTINES             
         USING TWADCOND,RE                                                      
         MVC   BINSRCH,TBINSRCH                                                 
         DROP  RE                                                               
*                                                                               
         LR    RE,RF               SAVE ADDRESSES OF SAVED PP BLOCK             
         A     RE,=A(6144)             AND ADD PP BLOCK - STARTING              
         ST    RE,ASPBLK               AT 6144 PAST BEGIN OF TWA AND            
         A     RE,=A(PPBLOCKL)         SPANNING A TOTAL OF 6144 BYTES           
         ST    RE,AAPBLK                                                        
         DROP  RF                                                               
*                                                                               
         L     RF,AMASTD           SAVE MASTD VARIABLES                         
         USING MASTD,RF                                                         
         MVC   RERUN,MCRERUN       MCRERUN (RERUN INDICATOR)                    
*                                                                               
         CLI   RECNUM,DC           IF RUNNING DMB&B CHECKS                      
         BNE   *+8                                                              
         MVI   MCPOSTNG,C'N'       SET TO NOT POST                              
*                                                                               
         L     RF,ASPOOLD          SAVE ADDRESS OF SPOOLD ROUTINES              
         USING SPOOLD,RF                                                        
         MVC   VSORTER,SORTER                                                   
         DROP  RF                                                               
*                                                                               
         L     RF,ACOMFACS         SAVE ADDRESS OF COMFACSD ROUTINES            
         USING COMFACSD,RF                                                      
         MVC   GETRET,CGETRET                                                   
         DROP  RF                                                               
*                                                                               
         TM    REQOPTS,REQOTAX     IF TESTING TASEGUE                           
         BZ    INITADR5                                                         
         LOAD  EP=TASEGUEA,ERRET=LOADERR  LOAD A VERSION                        
         B     INITADR8                                                         
INITADR5 LOAD  EP=TASEGUE,ERRET=LOADERR   SAVE A(LOADED PHASES)                 
INITADR8 ST    R0,TASEGUE                                                       
         LOAD  EP=TACKERI,ERRET=LOADERR                                         
         ST    R0,AERITAB                                                       
         LOAD  EP=TACKWAR,ERRET=LOADERR                                         
         ST    R0,AWARTAB                                                       
         LOAD  EP=TACKESU,ERRET=LOADERR                                         
         ST    R0,AESUTAB                                                       
         A     R0,=AL4(MAXESUS*ESULEN+BNPRMLNQ)                                 
         ST    R0,AESUINV                                                       
         LOAD  EP=TACKDUE,ERRET=LOADERR                                         
         ST    R0,ADUETAB                                                       
         LOAD  EP=TACKLIN,ERRET=LOADERR                                         
         ST    R0,ALINTAB                                                       
         LOAD  EP=TACKTRST,ERRET=LOADERR                                        
         ST    R0,ATRSTAB                                                       
         LOAD  EP=TACKACC,ERRET=LOADERR                                         
         ST    R0,AACCTAB                                                       
         LOAD  EP=TACKLCL,ERRET=LOADERR                                         
         ST    R0,ALCLTAB                                                       
         LOAD  EP=TACKPYTD,ERRET=LOADERR                                        
         ST    R0,APYTDTAB                                                      
         A     R0,=AL4(MAXPYTDS*PYTDLEN+BNPRMLNQ)                               
         ST    R0,APYTDINV                                                      
         LOAD  EP=TACKEPI,ERRET=LOADERR                                         
         ST    R0,AEPITAB                                                       
         LOAD  EP=TACKAYF,ERRET=LOADERR                                         
         ST    R0,AAYFTAB                                                       
         LOAD  EP=TACKCLF,ERRET=LOADERR                                         
         ST    R0,ACLFTAB                                                       
         B     *+6                                                              
LOADERR  DC    H'0'    COULDN'T LOAD - R0=A(PHASE NAME), RF=RETURN CODE         
*                                                                               
         MVC   GETESU,=A(VGETESU)  SAVE A(COMMON ROUTINES)                      
         MVC   GETSUC,=A(VGETSUC)                                               
         MVC   DOTRACE,=A(VDOTRACE)                                             
         MVC   ADDPTRS,=A(VADDPTRS)                                             
         MVC   NOCALC,=A(VNOCALC)                                               
         MVC   NOPRNT,=A(VNOPRNT)                                               
         MVC   ADDBIN,=A(VADDBIN)                                               
         MVC   BLDSORT,=A(VBLDSORT)                                             
         MVC   UPDPYTD,=A(VUPDPYTD)                                             
         MVC   FLUSHI,=A(VFLUSHI)                                               
         MVC   GETPYTD,=A(VGETPYTD)                                             
         MVC   INITCTR,=A(VINITCTR)                                             
         MVC   CALLTAPP,=A(VCALLTAP)                                            
*                                                                               
         ST    RB,SAVERB           SAVE BASE REGISTERS                          
         ST    RA,SAVERA                                                        
         ST    R9,SAVER9                                                        
         ST    R8,SAVER8                                                        
         ST    RC,SAVERC           SAVE RC                                      
*                                                                               
         LA    RF,SRTCHECK         SAVE A(CHECK RECORD IOAREA)                  
         ST    RF,ASRTCHK                                                       
         MVC   AINVIO,=A(INVIO)    SAVE A(INVOICE RECORD IOAREA)                
         MVC   AEMPIO,=A(EMPIO)         A(EMPLOYER RECORD IOAREA)               
         MVC   AW4IO,=A(W4IO)           A(W4 RECORD IOAREA)                     
*                                                                               
         LA    R5,BLANKACC         BUILD INITIALIZED ACCTAB LOOKUP              
         USING ACCTABD,R5              ENTRY                                    
         MVC   ACCENTRY,MYSPACES                                                
         XC    ACCAMNT,ACCAMNT                                                  
         B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
* CHECK PASS 1 - READ THROUGH THE INVOICE PASSIVE KEYS BY DUE DATE              
* AND PROCESS EACH INVOICE IN TURN.  LOWER LEVEL ROUTINES WILL EXTRACT          
* INFORMATION FROM THE INVOICE RECORDS,  LOOP THROUGH THE CHECK RECORDS         
* FOR EACH INVOICE, AND WRITE A SORT RECORD FOR EACH CHECK.                     
*                                                                               
CHKPASS1 NTR1                                                                   
         LA    R3,KEY              BUILD DUE DATE PASSIVE INVOICE KEY           
         USING TLINPD,R3                                                        
         XC    TLINPKEY,TLINPKEY                                                
         MVI   TLINPCD,TLINCCDQ                                                 
         MVC   TLINCDUE,TGTHREE    SET START DATE                               
*                                                                               
         CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    *+12                    R(ECALC)                                 
         CLI   RERUN,C'R'                                                       
         BNE   CPO5                                                             
         XC    TLINPKEY,TLINPKEY   SET TO READ VIA CHECK DATE PTRS              
         MVI   TLINPCD,TLINKCDQ                                                 
         MVC   TLINKDTE,CHKDATE1   SET CHECK DATE                               
         MVC   TLINKAGY,REQAGY     SET POSSIBLE AGENCY FILTER                   
         MVC   TLINKINV,REQINV                  INVOICE FILTER                  
*                                                                               
CPO5     GOTO1 HIGH                READ FIRST INVOICE                           
*                                                                               
CPO10    CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    CPO20                   R(ECALC) THEN SKIP                       
         CLI   RERUN,C'R'                                                       
         BE    CPO20                                                            
*                                                                               
         CLI   TLINPCD,TLINCCDQ    WHILE STILL DUE DATE POINTERS                
         BNE   CPO100                                                           
         CLC   TLINCDUE,END1       DUE DATE MUST BE <= END DATE                 
         BH    CPO100                                                           
*                                                                               
         MVC   SRTAGY,TLINCAGY     SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLINCINV          INVOICE                                 
         B     CPO30                                                            
*                                                                               
CPO20    CLI   TLINPCD,TLINKCDQ    WHILE STILL CHECK DATE POINTERS              
         BNE   CPO100                                                           
         CLC   TLINKDTE,CHKDATE1   CHECK DATE MUST BE THE SAME                  
         BNE   CPO100                                                           
*                                                                               
         MVC   SRTAGY,TLINKAGY     SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLINKINV          INVOICE                                 
*                                                                               
CPO30    MVC   SVIKEY,TLINPKEY     SAVE INVOICE KEY                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'5',TLINPKEY),38,=C'INV KEY'                      
*                                                                               
         USING TLDRD,R3                                                         
         MVC   SRTINDA,TLDRDA      SAVE DISK ADDRESS IN SORT RECORD             
*                                                                               
         XC    SRTAAGY,SRTAAGY     CLEAR ADJUSTMENT AGY/INV                     
         XC    SRTAINV,SRTAINV                                                  
*                                                                               
         OC    REQAGY,REQAGY       IF AGENCY FILTER GIVEN                       
         BZ    *+14                                                             
         CLC   SRTAGY,REQAGY       THEN ONLY TAKE THAT AGENCY                   
         BNE   CPO90                                                            
*                                                                               
         OC    REQINV,REQINV       IF INVOICE FILTER GIVEN                      
         BZ    *+14                                                             
         CLC   SRTINV,REQINV       THEN ONLY TAKE THAT INVOICE                  
         BNE   CPO90                                                            
*                                                                               
         MVI   INVTERR,0           CLEAR INVOICE ERROR TYPE                     
*                                                                               
         MVC   AIO,AINVIO          READ RECORD INTO AINVIO                      
         GOTO1 GETREC                                                           
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,AINVIO           R4 = A(INVOICE RECORD)                       
         USING TLIND,R4                                                         
         MVC   SRTAGY,TLINAGY      SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLININV           INVOICE                                 
         XC    SRTINV,XFFS                                                      
*                                                                               
         OC    REQAGY,REQAGY       IF AGY/INV FILTER REQUESTED                  
         BZ    *+14                                                             
         OC    REQINV,REQINV                                                    
         BNZ   CPO60               THEN ACCEPT INVOICE NO MATTER WHAT           
*                                                                               
         MVI   ELCODE,TAINELQ      R4 = A(INVOICE STATUS ELEMENT)               
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         TM    TAINSTAT,TAINSCHK   IF CHECKS WRITTEN BIT SET                    
         BZ    *+12                                                             
         BAS   RE,TESTRR           THEN IF NOT RERUNABLE                        
         BNE   CPO90               THEN SKIP                                    
*                                                                               
         TM    TAINSTA2,TAINSADJ   IF NOT PAYROLL ADJUSTMENT INVOICE            
         BO    *+12                                                             
         TM    TAINSTAT,TAINSAPR   THEN SKIP IF NOT QC APPROVED                 
         BZ    CPO90                                                            
*                                  SKIP IF IN ERROR, HELD, OR CANCELLED         
         TM    TAINSTAT,TAINSERR+TAINSHLD+TAINSCAN                              
         BNZ   CPO90                                                            
*                                                                               
         TM    TAINSTA2,TAINSRTH   SKIP IF RETRO INVOICE ON HOLD                
         BNZ   CPO90                                                            
*                                                                               
CPO60    L     R4,AINVIO           R4 = A(DUE DATE ELEMENT)                     
         MVI   ELCODE,TADDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINENDD    ERROR NO DUE DATE ELEMENT                    
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TADDD,R4                                                         
         MVC   DUEDATE,TADDDATE    SAVE DUE DATE                                
*                                                                               
         L     R4,AINVIO           R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINECPI    ERROR NO PAYMENT ELEMENT/INVOICE             
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TAPDD,R4            GET USE TABLE ENTRY FOR THIS USE,TYP         
         GOTO1 USEVAL,DMCB,TAPDUSE,TAPDTYPE                                     
*                                                                               
         TM    TAPDPST1,TAPDPBNP   SKIP BILL NO PAYROLL INVOICES                
         BNZ   CPO90                                                            
*                                                                               
         TM    TAPDSTAT,TAPDSCAN   IF THIS IS CAN$ INVOICE                      
         BZ    *+16                                                             
         CLI   RECNUM,CN           THEN REJECT IF NOT PRINTING THEM             
         BNE   CPO90                                                            
         B     CPO76               PRINT ALL EMPLOYERS ON CN IF CAN$            
         CLI   RECNUM,CN           ELSE REJECT IF PRINTING CAN$ CHKS            
         BE    CPO90                                                            
*                                                                               
         CLI   RECNUM,DC           IF DMB&B CHECK RUN                           
         BNE   *+14                                                             
         CLC   TAPDEMP,=C'DM '     REJECT IF NOT THEM                           
         BNE   CPO90                                                            
*                                                                               
         CLI   RECNUM,CK           IF REGULAR CHECK RUN                         
         BNE   *+14                                                             
         CLC   TAPDEMP,=C'DM '     REJECT IF DMB&B EMPLOYER                     
         BE    CPO90                                                            
*                                  IF TAX/REFUND OR TAX/SSN XFER                
         TM    TAPDADJS,TAPDADTR+TAPDADTT+TAPDADST                              
         BZ    CPO65                                                            
*                                  CHECK EMP BECAUSE NO TACOD ELEMENT           
         CLC   TAPDEMP,=C'PP '     IF EMPLOYER IS PRINT PAYROLL SVCS            
         BNE   *+16                                                             
         CLI   RECNUM,PC           THEN REJECT IF NOT PRINTING THEM             
         BNE   CPO90                                                            
         B     CPO76                                                            
         CLI   RECNUM,PC           ELSE REJECT IF PRINTING PP CHKS              
         BE    CPO90                                                            
         B     CPO76                                                            
*                                                                               
CPO65    L     R4,AINVIO                                                        
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINECCO    ERROR NO COMMERCIAL DETAILS ELEMENT          
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TACOD,R4            R4=A(COMMERCIAL DETAILS ELEMENT)             
         CLI   RECNUM,PC           IF PRINT CHECK RUN                           
         BNE   *+16                                                             
         CLI   TACOMED,TACOMEDP    REJECT IF NOT MEDIA PRINT COMML              
         BNE   CPO90                                                            
         B     *+12                                                             
         CLI   TACOMED,TACOMEDP    ELSE REJECT IF NOT PRINTING THEM             
         BE    CPO90                                                            
*                                                                               
CPO76    CLI   REQURG,C'U'         IF THIS IS URGENT CHECK RUN                  
         BNE   *+12                                                             
         BAS   RE,TESTURG          DETERMINE IF INVOICE IS ELIGIBLE             
         BNE   CPO90                                                            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',AINVIO),0,=C'INVOICE'                         
*                                                                               
         BAS   RE,PROCINVC         PROCESS ALL CHECKS FOR THIS INVOICE          
*                                                                               
CPO80    BAS   RE,UPDINV           UPDATE INVOICE RECORD IN THE FILE            
*                                                                               
         GOTO1 FLUSHI              FLUSH INVOICE TOTALS FROM TABLES             
*                                                                               
         MVC   KEY,SVIKEY          RESTORE INVOICE KEY READING SEQUENCE         
         GOTO1 HIGH                                                             
*                                                                               
CPO90    GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     CPO10                                                            
*                                                                               
CPO100   GOTO1 DOTRACE,DMCB,(C'5',KEY),38,=C'INV KEY'                           
*                                                                               
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
* THIS ROUTINE DETERMINES IF THE INVOICE SHOULD BE RERUN.  IT LOOKS AT          
* THE RERUN INDICATOR IN MASTD AND THE CHECK RUN DATE FROM THE INVOICE.         
* IT RETURNS 'YES' TO RERUN THE INVOICE AND 'NO' OTHERWISE.                     
*                                                                               
         USING TAIND,R4            R4=A(INVOICE STATUS ELEMENT)                 
TESTRR   NTR1                                                                   
         CLI   RERUN,C'Y'          RERUN INDICATOR MUST BE Y(ES),               
         BE    TR10                    A(LL), R(ECALC), OR D(IED)               
         CLI   RERUN,C'A'                                                       
         BE    TR10                                                             
         CLI   RERUN,C'R'                                                       
         BE    TR10                                                             
         CLI   RERUN,C'D'                                                       
         BNE   NO                                                               
*                                                                               
TR10     CLC   TAINCKID,REQURG     DON'T RERUN IF NOT SAME URGENT ID            
         BNE   NO                                                               
         CLC   TAINCKRN,TGTODAY1   MUST BE SAME DATE AS TODAY                   
         BNE   NO                                                               
         B     YES                                                              
         EJECT                                                                  
* THIS ROUTINE DETERMINES WHETHER THE CURRENT INVOICE IS ELIGIBLE TO            
* BE PICKED UP BY THE URGENT CHECK RUN.  DUE DATE REQUIREMENTS HAVE             
* ALREADY BEEN MET.                                                             
*                                                                               
TESTURG  NTR1                                                                   
         L     R4,AINVIO           GET INVOICE STATUS ELEMENT                   
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAIND,R4                                                         
         TM    TAINSTA2,TAINSADJ+TAINSURG  WANT ADJUSTMENTS/URGENTS             
         BNZ   YES                                                              
*                                                                               
         TM    TGUSSTA2,HLDTYPE    WANT HOLDING FEE PAYMENTS                    
         BO    YES                                                              
         TM    TGUSSTA3,SOAPUSE    BUT NEVER WANT SOAP PAYMENTS                 
         BO    NO                                                               
*                                                                               
         L     R4,AINVIO           LOOK FOR UNIONS PAID ON THIS INVOICE         
         MVI   ELCODE,TAULELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAULD,R4                                                         
*                                                                               
         ZIC   R0,TAULNULS         R0=N'SUB ELEMENTS                            
         LA    R1,TAULULS          R1=A(FIRST SUB ELEMENT)                      
*                                                                               
TU20     CLC   =C'AFT',0(R1)       IF AFT HERE THEN INVOICE ELIGIBLE            
         BE    YES                                                              
         LA    R1,L'TAULULS(R1)    TRY NEXT SUB ELEMENT                         
         BCT   R0,TU20                                                          
         B     NO                  INVOICE NOT ELIGIBLE FOR URGENT RUN          
         EJECT                                                                  
* PROCESS THE CURRENT INVOICE - EXTRACT INFORMATION FROM IT AND VARIOUS         
* SUPPORT RECORDS.  THEN READ ALL CHECKS FOR THE INVOICE AND PROCESS            
* EACH IN TURN.                                                                 
*                                                                               
PROCINVC NTR1                                                                   
         ST    RD,INVRD            SAVE RD AND USE TO ABORT INVOICE             
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         MVI   SRTWTYP,0           CLEAR WARNING FLAGS                          
         MVI   SRTWARN,0                                                        
*                                                                               
         L     R4,AINVIO           R4 = A(INVOICE STATUS ELEMENT)               
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         MVC   SRTINVS,TAINSTAT    SAVE INVOICE STATUS IN SORT RECORD           
         MVC   SRTINVS2,TAINSTA2        INVOICE STATUS TWO                      
         MVC   SRTPDTE,TAINPDTE         PAYMENT DATE                            
         MVC   SRTCDTE,CHKDATE1         CHECK DATE                              
*                                                                               
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+10                                                             
         MVC   SRTCDTE,CHKDATEL    SAVE DIFFERENT CHECK DATE                    
*                                                                               
         BAS   RE,EXTRIPAY         EXTRACT PAY INFO FROM INVOICE REC            
         BAS   RE,EXTRIMSC                 MISC INFO                            
*                                                                               
*                                  IF TAX REFUND OR TRANSFER INVOICE            
         TM    SRTADJS,TAPDADTR+TAPDADTT+TAPDADST                               
         BZ    PI10                                                             
         BAS   RE,EXTREMP          EXTRACT EMP INFO FROM EMP RECORD             
         B     PI50                SKIP TO CHECKS                               
*                                                                               
PI10     CLI   SRTADJS,0           IF THIS IS AN ADJUSTMENT INVOICE             
         BE    PI20                                                             
*                                                                               
         MVC   SRTAAGY,SRTAGY      THEN SAVE ADJUSTMENT AGY/INV                 
         MVC   SRTAINV,SRTINV                                                   
*                                                                               
         L     R4,AINVIO           R4 = A(ORIGINAL AGY/INV ELEMENT)             
         MVI   ELCODE,TAOIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERREIOI             ERROR IF NOT FOUND                           
         USING TAOID,R4                                                         
*                                                                               
         MVC   SRTAGY,TAOIAGY      SAVE AGY/INV IN SORT RECORD                  
         MVC   SRTINV,TAOIINV                                                   
*                                                                               
PI20     BAS   RE,EXTRICOM         EXTRACT CMCL INFO FROM INVOICE REC           
         BAS   RE,EXTRIUSE                 USE INFO FROM INVOICE REC            
         BAS   RE,EXTREMP                  EMP INFO FROM EMP RECORD             
         BAS   RE,EXTRAGYN                 AGY NAME FROM AGY RECORD             
         BAS   RE,EXTRCLIN                 CLI NAME FROM CLI RECORD             
         BAS   RE,EXTRPRDN                 PRD NAME FROM PRD OR INV REC         
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PI30                                                             
         MVI   BYTE,C'S'           EXTRACT SERVICES FOR NAME                    
         BAS   RE,EXTPNAME                                                      
         MVI   BYTE,C'P'           EXTRACT PHOTOGRAPHER'S NAME                  
         BAS   RE,EXTPNAME                                                      
*                                                                               
PI30     XC    SRTLEAD,SRTLEAD     PRE-CLEAR AFM LEADER                         
*                                                                               
         MVC   TGAGY,SRTAGY        CALL RECVAL TO GET FIRST MUSICIAN            
         MVC   TGCOM,SRTCOM            IN THE CAST                              
         XC    TGCSORT,TGCSORT                                                  
         MVI   TGCSORT,X'80'                                                    
         XC    TGSSN,TGSSN                                                      
         GOTO1 RECVAL,DMCB,TLCACDQ,(X'80',0)                                    
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CAST KEY'                          
*                                                                               
*                                  IF SAME COMMERCIAL                           
         CLC   KEY(TLCASORT-TLCAD),KEYSAVE                                      
         BNE   PI50                                                             
*                                                                               
         LA    R3,KEY              AND THERE IS A MUSICIAN IN THE CAST          
         USING TLCAD,R3                                                         
         TM    TLCASORT,X'80'                                                   
         BZ    PI50                                                             
*                                                                               
         MVC   SRTSSN,TLCASSN      THEN SET SSN TO FIRST MUSICIAN               
*                                                                               
         BAS   RE,EXTRW4           EXTRACT NAME FROM W4 RECORD                  
*                                                                               
         MVC   SRTLEAD,SRTNAME     SAVE NAME IN SORT RECORD                     
*                                                                               
PI50     BAS   RE,CHKLOOP          LOOP THROUGH AND PROCESS CHECK RECS          
*                                                                               
PIX      B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
* UPDATE THE INVOICE STATUS ELEMENT ONB THE INVOICE RECORD WITH THE             
* CHECK DATE, THE CHECKS WRITTEN BIT, AND THE URGENT ID AND WRITE               
* THE RECORD BACK TO THE FILE.  ALSO UPDATE THE STATUS BYTE IN THE              
* BILLING/STATUS PASSIVE POINTER WITH THE NEW INVOICE STATUS.                   
*                                                                               
UPDINV   NTR1                                                                   
         MVC   KEY+TLDRDA-TLDRD(4),SRTINDA  SET D/A OF INVOICE RECORD           
         GOTO1 GETREC                       GET INVOICE RECORD                  
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR                   
         BNE   *+10                                                             
         MVC   AIO,AINVIO          THEN USE INVOICE I/O AREA                    
*                                                                               
         L     R4,AIO              R4 = A(INVOICE STATUS ELEMENT)               
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    UI20                                                             
         OI    TAINSTAT,TAINSERR   THEN SET ERROR BIT IN STATUS                 
         MVC   TAINTERR,INVTERR    AND SAVE ERROR TYPE                          
         B     UI50                                                             
*                                                                               
UI20     OI    TAINSTAT,TAINSCHK   ELSE SET CHECKS WRITTEN BIT                  
*                                                                               
         MVC   TAINCDTE,SRTCDTE    SAVE CHECK DATE                              
         MVC   TAINCKID,REQURG          URGENT ID                               
         MVC   TAINCKRN,TGTODAY1        RUN DATE                                
*                                                                               
UI50     GOTO1 PUTREC              WRITE BACK UPDATED RECORD                    
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',AINVIO),0,=C'UPDATED INV'                     
*                                                                               
         GOTO1 ADDPTRS             ADD PASSIVE POINTERS TO FILE                 
*                                                                               
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
UIX      B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE PAYMENT DETAILS ELEMENT IN THE INVOICE           
* RECORD INTO THE SORT RECORD.                                                  
*                                                                               
EXTRIPAY NTR1                                                                   
         L     R4,AINVIO           R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRECPI                                                          
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTCOM,TAPDCOM      SAVE INTERNAL COMMERCIAL NUMBER              
         MVC   SRTUSE,TAPDUSE           USE CODE                                
         MVC   SRTUTYP,TAPDTYPE         USE TYPE                                
         MVC   SRTCYCS,TAPDCYCS         CYCLE START DATE                        
         MVC   SRTCYCE,TAPDCYCE         CYCLE END DATE                          
         MVC   SRTMAJ,TAPDMAJ           MAJORS                                  
         MVC   SRTUNT,TAPDUNIT          UNITS                                   
         MVC   SRTINS,TAPDINS           INSERTS                                 
         MVC   SRTITAG,TAPDTAGS         TAGS                                    
         MVC   SRTDEMO,TAPDDEMS         DEMOS                                   
         MVC   SRTEMP,TAPDEMP           EMPLOYER OF RECORD                      
         MVC   SRTCLI,TAPDCLI           CLIENT                                  
         MVC   SRTPRD,TAPDPRD           PRODUCT                                 
         MVC   SRTADJS,TAPDADJS         PAYROLL ADJUSTMENT INDICATORS           
         MVC   SRTPST1,TAPDPST1         PAYMENT STATUS BYTE                     
         MVC   SRTTPOF,TAPDOFF          TP OFFICE                               
*                                                                               
EIPX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFO FROM THE ELEMENTS ON THE INVOICE RECORD THAT COME FROM           
* THE COMMERCIAL RECORD AT THE TIME OF THE PAYMENT.                             
*                                                                               
EXTRICOM NTR1                                                                   
*                                  PRE-CLEAR INVOICE CMCL INFO VARS             
         XC    SRTICOMS(SRTICOML),SRTICOMS                                      
*                                                                               
         MVC   AIO,AINVIO          EXTRA COMMERCIAL TITLE                       
         GOTO1 CHAROUT,DMCB,TAFNELQ,0,TAFNTTTL                                  
         MVC   AIO,AIO1                                                         
*                                                                               
         BE    *+12                IF ERROR CODE RETURNED THEN SET              
         BAS   RE,WARECTI              WARNING IN SORT RECOD                    
         B     EIC5                                                             
*                                                                               
         MVC   SRTTITL,TGNAME      ELSE SAVE TITLE IN SORT RECORD               
*                                                                               
EIC5     L     R4,AINVIO           R4 = A(COMMERCIAL DETAILS ELEMENT)           
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRECCO                                                          
         USING TACOD,R4                                                         
*                                                                               
         MVC   SRTCID,TACOCID      SAVE COMMERCIAL ID                           
         MVC   TGCID,TACOCID            IN TGCID ALSO                           
         MVC   SRTAIR,TACOAIR           FIRST AIR DATE                          
         MVC   SRTFCYC,TACOFCYC         FIRST FIXED CYCLE                       
         MVC   SRTEXP,TACOEXP           EXPIRATION DATE                         
         MVC   SRTDUB,TACODUB           DUB DATE                                
         MVC   SRTMED,TACOMED           MEDIA                                   
         MVC   SRTCTYP,TACOTYPE         COMMERCIAL TYPE                         
         MVC   SRTCOSTA,TACOSTAT        STATUS                                  
*                                                                               
         L     R4,AINVIO           R4 = A(LIFT DETAILS ELEMENT)                 
         MVI   ELCODE,TALFELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC10               IF NONE THEN DONE                            
         USING TALFD,R4                                                         
*                                                                               
         MVC   SRTEDIT,TALFLID     SAVE EDIT VERSION # IN SORT RECORD           
         B     EIC12               SKIP VERSIONS ELEMENT                        
*                                                                               
EIC10    L     R4,AINVIO           R4 = A(VERSIONS ELEMENT)                     
         MVI   ELCODE,TAVRELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC12               IF NONE THEN DONE                            
         USING TAVRD,R4                                                         
*                                                                               
         MVC   SRTVERS,TAVRVERS    SAVE VERSION CODE                            
*                                                                               
EIC12    L     R4,AINVIO           R4 = A(SOAP CABLE ELEMENT)                   
         XC    CID2,CID2                                                        
         MVI   ELCODE,TASBELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC15               IF NONE THEN DONE                            
         USING TASBD,R4                                                         
*                                                                               
         MVC   CID2,TASBCID2       SAVE 2ND CID                                 
*                                                                               
EIC15    L     R4,AINVIO           R4 = A(COMMERCIAL CONTRACT ELEMENT)          
         MVI   ELCODE,TACCELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC20               IF NONE THEN DONE                            
         USING TACCD,R4                                                         
*                                                                               
         CLI   TACCNCON,0          IF N'CONTRACTS = 0 THEN DONE                 
         BE    EIC20                                                            
*                                                                               
         LA    RF,11               MOVE UP TO TWO CONTRACTS TO SORTREC          
         CLI   TACCNCON,1                                                       
         BE    *+8                                                              
         LA    RF,23                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTCON(0),TACCCON                                                
*                                                                               
EIC20    L     R4,AINVIO           R4 = A(FIRST CMCL STUDIO ELEMENT)            
         MVI   ELCODE,TACSELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC70               SKIP IF NONE FOUND                           
         USING TACSD,R4                                                         
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   EIC30                                                            
         CLI   TACSTYPE,TACSTYPS   AND IF SHOOT ELEMENT                         
         BNE   EIC30                                                            
         MVC   SRTSHOOT,TACSDATE   THEN SAVE SHOOT DATE                         
         MVC   SRTSTUD,TACSSTUD    AND SHOOT STUDIO                             
         MVC   SRTCITY,TACSCITY    AND SHOOT CITY                               
         B     EIC70                                                            
*                                                                               
EIC30    CLI   TACSTYPE,TACSTYPF   IF FILM ELEMENT                              
         BNE   EIC40                                                            
         MVC   SRTFILM,TACSDATE    THEN SAVE FILM DATE                          
         B     EIC60                                                            
*                                                                               
EIC40    CLI   TACSTYPE,TACSTYPR   ELSE IF RECORD ELEMENT                       
         BNE   EIC50                                                            
         MVC   SRTRCRD,TACSDATE    THEN SAVE RECORD DATE                        
         B     EIC60                                                            
*                                                                               
EIC50    CLI   TACSTYPE,TACSTYPM   ELSE IF MUSIC ELEMENT                        
         BNE   EIC60                                                            
         MVC   SRTMUS,TACSDATE     THEN SAVE MUSIC DATE                         
*                                                                               
EIC60    BAS   RE,NEXTEL           GET NEXT CS ELEMENT                          
         BE    EIC30               REPEAT UNTIL NO MORE FOUND                   
*                                                                               
EIC70    L     R4,AINVIO           R4 = A(OLD AGENCY/CID ELEMENT)               
         MVI   ELCODE,TAOCELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC90               SKIP IF NONE FOUND                           
         USING TAOCD,R4                                                         
*                                                                               
EIC80    MVC   SRTORIG,TAOCCID     SAVE LAST ORIGINAL CID                       
*                                                                               
         BAS   RE,NEXTEL           BUMP TO NEXT OC ELEMENT                      
         BE    EIC80               REPEAT UNTIL NO MORE FOUND                   
*                                                                               
EIC90    DS    0H                                                               
*                                                                               
EICX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE USE ELEMENTS IN THE INVOICE RECORD INTO          
* THE SORT RECORD.                                                              
*                                                                               
EXTRIUSE NTR1                                                                   
         XC    SRTUDATA(SRTULEN),SRTUDATA  CLEAR USE ELEMENT DATA               
*                                                                               
         L     R4,AINVIO           R4 = A(CLASS A PROGRAM DETAILS ELEM)         
         MVI   ELCODE,TANPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIU50               SKIP IF NONE FOUND                           
         USING TANPD,R4                                                         
*                                                                               
         LA    R2,SRTCLA           R2 = A(FIRST CLASS A DATE)                   
         LA    R3,SRTCLA+L'SRTCLA  R3 = A(END OF CLASS A DATES)                 
*                                                                               
EIU10    MVC   0(3,R2),TANPDATE    MOVE CLASS A DATE TO SRTCLA                  
         MVC   3(1,R2),TANPLFT          LIFT CODE                               
*                                                                               
         LA    R2,4(R2)            BUMP R2 TO NEXT CLASS A DATE                 
         CR    R2,R3               IF REACHED END OF CLASS A DATES              
         BNL   EIU50               THEN DONE                                    
*                                                                               
         BAS   RE,NEXTEL           BUMP TO NEXT CLASS A ELEMENT                 
         BE    EIU10               REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
EIU50    L     R4,AINVIO           R4 = A(UPGRADE DETAILS ELEMENT)              
         MVI   ELCODE,TAUPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIU100              SKIP IF NONE FOUND                           
         USING TAUPD,R4                                                         
*                                                                               
         MVC   SRTUMAJ,SRTMAJ      SAVE MAJORS/UNITS FROM PD ELEM AS            
         MVC   SRTUUNT,SRTUNT          UPGRADE MAJORS/UNITS IN SORT REC         
         MVC   SRTUINS,SRTINS      SAME GOES FOR INSERTS                        
*                                                                               
         MVC   SRTMAJ,TAUPIMAJ     SAVE INITIAL MAJORS/UNITS IN                 
         MVC   SRTUNT,TAUPIUNT         SORT RECORD                              
         TM    TGUSTYST,INSERTS                                                 
         BZ    *+10                                                             
         MVC   SRTINS,TAUPIINS     IF INSERTS REQUIRED, SAVE INSERTS            
*                                                                               
EIU100   DS    0H                                                               
*                                                                               
EIUX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT MISCELLANEOUS INFORMATION FROM THE INVOICE RECORD INTO THE            
* SORT RECORD.                                                                  
*                                                                               
EXTRIMSC NTR1                                                                   
         ZAP   SRTCCVT,CCVTRATE    SET CURRENT CANADIAN $ CONV RATE             
*                                                                               
         CLI   RECNUM,CN           PROTECT AGAINST BAD DATA - ONLY              
         BNE   EIM10               NEED FOR CANADIAN DOLLAR CHECKS              
*                                                                               
         L     R4,AINVIO           R4 = A(BILLING DETAILS ELEMENT)              
         MVI   ELCODE,TABDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIM10                                                            
         USING TABDD,R4                                                         
         OC    TABDCCVT,TABDCCVT   IF DEFINED                                   
         BZ    EIM10                                                            
         ZAP   SRTCCVT,TABDCCVT    SET CANADIAN $ CONV RATE FROM BILL           
*                                                                               
EIM10    XC    SRTINVC,SRTINVC     PRE-CLEAR INVOICE COMMENTS                   
*                                                                               
         MVI   ELCODE,TACMELQ      SEARCH FOR COMMENT TYPE GENERAL              
         MVC   AIO,AINVIO                                                       
         GOTO1 GETL,DMCB,(1,=C'G')                                              
         MVC   AIO,AIO1                                                         
         BNE   EIMX                DONE IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(COMMENT ELEMENT)                      
         USING TACMD,R4                                                         
*                                                                               
         ZIC   RF,TACMLEN          MOVE INVOICE COMMENT TO SORT RECORD          
         SH    RF,=H'4'                                                         
         CH    RF,=AL2(L'SRTINVC-1) PROTECT AGAINST BIG CMTS IN OLD INV         
         BNH   *+8                                                              
         LH    RF,=AL2(L'SRTINVC-1)                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTINVC(0),TACMCOMM                                              
*                                                                               
EIMX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFO FROM THE EMPLOYER RECORD FOR THIS INVOICE IF IT IS               
* DIFFERENT THAN THE EMPLOYER FOR THE PREVIOUS INVOICE.  * NOTE * THE           
* EMPLOYER RECORD IS READ INTO AEMPIO.  THE ROUTINE THAT EXTRACTS THE           
* CAST DETAIL INFORMATION WILL ADDITIONALLY LOOK IN THE EMPLOYER RECORD         
* FOR THE STATE U.C. # FOR THE STATE OF WORK.                                   
*                                                                               
EXTREMP  NTR1                                                                   
         CLC   SRTEMP,PREVEMP      IF EMPLOYER HAS CHANGED                      
         BE    EEMX                                                             
*                                                                               
         XC    SRTEMPN,SRTEMPN     PRE-CLEAR EMPLOYER NAME                      
         XC    DEFSTATE,DEFSTATE             DEFAULT TAXABLE STATE              
*                                                                               
         L     RF,AEMPIO           PRE-SET EMPLOYER RECORD AS NOT FOUND         
         MVI   0(RF),0                                                          
*                                                                               
         XC    PREVEMP,PREVEMP     PRE-CLEAR PREVIOUS EMPLOYER                  
*                                                                               
         XC    BLOCK(44),BLOCK     GET EMP REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         MVC   AIO,AEMPIO                                                       
         GOTO1 RECVAL,DMCB,TLEMCDQ,(X'88',SRTEMP),BLOCK                         
         MVC   AIO,AIO1                                                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECEM              IN SORT RECORD AND RETURN                
         B     EEMX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AEMPIO),0,=C'EMP'                             
*                                                                               
         MVC   PREVEMP,SRTEMP      SET PREVIOUS EMP TO THIS EMP                 
*                                                                               
         MVC   SRTEMPN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
         L     R4,AEMPIO           R4 = A(EMPLOYER DETAILS ELEMENT)             
         MVI   ELCODE,TAEDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EEMX                                                             
         USING TAEDD,R4                                                         
         MVC   DEFSTATE,TAEDDST    SAVE DEFAULT TAXABLE STATE                   
*                                                                               
EEMX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT THE AGENCY NAME FROM THE AGENCY RECORD FOR THIS INVOICE IF IT         
* IS DIFFERENT THAN THE AGENCY FOR THE PREVIOUS INVOICE.                        
*                                                                               
EXTRAGYN NTR1                                                                   
         MVC   TGAGY,SRTAGY        SAVE AGENCY IN GLOBAL                        
*                                                                               
         CLC   SRTAGY,PREVAGY      IF AGENCY HAS CHANGED                        
         BE    EAYX                                                             
*                                                                               
         XC    SRTAGYN,SRTAGYN     PRE-CLEAR AGENCY NAME                        
*                                                                               
         XC    PREVAGY,PREVAGY     PRE-CLEAR PREVIOUS AGENCY                    
         XC    PREVCLI,PREVCLI     PRE-CLEAR PREVIOUS CLIENT                    
*                                                                               
         XC    BLOCK(44),BLOCK     GET AGY REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'88',SRTAGY),BLOCK                         
         BNE   ERRECAG                                                          
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'AGENCY'                             
*                                                                               
         MVC   SRTAGYN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
* NO-OP  L     R4,AIO              R4 = A(AGENCY DETAILS ELEMENT)               
* NO-OP  MVI   ELCODE,TAAYELQ                                                   
* NO-OP  BAS   RE,GETEL                                                         
* NO-OP  BNE   ERREAYE                                                          
* NO-OP  USING TAAYD,R4                                                         
* NO-OP                                                                         
* NO-OP  MVC   SRTTPOF,TAAYTPOF    SAVE TP OFFICE IN SORT RECORD                
*                                                                               
         MVC   PREVAGY,SRTAGY      SET PREVIOUS AGY TO THIS AGY                 
*                                                                               
EAYX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT THE CLIENT NAME FROM THE CLIENT RECORD FOR THIS INVOICE IF IT         
* IS DIFFERENT THAN THE CLIENT FOR THE PREVIOUS INVOICE.                        
*                                                                               
EXTRCLIN NTR1                                                                   
         CLC   SRTCLI,PREVCLI      IF CLIENT HAS CHANGED                        
         BE    ECLX                                                             
*                                                                               
         XC    SRTCLIN,SRTCLIN     PRE-CLEAR CLIENT NAME                        
*                                                                               
         XC    PREVCLI,PREVCLI     PRE-CLEAR PREVIOUS CLIENT                    
*                                                                               
         XC    BLOCK(44),BLOCK     GET CLI REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLCLCDQ,(X'88',SRTCLI),BLOCK                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECCL              IN SORT RECORD                           
         B     ECLX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'CLI'                                
*                                                                               
         MVC   PREVCLI,SRTCLI      SET PREVIOUS CLI TO THIS CLI                 
*                                                                               
         MVC   SRTCLIN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
ECLX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT THE PRODUCT NAME FROM THE PRODUCT RECORD IF THE PRODUCT IS            
* NON-ZERO.  OTHERWISE LOOK IN THE COMMERCIAL RECORD FOR IT.                    
*                                                                               
EXTRPRDN NTR1                                                                   
         XC    SRTPRDN,SRTPRDN     PRE-CLEAR PRODUCT NAME                       
*                                                                               
         OC    SRTPRD,SRTPRD       IF PRODUCT IS NOT ZEROS                      
         BZ    EPR10                                                            
*                                                                               
         XC    BLOCK(44),BLOCK     GET PRD REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLPRCDQ,(X'88',SRTPRD),BLOCK                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECPR              IN SORT RECORD AND RETURN                
         B     EPRX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'PROD'                               
*                                                                               
         MVC   SRTPRDN,BLOCK+8     ELSE SAVE NAME IN SORT RECORD                
         B     EPRX                                                             
*                                                                               
EPR10    MVC   TGAGY,SRTAGY        ELSE GET COMMERCIAL RECORD                   
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A0',SRTCOM)                              
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECOM              IN SORT RECORD AND RETURN                
         B     EPRX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'3',AIO),0,=C'COMML'                              
*                                                                               
         GOTO1 CHAROUT,DMCB,TAFNELQ,0,TAFNTPRD  EXTR PRODUCT NAME               
*                                                                               
         MVC   SRTPRDN,TGNAME      SAVE PRODUCT NAME IN SORT RECORD             
*                                                                               
EPRX     B     XIT                                                              
         EJECT                                                                  
* EXTRACTS SERVICES FOR OR PHOTOGRAPHER NAME.  FIRST TRIES TANUD                
* ELEMENT, IF NONE, WILL THEN TRY TAFND ELEMENT.                                
*                                  NTRY BYTE = NAME TO GET (S,P)                
*                                                                               
EXTPNAME NTR1                                                                   
         ZIC   R2,BYTE             NAME TYPE REQUESTED                          
         MVI   ELCODE,TANUELQ      LOOK FOR TANUD ELEMENT                       
         CLM   R2,1,=C'S'          IF READING FOR SVC FOR NAME                  
         BNE   EXTPNM5                                                          
         MVC   SRTSVCNM,MYSPACES   PRECLEAR THE NAME                            
         MVI   BYTE,TANUTSVC       SET ELEMENT TYPE                             
         B     EXPNM10                                                          
EXTPNM5  MVC   SRTPHONM,MYSPACES   ELSE READING FOR PHOTOGRAPHER NAME           
         MVI   BYTE,TANUTPHO       SET ELEMENT TYPE                             
*                                                                               
EXPNM10  MVC   AIO,AINVIO          SET INVOICE IOAREA                           
         GOTO1 GETL,DMCB,(1,BYTE)                                               
         BNE   EXPNM20                                                          
         L     R4,TGELEM           R4=A(TANUD ELEMENT)                          
         USING TANUD,R4                                                         
         XC    DMCB,DMCB                                                        
         MVI   DMCB+3,TLAYCDQ      SET AGENCY RECORD CODE                       
         CLM   R2,1,=C'S'          UNLESS LOOKING FOR PHOTOGRAPHER              
         BE    *+8                                                              
         MVI   DMCB+3,TLW4CDQ      IN WHICH CASE, SET W4 FOR PHOTO              
         MVC   AIO,AIO1                                                         
         GOTO1 RECVAL,DMCB,,(X'A8',TANUMBER),0                                  
         BE    EXPNM30                                                          
*                                  NOW TRY FOR TAFND (OVERRIDE NAME)            
EXPNM20  XC    DMCB+8(4),DMCB+8                                                 
         MVI   DMCB+11,TAFNTPHO    SET FOR PHOTOGRAPHER OVERRIDE NAME           
         CLM   R2,1,=C'S'          UNLESS LOOKING FOR SERVICES FOR              
         BNE   *+8                                                              
         MVI   DMCB+11,TAFNTSVC    IN WHICH CASE, SET SVC FOR OVERRIDE          
         GOTO1 CHAROUT,DMCB,TAFNELQ,0,,                                         
*                                                                               
EXPNM30  CLM   R2,1,=C'S'          IF READING FOR SERVICES FOR NAME             
         BNE   *+14                                                             
         MVC   SRTSVCNM,TGNAME     SET SERVICES FOR                             
         B     *+10                                                             
         MVC   SRTPHONM,TGNAME     ELSE, SET PHOTOGRAPHER                       
*                                                                               
         MVC   AIO,AIO1            RESET IOAREA                                 
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* LOOP THROUGH THE CHECK RECORDS FOR THIS INVOICE AND PROCESS EACH IN           
* TURN.                                                                         
*                                                                               
CHKLOOP  NTR1                                                                   
         LA    R3,KEY              BUILD FIRST CHECK KEY FOR CURRENT            
         USING TLCKD,R3                AGENCY/INVOICE                           
         XC    TLCKKEY,TLCKKEY                                                  
         MVI   TLCKCD,TLCKCDQ                                                   
         MVC   TLCKAGY,SRTAGY                                                   
         MVC   TLCKINV,SRTINV                                                   
*                                                                               
         CLI   SRTADJS,0           IF THIS IS AN ADJUSTMENT INVOICE             
         BE    CH5                 AND NOT TAX REFUND OR TRANSFER               
         TM    SRTADJS,TAPDADTR+TAPDADTT+TAPDADST                               
         BNZ   CH5                                                              
         MVC   TLCKAGY,SRTAAGY     THEN SET AGY/INV FOR ADJUSTMENT              
         MVC   TLCKINV,SRTAINV                                                  
*                                                                               
CH5      MVC   FILENAME,CHKDIR     READ FIRST KEY                               
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
*                                                                               
*                                  WHILE NOT END OF AGENCY/INVOICE              
CH10     CLC   TLCKKEY(TLCKSORT-TLCKD),KEYSAVE                                  
         BNE   CH100                                                            
*                                                                               
         MVC   SVCKEY,TLCKKEY      SAVE CHECK KEY                               
*                                                                               
         OC    REQSSN,REQSSN       IF WE HAVE PERFORMER FILTER                  
         BZ    *+14                                                             
         CLC   REQSSN,TLCKSSN      TEST WE HAVE A MATCH                         
         BNE   CH90                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CHK KEY'                           
*                                                                               
         MVC   AIO,ASRTCHK         READ CHECK RECORD INTO SORT RECORD           
         MVC   FILENAME,CHKFIL                                                  
         GOTO1 GETREC                                                           
         XC    FILENAME,FILENAME                                                
         MVC   AIO,AIO1                                                         
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'C',ASRTCHK),0,=C'CHECK'                          
*                                                                               
         L     R4,ASRTCHK          IF CHECK DETAILS ELEMENT NOT FOUND           
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    CH20                                                             
*                                                                               
         LA    R4,ELEM             THEN BUILD NEW ONE                           
         USING TACDD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACDEL,TACDELQ                                                   
         MVI   TACDLEN,TACDLNQ                                                  
*                                                                               
         MVC   AIO,ASRTCHK         ADD CHECK DETAILS ELEMENT                    
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CH20     OC    REQCHKN,REQCHKN     IF CHECK NUMBER FILTER OPTION                
         BZ    *+14                                                             
         CLC   TACDCHK,REQCHKN     THEN CHECK NUMBER MUST BE THE SAME           
         BNE   CH90                                                             
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM NOT RUN FOR EXTRACTION            
         BE    CH50                    ONLY                                     
         BAS   RE,DELLIEN          AND CHECK WAS PREVIOUSLY GENERATED           
         BE    CH90                    FOR LIEN PAYEE THEN SKIP                 
         BAS   RE,DELTRUST         AND CHECK WAS PREVIOUSLY GENERATED           
         BE    CH90                    FOR W4 TRUSTEE THEN SKIP                 
*                                                                               
CH50     BAS   RE,PROCCHK          ELSE PROCESS CHECK RECORD                    
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM NOT RUN FOR EXTRACTION            
         BE    CH90                    ONLY                                     
         OC    SRTTRST,SRTTRST     IF W4 MINOR WITHHOLDINGS TO TRUSTEE          
         BZ    *+12                                                             
         BAS   RE,TRSTCHK          THEN GENERATE CHECK FOR TRUSTEE              
         B     CH90                                                             
         OC    SRTLIEN,SRTLIEN     IF CHECK HAS LIEN AMOUNT                     
         BZ    *+8                                                              
         BAS   RE,LIENCHK          THEN GENERATE CHECK FOR LIEN PAYEE           
*                                                                               
         USING TLCKD,R3                                                         
CH90     MVC   TLCKKEY,SVCKEY      RESTORE CHECK KEY READING SEQUENCE           
         MVC   FILENAME,CHKDIR                                                  
         OI    DMINBTS,X'08'       READ FOR DELETED IN CASE DEL'D RECS          
         GOTO1 HIGH                                                             
         NI    DMINBTS,X'F7'                                                    
*                                                                               
         GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         XC    FILENAME,FILENAME                                                
         B     CH10                                                             
*                                                                               
CH100    GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CHK KEY'                           
*                                                                               
CHX      B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
* THIS ROUTINE DETERMINES IF THE CURRENT CHECK IS A LIEN PAYEE CHECK            
* GENERATED BY A PREVIOUS CHECK RUN.  IF IT IS, THEN THE RECORD IS              
* DELETED AND THE ROUTINE RETURNS 'YES'.  OTHERWISE THE ROUTINE RETURNS         
* 'NO'.                                                                         
*                                                                               
DELLIEN  NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSLIN   IF CHECK WAS NOT GENERATED FOR LIEN          
         BZ    NO                      PAYEE THEN RETURN 'NO'                   
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         L     R4,ASRTCHK          DELETE RECORD AND WRITE BACK                 
         USING TLCKD,R4                                                         
         OI    TLCKSTAT,X'80'                                                   
         MVC   FILENAME,CHKFIL                                                  
         GOTO1 PUTREC                                                           
         MVC   AIO,AIO1                                                         
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'DELETED LIEN CHK'               
*                                                                               
         LA    R3,KEY              DELETE KEY AND WRITE BACK                    
         USING TLDRD,R3                                                         
         MVC   TLDRREC,SVCKEY      INSURE WE HAVE CHECK RECORD KEY              
         OI    TLDRSTAT,X'80'                                                   
         MVC   FILENAME,CHKDIR                                                  
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',KEY),38,=C'DELETED LIEN KEY'                  
         GOTO1 ADDPTRS             REMOVE OLD PASSIVE POINTERS                  
         XC    FILENAME,FILENAME   RESET FILE NAME                              
         B     YES                 RETURN 'YES'                                 
         DROP  R3,R4                                                            
         EJECT                                                                  
* THIS ROUTINE DETERMINES IF THE CURRENT CHECK IS A W4 TRUSTEE CHECK            
* GENERATED BY A PREVIOUS CHECK RUN.  IF IT IS, THEN THE RECORD IS              
* DELETED AND THE ROUTINE RETURNS 'YES'.  OTHERWISE THE ROUTINE RETURNS         
* 'NO'.                                                                         
*                                                                               
DELTRUST NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSTRS   IF CHECK WAS NOT GENERATED FOR W4            
         BZ    NO                  TRUSTEE THEN RETURN 'NO'                     
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         L     R4,ASRTCHK          DELETE RECORD AND WRITE BACK                 
         USING TLCKD,R4                                                         
         OI    TLCKSTAT,X'80'                                                   
         MVC   FILENAME,CHKFIL                                                  
         GOTO1 PUTREC                                                           
         MVC   AIO,AIO1                                                         
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'DELETED TRUSTEE CHK'            
*                                                                               
         LA    R3,KEY              DELETE KEY AND WRITE BACK                    
         USING TLDRD,R3                                                         
         MVC   TLDRREC,SVCKEY      INSURE WE HAVE CHECK RECORD KEY              
         OI    TLDRSTAT,X'80'                                                   
         MVC   FILENAME,CHKDIR                                                  
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',KEY),38,=C'DELETED TRUSTEE KEY'               
         GOTO1 ADDPTRS             REMOVE OLD PASSIVE POINTERS                  
         XC    FILENAME,FILENAME   RESET FILE NAME                              
         B     YES                 RETURN 'YES'                                 
         DROP  R3,R4                                                            
         EJECT                                                                  
* PROCESS THE CHECK RECORD THAT HAS BEEN READ INTO THE END OF THE SORT          
* RECORD.  THE ROUTINE WILL READ SEVERAL SUPPORT RECORDS AND BUILD THE          
* REST OF THE SORT RECORD.  IF NECESSARY IT CALLS ALLTAX TO CALCULATE           
* THE WITHHOLDING.  FINALLY, THE ROUTINE WILL WRITE THE SORT RECORD TO          
* THE SORT FILE.                                                                
*                                                                               
PROCCHK  NTR1                                                                   
         LA    RE,SRTCVARS         PRE-CLEAR CHECK VARIABLES                    
         L     RF,=A(SRTCVARL)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVI   SRTBITS,0           PRE-CLEAR DESCRIPTION BITS                   
         MVI   SRTBITS2,0          PRE-CLEAR DESCRIPTION BITS                   
*                                                                               
         CLI   SRTWTYP,C'C'        IF PREVIOUS WARNING WAS FOR A CHECK          
         BNE   *+12                                                             
         MVI   SRTWTYP,0           THEN CLEAR WARNING FLAGS                     
         MVI   SRTWARN,0                                                        
*                                                                               
         LA    R3,KEY              R3 = A(CHECK KEY)                            
         USING TLCKD,R3                                                         
*                                                                               
         MVC   SRTCST,TLCKSORT     SAVE CAST SORT KEY IN SORT RECORD            
         MVC   SRTSSN,TLCKSSN           SSN                                     
         MVC   SRTCAT,TLCKCAT           CATEGORY                                
*                                                                               
         USING TLDRD,R3            SAVE DISK ADDRESS IN SORT RECORD             
         MVC   SRTDSKA,TLDRDA                                                   
*                                                                               
         GOTO1 CATVAL,DMCB,SRTCAT  GET CATEGORY ENTRY FOR LATER                 
*                                                                               
         BAS   RE,EXTRW4           EXTRACT W4 INFO FROM W4 RECORD               
*                                                                               
         BAS   RE,EXTRCPAY         EXTRACT PAY INFO FROM CHECK RECORD           
         BAS   RE,EXTRCUPG                 UPGRADE INFO                         
         BAS   RE,EXTRSESS                 SESSION INFO                         
         BAS   RE,EXTRCAST                 CAST INFO                            
*                                                                               
         BAS   RE,EXTRNCDE         EXTRACT AGENT INFO FROM AGENT RECORD         
*                                                                               
         BAS   RE,TAKEGUAR         TAKE GUAR CREDITS FROM CHECK                 
*                                                                               
         GOTO1 GETESU,DMCB,=C'FD ' MAKE SURE PERF ESU ENTRIES ARE BUILT         
*                                                                               
         GOTO1 NOCALC              IF WE DON'T WANT TO CALCULATE THIS           
         BNE   PC20                    CHECK                                    
*                                                                               
         BAS   RE,EXTRAMNT         THEN EXTRACT CHECK AMOUNTS                   
         B     PC40                                                             
*                                                                               
PC20     CLI   REQFRCW4,C'Y'       ELSE IF REQ OPTION TO FORCE W4 TYPE          
         BNE   PC30                                                             
         MVC   SRTW4TY,SRTTYPE     SET CHK W4 TYPE AS W4 TYPE ON W4 REC         
*                                                                               
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY    MOVE TO ELEMENT AS WELL                      
         DROP  R4                                                               
*                                                                               
PC30     CLC   SRTTYPE,SRTW4TY     ELSE IF W4 TYPE DOES NOT MATCH               
         BNE   ERREMAT                 PAYMENT W4 TYPE THEN ERROR               
*                                                                               
         BAS   RE,CALCAMNT         CALCULATE CHECK AMOUNTS                      
*                                                                               
PC40     BAS   RE,CHKESU           UPDATE ESUTAB W/ THIS CHECK                  
*                                                                               
         CLI   RERUN,C'Y'          IF NOT RUNNING FOR EXTRACTION ONLY           
         BE    PC45                                                             
         GOTO1 UPDPYTD             UPDATE PYTDTAB & CHECK WITH YTD AMTS         
         BAS   RE,UPDYTD           UPDATE CHECK AND SORT WITH YTD WITH.         
*                                                                               
PC45     L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   TACDRUN,TGTODAY1    SAVE RUN DATE IN ELEMENT                     
         MVC   TACDDTE,SRTCDTE     SAVE CHECK DATE                              
         TM    SRTBITS,SRTBWIRE    IF PERF'S CHKS ARE WIRE TRANSFERRED          
         BO    *+12                                                             
         TM    SRTBITS,SRTBDD      OR PERF'S CHKS ARE DIRECT DEPOSIT            
         BZ    *+10                                                             
         MVC   TACDCSH,SRTCDTE     SET CASHED DATE AS CHECK DATE                
         MVC   TACDBNK,BANKCODE    SAVE BANK ACCOUNT CODE                       
         MVC   TACDSEQ,PROCSEQ     SAVE PROCESSED SEQUENCE NUMBER               
*                                                                               
         MVC   SRTCID,TGCID        SET CID WITH TGCID                           
         TM    TACDSTAT,TACDS2ND   IF CHECK FROM 2ND COMM'L OF SOC USE          
         BZ    PC50                                                             
         OC    CID2,CID2           AND HAVE 2ND CID                             
         BZ    *+10                                                             
         MVC   SRTCID,CID2         USE 2ND CID INSTEAD                          
*                                                                               
PC50     TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         BAS   RE,GETSEQ           GET SPECIAL SEQUENCE NUMBER FOR IT           
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                                                               
         BAS   RE,TRACEESU         TRACE ESUTAB ENTRIES FOR THIS PERF           
*                                                                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
*                                                                               
         MVC   BLOCK(14),MYSPACES                                               
         MVC   BLOCK(8),=C'SORT PUT'                                            
         LA    R2,TACDSEQ                                                       
         EDIT  (3,1(R2)),(5,BLOCK+9),ALIGN=RIGHT,FILL=0                         
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,(14,BLOCK)            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
*                                                                               
PCX      B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
* THIS ROUTINE USES THE CURRENT CHECK, WHICH HAS A LIEN AMOUNT IN IT,           
* TO BUILD A CHECK FOR THE LIEN PAYEE.  IT THEN PUTS THE CHECK TO THE           
* SORTER FILE.                                                                  
*                                                                               
LIENCHK  NTR1                                                                   
         XC    SRTAMNTS(SRTAMNTL),SRTAMNTS  PRE-CLEAR CHECK AMOUNTS             
         XC    SRTPNH,SRTPNH                                                    
*                                                                               
         XC    SRTFSO,SRTFSO       INSURE THERE'S NO FSO INFO SAVED             
         XC    SRTFSON,SRTFSON                                                  
*                                                                               
         MVI   SRTBITS,SRTBLIN     SET LIEN GENERATED CHECK BIT                 
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          3) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          4) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          5) BILLING YTD EARNINGS ELEMENT          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TABYELQ          6) BILLING YTD ELEMENT                   
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TATIELQ          7) TAX ID ELEMENT                        
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TANUELQ          8) GST ID NUMBER ELEMENT                 
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
*                                      9) LIEN PAID TO S/S NUMBER               
         GOTO1 DELL,DMCB,(1,=AL1(TANUTLIN))                                     
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK RECORD)                         
         USING TLCKD,R4                                                         
         MVC   SRTLSSN,TLCKSSN     SAVE SSN OF LIEN PAYER                       
         MVC   TLCKSSN,LIENSSN     MOVE LIEN PAYEE SSN TO KEY                   
         MVC   SRTSSN,LIENSSN      AND TO SORT RECORD                           
*                                                                               
         LA    R4,ELEM             BUILD LIEN PAYER ELEMENT                     
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTLIN   SET LIEN PAYER TYPE                          
         MVC   TANUMBER(9),SRTLSSN SET LIEN PAYER S/S NUMBER                    
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         L     R4,ASRTCHK          R4 = A(LIEN WITHHOLDING ELEMENT)             
         MVI   ELCODE,TALWELQ                                                   
         BAS   RE,GETEL                                                         
         USING TALWD,R4                                                         
*                                                                               
         ICM   RF,15,TALWREC       RF = LIEN AMOUNT RECOVERED                   
         ST    RF,SRTNET           SAVE AS CHECK AMOUNT IN SORT RECORD          
*                                                                               
         LNR   RF,RF               CONVERT TO NEGATIVE AND SAVE BACK IN         
         STCM  RF,15,TALWREC           ELEMENT                                  
*                                                                               
         ST    RF,SRTLIEN          SAVE LIEN AMOUNT IN SORT RECORD              
*                                                                               
         BAS   RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4TY,SRTTYPE     SET W4 TYPE                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R4                                                         
         XC    TACAGUA,TACAGUA     CLEAR GUARANTEE CODE                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE HERE                        
         XC    SRTNCDE,SRTNCDE     AND IN SORT RECORD                           
*                                                                               
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY             SET W4 TYPE                         
         XC    TAPDAMTS(TAPDAMTL),TAPDAMTS  CLEAR ALL AMOUNTS                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         XC    TACDEARN,TACDEARN   CLEAR GROSS TAXABLE EARNINGS                 
         XC    TACDNTAX,TACDNTAX         NON-TAXABLE EARNINGS                   
         XC    TACDYREX,TACDYREX         YTD REIMB. EXPENSES                    
         MVC   TACDSEQ,PROCSEQ           PROCESSED SEQUENCE NUMBER              
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,=C'LIEN PUT'          
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE USES THE CURRENT CHECK, WHICH HAS A W4 TRUST AMOUNT              
* TO BUILD A CHECK FOR THE TRUSTEE.  IT THEN PUTS THE CHECK TO THE              
* SORTER FILE.                                                                  
*                                                                               
TRSTCHK  NTR1                                                                   
         OC    TRUSTSSN,TRUSTSSN   IF W4 TRUSTEE SSN SPECIFIED                  
         BZ    XIT                                                              
*                                                                               
         XC    SRTAMNTS(SRTAMNTL),SRTAMNTS  PRE-CLEAR CHECK AMOUNTS             
         XC    SRTPNH,SRTPNH                                                    
         XC    SRTFSO,SRTFSO       INSURE THERE'S NO FSO INFO SAVED             
         XC    SRTFSON,SRTFSON                                                  
*                                                                               
         MVI   SRTBITS2,SRTBTRST   SET W4 TRUSTEE GENERATED CHK BIT             
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          3) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          4) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          5) BILLING YTD EARNINGS ELEMENT          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TABYELQ          6) BILLING YTD ELEMENT                   
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TATIELQ          7) TAX ID ELEMENT                        
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TANUELQ          8) GST ID NUMBER ELEMENT                 
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
*                                      9) TRUSTEE S/S NUMBER                    
         GOTO1 DELL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK RECORD)                         
         USING TLCKD,R4                                                         
         MVC   SRTTSSN,TLCKSSN     SAVE SSN OF MINOR PAYER                      
         MVC   TLCKSSN,TRUSTSSN    MOVE TRUSTEE SSN TO KEY                      
         MVC   SRTSSN,TRUSTSSN     AND TO SORT RECORD                           
*                                                                               
         LA    R4,ELEM             BUILD MINOR PAYER ELEMENT                    
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTRST   SET W4 TRUSTEE TYPE                          
         MVC   TANUMBER(9),SRTTSSN SET MINOR PAYER S/S NUMBER                   
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         ICM   RF,15,TRUSTCOL      RF = AMOUNT IN TRUST COLLECTED               
         LPR   RF,RF               MAKE SURE ALWAYS POSTIVE                     
         ST    RF,SRTNET           SAVE AS CHECK AMOUNT IN SORT RECORD          
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R4,ELEM             BUILD MINOR PAYER ELEMENT                    
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPT   SET W4 TRUSTEE TYPE                          
         LNR   RF,RF               CONVERT TO NEG. & SAVE IN ELEMENT            
         STCM  RF,15,TAODAMT                                                    
         ST    RF,SRTTRST                          & IN SORT RECORD             
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         BAS   RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4TY,SRTTYPE     SET W4 TYPE                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R4                                                         
         XC    TACAGUA,TACAGUA     CLEAR GUARANTEE CODE                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE HERE                        
         XC    SRTNCDE,SRTNCDE     AND IN SORT RECORD                           
*                                                                               
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY             SET W4 TYPE                         
         XC    TAPDAMTS(TAPDAMTL),TAPDAMTS  CLEAR ALL AMOUNTS                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         XC    TACDEARN,TACDEARN   CLEAR GROSS TAXABLE EARNINGS                 
         XC    TACDNTAX,TACDNTAX         NON-TAXABLE EARNINGS                   
         XC    TACDYREX,TACDYREX         YTD REIMB. EXPENSES                    
         MVC   TACDSEQ,PROCSEQ           PROCESSED SEQUENCE NUMBER              
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,=C'TRST PUT'          
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
         B     XIT                                                              
         EJECT                                                                  
* READ THE W4 RECORD FOR THE CURRENT PERFORMER AND EXTRACT INFORMATION          
* FROM IT INTO THE SORT RECORD.  * NOTE * THE RECORD IS READ INTO AW4IO         
* FOR USE BY OTHER ROUTINES.                                                    
*                                                                               
EXTRW4   NTR1                                                                   
         XC    SRTW4S(SRTW4L),SRTW4S  CLEAR W4 RECORD VARIABLES                 
         NI    SRTBITS,ALL-SRTBW4           CHECKS FIRST BIT                    
*                                                                               
         MVC   AIO,AW4IO           READ W4 RECORD INTO AW4IO                    
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',SRTSSN)                               
         BNE   ERRECW4                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'W',AW4IO),0,=C'W4'                               
*                                                                               
         L     R4,AW4IO            R4 = A(W4 DETAILS ELEMENT)                   
         MVI   ELCODE,TAW4ELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERREW4E                                                          
         USING TAW4D,R4                                                         
*                                                                               
         TM    TAW4STAT,TAW4SCKF   IF THIS PERF'S CHECKS COME FIRST             
         BZ    *+8                                                              
         OI    SRTBITS,SRTBW4      THEN SET BIT IN STATUS                       
*                                                                               
*********TM    TAW4STA2,TAW4SNFI   IF THIS PERF TAKES NO FICA                   
*********BZ    *+8                                                              
*********OI    SRTBITS,SRTBNFI     THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SNDU   IF THIS PERF TAKES NO DUE COMP               
         BZ    *+8                                                              
         OI    SRTBITS,SRTBNDU     THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SDD    IF THIS PERF HAS DIRECT DEPOSIT              
         BZ    *+8                                                              
         OI    SRTBITS,SRTBDD      THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SWIR   IF THIS PERF HAS WIRE TRANSFER               
         BZ    *+8                                                              
         OI    SRTBITS,SRTBWIRE    THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STAT,TAW4STNY   IF YTD AMOUNTS SHOULD NOT PRINT              
         BZ    *+8                                                              
         OI    SRTBITS,SRTBNYTD    THEN SET BIT IN STATUS                       
*                                                                               
         MVC   RECIPST,TAW4RECP    SAVE RECIPROCAL STATE FOR TAX CALC           
         MVC   SRTTYPE,TAW4TYPE    SAVE PERFORMER TYPE IN SORT RECORD           
         MVC   SRTFREQ,TAW4FREQ         FREQUENCY                               
*                                                                               
*                                  SAVE NAME FOR SORT KEY                       
         MVC   LASTNAME(32),TAW4NAM2                                            
*                                                                               
         CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS NOT CORPORATION              
         BE    EW10                                                             
         CLI   SRTTYPE,TAW4TYTR    OR TRUSTEE                                   
         BE    EW10                                                             
         CLI   SRTTYPE,TAW4TYES    OR ESTATE                                    
         BE    EW10                                                             
         MVC   BLOCK(16),FRSTNAME  THEN SQUASH FIRST NAME WITH LAST             
         MVC   BLOCK+16(16),LASTNAME   NAME                                     
         GOTO1 SQUASHER,DMCB,BLOCK,32                                           
         MVC   SRTNAME,BLOCK       SAVE IN SORT RECORD                          
         B     EW20                                                             
*                                                                               
EW10     MVC   SRTNAME,LASTNAME    ELSE SAVE WHOLE NAME IN SORT RECORD          
*                                                                               
EW20     L     R4,AW4IO            R4 = A(ADDRESS ELEMENT)                      
         MVI   ELCODE,TAA2ELQ      LOOK FOR NEW STYLE ADDRESS                   
         BAS   RE,GETEL                                                         
         BE    EW30                                                             
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAADELQ      ELSE LOOK FOR OLD STYLE ADDRESS              
         BAS   RE,GETEL                                                         
*                                                                               
         BE    EW25                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WAREW4A              IN SORT RECORD                           
* NO-OP  L     R0,=A(CHECKL)                                                    
* NO-OP  GOTO1 DOTRACE,DMCB,(C'R',CHECKD),(R0),=C'NO W4 ADDRESS'                
         B     EW35                                                             
*                                                                               
         USING TAADD,R4                                                         
EW25     ZIC   RF,TAADLEN          SAVE ADDRESS IN SORT RECORD                  
         SH    RF,=H'4'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD(0),TAADADD                                                
         B     EW35                                                             
*                                                                               
         USING TAA2D,R4                                                         
EW30     MVC   SRTADD(3*30),TAA2ADDR  SAVE FIRST 3 ADDRESS LINES                
         MVC   WORK(25),TAA2CITY      CITY                                      
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   SRTADD+90(39),WORK     SAVE IT                                   
*                                                                               
EW35     L     R4,AW4IO            R4 = A(PAYEE ELEMENT) IF EXISTS              
         MVI   ELCODE,TAPEELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EW40                                                             
         USING TAPED,R4                                                         
         OC    TAPEEXP,TAPEEXP     IF EXPIRY DATE DEFINED                       
         BZ    *+14                                                             
         CLC   CHKDATE1,TAPEEXP    INSURE CHECK DATE NOT AFTER EXPIRY           
         BH    EW40                                                             
*                                                                               
         MVC   SRTPNAM,TAPENAME    SAVE PAYEE NAME                              
*                                                                               
*                                  IF PAYEE ADDRESS GIVEN                       
         OC    TAPEADD1(4*30),TAPEADD1                                          
         BZ    *+16                THEN SAVE PAYEE ADDR OVER W4 ADDR            
         XC    SRTADD,SRTADD                                                    
         MVC   SRTADD(4*30),TAPEADD1                                            
*                                                                               
EW40     L     R4,AW4IO            R4=A(W4 RECORD)                              
         MVI   ELCODE,TAWXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EW42                                                             
         USING TAWXD,R4            R4=A(EXTRA W4 DETAILS ELEMENT)               
         MVC   SRTW4DOB,TAWXDOB    SAVE MINOR DATE OF BIRTH                     
         MVC   SRTW4PCT,TAWXPCT    SAVE DEDUCT PERCENTAGE                       
         MVC   SRTW4SSN,TAWXTSSN   SAVE TRUSTEE SSN                             
*                                                                               
EW42     MVC   AIO,AW4IO                                                        
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTGST  EXTRACT GST#                    
         MVC   AIO,AIO1                                                         
         MVC   SRTGST#,TGNAME                   SAVE IN SORT RECORD             
*                                                                               
         L     R4,AW4IO            R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         MVI   ELCODE,TAWHELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
EW44     BAS   RE,NEXTEL                                                        
         BNE   EWX                                                              
*                                                                               
         USING TAWHD,R4                                                         
         OC    TAWHEMP,TAWHEMP     IF NO EMPLOYER DEFINED                       
         BNZ   *+10                                                             
         MVC   TAWHEMP,TGTPEMP     SET TO DEFAULT EMPLOYER                      
*                                                                               
         CLC   TAWHEMP,TGTPEMP     ONLY USE TALENT PARTNERS FOR NOW             
         BNE   EW44                ** NOT SUPPORTING MULT. RULES YET **         
*                                                                               
         CLC   TAWHUNIT,=C'FD '    SKIP IF THIS IS FEDERAL                      
         BE    EW44                                                             
*                                                                               
         CLI   TAWHUNIT+2,C' '     IF THIS IS STATE                             
         BNE   *+14                                                             
         MVC   SRTSOR,TAWHUNIT     SAVE STATE OF RESIDENCE                      
         B     EW44                                                             
*                                                                               
         MVC   SRTCOR,TAWHUNIT     ELSE SAVE CITY OF RESIDENCE                  
         B     EW44                                                             
*                                                                               
EWX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE PAYMENT DETAILS ELEMENT IN THE CHECK             
* RECORD INTO THE SORT RECORD.                                                  
*                                                                               
EXTRCPAY NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTW4TY,TAPDW4TY    SAVE W4 TYPE IN SORT RECORD                  
         MVC   SRTOV1,TAPDOV1           OVERSCALE PERCENTAGE                    
         MVC   SRTACDE,TAPDACDE         APPLIED CODE                            
         MVC   SRTPNH,TAPDPNH           PENSION & HEALTH AMOUNT                 
*                                                                               
         L     RE,TAPDPAYI         PAYMENT AMOUNT WILL COME FROM EITHER         
         A     RE,TAPDPAYC             TAPDPAYI OR TAPDPAYC                     
         ST    RE,SRTPAY                                                        
*                                                                               
         ST    RE,SRTNET           SAVE PAYMENT AMOUNT IN CHECK ACCUM           
*                                                                               
         MVC   SRTAPPL,TAPDAPPL    SAVE APPLIED CREDITS IN SORT RECORD          
         MVC   SRTGUAR,TAPDGUAR         GUARANTEE CREDITS                       
         MVC   SRTREXP,TAPDREXP         REIMBURSED EXPENSES                     
         MVC   SRTMDED,TAPDMDED         MISCELLANEOUS DEDUCTIONS                
         MVC   SRTDUES,TAPDDUES         UNION DUES                              
         MVC   SRTINR,TAPDINR           I&R FRATERNAL CONTRIBUTION              
         MVC   SRTSTUS,TAPDSTUS         STARTING USE NUMBER                     
         MVC   SRTUSES,TAPDUSES         NUMBER OF USES                          
         MVC   SRTVMAJ,TAPDMAJ          MAJORS FOR VERSIONS                     
         MVC   SRTVUNT,TAPDUNIT         UNITS                                   
*                                                                               
         TM    TAPDOPT1,TAPDOCAN   IF CANADIAN TAXES ARE TO BE TAKEN            
         BZ    *+8                                                              
         OI    SRTBITS,SRTBCAN     THEN SET CANADIAN TAKES BIT IN SORT          
*                                                                               
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE UPGRADE DETAILS ELEMENT IN THE CHECK             
* RECORD INTO THE SORT RECORD.  ONLY PRESENT IF PAID A VERSION.                 
*                                                                               
EXTRCUPG NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAUPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   XIT                 SKIP IF NONE FOUND                           
         USING TAUPD,R4                                                         
*                                                                               
         MVC   SRTVUMAJ,SRTVMAJ    SAVE MAJORS/UNITS FROM PD ELEM AS            
         MVC   SRTVUUNT,SRTVUNT      UPGRADE MAJORS/UNITS IN SORT REC           
*                                                                               
         CLI   TGUSEQU,UCBL        IF CBL USE                                   
         BNE   ECU5                                                             
         MVC   SRTVUNT,TAUPICBU      SAVE INITIAL UNITS                         
         B     XIT                                                              
ECU5     MVC   SRTVMAJ,TAUPIMAJ   ELSE SAVE INITIAL MAJORS/UNITS IN             
         MVC   SRTVUNT,TAUPIUNT       SORT RECORD                               
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT SESSION DETAILS INFORMATION IF A SESSION DETAILS ELEMENT              
* EXISTS ON THE CHECK RECORD.                                                   
*                                                                               
EXTRSESS NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(SESSION DETAILS ELEMENT)              
         MVI   ELCODE,TASDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ESX                 RETURN IF NONE FOUND                         
         USING TASDD,R4                                                         
*                                                                               
         TM    TGCAUNI,AFM         IF MUSICIAN                                  
         BZ    ES10                                                             
         MVC   SRTSP,TASDMSP       THEN SAVE SPOTS IN SORT RECORD               
         B     ESX                                                              
*                                                                               
ES10     CLI   SRTMED,TACOMEDR     ELSE IF RADIO COMMERCIAL                     
         BNE   ES20                                                             
         MVC   SRTSP,TASDRSP       THEN SAVE SPOTS IN SORT RECORD               
         MVC   SRTTAG,TASDRTG                TAGS                               
         B     ESX                                                              
*                                                                               
ES20     MVC   SRTSP,TASDSP        ELSE SAVE SPOTS IN SORT RECORD               
         MVC   SRTDAY,TASDDAY                DAYS                               
         MVC   SRTOT,TASDOT                  OVERTIME HOURS                     
         MVC   SRTDT,TASDDT                  DOUBLE-TIME HOURS                  
         MVC   SRTTRV,TASDTRV                TRAVEL HOURS                       
         MVC   SRTPDW,TASDPDW                PRIOR DAY WARDROBE HOURS           
         MVC   SRTTAG,TASDTAG                TAGS                               
*                                                                               
ESX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE CAST DETAILS ELEMENT IN THE CHECK                
* RECORD INTO THE SORT RECORD.  IF INDIVIDUAL IS BEING PAID AS A CORP           
* THEN EXTRACT THE INDIVIDUAL CORP FROM THE TAX ID ELEMENT IN THE CHECK         
* RECORD.                                                                       
*                                                                               
EXTRCAST NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRECCA                                                          
         USING TACAD,R4                                                         
*                                                                               
         OC    TACAFRST,TACAFRST   IF FIRST SERVICES DATE IS ZERO               
         BNZ   ECA50                                                            
*                                                                               
         TM    TGCAUNI,AFM         THEN IF UNION IS AFM                         
         BZ    ECA10                                                            
         MVC   TACAFRST,SRTMUS     THEN SAVE MUSIC DATE IN ELEMENT              
         B     ECA50                                                            
*                                                                               
ECA10    CLC   TACAONOF,=C'ON '    ELSE IF ON CAMERA                            
         BNE   ECA20                                                            
         MVC   TACAFRST,SRTFILM    THEN SAVE FILM DATE IN ELEMENT               
         B     ECA50                                                            
*                                                                               
ECA20    MVC   TACAFRST,SRTRCRD    ELSE SAVE RECORD DATE IN ELEMENT             
*                                                                               
ECA50    MVC   SRTONOF,TACAONOF    SAVE CAMERA (ON/OFF) IN SORT RECORD          
         MVC   SRTUNIT,TACAUNIT         TAX UNIT                                
         MVC   SRTDBL,TACADBL           N'DOUBLES                               
         MVC   SRTFRST,TACAFRST         FIRST SERVICES DATE                     
         MVC   SRTUN,TACAUN             UNION                                   
         MVC   SRTLOCL,TACALOCL         LOCAL                                   
         MVC   SRTYEAR,TACAYEAR         CONTRACT YEAR                           
         MVC   SRTNCDE,TACANCDE         AGENT CODE                              
         MVC   SRTCFCY,TACAFCYC         OVERRIDE OF CMCL FIRST CYCLE            
         MVC   SRTCEXP,TACAEXP          OVERRIDE OF CMCL EXPIRATION             
         MVC   SRTOV2,TACAOV2           OVERSCALE PERCENTAGE 2                  
         MVC   SRTCSTAT,TACASTAT        STATUS BYTE 1                           
         MVC   SRTCSTA2,TACASTA2        STATUS BYTE 2                           
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,SRTUNIT)  LOOK UP TAX UNIT                        
         BE    ECA60                                                            
         CLI   SRTW4TY,TAW4TYCO    INVALID OK ONLY FOR CORPS & TRUSTEE          
         BE    ECA70                                                            
         CLI   SRTW4TY,TAW4TYTR                                                 
         BE    ECA70                                                            
         CLI   SRTADJS,0           OR IF CHECK IS AN ADJUSTMENT                 
         BNE   ECA70                                                            
         B     ERRETAX                                                          
*                                                                               
ECA60    CLI   SRTUNIT+2,C' '      IF TAX UNIT IS A STATE                       
         BNE   *+14                                                             
         MVC   SRTSOW,SRTUNIT      THEN SAVE STATE OF WORK                      
         B     *+16                                                             
         MVC   SRTSOW,TGTASTCY     ELSE SAVE STATE OF WORK FOR CITY             
         MVC   SRTCOW,SRTUNIT      AND SAVE CITY OF WORK                        
*                                                                               
         CLI   SRTW4TY,TAW4TYIN    IF THIS IS INDIVIDUAL                        
         BNE   ECA70                                                            
         CLC   SRTSOR,=C'CN '      AND CANADIAN RESIDENT                        
         BNE   ECA70                                                            
         CLC   SRTSOW,=C'CN '      AND WORKING IN CANADA                        
         BE    ERRECNI             THEN SET ERROR - SHOULD BE CORP              
*                                                                               
ECA70    L     R4,ASRTCHK          IF TAX ID ELEMENT EXISTS                     
         MVI   ELCODE,TATIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ECA100                                                           
         USING TATID,R4                                                         
*                                                                               
         MVC   SRTFSO,SRTSSN       THEN SAVE INDIVIDUAL SSN AND NAME AS         
         MVC   SRTFSON,SRTNAME          FSO SSN AND NAME                        
         MVC   SRTW4SOR,SRTSOR           SAVE ST OF RESID. FROM IND.            
         MVC   COMMNAME(L'SRTW4T),SRTW4T SAVE W4 TRUSTEE FROM IND.              
*                                                                               
         MVC   SRTSSN,TATIID       SAVE CORPORATION ID AS SSN                   
*                                                                               
         BAS   RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4T,COMMNAME     RESET W4 TRUSTEE FROM INDIVID.               
         XC    COMMNAME,COMMNAME                                                
*                                                                               
ECA100   L     R2,ALCLTAB          R2 = A(AFT LCLS NOT GETTING CHECKS)          
*                                                                               
ECA110   CLI   0(R2),0             IF END OF TABLE THEN DONE                    
         BE    ECAX                                                             
*                                                                               
         CLC   SRTUN,0(R2)         IF UNION/LOCAL MATCH TABLE ENTRY             
         BNE   ECA120                                                           
         CLC   SRTLOCL,3(R2)                                                    
         BE    ECA130              THEN GO SET NO UNION COPY BIT                
*                                                                               
ECA120   LA    R2,6(R2)            ELSE BUMP TO NEXT LCLTAB ENTRY               
         B     ECA110              AND LOOP BACK                                
*                                                                               
ECA130   OI    SRTBITS,SRTBLCL     SET BIT FOR NO UNION COPIES                  
*                                                                               
ECAX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT THE NAME AND ADDRESS OF THE AGENT IF THE CAST MEMBER USED             
* ONE AND PUT THE INFORMATION OVER THE PAYEE NAME AND ADDRESS.                  
*                                                                               
EXTRNCDE NTR1                                                                   
         OC    SRTNCDE,SRTNCDE     IF CAST MEMBER USED AN AGENT                 
         BZ    ENCX                                                             
*                                                                               
         XC    BLOCK(44),BLOCK     READ AGENT RECORD AND EXTRACT NAME           
         MVI   BLOCK,8+36                                                       
         EDIT  (2,SRTNCDE),(4,WORK),ALIGN=RIGHT,FILL=0                          
         GOTO1 RECVAL,DMCB,TLANCCDQ,(X'88',WORK),BLOCK                          
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WAREAN               IN SORT RECORD                           
         B     ENCX                                                             
*                                                                               
         USING TAAND,R4                                                         
         L     R4,AIO              R4 = A(AGENT ELEMENT)                        
         MVI   ELCODE,TAANELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ENC5                SKIP IF NOT FOUND                            
         TM    TAANSTAT,TAANSNCK   IF IGNORING THIS AGENT ON CHECKS             
         BZ    ENC5                                                             
         XC    SRTNCDE,SRTNCDE     CLEAR AGENT CODE FROM SORT RECORD            
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS EL ON CHECK REC)         
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ENCX                                                             
         USING TACAD,R4                                                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE                             
         B     ENCX                                                             
*                                                                               
ENC5     MVC   SRTPNAM,BLOCK+8     SAVE PAYEE NAME IN SORT RECORD               
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'N',AIO),0,=C'AGENT'                              
*                                                                               
         XC    SRTADD,SRTADD       PRE-CLEAR ADDRESS                            
*                                                                               
         L     R4,AIO              R4 = A(ADDRESS ELEMENT)                      
         MVI   ELCODE,TAADELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAADD,R4                                                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WAREANA              IN SORT RECORD                           
         B     ENCX                                                             
*                                                                               
         ZIC   RF,TAADLEN          SAVE ADDRESS IN SORT RECORD                  
         SH    RF,=H'4'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD(0),TAADADD                                                
ENCX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE GETS THE CHECK AMOUNTS (WITHHOLDINGS, ETC.) FROM THE             
* CHECK RECORD.  THE AMOUNTS ARE ALREADY THERE BECAUSE THE CHECK HAS            
* ALREADY BEEN WRITTEN BY A PREVIOUS CHECK RUN WHICH DID ALL THE                
* CALCULATIONS NECESSARY TO COMPUTE THE WITHHOLDINGS AND THE NET CHECK          
* AMOUNT.  THIS ROUTINE EXTRACTS THOSE AMOUNTS FROM THE CHECK WITHHOLD          
* ELEMENTS, THE YTD ELEMENTS, AND THE CHECK DETAILS ELEMENT.                    
*                                                                               
EXTRAMNT NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         CLI   RERUN,C'Y'          IF RUNNING FOR EXTRACTION ONLY               
         BNE   *+10                                                             
         MVC   PROCSEQ,TACDSEQ     THEN SAVE ORIGINAL SEQUENCE NUMBER           
*                                                                               
         MVC   SRTNET,TACDNET      SAVE NET CHECK AMOUNT IN SORT RECORD         
         MVC   SRTYREX,TACDYREX         YTD REIMBURSED EXPENSES                 
         MVC   SRTEARN,TACDEARN    SAVE TAXABLE EARNINGS IN SORT REC            
         MVC   SRTNTAX,TACDNTAX    NON-TAXABLE EARNINGS                         
*                                                                               
         MVC   SRTGRS,SRTEARN      SET GROSS EARNINGS=TAXABLE EARNINGS          
         CLI   RECNUM,CN           IF CHECK IS FOR CAN$                         
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYCO    OR FOR CORPORATION                           
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYTR    OR FOR TRUSTEE                               
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYFO    OR FOR FOREIGNER                             
         BNE   *+16                                                             
EA3      L     RF,SRTNTAX          GROSS EARNINGS=NON-TAXABLE EARNINGS          
         S     RF,SRTREXP          LESS REIMBURSED EXPENSES                     
         ST    RF,SRTGRS                                                        
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK EXTRA DETAILS ELEMENT)          
         MVI   ELCODE,TACXELQ                                                   
         USING TACXD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   *+14                                                             
         L     RF,TACXGST          SAVE GOODS AND SERVICES TAX                  
         LCR   RF,RF               (COMPLEMENT INTERNALLY)                      
         ST    RF,SRTGST                                                        
*                                                                               
         MVC   AIO,ASRTCHK         GET YTD ELEMENT                              
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVC   AIO,AIO1                                                         
         BNE   EA5                                                              
         L     R3,TGELEM           R3=A(YTD EARNINGS ELEMENT)                   
         USING TAYED,R3                                                         
         MVC   SRTYEARN,TAYEEARN   SAVE YTD TAXABLE EARNINGS                    
         MVC   SRTYNTAX,TAYENTAX            NON-TAXABLE EARNINGS                
*                                                                               
EA5      L     R4,ASRTCHK          R4 = A(FIRST CHECK WITHHOLD ELEMENT)         
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         BE    EA10                                                             
         LA    R4,DMYCWEL          IF NONE, POINT TO DUMMY AND PROCESS          
         USING TACWD,R4                                                         
*                                                                               
EA10     MVC   AIO,ASRTCHK         GET YTD ELEMENT WITH SAME UNIT               
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 GETL,DMCB,(3,TACWUNIT)                                           
         MVC   AIO,AIO1                                                         
         BE    EA15                IF ERROR RETURNED                            
*                                                                               
         C     R4,=A(DMYCWEL)      THEN IF NOT POINTING TO DUMMY EL.            
         BE    *+16                                                             
         CLI   RERUN,C'Y'          AND IF RUNNING FOR EXTRACTION ONLY           
         BNE   *+8                                                              
         BAS   RE,WARECKY          THEN SET WARNING IN SORT RECORD              
*                                                                               
         XC    ELEM,ELEM           ELSE R5 = A(CLEAR BLOCK)                     
         LA    R5,ELEM                                                          
         B     EA17                                                             
*                                                                               
EA15     L     R5,TGELEM           ELSE R5 = A(YTD WITHHOLD ELEMENT)            
         USING TACYD,R5                                                         
*                                                                               
EA17     CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   EA40                                                             
         MVC   SRTFTAX,TACWTAX     THEN SAVE FEDERAL TAX IN SORT RECORD         
         MVC   SRTFICA,TACWFICA              FICA                               
         MVC   SRTYFED,TACYTAX               YTD FEDERAL TAX                    
         MVC   SRTYFIC,TACYFICA              YTD FICA                           
         B     EA90                                                             
*                                                                               
EA40     CLI   TACWUNIT+2,C' '     ELSE IF UNIT IS A CITY                       
         BNH   EA50                                                             
         TM    TACWSTAT,TACWSTAX   THEN IF CITY IS TAXABLE CITY                 
         BZ    EA45                                                             
         MVC   SRTLTAX,TACWTAX     SAVE LOCAL TAX IN SORT RECORD                
         MVC   SRTYLOC,TACYTAX          YTD LOCAL TAX                           
         MVC   SRTTXCY,TACWUNIT    SAVE TAXABLE CITY                            
*                                                                               
EA45     TM    TACWSTAT,TACWSRES   IF CITY IS RESIDENCE CITY                    
         BZ    *+10                                                             
         MVC   SRTCOR,TACWUNIT     THEN SAVE CITY OF RESIDENCE                  
*                                                                               
         TM    TACWSTAT,TACWSWRK   IF CITY IS WORK CITY                         
         BZ    *+10                                                             
         MVC   SRTCOW,TACWUNIT     THEN SAVE CITY OF WORK                       
         B     EA90                                                             
*                                                                               
EA50     CLC   TACWUNIT,=C'CN '    ELSE IF UNIT IS CANADA                       
         BNE   EA60                                                             
         MVC   SRTCTAX,TACWTAX     THEN SAVE CANADIAN TAX IN SORT REC           
         MVC   SRTGSTPD,TACWGST              GOODS & SERVICES TAX PAID          
         MVC   SRTYCAN,TACYTAX               YTD CANADIAN TAX                   
         MVC   SRTYGST,TACYGST               YTD GOODS & SERVICES TAX           
         TM    TACWSTAT,TACWSWRK   IF STATE IS WORK STATE                       
         BZ    *+10                                                             
         MVC   SRTSOW,TACWUNIT     SAVE STATE OF WORK IN SORT RECORD            
         B     EA90                                                             
*                                                                               
EA60     TM    TACWSTAT,TACWSTAX   ELSE IF STATE IS TAXABLE STATE               
         BZ    EA70                                                             
         MVC   SRTSDI,TACWSDI      THEN SAVE SDI IN SORT REC                    
         MVC   SRTSUI,TACWSUI                SUI                                
         MVC   SRTYSTA,TACYTAX               YTD STATE TAX                      
*                                                                               
         MVC   SRTTXST,TACWUNIT    SAVE TAXABLE STATE IN SORT RECORD            
*                                                                               
         GOTO1 GETSUC,DMCB,SRTTXST GET STATE U.C. # INTO SORT RECORD            
*                                                                               
EA70     TM    TACWSTAT,TACWSWRK   IF STATE IS WORK STATE                       
         BZ    EA80                                                             
         MVC   SRTWTAX,TACWTAX     THEN SAVE WORK STATE TAX IN SORT REC         
         MVC   SRTSOW,TACWUNIT     SAVE STATE OF WORK IN SORT RECORD            
         B     EA90                                                             
*                                                                               
EA80     MVC   SRTRTAX,TACWTAX     ELSE SAVE RES STATE TAX IN SORT REC          
         MVC   SRTSOR,TACWUNIT     SAVE STATE OF RES IN SORT RECORD             
*                                                                               
EA90     MVI   ELCODE,TACWELQ      GET NEXT CHECK WITHHOLD ELEMENT              
         BAS   RE,NEXTEL                                                        
         BE    EA10                REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
EA100    MVI   ELCODE,TAODELQ      LOOK FOR FEDERAL OTHER DEDUCT ELEM           
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPF))                                     
         BNE   EA110               SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(FEDERAL OTHER DEDUCTION ELEM)         
         USING TAODD,R4                                                         
         L     RF,SRTFTAX          ADD TO FEDERAL TAXES                         
         A     RF,TAODAMT                                                       
         ST    RF,SRTFTAX                                                       
*                                                                               
EA110    GOTO1 GETL,DMCB,(1,=AL1(TAODTYPM))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(MPTRF OTHER DEDUCTION ELEM)           
         MVC   SRTMPT,TAODAMT                                                   
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPP))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(CHARITY OTHER DEDUCTION ELEM)         
         MVC   SRTCHAR,TAODAMT                                                  
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPD))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(DIRCET OTHER DEDUCTION ELEM)          
         MVC   SRTDIRCT,TAODAMT                                                 
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPW))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(WIRE OTHER DEDUCTION ELEM)            
         MVC   SRTWIRE,TAODAMT                                                  
*                                                                               
         MVC   AIO,AIO1                                                         
         L     RF,SRTFTAX          TOTAL DEDUCTIONS = FEDERAL TAX               
         A     RF,SRTFICA              + FICA                                   
         A     RF,SRTRTAX              + RESIDENT STATE TAX                     
         A     RF,SRTWTAX              + WORK STATE TAX                         
         A     RF,SRTSDI               + STATE DISABILITY INSURANCE             
         A     RF,SRTSUI               + STATE UNEMPLOYMENT INSURANCE           
         A     RF,SRTLTAX              + LOCAL TAX                              
         A     RF,SRTCTAX              + CANADIAN TAX                           
**NO-OP**A     RF,SRTGSTPD             + GST (NOT A DEDUCTION 9/99)             
         A     RF,SRTMPT               + MPTRF                                  
         A     RF,SRTCHAR              + PERMANENT CHARITIES                    
         A     RF,SRTDIRCT             + DIRECT DEPOSIT                         
         A     RF,SRTWIRE              + WIRE TRANSFER                          
         A     RF,SRTMDED              + MISCELLANEOUS DEDUCTIONS               
         A     RF,SRTDUES              + UNION DUES                             
         ST    RF,SRTTDED          SAVE TOTAL DEDUCTIONS                        
*                                                                               
         BAS   RE,DUEWITH          EXTRACT DUE COMPANY AMOUNT                   
         BAS   RE,LIENWITH         EXTRACT LIEN AMOUNT                          
         BAS   RE,TRSTWITH         EXTRACT W4 TRUSTEE AMOUNT                    
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR DUE COMPANY WITHHOLDING            
* ELEMENTS, AND IF IT FINDS ANY, IT CALCULATES THE DUE COMPANY AMOUNT           
* FROM THEM AND SAVES THE AMOUNT IN SRTDUE.  ADDITIONALLY, IF THE CHECK         
* IS A VOID OR ISSUE CHECK, THEN THE DUE COMPANY AMOUNTS WILL BE                
* DEDUCTED FROM THEIR CORRESPONDING DUE COMPANY TABLE ENTRIES.                  
*                                                                               
DUEWITH  NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(FIRST DUECOMP WITH ELEMENT)           
         MVI   ELCODE,TADWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   DWX                 IF NONE FOUND THEN DONE                      
         USING TADWD,R4                                                         
*                                                                               
DW10     ICM   RF,15,TADWREC       ADD AMOUNT TO DUE COMPANY AMOUNT             
         A     RF,SRTDUE                                                        
         ST    RF,SRTDUE                                                        
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS A VOID OR ISSUE CHECK            
         BE    DW90                                                             
         TM    SRTADJS,TAPDADVD                                                 
         BO    DW20                                                             
         TM    SRTADJS,TAPDADIS                                                 
         BZ    DW90                                                             
*                                                                               
DW20     ST    R4,TGDUB            THEN SAVE A(DW ELEMENT) IN TGDUB             
         MVC   FULL,TADWREC        SAVE COLLECTED AMOUNT IN FULL                
*                                                                               
         LA    R3,KEY              BUILD KEY FOR DUE COMPANY RECORD             
         USING TLDUD,R3                                                         
         XC    TLDUKEY,TLDUKEY                                                  
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,TADWDUC                                                  
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND THEN ERROR               
         CLC   TLDUKEY,KEYSAVE                                                  
         BNE   ERREDU                                                           
*                                  GET DUETAB ENTRY FOR THIS CODE               
         GOTO1 GETDUE,DMCB,TADWDUC                                              
         BNE   ERREDU                                                           
*                                                                               
         L     R5,0(R1)            R5 = A(DUETAB ENTRY)                         
         USING DUETABD,R5                                                       
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         ICM   RF,15,TADUCOL       ADD COLLECTED AMOUNT TO ELEMENT              
         A     RF,FULL                                                          
         STCM  RF,15,TADUCOL                                                    
*                                                                               
         L     R4,TGDUB            SAVE BALANCE REMAINING IN DW ELEM            
         USING TADWD,R4                                                         
         STCM  RF,15,TADWBAL                                                    
*                                                                               
DW90     MVI   ELCODE,TADUELQ      GET NEXT WITHHOLDING ELEMENT                 
         BAS   RE,NEXTEL                                                        
         BE    DW10                REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
DWX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR A LIEN WITHHOLDING                 
* ELEMENT, AND IF IT FINDS ONE, IT SAVES THE AMOUNT IN SRTLIEN.                 
* ADDITIONALLY, IF THE CHECK IS A VOID OR ISSUE CHECK, THEN THE LIEN            
* AMOUNT WILL BE DEDUCTED FROM ITS CORRESPONDING LIEN TABLE ENTRY.              
*                                                                               
LIENWITH NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(LIEN WITHHOLDING ELEMENT)             
         MVI   ELCODE,TALWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   LWX                 IF NOT FOUND THEN DONE                       
         USING TALWD,R4                                                         
*                                                                               
         ST    R4,TGDUB            SAVE A(LW ELEMENT) IN DUB                    
*                                                                               
         MVC   SRTLIEN,TALWREC     SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSLIN   IF CHECK IS FOR LIEN PAYEE THEN              
         BZ    LW20                                                             
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTLIN  EXTRACT LIEN PAYER SSN          
         MVC   AIO,AIO1                                                         
         MVC   SRTLSSN,TGNAME                   SAVE IN SORT RECORD             
         B     LWX                              AND THEN GET OUT                
*                                                                               
LW20     LA    R3,KEY              ELSE BUILD KEY FOR LIEN                      
         USING TLLND,R3                                                         
         XC    TLLNKEY,TLLNKEY                                                  
         MVI   TLLNCD,TLLNCDQ                                                   
         MVC   TLLNSSN,SRTSSN                                                   
         L     R4,TGDUB                                                         
         USING TALWD,R4                                                         
         MVC   TLLNLIN,TALWLIN                                                  
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND THEN ERROR               
         CLC   TLLNKEY,KEYSAVE                                                  
         BNE   ERRELN                                                           
*                                  ELSE GET LIEN TABLE ENTRY                    
         GOTO1 GETLIEN,DMCB,TLLNLIN                                             
*                                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         MVC   LIENSSN,TALNPAYE    SAVE SSN FOR LIEN PAYEE CHECK                
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS A VOID OR ISSUE CHECK            
         BE    LWX                                                              
         TM    SRTADJS,TAPDADVD                                                 
         BO    *+12                                                             
         TM    SRTADJS,TAPDADIS                                                 
         BZ    LWX                                                              
*                                                                               
         ICM   RF,15,TALNCOL       ADD COLLECTED AMOUNT TO ELEMENT              
         A     RF,SRTLIEN                                                       
         STCM  RF,15,TALNCOL                                                    
*                                                                               
         L     R4,TGDUB            SAVE BALANCE REMAINING IN LW ELEM            
         USING TALWD,R4                                                         
         STCM  RF,15,TALWBAL                                                    
*                                                                               
LWX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR A W4 TRUSTEE DEDUCTION             
* ELEMENT, AND IF IT FINDS ONE, IT SAVES THE AMOUNT IN SRTTRST.                 
* ADDITIONALLY, IF THE CHECK IS A VOID OR ISSUE CHECK, THEN THE AMOUNT          
* IN TRUST WILL BE DEDUCTED FROM ITS CORRESPONDING TABLE ENTRY.                 
*                                                                               
TRSTWITH NTR1                                                                   
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   TRSTWX                                                           
         L     R4,TGELEM           R4 = A(W4 TRUSTEE DEDUCTION ELE)             
         ST    R4,TGDUB            SAVE ELEMENT ADDRESS IN DUB                  
         USING TAODD,R4                                                         
         MVC   SRTTRST,TAODAMT     SAVE AMOUNT IN SORT RECORD                   
         MVC   TRUSTCOL,TAODAMT    SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTRST  EXTRACT SSN                     
         MVC   AIO,AIO1                                                         
         TM    TACDSTAT,TACDSTRS   IF CHECK IS FOR W4 TRUSTEE THEN              
         BZ    *+14                                                             
         MVC   SRTTSSN,TGNAME      THEN SAVE MINOR PAYER IN SORT REC            
         B     TRSTWX              AND EXIT                                     
*                                                                               
         XC    TRUSTSSN,TRUSTSSN   ELSE, IF TRUSTEE SSN FOUND                   
         CLC   TGNAME,MYSPACES                                                  
         BNH   *+10                                                             
         MVC   TRUSTSSN,TGNAME     SAVE SSN OF TRUSTEE GEN. CHK                 
         BAS   RE,GETTRST          GET TABLE ENTRY                              
         L     R5,0(R1)            R5 = A(TABLE ENTRY)                          
         USING TRSTABD,R5                                                       
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS A VOID, ISSUE, REFUND            
         BE    TRSTWX                                                           
         TM    SRTADJS,TAPDADVD                                                 
         BO    TRSTW10                                                          
         TM    SRTADJS,TAPDADIS                                                 
         BO    TRSTW10                                                          
         TM    SRTADJS,TAPDADTR                                                 
         BZ    TRSTWX                                                           
*                                                                               
TRSTW10  ICM   RF,15,TRSTCOL       ADD COLLECTED AMT TO TABLE                   
         A     RF,SRTTRST                                                       
         STCM  RF,15,TRSTCOL                                                    
*                                                                               
         TM    SRTADJS,TAPDADTR    IF NOT TRUST REFUND CHECK                    
         BO    TRSTWX                                                           
         L     R4,TGDUB            SAVE BALANCE REMAINING IN ELEM               
         USING TAODD,R4                                                         
         STCM  RF,15,TAODAMT                                                    
*                                                                               
TRSTWX   B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
* THIS ROUTINE CALCULATES THE CHECK AMOUNTS AND DETERMINES THE FINAL            
* NET CHECK AMOUNT.  IT MAKE CALLS TO DUECOMP, TAXES, MISCDED, LIENS,           
* AND TRUSTEES TO COMPUTE THE W/HOLDINGS. THE FINAL NET CHECK AMT WILL          
* BE IN SRTNET.   LASTLY, THE ROUTINE WILL UPDATE THE CHECK DETAILS             
* ELEMENT WITH CHECK AMOUNTS AND OTHER INFORMATION.                             
*                                                                               
CALCAMNT NTR1                                                                   
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TALWELQ          3) LIEN WITHHOLDING ELEMENTS             
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          4) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          5) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          6) YTD EARNINGS ELEMENT                  
         GOTO1 DELL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVI   ELCODE,TANUELQ          7) GST ID NUMBER ELEMENT                 
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
         MVC   AIO,AIO1                                                         
*                                                                               
         BAS   RE,DUECOMP          APPLY DUE COMPANIES TO CHECK                 
*                                                                               
         MVC   SRTGRS,SRTNET       SAVE GROSS EARNINGS IN SORT RECORD           
*                                                                               
         GOTO1 =V(TACKTAX),DMCB,(RC)  TAKE TAXES FROM CHECK                     
*                                                                               
         BAS   RE,MISCDED          SUBTRACT OUT MISC DEDUCTIONS                 
*                                                                               
         BAS   RE,TRST             APPLY W4 TRUSTEE TO CHECK                    
         OC    SRTTRST,SRTTRST     IF W4 TRUSTEE APPLIED                        
         BNZ   *+8                 SKIP LIENS                                   
         BAS   RE,LIENS            APPLY LIENS TO CHECK                         
*                                                                               
         L     RF,SRTNET           ADD REIMBURSED EXPENSES TO CHECK             
         A     RF,SRTREXP                                                       
         ST    RF,SRTNET                                                        
*                                                                               
         BAS   RE,DIRECTD          CHECK DIRECT DEPOSIT OF CHECK                
         BAS   RE,WIRED            CHECK WIRE TRANSFER CHECK                    
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         CLI   RECNUM,CN           IF CHECK IS NOT FOR CAN$                     
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYCO    AND NOT FOR CORPORATION                      
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYTR    AND NOT FOR TRUSTEE                          
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYFO    AND NOT FOR FOREIGNER                        
         BE    CA20                                                             
         MVC   TACDEARN,SRTGRS     SAVE TAXABLE EARNINGS IN ELEMENT             
         MVC   TACDNTAX,SRTREXP         REIMBURSED EXPENSES                     
         B     CAX                                                              
*                                                                               
CA20     L     RF,SRTGRS           ELSE NON-TAXABLE EARNINGS = GROSS            
         A     RF,SRTREXP              EARNINGS + REIMBURSED EXPENSES           
         ST    RF,TACDNTAX         SAVE IN ELEMENT                              
*                                                                               
CAX      MVC   SRTEARN,TACDEARN    SAVE TAXABLE EARNINGS IN SORT RECORD         
         MVC   SRTNTAX,TACDNTAX         NON-TAXABLE EARNINGS                    
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE CALLS THE ROUTINE TAPPGUAR TO TAKE GUARANTEE CREDITS             
* FROM THIS CHECK IF THE PERFORMER IS ON A GUARANTEE.  ADDITIONALLY,            
* IT TAKES THE INVOICE COMMENT ELEMENT ON THE INVOICE RECORD AND ADDS           
* IT TO THE CHECK RECORD FOR USE BY TAPPGUAR.                                   
*                                                                               
TAKEGUAR NTR1                                                                   
         TM    SRTINVS,TAINSBIL    IF INVOICE BILLED THEN RETURN                
         BO    TGX                                                              
         MVI   BYTE,TACMTYPG       SEARCH INVOICE RECORD FOR INVOICE            
         MVC   AIO,AINVIO              COMMENT ELEMENT                          
         MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,BYTE)                                               
         MVC   AIO,AIO1                                                         
         BNE   TG10                                                             
*                                                                               
         L     RF,TGELEM           MOVE ELEMENT TO ELEM                         
         MVC   ELEM,0(RF)                                                       
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                  CALL TAPPGUAR TO TAKE GUAR CREDITS           
TG10     GOTO1 CALLTAPP,DMCB,(RC),(X'A0',ASRTCHK),AINVIO,SYSCOMM                
         BE    TG20                                                             
         MVC   INVTERR,0(R1)       IF ERROR THEN SAVE INVOICE ERROR             
         TM    INVTERR,X'80'       TEST THIS IS ACTUALLY A WARNING              
         BZ    ERRABORT            NO - ABORT INVOICE                           
         BAS   RE,ADDERI           YES - ADD TO ERROR TABLE                     
         MVI   INVTERR,0           CLEAR ERROR NUMBER                           
         CLI   0(R1),TAINWGDU      IF THIS PARTICULAR WARNING                   
         BNE   *+12                                                             
         MVI   0(R1),X'FE'         SIMULATE UPDATE RETURN CODE                  
         B     *+8                                                              
         MVI   0(R1),X'FF'         ELSE SIMULATE NO CHANGES MADE                
*                                                                               
TG20     CLI   0(R1),X'FF'         IF NO CHANGES MADE THEN RETURN               
         BE    TGX                                                              
         L     R4,ASRTCHK          ELSE R4 = A(PAYMENT DETAILS ELEM)            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTGUAR,TAPDGUAR    SAVE GUARANTEE CREDITS IN SORT REC           
         MVC   SRTACDE,TAPDACDE         APPLIED CODE                            
         MVC   SRTPNH,TAPDPNH           PENSION & HEALTH AMOUNT                 
*                                                                               
         L     RE,TAPDPAYI         PAYMENT AMOUNT WILL COME FROM EITHER         
         A     RE,TAPDPAYC             TAPDPAYI OR TAPDPAYC                     
         ST    RE,SRTPAY                                                        
*                                                                               
         ST    RE,SRTNET           SAVE PAYMENT AMOUNT IN CHECK ACCUM           
TGX      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE DUE COMPANY AMOUNT FOR THE CHECK.  IT MAKES         
* CALLS TO GETDUE FOR EACH DUE COMPANY KEY ON FILE.  GETDUE WILL                
* EITHER FIND A TABLE ENTRY FOR THE KEY OR READ A RECORD AND BUILD A            
* NEW TABLE ENTRY.  CALCDUE WILL USE THE TABLE ENTRY TO CALCULATE               
* THE AMOUNT.                                                                   
*                                                                               
DUECOMP  NTR1                                                                   
         TM    SRTBITS,SRTBNDU     IF THIS PERF TAKES NO DUE COMP               
         BO    DCX                 THEN DON'T BOTHER                            
*                                                                               
         MVI   DUESTAT,0           CLEAR STATUS                                 
*                                                                               
         LA    R3,KEY              BUILD KEY FOR DUE COMPANY                    
         USING TLDUD,R3                                                         
         XC    TLDUKEY,TLDUKEY                                                  
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
*                                                                               
         GOTO1 HIGH                READ FOR KEY                                 
*                                                                               
DC10     CLC   TLDUKEY(TLDUDUC-TLDUD),KEYSAVE  WHILE SAME EMPLOYEE              
         BNE   DCX                                                              
*                                                                               
         MVC   SVDUEKEY,TLDUKEY    SAVE DUE COMPANY KEY                         
         L     RE,SRTNET                                                        
         L     R1,SRTMDED                                                       
         A     R1,SRTDUES                                                       
         CR    RE,R1               AND CHECK AMOUNT > MISC DED + DUES           
         BNH   DCX                                                              
*                                                                               
         GOTO1 GETDUE,DMCB,TLDUDUC GET DUE COMPANY TABLE ENTRY                  
         BNE   DC90                SKIP IF CAN'T FIND OR BUILD                  
         L     R5,0(R1)            R5 = A(DUE COMPANY TABLE ENTRY)              
         USING DUETABD,R5          RETURNED FROM GETDUE CALL                    
*                                                                               
         GOTO1 =A(CALCDUE),DMCB,(R5) CALCULATE DUE COMPANY AMOUNT               
         TM    DUESTAT,DUESERR     IF ERROR RETURNED                            
         BZ    *+16                                                             
         BAS   RE,WAREDUE2         SET WARNING IN SORT RECORD                   
         NI    DUESTAT,X'FF'-DUESERR  CAN'T FIND FLIST                          
         B     DC90                                                             
*                                                                               
         OC    DUECOL,DUECOL       IF NONE FROM THIS RECORD THEN TRY            
         BZ    DC90                    NEXT RECORD                              
***********************************************************************         
* APPLY DUE COMPANY COLLECTED THIS TIME IN THE FOLLOWING WAYS:        *         
*     1) ADD TO TOTAL COLLECTED AMOUNT IN TABLE ENTRY                 *         
*     2) COMPUTE REMAINING BALANCE                                    *         
*     3) SUBTRACT FROM CHECK AMOUNT IN SORT RECORD                    *         
*     4) ADD TO TOTAL DUE COMPANY COLLECTED IN SORT RECORD            *         
*     5) ADD DUE COMPANY WITHHOLDING ELEMENT TO CHECK RECORD          *         
*        AND PUT THE AMOUNT THERE                                     *         
***********************************************************************         
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         ICM   RF,15,TADUCOL       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,DUECOL               IN THE DUE COMPANY DETAILS               
         STCM  RF,15,TADUCOL           ELEMENT                                  
*                                                                               
         ICM   RF,15,DUEINV        ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,DUECOL               FOR THIS INVOICE                         
         STCM  RF,15,DUEINV                                                     
*                                                                               
         ICM   RF,15,TADUDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TADUCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
*                                                                               
         ST    RF,DUEBAL           SAVE BALANCE REMAINING IN DUEBAL             
*                                                                               
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,DUECOL               AMOUNT IN SORT RECORD                    
         ST    RF,SRTNET                                                        
*                                                                               
         L     RF,SRTDUE           ADD AMOUNT TO THE DUE COMPANY                
         A     RF,DUECOL               AMOUNT IN SORT RECORD                    
         ST    RF,SRTDUE                                                        
*                                                                               
         LA    R4,ELEM             BUILD DUECOMP WITHHOLDING ELEMENT            
         USING TADWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TADWEL,TADWELQ                                                   
         MVI   TADWLEN,TADWLNQ                                                  
         MVC   TADWDUC,TLDUDUC                                                  
*                                                                               
         MVC   TADWREC,DUECOL      SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   TADWBAL,DUEBAL      SAVE REMAINING BALANCE IN ELEMENT            
*                                                                               
         MVC   AIO,ASRTCHK         ADD DW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
DC90     TM    DUESTAT,DUESPCT     IF A PERCENTAGE WAS TAKEN                    
         BO    DCX                 THEN DONE                                    
         GOTO1 SEQ                 GET NEXT DUECOMP KEY AND LOOP BACK           
         B     DC10                                                             
*                                                                               
DCX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE DUE COMPANY TABLE FOR AN ENTRY THAT HAS THE         
* DUE COMPANY CODE POINTED TO BY PARAMETER 1.  IF IT DOESN'T FIND               
* ONE IT READS THE DUE COMPANY RECORD AND BUILDS A NEW ONE FROM THE             
* DETAILS ELEMENT.  IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN                
* PARAMETER 1.  IF THE DUE COMPANY RECORD FOR SOME REASON HAS NO DUE            
* COMPANY DETAILS ELEMENT THEN THE ROUTINE RETURNS 'NO', OTHERWISE IT           
* RETURNS 'YES'.                                                                
*                                                                               
GETDUE   NTR1                                                                   
         L     R3,0(R1)            R3 = A(DUE COMPANY CODE)                     
*                                                                               
         LA    R5,BLOCK            R5 = A(DUETAB LOOKUP ENTRY)                  
         USING DUETABD,R5                                                       
*                                  BUILD LOOKUP ENTRY                           
         XC    BLOCK(DUELEN),BLOCK                                              
         MVC   DUESSN,SRTSSN                                                    
         MVC   DUEDUC,0(R3)                                                     
         MVC   DUEEMP,SRTEMP                                                    
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ADUETAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         BNE   YES                                                              
         GOTO1 GETREC              ELSE READ RECORD AND BUILD NEW ENTRY         
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GD100                                                            
         USING TADUD,R4                                                         
*                                                                               
         TM    TADUSTAT,TADUSHLD   IGNORE RECORDS STILL ON HOLD                 
         BO    NO                                                               
         TM    TADUSTAT,TADUSLCK   IGNORE RECORDS LOCKED                        
         BO    NO                                                               
         CLC   TADUEMP,SRTEMP      MUST BE SAME EMPLOYER                        
         BNE   NO                                                               
         L     RF,TADUDUE          AMOUNT TO COLLECT = AMOUNT DUE -             
         S     RF,TADUCOL              AMOUNT ALREADY COLLECTED                 
         BNP   NO                  SKIP IF NOTHING LEFT TO TAKE                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'D',AIO),0,=C'DUE COMP'                           
*                                                                               
         LA    R5,BLOCK            BUILD NEW ENTRY FROM DETAILS ELEMENT         
         MVC   DUEELEM,TADUEL                                                   
*                                  ADD ENTRY TO TABLE                           
         GOTO1 ADDBIN,DMCB,ADUETAB,(R5)                                         
         B     YES                 AND RETURN SUCCESS                           
*                                                                               
GD100    BAS   RE,WAREDUE          SET WARNING IN SORT RECORD                   
         B     NO                  AND RETURN FAILURE                           
         EJECT                                                                  
* THIS ROUTINE REMOVES MISCELLANEOUS DEDUCTIONS FROM THE CHECK.                 
*                                                                               
MISCDED  NTR1                                                                   
         L     RE,SRTMDED          IF MISC DED + UNION DUES > CHECK AMT         
         A     RE,SRTDUES                                                       
         L     R1,SRTNET                                                        
         CR    RE,R1                                                            
         BNH   MISCD40                                                          
*                                                                               
         L     RE,SRTMDED          THEN CHECK MISC DED > CHECK AMOUNT           
         CR    RE,R1                                                            
         BE    MISCD20               IF   EQ, CLEAR UNION DUES                  
         BH    MISCD10               IF   >,  MDED=CHECK & CLEAR DUES           
         SR    R1,RE                 ELSE <,  REMAINDER TO DUES                 
         ST    R1,SRTDUES                                                       
         B     MISCD40                                                          
*                                                                               
MISCD10  MVC   SRTMDED,SRTNET      MISC DED = CHECK AMOUNT                      
MISCD20  XC    SRTDUES,SRTDUES     CLEAR UNION DUES                             
*                                                                               
MISCD40  L     RE,SRTMDED          RE=(NEW MISC AMT + UNION DUES)               
         A     RE,SRTDUES                                                       
*                                                                               
         L     RF,SRTNET           SUBTRACT FROM CHECK AMOUNT                   
         SR    RF,RE                                                            
         ST    RF,SRTNET                                                        
*                                                                               
         A     RE,SRTTDED          ADD TO TOTAL DED AMOUNT                      
         ST    RE,SRTTDED                                                       
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE LIEN AMOUNT FOR THE CHECK.  IT MAKES CALLS          
* TO GETLIEN AND CALCLIEN FOR EACH LIEN KEY UNTIL A USABLE LIEN AMOUNT          
* IS FOUND.                                                                     
*                                                                               
LIENS    NTR1                                                                   
         OC    SRTNET,SRTNET       IF CHECK AMOUNT ZERO THEN DONE               
         BZ    LNX                                                              
*                                                                               
         LA    R3,KEY              BUILD KEY FOR LIEN                           
         USING TLLNPD,R3                                                        
         XC    TLLNPKEY,TLLNPKEY                                                
         MVI   TLLNPCD,TLLNRCDQ                                                 
         MVC   TLLNRSSN,SRTSSN                                                  
*                                                                               
         GOTO1 HIGH                READ FOR KEY                                 
*                                                                               
*                                  WHILE SAME EMPLOYEE                          
LN10     CLC   TLLNPKEY(TLLNRRNK-TLLNPD),KEYSAVE                                
         BNE   LNX                                                              
*                                  GET LIEN TABLE ENTRY                         
         GOTO1 GETLIEN,DMCB,TLLNRLIN    ADDRESS RETURNED IN PARM 1              
*                                                                               
         BNE   LN40                SKIP IF CAN'T FIND OR BUILD                  
*                                                                               
         GOTO1 CALCLIEN,DMCB,,     CALCULATE LIEN AMOUNT PASSING PARM 1         
*                                      RETURNED FROM GETLIEN CALL               
*                                                                               
         OC    LIENCOL,LIENCOL     IF LIEN AMOUNT FOUND FOR THIS RECORD         
         BNZ   LN50                    THEN APPLY                               
*                                                                               
LN40     GOTO1 SEQ                 ELSE GET NEXT LIEN KEY AND LOOP BACK         
         B     LN10                                                             
***********************************************************************         
* APPLY LIEN COLLECTED THIS TIME IN THE FOLLOWING WAYS:               *         
*     1) ADD TO TOTAL COLLECTED AMOUNT IN TABLE ENTRY                 *         
*     2) COMPUTE REMAINING BALANCE                                    *         
*     3) SAVE SSN FOR LIEN PAYEE CHECK                                *         
*     4) SUBTRACT FROM CHECK AMOUNT IN SORT RECORD                    *         
*     5) ADD LIEN WITHHOLDING ELEMENT TO CHECK RECORD AND PUT         *         
*        AMOUNT THERE                                                 *         
***********************************************************************         
LN50     DS    0H                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         ICM   RF,15,TALNCOL       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,LIENCOL              IN THE LIEN DETAILS ELEMENT              
         STCM  RF,15,TALNCOL                                                    
*                                                                               
         ICM   RF,15,LININV        ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,LIENCOL              FOR THIS INVOICE                         
         STCM  RF,15,LININV                                                     
*                                                                               
         MVC   LIENSSN,TALNPAYE    SAVE SSN FOR LIEN PAYEE'S CHECK              
*                                                                               
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,LIENCOL              AMOUNT IN SORT RECORD                    
         ST    RF,SRTNET                                                        
*                                                                               
         MVC   SRTLIEN,LIENCOL     SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         LA    R4,ELEM             BUILD LIEN WITHHOLDING ELEMENT               
         USING TALWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TALWEL,TALWELQ                                                   
         MVI   TALWLEN,TALWLNQ                                                  
         MVC   TALWLIN,TLLNRLIN                                                 
*                                                                               
         MVC   TALWREC,LIENCOL     SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   TALWBAL,LIENBAL     SAVE REMAINING BALANCE IN ELEMENT            
*                                                                               
         MVC   AIO,ASRTCHK         ADD LW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         LA    R4,ELEM             BUILD ELEMENT LIEN PAID TO                   
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTLIN   SET TYPE                                     
         MVC   TANUMBER(9),LIENSSN SET S/S NUMBER FOR WHO LIEN PAID TO          
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
LNX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE LIEN TABLE FOR AN ENTRY WITH THE LIEN               
* CODE POINTED TO BY PARAMETER 1.  IF IT DOESN'T FIND ONE THEN                  
* IT READS THE LIEN RECORD AND BUILDS A NEW ONE FROM THE DETAILS                
* ELEMENT.  IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN PARAMETER 1.           
* IF THE LIEN RECORD FOR SOME REASON HAS NO LIEN DETAILS ELEMENT THEN           
* THE ROUTINE RETURNS 'NO', OTHERWISE IT RETURNS 'YES'.                         
*                                                                               
GETLIEN  NTR1                                                                   
         L     R3,0(R1)            R3 = A(LIEN CODE)                            
*                                                                               
         LA    R5,BLOCK            R5 = A(LINTAB LOOKUP ENTRY)                  
         USING LINTABD,R5                                                       
*                                  BUILD LOOKUP ENTRY                           
         XC    BLOCK(LINLEN),BLOCK                                              
         MVC   LINSSN,SRTSSN                                                    
         MVC   LINLIN,0(R3)                                                     
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ALINTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         BNE   YES                                                              
*                                                                               
         GOTO1 GETREC              ELSE READ RECORD AND BUILD NEW ENTRY         
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GL100                                                            
         USING TALND,R4                                                         
*                                                                               
         GOTO1 NOCALC              IF WE DON'T WANT TO CALCULATE THIS           
         BE    GL50                    CHECK THEN SKIP BALANCE TEST             
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BE    GL50                    SKIP BALANCE TEST                        
         ICM   RF,15,TALNDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TALNCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         BNP   NO                  SKIP IF NOTHING LEFT TO TAKE                 
*                                                                               
GL50     GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'LIEN'                               
*                                                                               
         LA    R5,BLOCK            BUILD NEW ENTRY FROM DETAILS ELEMENT         
         MVC   LINELEM,TALNEL                                                   
*                                  ADD ENTRY TO TABLE                           
         GOTO1 ADDBIN,DMCB,ALINTAB,(R5)                                         
         B     YES                 AND RETURN SUCCESS                           
*                                                                               
GL100    BAS   RE,WARELNE          SET WARNING IN SORT RECORD                   
         B     NO                  AND RETURN FAILURE                           
         DROP  R5                                                               
         EJECT                                                                  
* THIS ROUTINE USES THE TABLE ENTRY, POINTED TO BY PARAMETER ONE, TO            
* CALCULATE THE LIEN AMOUNT TO COLLECT.  THE COLLECTED AMOUNT IS                
* STORED IN LIENCOL.  IF THE TABLE ENTRY IS FILTERED FOR SOME REASON,           
* LIENCOL WILL BE ZERO.                                                         
*                                                                               
CALCLIEN NTR1                                                                   
         XC    LIENCOL,LIENCOL     INITIALIZE COLLECTED AMOUNT TO ZERO          
*                                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         CLI   RECNUM,CN           ONLY PROCESS CAN$ LIENS ON CNCHECKS          
         BNE   *+16                 AND OTHERS ON US$ RUNS                      
         TM    TALNSTAT,TALNSCAN                                                
         BZ    CLX                                                              
         B     *+12                                                             
         TM    TALNSTAT,TALNSCAN                                                
         BO    CLX                                                              
*                                                                               
         OC    TALNEMP,TALNEMP     SKIP RECS WHERE EMPLOYER DON'T MATCH         
         BZ    *+18                                                             
         CLC   TALNEMP,SRTEMP                                                   
         BNE   CLX                                                              
         B     *+14                                                             
         CLC   TGTPEMP,SRTEMP                                                   
         BNE   CLX                                                              
*                                                                               
         CLC   SRTCDTE,TALNEXP     SKIP RECORDS WHERE THE EXPIRATION            
         BNL   CLX                     DATE IS NOT AFTER TODAY                  
*                                                                               
         CLC   SRTNET,TALNXMPT     SKIP RECORDS WHERE THE CHECK AMOUNT          
         BNH   CLX                     IS NOT > THE EXEMPT AMOUNT               
*                                                                               
         OC    SRTTRST,SRTTRST     SKIP RECORDS WHERE THERE WAS ALREADY         
         BNZ   CLX                     AN AMT W/HELD FOR W4 TRUSTEE             
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BE    CL10                AMOUNT COLLECTING UNLIMITED                  
         ICM   RF,15,TALNDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TALNCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         LTR   RF,RF               SKIP RECS WITH NO REMAINING BALANCE          
         BZ    CLX                                                              
         ST    RF,LIENBAL          SAVE BALANCE REMAINING IN LIENBAL            
*                                                                               
CL10     L     RF,SRTNET           GREATEST POSSIBLE AMOUNT TO COLLECT          
         ICM   RE,15,TALNXMPT          = CHECK AMOUNT - EXEMPT AMOUNT           
         SR    RF,RE                                                            
         ST    RF,FULL                                                          
*                                                                               
         ICM   RF,15,TALNAMT       IF DEDUCTION AMOUNT NOT ZERO THEN            
         BNZ   CL30                    USE IT                                   
*                                                                               
         ICM   R0,3,TALNPCT        ELSE IF DEDUCTION PERCENTAGE NOT             
         BZ    CL20                    ZERO THEN USE IT                         
         L     RF,SRTNET                                                        
         CLC   TALNTYPE,=AL2(TALNTYGT) IF WAGE GARNISHMENT FROM GROSS           
         BE    *+14                                                             
         CLC   TALNTYPE,=AL2(TALNTYGG)                                          
         BNE   *+8                                                              
         L     RF,SRTGRS           THEN USE GROSS AMOUNT                        
         MR    RE,R0                                                            
         D     RE,=F'5000'         PERCENTAGE                                   
         LTR   RF,RF                                                            
         BM    *+8                 & ROUND IT                                   
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         B     CL30                                                             
*                                                                               
CL20     L     RF,FULL             ELSE USE GREATEST POSSIBLE AMOUNT            
*                                                                               
CL30     C     RF,FULL             IF AMOUNT TO COLLECT > GREATEST              
         BNH   *+8                                                              
         L     RF,FULL             THEN AMOUNT TO COLLECT = GREATEST            
*                                                                               
         CLM   RF,15,SRTNET        IF AMOUNT TO COLLECT > CHECK AMOUNT          
         BNH   *+8                                                              
         L     RF,SRTNET           THEN AMOUNT TO COLLECT = CHECK AMNT          
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BNE   CL40                                                             
         ST    RF,LIENCOL          SAVE AMOUNT TO COLLECT IN LIENCOL            
         XC    LIENBAL,LIENBAL     AND DON'T KEEP TRACK OF BALANCE              
         B     CLX                 EXIT                                         
*                                                                               
CL40     CLM   RF,15,LIENBAL       IF AMOUNT TO COLLECT > REM BAL               
         BNH   *+8                                                              
         L     RF,LIENBAL          THEN AMOUNT TO COLLECT = REM BAL             
*                                                                               
         ST    RF,LIENCOL          SAVE AMOUNT TO COLLECT IN LIENCOL            
*                                                                               
         L     RF,LIENBAL          SUBTRACT COLLECTED AMOUNT FROM               
         S     RF,LIENCOL              REMAINING BALANCE                        
         ST    RF,LIENBAL                                                       
*                                                                               
CLX      B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE W4 TRUSTEE AMT FOR THE CHECK.  SAVES IT             
* IN TRUSTEE TABLE, COMPUTES REMAINING BALANCE, SAVES SSN FOR TRUSTEE           
* CHECK, SUBTRACTS FROM CHK AMOUNT IN SORT RECORD, ADDS OTHER DEDUCTION         
* ELEMENT AND SAVES TRUSTEE SSN TO CHECK RECORD                                 
*                                                                               
TRST     NTR1                                                                   
         OC    SRTNET,SRTNET       IF CHECK AMOUNT ZERO THEN DONE               
         BZ    TRSTX                                                            
*                                                                               
         OC    SRTW4DOB,SRTW4DOB   IF DATE OF BIRTH SPECIFIED                   
         BZ    TRST2                                                            
         GOTO1 DATCON,DMCB,(1,SRTW4DOB),(0,WORK)                                
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+8,18                                 
         CLC   TGTODAY0,WORK+8     AND PERFORMER STILL A MINOR                  
         BNL   TRSTX                                                            
         B     *+14                                                             
TRST2    OC    SRTW4SSN,SRTW4SSN   OR IF TRUSTEE SPECIFIED                      
         BZ    TRSTX                                                            
*                                                                               
         CLC   SRTSOW,=C'CA '      IF LIVE OR WORK IN CALIFORNIA                
         BE    TRST3                                                            
         CLC   SRTW4SOR,=C'CA '    (STATE OF RESIDENCE IF INDIVIDUAL            
         BE    TRST3               PAID AS CORP)                                
         CLC   SRTSOR,=C'CA '                                                   
         BNE   TRSTX                                                            
*                                                                               
TRST3    TM    TGUSSTAT,SESSION    IF IT IS A RESIDUAL PAYMENT                  
         BO    TRST5                                                            
         MVC   WORK(3),SRTFRST     WORK DATE MUST BE 1/1/00 OR LATER            
         OC    SRTFRST,SRTFRST                                                  
         BNZ   *+10                                                             
         MVC   WORK(3),SRTFCYC                                                  
         CLC   WORK(3),=X'A00101'                                               
         BL    TRSTX                                                            
*                                                                               
TRST5    L     RF,SRTGRS           USE GROSS                                    
         ICM   R0,15,SRTW4PCT      ELSE IF DEDUCTION PERCENTAGE NOT             
         BNZ   *+8                                                              
         LH    R0,=AL2(1500)       DEFAULT TO 15%                               
         MR    RE,R0                                                            
         D     RE,=F'5000'         PERCENTAGE                                   
         LTR   RF,RF                                                            
         BM    *+8                 & ROUND IT                                   
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         CLM   RF,15,SRTNET        IF AMOUNT TO COLLECT > CHECK AMOUNT          
         BNH   *+8                                                              
         L     RF,SRTNET           THEN AMOUNT TO COLLECT = CHECK AMNT          
         ST    RF,TRUSTCOL         SAVE AMOUNT TO COLLECT IN TRUSTCOL           
         MVC   TRUSTSSN,SRTW4SSN   SAVE SSN FOR GENERATED TRUSTEE CHK           
*                                                                               
         BAS   RE,GETTRST          GET TABLE ENTRY                              
         L     R5,0(R1)            R5 = A(TABLE ENTRY)                          
         USING TRSTABD,R5                                                       
*                                                                               
         ICM   RF,15,TRSTCOL       ADD AMT TO COLLECTED AMOUNT                  
         A     RF,TRUSTCOL                                                      
         STCM  RF,15,TRSTCOL                                                    
*                                                                               
         ICM   RF,15,TRSTINV       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,TRUSTCOL         FOR THIS INVOICE                             
         STCM  RF,15,TRSTINV                                                    
         MVC   TRSTSSN,TRUSTSSN    SAVE SSN FOR TRUSTEE GENERATED CHK           
*                                                                               
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,TRUSTCOL         AMOUNT IN SORT RECORD                        
         ST    RF,SRTNET                                                        
         MVC   SRTTRST,TRUSTCOL    SAVE COLLECTED AMOUNT IN SORT RECORD         
*                                                                               
         LA    R4,ELEM             BUILD W4 TRUSTEE OTHER DEDUCT ELE            
         USING TAODD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPT   SET W4 TRUSTEE DEDUCTION                     
         MVC   TAODAMT,TRUSTCOL    SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         OC    TRSTSSN,TRSTSSN     IF NO TRUSTEE SSN                            
         BZ    TRSTX                                                            
         LA    R4,ELEM             BUILD ELEMENT LIEN PAID TO                   
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTRST   SET TYPE                                     
         MVC   TANUMBER(9),TRSTSSN SET S/S NUMBER OF TRUSTEE                    
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
TRSTX    B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE W4 TRUSTEE TABLE FOR AN ENTRY                       
* IF IT DOESN'T FIND ONE THEN IT BUILDS A NEW ONE                               
* IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN PARAMETER 1.                     
*                                                                               
GETTRST  NTR1                                                                   
         LA    R5,BLOCK            BUILD LOOKUP ENTRY                           
         USING TRSTABD,R5                                                       
         XC    BLOCK(TRSTLEN),BLOCK BUILD LOOKUP ENTRY                          
         MVC   TRSTSSN,SRTSSN                                                   
         MVC   TRSTEMP,SRTEMP                                                   
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ATRSTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         BNE   XIT                                                              
*                                                                               
         LA    R5,BLOCK            ADD ENTRY TO TABLE                           
         GOTO1 ADDBIN,DMCB,ATRSTAB,(R5)                                         
         B     XIT                 AND RETURN SUCCESS                           
         DROP  R5                                                               
         EJECT                                                                  
* THIS ROUTINE TAKES THE FINAL NET AMOUNT AND SETS IT TO DIRECT                 
* DEPOSIT.  IT ADDS THE NEW DIRECT DEPOSIT AMOUNT TO THE TOTAL                  
* DEDUCTIONS AND SUBTRACTS IT FROM NET AMOUNT (CAUSING NET TO BE ZERO)          
*                                                                               
DIRECTD  NTR1                                                                   
         XC    SRTDIRCT,SRTDIRCT   PRE CLEAR DIRECT AMOUNT                      
*                                                                               
         CLC   SRTEMP,=C'PG '      FOR EMPLOYER PG                              
         BNE   DIRECTDX                                                         
         TM    SRTBITS,SRTBDD      IF PERF'S CHKS ARE DIRECT DEPOSIT            
         BZ    DIRECTDX                                                         
         OC    SRTNET,SRTNET       AND IF CHECK AMOUNT                          
         BZ    DIRECTDX                                                         
*                                                                               
         MVC   SRTDIRCT,SRTNET     SET CHECK AMOUNT TO DIRECT                   
*                                                                               
         XC    ELEM,ELEM           BUILD DIR DEP OTHER WITHHOLDING ELE          
         LA    R4,ELEM                                                          
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPD                                                
         MVC   TAODAMT,SRTDIRCT                                                 
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     RF,SRTTDED          ADD DIRECT TO TOTAL DEDUCTIONS               
         A     RF,SRTDIRCT                                                      
         ST    RF,SRTTDED                                                       
*                                                                               
         L     RF,SRTNET           SUBTRACT DIRECT FROM CHECK AMOUNT            
         S     RF,SRTDIRCT                                                      
         ST    RF,SRTNET                                                        
*                                                                               
DIRECTDX B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE TAKES THE FINAL NET AMOUNT AND SETS IT TO WIRE                   
* TRANSFER.  IT ADDS THE NEW WIRE TRANSFER AMOUNT TO THE TOTAL                  
* DEDUCTIONS AND SUBTRACTS IT FROM NET AMOUNT (CAUSING NET TO BE ZERO)          
*                                                                               
WIRED    NTR1                                                                   
         XC    SRTWIRE,SRTWIRE     PRE CLEAR WIRE AMOUNT                        
*                                                                               
         TM    SRTBITS,SRTBWIRE    IF PERF'S CHKS ARE WIRED                     
         BZ    WIREDX                                                           
         OC    SRTNET,SRTNET       AND IF CHECK AMOUNT                          
         BZ    WIREDX                                                           
*                                                                               
         MVC   SRTWIRE,SRTNET      SET CHECK AMOUNT TO WIRE AMOUNT              
*                                                                               
         XC    ELEM,ELEM           BUILD OTHER WITHHOLDING EL 'W'               
         LA    R4,ELEM                                                          
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPW                                                
         MVC   TAODAMT,SRTWIRE                                                  
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     RF,SRTTDED          ADD IT TO TOTAL DEDUCTIONS                   
         A     RF,SRTWIRE                                                       
         ST    RF,SRTTDED                                                       
*                                                                               
         L     RF,SRTNET           SUBTRACT IT FROM CHECK AMOUNT                
         S     RF,SRTWIRE                                                       
         ST    RF,SRTNET                                                        
*                                                                               
WIREDX   B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE ADDS ESUTAB ENTRIES FOR THE CHECK RECORD.                        
*                                                                               
CHKESU   NTR1                                                                   
         XC    TGFULL,TGFULL       CLEAR TEMP. AREA                             
*                                                                               
         MVI   ELCODE,TAODELQ      LOOK FOR FEDERAL OTHER DEDUCT ELEM           
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPF))                                     
         MVC   AIO,AIO1                                                         
         BNE   CE20                SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(FEDERAL OTHER DEDUCTION ELEM)         
         USING TAODD,R4                                                         
         MVC   TGFULL,TAODAMT      SAVE FOREIGN FEDERAL TAXES                   
*                                                                               
CE20     MVI   ELCODE,TACWELQ      LOOK FOR FEDERAL WITHHOLDING ELEM            
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,=C'FD '                                                
         MVC   AIO,AIO1                                                         
         BE    CE26                SKIP IF FOUND                                
*                                                                               
         LA    R5,BLOCK            ELSE BUILD NEW ENTRY FOR FEDERAL             
         USING ESUTABD,R5                                                       
         XC    0(ESULEN,R5),0(R5)  (THIS MUST BE CORP.)                         
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,=C'FD '                                                  
         MVC   ESUREXP,SRTREXP     ACCUMULATE REIMB. EXPENSES                   
         MVC   ESUEARN,SRTEARN                TAXABLE EARNINGS                  
         MVC   ESUTAX,TGFULL                  FOREIGN FEDERAL TAX               
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5)  ADD ESUTAB ENTRY                       
         GOTO1 ADDBIN,DMCB,AESUINV,(R5)  ADD ESUINV ENTRY                       
*                                                                               
CE26     L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ      GET CHECK WITHHOLDING ELS.                   
         BAS   RE,GETEL                                                         
         BNE   CEX                                                              
         USING TACWD,R4            R4 = A(CHECK WITHHOLDING ELEMENT)            
*                                                                               
CE30     LA    R5,BLOCK            BUILD NEW ENTRY FOR UNIT                     
         XC    BLOCK(ESULEN),BLOCK                                              
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,TACWUNIT                                                 
*                                                                               
         TM    TACWSTAT,TACWSTAX   IF THIS IS TAXABLE UNIT                      
         BZ    *+10                                                             
         MVC   ESUEARN,SRTEARN     ACCUMULATE EARNINGS                          
*                                                                               
         MVC   ESUTAX,TACWTAX      ACCUMULATE TAXES                             
*                                                                               
         CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   CE60                                                             
         ICM   RF,15,ESUTAX                                                     
         A     RF,TGFULL           ADD FOREIGN FEDERAL TAX                      
         STCM  RF,15,ESUTAX                                                     
         MVC   ESUREXP,SRTREXP     ACCUMULATE REIMB. EXPENSES                   
         MVC   ESUFICA,TACWFICA               FICA                              
         B     CE90                                                             
*                                                                               
CE60     CLC   TACWUNIT,=C'CN '    IF THIS IS CANADA                            
         BNE   *+14                                                             
         MVC   ESUGST,TACWGST      SAVE GOODS AND SERVICES TAX                  
         B     CE90                                                             
*                                                                               
         CLI   TACWUNIT+2,C' '     IF THIS IS A STATE                           
         BH    CE90                                                             
         MVC   ESUSDI,TACWSDI      SAVE DISABILITY                              
         MVC   ESUSUI,TACWSUI           UNEMPLOYMENT                            
*                                                                               
*                                  UPDATE ESUTAB ENTRY FOR THIS UNIT            
CE90     GOTO1 ADDBIN,DMCB,AESUTAB,(R5)                                         
*                                  UPDATE ESUINV ENTRY FOR THIS UNIT            
         GOTO1 ADDBIN,DMCB,AESUINV,(R5)                                         
*                                                                               
         BAS   RE,NEXTEL                                                        
         BE    CE30                GO PROCESS CHECK WITHHOLDING EL.             
*                                                                               
CEX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
* THIS ROUTINE UPDATES THE CHECK RECORD WITH THE YTD AMOUNTS FOR THIS           
* CHECK.  IT GETS THE YTD AMOUNTS FROM ESUTAB AND SAVES THEM IN YTD             
* ELEMENTS, WHICH IT ADDS, AND THE CHECK DETAILS ELEMENT.                       
* ADDITIONALLY, IT SAVES THE YTD AMOUNTS IN THE SORT RECORD FOR                 
* PRINTING.                                                                     
*                                                                               
UPDYTD   NTR1                                                                   
         MVC   AIO,ASRTCHK         REMOVE EXISTING YTD WITHHOLDING ELS          
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 REMELEM                                                          
         MVC   AIO,AIO1                                                         
         XR    R2,R2               CLEAR PROCESSED FD ESUTAB ENTRY FLAG         
*                                                                               
         LA    R5,ELEM             R5 = A(SKELETON YTD ELEMENT)                 
         USING TACYD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACYEL,TACYELQ                                                   
         MVI   TACYLEN,TACYLNQ                                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(FIRST WITHHOLDING ELEMENT)            
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         BE    UY10                                                             
         LA    R4,DMYCWEL          IF NONE, POINT TO DUMMY AND PROCESS          
         USING TACWD,R4                                                         
*                                  R3 = A(ESUTAB ENTRY FOR THIS UNIT)           
UY10     GOTO1 GETESU,DMCB,TACWUNIT                                             
         L     R3,0(R1)                                                         
         USING ESUTABD,R3                                                       
*                                                                               
         MVC   TACYUNIT,TACWUNIT   SAVE UNIT IN YTD ELEMENT                     
         MVC   TACYTAX,ESUTAX           TAX                                     
         MVC   TACYFICA,ESUFICA         FICA (OR SDI)                           
         MVC   TACYEARN,ESUEARN         EARNINGS                                
*                                                                               
UY16     CLC   TACYUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   UY20                                                             
         MVC   SRTYFED,TACYTAX     THEN SAVE YTD FEDERAL TAX IN SORTREC         
         MVC   SRTYFIC,TACYFICA              YTD FICA                           
         MVC   SRTYREX,ESUREXP               YTD REIMBURSED EXP.                
         LA    R2,1                SET PROCESSED FD ESUTAB ENTRY                
         B     UY70                                                             
*                                                                               
UY20     CLI   TACYUNIT+2,C' '     ELSE IF UNIT IS A CITY                       
         BNH   UY30                                                             
         TM    TACWSTAT,TACWSTAX   AND IT'S MARKED TAXABLE                      
         BZ    *+16                                                             
         L     RF,TACYTAX          THEN ADD LOCAL TAX TO YTD LOCAL TAX          
         A     RF,SRTYLOC              IN SORT RECORD                           
         ST    RF,SRTYLOC                                                       
         B     UY70                                                             
*                                                                               
UY30     CLC   TACYUNIT,=C'CN '    ELSE IF UNIT IS CANADA                       
         BNE   UY40                                                             
         MVC   SRTYCAN,TACYTAX     THEN SAVE YTD CAN TAX IN SORTREC             
         MVC   SRTYGST,ESUGST                YTD GST                            
         MVC   TACYGST,ESUGST      SET YTD GST IN YTD ELEMENT                   
         B     UY70                                                             
*                                                                               
UY40     TM    TACWSTAT,TACWSTAX   ELSE IF THIS IS TAXABLE STATE                
         BZ    *+16                                                             
         L     RF,TACYTAX          THEN ADD STATE TAX TO YTD STATE TAX          
         A     RF,SRTYSTA              IN SORT RECORD                           
         ST    RF,SRTYSTA                                                       
         MVC   TACYSUI,ESUSUI      SET YTD SUI IN YTD ELEMENT                   
*                                                                               
UY70     C     R4,=A(DMYCWEL)      IF POINTING TO DUMMY ELEMENT                 
         BNE   *+14                                                             
         OC    TACYAMTS,TACYAMTS   AND NO AMOUNTS IN YTD EL.                    
         BZ    UY90                THEN DON'T BOTHER ADDING IT                  
*                                                                               
         MVC   AIO,ASRTCHK         ELSE ADD YTD ELEMENT TO CHECK RECORD         
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
UY90     MVI   ELCODE,TACWELQ      BUMP TO NEXT WITHHOLDING ELEMENT             
         BAS   RE,NEXTEL                                                        
         BE    UY10                REPEAT UNTIL NO MORE WITHHOLD ELEMS          
*                                                                               
         LTR   R2,R2               IF DIDN'T PROCESS FD ESUTAB ENTRY            
         BNZ   UY100               (OCCURS FOR CORPS WITH CAN TAX WHLD)         
         GOTO1 GETESU,DMCB,=C'FD ' GET IT NOW                                   
         L     R3,0(R1)                                                         
         MVC   SRTYREX,ESUREXP     AND EXTRACT YTD REIMBURSED EXP.              
*                                                                               
UY100    L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDYREX,SRTYREX    SAVE YTD REIMBURSED EXP IN CD ELEM           
         B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS UP THE FIRST TLCKY POINTER ON THE FILE FOR CHECKS          
* WHICH ARE GOING TO BE BACK DATED TO 12/31 OF LAST YEAR.  THIS NUMBER          
* IS ADDED TO THE CURRENT SEQUENCE NUMBER TO GUARANTEE THAT THESE               
* POINTERS SORT IN THE CORRECT ORDER.                                           
*                                                                               
         USING TACDD,R4            R4 = A(CHECK DETAILS ELEMENT)                
GETSEQ   NTR1                                                                   
         LA    R3,KEY              BUILD KEY FOR TLCKY POINTER                  
         USING TLCKPD,R3                                                        
         XC    TLCKPKEY,TLCKPKEY                                                
         MVI   TLCKPCD,TLCKYCDQ    RECORD CODE                                  
         MVC   TLCKYEAR,CHKDTEAL   LAST YEAR                                    
         MVI   TLCKYCUR,C'U'       IF CURRENCY IS US THEN TLCKYCUR = U          
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TLCKYCUR,C'C'       ELSE TLCKYCUR = C                            
         MVC   TLCKYEMP,SRTEMP     EMPLOYER                                     
         MVC   TLCKYSSN,SRTSSN     S/S NUMBER                                   
         MVC   TLCKYTXU,=C'FD '    TAX UNIT                                     
         MVC   TLCKYDTE,CHKDATEL   CHECK DATE                                   
         XC    TLCKYDTE,XFFS       (COMPLEMENTED)                               
*                                                                               
         MVC   FILENAME,CHKDIR     SET TO READ FROM CHKDIR                      
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
*                                                                               
         CLC   TLCKPKEY(TLCKYSEQ-TLCKPD),KEYSAVE  IF FOUND A GOOD KEY           
         BNE   GSEQX                                                            
         ICM   R1,15,TLCKYSEQ      ITS SEQUENCE NUMBER                          
         LCR   R1,R1               (IT'S COMPLEMENTED IN KEY)                   
         AH    R1,TACDSEQ+2        + CURRENT SEQUENCE NUMBER                    
         ST    R1,TACDSEQ2         = SPECIAL SEQUENCE NUMBER                    
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',TLCKPKEY),38,=C'TLCKY KEY'                    
*                                                                               
GSEQX    B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE TRACES THE PERFORMER'S ESUTAB ENTRIES FOR EACH UNIT              
* THE PERFORMER USES FOR THE EMPLOYER.  IT NOW ALSO TRACES PYTDTAB              
* ENTRY FOR PERFORMER/EMPLOYER.                                                 
*                                                                               
TRACEESU NTR1                                                                   
         GOTO1 GETPYTD             READ ENTRY FOR THIS EMPLOYER/SSN             
         L     R5,DMCB             R5 = A(RECORD)                               
*                                                                               
*                                  TRACE IF USER WANTS HISTORY TRACE            
         GOTO1 DOTRACE,DMCB,(C'H',(R5)),PYTDLEN,=C'PYTDTAB'                     
*                                                                               
         LA    R5,BLOCK            READ HIGH FOR FIRST UNIT FOR THIS            
         USING ESUTABD,R5              EMPLOYER/SSN                             
         XC    0(ESULEN,R5),0(R5)                                               
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   TGTHREE,ESUEMP                                                   
         MVC   ESUSSN,SRTSSN                                                    
*                                                                               
         L     R2,AESUTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(2,(R5)),BINTABLE                                   
*                                                                               
         L     R5,0(R1)            R5 = A(FIRST UNIT)                           
*                                                                               
TE10     CLC   ESUEMP,TGTHREE      WHILE SAME EMPLOYER                          
         BNE   TE50                                                             
         CLC   ESUSSN,SRTSSN       AND SAME PERFORMER                           
         BNE   TE50                                                             
*                                                                               
         LA    R5,ESULEN(R5)       BUMP R2 TO NEXT ESUTAB ENTRY                 
         B     TE10                LOOP BACK                                    
*                                                                               
TE50     L     R3,0(R1)            R3 = A(FIRST UNIT)                           
         SR    R5,R3               R5 = LENGTH OF PERFORMER'S ENTRIES           
*                                                                               
*                                  TRACE IF USER WANTS HISTORY TRACE            
         GOTO1 DOTRACE,DMCB,(C'H',(R3)),(R5),=C'ESUTAB'                         
*                                                                               
TEX      B     XIT                                                              
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF AN ERROR OCCURS         
* THAT MAKES IT IMPOSSIBLE TO CONTINUE PROCESSING THE INVOICE.  THE             
* TYPE OF THE ERROR IS SAVED IN INVTERR AND THEN THE PROCESSING OF              
* THE INVOICE IS ABORTED BY LOADING RD WITH INVRD (WHICH WAS SET BY             
* PROCINVC) AND DOING AND XIT. CONTROL WILL BE RETURNED TO CHKPASS1             
* WHICH WILL CALL UPDINV TO WRITE BACK THE INVOICE RECORD WITH THE              
* ERROR STATUS INCLUDED.                                                        
*                                                                               
ERRECPI  MVI   INVTERR,TAINECPI    NO PAYMENT DETAILS ELEMENT/INVOICE           
         B     ERRABORT                                                         
*                                                                               
ERRECCO  MVI   INVTERR,TAINECCO    NO CMCL DETAILS ELEMENT/INVOICE              
         B     ERRABORT                                                         
*                                                                               
ERRECAG  MVI   INVTERR,TAINECAG    NO AGENCY RECORD                             
         B     ERRABORT                                                         
*                                                                               
ERREAYE  MVI   INVTERR,TAINEAYE    NO AGENCY DETAILS ELEMENT                    
         B     ERRABORT                                                         
*                                                                               
ERRECPC  MVI   INVTERR,TAINECPC    NO PAYMENT DETAILS ELEMENT/CHECK             
         B     ERRABORT                                                         
*                                                                               
ERRECW4  MVI   INVTERR,TAINECW4    NO W4 RECORD                                 
         B     ERRABORT                                                         
*                                                                               
ERREW4E  MVI   INVTERR,TAINEW4E    NO W4 DETAILS ELEMENT                        
         B     ERRABORT                                                         
*                                                                               
ERRECCA  MVI   INVTERR,TAINECCA    NO CAST ELEMENT                              
         B     ERRABORT                                                         
*                                                                               
ERRECRP  MVI   INVTERR,TAINECRP    NO W4 RECORD FOR INDIV CORPORATION           
         B     ERRABORT                                                         
*                                                                               
ERRETID  MVI   INVTERR,TAINETID    NO TAX ID ELEMENT IN EMP RECORD              
         B     ERRABORT                                                         
*                                                                               
ERRELN   MVI   INVTERR,TAINELN     NO LIEN RECORD                               
         B     ERRABORT                                                         
*                                                                               
ERRELNE  MVI   INVTERR,TAINELNE    NO LIEN DETAILS ELEMENT                      
         B     ERRABORT                                                         
*                                                                               
ERREDU   MVI   INVTERR,TAINEDU     NO DUE COMPANY RECORD                        
         B     ERRABORT                                                         
*                                                                               
ERREIOI  MVI   INVTERR,TAINEIOI    NO ORIGINAL AGY/INV ELEMENT                  
         B     ERRABORT                                                         
*                                                                               
ERREMAT  MVI   INVTERR,TAINEMAT    W4 TYPE DOES NOT MATCH PAYMENT               
         B     ERRABORT                                                         
*                                                                               
ERRETAX  MVI   INVTERR,TAINETAX    INVALID TAX UNIT IN CAST ELEMENT             
         B     ERRABORT                                                         
*                                                                               
ERRECNI  MVI   INVTERR,TAINECNI    CANADIAN SET UP AS INDIVIDUAL                
         B     ERRABORT                                                         
*                                                                               
ERRABORT BAS   RE,ADDERI           ADD INVOICE ERROR TO ERITAB                  
         MVC   AIO,AIO1            RESTORE AIO JUST IN CASE                     
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',SRTREC),SRTCHECK-SRTREC,=C'ERROR'             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',ASRTCHK),0,=C'SRTCHECK'                       
*                                                                               
         L     RD,INVRD            RETURN TO CALLER OF PROCINVC                 
         B     XIT                                                              
*                                                                               
* THIS ROUTINE ADDS AN ENTRY TO THE ERROR INVOICE TABLE AND PUTS THE            
* INVOICE NUMBER AND INVOICE ERROR NUMBER IN THE ENTRY.                         
*                                                                               
ADDERI   L     RF,AERITAB          DIE IF NO MORE ROOM IN ERITAB                
         A     RF,=A(MAXERIS*ERILEN)                                            
         C     RF,NEXTERI                                                       
         BH    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,NEXTERI          R5 = A(NEXT ERITAB ENTRY TO ADD)             
         USING ERITABD,R5                                                       
*                                                                               
         MVC   ERIAGY,SRTAGY       SAVE AGENCY NUMBER IN ENTRY                  
         MVC   ERIINV,SRTINV       SAVE INVOICE NUMBER IN ENTRY                 
         MVC   ERITERR,INVTERR     SAVE INVOICE ERROR NUMBER IN ENTRY           
*                                                                               
         OC    SRTAAGY,SRTAAGY     IF ADJUSTMENT AGY/INV                        
         BZ    *+16                                                             
         MVC   ERIAGY,SRTAAGY      THEN SAVE IN ENTRY                           
         MVC   ERIINV,SRTAINV                                                   
*                                                                               
         LA    R5,ERILEN(R5)       SAVE ADDRESS OF NEXT ENTRY                   
         ST    R5,NEXTERI                                                       
         BR    RE                                                               
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF A CONDITION             
* OCCURS THAT MAKES IT NECESSARY TO ISSUE A WARNING BUT DOES NOT MAKE           
* IT IMPOSSIBLE TO CONTINUE PROCESSING THE INVOICE.  THE WARNING TYPE           
* (INVOICE/CHECK) AND NUMBER ARE SAVED IN THE SORT RECORD.  THE SECOND          
* PASS OF THE PROGRAM WHICH READS THE SORTER RECORDS WILL ADD ENTRIES           
* TO THE WARNING TABLE FOR EACH CHECK THAT HAS A WARNING BECAUSE IT             
* IS AT THAT TIME THAT THE PROGRAM WILL KNOW THE CHECK NUMBER.  WARNING         
* MESSAGES WILL APPEAR AS PART OF THE ERROR REPORT AT THE END.                  
*                                                                               
WARECTI  MVI   SRTWARN,TAINECTI    NO CMCL TITLE ELEMENT/INVOICE                
         B     WARITYP                                                          
*                                                                               
WARECOM  MVI   SRTWARN,TAINECOM    NO COMMERCIAL RECORD                         
         B     WARITYP                                                          
*                                                                               
WARECEM  MVI   SRTWARN,TAINECEM    NO EMPLOYER RECORD                           
         B     WARITYP                                                          
*                                                                               
WARECCL  MVI   SRTWARN,TAINECCL    NO CLIENT RECORD                             
         B     WARITYP                                                          
*                                                                               
WARECPR  MVI   SRTWARN,TAINECPR    NO PRODUCT RECORD                            
         B     WARITYP                                                          
*                                                                               
WAREW4A  MVI   BYTE,TAINEW4A       NO W4 ADDRESS ELEMENT                        
         B     WARCTYP                                                          
*                                                                               
WAREAN   MVI   BYTE,TAINEAN        NO AGENT RECORD                              
         B     WARCTYP                                                          
*                                                                               
WAREANA  MVI   BYTE,TAINEANA       NO AGENT ADDRESS ELEMENT                     
         B     WARCTYP                                                          
*                                                                               
WAREDUE  MVI   BYTE,TAINEDUE       NO DUE COMPANY DETAILS ELEMENT               
         B     WARCTYP                                                          
*                                                                               
WAREDUE2 MVI   BYTE,TAINEDU2       NO FLIST RECORD AS INDICATED ON              
         B     WARCTYP             DUE COMPANY RECORD                           
*                                                                               
WARELNE  MVI   BYTE,TAINELNE       NO LIEN DETAILS ELEMENT                      
         B     WARCTYP                                                          
*                                                                               
WARECKY  MVI   BYTE,TAINECKY       NO CHECK YTD ELEMENT                         
         B     WARCTYP                                                          
*                                                                               
WARITYP  MVI   SRTWTYP,C'I'        SET WARNING TYPE TO 'I' FOR INVOICE          
         BR    RE                      AND RETURN                               
*                                                                               
WARCTYP  CLI   SRTWTYP,C'I'        IF WARNING TYPE ALREADY 'I' FOR              
         BER   RE                      INVOICE THEN RETURN                      
*                                                                               
         MVI   SRTWTYP,C'C'        ELSE SET WARNING TYPE TO 'C' FOR             
         MVC   SRTWARN,BYTE            CHECK AND SET WARNING NUMBER             
         BR    RE                                                               
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF A CONDITION             
* OCCURS THAT MAKES IT IMPOSSIBLE FOR THE PROGRAM TO CONTINUE.  A               
* PROGRAM TERMINATION REASON WILL PRINT ON ITS OWN PAGE 20 TIMES, AND           
* THEN THE PROGRAM WILL DIE.                                                    
*                                                                               
ERRSYS   MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(131),BLOCK                                               
         MVC   BLOCK(34),=C'** BAD OR MISSING SYSTEM RECORD **'                 
         B     ERRDIE                                                           
*                                                                               
ERRNRE   MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(131),BLOCK                                               
         MVC   BLOCK(40),=C'** RERUN TRIED WITHOUT RERUN=YES CARD **'           
         B     ERRDIE                                                           
*                                                                               
* THIS ROUTINE PRINTS THE ERROR MESSAGE IN BLOCK 20 TIMES ON ITS OWN            
* PAGE.  THEN IT TAKES AN INTENTIONAL HIT.                                      
*                                                                               
ERRDIE   L     R3,ASPOOLD          R3 = A(SPOOL DSECT)                          
         USING SPOOLD,R3                                                        
*                                                                               
         MVI   LINE,99             FORCE PAGE EJECT                             
         LA    R2,20               PRINT 20 TIMES                               
*                                                                               
ED10     MVC   P,BLOCK             MOVE BLOCK TO PRINT LINE                     
         GOTO1 SPOOL,DMCB,(R3)     PRINT IT                                     
         BCT   R2,ED10             REPEAT UNTIL 20 TIMES                        
*                                                                               
         DC    H'0'                CAUSE PROGRAM FAILURE                        
         DROP  R3                                                               
         EJECT                                                                  
         GETEL R4,DATADISP,ELCODE                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
*                                                                               
XFFS     DC    6X'FF'                                                           
CHKDIR   DC    CL8'CHKDIR'                                                      
CHKFIL   DC    CL8'CHKFIL'                                                      
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,00,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=XXXX'                                  
*                                                                               
DMYCWEL  DC    AL1(TACWELQ,TACWLNQ)  DUMMY FEDERAL WITHHOLDING EL.              
         DC    C'FD '                                                           
         DC    (TACWLNQ-(*-DMYCWEL))X'00'                                       
         DC    X'00'               SET EOR                                      
*                                                                               
MYSPACES DC    CL36' '                                                          
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
         DROP  RA,R9,R8                                                         
         EJECT                                                                  
*              ROUTINE TO INITIALIZE LCLTAB AND OFFTAB                          
         SPACE 1                                                                
         DS    0D                                                               
VINITABS NMOD1 0,INITAB                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         BAS   RE,INITLCL          INITIALIZE LCLTAB                            
*                                                                               
         BAS   RE,INITOFF          INITIALIZE OFFTAB                            
*                                                                               
INITABSX XIT1                                                                   
         EJECT                                                                  
* INITIALIZE TABLE OF AFTRA LOCALS THAT ARE NOT TO HAVE THEIR CHECKS            
* SENT DIRECTLY TO THEM.  THESE AFTRA LOCALS WILL SORT WITH THE SAG             
* CHECKS.                                                                       
*                                                                               
INITLCL  NTR1                                                                   
         L     R2,ALCLTAB          R2=A(FIRST LCLTAB ENTRY)                     
         LA    R0,MAXLCLS          R0=MAXIMUM NUMBER OF LCLTAB ENTRIES          
*                                                                               
         LA    R3,KEY              BUILD KEY OF UNION LOCAL RECORD              
         XC    KEY,KEY                 FOR UNION 'AFT'                          
         USING TLLOD,R3                                                         
         MVI   TLLOCD,TLLOCDQ                                                   
         MVC   TLLOUN,=C'AFT'                                                   
*                                                                               
         GOTO1 HIGH                GET FIRST UNION LOCAL KEY                    
*                                                                               
*                                  WHILE STILL UNION 'AFT'                      
IL10     CLC   TLLOKEY(TLLOLCL-TLLOKEY),KEYSAVE                                 
         BNE   INITABSX                                                         
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R4,AIO              R4 = A(LOCAL ELEMENT)                        
         MVI   ELCODE,TALOELQ                                                   
         BAS   RE,GETEL2                                                        
         BNE   IL50                IF NONE FOUND THEN ADD TO TABLE              
         USING TALOD,R4                                                         
*                                                                               
         TM    TALOSTAT,TALOSUCP   IF UNION COPIES STATUS BIT NOT SET           
         BO    IL90                                                             
*                                                                               
IL50     LTR   R0,R0               TEST DIE IF NO MORE ROOM IN LCLTAB           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   0(3,R2),TLLOUN      SAVE UNION/LOCAL IN LCLTAB                   
         MVC   3(3,R2),TLLOLCL                                                  
*                                                                               
         LA    R2,6(R2)            BUMP R2 TO NEXT LCLTAB ENTRY                 
         SH    R0,=H'1'            DECREMENT NUMBER OF ENTRIES LEFT             
*                                                                               
IL90     GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     IL10                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
* INITIALIZE TABLE OF OFFICES.  THIS IS DONE STRICTLY FOR EFFICIENCY            
* REASONS - PROGRAM USED TO READ OFFICE RECORD FOR EACH PRINTED CHECK.          
*                                                                               
INITOFF  NTR1                                                                   
         LA    R2,OFFTAB           R2=A(FIRST OFFTAB ENTRY)                     
         USING OFFD,R2                                                          
         LA    R0,MAXOFFS          R0=MAXIMUM NUMBER OF OFFTAB ENTRIES          
*                                                                               
         LA    R3,KEY              INITIALIZE KEY                               
         XC    KEY,KEY                                                          
         USING TLOFD,R3                                                         
         MVI   TLOFCD,TLOFCDQ                                                   
*                                                                               
         GOTO1 HIGH                GET FIRST OFFICE KEY                         
*                                                                               
IO10     CLI   TLOFKEY,TLOFCDQ     WHILE STILL OFFICE RECORD                    
         BNE   INITABSX                                                         
*                                                                               
         LTR   R0,R0               TEST DIE IF NO MORE ROOM IN OFFTAB           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   OFFCODE,TLOFOFF     SAVE OFFICE CODE IN OFFTAB                   
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         GOTO1 CHAROUT,DMCB,TANAELQ,0  GET NAME                                 
         MVC   OFFNAME,TGNAME                                                   
*                                                                               
         L     R4,AIO              R4 = A(OFFICE ELEMENT)                       
         MVI   ELCODE,TAOFELQ                                                   
         USING TAOFD,R4                                                         
         BAS   RE,GETEL2                                                        
         BNE   *+10                                                             
         MVC   OFFTEL,TAOFTEL      SAVE TELEPHONE NUMBER IN TABLE               
*                                                                               
         LA    R2,OFFNEXT          BUMP R2 TO NEXT OFFTAB ENTRY                 
         SH    R0,=H'1'            DECREMENT NUMBER OF ENTRIES LEFT             
*                                                                               
         GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     IO10                                                             
         DROP  R2,R3,R4                                                         
         SPACE                                                                  
         GETEL2 R4,DATADISP,ELCODE                                              
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE USES THE TABLE ENTRY, POINTED TO BY PARAMETER ONE, TO            
* CALCULATE THE DUE COMPANY AMOUNT TO COLLECT.  THE COLLECTED AMOUNT IS         
* STORED IN DUECOL.  IF THE TABLE ENTRY IS FILTERED FOR SOME REASON,            
* DUECOL WILL BE ZERO.                                                          
*                                  XIT DUESTAT SET                              
*                                                                               
         DS    0D                                                               
CALCDUE  NMOD1 0,CLCDUE                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         XC    DUECOL,DUECOL       INITIALIZE COLLECTED AMOUNT TO ZERO          
         L     R5,0(R1)            R5 = A(DUE COMPANY ENTRY)                    
         USING DUETABD,R5                                                       
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         OC    TADUUNI,TADUUNI     IF UNION IS BLANK - USE IT                   
         BZ    *+14                                                             
         CLC   TADUUNI,SRTUN       ELSE IT MUST BE SAME UNION                   
         BNE   CD90                                                             
*                                                                               
         TM    TADUSTAT,TADUSCAN   IF RECORD IS FOR CANADIAN $                  
         BZ    *+16                                                             
         CLI   RECNUM,CN           THEN ONLY PROCESS IF WE'RE CAN$ CKS          
         BNE   CD90                                                             
         B     *+12                                                             
         CLI   RECNUM,CN           ELSE ONLY PROCESS IF NOT CAN$ CKS            
         BE    CD90                                                             
*                                  IF BOTH COLLECT ONLY BITS SET                
         TM    TADUSTAT,TADUSAGY+TADUSCLI                                       
         BNO   CD10                                                             
         BAS   RE,CKDUEAGY         THEN MUST BE EITHER SAME AGENCY              
         BL    CD90                                                             
         BE    CD20                                                             
         BAS   RE,CKDUECLI         OR SAME CLIENT AS SORT RECORD                
         BE    CD20                                                             
         B     CD90                                                             
*                                                                               
CD10     TM    TADUSTAT,TADUSAGY   IF COLLECT ONLY FROM AGENCY BIT SET          
         BZ    *+12                                                             
         BAS   RE,CKDUEAGY         THEN MUST BE SAME AGENCY AS SORT REC         
         BNE   CD90                                                             
*                                                                               
         TM    TADUSTAT,TADUSCLI   IF COLLECT ONLY FROM CLIENT BIT SET          
         BZ    *+12                                                             
         BAS   RE,CKDUECLI         THEN MUST BE SAME CLIENT AS SORT REC         
         BNE   CD90                                                             
*                                                                               
CD20     L     RF,TADUDUE          AMOUNT TO COLLECT = AMOUNT DUE -             
         S     RF,TADUCOL              AMOUNT ALREADY COLLECTED                 
*                                                                               
         L     R3,SRTNET           AVAILABLE CHECK BALANCE = CHECK              
         S     R3,SRTMDED              AMOUNT - MISC DED                        
         S     R3,SRTDUES                     - UNION DUES                      
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,3,TADUPCT        IF DEDUCTION PERCENTAGE NOT ZERO             
         BZ    CD30                                                             
         CLI   TADULEN,X'2C'       AND NEW LENGTH                               
         BNH   CD23                                                             
         OC    TADUAYFL,TADUAYFL   AND AGENCY FLIST                             
         BNZ   CD25                                                             
         OC    TADUCLFL,TADUCLFL   OR CLIENT FLIST                              
         BNZ   CD25                                                             
CD23     CLC   TADUAGY,SRTAGY      AND DIFFERENT AGENCY                         
         BNE   CD25                                                             
         CLC   TADUFCLI,=6C' '     & SPECIFIC,                                  
         BNH   CD30                                                             
         CLC   TADUFCLI,SRTCLI     DIFFERENT CLIENT                             
         BE    CD30                                                             
*                                                                               
CD25     OI    DUESTAT,DUESPCT     PERCENTAGE TAKEN                             
         MR    R2,R0               USE THE PECENTAGE                            
         D     R2,=F'5000'                                                      
         LTR   R3,R3                                                            
         BM    *+8                 & ROUND IT                                   
         AHI   R3,1                                                             
         SRA   R3,1                                                             
*                                                                               
CD30     CR    RF,R3               IF AMOUNT TO COLLECT > AVAIL BAL             
         BNH   *+6                                                              
         LR    RF,R3               THEN AMOUNT TO COLLECT = AVAIL BAL           
         ST    RF,DUECOL           SAVE AMOUNT TO COLLECT IN DUECOL             
*                                                                               
CD90     TM    DUESTAT,DUESRERD    IF NEED TO RE-ESTABLISH READ                 
         BZ    CDX                                                              
         NI    DUESTAT,X'FF'-DUESRERD                                           
         MVC   KEY,SVDUEKEY        USE SAVED DUE COMPANY KEY                    
         GOTO1 HIGH                                                             
         B     CDX                                                              
         SPACE 2                                                                
CDLOW    LNR   RC,RC                                                            
         B     CDNO                                                             
CDYES    SR    RC,RC                                                            
CDNO     LTR   RC,RC                                                            
CDX      XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*              ROUTINE CHECKS DUE COMPANY AGENCY MATCHES                        
*              AGENCY ON CHECK                                                  
*                                                                               
CKDUEAGY NTR1                                                                   
         XR    R0,R0               SET INDICATOR PROCESSING AGENCY              
         CLI   TADULEN,X'2C'       IF NEW ELEMENT LENGTH                        
         BNH   CKDUEAY8                                                         
         OC    TADUAYFL,TADUAYFL   AND AGENCY FLIST SPECIFIED                   
         BZ    CKDUEAY8                                                         
         BAS   RE,PROCFLST         CHECK AGAINST AGENCY FLIST                   
         B     *+10                                                             
CKDUEAY8 CLC   TADUAGY,SRTAGY      ELSE, CHECK AGAINST AGENCY CODE              
         B     CDX                 CC LOW/YES/NO SET                            
         SPACE 2                                                                
*              ROUTINE CHECKS DUE COMPANY CLIENT MATCHES                        
*              CLIENT ON CHECK                                                  
*                                                                               
CKDUECLI NTR1                                                                   
         LA    R0,1                SET INDICATOR PROCESSING CLIENT              
         CLI   TADULEN,X'2C'       IF NEW ELEMENT LENGTH                        
         BNH   CKDUECL8                                                         
         OC    TADUCLFL,TADUCLFL   AND CLIENT FLIST SPECIFIED                   
         BZ    CKDUECL8                                                         
         BAS   RE,PROCFLST         CHECK AGAINST CLIENT FLIST                   
         B     *+10                                                             
CKDUECL8 CLC   TADUFCLI,SRTCLI     ELSE, CHECK AGAINST CLIENT CODE              
         B     CDX                 CC LOW/YES/NO SET                            
         EJECT                                                                  
*              ROUTINE CHECKS AGENCY OR CLIENT ON CHECK IS IN                   
*              FLIST INDICATED ON THE DUE COMPANY RECORD                        
*                                  NTRY - R0=TABLE INDICATOR                    
*                                  XIT    CC SET LOW/YES/NO                     
*                                                                               
PROCFLST NTR1                                                                   
         MVC   TGLST,TADUCLFL      SET CLIENT FLIST RECORD CODE                 
         L     R2,ACLFTAB          R2=A(CLIENT FLIST TABLE)                     
         LTR   R0,R0               IF PROCESSING CLIENT                         
         BNZ   *+14                                                             
         MVC   TGLST,TADUAYFL      ELSE, SET AGENCY FLIST RECORD CODE           
         L     R2,AAYFTAB          R2=A(AGENCY FLIST TABLE)                     
*                                                                               
         USING FLSTTABD,R2         R2=A(TABLE TO CHECK)                         
PFLIST2  CLI   0(R2),X'FF'         IF END OF TABLE                              
         BNE   PFLIST3                                                          
         L     R2,ACLFTAB          START OVER, CLEARING TABLE                   
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         L     R2,AAYFTAB                                                       
         BAS   RE,CLRFTAB          CLEAR FLIST TABLE                            
*                                                                               
PFLIST3  CLI   FLSTCODE,0          IF END OF TABLE                              
         BNE   *+12                                                             
         BAS   RE,ADDFLST          ADD FLIST TO TABLE                           
         BNE   CDLOW               FLIST RECORD NOT FOUND(EXIT LOW)             
*                                                                               
         CLC   FLSTCODE,TGLST      ELSE CHECK FLIST IN TABLE                    
         BNE   *+12                                                             
         BAS   RE,CKFLST           CHECK CODE IN FLIST                          
         B     CDX                 CC YES/NO SET                                
*                                                                               
         ZIC   R1,FLSTNUM          BUMP TO NEXT FLIST ENTRY                     
         MH    R1,=AL2(L'FLSTDATA)                                              
         LA    R1,FLSTLEN(R1)                                                   
         AR    R2,R1                                                            
         B     PFLIST2                                                          
         SPACE 2                                                                
*              ROUTINE TO CLEAR FLIST TABLE                                     
*                                  R2=A(TABLE)                                  
CLRFTAB  NTR1                                                                   
         LR    RE,R2                                                            
         L     RF,=A(FLISTABL)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     CDX                                                              
         EJECT                                                                  
*              ROUTINE CHECKS CODE IN FLIST                                     
*                                                                               
CKFLST   NTR1                                                                   
         ZIC   R1,FLSTNUM          R1=N'CODES IN FLIST                          
         LA    R3,FLSTDATA         R3=A(LIST OF CODES IN FLIST)                 
*                                                                               
CKFLST5  LTR   R0,R0                                                            
         BZ    *+14                                                             
         CLC   SRTCLI,0(R3)        CHECK CLIENT IN FLIST                        
         B     *+10                                                             
         CLC   SRTAGY,0(R3)        CHECK AGENCY IN FLIST                        
         BE    CDYES                                                            
         LA    R3,L'FLSTDATA(R3)   BUMP TO NEXT CODE IN THIS FLIST              
         BCT   R1,CKFLST5                                                       
         B     CDNO                                                             
         SPACE 2                                                                
*              ROUTINE READS FLIST RECORD AND ADDS TO TABLE                     
*                                                                               
ADDFLST  NTR1                                                                   
         MVI   TGLTYP,TLGLTYPF                                                  
         MVC   AIO,AIO2                                                         
         OI    DUESTAT,DUESRERD    SET TO REREAD DUE COMPANY                    
         GOTO1 RECVAL,DMCB,TLGLCDQ,(X'A0',0)                                    
         MVC   AIO,AIO1                                                         
         BE    *+12                                                             
ADDFLST2 OI    DUESTAT,DUESERR     SET ERROR READING FLIST RECORD               
         B     CDNO                                                             
*                                                                               
         XR    R1,R1               R1=NUMBER OF CODES                           
         LA    R3,FLSTDATA         R3=A(CODES)                                  
         L     R4,AIO2                                                          
         MVI   ELCODE,TAGLELQ                                                   
         BAS   RE,GETEL3                                                        
         B     *+8                                                              
ADDFLST5 BAS   RE,NEXTEL3                                                       
         BNE   ADDFLST7                                                         
*                                                                               
         USING TAGLD,R4                                                         
         ZIC   RF,TAGLLEN                                                       
         SH    RF,=Y(TAGLLNQ)                                                   
         CH    RF,=AL2(L'FLSTDATA)                                              
         BH    ADDFLST2            BAD FLIST DATA - PUT IN ERROR                
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),TAGLDATA                                                 
         OC    0(L'FLSTDATA,R3),=6C' '                                          
         LA    R3,L'FLSTDATA(R3)                                                
         LA    R1,1(R1)            INCREMENT NUMBER OF CODES                    
         B     ADDFLST5                                                         
*                                                                               
ADDFLST7 STC   R1,FLSTNUM                                                       
         MVC   FLSTCODE,TGLST      SET FLIST CODE IN TABLE                      
         B     CDYES                                                            
         DROP  R2,R4                                                            
         EJECT                                                                  
         GETELN R4,DATADISP,ELCODE,3                                            
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
* THIS ROUTINE RETURNS THE ESUTAB ENTRY FOR THE EMPLOYER/PERFORMER              
* SPECIFIED BY THE SORT RECORD AND THE UNIT SPECIFIED BY PARAMETER 1.           
* THE ROUTINE WILL LOOK FOR THE REQUESTED ENTRY AND BUILD A NEW ONE             
* IF IT DOESN'T FIND IT.  IF THE NEW ENTRY IS FOR FD, THE ROUTINE WILL          
* BUILD NEW ENTRIES FOR ALL UNITS THAT PERFORMER HAS BEEN PAID ON.              
* THE ASSUMPTION IS THAT THIS ROUTINE IS ALWAYS CALLED FIRST FOR FD.            
* THE ADDRESS OF THE ENTRY IN ALL CASES WILL BE RETURNED IN DMCB.               
*                                                                               
         DS    0D                                                               
VGETESU  NMOD1 0,GETESU                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R2,0(R1)            R2 = A(UNIT FOR LOOKUP)                      
*                                                                               
         LA    R5,BLOCK            R5 = A(ESUTAB LOOKUP ENTRY)                  
         USING ESUTABD,R5                                                       
*                                                                               
GE10     XC    BLOCK(ESULEN),BLOCK  BUILD LOOKUP ENTRY FOR UNIT                 
         MVC   ESUEMP,SRTEMP              REQUESTED BY CALLER                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,0(R2)                                                    
*                                                                               
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5)  LOOKUP/ADD ENTRY TO TABLE              
*                                                                               
         CLI   0(R1),1             IF ENTRY WAS FOUND THEN RETURN IT            
         BNE   GEX                     (DMCB HAS ADDRESS)                       
*                                                                               
         CLC   ESUUNIT,=C'FD '     ELSE IF THIS UNIT IS FEDERAL                 
         BNE   GEX                                                              
         BAS   RE,BLDESU           BUILD ALL ESUTAB ENTRIES FOR PERF.           
         B     GE10                GO BACK AND FIND FD ENTRY JUST ADDED         
*                                                                               
GEX      XIT1                                                                   
         EJECT                                                                  
* THIS ROUTINE WILL BUILD NEW ESUTAB ENTRIES FOR A PERFORMER.  THE              
* NEW ENTRIES WILL BE BUILT BY GOING TO TAYTD, WHICH BUILDS A TABLE             
* WITH EACH TAX UNIT THE PERFORMER HAS BEEN INVOLVED WITH.  EACH ENTRY          
* IN THE TABLE RETURNED BY TAYTD CORRESPONDS TO AN ENTRY IN ESUTAB.             
*                                                                               
BLDESU   NTR1                                                                   
         XC    YTDBLK,YTDBLK       CLEAR TAYTD PARAMETER BLOCK                  
         LA    R3,YTDBLK                                                        
         USING TYD,R3                                                           
         MVC   TYASYSIO,TASYSIO    A(SYSIO)                                     
         MVC   TYTRACE,YTDTRACE    TRACE SWITCH                                 
         L     R4,=A(YTDTAB)                                                    
         ST    R4,TYATAB           A(YTD TABLE)                                 
         USING YTDD,R4                                                          
*                                                                               
         CLC   TGTODAY1(1),CHKDATE1 IF CHECK DATE IN THIS YEAR                  
         BNE   *+14                                                             
         MVC   TYPEND,TGTODAY1     THEN SET END DATE AS TODAY                   
         B     *+10                                                             
         MVC   TYPEND,CHKDATE1     ELSE, SET END DATE AS CHECK DATE             
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+10                                                             
         MVC   TYPEND,CHKDATEL     USE THAT DATE                                
         MVC   TYEMP,SRTEMP        EMPLOYER                                     
         MVC   TYSSN,SRTSSN        SOCIAL SECURITY NUMBER                       
*                                                                               
         MVI   TYCUR,C'U'          IF CURRENCY IS US THEN TYCUR = U             
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TYCUR,C'C'          ELSE TYCUR = C                               
*                                                                               
         GOTO1 TGTAYTD,DMCB,(RC),SYSCOMM,(R3)  BUILD YTD TABLE                  
*                                                                               
         LA    R5,BLOCK            R5=A(PYTDTAB ENTRY)                          
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5) BUILD PYTDTAB ENTRY                          
         MVC   PYTDEMP,SRTEMP      EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN      SOCIAL SECURITY NUMBER                       
         MVC   PYTDAMTS,TYCYTD     YTD TOTALS                                   
         GOTO1 ADDBIN,DMCB,APYTDTAB,(R5)  ADD PYTDTAB ENTRY                     
*                                                                               
BE10     CLI   0(R4),0             END OF YTD TABLE BY UNIT                     
         BE    BEX                                                              
         USING ESUTABD,R5          R5=A(ESUTAB ENTRY)                           
         XC    0(ESULEN,R5),0(R5)  BUILD ESUTAB ENTRY                           
         MVC   ESUEMP,SRTEMP       EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN       SOCIAL SECURITY NUMBER                       
         MVC   ESUUNIT,YTDUNIT     TAX UNIT                                     
         MVC   ESUEARN,YTDEARN     EARNINGS                                     
         MVC   ESUTAX,YTDTAX       TAX                                          
*                                                                               
         CLC   ESUUNIT,=C'FD '     IF THIS IS FEDERAL                           
         BNE   BE20                                                             
         MVC   ESUREXP,YTDREXP     REIMB. EXPENSES                              
         MVC   ESUFICA,YTDFICA     FICA                                         
         B     BE30                                                             
*                                                                               
BE20     CLC   ESUUNIT,=C'CN '     IF THIS IS CANADA                            
         BNE   *+14                                                             
         MVC   ESUGST,YTDGST       GOODS AND SERVICES TAX                       
         B     BE30                                                             
*                                                                               
         CLI   ESUUNIT+2,C' '      IF THIS IS STATE                             
         BNE   BE30                                                             
         MVC   ESUSUI,YTDSUI       SUI                                          
         MVC   ESUSDI,YTDSDI       SDI                                          
*                                                                               
BE30     GOTO1 ADDBIN,DMCB,AESUTAB,(R5)  ADD ESUTAB ENTRY                       
*                                                                               
         LA    R4,YTDNEXT          BUMP TO NEXT TABLE ENTRY                     
         B     BE10                                                             
*                                                                               
BEX      B     GEX                                                              
         EJECT                                                                  
* CONSTANTS, ETC. FOR GETESU, BLDESU ROUTINES                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE ADDS THE BINSRCH RECORD PASSED AS PARAMETER 2 TO THE             
* BINSRCH TABLE PASSED AS PARAMTER 1.                                           
* IF THE ENTRY IS ALREADY IN THE TABLE IT ADDS THE AMOUNTS IN THE               
* NEW RECORD TO THOSE IN THE TABLE ALREADY.  THE ADDRESS OF THE ENTRY           
* IS ALWAYS RETURNED IN DMCB.                                                   
*                                                                               
         DS    0D                                                               
VADDBIN  NMOD1 0,ADDBIN                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         LM    R2,R3,0(R1)         R3=A(NEW RECORD)                             
         USING BIND,R2             R2=A(BINSRCH PARAMETERS/TABLE)               
*                                                                               
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(1,(R3)),BINTABLE                                   
*                                                                               
         OC    1(3,R1),1(R1)       DIE IF NO MORE ROOM IN TABLE                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   BININ,8(R1)         UPDATE COUNT                                 
*                                                                               
         CLI   0(R1),1             IF NEW RECORD ADDED                          
         BE    ABX                 THEN DONE                                    
*                                                                               
         L     R4,0(R1)            R4=A(RECORD FOUND)                           
         ZIC   R1,BINFRST          DISP. TO FIRST BUCKET                        
         LA    RE,0(R1,R4)         RE=A(1ST BUCKET IN RECORD FOUND)             
         LA    RF,0(R1,R3)         RF=A(1ST BUCKET IN NEW RECORD)               
         ICM   R1,1,BINNUMB        R1=NUMBER OF BUCKETS                         
         BZ    ABX                                                              
         SPACE 1                                                                
         TM    BINSTAT,X'80'       ACCUMS ARE BINARY                            
         BO    AB2                                                              
         AP    0(8,RE),0(8,RF)     ADD NEW TO OLD                               
         LA    RE,8(RE)                                                         
         LA    RF,8(RF)                                                         
         BCT   R1,*-14                                                          
         B     ABX                                                              
*                                                                               
AB2      L     R0,0(RE)            ADD NEW TO OLD                               
         A     R0,0(RF)                                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R1,AB2                                                           
*                                                                               
ABX      XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE BUILDS THE SORT KEY PART OF THE SORT RECORD FROM THE             
* DATA CONTAINED BOTH IN THE GENERAL STORAGE ARE AND THE SORT RECORD            
* ITSELF.                                                                       
*                                                                               
         DS    0D                                                               
VBLDSORT NMOD1 0,BLDSRT                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                  PRE-CLEAR SORT KEY                           
         XC    SRTKEY(SRTKEYL),SRTKEY                                           
*                                                                               
         TM    SRTBITS,SRTBW4      FIRST COME PERFORMERS CHECKS WHO             
         BZ    BS10                    NEED THEM FIRST (SORTED BY LAST          
         MVI   SRTKSTA,SRTKSW4         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS10     OC    SRTDUE,SRTDUE       THEN COME CHECKS WITH DUE COMPANY            
         BZ    BS20                    AMOUNTS IN THEM (SORTED BY LAST          
         MVI   SRTKSTA,SRTKSDU         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS20     OC    SRTLIEN,SRTLIEN     THEN COME CHECKS WITH LIEN AMOUNTS           
         BZ    BS25                    IN THEM (SORTED BY LAST NAME)            
         MVI   SRTKSTA,SRTKSLN                                                  
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS25     OC    SRTTRST,SRTTRST     THEN COME CHECKS TO TRUSTEE'S                
         BZ    BS30                    (SORTED BY LAST NAME)                    
         CLI   SRTW4TY,TAW4TYTR                                                 
         BNE   BS30                                                             
         MVI   SRTKSTA,SRTKSW4T                                                 
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS30     CLC   SRTNET,=F'500000'   THEN COME CHECKS WITH NET AMOUNTS OF         
         BL    BS40                    $5000.00 OR MORE (SORTED BY LAST         
         MVI   SRTKSTA,SRTKS50         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS40     CLI   RECNUM,PC           IF NOT PRINT CHECKS                          
         BE    BS50                                                             
         CLC   SRTNCDE,=H'1999'    THEN COME CHECKS FOR AGENT 1999 AND          
         BNE   *+12                    ASIAN CHECKS AND SPANISH CHECKS          
         MVI   SRTKSTA,SRTKS199                                                 
         B     BS50                                                             
         CLI   SRTCTYP,CTYASIAN                                                 
         BNE   BS45                                                             
         CLC   SRTUN,=C'SAG'       SPECIAL FOR ASIAN ONLY IF SAG                
         BNE   BS60                                                             
         MVI   SRTKSTA,SRTKASN                                                  
         B     BS50                                                             
BS45     CLI   SRTCTYP,CTYSPAN                                                  
         BNE   BS60                                                             
         CLC   SRTUN,=C'SAG'       SPECIAL FOR SPANISH ONLY IF SAG              
         BNE   BS60                                                             
         MVI   SRTKSTA,SRTKSSP                                                  
*                                                                               
BS50     MVC   SRTKAGY,SRTAGY      SORTED BY AGENCY, INVOICE, LAST NAME         
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS60     CLC   SRTUN,=C'AFT'       THEN COME CHECKS FOR AFTRA LOCALS            
         BNE   BS70                    THAT GET UNION COPIES                    
         TM    SRTBITS,SRTBLCL                                                  
         BO    BS70                                                             
*                                                                               
         MVI   SRTKSTA,SRTKSAF     SORTED BY LOCAL, AGENCY, INVOICE,            
         MVC   SRTKLOCL,SRTLOCL        CAST SORT KEY, SSN, AND                  
         MVC   SRTKAGY,SRTAGY          CATEGORY                                 
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKCST,SRTCST                                                   
         MVC   SRTKSSN,SRTSSN                                                   
         MVC   SRTKCAT,SRTCAT                                                   
         B     BS200                                                            
*                                                                               
BS70     CLC   SRTUN,=C'SAG'       THEN COME SAG CHECKS AND AFTRA               
         BE    BS80                    CHECKS FOR LOCALS THAT DON'T             
         CLC   SRTUN,=C'AFT'           GET UNION COPIES                         
         BNE   BS100                                                            
*                                                                               
BS80     OC    SRTNCDE,SRTNCDE     WITHOUT AGENTS (SORTED BY LAST NAME)         
         BNZ   BS90                                                             
         MVI   SRTKSTA,SRTKSNA                                                  
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS90     MVI   SRTKSTA,SRTKSWA     THEN WITH AGENTS (SORTED BY AGENT            
         MVC   SRTKNCDE,SRTNCDE        CODE)                                    
         B     BS200                                                            
*                                                                               
BS100    CLC   SRTUN,=C'AFM'       THEN COME AFM CHECKS                         
         BE    *+12                                                             
         CLI   RECNUM,CN           OR CANADIAN DOLLAR CHECKS                    
         BNE   BS190                                                            
         MVI   SRTKSTA,SRTKSAM     SORTED BY LOCAL, AGENCY, INVOICE,            
         MVC   SRTKLOCL,SRTLOCL        CAST SORT KEY, SSN, AND                  
         MVC   SRTKAGY,SRTAGY          CATEGORY                                 
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKCST,SRTCST                                                   
         MVC   SRTKSSN,SRTSSN                                                   
         MVC   SRTKCAT,SRTCAT                                                   
         B     BS200                                                            
*                                                                               
BS190    MVI   SRTKSTA,SRTKSOT     THEN COME ALL OTHER CHECKS (SORTED           
         MVC   SRTKUN,SRTUN            BY UNION/LOCAL/LAST NAME)                
         MVC   SRTKLOCL,SRTLOCL                                                 
         MVC   SRTKLST(32),LASTNAME                                             
*                                                                               
BS200    MVC   SRTKSEQ,PROCSEQ+1   ALL CHECKS SORTED LOWEST BY PROCSEQ          
*                                                                               
         OC    REQASOF,REQASOF     IF AS/OF DATE REQUESTED                      
         BZ    BSX                                                              
         CLC   DUEDATE,REQASOF     AND DUE DATE ON/AFTER AS/OF DATE             
         BL    BSX                                                              
         OI    SRTKSTA,SRTKASOF    THEN SET TO SORT SEPARATELY                  
*                                                                               
BSX      XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE SEARCHES THE EMPLOYER RECORD FOR THE STATE U.C. # FOR            
* THE STATE PASSED IN PARAMETER 1 AND SAVES IT IN THE SORT RECORD.              
*                                                                               
*                                  P1=A(STATE FOR LOOKUP)                       
         DS    0D                                                               
VGETSUC  NMOD1 0,GETSUC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R2,0(R1)            R2=A(STATE CODE)                             
*                                                                               
         XC    SRTSUC,SRTSUC       PRE-CLEAR STATE U.C. #                       
*                                                                               
         L     RF,AEMPIO           IF EMPLOYER RECORD NOT FOUND                 
         CLI   0(RF),0                                                          
         BE    GSX                 THEN RETURN                                  
*                                                                               
         MVC   AIO,AEMPIO          SEARCH EMPLOYER RECORD FOR TAX ID            
         MVI   ELCODE,TATIELQ          ELEMENT FOR STATE OF WORK                
         MVI   FULL,TATITYUN                                                    
         MVC   FULL+1(3),0(R2)                                                  
         GOTO1 GETL,DMCB,(4,FULL)  FIRST TRY TO FIND UNEMPLOYMENT ID            
         BE    GS10                                                             
*                                                                               
         MVI   FULL,TATITYTX       THEN TRY TO FIND TAX ID                      
         GOTO1 GETL,DMCB,(4,FULL)                                               
         BNE   GSX                 RETURN IF NOT FOUND                          
*                                                                               
GS10     L     R4,TGELEM           SAVE STATE U.C. # IN SORT RECORD             
         USING TATID,R4                                                         
         MVC   SRTSUC,TATIID                                                    
*                                                                               
GSX      MVC   AIO,AIO1                                                         
         XIT1                                                                   
         DROP  R4                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE WILL PASS THE FIRST 4 PARAMETERS ALONG TO 'TRACE' IF             
* THE HOB OF PARM 1 IS ONE OF THE REQUESTED TRACE BYTES.                        
*                                                                               
         DS    0D                                                               
VDOTRACE NMOD1 0,DOTRAC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         ZIC   RF,8(R1)            EXTRACT L'LITERAL                            
         ST    RF,12(R1)           AND MOVE TO P4                               
*                                                                               
         LA    R0,60               TEST 60 TRACE BYTES AGAINST ARGUMENT         
         LA    RE,REQTRACE                                                      
*                                                                               
DT10     CLI   0(RE),C' '          IF END OF TRACE BYTES THEN DONE              
         BNH   DTX                                                              
         CLC   0(1,RE),0(R1)       IF MATCH THEN DO TRACE                       
         BE    DT20                                                             
         LA    RE,1(RE)            ELSE BUMP TO NEXT TRACE BYTE                 
         BCT   R0,DT10                                                          
*                                                                               
         B     DTX                 NO MATCH - RETURN                            
*                                                                               
DT20     L     R4,AMASTD           SAVE AND CLEAR PRTQUE REMOTE KEY             
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
         MVC   SVREMOT,REMOTKEY                                                 
         XC    REMOTKEY,REMOTKEY                                                
*                                                                               
         GOTO1 TRACE               DO TRACE                                     
*                                                                               
         MVC   REMOTKEY,SVREMOT    RESTORE PRTQUE REMOTE KEY                    
         DROP  R4                                                               
*                                                                               
DTX      XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE INTERFACES INTO THE THE COMMON ROUTINE TO MAINTAIN               
* PASSIVE POINTERS ON THE FILE.                                                 
*                                                                               
         DS    0D                                                               
VADDPTRS NMOD1 0,ADDPTR                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                  INITIALIZE PARAMETER BYTE                    
         MVI   BYTE,X'08'+X'04'    SET PASSING A(NEW PTR BLOCK AREA)            
*                                  AND OK TO DELETE TLINBCDQ POINTER            
*                                                                               
         L     R3,AIO              IF NEW RECORD IS DELETED THEN SET            
         USING TLRCD,R3                FORCE DELETE PARAMETER BIT               
         TM    TLRCSTAT,X'80'                                                   
         BZ    *+8                                                              
         OI    BYTE,X'40'                                                       
*                                                                               
         CLI   RERUN,C' '          IF RERUNNING THEN PTRS MAY BE OUT            
         BNH   *+8                                                              
         OI    BYTE,X'02'          OF SYNC, SO SET TO ALLOW ALL CHANGES         
*                                                                               
         CLI   PTRTRACE,C'Y'       IF TRACING ADDPTR CALLS                      
         BNE   *+8                                                              
         OI    BYTE,X'10'          THEN SET APPROPRIATE PARAMETER BIT           
*                                                                               
         GOTO1 AADDPTRS,DMCB,(BYTE,ASPBLK),AAPBLK  MAINTAIN POINTERS            
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE RETURNS 'YES' IF THE CURRENT CHECK IS NOT TO UNDERGO             
* CALCULATIONS, THAT IS TO SAY, IT'S AMOUNTS ARE TO BE EXTRACTED.               
* OTHERWISE IT RETURN 'NO'.                                                     
*                                                                               
         DS    0D                                                               
VNOCALC  NMOD1 0,NOCALC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM REQUESTED FOR EXTRACTION          
         BE    NCYES                   ONLY THEN RETURN 'YES'                   
*                                                                               
         CLI   SRTADJS,0           ELSE IF CHECK IS A PAY ADJUSTMENT            
         BNE   NCYES               THEN RETURN 'YES'                            
*                                                                               
         TM    SRTPST1,TAPDPCRD    ELSE IF CHECK IS A CREDIT PAYMENT            
         BO    NCYES               THEN RETURN 'YES'                            
*                                                                               
         CLI   RERUN,C'D'          ELSE IF RERUN = DIED                         
         BNE   NCNO                                                             
*                                                                               
         L     R2,AIO              R2=A(CURRENT AIO)                            
         MVC   AIO,ASRTCHK         AND CHECK WAS ALREADY CALCULATED             
         MVI   ELCODE,TACDELQ          DURING RUN THAT DIED                     
         GOTO1 GETL,DMCB,0                                                      
         ST    R2,AIO              RESET A(CURRENT AIO)                         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R4,TGELEM                                                        
         USING TACDD,R4                                                         
         OC    TACDRUN,TACDRUN                                                  
         BNZ   NCYES               THEN RETURN 'YES'                            
         B     NCNO                                                             
*                                                                               
NCYES    SR    RC,RC               RETURN 'YES'                                 
NCNO     LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE RETURNS 'YES' IF THE CHECK IS NOT TO BE PRINTED.                 
* OTHERWISE IT RETURNS 'NO'.                                                    
*                                                                               
         DS    0D                                                               
VNOPRNT  NMOD1 0,NOPRNT                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS PAYROLL ADJUSTMENT               
         BE    NP10                                                             
         TM    SRTADJS,TAPDADVD    AND ADJUSTMENT TYPE IS VOID                  
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADIS    OR ISSUE                                     
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADTT    OR TAX TRANSFER                              
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADST    OR SSN TRANSFER                              
         BO    NPYES               THEN RETURN 'YES'                            
*                                                                               
NP10     OC    SRTPAY,SRTPAY       ELSE IF PAYMENT AMOUNT IS ZERO               
         BNZ   NPNO                                                             
         OC    SRTREXP,SRTREXP     AND REIMBURSED EXPENSES IS ZERO              
         BNZ   NPNO                                                             
         OC    SRTMDED,SRTMDED     AND MISCELLANEOUS DEDUCTIONS IS ZERO         
         BNZ   NPNO                                                             
         OC    SRTDUES,SRTDUES     AND UNION DUES IS ZERO                       
         BNZ   NPNO                                                             
         OC    SRTLIEN,SRTLIEN     AND LIEN AMOUNT IS ZERO                      
         BNZ   NPNO                                                             
         OC    SRTTRST,SRTTRST     AND W4 TRUSTEE AMOUNT IS ZERO                
         BNZ   NPNO                                                             
         TM    SRTADJS,TAPDADTR    AND NOT TAX REFUND CHECK                     
         BO    NPNO                                                             
*                                                                               
         TM    TGUSSTA2,HLDTYPE    IF THIS IS A HOLDING FEE PAYMENT             
         BZ    NPYES                                                            
         CLC   SRTONOF,=C'OFF'     AND AN OFF-CAMERA PERF.                      
         BNE   NPYES                                                            
         CLC   SRTCFCY,SRTCYCS     AND CAST FFC EQUAL TO CYCLE START            
         BNE   NPYES                                                            
         B     NPNO                                                             
*                                                                               
NPYES    SR    RC,RC               RETURN 'YES'                                 
NPNO     LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* INITIALIZE TABLES AND OTHER PROGRAM CONTROL VARIABLES.                        
*                                                                               
         USING TASYD,R4            R4=A(SYSTEMS DETAILS ELEMENT)                
         DS    0D                                                               
VINITCTR NMOD1 0,INICTR                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',AIO),0,=C'SYSTEM'                             
         ZAP   CCVTRATE,TASYCCVT   SAVE CURRENT CANADIAN $ CONV RATE            
         MVC   BANKCODE,TASYUSBK   SAVE BANK ACCOUNT CODE                       
*                                                                               
         LA    R1,TASYUSST         R1=A(STARTING CHECK NUMBER)                  
         CLI   RECNUM,CN           CAN$ CHECKS USE SEPARATE STOCK               
         BNE   *+14                                                             
         MVC   BANKCODE,TASYCNBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYCNST                                                      
         CLI   RECNUM,DC           DMB&B CHECKS USE SEPARATE STOCK              
         BNE   *+14                                                             
         MVC   BANKCODE,TASYDMBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYDMST                                                      
         CLI   RECNUM,PC           PRINT CHECKS USE SEPARATE STOCK              
         BNE   *+14                                                             
         MVC   BANKCODE,TASYPCBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYPCST                                                      
*                                                                               
         MVC   CHKNBIN,=F'1'       DEFAULT BINARY CHECK NUMBER TO 1             
         OC    0(8,R1),0(R1)       IF CHECK NUMBER IS DEFINED                   
         BZ    IC5                                                              
         PACK  DUB,0(8,R1)         CONVERT CHECK NUMBER TO BINARY               
         CVB   RF,DUB                                                           
         ST    RF,CHKNBIN                                                       
IC5      ICM   RF,15,REQSCHK       IF OVERRIDE STARTING CHECK NUMBER            
         BZ    *+8                                                              
         ST    RF,CHKNBIN                                                       
*                                                                               
         L     RE,AERITAB          CLEAR ERROR INVOICE TABLE                    
         L     RF,=A(MAXERIS*ERILEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,AWARTAB          CLEAR WARNING CHECK TABLE                    
         L     RF,=A(MAXWARS*WARLEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,ALCLTAB          CLEAR UNION LOCAL SORT TABLE                 
         L     RF,=A(MAXLCLS*6+1)                                               
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVC   NEXTERI,AERITAB     SET NEXT ERITAB ENTRY TO FIRST               
         MVC   NEXTWAR,AWARTAB     SET NEXT WARTAB ENTRY TO FIRST               
*                                                                               
         MVC   CHKDATE1,TGNXTBUS   CONVERT CHECK DATE TO 6-BYTE CHAR            
         TM    REQOPTS,REQOCDTE    IF FORCING CHECK DATE                        
         BZ    IC10                                                             
         MVC   CHKDATE1,REQEND1     SET SAME AS THRU DTE                        
IC10     CLI   REQURG,C'U'                                                      
         BNE   *+10                                                             
         MVC   CHKDATE1,TGTODAY1                                                
         GOTO1 DATCON,DMCB,(1,CHKDATE1),(20,CHKDATEA)                           
*                                                                               
         GOTO1 DATCON,DMCB,(1,CHKDATE1),(0,DUB)                                 
         GOTO1 ADDAY,DMCB,(C'Y',DUB),DUB,-1                                     
         MVC   DUB+2(4),=C'1231'                                                
         GOTO1 DATCON,DMCB,(0,DUB),(20,CHKDTEAL)                                
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(1,CHKDATEL)  SAVE PWOS TOO             
*                                                                               
         XC    PREVEMP,PREVEMP     CLEAR PREVIOUS EMPLOYER OF RECORD            
         XC    PREVAGY,PREVAGY           PREVIOUS AGENCY CODE                   
         XC    PREVCLI,PREVCLI           PREVIOUS CLIENT CODE                   
*                                                                               
         MVC   PROCSEQ,=F'1'       INITIALIZE PROCESSED SEQ NUMBER TO 1         
*                                                                               
         TIME  DEC                 PUT BINARY HOURS/MINS IN HIGH ORDER          
         STCM  R0,12,HALF              TWO BYTES OF PROCSEQ                     
         ZAP   DUB,=P'0'                                                        
         MVO   DUB,HALF                                                         
         CVB   R1,DUB                                                           
         STH   R1,PROCSEQ                                                       
*                                                                               
         CLI   REQURG,C'U'         IF URGENT CHECK RUN THEN SET                 
         BNE   *+8                     URGENT BIT IN HIGH ORDER                 
         OI    PROCSEQ,TACDSQUR        BYTE OF PROCSEQ                          
*                                                                               
         MVI   TAPPTRAC,C'N'       INITIALIZE SPECIAL TRACE SWITCHES            
         MVI   YTDTRACE,C'N'                                                    
         MVI   PTRTRACE,C'N'                                                    
*                                                                               
         LA    RF,REQTRACE         LOOP THROUGH REQTRACE LOOKING FOR            
         LA    RE,L'REQTRACE           TAPPGUAR TRACE INDICATOR                 
*                                                                               
IC60     CLI   0(RF),C'T'          IF TRACE TAPPGUAR INDICATOR FOUND            
         BNE   *+8                                                              
         MVI   TAPPTRAC,C'Y'       SET FLAG TO TRACE TAPPGUAR CALLS             
*                                                                               
         CLI   0(RF),C'H'          IF TRACE YTD INDICATOR FOUND                 
         BNE   *+8                                                              
         MVI   YTDTRACE,C'Y'       SET FLAG TO TRACE TAYTD CALLS                
*                                                                               
         CLI   0(RF),C'O'          IF TRACE PTRS INDICATOR FOUND                
         BNE   *+8                                                              
         MVI   PTRTRACE,C'Y'       SET FLAG TO TRACE ADDPTR CALLS               
*                                                                               
         LA    RF,1(RF)            BUMP TO NEXT TRACE CODE                      
         BCT   RE,IC60             REPEAT UNTIL NO MORE TRACE CODES             
*                                                                               
         XC    TOTALS(TOTLEN),TOTALS  CLEAR ACCUMS FOR TOTALS PAGE              
         XC    TOTDIRCT,TOTDIRCT                                                
*                                                                               
         XC    WORK(10),WORK       EXTRACT CONTROL FILE INFO FOR                
         MVC   WORK+8(2),ORIGID        SIGN-ON ID INTO GLOBAL STORAGE           
         GOTO1 USERVAL,DMCB,(X'80',WORK)                                        
*                                                                               
         GOTO1 SETMOS,DMCB,CHKDATEA,MOS  SET MONTH OF SERVICE                   
         GOTO1 (RF),(R1),CHKDTEAL,MOSL   SET SAME FOR END OF LAST YEAR          
*                                                                               
         ZAP   TOTRECS,=P'0'       INITIALIZE POSTING ACCUMULATORS              
         ZAP   TOTCASH,=P'0'                                                    
VINITCX  XIT1                                                                   
         EJECT                                                                  
* ROUTINE SETS ACC MOS BASED ON CHECK DATE PASSED IN PARAMETER 1, WHICH         
* IS OF THE FORM CCYYMMDD.  MOS IS RETURNED IN FIELD AT PARAMETER 2.            
*                                                                               
SETMOS   NTR1                                                                   
         LM    R2,R3,0(R1)         R2=A(DATE), R3=A(MOS)                        
*                                                                               
         MVC   0(1,R3),3(R2)       SET MONTH OF SERVICE                         
         MVC   1(1,R3),5(R2)                                                    
*                                                                               
         CLI   4(R2),C'1'                                                       
         BNE   SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'A'                                                       
         CLI   5(R2),C'0'                                                       
         BE    SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'B'                                                       
         CLI   5(R2),C'1'                                                       
         BE    SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'C'                                                       
*                                                                               
SETMOSX  B     VINITCX                                                          
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO CALL TAPPGUAR                                         
         SPACE 1                                                                
         DS    0D                                                               
VCALLTAP NMOD1 0,CTAPP                                                          
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         TM    SRTINVS,TAINSBIL    IF INVOICE BILLED                            
         BO    CALLPYES                                                         
         CLI   SRTADJS,0           OR THIS IS ADJUSTMENT INVOICE                
         BNE   CALLPYES                                                         
         CLI   REQTAPP,C'Y'        OR OPTION TO SUPPRESS TAPPGUAR CALLS         
         BE    CALLPYES            THEN RETURN                                  
*                                                                               
         CLI   TAPPTRAC,C'Y'       IF TRACING TAPPGUAR CALLS                    
         BNE   *+8                                                              
         OI    4(R1),X'40'         THEN SET TRACE BIT IN PARAMETER 2            
*                                                                               
         L     R4,AMASTD           SAVE AND CLEAR PRTQUE REMOTE KEY             
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
         MVC   SVREMOT,REMOTKEY                                                 
         XC    REMOTKEY,REMOTKEY                                                
*                                                                               
         GOTO1 =V(TAPPGUAR)        CALL TAPPGUAR - PARMS ALREADY SET            
*                                                                               
         MVC   REMOTKEY,SVREMOT    RESTORE PRTQUE REMOTE KEY                    
CALLPX   XIT1                                                                   
*                                                                               
CALLPYES CR    RC,RC                                                            
         B     CALLPX                                                           
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE GETS THE CURRENT ENTRY FROM PYTDTAB FOR THIS PERFORMER           
* AND USES IT AS INPUT TO TALIM ALONG WITH THE VALUES FROM THIS CHECK           
* IN ORDER TO UPDATE PYTDTAB AND TO ADD A NEW YTD ELEMENT (TAYE) TO             
* THE CHECK.  IT ALSO SETS SEVERAL YTD SORT FIELDS.                             
*                                                                               
         DS    0D                                                               
VUPDPYTD NMOD1 0,UPPYTD                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE EXISTING YTD EARNINGS EL.             
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 DELL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVC   AIO,AIO1                                                         
         SPACE 1                                                                
         GOTO1 GETPYTD             GET ENTRY FOR THIS EMPLOYER/SSN              
         SPACE 1                                                                
         L     R5,DMCB                                                          
         USING PYTDTABD,R5         R5=A(PYTDTAB RECORD)                         
         SPACE 1                                                                
         LA    R4,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R4                                                          
         XC    TMD(TMLNQ),TMD      BUILD PARAMETER BLOCK FOR TALIM              
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TMCURR,C'C'         ELSE TMCURR = C                              
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVC   TMUNIT,SRTTXST      TAXABLE STATE                                
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    UPYTD10                                                          
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
UPYTD10  ST    RC,TMRC             A(GEND)                                      
         SPACE 1                                                                
         TM    SRTADJS,TAPDADST    IF SSN TRANSFER                              
         BZ    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYCO    AND PERFORMER IS NOT CORPORATION             
         BE    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYTR    OR TRUSTEE                                   
         BE    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYFO    OR FOREIGNER                                 
         BE    UPYTD20                                                          
         MVI   ELCODE,TACWELQ      LOOK FOR FEDERAL WITHHOLDING ELEM            
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,=C'FD '                                                
         MVC   AIO,AIO1                                                         
         BNE   UPYTD30             NOT FOUND - DON'T PASS EARNINGS              
         L     R3,TGELEM                                                        
         USING TACWD,R3                                                         
         TM    TACWSTAT,TACWSTAX   IF FEDERAL NOT TAXABLE                       
         BZ    UPYTD30             THEN NO EARNINGS TO UPDATE                   
         DROP  R3                                                               
         SPACE 1                                                                
UPYTD20  MVC   TMTXEARN,SRTEARN    PASS TAXABLE EARNINGS                        
         MVC   TMNTEARN,SRTNTAX         NON-TAXABLE EARNINGS                    
         SPACE 1                                                                
UPYTD30  MVC   TMYTD(TMYTDN*4),PYTDAMTS  PASS PREVIOUS YTD AMOUNTS              
         SPACE 1                                                                
         GOTO1 =V(TALIM),DMCB,TMD    OFF TO TALIM                               
         SPACE 1                                                                
         GOTO1 DOTRACE,DMCB,(C'H',TMD),TMLNQ,=C'TALIM BLK'                      
         SPACE 1                                                                
         LA    R3,ELEMENT          BUILD NEW YTD ELEMENT                        
         USING TAYED,R3                                                         
         XC    TAYEEL(TAYELNQ),TAYEEL                                           
         MVI   TAYEEL,TAYEELQ                                                   
         MVI   TAYELEN,TAYELNQ                                                  
         MVI   TAYETYPE,TAYETCHK            CHECK TYPE                          
         MVC   TAYENYTD(TMYTDN*4),TMNYTD    NEW YTD AMOUNTS                     
         MVC   TAYETXBL(TMTNAMT*4),TMTAXBLE TAXABLE AMOUNTS THIS CHECK          
         SPACE 1                                                                
         MVC   SRTYEARN,TAYEEARN   SAVE NEW YTD TAXABLE EARNINGS                
         MVC   SRTYNTAX,TAYENTAX                NON-TAXABLE EARNINGS            
         SPACE 1                                                                
         MVC   AIO,ASRTCHK         ADD NEW ELEMENT TO CHECK RECORD              
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
         SPACE 1                                                                
         LA    R5,WORK             R5=A(PYTDTAB ENTRY)                          
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5) BUILD PYTDTAB ENTRY                          
         MVC   PYTDEMP,SRTEMP      EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN      SOCIAL SECURITY NUMBER                       
         SPACE 1                                                                
         LA    RE,TMNYTD           RE=A(NEW YTD AMOUNTS)                        
         LA    RF,TMYTD            RF=A(OLD YTD AMOUNTS)                        
         LA    R2,PYTDAMTS         R2=A(YTD AMOUNTS THIS TIME)                  
         LA    R0,L'PYTDAMTS/4     R0=N'YTD AMOUNTS                             
         SPACE 1                                                                
UPYTD40  L     R1,0(RE)            NEW YTD                                      
         S     R1,0(RF)            LESS OLD YTD                                 
         ST    R1,0(R2)            IS YTD THIS TIME                             
         SPACE 1                                                                
         LA    RE,4(RE)            BUMP TO NEXTS                                
         LA    RF,4(RF)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,UPYTD40                                                       
         SPACE 1                                                                
         GOTO1 ADDBIN,DMCB,APYTDTAB,(R5)  ADD TO PYTDTAB ENTRY                  
         SPACE 1                                                                
         GOTO1 ADDBIN,DMCB,APYTDINV,(R5)  ADD TO PYTDINV ENTRY                  
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH THE ESUTAB, DUETAB, LINTAB, & TRSTABS AND          
* CLEARS OUT THE INVOICE TOTALS IF THE INVOICE WAS NOT IN ERROR.                
* OTHERWISE IT PUTS BACK THE INVOICE TOTALS INTO THE TABLE ENTRIES SO           
* AS TO UNDO THE EFFECT OF THE INVOICE ON THE TABLES.                           
*                                                                               
         DS    0D                                                               
VFLUSHI  NMOD1 0,FLUSHI                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R2,AESUINV          R2 = A(ESUINV BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO ESUINV ENTRIES THEN SKIP               
         BZ    FI40                                                             
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR THEN              
         BE    FI30                CLEAR INVOICE TABLE                          
*                                                                               
         LA    R5,BINTABLE         ELSE SET TO BACK OUT INV AMOUNTS             
         USING ESUTABD,R5          R5 = A(ESUINV)                               
         L     R4,BININ            R4 = NUMBER OF ESUINV ENTRIES                
*                                                                               
FI10     MVC   SRTEMP,ESUEMP       SET FIELDS FOR ESUTAB LOOKUP                 
         MVC   SRTSSN,ESUSSN                                                    
         GOTO1 GETESU,DMCB,ESUUNIT  GET CORRESPONDING ESUTAB ENTRY              
*                                                                               
         L     R3,DMCB             R3 = A(AMOUNTS IN ESUTAB ENTRY)              
         LA    R3,ESUAMNTS-ESUTABD(R3)                                          
*                                                                               
         LA    RE,ESUAMNTS         RE = A(AMOUNTS IN ESUINV ENTRY)              
         LA    R1,ESUAMNTL/4       R1 = NUMBER OF AMOUNTS                       
*                                                                               
FI20     L     RF,0(R3)            SUBTRACT INVOICE AMOUNT FROM AMOUNT          
         S     RF,0(RE)                FOR ENTIRE CHECK RUN                     
         ST    RF,0(R3)                                                         
*                                                                               
         LA    RE,4(RE)            BUMP TO NEXT AMONNTS                         
         LA    R3,4(R3)                                                         
         BCT   R1,FI20             REPEAT UNTIL NO MORE AMOUNTS                 
*                                                                               
         LA    R5,ESULEN(R5)       BUMP TO NEXT ESUINV ENTRY                    
         BCT   R4,FI10             REPEAT UNTIL NO MORE ENTRIES                 
*                                                                               
FI30     XC    BININ,BININ         CLEAR ESUINV TABLE                           
         L     RF,=AL4(MAXCAST*5*ESULEN)                                        
         XCEFL BINTABLE                                                         
*                                                                               
FI40     BAS   RE,FLUSHDUE         FLUSH DUE COMPANY TABLE                      
         BAS   RE,FLUSHLIN         FLUSH LIEN TABLE                             
         BAS   RE,FLUSHTRS         FLUSH TRUSTEE TABLE                          
*                                                                               
         L     R2,APYTDINV         R2 = A(PYTDINV BINSRCH PARAMETERS)           
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO PYTDINV ENTRIES THEN SKIP              
         BZ    FI200                                                            
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR THEN              
         BE    FI130               CLEAR INVOICE TABLE                          
*                                                                               
         LA    R5,BINTABLE         ELSE SET TO BACK OUT INV AMOUNTS             
         USING PYTDTABD,R5         R5 = A(PYTDINV)                              
         L     R4,BININ            R4 = NUMBER OF PYTDINV ENTRIES               
*                                                                               
FI110    MVC   SRTEMP,PYTDEMP      SET FIELDS FOR PYTDTAB LOOKUP                
         MVC   SRTSSN,PYTDSSN                                                   
         GOTO1 GETPYTD             GET CORRESPONDING PYTDTAB ENTRY              
*                                                                               
         L     R3,DMCB             R3 = A(AMOUNTS IN PYTDTAB ENTRY)             
         LA    R3,PYTDAMTS-PYTDTABD(R3)                                         
*                                                                               
         LA    RE,PYTDAMTS         RE = A(AMOUNTS IN PYTDINV ENTRY)             
         LA    R1,L'PYTDAMTS/4     R1 = NUMBER OF AMOUNTS                       
*                                                                               
FI120    L     RF,0(R3)            SUBTRACT INVOICE AMOUNT FROM AMOUNT          
         S     RF,0(RE)                FOR ENTIRE CHECK RUN                     
         ST    RF,0(R3)                                                         
*                                                                               
         LA    RE,4(RE)            BUMP TO NEXT AMONNTS                         
         LA    R3,4(R3)                                                         
         BCT   R1,FI120            REPEAT UNTIL NO MORE AMOUNTS                 
*                                                                               
         LA    R5,PYTDLEN(R5)      BUMP TO NEXT PYTDINV ENTRY                   
         BCT   R4,FI110            REPEAT UNTIL NO MORE ENTRIES                 
*                                                                               
FI130    XC    BININ,BININ         CLEAR PYTDINV TABLE                          
         LH    RF,=Y(MAXCAST*PYTDLEN)                                           
         XCEFL BINTABLE                                                         
*                                                                               
FI200    CLI   INVTERR,0           IF NO ERROR THIS INVOICE                     
         BNE   FI210               THEN CALL TAPPGUAR TO CLEAR INV TOTS         
         GOTO1 CALLTAPP,DMCB,(RC),(X'10',0),,SYSCOMM                            
         B     FIX                                                              
*                                  ELSE CALL TAPPGUAR TO SUBTR INV TOTS         
FI210    GOTO1 CALLTAPP,DMCB,(RC),(X'08',0),,SYSCOMM                            
FIX      XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*              ROUTINE TO FLUSH DUETAB                                          
         SPACE 1                                                                
FLUSHDUE NTR1                                                                   
         L     R2,ADUETAB          R2 = A(DUETAB BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO DUETAB ENTRIES THEN SKIP               
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(DUETAB)                               
         USING DUETABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF DUETAB ENTRIES                
*                                                                               
FDUE5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FDUE10                                                           
*                                                                               
         LA    R3,DUEELEM          THEN R3 = DUE COMPANY DETAILS ELEM           
         USING TADUD,R3                                                         
         L     RF,TADUCOL          SUBTRACT INVOICE AMOUNT FROM                 
         ICM   RE,15,DUEINV            COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         ST    RF,TADUCOL                                                       
*                                                                               
FDUE10   XC    DUEINV,DUEINV       CLEAR INVOICE AMOUNT                         
         LA    R5,DUELEN(R5)       BUMP TO NEXT DUETAB ENTRY                    
         BCT   R4,FDUE5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R3,R5                                                            
         EJECT                                                                  
*              ROUTINE TO FLUSH LINTAB                                          
         SPACE 1                                                                
FLUSHLIN NTR1                                                                   
         L     R2,ALINTAB          R2 = A(LINTAB BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO LINTAB ENTRIES THEN SKIP               
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(LINTAB)                               
         USING LINTABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF LINTAB ENTRIES                
*                                                                               
FLIN5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FLIN10                                                           
         LA    R3,LINELEM          THEN R3 = LIEN DETAILS ELEMENT               
         USING TALND,R3                                                         
         ICM   RF,15,TALNCOL       SUBTRACT INVOICE AMOUNT FROM                 
         ICM   RE,15,LININV            COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         STCM  RF,15,TALNCOL                                                    
*                                                                               
FLIN10   XC    LININV,LININV       CLEAR INVOICE AMOUNT                         
         LA    R5,LINLEN(R5)       BUMP TO NEXT LINTAB ENTRY                    
         BCT   R4,FLIN5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R3,R5                                                            
         SPACE 2                                                                
*              ROUTINE TO FLUSH TRUSTEE TABLE                                   
         SPACE 1                                                                
FLUSHTRS NTR1                                                                   
         L     R2,ATRSTAB          R2 = A(TRUSTEE BINSRCH PARAMETERS)           
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO TABLE ENTRIES THEN SKIP                
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(TRUSTEE TABLE)                        
         USING TRSTABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF ENTRIES                       
*                                                                               
FTRS5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FTRS10                                                           
         ICM   RF,15,TRSTCOL       SUBTRACT COLLECTED AMT FROM INV AMT          
         ICM   RE,15,TRSTINV       COLLECTED AMOUNT                             
         SR    RF,RE                                                            
         STCM  RF,15,TRSTCOL                                                    
*                                                                               
FTRS10   XC    TRSTINV,TRSTINV     CLEAR INVOICE AMOUNT                         
         LA    R5,TRSTLEN(R5)      BUMP TO NEXT ENTRY                           
         BCT   R4,FTRS5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R5                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE GETS THE CURRENT PERFORMER'S PYTDTAB ENTRY AND RETURNS           
* ITS ADDRESS IN DMCB.                                                          
*                                                                               
VGETPYTD NMOD1 0,PYTD                                                           
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         LA    R5,BLOCK            BUILD ENTRY FOR THIS EMPLOYER/SSN            
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5)                                              
         MVC   PYTDEMP,SRTEMP                                                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN                                                   
*                                                                               
         L     R2,APYTDTAB         R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),1             MUST BE FOUND                                
         BNE   *+6                                                              
         DC    H'0'                                                             
         XIT1                                                                   
         EJECT                                                                  
*        MISCELLANEOUS IO AREAS                                                 
*                                                                               
INVIO    DS    CL2000              INVOICE RECORD                               
EMPIO    DS    CL2000              EMPLOYER RECORD                              
W4IO     DS    CL2000              W4 RECORD                                    
YTDTAB   DS    CL(55*YTDLNQ)       TAYTD TABLE                                  
         EJECT                                                                  
       ++INCLUDE TACKREPD                                                       
         EJECT                                                                  
TMDD     DSECT                                                                  
       ++INCLUDE TALIMD                                                         
         EJECT                                                                  
*TAREPFFD                                                                       
*TAREPF4D                                                                       
*DDGENTWA                                                                       
*DDTWADCOND                                                                     
*DDREPMASTD                                                                     
*DDREMOTED                                                                      
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDBIGBOX                                                                       
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TAREPWORKD                                                                     
*TALDCPTRD                                                                      
*DDCOMFACS                                                                      
*DDGETRETD                                                                      
         PRINT OFF                                                              
       ++INCLUDE TAREPFFD                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE TAREPF4D                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDTWADCOND                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TALDCPTRD                                                      
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DDGETRETD                                                      
       ++INCLUDE TAREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'095TACKREPS  05/01/02'                                      
         END                                                                    
