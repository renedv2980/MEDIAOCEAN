*          DATA SET REGETKSRCS AT LEVEL 040 AS OF 06/23/04                      
*CATALP GETKSRC                                                                 
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF REINVKSRC FIELD'                
*********************************************************************           
* HISTORY OF CHANGES:                                               *           
* JUN09/04 (BU ) --- ADD 'REGULAR LPM' CODE                         *           
*                                                                   *           
*                                                                   *           
*********************************************************************           
* INPUT  PARM1:  0 - C'Q' = IBLK MUST PASS: RTGSVC, QLFYR & BKTYPE              
*                    C'B' = IBLK MUST PASS: BKVAL BITS & BKTYPE                 
*                    C'K' = IBLK MUST PASS: KSRC                                
*                                                                               
*              1-3 - A(IBLOCK)                                                  
*                                                                               
*        PARM2:    - A(OUTPUT BLOCK)                                            
*                                                                               
* ON EXIT:                                                                      
* THE OUTPUT BLOCK WILL BE FILLED I WITH THE FIELDS FROM THE TABLE              
* WARNING: IF THE SEARCH IS DONE BASED ON KSRC ONLY, THE BKVAL BITS             
*   RETURNED WILL BE FOR THE NON-SURPRESSED CPM                                 
*        DMCB+4(1) = ERROR CODE  (IF AN ERROR OCCURED, OBLK='FFFFFF')           
*                    00= NO ERRORS                                              
*                    01= NO MATCH ON INPUT PARMS                                
*                    02= UNDEFINED BKTYPE                                       
*                                                                               
*FORMAT OF INPUT/OUTPUT BLOCK:                                                  
*IPARMS   DS    0CL5                INPUT                                       
*IRSVC    DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX                
*IQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX                   
*IKSRC    DS    CL1       2         RINVKSRC FOR KEY                            
*IBITS    DS    XL1       3         BOOKVAL BITS                                
*IBKTYP   DS    CL1       4         BOOKTYPE                                    
*                                                                               
*********************************************************************           
                                                                                
                                                                                
*                                                                               
GETKSRC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,GETKSRC,RR=RE,CLEAR=YES                              
         USING WORKD,RC            RC=A(W/S)                                    
         ST    RE,RELO                                                          
         ST    R1,APARMS           A(PARAM) LIST                                
         SR    R9,R9                                                            
         ICM   R9,7,1(R1)          P1 = A(INPUT BLOCK) ->> R9                   
         BNZ   *+6                                                              
         DC    H'0'                MISSING A(OUTPUT BLK)  (PARM2)               
         OC    4(4,R1),4(R1)       P2 = A(OUTPUT BLOCK)                         
         BNZ   *+6                                                              
         DC    H'0'                MISSING A(OUTPUT BLK)  (PARM2)               
         MVC   IPARMS,0(R9)                                                     
         MVC   MODE,0(R1)                                                       
         CLI   MODE,MQLFY          VALID MODES                                  
         BE    GETK10                                                           
         CLI   MODE,MBKVL                                                       
         BE    GETK10                                                           
         CLI   MODE,MKSRC                                                       
         BE    GETK5                                                            
         DC    H'0'                INV PARM1,BYTE 0 (Q/B/K ONLY)                
         EJECT                                                                  
*                                                                               
GETK5    LA    RF,KSRCTBL          GIVEN KSRC, EXTRACT BKTYPE                   
         ST    RF,ATABLE                                                        
         TM    IKSRC,B'11000000'   NON-BKTYPE INVKSRC?                          
         BO    GETK20              YES                                          
         MVC   BYTE,IKSRC                                                       
         NI    BYTE,BKTBITS        STRIP SRC/QLF -> LEAVE BKTYPE BITS           
*                                                                               
GETK10   LA    RE,BKTBL            GIVEN A BKTYPE-> GET A(BK-SRCTBL)            
         MVI   ERRCODE,ERUNDEF                                                  
GETK11   CLI   0(RE),X'FF'                                                      
         BE    GETKX               UNDEFINED BK-TYPE                            
         CLI   MODE,MKSRC                                                       
         BNE   *+18                                                             
         CLC   BYTE,1(RE)          MATCH ON BITS IF GIVEN KSRC                  
         BE    GETK18                                                           
         B     GETK15              TRY NEXT TBL ENTRY                           
         CLC   IBKTYP,0(RE)        MATCH ON CHAR BK TYPE                        
         BE    GETK18                                                           
GETK15   LA    RE,L'BKTBL(RE)      NEXT ENTRY                                   
         B     GETK11              UNDEFINED BKTYPE                             
GETK18   ICM   RF,7,2(RE)                                                       
         LA    RF,GETKSRC(RF)                                                   
         ST    RF,ATABLE                                                        
         MVC   IBKTYP,0(RE)        IF KSCR BITS GIVEN, NEED IBKTYP              
*                                                                               
GETK20   L     RE,ATABLE           LK THRU TBL ENTRIES FOR MATCH                
         USING KSRCTBLD,RE                                                      
         MVI   ERRCODE,ERNTFND                                                  
GETK22   CLI   0(RE),X'FF'         END OF TABLE --> ENTRY NOT FOUND             
         BE    GETKX                                                            
         CLC   IBKTYP,4(RE)        IF KSCR BITS GIVEN, NEED IBKTYP              
         BNE   GETKX               END OF BKTYPE SECTION -- NOT FOUND           
         CLI   MODE,MQLFY          COMPARE ON SRC & QLFYR                       
         BNE   GETK25                                                           
         CLC   IRSVC,KSRCRSVC                                                   
         BNE   GETK40                                                           
         CLC   IQLF,KSRCQLF                                                     
         BE    GETK50                                                           
*                                                                               
GETK25   CLI   MODE,MBKVL          COMPARE ON BKBITS                            
         BNE   *+14                                                             
         CLC   IBITS,KSRCBITS                                                   
         BE    GETK50                                                           
*                                                                               
         CLI   MODE,MKSRC          COMPARE ON KSRC                              
         BNE   GETK40                                                           
         CLC   IKSRC,KSRC                                                       
         BE    GETK50                                                           
*                                                                               
GETK40   LA    RE,L'KSRCTBL(RE)    NEXT ENTRY IN TABLE                          
         B     GETK22                                                           
*                                                                               
GETK50   MVI   ERRCODE,0           FOUND                                        
         LA    R9,IPARMS                                                        
         MVC   0(KSRCNTYL,R9),KSRCNTY                                           
*        CLI   MODE,MBKVL          WAS BKBITS PASSED                            
*        BE    *+8                                                              
*        MVI   KSRCBITS-KSRCTBLD(R9),0     DON'T PASS EM BACK->NOT SURE         
         B     GETKX                                                            
*                                                                               
GETKX    DS    0H                                                               
         CLI   ERRCODE,0           NO ERRORS                                    
         BE    *+10                                                             
         MVC   IPARMS,=X'FFFFFFFFFF'                                            
         L     R1,APARMS           INPUT PARAM LIST                             
         SR    R9,R9                                                            
         ICM   R9,7,5(R1)          A(OUTPUT BLOCK)                              
         MVC   0(KSRCNTYL,R9),IPARMS   OUTPUT                                   
         MVC   4(1,R1),ERRCODE                                                  
         XIT1                                                                   
*                                                                               
         DROP  RE                                                               
         EJECT                                                                  
*********************************************************************           
* KSRC FOR BOOK TYPES ARE DEFINED AS FOLLOWS:                                   
*  BITS 0-1 = RTG SRVC : '00'=MFX,  '01'=NSI  '10'=SRC '11'=RESVD               
*  BITS 2-4 = TRK QLFYR: '001'=ACTL '010'=PRJ '011'=TP '100'=SPCL               
*                        '101'=ESTM  (000/110/111 -NOT USED)                    
*  BITS 5-7 = BOOK TYPE: '001'=BLK '010'=HISP '011'=METRO                       
*                        '100'=TRD.AREA '101'=OLYM & OTHER                      
*                        '110'=EXTENDED '111'=REG LPM                           
*                        (000 - NOT USED)                                       
* NOTE: BITS 0-1 CANNOT BOTH BE ON ('11') FOR TRACKS W/BKTYPES                  
*      KSRC CANNOT EQUAL: X'00',X'FF', X'D4', X'E2'  (THESE ARE RESVD)          
*********************************************************************           
*                                                                               
BKTBL    DS    0XL5                                                             
         DC    X'00',X'00',AL3(KSRCTBL-GETKSRC)      BKTYP=00                   
         DC    C'B',B'00000001',AL3(BKSRCT-GETKSRC)  BLACK                      
         DC    C'H',B'00000010',AL3(HKSRCT-GETKSRC)  HISPANIC                   
         DC    C'M',B'00000011',AL3(MKSRCT-GETKSRC)  METRO                      
         DC    C'T',B'00000100',AL3(TKSRCT-GETKSRC)  TRADING AREA               
         DC    C'O',B'00000101',AL3(OKSRCT-GETKSRC)  OLYM & OTHER               
         DC    C'E',B'00000110',AL3(EKSRCT-GETKSRC)  EXTENDED                   
         DC    C'P',B'00000111',AL3(PKSRCT-GETKSRC)  REG LPM                    
         DC    X'FFFF'                                                          
*                                                                               
BANNER   DC    C'BILLUHR: KSRC: ORIG+REG LPM'                                   
         DC    H'0'                                                             
                                                                                
       ++INCLUDE RESVCTABN                                                      
                                                                                
         EJECT                                                                  
***********************************************************************         
* STORAGE, LTORG, DSECTS                                                        
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                     ** REDEMLK GLOBAL W/S **                     
DUB      DS    D                                                                
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
RELO     DS    A                                                                
BYTE     DS    X                                                                
*                                                                               
MODE     DS    XL1                                                              
MQLFY    EQU   C'Q'                RATING SRVC/ QLFYR/ BOOKTYP                  
MBKVL    EQU   C'B'                BKVAL BITS / BOOKTYPE                        
MKSRC    EQU   C'K'                KSRC INPUT                                   
*                                                                               
APARMS   DS    A                                                                
ATABLE   DS    A                                                                
IPARMS   DS    0CL5                INPUT                                        
IRSVC    DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX                 
IQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX                    
IKSRC    DS    CL1       2         RINVKSRC FOR KEY                             
IBITS    DS    XL1       3         BOOKVAL BITS                                 
IBKTYP   DS    CL1       4         BOOKTYPE                                     
ERRCODE  DS    XL1                                                              
ERNTFND  EQU   1                   NOT FOUND                                    
ERUNDEF  EQU   2                   UNDEFINED BK TYPE                            
*                                                                               
WORKX    EQU   *                                                                
KSRCTBLD DSECT          BYTE       KSRCTBL DSECT                                
KSRCNTY  DS    0C                                                               
KSRCRSVC DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX                 
KSRCQLF  DS    CL1       1         TRACK QUALIFIER/BK PREFIX                    
KSRC     DS    CL1       2         RINVKSRC FOR KEY                             
KSRCBITS DS    XL1       3         BOOKVAL BITS                                 
KSRCBTYP DS    CL1       4         BOOKTYPE                                     
KSRCNTYL EQU   *-KSRCNTY                                                        
*                                                                               
*REGENINVA                                                                      
         PRINT OFF                                                              
       ++INCLUDE REGENINVA                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'040REGETKSRCS06/23/04'                                      
         END                                                                    
