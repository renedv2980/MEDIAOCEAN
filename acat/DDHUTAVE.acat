*          DATA SET DDHUTAVE   AT LEVEL 008 AS OF 05/01/02                      
*CATALP HUTAVE                                                                  
         TITLE 'HUTAVE - HANDLES HUT AVERAGING FOR NETPAK'                      
HUTAVE   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 200,**HTAV**                                                     
         LR    R9,R1                                                            
         USING HUTD,RC                                                          
         SPACE 3                                                                
*        PARAMETERS                P1   M    A(2-BYTE MILITARY TIME)            
*        ----------                     Q    A(1-BYTE 1/4 HR.)                  
*                                  P2   R    A(1-BYTE DAY CODE 0-9)             
*                                       S    A(1-BYTE DAY CODE (SPOT))          
*                                  P3        A(BOOKS)                           
*                                  P4        A(DATAMGR)                         
*                                  P5        A(2-BYTE OUTPUT AREA)              
         SPACE 1                                                                
         MVC   DATAMGR,12(R9)                                                   
         L     R2,8(R9)            SET UP HUT WEEK RANGE                        
         ZIC   R0,2(R2)            START WEEK                                   
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         LA    R0,1                ENSURE 1-48                                  
         CH    R0,=H'48'                                                        
         BL    *+8                                                              
         LA    R0,48                                                            
         SPACE 1                                                                
         ZIC   R1,5(R2)            END WEEK                                     
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         LA    R1,1                                                             
         CH    R1,=H'48'                                                        
         BL    *+8                                                              
         LA    R1,48                                                            
         CR    R1,R0               ENSURE END NOT BEFORE START                  
         BH    *+8                                                              
         LA    R1,48                                                            
         LA    R1,1(R1)            WORK OUT N'WEEKS                             
         SR    R1,R0                                                            
         ST    R1,HUTNWKS                                                       
         ST    R0,HUTSTWK                                                       
         EJECT                                                                  
*              SET UP PRELIMINARY HUT KEY                                       
         SPACE 3                                                                
         LA    R4,KEY              BUILD KEY FOR HUT RECORD                     
         USING CTHREC,R4                                                        
         XC    CTHKEY,CTHKEY                                                    
         MVI   CTHTYP,C'H'                                                      
         SPACE 1                                                                
         LM    R2,R3,0(R9)         DAY CODE                                     
         MVC   CTHDAY,0(R3)                                                     
         CLI   4(R9),C'S'          EITHER SPOT TYPE PASSED                      
         BE    HA2                 OR CONVERT TO SPOT TYPE                      
         ZIC   R1,0(R3)                                                         
         LA    R1,DAYLIST(R1)                                                   
         MVC   CTHDAY,0(R1)                                                     
         SPACE 1                                                                
HA2      MVC   CTHTIME,0(R2)       TIME CODE - EITHER MILITARY                  
         CLI   0(R9),C'Q'                                                       
         BNE   HA4                                                              
         ZIC   R1,0(R2)            OR CONVERT 1/4 HR. TO MILITARY               
         SR    R0,R0                                                            
         D     R0,=F'4'            (HRS IN R1, 1/4'S IN R0)                     
         LA    R1,6(R1)                                                         
         MH    R1,=H'100'                                                       
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,30(R1)                                                        
         CH    R1,=H'2430'         12.30AM = 0030 ETC                           
         BL    *+8                                                              
         SH    R1,=H'2400'                                                      
         STH   R1,DUB                                                           
         MVC   CTHTIME,DUB                                                      
         SPACE 1                                                                
HA4      LA    R2,HUTYEARS         BUILD A HUT YEAR LIST                        
         XC    HUTYEARS,HUTYEARS                                                
         LA    R3,5                MAX 5                                        
         L     R4,8(R9)            A(BOOKS)                                     
         ZIC   R1,1(R4)            STARTING YEAR                                
         ZIC   R0,4(R4)            END YEAR                                     
         CR    R0,R1                                                            
         BH    *+6                                                              
         LR    R0,R1                                                            
         SPACE 1                                                                
HA6      STC   R1,0(R2)                                                         
         CR    R1,R0                                                            
         BE    HA8                                                              
         LA    R1,1(R1)                                                         
         LA    R2,1(R2)                                                         
         BCT   R3,HA6                                                           
         EJECT                                                                  
*              HANDLE THE IO                                                    
         SPACE 3                                                                
HA8      LA    R2,HUTYEARS                                                      
         LA    R4,KEY                                                           
         XC    TOTHUT,TOTHUT                                                    
         XC    HUTHITS,HUTHITS                                                  
         MVC   KEYSAVE,KEY                                                      
         SPACE 1                                                                
HA10     MVC   KEY,KEYSAVE         TRY FOR EACH SPECIFIED YEAR                  
         MVC   CTHYEAR,0(R2)                                                    
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BNE   HA12                                                             
         BAS   RE,DIGHUT                                                        
         SPACE 1                                                                
HA12     LA    R2,1(R2)                                                         
         CLI   0(R2),0                                                          
         BNE   HA10                                                             
         OC    HUTHITS,HUTHITS                                                  
         BNZ   HA14                                                             
         MVC   KEY,KEYSAVE                                                      
         ZIC   R1,HUTYEARS                                                      
         BCTR  R1,R0                                                            
         STC   R1,CTHYEAR                                                       
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BNE   HA16                                                             
         BAS   RE,DIGHUT                                                        
         SPACE 1                                                                
HA14     L     R0,TOTHUT           AVERAGE HUT                                  
         SRDA  R0,31                                                            
         D     R0,HUTHITS                                                       
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         ST    R1,TOTHUT                                                        
         SPACE 1                                                                
HA16     L     R1,16(R9)                                                        
         MVC   0(2,R1),TOTHUT+2                                                 
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO EXTRACT HUT INFO                                      
         SPACE 3                                                                
DIGHUT   NTR1                                                                   
         L     R1,HUTHITS          BUMP HITS                                    
         LA    R1,1(R1)                                                         
         ST    R1,HUTHITS                                                       
         SPACE 1                                                                
         LA    R6,IO               ADDRESS HUT ELEMENT                          
         MVI   ELCODE,X'90'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING CTHUTD,R6                                                        
         L     R2,HUTSTWK                                                       
         L     R3,HUTNWKS                                                       
         SR    R1,R1                                                            
         SPACE 1                                                                
DIGHUT2  LR    R5,R2               PRESENT WEEK NUMBER                          
         BCTR  R5,0                                                             
         SLL   R5,1                                                             
         LA    R5,CTHUTLST(R5)                                                  
         AH    R1,0(R5)                                                         
         LA    R2,1(R2)                                                         
         BCT   R3,DIGHUT2                                                       
         SPACE 1                                                                
         SR    R0,R0               AVERAGE PER WEEK                             
         SLDA  R0,1                                                             
         D     R0,HUTNWKS                                                       
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         A     R1,TOTHUT           ADD INTO TOTAL                               
         ST    R1,TOTHUT                                                        
         B     XIT                                                              
         EJECT                                                                  
*              DATAMGR SUPPORT  ETC.,                                           
         SPACE 2                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',KEY,IO,0                      
         MVC   KEY,IO                                                           
         B     XIT                                                              
         SPACE 1                                                                
         GETEL R6,28,ELCODE                                                     
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
DAYLIST  DC    X'7C402010080402017F'                                            
         SPACE 3                                                                
*              DSECT FOR MODULE                                                 
         SPACE 2                                                                
HUTD     DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
TOTHUT   DS    F                                                                
HUTHITS  DS    F                                                                
HUTYEARS DS    CL6                                                              
HUTSTWK  DS    F                                                                
HUTNWKS  DS    F                                                                
ELCODE   DS    CL1                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
IO       DS    1000C                                                            
         SPACE 2                                                                
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDHUTAVE  05/01/02'                                      
         END                                                                    
