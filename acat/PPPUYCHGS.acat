*          DATA SET PPPUYCHGS  AT LEVEL 023 AS OF 12/20/95                      
*CATALP PPBUYCHG                                                                
         TITLE 'PPBUYCHG - LIST CHANGES TO PRINT BUY RECORD'                    
*                                                                               
*   PARAMETER LIST                                                              
*   INPUT    A(PBUYREC)                                                         
*            A(CONTROL DATE)    BINARY 4(R1) HAS CHANGE FILTER                  
*                                 NON-ZERO  MEANS EFFECTIVE CHGS ONLY           
*                                           CHG1 X'FC' CHG2 X'06'               
*                                      C'L' MEANS INCLUDE MADE LIVE             
*                                           (CHG3 X'04') WITH EFFECTIVE         
*                                           CHANGES VALUES                      
*                                                                               
*                                      C'A' MEANS INCLUDE AD CHANGES            
*                                           WITH VALUES FOR C'E'                
*                                           CHG1 X'FC' CHG2 X'0E'               
*                                      X'00' MEANS ALL CHANGES                  
*            8(R1)    IF NON ZERO - LIST CHANGES                                
*            A(OUTPUT LIST)                                                     
*            A(DATCON)                                                          
*                                                                               
*                                                                               
*   OUTPUT                                                                      
*            4(R1)    HAS CHANGE TYPE B=NEW, C=CHANGE, D=DELETE                 
*            5(3,R1)  HAS LAST CHANGE DATE                                      
*            9(3,R1)  HAS A(CHANGES) (IF 8(R1) WAS NON ZERO ON INPUT)           
*            8(R1)    HAS NUMBER OF OUTPUT LINES                                
*                                                                               
         EJECT                                                                  
PPBUYCHG CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 7,PPBUYCHG                                                       
         SPACE 2                                                                
         USING CHGWRKD,RC                                                       
         L     RA,0(R1)            A(BUY REC)                                   
         USING PBUYREC,RA                                                       
         L     RF,4(R1)                                                         
         MVC   CTLDAT,0(RF)        A(CTL DATE)                                  
*                                  CHANGE DATE IS SET OVER CONTROL DATE         
*                                  CHG TYP (N,C,D) IN PARAM2 BYTE 0             
         MVC   CHGFILT,4(R1)       CHANGE FILTER                                
*                                  PARAM3 = A(OUTPUT AREA) OR 0                 
         MVC   VDATCON,12(R1)      PARAM4 = A(DATCON)                           
         ST    R1,SAVR1                                                         
         GOTO1 VDATCON,DMCB,(3,CTLDAT),(2,CTLDATP)                              
         L     R1,SAVR1                                                         
*                                                                               
*                                                                               
         XC    CHG1(6),CHG1                                                     
         MVI   CHGTYP,0                                                         
*                                                                               
         TM    PBUYCNTL,X'80'                                                   
         BZ    PC8                                                              
*                                                                               
         CLC   PBDBUYDT,CTLDAT                                                  
         BH    PCX                 ADDED AND DELETED SINCE CLT DATE             
         CLC   PBDDATE,CTLDAT                                                   
         BNH   PCX                                                              
         MVI   CHGFILT,0                                                        
         MVC   CHGDAT,PBDDATE      SET DELETED DATE AS CHGDAT                   
*                                                                               
PC8      DS    0H                                                               
         CLC   PBDBUYDT,CTLDAT                                                  
         BNH   PC9                                                              
         MVC   CHGDAT,PBDBUYDT                                                  
         MVI   CHGTYP,C'B'                                                      
         B     PC60                                                             
*                                                                               
PC9      DS    0H                                                               
***      CODE NO-OPED 9/13/89 NOW GET CHANGES FROM PCHGELEMS                    
***                                                                             
***      LA    RF,PBDDATE          USE OVERALL CHG DATE                         
***      CLI   CHGFILT,0                                                        
***      BE    *+8                                                              
***      LA    RF,PBDCHGDT         OR SELECTIVE CHG DATE                        
***      CLC   0(3,RF),CTLDAT                                                   
***      BNH   PC9B                                                             
***      MVC   CHG1,PBDDTIND                                                    
***      MVC   CHG2,PBDDTIN2                                                    
***      MVC   CHG3,PBDDTIN3                                                    
***      MVC   CHGDAT,0(RF)                                                     
*                                                                               
*                                                                               
PC9B     LA    R2,PBUYREC+33                                                    
PC10     DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    PC30                                                             
         CLI   0(R2),X'24'                                                      
         BNE   PC10                                                             
*                                                                               
         USING PCHGELEM,R2                                                      
         CLC   PCHGDAT,CTLDATP                                                  
         BNH   PC10                                                             
         CLI   CHGFILT,0                                                        
         BE    PC12                                                             
         TM    PCHGIND1,X'FC'      ONLY SELECTIVE CHANGES                       
         BNZ   PC12                                                             
         TM    PCHGIND2,X'06'                                                   
         BNZ   PC12                                                             
         CLI   CHGFILT,C'L'        SEE IF INCLUDING MAKE LIVES                  
         BNE   PC11                                                             
         TM    PCHGIND3,X'04'      SEE IF MADE LIVE                             
         BNZ   PC12                                                             
*                                                                               
PC11     CLI   CHGFILT,C'A'        SEE IF INCLUDING AD CHANGES                  
         BNE   PC10                                                             
         TM    PCHGIND2,X'08'       AD CODE CHG                                 
         BNZ   PC12                                                             
         TM    PCHGIND3,X'40'       AD CODE ADDED                               
         BNZ   PC12                                                             
         B     PC10                                                             
*                                                                               
PC12     DS    0H                                                               
         OC    CHG1(3),PCHGIND1                                                 
         GOTO1 VDATCON,DMCB,(2,PCHGDAT),(3,CHGDAT)                              
         B     PC10                                                             
*                                                                               
PC30     DS    0H                                                               
         L     R1,SAVR1                                                         
*                                                                               
         L     R9,8(R1)                                                         
         LTR   R9,R9               TEST LISTING CHANGES                         
         BZ    PC60                NO                                           
*                                                                               
         MVI   12(R1),0                                                         
         MVI   0(R9),C' '                                                       
         MVC   1(249,R9),0(R9)                                                  
*                                                                               
         OC    CHGDAT,CHGDAT       NO CHANGES                                   
         BZ    PC60                                                             
*                                                                               
         MVC   0(13,R9),=C'DATA CHANGED-'                                       
         LA    R8,1                COUNT LINES                                  
         LA    R5,12(R9)                                                        
         LA    R6,40(R9)                                                        
         SR    R2,R2                                                            
         ICM   R2,7,CHG1                                                        
         SR    R3,R3                                                            
         LA    R7,CHGLIST                                                       
*                                                                               
PC31     DS    0H                                                               
         SRDL  R2,1                                                             
         LTR   R3,R3                                                            
         BNM   PC35                                                             
*                                                                               
         CLI   CHGFILT,0           TEST SHOWING ALL CHANGES                     
         BE    PC32                YES                                          
         CLI   0(R7),0             TEST 'EFFECTIVE' CHANGE                      
         BE    PC36                NO                                           
PC32     DS    0H                                                               
         CLI   1(R7),0                                                          
         BE    PC36                                                             
*                                                                               
         ZIC   RF,1(R7)                                                         
         LA    RE,2(R5,RF)         TEST WILL FIT ON THIS LINE                   
         CR    RE,R6                                                            
         BNH   PC33                YES                                          
         LA    R6,40(R6)           NEXT LINE                                    
         LR    R5,R6                                                            
         SH    R5,=H'28'                                                        
         LA    R8,1(R8)            COUNT LINES                                  
*                                                                               
PC33     DS    0H                                                               
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   2(0,R5),2(R7)                                                    
         LA    R5,3(R5,RF)                                                      
         MVI   0(R5),C','                                                       
*                                                                               
PC35     DS    0H                                                               
         LTR   R2,R2                                                            
         BZ    PC40                                                             
PC36     DS    0H                                                               
         ZIC   RF,1(R7)                                                         
         LA    R7,2(R7,RF)                                                      
         B     PC31                                                             
*                                                                               
PC40     DS    0H                                                               
         MVI   0(R5),C' '                                                       
         LA    R0,12(R9)                                                        
         CR    R5,R0               IF NO CHANGES LISTED                         
         BNE   *+10                                                             
         MVC   0(13,R9),20(R9)     CLEAR FIRST LINE                             
         L     R1,SAVR1                                                         
         STC   R8,8(R1)           LINE COUNT                                    
*                                                                               
PC50     DS    0H                                                               
         MVI   CHGTYP,C'C'                                                      
         TM    PBUYCNTL,X'80'                                                   
         BZ    *+8                                                              
         MVI   CHGTYP,C'D'                                                      
*                                                                               
PC60     DS    0H                                                               
PCX      DS    0H                                                               
         L     R1,SAVR1                                                         
         L     RF,4(R1)                                                         
         MVC   0(3,RF),CHGDAT                                                   
         MVC   4(1,R1),CHGTYP                                                   
*                                                                               
         XIT1                                                                   
*                                                                               
         SPACE 3                                                                
*                                                                               
CHGLIST  DS    0C                                                               
*                                                                               
         DC    X'00',AL1(17),C'POSITION INSTRTNS'                               
         DC    X'00',AL1(15),C'MAT. CLOSE DATE'                                 
         DC    X'01',AL1(09),C'MADE LIVE'                                       
         DC    X'01',AL1(03),C'TAX'                                             
         DC    X'00',AL1(12),C'PLANNED COST'                                    
         DC    X'00',AL1(11),C'SPECIAL REP'                                     
         DC    X'01',AL1(12),C'AD NO. ADDED'                                    
         DC    X'00',AL1(12),C'2ND INS DATE'                                    
*                                                                               
         DC    X'00',AL1(15),C'INS. ORDER DATE'                                 
         DC    X'01',AL1(09),C'CASH DISC'                                       
         DC    X'01',AL1(10),C'COMMISSION'                                      
         DC    X'01',AL1(06),C'AD NO.'                                          
         DC    X'00',AL1(09),C'PYBL DATE'                                       
         DC    X'00',AL1(13),C'BILLABLE DATE'                                   
         DC    X'00',AL1(09),C'SALE DATE'                                       
         DC    X'00',AL1(10),C'CLOSE DATE'                                      
         DC    X'00',AL1(15),C'INS. ORDER COMM'                                 
         DC    X'00',AL1(07),C'COMMENT'                                         
         DC    X'01',AL1(04),C'PREM'                                            
         DC    X'01',AL1(14),C'INSERTION DATE'                                  
         DC    X'01',AL1(05),C'SPACE'                                           
         DC    X'01',AL1(05),C'UNITS'                                           
         DC    X'01',AL1(04),C'RATE'                                            
         DC    X'01',AL1(10),C'POOL ALLOC'                                      
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
CHGWRKD  DSECT                                                                  
CTLDAT   DS    XL3           CONTROL DATE BINARY                                
CTLDATP  DS    XL2           CONTROL DATE PACKED                                
CHGFILT  DS    X                                                                
CHGTYP   DS    C                                                                
CHG1     DS    XL1                                                              
CHG2     DS    XL1                                                              
CHG3     DS    XL1                                                              
CHGDAT   DS    XL3                                                              
SAVR1    DS    F                                                                
VDATCON  DS    F                                                                
DMCB     DS    6F                                                               
*                                                                               
         SPACE 3                                                                
*                                                                               
       ++INCLUDE PBUYREC                                                        
*                                                                               
       ++INCLUDE PBDELEM                                                        
*                                                                               
       ++INCLUDE PCHGELEM                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023PPPUYCHGS 12/20/95'                                      
         END                                                                    
