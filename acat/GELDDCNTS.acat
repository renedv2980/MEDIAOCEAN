*          DATA SET GELDDCNTS  AT LEVEL 004 AS OF 03/03/94                      
*CATALP GELDDCNT                                                                
         TITLE 'LDCOUNT - GENFIL/GENDIR - DIR REC TYPE COUNT/PRINT'             
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'81'=COUNT DELETED PASSIVE            
*                           X'FF'=PRINT                                         
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 WRKX-WRKD,GELDDCNT                                               
         USING WRKD,RC                                                          
*                                                                               
         MVC   BYTE,0(R1)                                                       
         CLI   BYTE,X'FF'          TEST PRINT ACTION                            
         BE    LDCOUNTP                                                         
         CLI   BYTE,X'81'                                                       
         BE    LDCOUNTX                                                         
         L     R2,0(R1)            A(RECORD)                                    
*                                                                               
         LA    R3,KEYTAB                                                        
*                                                                               
LDCOUNT1 SR    RE,RE                                                            
         SR    R1,R1                                                            
         IC    RE,0(R3)            GET INDEX FOR COUNT TABLE                    
         ICM   R1,1,1(R3)          GET LEN FOR COMPARE                          
         BZ    LDCOUNT4            ZERO LEN IS END OF TABLE                     
         BCTR  R1,0                                                             
         EX    R1,*+8              TEST KEY                                     
         B     *+10                                                             
         CLC   0(0,R2),2(R3)                                                    
         BE    LDCOUNT2                                                         
         LA    R3,3(R1,R3)         NOT MATCHED SO TRY NEXT KEY                  
         B     LDCOUNT1                                                         
*                                                                               
LDCOUNT2 CLI   2(R2),GMKRECQ              IF MESSAGE RECORD,                    
         BNE   LDCOUNT4                                                         
         CLI   GMKTYP-GMSGD(R2),GMKTGDIC   SEPARATE NEW-STYLE DATA              
         BNE   LDCOUNT4                    DICTIONARY FROM OTHERS               
         LA    RE,19               INDEX FOR COUNT TABLE, DATA-DICT             
*                                                                               
LDCOUNT4 SLL   RE,2                LOCATE REAL ADDRESS IN COUNT TABLE           
         L     RE,ADRTAB(RE)                                                    
         B     LDCOUNT8                                                         
*                                                                               
LDCOUNT8 TM    32(R2),X'80'        TEST RECORD MARKED FOR DELETION              
         BZ    *+8                                                              
         LA    RE,4(RE)                                                         
         ICM   RF,15,0(RE)                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,15,0(RE)                                                      
         LA    RE,FTS                                                           
         TM    32(R2),X'80'                                                     
         BZ    *+8                                                              
         LA    RE,4(RE)                                                         
         ICM   RF,15,0(RE)                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,15,0(RE)                                                      
         B     LDCOUNTX                                                         
         EJECT                                                                  
LDCOUNTP L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   MID1(L'MYMID1+L'MYMID1A),MYMID1                                  
         MVC   MID2(L'MYMID2+L'MYMID2A),MYMID2                                  
*                                                                               
         LA    R2,COUNTS                                                        
LDCOUNTQ CLI   0(R2),X'FF'                                                      
         BE    LDCOUNTX                                                         
         OC    0(8,R2),0(R2)                                                    
         BZ    LDCOUNTT                                                         
         MVC   P(40),8(R2)                                                      
         ICM   R0,15,0(R2)                                                      
         BNZ   *+14                                                             
         MVC   P+44(7),=7C'.'                                                   
         B     LDCOUNTR                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+44(7),DUB                                                      
LDCOUNTR ICM   R1,15,4(R2)                                                      
         BNZ   *+14                                                             
         MVC   P+54(7),=7C'.'                                                   
         B     LDCOUNTS                                                         
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+54(7),DUB                                                      
LDCOUNTS AR    R1,R0                                                            
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+64(7),DUB                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
LDCOUNTT LA    R2,L'COUNTS(R2)                                                  
         B     LDCOUNTQ                                                         
*                                                                               
LDCOUNTX XIT1                                                                   
         EJECT                                                                  
******************************************************************              
*         TABLE OF KNOWN GENDIR/GENFIL KEY FORMATS               *              
******************************************************************              
         SPACE 1                                                                
*        AL1(INDEX FOR ADRTAB)                                                  
*        X'LL' LENGTH OF KEY TO COMPARE                                         
*        KEY DATA FOR LENGTH OF LL                                              
         SPACE 1                                                                
KEYTAB   DS    0F                                                               
         DC    AL1(00),X'1F',31X'00'       HEADER                               
         DC    AL1(01),X'03',X'00000A'     ACCESS                               
         DC    AL1(02),X'03',X'00000B'     DEPARTMENT                           
         DC    AL1(03),X'03',X'00000F'     PUBLIC IDS                           
         DC    AL1(04),X'03',X'0000',C'C'  CURRENCY                             
         DC    AL1(05),X'03',X'0000',C'E'  EXCHANGE                             
         DC    AL1(06),X'03',X'0000',C'M'  MESSAGE                              
         DC    AL1(07),X'03',X'0000',C'N'  NARRATIVE                            
         DC    AL1(08),X'02',X'0002'       HELP                                 
         DC    AL1(09),X'02',X'0003'       DRIVER DICTIONARY                    
         DC    AL1(10),X'02',X'0009'       BROADCAST                            
         DC    AL1(11),X'03',X'00',C'FC'   CHANNEL CONFIG   (FEES)              
         DC    AL1(12),X'03',X'00',C'FN'   NAT INSURANCE    (FEES)              
         DC    AL1(13),X'03',X'00',C'FP'   PAYMENT PARMS    (FEES)              
         DC    AL1(14),X'03',X'00',C'FR'   ROLE DESCRIPTION (FEES)              
         DC    AL1(15),X'03',X'000010'     EXTRACT AGENCY                       
         DC    AL1(16),X'03',X'000020'     EXTRACT TRANSFER CONTROL             
         DC    AL1(17),X'03',X'000030'     EXTRACT FILE                         
         DC    AL1(18),X'03',X'000040'     EXTRACT SYSTEM LOG                   
         DC    AL1(20),X'03',X'000094'     EQUATE NAME (PASSIVE)                
         DC    AL1(21),X'01',X'FF'         FOOTER RECORD                        
         DC    AL1(22),X'00'               UNKNOWN RECORD                       
*                                                                               
ADRTAB   DC    A(FHD),A(ACS),A(DPT),A(PID)                                      
         DC    A(CUR),A(EXC),A(MSG),A(NAR)                                      
         DC    A(HLP),A(DIC),A(BRD),A(CHN)                                      
         DC    A(NAT),A(PAY),A(ROL),A(XAG)                                      
         DC    A(XTR),A(XFI),A(XLO),A(DDC)                                      
         DC    A(EQN),A(FTR),A(XXX)                                             
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
MYMID1   DC    CL44'SYSTEM NAME     RECORD TYPE'                                
MYMID1A  DC    C' ACTIVE   DELETED    TOTAL '                                   
MYMID2   DC    CL44'-----------     -----------'                                
MYMID2A  DC    C'RECORDS   RECORDS   RECORDS'                                   
*                                                                               
COUNTS   DS    0XL48                                                            
FHD      DC    8X'0',CL40'                FILE HEADER RECORDS'                  
FTR      DC    8X'0',CL40'                FILE TRAILER RECORDS'                 
XXX      DC    8X'0',CL40'                UNKNOWN RECORD TYPES'                 
HLP      DC    8X'0',CL40'                HELP RECORDS'                         
DIC      DC    8X'0',CL40'                DICTIONARY RECORDS'                   
BRD      DC    8X'0',CL40'                BROADCAST RECORDS'                    
CUR      DC    8X'0',CL40'                CURRENCY RECORDS'                     
EXC      DC    8X'0',CL40'                EXCHANGE RECORDS'                     
MSG      DC    8X'0',CL40'                MESSAGE RECORDS'                      
NAR      DC    8X'0',CL40'                NARRATIVE RECORDS'                    
ACS      DC    8X'0',CL40'                ACCESS RECORDS'                       
DPT      DC    8X'0',CL40'                DEPARTMENT RECORDS'                   
PID      DC    8X'0',CL40'                PUBLIC-ID RECORDS'                    
CHN      DC    8X'0',CL40'                CHANNEL CONFIG   (FEE)'               
NAT      DC    8X'0',CL40'                NAT INSURANCE    (FEE)'               
PAY      DC    8X'0',CL40'                PAYMENT PARMS    (FEE)'               
ROL      DC    8X'0',CL40'                ROLE DESCRIPTION (FEE)'               
XAG      DC    8X'0',CL40'                EXTRACT AGENCY'                       
XTR      DC    8X'0',CL40'                EXTRACT TRANSFER'                     
XFI      DC    8X'0',CL40'                EXTRACT FILE'                         
XLO      DC    8X'0',CL40'                EXTRACT SYSTEM LOG'                   
DDC      DC    8X'0',CL40'                DATA DICT. (NEW-STYLE)'               
EQN      DC    8X'0',CL40'                EQUATE NAME (PASSIVE) '               
FTS      DC    8X'0',CL40'                ** FILE TOTALS **'                    
         DC    X'FF'                                                            
         EJECT                                                                  
*DDDPRINT                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
*                                                                               
*GEGENMSG                                                                       
         PRINT OFF                                                              
       ++INCLUDE GEGENMSG                                                       
         PRINT ON                                                               
*                                                                               
WRKD     DSECT                                                                  
BYTE     DS    X                                                                
DUB      DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004GELDDCNTS 03/03/94'                                      
         END                                                                    
