*          DATA SET GELDPRGE   AT LEVEL 007 AS OF 05/25/11                      
*CATALP GELDPRGE                                                                
         TITLE 'LDPURGE - GENDIR/GENFIL - LOAD PURGE TEST'                      
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*                                                                               
*        UPSI  XXXXXXX1 DROP DELETED RECORDS                                    
*                                                                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,GELDPRGE                                                       
         SPACE 2                                                                
         ST    R1,SAVER1           STORE PARAMETER REG IN SAVER1                
         L     R8,0(R1)            R8=A(RECORD)                                 
*                                                                               
         L     R2,12(R1)           USER R2 FOR ADCONS                           
         USING LDDEFND,R2                                                       
*                                                                               
         OC    0(3,R8),0(R8)                                                    
         BZ    EXIT                IGNORE HEADER RECORDS                        
         CLI   0(R8),X'FF'                                                      
         BE    EXIT                IGNORE TRAILER RECORDS                       
*                                                                               
         L     RE,LUPSIVAL         COPY UPSI                                    
         TM    0(RE),X'01'         TEST FOR DELETION REQUESTED                  
         BZ    EXIT                EXIT IF NOT                                  
         EJECT                                                                  
*                                                                               
         USING BRDKEYD,R8          EXPIRE OLD TEMP BROADCASTS                   
EXPIRE   GOTO1 LDATCON,DMCB,(5,0),(0,DUB)                                       
*                                                                               
         CLI   BRDKSYS,BRDKSYSQ    IS THIS A BC RECORD                          
         BNE   EXP010                                                           
         CLI   BRDKSTYP,BRDKSTYQ                                                
         BNE   EXP010                                                           
         CLI   BRDKTYPE,BRDKTEMQ   IS THIS A TEMPORARY BC                       
         BNE   EXP010                                                           
*                                                                               
         LA    R6,BRDFSTEL                                                      
         USING BRDFLTD,R6                                                       
EXP001   CLI   BRDFLTC,BRDFLTCQ    LOOK FOR FILTER ELEMENT                      
         BE    EXP002                                                           
         CLI   BRDFLTC,0           TEST FOR END                                 
         BE    EXPXXX                                                           
         SR    R0,R0                                                            
         ICM   R0,1,1(R1)          NEXT ELEMENT                                 
         BZ    EXPXXX                                                           
         AR    R6,R0                                                            
         B     EXP001                                                           
*                                                                               
EXP002   GOTO1 LDATCON,DMCB,(2,BRDFENDT),(0,DUB1)                               
         CLC   DUB(6),DUB1         DOES MESSAGE EXPIRE AFTER TODAY              
         BNH   EXPXXX                                                           
         GOTO1 LPERVERT,DMCB,DUB1,DUB                                           
         CLC   DMCB+8(2),=Y(MAXDAYS) IS MESSAGE OLDER THAN MAXDAYS              
         BL    EXPXXX                                                           
         OI    34(R8),X'80'        YES SO DELETE IT NOW                         
         B     EXPXXX                                                           
         DROP  R8,R6                                                            
*                                                                               
         USING EZMKEY,R8                                                        
EXP010   DS    0H                                                               
         CLC   EZMKID,=C'ZM'                                                    
         BNE   EXPXXX                                                           
*                                                                               
         LA    R6,EZMDTAEL                                                      
         USING EZMDTAEL,R6                                                      
EXP011   CLI   0(R6),X'10'         BILLING DATA ELEMENT                         
         BNE   EXPXXX                                                           
         OC    EZMDATE,EZMDATE     MOVE DATE PRESENT?                           
         BZ    EXPXXX                                                           
         GOTO1 LDATCON,DMCB,(2,EZMDATE),(0,DUB1) MOVE DATE                      
         GOTO1 LPERVERT,DMCB,DUB1,DUB                                           
         CLC   DMCB+8(2),=Y(MAXDAYS2) OLDER THAN MAXDAYS2                       
         BL    EXPXXX                                                           
         OI    34(R8),X'80'        YES SO DELETE IT NOW                         
         B     EXPXXX                                                           
         DROP  R8,R6                                                            
*                                                                               
EXPXXX   EQU   *                                                                
*                                                                               
MAXDAYS  EQU   365                 365 DAYS (LIFE OF A TEMP BROADCAST)          
MAXDAYS2 EQU   214                 214 DAYS (EASI MOV 7MOS = 4*31+3*30)         
         EJECT                                                                  
PURGE    TM    34(R8),X'80'        IS RECORD DELETED                            
         BZ    EXIT                NO - NO NEED TO PURGE                        
         L     R1,SAVER1                                                        
         MVI   0(R1),X'FF'         YES - RETURN PURGE FLAG                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
DUB      DC    D'0'                                                             
DUB1     DC    D'0'                                                             
DMCB     DC    6F'0'                                                            
SAVERE   DC    F'0'                                                             
SAVER1   DC    F'0'                SAVE PARAMETER ADDRESS                       
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*DDDPRINT                                                                       
*DMLDDEFN                                                                       
*DMLDXTRNS                                                                      
*CTGENBRD                                                                       
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE DMLDDEFN                                                       
       ++INCLUDE CTGENBRD                                                       
       ++INCLUDE SPEZMOV                                                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007GELDPRGE  05/25/11'                                      
         END                                                                    
