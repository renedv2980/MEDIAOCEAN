*          DATA SET TAYTD      AT LEVEL 061 AS OF 10/10/14                      
*CATALP TAYTD                                                                   
*                                                                               
*        PARAMETERS                                                             
*        ----------                                                             
*                                                                               
*        P1 = A(GENCON W/S)                                                     
*                                                                               
*        P2 = A(SYSTEM COMMON ROUTINES)                                         
*                                                                               
*        P3 = A(PARAMETER BLOCK)                                                
*                                                                               
*                                                                               
         TITLE 'TAYTD - GET YEAR-TO-DATE DATA'                                  
TAYTD    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TYTDLNQ,TAYTD,CLEAR=YES                                          
         LR    RA,RC               RA=A(LOCAL WORKING STORAGE)                  
         USING TYTDD,RA                                                         
         L     RC,0(R1)            RC=A(CONTROLLER STORAGE AREA)                
         USING GEND,RC                                                          
         L     R9,ASTARTSV         R9=A(GLOBAL SAVED VALUES)                    
         USING SYSWORKD,R9                                                      
         L     R8,ASPOOLD          R8=A(SPOOL STORAGE AREA)                     
         USING SPOOLD,R8                                                        
         L     R7,4(R1)            R7=A(COMMON FACILITIES FOR SYSTEM)           
         USING SYSCOMMD,R7                                                      
         L     R6,8(R1)            R6=A(PARAMETER BLOCK)                        
         USING TYD,R6                                                           
         EJECT                                                                  
*              MAIN CONTROL                                                     
         SPACE 1                                                                
         TM    TYSTAT,TYSTBILL     TEST BILLING YTD REQUESTED                   
         BO    M10                                                              
         L     R2,TYATAB           R2=A(YTD TABLE)                              
         XC    0(YTDLNQ,R2),0(R2)  INITIALIZE IT                                
         ST    R2,TYANEXT          SAVE AS A(NEXT)                              
         SPACE 1                                                                
M10      MVI   TYRETURN,0          CLEAR RETURN BYTE                            
         SPACE 1                                                                
         TM    TYSTAT,TYSTCHK      TEST YTD FOR CHECK REQUESTED                 
         BO    CYTD                                                             
         SPACE 1                                                                
         OC    TYPEND,TYPEND       END DATE FILTER                              
         BNZ   *+6                                                              
         DC    H'0'                IT'S MISSING                                 
         SPACE 1                                                                
         CLI   TYCUR,0             CURRENCY FILTER                              
         BNE   *+8                                                              
         MVI   TYCUR,C'U'          DEFAULT TO US$                               
         SPACE 1                                                                
         OC    TYEMP,TYEMP         EMPLOYER FILTER                              
         BNZ   *+6                                                              
         DC    H'0'                IT'S MISSING                                 
         SPACE 1                                                                
         TM    TYSTAT,TYSTBILL     TEST BILLING YTD REQUESTED                   
         BO    BYTD                                                             
         B     PYTD                ELSE BUILD PAYROLL YTD BLOCK                 
         EJECT                                                                  
*               GENERATE BILLING YTD                                            
         SPACE 1                                                                
BYTD     DS    0H                                                               
         MVC   TYSVAIO,AIO         SAVE USER'S I/O AREA                         
         LA    R3,KEY              R3=A(KEY)                                    
         USING TLCKPD,R3                                                        
         XC    TLCKPKEY,TLCKPKEY   BUILD BILLING YTD KEY                        
         MVI   TLCKPCD,TLCKBCDQ    RECORD CODE                                  
         MVC   TLCKBCUR,TYCUR      CURRENCY                                     
         MVC   TLCKBEMP,TYEMP      EMPLOYER                                     
         MVC   TLCKBSSN,TYSSN      SOCIAL SECURITY NUMBER                       
         MVC   TLCKBDTE,TYPEND     END DATE                                     
         XC    TLCKBDTE,=3X'FF'    (COMPLEMENTED)                               
         SPACE 1                                                                
         MVC   FILENAME,=CL8'CHKDIR' SET TO READ CHKDIR                         
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
         SPACE 1                                                                
         CLI   TYTRACE,C'Y'        IF TRACE REQUESTED                           
         BNE   BY20                                                             
         MVC   WORK(6),=C'TAYTD-'  DISPLAY KEYS                                 
         MVC   WORK+6(9),TYSSN     WITH SOCIAL SECURITY NUMBER                  
         GOTO1 TRACE,DMCB,KEY,96,WORK,15                                        
         SPACE 1                                                                
*                                  TEST FOUND POINTER FOR THIS YEAR             
BY20     CLC   TLCKPKEY(TLCKBDTE-TLCKPD+1),KEYSAVE                              
         BNE   BYX                                                              
         OI    TYRETURN,TYFNDPTR   SET FOUND A POINTER                          
         LA    R1,TYIO             USE OWN I/O AREA                             
         ST    R1,AIO                                                           
         MVC   FILENAME,=CL8'CHKFIL' SET TO READ CHKFIL                         
         GOTO1 GETREC                                                           
         XC    FILENAME,FILENAME                                                
         SPACE 1                                                                
         MVI   BYTE,TAYETBIL                                                    
         BAS   RE,GETYE            GET TAYE ELEMENT FOR BILLING                 
         SPACE 1                                                                
BYX      MVC   AIO,TYSVAIO         RESTORE USER'S I/O AREA                      
         B     XIT                                                              
         EJECT                                                                  
*              GET TAYE ELEMENT FOR REQUESTED TYPE                              
         SPACE 1                                                                
GETYE    NTR1                                                                   
         LA    R1,TYBDA            SET D/A FOR BILLING                          
         CLI   BYTE,TAYETBIL                                                    
         BE    *+8                                                              
         LA    R1,TYCDA            OR FOR CHECKS                                
         MVC   0(4,R1),DMDSKADD                                                 
         SPACE 1                                                                
         MVI   ELCODE,TAYEELQ      GET YTD EARNINGS ELEMENT                     
         GOTO1 GETL,DMCB,(1,BYTE)  GET REQUESTED TYPE                           
         BNE   GYX                                                              
         LA    R1,6                N'ENTRIES FOR EACH ELEMENT TYPE              
         USING TAYED,R3                                                         
         L     R3,TGELEM                                                        
         USING YTDD,R2                                                          
         LA    R3,TAYENYTD         NEW YTD                                      
         LA    R2,TYBYTD           R2=A(YTD AMOUNTS)                            
         CLI   BYTE,TAYETBIL                                                    
         BE    *+8                                                              
         LA    R2,TYCYTD                                                        
         SPACE 1                                                                
GY10     MVC   0(4,R2),0(R3)       RETURN YTD EARNINGS                          
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R1,GY10                                                          
*                                                                               
         CLI   BYTE,TAYETBIL       P+ CANADIAN CHECKS?                          
         BE    GYX                                                              
         L     R3,TGELEM                                                        
         CLI   TAYELEN,TAYELN2Q                                                 
         BL    GYX                                                              
*                                                                               
         LA    R1,8                N'ENTRIES FOR EACH ELEMENT TYPE              
         USING TAYED,R3            R2 ALREADY POINTING TO TYCCYTD               
         L     R3,TGELEM                                                        
         LA    R3,TAYECYTD         NEW YTD                                      
         SPACE 1                                                                
GY20     MVC   0(4,R2),0(R3)       RETURN YTD EARNINGS                          
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R1,GY20                                                          
         SPACE 1                                                                
GYX      B     XIT                                                              
         EJECT                                                                  
         SPACE 1                                                                
*               GENERATE YTD FOR CHECK RECORD                                   
         SPACE 1                                                                
CYTD     DS    0H                                                               
         L     R3,TYAREC           R3=A(CHECK RECORD)                           
         ST    R3,TIAREC                                                        
         SPACE 1                                                                
         MVC   TYSVAIO,AIO         SAVE USER'S I/O AREA                         
         MVC   AIO,TIAREC                                                       
         MVI   BYTE,TAYETBIL       GET TAYE ELEMENT FOR BILLING                 
         BAS   RE,GETYE            & CHECKS (CAN'T WAIT FOR IOHOOK              
         MVI   BYTE,TAYETCHK       BECAUSE THERE MIGHT NOT BE ANY               
         BAS   RE,GETYE            TACY ELEMENTS & WON'T GET THERE)             
         MVC   AIO,TYSVAIO         RESTORE USER'S I/O AREA                      
         SPACE 1                                                                
         USING TLCKD,R3                                                         
         MVC   TISSN,TYSSN         SET S/S NUMBER                               
         SPACE 1                                                                
         MVI   ELCODE,TACYELQ                                                   
         BAS   RE,GETEL            LOOP THROUGH CHECK YTD ELEMENTS              
         B     *+8                                                              
CY10     BAS   RE,NEXTEL                                                        
         BNE   CY20                                                             
         USING TACYD,R3            R3 = A(CHECK YTD ELEMENT)                    
         SPACE 1                                                                
         MVC   TIUNIT,TACYUNIT     EXTRACT TAX UNIT INTO SYSIO BLOCK            
         MVI   TIMODE,PROCREC                                                   
         MVC   TIDSKADD,DMDSKADD                                                
         BAS   RE,IOHOOK           PROCESS AS IF HOOK FROM SYSIO                
         SPACE 1                                                                
         MVI   ELCODE,TACYELQ                                                   
         B     CY10                GET NEXT YTD ELEMENT                         
         SPACE 1                                                                
CY20     B     PY90                SORT TABLE IF NECESSARY                      
         EJECT                                                                  
*               GENERATE PAYROLL YTD                                            
         SPACE 1                                                                
PYTD     DS    0H                                                               
         MVC   TIACOMFC,ACOMFACS   SET UP SOME FIELDS FOR SYSIO                 
         L     R1,ATWA                                                          
         USING TWAD,R1                                                          
         MVC   TIUSERID,TWAORIG                                                 
         DROP  R1                                                               
         MVC   TIQSTAFF,TGCTSTAF                                                
         MVI   TIREAD,TLCKYCDQ     SET TO READ YTD POINTERS                     
         MVI   TIQFLAGS,TIQFSKIP   SET SKIP READING                             
         OI    TIQFLAG2,TIQFPGRY   RECOGNIZE GREY PAYROLL                       
         SPACE 1                                                                
         MVC   TIQPEND,TYPEND      END DATE FILTER                              
         MVC   TIFCUR,TYCUR        CURRENCY FILTER                              
         MVC   TIFEMP,TYEMP        EMPLOYER FILTER                              
         MVC   TIFSSN,TYSSN        SOCIAL SECURITY NUMBER FILTER                
         SPACE 1                                                                
         LA    R0,IOHOOK           SET A(I/O HOOK FOR SYSIO)                    
         ST    R0,TIHOOK                                                        
         SPACE 1                                                                
         GOTO1 TYASYSIO,DMCB,TASYSIOD  OFF TO SYSIO TO READ RECORDS             
         SPACE 1                                                                
PY90     CLI   TYNRECS,1           IF WE FOUND SOME RECORDS                     
         BNH   PYX                                                              
         ZIC   R3,TYNRECS          SORT THEM                                    
         GOTO1 XSORT,DMCB,TYATAB,(R3),YTDLNQ,YTDSRTLQ,YTDSORT-YTDD              
         SPACE 1                                                                
PYX      B     XIT                                                              
         EJECT                                                                  
*               PROCESS SYSIO RECORDS                                           
         SPACE 1                                                                
IOHOOK   NTR1                                                                   
         CLI   TIMODE,PROCREC      PROCESS RECORDS                              
         BNE   IOHX                                                             
         L     R2,TYANEXT          R2=A(YTD TABLE ENTRY)                        
         USING YTDD,R2                                                          
         XC    0(YTDLNQ+1,R2),0(R2)  PRE-CLEAR THIS + 1ST BYTE OF NEXT          
*                                                                               
         MVC   YTDUNIT,TIUNIT      SET CURRENT TAX UNIT                         
         MVI   YTDSORT,2           SET SORT CODE                                
*                                                                               
         CLC   YTDUNIT,=C'FD '     IF THIS IS FEDERAL                           
         BE    IOH10                                                            
         CLC   YTDUNIT,=C'EU '     OR IF THIS IS EUROS                          
         BE    IOH10                                                            
         CLC   YTDUNIT,=C'CN '     OR IF P+ CANADIAN                            
         BNE   IOH20                                                            
         CLC   TYEMP,=C'P+ '                                                    
         BNE   IOH20                                                            
         MVI   YTDSORT,1                                                        
         B     IOH20                                                            
*                                                                               
IOH10    MVC   TYSVAIO,AIO         SAVE USER'S I/O AREA                         
         MVC   AIO,TIAREC                                                       
         MVC   DMDSKADD,TIDSKADD                                                
         MVI   BYTE,TAYETCHK                                                    
         BAS   RE,GETYE            GET TAYE ELEMENT FOR CHECKS                  
         MVC   AIO,TYSVAIO         RESTORE USER'S I/O AREA                      
         SPACE 1                                                                
         MVI   YTDSORT,1           SET TO SORT HIGH                             
         L     R3,TIAREC           GET CHECK DETAILS EL.                        
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   IOH20                                                            
         USING TACDD,R3            R3 = A(CHECK DETAILS ELEMENT)                
         SPACE 1                                                                
         MVC   YTDREXP,TACDYREX    SAVE YTD REIMBURSED EXPENSES                 
         SPACE 1                                                                
IOH20    MVI   ELCODE,TACYELQ      SET TO GET YTD ELEMENT                       
         L     R0,AIO                                                           
         MVC   AIO,TIAREC                                                       
         GOTO1 GETL,DMCB,(3,YTDUNIT)  GET YTD ELEMENT FOR CURRENT UNIT          
         ST    R0,AIO                                                           
         BNE   IOH50               DONE                                         
*                                                                               
         L     R3,TGELEM           R3 = A(CHECK YTD ELEMENT)                    
         USING TACYD,R3                                                         
         CLI   TACYLEN,TACYLN3Q                                                 
         BL    IOH30                                                            
         MVC   YTDTXRE,TACYTXRE    TAXABLE REIMBURSEMENTS                       
         MVC   YTDNTRE,TACYNTRE    NON-TAXABLE REIMBURSEMENTS                   
         CLI   TACYLEN,TACYLN4Q                                                 
         BL    IOH30                                                            
         MVC   YTDTSUI,TACYTSUI    TAXABLE SUI FOR UNIT                         
         MVC   YTDYSUI,TACYYSUI    YTD TAXABLE SUI FOR THIS CHECK               
         CLI   TACYLEN,TACYLN5Q                                                 
         BL    IOH30                                                            
         MVC   YTDCTXRE,TACYCTXR                                                
         MVC   YTDCNTRE,TACYCNTR                                                
         MVC   YTDCEARN,TACYCERN                                                
         MVC   YTDUTAX,TACYUTAX    P+ CANADIAN TAXES                            
         MVC   YTDUPP,TACYUPP                                                   
         MVC   YTDUEI,TACYUEI                                                   
         MVC   YTDUPIP,TACYUPIP                                                 
         MVC   YTDCTAX,TACYCTAX                                                 
         MVC   YTDCPP,TACYCPP                                                   
         MVC   YTDCEI,TACYCEI                                                   
         MVC   YTDCPIP,TACYCPIP                                                 
         CLI   TACYLEN,TACYLN6Q                                                 
         BL    IOH30                                                            
         MVC   YTDFERN,TACYFERN    FEDERAL EARNINGS AT PROV LEVEL               
*                                                                               
IOH30    CLI   TACYLEN,TACYEARN+L'TACYEARN-TACYD                                
         BL    *+10                                                             
         MVC   YTDEARN,TACYEARN    SET EARNINGS                                 
         MVC   YTDTAX,TACYTAX          TAX                                      
*                                                                               
         CLC   YTDUNIT,=C'EU '     IF THIS IS EUROS                             
         BE    IOH50                                                            
*                                                                               
         CLC   YTDUNIT,=C'FD '     IF THIS IS FEDERAL                           
         BNE   *+14                                                             
         MVC   YTDFICA,TACYFICA        FICA                                     
         B     IOH50                                                            
*                                                                               
         CLC   YTDUNIT,=C'CN '     IF THIS IS CANADA                            
         BNE   IOH40                                                            
         MVC   YTDGST,TACYGST          GOODS & SERVICES TAX                     
         B     IOH50                                                            
*                                                                               
IOH40    CLI   YTDUNIT+2,C' '      IF THIS IS A STATE                           
         BNE   IOH80                                                            
         MVC   YTDSUI,TACYSUI      SET UNEMPLOYMENT                             
         MVC   YTDSDI,TACYSDI          DISABILITY                               
         CLI   TACYLEN,TACYLN2Q                                                 
         BL    IOH80                                                            
         MVC   YTDSFLI,TACYSFLI        FAMILY LEAVE INSURANCE                   
         B     IOH80                                                            
*                                                                               
         USING TACXD,R3                                                         
IOH50    L     R3,TIAREC           READ TACX ELEMENT                            
         MVI   ELCODE,TACXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   IOH80               NONE, FINISH UP                              
         MVC   YTDMDD,TACXMDDY                                                  
         MVC   YTDMTR,TACXMTRY                                                  
         MVC   YTDDTP,TACXDTPY                                                  
         MVC   YTDPRC,TACXPRCY                                                  
         MVC   YTDLIE,TACXLIEY                                                  
         MVC   YTDUND,TACXUNDY                                                  
         MVC   YTDTRS,TACXTRSY                                                  
         MVC   YTDDRT,TACXDRTY                                                  
         MVC   YTDWRD,TACXWRDY                                                  
         CLI   TACXLEN,TACXLNQ                                                  
         BL    IOH55                                                            
         MVC   YTDPPA,TACXPPAC                                                  
         CLI   TACXLEN,TACXLNQ2                                                 
         BL    IOH55                                                            
         MVC   YTDDTPTR,TACXDTPT                                                
         MVC   YTDDTPNR,TACXDTPN                                                
                                                                                
IOH55    CLC   YTDUNIT,=C'CN '     IF THIS IS CANADA                            
         BNE   *+10                                                             
         MVC   YTDPST,TACXPSTY                                                  
*                                                                               
IOH80    CLI   TYTRACE,C'Y'        IF TRACE REQUESTED                           
         BNE   IOH90                                                            
         MVC   WORK(6),=C'TAYTD-'  DISPLAY THIS TABLE ENTRY                     
         MVC   WORK+6(9),TISSN     WITH SOCIAL SECURITY NUMBER                  
         GOTO1 TRACE,DMCB,(R2),YTDLNQ,WORK,15                                   
         SPACE 1                                                                
IOH90    LA    R2,YTDNEXT          SET A(NEXT SLOT IN TABLE)                    
         ST    R2,TYANEXT                                                       
         SPACE 1                                                                
         ZIC   R1,TYNRECS          BUMP RECORD COUNT                            
         LA    R1,1(R1)                                                         
         STC   R1,TYNRECS                                                       
         SPACE 1                                                                
IOHX     B     XIT                                                              
         EJECT                                                                  
*              EXITS, ETC.                                                      
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 3                                                                
         GETEL R3,DATADISP,ELCODE                                               
         EJECT                                                                  
*              CONSTANTS, ETC.                                                  
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TAYTDD                                                         
         EJECT                                                                  
*              DSECT TO COVER LOCAL WORKING STORAGE                             
TYTDD    DSECT                                                                  
TYSVAIO  DS    A                   SAVED USER'S I/O                             
TYANEXT  DS    A                   A(NEXT SLOT IN YTD TABLE)                    
TYNRECS  DS    XL1                 N'RECORDS FOUND                              
         SPACE 3                                                                
       ++INCLUDE TASYSIOD                                                       
         SPACE 1                                                                
TYIO     DS    CL4000              I/O AREA                                     
TYTDLNQ  EQU   *-TYTDD                                                          
         EJECT                                                                  
* TASYSVALD                                                                     
* TASYSWORKD                                                                    
* TASYSDSECT                                                                    
* TASYSEQUS                                                                     
* DDSPLWORKD                                                                    
* DDSPOOLD                                                                      
* TAGENFILE                                                                     
* DDGENTWA (TWAD)                                                               
         PRINT OFF                                                              
SYSCOMMD DSECT                                                                  
       ++INCLUDE TASYSVALD                                                      
SYSWORKD DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE TAGENFILE                                                      
TWAD     DSECT                                                                  
         DS    CL64                                                             
CONHEADH EQU   *                                                                
       ++INCLUDE DDGENTWA                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061TAYTD     10/10/14'                                      
         END                                                                    
