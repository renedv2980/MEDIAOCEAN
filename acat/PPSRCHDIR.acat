*          DATA SET PPSRCHDIR  AT LEVEL 062 AS OF 05/01/02                      
*CATALP SRCHDPRT                                                                
         TITLE 'PRINTPAK SEARCH DIRECTORY AND SUBROUTINES'                      
         PRINT NOGEN                                                            
SRCHPRT  CSECT                                                                  
         SPACE                                                                  
       ++INCLUDE GESRCHDIRH                                                     
         EJECT                                                                  
         NOP   SRCHDPPK-SRCHPRT    +2 IS HWORD DISP OF DIRECTORY                
*                                                                               
RNTRY    NMOD1 SRCHWRKL,SPRT**,RR=RE                                            
*                                                                               
         SH    RB,=YL2(RNTRY-SRCHPRT) RESET RB TO BEGINNING OF CSECT            
         DROP  RB                                                               
         USING SRCHPRT,RB                                                       
*                                                                               
         USING SRCHWORK,RC         RC=TEMP WORK AREA                            
         ST    RE,RELO                SAVE RELO FACTOR                          
         ST    R1,APARMS              SAVE A(PARMS)                             
         L     RA,0(,R1)           RA=ADDRESS OF CONTROL BLOCK                  
         USING SEARCHD,RA                                                       
*                                                                               
         CLI   SBLANG,0                                                         
         BNE   *+8                                                              
         MVI   SBLANG,1            DEFAULT TO ENGLISH                           
*                                                                               
         MVI   SBRETURN,0          CLEAR ERROR FLAG                             
         CLI   0(R1),SDSSYPPQ      TEST PASSIVE POINTER ACTION                  
         BNE   RNTRY01                                                          
         LH    R8,=Y(RPASSIVE-SRCHPRT) DISP TO PASSIVE POINTER SRTN             
         AR    R8,RB               MAKE IT BASE                                 
         BR    R8                  PASSIVE POINTER SUBROUTINE                   
RNTRY01  NC    SBASYS,SBASYS                                                    
         BZ    ENIT                SEARCH BLOCK NOT INITIALIZED                 
         CLI   0(R1),SDSSYKSQ                                                   
         BE    RSYSKSET            SYSTEM KEY SET ROUTINE                       
         CLI   0(R1),SDSSERXQ                                                   
         BH    GUDEXIT             GO STRAIGHT OUT IF UNKNOWN SRTN              
*                                                                               
         L     R9,SBASRCH          R9=SEARCH ENTRY                              
         USING SDSRCHD,R9          (NOT AVAILABLE AT SYSTEM EXIT)               
*                                                                               
         XR    RF,RF                                                            
         IC    RF,0(,R1)           GET NUMBER OF REQUIRED SUBROUTINE            
         BCTR  RF,0                LESS 1                                       
         SLL   RF,2                TIMES 4                                      
         LH    R8,SDSRRTNS-SDSRCHD(,R9) GET ROUTINE BRANCH TAB OFFSET           
         AR    R8,RB               SET UP BASE FOR ROUTINE                      
         B     0(RF,R8)            GO TO IT WITH ORIGINAL R1.                   
         EJECT                                                                  
***********************************************************************         
*        ASSORTED EXITS                                               *         
***********************************************************************         
EFLT     MVI   SBRETURN,SBRFLTQ    ITEM FILTERED OUT                            
         B     BADEXIT                                                          
ESEC     MVI   SBRETURN,SBRSECQ    ITEM FAILED SECURITY CHECK                   
         B     BADEXIT                                                          
*                                                                               
EMIF     MVI   SBRETURN,SBEMIFQ    MISSING INPUT FIELD                          
         B     BADEXIT                                                          
EIIF     MVI   SBRETURN,SBEIIFQ    INVALID INPUT FIELD                          
         B     BADEXIT                                                          
EIKW     MVI   SBRETURN,SBEIKWQ    INVALID KEYWORD                              
         B     ETXTSET                                                          
EDKW     MVI   SBRETURN,SBEDKWQ    DUPLICATE KEYWORD                            
         B     ETXTSET                                                          
EIFV     MVI   SBRETURN,SBEIFVQ    INVALID VALUE FOR FILTER                     
         B     ETXTSET                                                          
EFTS     MVI   SBRETURN,SBEFTSQ    FIELD TOO SHORT                              
         B     ETXTSET                                                          
EFTL     MVI   SBRETURN,SBEFTLQ    FIELD TOO LONG                               
         B     ETXTSET                                                          
ETMN     MVI   SBRETURN,SBETMNQ    TOO MANY ITEMS INPUT                         
         B     BADEXIT                                                          
EHLP     MVI   SBRETURN,SBEHLPQ    HELP DISPLAYED                               
         B     BADEXIT                                                          
*                                                                               
EIIO     MVI   SBRETURN,SBEIIOQ    INVALID IO                                   
         B     BADEXIT                                                          
ENIT     MVI   SBRETURN,SBENITQ    NOT INITIALIZED                              
         B     BADEXIT                                                          
*                                                                               
ETXTSET  CLI   GTLTXT-GTBLOCK+SBTXTBLK,0                                        
         BE    BADEXIT                                                          
         LA    RE,SBOPTTXT                                                      
         STCM  RE,B'0111',GTATXT-GTBLOCK+SBTXTBLK                               
         B     BADEXIT                                                          
*                                                                               
EMIFM    MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,1 MISSING INPUT FIELD (MEDIA)          
         B     MEDERR              (REQUIRED IF MEDD ITEM IN EXTRATEXT)         
EMNA     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,9 MEDIA NOT AUTHORISED                 
         B     MEDERR                                                           
EMIN     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,13 INVALID MEDIA                       
         B     MEDERR                                                           
ECIN     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,14 INVALID CLIENT                      
         B     MEDERR                                                           
EPIN     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,15 INVALID PRODUCT                     
         B     MEDERR                                                           
ECNF     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,40 CLIENT NOT ON FILE                  
         B     MEDERR                                                           
EPNF     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,41 PRODUCT NOT ON FILE                 
         B     MEDERR                                                           
ESECLO   MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,55  SECURITY LOCKOUT                   
         B     MEDERR                                                           
EFOL     MVI   GTMSGNO1-GTBLOCK+SBTXTBLK,155 FOLIO NOT ON FILE                  
MEDERR   L     RE,SBASYS           SET MEDIA MESSAGE SYSTEM                     
         MVC   GTMSYS-GTBLOCK+SBTXTBLK,SDOVSYS-SDSYSD(RE)                       
         MVI   SBRETURN,SBETXTQ    INDICATE MESSAGE IN BLOCK                    
         SPACE                                                                  
BADEXIT  LTR   RB,RB               CC NONZERO    = BAD EXIT                     
         B     EXIT                                                             
GUDEXIT  CR    RB,RB               CC ZERO/EQUAL = GOOD EXIT                    
EXIT     XIT1                                                                   
*                                                                               
PACHAREA DC    32S(*)                                                           
         EJECT                                                                  
***********************************************************************         
*        START OF SEARCH DIRECTORY (DSECT SDSRCHD)                    *         
***********************************************************************         
         ENTRY SRCHDPPK                                                         
SRCHDPPK DC    F'0'                INITIALLY END OF TABLE                       
         SPACE 3                                                                
***********************************************************************         
*        CLIENT SEARCH ENTRY                                          *         
***********************************************************************         
         SPACE                                                                  
         ORG   *-4                 BACK OVER PREVIOUS END                       
SRCHCLI  DC    Y(SRCHCLIX-SRCHCLI) LENGTH OF ENTRY                              
         DC    CL8'CLIENT'         SEARCH CODE                                  
         DC    CL6' '              NO OVERIDE SYSTEM                            
         DC    Y(RCLRTNS-SRCHPRT)  DISP OF CLIENT ROUTINES                      
         DC    Y(PPNSKTYP-PPNSKEY) DISP OF TYPE 1/2 MODIFIER IN KEY             
         DC    AL1(1)              VALUE OF TYPE 1 MODIFIER                     
         DC    AL1(2)              VALUE OF TYPE 2 MODIFIER                     
         DC    Y(PPNSKWD1-PPNSKEY) DISP OF WORD/SEQNO PAIR IN KEY               
         DC    Y(L'PPNSKWD1)       LENGTH OF WORD.                              
         DC    Y(L'PPNSKSQ1)       LENGTH OF SEQNO (2 OR 3)                     
         DC    AL1((SDSRITYQ)+0+0) INDICATORS                                   
*                                  SEARCH TYPE IS PART OR EXACT MATCH           
*                                  DUPLICATE WORDS ARE DROPPED                  
*                                  WORD SEQUENCE IS NOT RELEVANT                
         DC    AL1(0)              MORE INDICATORS                              
         DC    AL1(0)              SEARCH ID NUMBER                             
         DC    AL1(0)              COUNTRY FILTER                               
         SPACE                                                                  
SRCHCLIX DC    F'0'                END SEARCH ENTRIES                           
         SPACE 3                                                                
***********************************************************************         
*        PRODUCT SEARCH ENTRY                                         *         
***********************************************************************         
         SPACE                                                                  
         ORG   *-4                 BACK OVER PREVIOUS END                       
SRCHPRO  DC    Y(SRCHPROX-SRCHPRO) LENGTH OF ENTRY                              
         DC    CL8'PRODUCT'        SPARE                                        
         DC    CL6' '              NO OVERIDE SYSTEM                            
         DC    Y(RPRRTNS-SRCHPRT)  DISP OF PRODUCT ROUTINES                     
         DC    Y(PPNSKTYP-PPNSKEY) DISP OF TYPE 1/2 MODIFIER IN KEY             
         DC    AL1(1)              VALUE OF TYPE 1 MODIFIER                     
         DC    AL1(2)              VALUE OF TYPE 2 MODIFIER                     
         DC    Y(PPNSKWD1-PPNSKEY) DISP OF WORD/SEQNO PAIR IN KEY               
         DC    Y(L'PPNSKWD1)       LENGTH OF WORD.                              
         DC    Y(L'PPNSKSQ1)       LENGTH OF SEQNO (2 OR 3)                     
         DC    AL1((SDSRITYQ)+0+0) INDICATORS                                   
*                                  SEARCH TYPE IS PART OR EXACT MATCH           
*                                  DUPLICATE WORDS ARE DROPPED                  
*                                  WORD SEQUENCE IS NOT RELEVANT                
         DC    AL1(0)              MORE INDICATORS                              
         DC    AL1(0)              SEARCH ID NUMBER                             
         DC    AL1(0)              COUNTRY FILTER                               
         SPACE                                                                  
SRCHPROX DC    F'0'                END SEARCH ENTRIES                           
         SPACE 3                                                                
***********************************************************************         
*        CAMPAIGN SEARCH ENTRY (ESTIMATE)                             *         
***********************************************************************         
         SPACE                                                                  
         ORG   *-4                 BACK OVER PREVIOUS END                       
SRCHCAM  DC    Y(SRCHCAMX-SRCHCAM) LENGTH OF ENTRY                              
         DC    CL8'ESTIMATE'       SPARE                                        
         DC    CL6' '              NO OVERIDE SYSTEM                            
         DC    Y(RCARTNS-SRCHPRT)  DISP OF CAMPAIGN ROUTINES                    
         DC    Y(PPNSKTYP-PPNSKEY) DISP OF TYPE 1/2 MODIFIER IN KEY             
         DC    AL1(1)              VALUE OF TYPE 1 MODIFIER                     
         DC    AL1(2)              VALUE OF TYPE 2 MODIFIER                     
         DC    Y(PPNSKWD1-PPNSKEY) DISP OF WORD/SEQNO PAIR IN KEY               
         DC    Y(L'PPNSKWD1)       LENGTH OF WORD.                              
         DC    Y(L'PPNSKSQ1)       LENGTH OF SEQNO (2 OR 3)                     
         DC    AL1((SDSRITYQ)+0+0) INDICATORS                                   
*                                  SEARCH TYPE IS PART OR EXACT MATCH           
*                                  DUPLICATE WORDS ARE DROPPED                  
*                                  WORD SEQUENCE IS NOT RELEVANT                
         DC    AL1(0)              MORE INDICATORS                              
         DC    AL1(0)              SEARCH ID NUMBER                             
         DC    AL1(0)              COUNTRY FILTER                               
         SPACE                                                                  
SRCHCAMX DC    F'0'                END SEARCH ENTRIES                           
         SPACE 3                                                                
***********************************************************************         
*        FOLIO SEARCH ENTRY (REP)                                     *         
***********************************************************************         
         SPACE                                                                  
         ORG   *-4                 BACK OVER PREVIOUS END                       
SRCHFOL  DC    Y(SRCHFOLX-SRCHFOL) LENGTH OF ENTRY                              
         DC    CL8'REP'            SPARE                                        
         DC    CL6' '              NO OVERIDE SYSTEM                            
         DC    Y(RFORTNS-SRCHPRT)  DISP OF FOLIO ROUTINES                       
         DC    Y(PPNSKTYP-PPNSKEY) DISP OF TYPE 1/2 MODIFIER IN KEY             
         DC    AL1(1)              VALUE OF TYPE 1 MODIFIER                     
         DC    AL1(2)              VALUE OF TYPE 2 MODIFIER                     
         DC    Y(PPNSKWD1-PPNSKEY) DISP OF WORD/SEQNO PAIR IN KEY               
         DC    Y(L'PPNSKWD1)       LENGTH OF WORD.                              
         DC    Y(L'PPNSKSQ1)       LENGTH OF SEQNO (2 OR 3)                     
         DC    AL1((SDSRITYQ)+0+0) INDICATORS                                   
*                                  SEARCH TYPE IS PART OR EXACT MATCH           
*                                  DUPLICATE WORDS ARE DROPPED                  
*                                  WORD SEQUENCE IS NOT RELEVANT                
         DC    AL1(0)              MORE INDICATORS                              
         DC    AL1(0)              SEARCH ID NUMBER                             
         DC    AL1(0)              COUNTRY FILTER                               
         SPACE                                                                  
SRCHFOLX DC    F'0'                END SEARCH ENTRIES                           
         SPACE 3                                                                
***********************************************************************         
*        SUPPLIER SEARCH ENTRY (PUBLICATION)                          *         
***********************************************************************         
         SPACE                                                                  
         ORG   *-4                 BACK OVER PREVIOUS END                       
SRCHMSU  DC    Y(SRCHMSUX-SRCHMSU) LENGTH OF ENTRY                              
         DC    CL8'PUB'            SPARE                                        
         DC    CL6' '              NO OVERIDE SYSTEM                            
         DC    Y(RSURTNS-SRCHPRT)  DISP OF SUPPLIER ROUTINES                    
         DC    Y(PPNSKTYP-PPNSKEY) DISP OF TYPE 1/2 MODIFIER IN KEY             
         DC    AL1(1)              VALUE OF TYPE 1 MODIFIER                     
         DC    AL1(2)              VALUE OF TYPE 2 MODIFIER                     
         DC    Y(PPNSKWD1-PPNSKEY) DISP OF WORD/SEQNO PAIR IN KEY               
         DC    Y(L'PPNSKWD1)       LENGTH OF WORD.                              
         DC    Y(L'PPNSKSQ1)       LENGTH OF SEQNO (2 OR 3)                     
         DC    AL1((SDSRITYQ)+0+0) INDICATORS                                   
*                                  SEARCH TYPE IS PART OR EXACT MATCH           
*                                  DUPLICATE WORDS ARE DROPPED                  
*                                  WORD SEQUENCE IS NOT RELEVANT                
         DC    AL1(0)              MORE INDICATORS                              
         DC    AL1(0)              SEARCH ID NUMBER                             
         DC    AL1(0)              COUNTRY FILTER                               
         SPACE                                                                  
SRCHMSUX DC    F'0'                END SEARCH ENTRIES                           
         EJECT                                                                  
***********************************************************************         
*        SYSTEM KEY SET SUBROUTINE, FOR ALL MEDIA SEARCHES            *         
***********************************************************************         
         USING PPNSKEY,R2                                                       
RSYSKSET L     R2,4(,R1)           ADDRESS OF KEY                               
         XC    PPNSKEY,PPNSKEY                                                  
         MVI   PPNSKRCD,PPNSKRQ    RECORD CODE - BYTE 1                         
         MVI   PPNSKSRC,PPNSKSRQ   BYTE 2                                       
         MVC   PPNSKAGY,SBAGY      AGENCY ALPHA                                 
*                                                                               
         XC    DSCOMMON(DSCOMML),DSCOMMON                                       
*                                                                               
         L     RF,SBASRCH                                                       
         USING SDSRCHD,RF                                                       
         CLC   =C'PUB',SDSRCHCD    UNLESS PUB NAME SEARCH                       
         BE    GUDEXIT                                                          
         MVC   DSPRTDIR,=CL8'PRTDIR'  SWITCH TO PRTDIR AND FIL                  
         MVC   DSPRTFIL,=CL8'PRTFIL'                                            
         MVC   SBDODIRN,=Y(DSPRTDIR-SBASAVE)                                    
         MVC   SBDOFILN,=Y(DSPRTFIL-SBASAVE)                                    
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        COMMON SUBROUTINES                                           *         
***********************************************************************         
         SPACE                                                                  
***********************************************************************         
*        AQUIRE WORKING STORAGE                                       *         
*        R1=LENGTH OF STORAGE TO AQUIRE                               *         
*        R0=ADDRESS OF STORAGE AQUIRED ON EXIT                        *         
***********************************************************************         
R$GETWRK LA    R1,7(,R1)           ROUND TO NEXT DWRD                           
         SRL   R1,3                                                             
         SLL   R1,3                                                             
         L     RF,4(,RD)           GET PREVIOUS SAVE AREA                       
         LR    R0,RD               ADDRESS OF STORAGE AQUIRED                   
         AR    RD,R1               EXTEND STORAGE                               
         ST    RD,8(,RF)           SET NEW FORWARD POINTER                      
         ST    RF,4(,RD)           SAVE BACKWARD POINTER IN NEW SVAREA          
         BR    RE                                                               
         SPACE                                                                  
***********************************************************************         
*        FREE ALL "GETWRK'D" STORAGE                                  *         
***********************************************************************         
R$PUTWRK L     RF,4(,RD)           GET PREVIOUS SAVE AREA                       
         LA    RD,72(,RF)          RESET RD TO END OF IT                        
         CR    RD,RC               IS THIS OUR NMODDED W/S                      
         BNE   *+8                                                              
         LA    RD,(((SRCHWRKL+7)/8)*8)(,RD) ALLOW FOR IT IF IT IS               
         ST    RD,8(,RF)           RESET FORWARD POINTER                        
         ST    RF,4(,RD)           RESET BACKWARD POINTER                       
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*        MEDIA EXTRACT ROUTINE                                        *         
***********************************************************************         
R$GETMED NTR1                                                                   
         CLI   DSMEDCOD,0          HAVE WE ALREADY GOT MEDIA                    
         BNE   GUDEXIT             YES, FOLLY TO REPEAT                         
         MVI   SBERRFLD,1          INDICATE ID FIELD                            
         MVI   GTINDX-GTBLOCK+SBTXTBLK,3 MEDIA IS 3RD ID FIELD                  
         CLI   SBOTHER,C' '        HAVE WE GOT A MEDIA                          
         BNE   R$GETM10            YES                                          
         CLC   SBOTHER(L'SBOTHER-1),SBOTHER+1 IS WHOLE FIELD BLANK              
         BNE   EMIN                NO, INVALID MEDIA                            
         MVCDD SBOPTTXT(12),PP#MED ELSE MISSING MEDIA                           
         MVI   GTLTXT-GTBLOCK+SBTXTBLK,12                                       
         LA    RE,SBOPTTXT                                                      
         STCM  RE,B'0111',GTATXT-GTBLOCK+SBTXTBLK                               
         B     EMIFM                                                            
*                                                                               
R$GETM10 DS    0H                                                               
         LA    R1,512              GRAB STORAGE FOR SCANNER BLOCK               
         BAS   RE,R$GETWRK                                                      
         LR    R4,R0               BLOCK ADDRESS                                
*                                                                               
         L     RF,SBCOMFAC                                                      
         L     RF,CSCANNER-COMFACSD(,RF)                                        
         XC    WORK(8),WORK                                                     
         LA    R1,SBOTHER+L'SBOTHER-1                                           
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    R0,SBOTHER-1                                                     
         SR    R1,R0                                                            
         STC   R1,WORK+5           LENGTH                                       
         MVC   WORK+8(L'SBOTHER),SBOTHER                                        
         GOTO1 (RF),DMCB,WORK,(3,(R4)),C',=/ '                                  
         ZIC   R0,4(R1)                                                         
         CLI   4(R1),1                                                          
         BL    EIIF                SCANNER ERROR                                
         CLI   4(R1),3                                                          
         BH    ETMN                TOO MANY ITEMS                               
*                                  SET VALUES                                   
R$GETM12 DS    0H                                                               
         CLI   0(R4),0             END OF FIELDS                                
         BE    R$GETM20                                                         
         CLI   DSMEDCOD,C' '       MEDIA                                        
         BH    *+14                                                             
         MVC   DSMEDCOD,12(R4)                                                  
         B     R$GETM18                                                         
         CLI   DSCLICOD,C' '       CLIENT                                       
         BH    *+14                                                             
         MVC   DSCLICOD,12(R4)                                                  
         B     R$GETM18                                                         
         CLI   DSPRDCOD,C' '       PRODUCT                                      
         BH    *+14                                                             
         MVC   DSPRDCOD,12(R4)                                                  
         B     R$GETM18                                                         
*                                                                               
R$GETM18 DS    0H                                                               
         LA    R4,32(R4)           NEXT ENTRY                                   
         B     R$GETM12                                                         
*                                                                               
R$GETM20 DS    0H                                                               
         BAS   RE,R$PUTWRK         FREE SCANNER WORK                            
         LA    R2,WORK             ADDR OF KEY WORK AREA                        
         USING PAGYREC,R2                                                       
         XC    PAGYKEY,PAGYKEY                                                  
         MVC   PAGYKAGY,SBAGY      COPY AGENCY ALPHA                            
         CLI   SBAGY,0                                                          
         BE    EMIN                MEDIA INVALID IF AGENCY ZERO                 
         MVC   PAGYKMED,DSMEDCOD   COPY MEDIA CODE AS INPUT                     
         MVI   PAGYKRCD,1                                                       
R$GETM30 L     RF,SBCOMFAC                                                      
         L     RF,CDATAMGR-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,DMREAD,PRTDIR,(R2),WORK2                               
         TM    DMCB+8,X'10'                                                     
         BO    EMIN                MEDIA NOT ON FILE                            
         CLI   DMCB+8,0                                                         
         BNE   EIIO                                                             
         LA    R1,1024             GET CORE FOR GETREC                          
         BAS   RE,R$GETWRK                                                      
         LR    R3,R0                                                            
         L     RF,SBCOMFAC                                                      
         L     RF,CDATAMGR-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,GETREC,PRTFIL,WORK2+27,(R3),WORK                       
         CLI   DMCB+8,0                                                         
         BNE   EIIO                                                             
         LR    R2,R3               ADDRESS RECORD READ                          
         MVC   DSMEDNAM,PAGYMED    SAVE MEDIA NAME                              
         DROP  R2                                                               
         MVI   SBERRFLD,0                                                       
         MVI   GTINDX-GTBLOCK+SBTXTBLK,0                                        
         BAS   RE,R$PUTWRK                                                      
         B     GUDEXIT                                                          
         EJECT                                                                  
***********************************************************************         
*        CLIENT EXTRACT ROUTINE                                       *         
***********************************************************************         
R$GETCLI NTR1                                                                   
         LA    R2,WORK             ADDRESS OF KEY WORK AREA                     
         USING PCLTKEY,R2                                                       
         XC    PCLTKEY,PCLTKEY                                                  
R$GETC25 MVI   SBERRFLD,1          INDICATE ID FIELD                            
         MVI   GTINDX-GTBLOCK+SBTXTBLK,4 CLIENT IS 4TH ID FIELD                 
         CLI   DSCLICOD,C' '       DO WE HAVE CLIENT CODE?                      
         BH    R$GETC30                                                         
*                                                                               
         MVCDD SBOPTTXT(12),PP#CLI ERROR: MISSING CLIENT                        
         MVI   GTLTXT-GTBLOCK+SBTXTBLK,12                                       
         LA    RE,SBOPTTXT                                                      
         STCM  RE,B'0111',GTATXT-GTBLOCK+SBTXTBLK                               
         B     EMIFM                                                            
*                                                                               
R$GETC30 DS    0H                                                               
         MVC   PCLTKAGY,SBAGY      COPY IN AGY CODE                             
         MVC   PCLTKMED,DSMEDCOD   AND MEDIA                                    
         MVC   PCLTKCLT,DSCLICOD   AND CLIENT                                   
         MVI   PCLTKRCD,2                                                       
*                                                                               
         L     RF,SBCOMFAC                                                      
         L     RF,CDATAMGR-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,DMREAD,PRTDIR,(R2),WORK2                               
         TM    DMCB+8,X'10'                                                     
         BO    ECNF                CLIENT NOT ON FILE                           
         CLI   DMCB+8,0                                                         
         BNE   EIIO                                                             
         DROP  R2                                                               
         MVI   SBERRFLD,0                   REMOVE ERROR MESSAGE                
         MVI   GTINDX-GTBLOCK+SBTXTBLK,0    INDICATORS                          
         B     GUDEXIT                                                          
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        PRODUCT EXTRACT ROUTINE                                      *         
***********************************************************************         
R$GETPRO NTR1                                                                   
         LA    R2,WORK             ADDRESS OF KEY WORK AREA                     
         USING PPRDKEY,R2                                                       
*                                                                               
         XC    PPRDKEY,PPRDKEY                                                  
         MVC   PPRDKAGY,SBAGY      COPY IN AGY ALPHA                            
         MVC   PPRDKMED,DSMEDCOD   AND INSERT SAVED MEDIA                       
         MVI   SBERRFLD,1          INDICATE ID FIELD                            
         MVI   GTINDX-GTBLOCK+SBTXTBLK,5 PRODUCT IS 5TH ID FIELD                
*                                                                               
         CLI   DSPRDCOD,C' '       ANY PRODUCT CODE?                            
         BH    R$GETP30                                                         
*                                                                               
         MVCDD SBOPTTXT(12),PP#PRO ERROR: MISSING PRODUCT                       
         MVI   GTLTXT-GTBLOCK+SBTXTBLK,12                                       
         LA    RE,SBOPTTXT                                                      
         STCM  RE,B'0111',GTATXT-GTBLOCK+SBTXTBLK                               
         B     EMIFM                                                            
*                                                                               
R$GETP30 DS    0H                                                               
         MVC   PPRDKAGY,SBAGY      AGENCY                                       
         MVC   PPRDKMED,DSMEDCOD   MEDIA                                        
         MVC   PPRDKCLT,DSCLICOD   CLIENT                                       
         MVC   PPRDKPRD,DSPRDCOD   PRODUCT                                      
         MVI   PPRDKRCD,6                                                       
*                                                                               
         L     RF,SBCOMFAC                                                      
         L     RF,CDATAMGR-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,DMREAD,PRTDIR,(R2),WORK2                               
         TM    DMCB+8,X'10'                                                     
         BO    EPNF                PRODUCT NOT ON FILE                          
         CLI   DMCB+8,0                                                         
         BNE   EIIO                                                             
         DROP  R2                                                               
         MVI   SBERRFLD,0                   REMOVE ERROR MESSAGE                
         MVI   GTINDX-GTBLOCK+SBTXTBLK,0    INDICATORS                          
         B     GUDEXIT                                                          
         EJECT                                                                  
***********************************************************************         
*        FILTER COMMON VALIDATION                                     *         
*        R2=A(TWA FIELD INPUT)                                        *         
*        R3=A(TABLE OF FILTER KEYWORDS AND ROUTINE ADDRESSES)         *         
***********************************************************************         
R$FILTER NTR1                                                                   
         MVI   EQUALS,C'='                                                      
         MVI   COMMA,C','                                                       
*                                                                               
         GOTO1 ,DMCB,C'LU  ',INDD,OUTDD                                         
         MVI   DMCB+2,4            SET PRINTPAK SYSTEM                          
         L     RF,SBCOMFAC                                                      
         L     RF,CDICTATE-COMFACSD(,RF)                                        
         GOTO1 (RF)                                                             
*                                                                               
         MVI   GTINDX-GTBLOCK+SBTXTBLK,0                                        
         MVI   SBERRFLD,2          INDICATE KEY FIELD                           
         MVI   SBNNAMES,0          INDICATE NO NAMES FOUND YET                  
*                                                                               
         CLI   5(R2),0                                                          
         BE    EMIF                MUST BE SOMETHING INPUT                      
*                                                                               
         CLI   8(R2),C'?'                                                       
         BE    R$FILHLP                                                         
         CLI   5(R2),L'PP@HELP                                                  
         BH    R$FIL05             TOO LONG TO BE 'HELP'                        
         XR    R1,R1                                                            
         IC    R1,5(,R2)                                                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   8(0,R2),PP@HELP                                                  
         BE    R$FILHLP                                                         
*                                                                               
R$FIL05  LA    R1,512              GRAB STORAGE FOR SCANNER BLOCK               
         BAS   RE,R$GETWRK                                                      
         LR    R4,R0               BLOCK ADDRESS                                
*                                                                               
         L     RF,SBCOMFAC                                                      
         L     RF,CSCANNER-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,('SBNALENQ',(R2)),(9,(R4)),C',=  '                     
         ZIC   R0,4(R1)                                                         
         CLI   4(R1),1                                                          
         BL    EIIF                SCANNER ERROR                                
         BE    R$FIL10                                                          
         CLI   4(R1),8                                                          
         BH    ETMN                TOO MANY ITEMS                               
*                                                                               
R$FIL10  IC    R1,GTINDX-GTBLOCK+SBTXTBLK CHECK NEXT ITEM FROM SCANNER          
         LA    R1,1(R1)                                                         
         STC   R1,GTINDX-GTBLOCK+SBTXTBLK                                       
*                                                                               
         NC    0(2,R4),0(R4)                                                    
         BZ    R$FIL20             IGNORE NULL ENTRIES                          
         CLI   1(R4),0             IF NO RIGHT HALF,                            
         BNE   R$FIL12             ASSUME ITS NAME FIELD                        
         MVC   SBOPTTXT(L'PP@NAME),PP@NAME                                      
         MVI   GTLTXT-GTBLOCK+SBTXTBLK,L'PP@NAME                                
         B     R$FILNM                                                          
*                                                                               
R$FIL12  MVC   SBOPTTXT(10),12(R4)                                              
         MVC   GTLTXT-GTBLOCK+SBTXTBLK,0(R4)                                    
*                                                                               
         ZIC   R1,0(R4)            GET LENGTH OF KEYWORD                        
         SH    R1,=H'1'                                                         
         BM    EIKW                ERROR IF RIGHT SIDE WITHOUT LEFT             
*                                                                               
         LR    RE,R3               ADDRESS KWD TABLE                            
         USING FILTABD,RE                                                       
R$FIL15  CLI   0(RE),0                                                          
         BE    EIKW                NOT IN TABLE                                 
         CLM   R1,1,FILTLKWD       TEST INPUT LONGER THAN KEYWORD               
         BNL   R$FIL15C            YES, TRY NEXT                                
         XR    RF,RF                                                            
         ICM   RF,3,FILTYKWD       GET OFFSET OF KEYWORD                        
         AR    RF,RC                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   12(0,R4),0(RF)                                                   
         BE    *+12                                                             
R$FIL15C LA    RE,FILTABLQ(,RE)                                                 
         B     R$FIL15                                                          
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,FILTYRTN       GET OFFSET OF ROUTINE                        
         BZ    R$FILNM             IF NULL OFFSET, FIELD IS A NAME              
*                                                                               
R$FIL16  DS    0H                                                               
R$FIL18  BAS   RE,0(RB,RF)         GO TO IT                                     
         BNZ   EXIT                STRAIGHT OUT IF ANY ERROR                    
         DROP  RE                                                               
*                                                                               
R$FIL20  LA    R4,22+SBNALENQ(,R4) NEXT FILTER ENTRY                            
         MVI   GTLTXT-GTBLOCK+SBTXTBLK,0                                        
         BCT   R0,R$FIL10          LOOP TILL ALL DONE                           
*                                                                               
R$FIL30  MVI   GTINDX-GTBLOCK+SBTXTBLK,0 ALL DONE                               
         MVI   SBERRFLD,0                                                       
         B     GUDEXIT                                                          
         SPACE                                                                  
*        HANDLE A NAME FIELD IN FORM NAME=XXXXX OR JUST XXXXX                   
*                                                                               
R$FILNM  LA    RF,22(,R4)          ADDRESS NAME OPERAND                         
         ICM   RF,8,1(R4)          AND LENGTH                                   
         CLI   1(R4),0                                                          
         BNE   R$FILNM1            SKIP IF KEYWORD USED                         
         LA    RF,12(,R4)          ELSE ADDRESS NAME                            
         ICM   RF,8,0(R4)          AND LENGTH                                   
*                                                                               
R$FILNM1 CLM   RF,8,=AL1(L'PUBNAME) IS NAME TOO LONG                            
         BH    EFTL                                                             
*                                                                               
R$FILNM2 GOTO1 ,DMCB,(RA),(RF)                                                  
         MVI   DMCB,SBPARSKQ       KEY STYLE CALL                               
         GOTO1 SBPARSER            PARSE NAME                                   
         BZ    R$FIL20                     AL1(0),AL3(SEARCH BLOCK)             
         B     ETXTSET             PARSER FOUND AN ERROR                        
         SPACE                                                                  
*        FILTER HELP ROUTINE                                                    
*                                                                               
         USING FILTABD,R3                                                       
R$FILHLP ZIC   R4,0(R2)            LENGTH OF O/P FIELD                          
         SH    R4,=Y(8)                                                         
         TM    1(R4),X'02'                                                      
         BZ    *+8                                                              
         SH    R4,=Y(8)                                                         
         OI    6(R2),X'C0'         CURSOR + TRANSMIT                            
*                                                                               
         LA    R2,8(,R2)           START OF O/P FIELD                           
         MVI   WORK,0              INDICATE FIRST                               
*                                                                               
R$FILH10 CLI   0(R3),0             END OF FILTER TABLE                          
         BE    EHLP                                                             
*                                                                               
R$FILH14 LA    RE,WORK                                                          
         CLI   WORK,0              FIRST TIME, NO COMMA                         
         BE    *+14                                                             
         MVC   WORK(1),COMMA                                                    
         LA    RE,WORK+1                                                        
         XR    RF,RF                                                            
         ICM   RF,3,FILTYKWD       DISP TO KEYWORD                              
         AR    RF,RC                                                            
         LA    R1,3                MOVE 3 BYTES OF KEYWORD                      
         CLI   FILTLKWD,3          UNLESS ITS SHORTER                           
         BNL   *+8                                                              
         IC    R1,FILTLKWD         MAX LENGTH KEYWORD                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)       MOVE FILTER KEYWORD                          
         AR    RE,R1                                                            
         CLI   0(RE),C' '          LOOK FOR ACTUAL END                          
         BNE   *+8                                                              
         BCT   RE,*-8                                                           
         MVC   1(1,RE),EQUALS                                                   
         XR    RF,RF                                                            
         ICM   RF,3,FILTYHLP       GET OFFSET OF HELP                           
         BZ    *+10                                                             
         AR    RF,RB                                                            
         B     *+8                                                              
         LA    RF,R$FILNMH         IF NULL, FIELD IS A NAME                     
         XR    R1,R1                                                            
R$FILH15 IC    R1,2(,RF)           LENGTH OF TEXT                               
         CLI   0(RF),0             LOOK FOR ALL CTRY ENTRIES AT END             
         BE    R$FILH16                                                         
         CLC   SBCTRY,0(RF)        OR ONE FOR THIS COUNTRY                      
         BNE   R$FILH17                                                         
R$FILH16 CLI   1(RF),0             LOOK FOR ALL LANG ENTRY AT END               
         BE    R$FILH18                                                         
         CLC   SBLANG,1(RF)        OR ONE FOR THIS LANGUAGE                     
         BE    R$FILH18                                                         
R$FILH17 LA    RF,3(RF,R1)         ELSE KEEP LOOKING                            
         B     R$FILH15                                                         
R$FILH18 BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   2(0,RE),3(RF)       MOVE HELP AFTER FILTER KWORD                 
         LA    RE,3(RE,R1)         POINT TO END OF TEXT                         
         LA    RF,WORK                                                          
         SR    RE,RF                                                            
         CR    RE,R4               COMPARE WITH SPACE LEFT                      
         BNH   R$FILH19            NO ROOM FOR THIS ENTRY                       
         LTR   RE,R4               USE WHATS LEFT IF NOT ENOUGH                 
         BZ    EHLP                NO SPACE LEFT                                
R$FILH19 SR    R4,RE                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8              MOVE IT IN                                   
         B     *+10                                                             
         MVC   0(0,R2),WORK                                                     
         LA    R2,1(R2,RE)                                                      
*                                                                               
R$FILH20 LA    R3,FILTABLQ(,R3)    NEXT ENTRY                                   
         B     R$FILH10                                                         
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*        DIRECT STORAGE (ADDRESSED BY BASE/DISPLACEMENT)              *         
***********************************************************************         
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFIL   DC    CL8'PRTFIL'                                                      
PUBDIR   DC    CL8'PUBDIR'                                                      
PUBFIL   DC    CL8'PUBFIL'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
GETREC   DC    CL8'GETREC'                                                      
         SPACE 3                                                                
SUPAEXHD DC    A(SUPEXHD)          PUBLICATION EXTRACT HEADER                   
CLIAEXHD DC    A(CLIEXHD)          CLIENT EXTRACT HEADER                        
PROAEXHD DC    A(PROEXHD)          PRODUCT EXTRACT HEADER                       
CAMAEXHD DC    A(CAMEXHD)          CAMPAIGN EXTRACT HEADER                      
FOLAEXHD DC    A(FOLEXHD)          FOLIO EXTRACT HEADER                         
SUAFILT  DC    A(SUFILT)           SUPPLIER FILTER TABLE                        
CLIAFILT DC    A(CLFILT)           CLIENT FILTER TABLE                          
PROAFILT DC    A(PRFILT)           PRODUCT FILTER TABLE                         
CAMAFILT DC    A(CAFILT)           CAMPAIGN FILTER TABLE                        
FOLAFILT DC    A(FOFILT)           FOLIO FILTER TABLE                           
         SPACE 3                                                                
SPACES   DC    CL132' '                                                         
         SPACE 3                                                                
R$FILNMH DC    AL1(0,0,12),C'UPTO 5 NAMES'                                      
         SPACE 3                                                                
SYSHD    DC    C'PRINTPAK,'    SYSTEM ID                                        
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
INDD     DS    0C                                                               
         DCDDL PP#HELP,4                                                        
         DCDDL PP#NAME,4                                                        
         DCDDL PP#FREQ,9                                                        
         DCDDL PP#EDIT,7                                                        
         DCDDL PP#ZONE,4                                                        
         DCDDL PP#DATE,4                                                        
         DCDDL PP#CLI,6                                                         
         DCDDL PP#PRD,7                                                         
         DCDDL PP#EST,8                                                         
         DCDDL PP#PUB,3                                                         
         DCDDL PP#REP,3                                                         
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
*        MEDIA SYSTEM APPLICATION SUBROUTINES                         *         
*        R8 = BASE REGISTER FOR EACH ROUTINE                          *         
***********************************************************************         
         SPACE                                                                  
************************************************                                
*        MEDIA SEARCH PASSIVE POINTER ROUTINES *                                
************************************************                                
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RPASSIVE L     R2,4(,R1)           RECORD ADDRESS/POINTER ADDRESS               
         CLI   SBMODE,0            MODE 0 IS TEST PASSIVES                      
         BNE   RPASR               ELSE TEST RECORD                             
*                                                                               
         USING PPNSKEY,R2                                                       
         CLI   PPNSKRCD,PPNSKRQ   IS IT NAME SEARCH PASSIVE                     
         BNE   EXIT                                                             
         CLI   PPNSKSRC,PPNSKSRQ   TEST 2ND BYTE TOO                            
         DROP  R2                                                               
         B     EXIT                EXIT CC EQ IF IT IS, ELSE NEQ                
*                                                                               
RPASR    CLI   SBMODE,1                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         LM    R3,R4,8(R1)         PASSIVE KEY AND NAME AREA ADDRESSES          
         LR    R5,R1               SAVE PARMS ADDRESS                           
         MVI   12(R5),0            PRESET NAME LENGTH                           
         USING PPNSKEY,R3                                                       
         XC    PPNSKEY(31),PPNSKEY   SET COMMON PART OF PASSIVE KEY             
         MVI   PPNSKRCD,PPNSKRQ    SET GENERAL NAME SEARCH PASSIVE TYPE         
         MVI   PPNSKSRC,PPNSKSRQ   AND 2ND BYTE                                 
         OI    PPNSCNTL,X'01'      GENERAL PRINT PASSIVE INDICATOR              
*                                                                               
*              NOTE- PUBFILE VS PRTFILE. PUBFILE RECS WILL ALWAYS               
*                    HAVE LESS THAT 'A' AT +1. HOKEY BUT CONVENIENT.            
*                                                                               
         CLI   1(R2),C'A'          TEST PUBFILE                                 
         BL    RPAS4               YES                                          
         MVC   PPNSKAGY(3),0(R2)   NO, COPY PRTFILE AGY/MED                     
         MVC   PPNSKREC,3(R2)      RECORD TYPE                                  
*                                                                               
         TM    SBCALLER,SBCALLDQ   IF LOAD/DUMP CALL SKIP FILE NAME             
         BO    RPAS5               OVERRIDES                                    
         MVC   DSPRTDIR,=CL8'PRTDIR'  SWITCH TO PRTDIR AND FIL                  
         MVC   DSPRTFIL,=CL8'PRTFIL'                                            
         MVC   SBDODIRN,=Y(DSPRTDIR-SBASAVE)                                    
         MVC   SBDOFILN,=Y(DSPRTFIL-SBASAVE)                                    
*                                                                               
         B     RPAS5                                                            
*                                                                               
RPAS4    DS    0H                  PUB RECORD                                   
         CLI   9(R2),X'81'         ONLY 81 TYPE RECS                            
         BNE   RPASNO                                                           
         MVC   PPNSKAGY(3),7(R2)   COPY AGENCY                                  
         MVC   PPNSKMED(1),0(R2)   MEDIA                                        
         MVI   PPNSKREC,0          REC TYPE IS NULL FOR PUB                     
         B     RPASSU                                                           
         DROP  R3                                                               
*                                                                               
RPAS5    DS    0H                                                               
         CLI   3(R2),2             CLIENT RECORD                                
         BE    RPASCL                                                           
         CLI   3(R2),6             PRODUCT RECORD                               
         BE    RPASPR                                                           
         CLI   3(R2),7             CAMPAIGN RECORD (EST)                        
         BE    RPASCA                                                           
         CLI   3(R2),17            FOLIO RECORD (REP)                           
         BE    RPASFO                                                           
*                                                                               
RPASNO   CLI   *,255               SET CC LOW (-VE) IF NOT SEARCHABLE           
         B     EXIT                                                             
*                                                                               
         USING PPNSKEY,R3                                                       
RPASYES  DS    0H                                                               
         TM    SBCALLER,SBCALLDQ   IF NOT LOAD/DUMP CALL SKIP SAME SET          
         BZ    GUDEXIT             TEST (SBASAVE USED)                          
         CLC   PPNSKEY(PPNSKTYP-PPNSKEY),SBASAVE   TEST SAME SET                
         MVC   SBASAVE(L'PPNSKEY),PPNSKEY    SAVE THIS KEY                      
         BE    GUDEXIT             RETURN CC EQUAL IF YES                       
         CLI   *,0                 RETURN CC HIGH (-VE) IF NEW SET              
         B     EXIT                                                             
         DROP  R3                                                               
*                                                                               
***********************************************************************         
* RECORD IS SUPPLIER RECORD                                           *         
***********************************************************************         
         USING PUBREC,R2                                                        
RPASSU   DS    0H                                                               
         LA    R9,SRCHMSU          SUPPLIER SEARCH                              
         ST    R9,SBASRCH                                                       
*                                                                               
         LR    R1,R4                                                            
         CLI   PUBKMED,C'N'        FOR NEWSPAPERS                               
         BNE   RPASSU6                                                          
         CLI   PUBCITY,C' '        DO CITY NAME IF THERE                        
         BNH   RPASSU6                                                          
         MVC   0(L'PUBCITY,R1),PUBCITY                                          
         LA    R1,L'PUBCITY-1(R1)                                               
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         MVI   1(R1),C' '                                                       
         LA    R1,2(R1)                                                         
*                                                                               
RPASSU6  DS    0H                                                               
         CLI   PUBNAME,C' '                                                     
         BNH   RPASNO                                                           
         MVC   0(L'PUBNAME,R1),PUBNAME                                          
         LA    R1,L'PUBNAME-1(R1)                                               
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         CLI   PUBZNAME,C' '                                                    
         BNH   RPASSU8                                                          
         MVI   1(R1),C' '                                                       
         LA    R1,2(R1)                                                         
         MVC   0(L'PUBZNAME,R1),PUBZNAME                                        
         LA    R1,L'PUBZNAME-1(R1)                                              
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
RPASSU8  DS    0H                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R4                                                            
         STC   R1,12(R5)           SET LENGTH OF TEXT                           
         B     RPASYES             GO TO GENERAL PASSIVE CODE                   
**********************************************************************          
*        RECORD IS CLIENT RECORD                                     *          
**********************************************************************          
         USING PCLTREC,R2                                                       
*                                                                               
RPASCL   DS    0H                                                               
         LA    R9,SRCHCLI          CLIENT SEARCH                                
         ST    R9,SBASRCH                                                       
*                                                                               
         B     RPASNO              NO-OP CLIENT                                 
*                                                                               
         CLI   PCLTNAME,C' '                                                    
         BNH   RPASNO                                                           
         LR    R1,R4                                                            
         MVC   0(L'PCLTNAME,R1),PCLTNAME                                        
         LA    R1,L'PCLTNAME-1(R1)                                              
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R4                                                            
         STC   R1,12(R5)           SET LENGTH OF TEXT                           
         B     RPASYES             GO TO GENERAL PASSIVE CODE                   
**********************************************************************          
*        RECORD IS PRODUCT RECORD                                    *          
**********************************************************************          
         USING PPRDREC,R2                                                       
*                                                                               
RPASPR   DS    0H                                                               
         USING PPNSKEY,R3                                                       
RPASPR10 LA    R9,SRCHPRO          PRODUCT SEARCH                               
         ST    R9,SBASRCH                                                       
*                                                                               
         B     RPASNO              NO-OP PRODUCT                                
*                                                                               
         MVC   PPNSKCLT(3),PPRDKCLT  INSERT CLIENT NAME IN SRCH KEY             
         CLI   PPRDNAME,C' '                                                    
         BNH   RPASNO                                                           
         LR    R1,R4                                                            
         MVC   0(L'PPRDNAME,R1),PPRDNAME                                        
         LA    R1,L'PPRDNAME-1(R1)                                              
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R4                                                            
         STC   R1,12(R5)           SET LENGTH OF TEXT                           
         B     RPASYES             GO TO GENERAL PASSIVE CODE                   
***********************************************************************         
*        RECORD IS CAMPAIGN RECORD                                    *         
***********************************************************************         
         USING PESTREC,R2                                                       
*                                                                               
RPASCA   DS    0H                                                               
         USING PPNSKEY,R3                                                       
RPASCA10 LA    R9,SRCHCAM          CAMPAIGN SEARCH                              
         ST    R9,SBASRCH                                                       
*                                                                               
         B     RPASNO              NO-OP CAMPAIGN                               
*                                                                               
         MVC   PPNSKCLT,PESTKCLT   INSERT CLIENT CODE IN SEARCH KEY             
         MVC   PPNSKPRD,PESTKPRD   INSERT PRODUCT CODE IN SEARCH KEY            
         CLI   PESTNAME,C' '                                                    
         BNH   RPASNO                                                           
         LR    R1,R4                                                            
         MVC   0(L'PESTNAME,R1),PESTNAME                                        
         LA    R1,L'PESTNAME-1(R1)                                              
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R4                                                            
         STC   R1,12(R5)           SET LENGTH OF TEXT                           
         B     RPASYES             GO TO GENERAL PASSIVE CODE                   
         DROP  R3                                                               
         SPACE 1                                                                
***********************************************************************         
*        RECORD IS FOLIO RECORD                                       *         
***********************************************************************         
         SPACE 1                                                                
         USING PREPREC,R2                                                       
*                                                                               
RPASFO   DS    0H                                                               
         LA    R9,SRCHFOL          FOLIO SEARCH                                 
         ST    R9,SBASRCH                                                       
         CLI   PREPNAME,C' '                                                    
         BNH   RPASNO                                                           
         LR    R1,R4                                                            
         MVC   0(L'PREPNAME,R1),PREPNAME                                        
         LA    R1,L'PREPNAME-1(R1)                                              
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R4                                                            
         STC   R1,12(R5)           SET LENGTH OF TEXT                           
         B     RPASYES             GO TO GENERAL PASSIVE CODE                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        CLIENT SEARCH SUBROUTINES, ENTERED VIA BRANCH TABLE          *         
***********************************************************************         
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RCLRTNS  B     RCLKSET             KEY SET                                      
         B     RCLSECH             SECURITY CHECK                               
         B     RCLFILT             FILTER                                       
         B     RCLEXTR             RECORD EXTRACT                               
         SPACE                                                                  
***********************************************************************         
*        CLIENT SEARCH KEY SET SUBROUTINE                             *         
***********************************************************************         
RCLKSET  L     R2,4(,R1)           ADDRESS OF KEY                               
         USING PPNSKEY,R2                                                       
         BAS   RE,R$GETMED         GO AND GET MEDIA                             
         BNZ   EXIT                EXIT IF ERROR                                
*                                                                               
RCLKSMOK MVC   PPNSKMED,DSMEDCOD   SET MEDIA IN KEY                             
*                                                                               
         MVI   PPNSKREC,2          SET PARENT RECORD TYPE = CLIENT              
*                                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
*        CLIENT SEARCH SECURITY CHECK SUBROUTINE                      *         
***********************************************************************         
RCLSECH  DS    0H                                                               
         B     GUDEXIT                                                          
         SPACE 2                                                                
***********************************************************************         
*        CLIENT SEARCH FILTER SUBROUTINE                              *         
***********************************************************************         
RCLFILT  L     R2,4(,R1)           RECORD ADDRESS/KEY FIELD ADDRESS             
         CLI   SBMODE,1                                                         
         BE    RCLFD               DIRECTORY FILTER                             
         BL    RCLFI               INITIALIZE FILTERS                           
         CLI   SBMODE,2                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
         USING PCLTREC,R2                                                       
RCLFF    B     GUDEXIT             NO FILTERS                                   
RCLFD    B     GUDEXIT                                                          
*                                                                               
         DROP  R2                                                               
         SPACE                                                                  
RCLFI    XC    DSCLFILT(DSCLFLNQ),DSCLFILT  CLEAR CLIENT FILTERS                
         MVI   SBLFILT,DSCLFLNQ    SET LENGTH CLIENT FILTER AREA                
         L     R3,CLIAFILT         ADDRESS CLIENT FILTER TABLE                  
         A     R3,RELO                                                          
         BAS   RE,R$FILTER         STANDARD FILTER PROCESSOR                    
         B     EXIT                EXIT WITH RETURNED CC SET                    
         EJECT                                                                  
***********************************************************************         
*        CLIENT EXTRACT SUBROUTINE                                    *         
***********************************************************************         
RCLEXTR  CLI   SBMODE,1                                                         
         BE    RCLEXD              EXTRACT DETAIL LINE                          
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RCLEXH   LA    R2,SBEXHEAD                                                      
         L     RE,RELO                                                          
         A     RE,CLIAEXHD         RETURN CLIENT HEADER DETAILS                 
         MVC   0(CLILEXHD,R2),0(RE)                                             
RCLIXH1  LA    RE,CLIEXTT-CLIEXHD(,R2)                                          
         MVC   0(L'SYSHD,RE),SYSHD                                              
         MVC   L'SYSHD+1(8,RE),=C'CLIENTS,'                                     
         MVC   L'SYSHD+8+2(10,RE),DSMEDNAM                                      
         B     GUDEXIT                                                          
         SPACE                                                                  
         USING PCLTREC,R3                                                       
RCLEXD   LM    R2,R3,4(R1)         OUTPUT AREA/RECORD AREA                      
         MVC   0(54,R2),SPACES     CLEAR ANY RUBBISH OUT                        
*                                                                               
         MVC   0(3,R2),PCLTKCLT    CLIENT CODE                                  
         MVC   7(L'PCLTNAME,R2),PCLTNAME   FULL NAME                            
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        PRODUCT SEARCH SUBROUTINES,ENTERED VIA BRANCH TABLE          *         
***********************************************************************         
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RPRRTNS  B     RPRKSET             KEY SET                                      
         B     RPRSECH             SECURITY CHECK                               
         B     RPRFILT             FILTER                                       
         B     RPREXTR             RECORD EXTRACT                               
         SPACE                                                                  
***********************************************************************         
*        PRODUCT SEARCH KEY SET SUBROUTINE                            *         
***********************************************************************         
RPRKSET  L     R2,4(,R1)           ADDRESS OF KEY                               
         USING PPNSKEY,R2                                                       
         BAS   RE,R$GETMED         GO AND GET MEDIA                             
         BNZ   EXIT                EXIT IF ERROR                                
         BAS   RE,R$GETCLI         GO AND GET CLIENT                            
         BNZ   EXIT                EXIT IF ERROR                                
*                                                                               
RPRKSMOK MVC   PPNSKMED,DSMEDCOD   SET MEDIA IN KEY                             
         MVC   PPNSKCLT,DSCLICOD   SET CLIENT IN KEY                            
*                                                                               
         MVI   PPNSKREC,6          SET PARENT RECORD TYPE = PRODUCT             
*                                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        PRODUCT SEARCH SECURITY CHECK SUBROUTINE                     *         
***********************************************************************         
RPRSECH  DS    0H                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
***********************************************************************         
*        PRODUCT SEARCH FILTER SUBROUTINE                             *         
***********************************************************************         
RPRFILT  L     R2,4(,R1)           RECORD ADDRESS/KEY FIELD ADDRESS             
         CLI   SBMODE,1                                                         
         BE    RPRFD               DIRECTORY FILTER                             
         BL    RPRFI               INITIALIZE FILTERS                           
         CLI   SBMODE,2                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
         USING PPRDREC,R2                                                       
RPRFD    B     GUDEXIT             NO FILTERS                                   
RPRFF    B     GUDEXIT                                                          
         DROP  R2                                                               
         SPACE                                                                  
RPRFI    XC    DSPRFILT(DSPRFLNQ),DSPRFILT  CLEAR PRODUCT FILTERS               
         MVI   SBLFILT,DSPRFLNQ    SET LENGTH PRODUCT FILTER AREA               
         L     R3,PROAFILT         ADDRESS PRODUCT FILTER TABLE                 
         A     R3,RELO                                                          
         BAS   RE,R$FILTER         STANDARD FILTER PROCESSOR                    
         B     EXIT                EXIT WITH RETURNED CC SET                    
*                                                                               
***********************************************************************         
*        PRODUCT EXTRACT SUBROUTINE                                   *         
***********************************************************************         
RPREXTR  CLI   SBMODE,1                                                         
         BE    RPREXD              EXTRACT DETAIL LINE                          
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RPREXH   LA    R2,SBEXHEAD                                                      
         L     RE,RELO                                                          
         A     RE,PROAEXHD         RETURN PRODUCT HEADER DETAILS                
         MVC   0(PROLEXHD,R2),0(RE)                                             
RPROXH1  LA    RE,PROEXTT-PROEXHD(,R2)                                          
         MVC   0(L'SYSHD,RE),SYSHD                                              
         MVC   L'SYSHD+1(9,RE),=C'PRODUCTS,'                                    
         MVC   L'SYSHD+9+2(10,RE),DSMEDNAM                                      
         B     GUDEXIT                                                          
         SPACE                                                                  
         USING PPRDREC,R3                                                       
RPREXD   LM    R2,R3,4(R1)         OUTPUT AREA/RECORD AREA                      
         MVC   0(48,R2),SPACES     CLEAR CONTENTS OF DISPLAY AREA               
*                                                                               
         MVC   0(3,R2),PPRDKPRD    PRODUCT CODE                                 
         MVC   8(L'PPRDNAME,R2),PPRDNAME  FULL NAME                             
         B     GUDEXIT                                                          
         DROP  R3                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        CAMPAIGN SEARCH SUBROUTINES,ENTERED VIA BRANCH TABLE         *         
***********************************************************************         
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RCARTNS  B     RCAKSET             KEY SET                                      
         B     RCASECH             SECURITY CHECK                               
         B     RCAFILT             FILTER                                       
         B     RCAEXTR             RECORD EXTRACT                               
         SPACE                                                                  
***********************************************************************         
*        CAMPAIGN SEARCH KEY SET SUBROUTINE                           *         
***********************************************************************         
RCAKSET  L     R2,4(,R1)           ADDRESS OF KEY                               
         USING PPNSKEY,R2                                                       
         BAS   RE,R$GETMED         GO AND GET MEDIA                             
         BNZ   EXIT                EXIT IF ERROR                                
*                                                                               
RCAKSMOK MVC   PPNSKMED,DSMEDCOD   SET MEDIA IN KEY                             
         BAS   RE,R$GETCLI         GO FETCH CLIENT                              
         BNZ   EXIT                EXIT IF ERROR                                
         MVC   PPNSKCLT,DSCLICOD   SET CLIENT IN KEY                            
         BAS   RE,R$GETPRO         ALSO NEED PRODUCT                            
         BNZ   EXIT                EXIT IF ERROR HERE                           
         MVC   PPNSKPRD,DSPRDCOD   SET PRODUCT IN KEY                           
*                                                                               
         MVI   PPNSKREC,7          RECORD TYPE                                  
*                                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        CAMPAIGN SEARCH SECURITY CHECK SUBROUTINE                    *         
***********************************************************************         
RCASECH  EQU   GUDEXIT             DOESN'T HAVE ONE                             
         SPACE                                                                  
***********************************************************************         
*        CAMPAIGN SEARCH FILTER SUBROUTINE                            *         
***********************************************************************         
RCAFILT  L     R2,4(,R1)           RECORD ADDRESS/KEY FIELD ADDRESS             
         CLI   SBMODE,1                                                         
         BE    RCAFD               DIRECTORY FILTER                             
         BL    RCAFI               INITIALIZE FILTERS                           
         CLI   SBMODE,2                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RCAFF    B     GUDEXIT             NO FILTERS FOR NOW                           
RCAFD    B     GUDEXIT                                                          
         SPACE                                                                  
RCAFI    MVI   SBLFILT,0           NO CAMPAIGN FILTERS AS YET                   
         L     R3,CAMAFILT         ADDRESS CAMPAIGN FILTER TABLE                
         A     R3,RELO                                                          
         BAS   RE,R$FILTER         STANDARD FILTER PROCESSOR                    
         B     EXIT                EXIT WITH RETURNED CC SET                    
         SPACE                                                                  
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        CAMPAIGN EXTRACT SUBROUTINE                                  *         
***********************************************************************         
RCAEXTR  CLI   SBMODE,1                                                         
         BE    RCAEXD              EXTRACT DETAIL LINE                          
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RCAEXH   LA    R2,SBEXHEAD                                                      
         L     RE,RELO                                                          
         A     RE,CAMAEXHD         RETURN CAMPAIGN HEADER DETAILS               
         MVC   0(CAMLEXHD,R2),0(RE)                                             
RCAMXH1  LA    RE,CAMEXTT-CAMEXHD(,R2)                                          
         MVC   0(L'SYSHD,RE),SYSHD                                              
         MVC   L'SYSHD+1(10,RE),=C'ESTIMATES,'                                  
         MVC   L'SYSHD+10+2(10,RE),DSMEDNAM                                     
         B     GUDEXIT                                                          
         SPACE                                                                  
         USING PESTREC,R3                                                       
RCAEXD   LM    R2,R3,4(R1)         OUTPUT AREA/RECORD AREA                      
         MVC   0(49,R2),SPACES     CLEAR OUT DISPLAY AREA                       
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,PESTKEST                                                    
         EDIT  (RE),(3,0(R2)),FILL=0                                            
         MVC   9(L'PESTNAME,R2),PESTNAME   FULL NAME                            
*                                                                               
         MVC   FULL(2),PESTST                                                   
         MVC   FULL+2(2),PESTEND                                                
         L     RF,SBCOMFAC                                                      
         L     RF,CDATCON-COMFACSD(RF)                                          
         GOTO1 (RF),DMCB,(X'12',FULL),(8,30(R2))                                
         B     GUDEXIT                                                          
         DROP  R3                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOLIO SEARCH SUBROUTINES, ENTERED VIA BRANCH TABLE           *         
***********************************************************************         
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RFORTNS  B     RFOKSET             KEY SET                                      
         B     RFOSECH             SECURITY CHECK                               
         B     RFOFILT             FILTER                                       
         B     RFOEXTR             RECORD EXTRACT                               
         SPACE                                                                  
***********************************************************************         
*        FOLIO SEARCH KEY SET SUBROUTINE                              *         
***********************************************************************         
RFOKSET  LM    R2,R3,4(R1)         ADDRESS OF KEYS                              
         USING PPNSKEY,R2                                                       
         BAS   RE,R$GETMED         GO AND GET MEDIA                             
         BNZ   EXIT                EXIT IF ERROR                                
*                                                                               
RFOKSMOK MVC   PPNSKMED,DSMEDCOD   SET MEDIA IN KEY                             
*                                                                               
         MVI   PPNSKREC,17         SET PARENT RECORD TYPE = FOLIO               
*                                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        FOLIO SEARCH SECURITY CHECK SUBROUTINE                       *         
***********************************************************************         
RFOSECH  EQU   GUDEXIT             NO SECURITY NEEDED                           
*                                                                               
***********************************************************************         
*        FOLIO SEARCH FILTER SUBROUTINE                               *         
***********************************************************************         
RFOFILT  L     R2,4(,R1)           RECORD ADDRESS/KEY FIELD ADDRESS             
         CLI   SBMODE,1                                                         
         BE    RFOFD               DIRECTORY FILTER                             
         BL    RFOFI               INITIALIZE FILTERS                           
         CLI   SBMODE,2                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RFOFF    B     GUDEXIT             NO FILTERS                                   
RFOFD    B     GUDEXIT                                                          
*                                                                               
RFOFI    MVI   SBLFILT,0           NO FILTERS YET                               
         L     R3,FOLAFILT         ADDRESS FOLIO FILTER TABLE                   
         A     R3,RELO                                                          
         BAS   RE,R$FILTER         STANDARD FILTER PROCESSOR                    
         B     EXIT                EXIT WITH RETURNED CC SET                    
         SPACE                                                                  
         EJECT                                                                  
*                                                                               
***********************************************************************         
*        FOLIO EXTRACT SUBROUTINE                                     *         
***********************************************************************         
RFOEXTR  CLI   SBMODE,1                                                         
         BE    RFOLXD              EXTRACT DETAIL LINE                          
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RFOEXH   LA    R2,SBEXHEAD                                                      
         L     RE,RELO                                                          
         A     RE,FOLAEXHD         RETURN FOLIO HEADER DETAILS                  
         MVC   0(FOLLEXHD,R2),0(RE)                                             
RFOLXH1  LA    RE,FOLEXTT-FOLEXHD(,R2)                                          
         MVC   0(L'SYSHD,RE),SYSHD                                              
         MVC   L'SYSHD+1(5,RE),=C'REPS,'                                        
         MVC   L'SYSHD+5+2(10,RE),DSMEDNAM                                      
         B     GUDEXIT                                                          
         SPACE                                                                  
         USING PREPREC,R3                                                       
RFOLXD   LM    R2,R3,4(R1)         OUTPUT AREA/RECORD AREA                      
         MVC   0(46,R2),SPACES     CLEAR ANY RUBBISH OUT                        
*                                                                               
         MVC   0(4,R2),PREPKREP    FOLIO CODE                                   
         MVC   5(L'PREPNAME,R2),PREPNAME   FULL NAME                            
         B     GUDEXIT                                                          
         DROP  R3                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        SUPPLIER SEARCH SUBROUTINES, ENTERED VIA BRANCH TABLE        *         
***********************************************************************         
         SPACE                                                                  
         DS    0D                                                               
         USING *,R8                                                             
RSURTNS  B     RSUKSET             KEY SET                                      
         B     RSUSECH             SECURITY CHECK                               
         B     RSUFILT             FILTER                                       
         B     RSUEXTR             RECORD EXTRACT                               
         SPACE                                                                  
***********************************************************************         
*        SUPPLIER SEARCH KEY SET SUBROUTINE                           *         
***********************************************************************         
RSUKSET  L     R2,4(,R1)           ADDRESS OF KEY                               
         USING PPNSKEY,R2                                                       
         BAS   RE,R$GETMED         GO AND GET MEDIA                             
         BNZ   EXIT                EXIT IF ERROR                                
*                                                                               
RSUKSMOK MVC   PPNSKMED,DSMEDCOD   SET MEDIA IN KEY                             
         MVI   PPNSKREC,0          SET PARENT RECORD TYPE = SUPPLIER            
*                                                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        SUPPLIER SEARCH SECURITY CHECK SUBROUTINE                    *         
***********************************************************************         
RSUSECH  EQU   GUDEXIT             DOESN'T HAVE ONE                             
         SPACE                                                                  
***********************************************************************         
*        SUPPLIER SEARCH FILTER SUBROUTINE                            *         
***********************************************************************         
RSUFILT  L     R2,4(,R1)           RECORD ADDRESS/KEY FIELD ADDRESS             
         CLI   SBMODE,1                                                         
         BE    RSUFD               DIRECTORY FILTER                             
         BL    RSUFI               INITIALIZE FILTERS                           
         CLI   SBMODE,2                                                         
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
         USING PUBREC,R2                                                        
RSUFD    DS    0H                  DIRECTORY FILTERS                            
*                                  NONE                                         
RSUFF    DS    0H                  FILE FILTERS                                 
         NC    DSSUFILT(DSSUFLNQ),DSSUFILT ANY FILTERS ?                        
         BZ    GUDEXIT             NO                                           
*                                                                               
         CLI   DSSUFEDT,0          ANY EDITION FILTER?                          
         BE    RSUFF3                                                           
         CLC   PUBKED,DSSUFEDT                                                  
         BE    RSUFF3                                                           
         CLI   DSSUFEDT,X'FF'      FF=NO EDIT                                   
         BNE   EFLT                                                             
         CLI   PUBKED,0                                                         
         BNE   EFLT                                                             
*                                                                               
RSUFF3   DS    0H                                                               
         CLI   DSSUFZON,0          ANY ZONE FILTER?                             
         BE    RSUFF4                                                           
         CLC   PUBKZON,DSSUFZON                                                 
         BE    RSUFF4                                                           
         CLI   DSSUFZON,X'FF'      FF=NO ZONE                                   
         BNE   EFLT                                                             
         CLI   PUBKZON,0                                                        
         BNE   EFLT                                                             
*                                                                               
RSUFF4   DS    0H                                                               
         CLI   DSSUFFRQ,0          ANY FREQ FILTER?                             
         BE    RSUFF5                                                           
         LA    R3,PUBNAMEL                                                      
*                                                                               
RSUFF4B  DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0                                                          
         BE    EFLT                                                             
         CLI   0(R3),X'20'         PUBGENEL                                     
         BE    RSUFF4D                                                          
         CLI   0(R3),X'21'         PUBSPREL                                     
         BNE   RSUFF4B                                                          
*                                                                               
         CLI   PUBKMED,C'N'        FOR NEWS USE PUBSPREL                        
         BNE   RSUFF4B                                                          
         USING PUBSPREL,R3                                                      
         CLC   PUBFREQ,DSSUFFRQ                                                 
         BNE   EFLT                                                             
         B     RSUFF5                                                           
*                                                                               
RSUFF4D  DS    0H                                                               
         CLI   PUBKMED,C'N'        FOR NON-NEWS USE PUBGENEL                    
         BE    RSUFF4B                                                          
         USING PUBGENEL,R3                                                      
         CLC   PUBMFREQ,DSSUFFRQ                                                
         BNE   EFLT                                                             
         DROP  R3                                                               
*                                                                               
RSUFF5   DS    0H                                                               
         B     GUDEXIT             ACCEPT RECORD                                
         DROP  R2                                                               
         SPACE                                                                  
RSUFI    XC    DSSUFILT(DSSUFLNQ),DSSUFILT INITIALIZE  FILTERS                  
         MVI   SBLFILT,DSSUFLNQ    SET LENGTH SUPPLIER FILTER AREA.             
         L     R3,SUAFILT          ADDRESS SUPPLIER FILTER TABLE                
         A     R3,RELO                                                          
         BAS   RE,R$FILTER         STANDARD FILTER PROCESSOR                    
         B     EXIT                EXIT WITH RETURNED CC SET                    
         SPACE                                                                  
RSUFFOL  NTR1  ,                   FOLIO FILTER VALIDATION                      
*                                  (**NOT USED**)                               
         CLI   DSSUFFOL,0          TEST ALREADY GOT FOLIO FILTER                
         BNE   EDKW                                                             
         CLI   1(R4),4             FOLIOS ARE UP TO 4 BYTES                     
         BH    EFTL                                                             
*                                                                               
         LA    R2,WORK             ADDRESS OF KEY WORK AREA                     
         USING PREPREC,R2                                                       
         XC    PREPKEY,PREPKEY                                                  
         MVC   PREPKAGY,SBAGY      AGENCY                                       
         OC    PREPKMED,DSMEDCOD   MEDIA                                        
         MVI   PREPKRCD,17         RECORD TYPE                                  
         MVC   PREPKREP,22(R4)     REP CODE                                     
         DROP  R2                                                               
*                                                                               
         L     RF,SBCOMFAC                                                      
         L     RF,CDATAMGR-COMFACSD(,RF)                                        
         GOTO1 (RF),DMCB,DMREAD,PRTDIR,(R2),WORK2                               
         TM    DMCB+8,X'10'                                                     
         BO    EFOL                FOLIO NOT ON FILE                            
         CLI   DMCB+8,0                                                         
         BNE   EIIO                                                             
         MVC   DSSUFFOL,22(R4)                                                  
         B     GUDEXIT                                                          
         SPACE                                                                  
RSUFFRQ  NTR1  ,                   FREQUENCY FILTER VALIDATION                  
         CLI   DSSUFFRQ,0          TEST ALREADY GOT FREQUENCY FILTER            
         BNE   EDKW                                                             
         CLI   1(R4),2             FREQUENCY IS 1 OR 2 BYTES                    
         BH    EFTL                                                             
         MVC   DSSUFFRQ,22(R4)                                                  
         B     GUDEXIT                                                          
         SPACE                                                                  
RSUFEDT  NTR1  ,                   EDITION FILTER VALIDATION                    
         CLI   DSSUFEDT,0          TEST ALREADY HAD                             
         BNE   EDKW                                                             
         CLI   1(R4),3             EDIT IS 1-3 BYTES                            
         BH    EFTL                                                             
         CLC   =C'NO',22(R4)                                                    
         BNE   *+12                                                             
         MVI   DSSUFEDT,X'FF'      FF MEANS NO EDITION                          
         B     GUDEXIT                                                          
*                                                                               
         MVC   WORK(2),=C'1,'                                                   
         MVC   WORK+2(3),22(R4)                                                 
         MVI   WORK+5,C' '                                                      
         GOTO1 =V(PUBVAL),DMCB,(5,WORK),WORK+10,RR=RELO                         
         CLI   DMCB,0                                                           
         BE    EIFV                                                             
         MVC   DSSUFEDT,WORK+10+5                                               
         B     GUDEXIT                                                          
         SPACE                                                                  
RSUFZON  NTR1  ,                   ZONE FILTER VALIDATION                       
         CLI   DSSUFZON,0          TEST ALREADY HAD                             
         BNE   EDKW                                                             
         CLI   1(R4),2             EDIT IS 1-2 BYTES                            
         BH    EFTL                                                             
         CLC   =C'NO',22(R4)                                                    
         BNE   *+12                                                             
         MVI   DSSUFZON,X'FF'      FF MEANS 'NO ZONE'                           
         B     GUDEXIT                                                          
*                                                                               
         TM    3(R4),X'80'         MUST BE NUMERIC                              
         BZ    EIFV                                                             
         L     RF,8(R4)            VALUE                                        
         CVD   RF,DUB                                                           
         MVO   HALF,DUB+6(2)                                                    
         MVC   DSSUFZON,HALF       PWOS                                         
         CLI   DSSUFZON,0                                                       
         BNE   *+8                                                              
         MVI   DSSUFZON,X'FF'      NULL BECOMES FF                              
*                                                                               
         B     GUDEXIT                                                          
         EJECT                                                                  
***********************************************************************         
*        SUPPLIER EXTRACT SUBROUTINE                                  *         
***********************************************************************         
RSUEXTR  CLI   SBMODE,1                                                         
         BE    RSUEXD              EXTRACT DETAIL LINE                          
         BH    GUDEXIT             UNKNOWN MODE                                 
         SPACE                                                                  
RSUEXH   LA    R2,SBEXHEAD                                                      
         L     RE,RELO                                                          
         A     RE,SUPAEXHD         RETURN PUBLICATION HEADER DETAILS            
         MVC   0(SUPLEXHD,R2),0(RE)                                             
         LA    RE,SUPEXTT-SUPEXHD(,R2)                                          
         MVC   0(L'SYSHD,RE),SYSHD                                              
         MVC   L'SYSHD+1(13,RE),=C'PUBLICATIONS,'                               
         MVC   L'SYSHD+13+2(10,RE),DSMEDNAM                                     
         B     GUDEXIT                                                          
         SPACE                                                                  
         USING PUBREC,R3                                                        
RSUEXD   LM    R2,R3,4(R1)         OUTPUT AREA/RECORD AREA                      
         MVC   0(80,R2),SPACES                                                  
         XC    WORK,WORK                                                        
         GOTO1 =V(PUBEDIT),DMCB,(8,PUBKPUB),(C'S',WORK),RR=RELO                 
         MVC   0(15,R2),WORK                                                    
         LA    RF,16(R2)                                                        
*                                                                               
         MVC   0(L'PUBNAME,RF),PUBNAME  NAME                                    
         LA    RF,L'PUBNAME-1(RF)                                               
         CLI   PUBZNAME,C' '                                                    
         BNH   RSUEXD6                                                          
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVC   2(L'PUBZNAME,RF),PUBZNAME  AND ZONE NAME                         
         LA    RF,2+L'PUBZNAME-1(RF)                                            
*                                                                               
RSUEXD4  DS    0H                                                               
         OC    PUBKILL,PUBKILL     KILL DATE ?                                  
         BZ    RSUEXD6             NO - STILL SHOW CITY/STATE                   
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         ST    RF,FULL             SAVE RF                                      
         MVC   2(3,RF),=C'KD='                                                  
         L     R6,SBCOMFAC                                                      
         L     R6,CDATCON-COMFACSD(R6)                                          
         GOTO1 (R6),DMCB,(3,PUBKILL),(5,5(RF))                                  
         L     RF,FULL             RESTORE RF                                   
         LA    RF,12(RF)           1 + 11 FOR KD=MMMDD/YY                       
         B     RSUEXDX             DO NOT SHOW CITY NAME                        
*                                                                               
RSUEXD6  DS    0H                  HERE IF NO ZONE                              
         CLI   PUBCITY,C' '        ANY CITY NAME                                
         BNH   RSUEXD9                                                          
         CLI   DSMEDCOD,C'N'       IF NEWSPAPERS, SHOW IT                       
         BNE   RSUEXD9                                                          
*                                                                               
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   2(RF),C'('                                                       
         MVC   3(L'PUBCITY,RF),PUBCITY                                          
         LA    RF,3+L'PUBCITY-1(RF)                                             
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C')'                                                       
         LA    RF,1(RF)                                                         
*                                                                               
RSUEXD9  DS    0H                  CAN SHOW KILL DATE AFTER CITY                
         OC    PUBKILL,PUBKILL     KILL DATE ?                                  
         BZ    RSUEXDX                                                          
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         ST    RF,FULL             SAVE RF                                      
         MVC   2(3,RF),=C'KD='                                                  
         L     R6,SBCOMFAC                                                      
         L     R6,CDATCON-COMFACSD(R6)                                          
         GOTO1 (R6),DMCB,(3,PUBKILL),(5,5(RF))                                  
         L     RF,FULL             RESTORE RF                                   
         LA    RF,12(RF)           1 + 11 FOR KD=MMMDD/YY                       
*                                                                               
RSUEXDX  DS    0H                                                               
         B     GUDEXIT                                                          
         DROP  R3                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        INDIRECT STORAGE (ADDRESSED BY ACONS)                        *         
***********************************************************************         
         SPACE                                                                  
***********************************************************************         
*        CLIENT EXTRACT HEADER                                        *         
***********************************************************************         
CLIEXHD  DS    0C                                                               
         DC    AL1(1)              MAXIMUM HEAD LINES USED                      
         DC    AL1(CLIEXHD1-CLIEXHD) DISPLACEMENT TO KEY ENTRY                  
         DC    AL1(CLIEXHD2-CLIEXHD) DISPLACEMENT TO NAME ENTRY                 
         DC    AL1(L'CLIEXTT)      LENGTH OF TITLE                              
CLIEXTT  DC    CL40' '                                                          
CLIEXHD1 DS    0C                  CLIENT CODE ENTRY                            
         DC    AL1(CLIEXHD2-*)     ENTRY LENGTH                                 
         DC    AL1(0)              ITEM DISPLACEMENT                            
         DC    AL1(6)              ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(6)              1ST HEAD LINE                                
         DCDD  PP#CLI,6                                                         
CLIEXHD2 DS    0C                  FULL NAME ENTRY                              
         DC    AL1(CLIEXHDZ-*)     ENTRY LENGTH                                 
         DC    AL1(7)              ITEM DISPLACEMENT                            
         DC    AL1(30)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#NAME,4                                                        
CLIEXHDZ DS    AL1(0)              TERMINATOR                                   
CLILEXHD EQU   *-CLIEXHD                                                        
         SPACE                                                                  
***********************************************************************         
*        'CLIENT' FILTER TABLE                                        *         
***********************************************************************         
CLFILT   DS    0C                                                               
CLFNAME  DC    AL1(10),AL2(PP@NAME-SRCHWORK,0,0),AL1(0,0,0,0)                   
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
*        PRODUCT EXTRACT HEADER                                       *         
***********************************************************************         
PROEXHD  DS    0C                                                               
         DC    AL1(1)              MAXIMUM HEAD LINES USED                      
         DC    AL1(PROEXHD1-PROEXHD) DISPLACEMENT TO KEY ENTRY                  
         DC    AL1(PROEXHD2-PROEXHD) DISPLACEMENT TO NAME ENTRY                 
         DC    AL1(L'PROEXTT)      LENGTH OF TITLE                              
PROEXTT  DC    CL40' '                                                          
PROEXHD1 DS    0C                  PRODUCT NUMBER (KEY) ENTRY                   
         DC    AL1(PROEXHD2-*)     ENTRY LENGTH                                 
         DC    AL1(0)              ITEM DISPLACEMENT                            
         DC    AL1(7)              ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(7)              1ST HEAD LINE                                
         DCDD  PP#PRD,7                                                         
PROEXHD2 DS    0C                  FULL NAME ENTRY                              
         DC    AL1(PROEXHDZ-*)     ENTRY LENGTH                                 
         DC    AL1(8)              ITEM DISPLACEMENT                            
         DC    AL1(30)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#NAME,4                                                        
PROEXHDZ DS    AL1(0)              TERMINATOR                                   
PROLEXHD EQU   *-PROEXHD                                                        
         SPACE                                                                  
***********************************************************************         
*        'PRODUCT' FILTER TABLE                                       *         
***********************************************************************         
PRFILT   DS    0C                                                               
PRFNAME  DC    AL1(10),AL2(PP@NAME-SRCHWORK,0,0),AL1(0,0,0,0)                   
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
*        CAMPAIGN EXTRACT HEADER                                      *         
***********************************************************************         
CAMEXHD  DS    0C                                                               
         DC    AL1(1)              MAXIMUM HEAD LINES USED                      
         DC    AL1(CAMEXHD1-CAMEXHD) DISPLACEMENT TO KEY ENTRY                  
         DC    AL1(CAMEXHD2-CAMEXHD) DISPLACEMENT TO NAME ENTRY                 
         DC    AL1(L'CAMEXTT)      LENGTH OF TITLE                              
CAMEXTT  DC    CL40' '                                                          
CAMEXHD1 DS    0C                  CAMPAIGN NUMBER (KEY) ENTRY                  
         DC    AL1(CAMEXHD2-*)     ENTRY LENGTH                                 
         DC    AL1(0)              ITEM DISPLACEMENT                            
         DC    AL1(8)              ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(8)              1ST HEAD LINE                                
         DCDD  PP#EST,8                                                         
CAMEXHD2 DS    0C                  CAMPAIGN NAME ENTRY                          
         DC    AL1(CAMEXHD3-*)     ENTRY LENGTH                                 
         DC    AL1(9)              ITEM DISPLACEMENT                            
         DC    AL1(20)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#NAME,4                                                        
CAMEXHD3 DS    0C                  CAMPAIGN NAME ENTRY                          
         DC    AL1(CAMEXHDZ-*)     ENTRY LENGTH                                 
         DC    AL1(30)             ITEM DISPLACEMENT                            
         DC    AL1(17)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#DATE,4                                                        
CAMEXHDZ DS    AL1(0)              TERMINATOR                                   
CAMLEXHD EQU   *-CAMEXHD                                                        
         SPACE                                                                  
***********************************************************************         
*        'CAMPAIGN' FILTER TABLE                                      *         
***********************************************************************         
CAFILT   DS    0C                                                               
CAFNAME  DC    AL1(10),AL2(PP@NAME-SRCHWORK,0,0),AL1(0,0,0,0)                   
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
*        FOLIO EXTRACT HEADER                                         *         
***********************************************************************         
FOLEXHD  DS    0C                                                               
         DC    AL1(1)              MAXIMUM HEAD LINES USED                      
         DC    AL1(FOLEXHD1-FOLEXHD) DISPLACEMENT TO KEY ENTRY                  
         DC    AL1(FOLEXHD2-FOLEXHD) DISPLACEMENT TO NAME ENTRY                 
         DC    AL1(L'FOLEXTT)      LENGTH OF TITLE                              
FOLEXTT  DC    CL40' '                                                          
FOLEXHD1 DS    0C                  FOLIO CODE ENTRY                             
         DC    AL1(FOLEXHD2-*)     ENTRY LENGTH                                 
         DC    AL1(0)              ITEM DISPLACEMENT                            
         DC    AL1(4)              ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(3)              1ST HEAD LINE                                
         DCDD  PP#REP,3                                                         
FOLEXHD2 DS    0C                  FULL NAME ENTRY                              
         DC    AL1(FOLEXHDZ-*)     ENTRY LENGTH                                 
         DC    AL1(5)              ITEM DISPLACEMENT                            
         DC    AL1(30)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#NAME,4                                                        
FOLEXHDZ DS    AL1(0)              TERMINATOR                                   
FOLLEXHD EQU   *-FOLEXHD                                                        
         SPACE                                                                  
***********************************************************************         
*        FOLIO FILTER TABLE                                           *         
***********************************************************************         
FOFILT   DS    0C                                                               
FOFNAME  DC    AL1(10),AL2(PP@NAME-SRCHWORK,0,0),AL1(0,0,0,0)                   
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
*        PUBLICATION EXTRACT HEADER                                   *         
***********************************************************************         
SUPEXHD  DS    0C                                                               
         DC    AL1(1)              MAXIMUM HEAD LINES USED                      
         DC    AL1(SUPEXHD1-SUPEXHD) DISPLACEMENT TO KEY ENTRY                  
         DC    AL1(SUPEXHD3-SUPEXHD) DISPLACEMENT TO NAME ENTRY                 
         DC    AL1(L'SUPEXTT)      LENGTH OF TITLE                              
SUPEXTT  DC    CL40' '                                                          
SUPEXHD1 DS    0C                  SUPPLIER NUMBER (KEY) ENTRY                  
         DC    AL1(SUPEXHD2-*)     ENTRY LENGTH                                 
         DC    AL1(0)              ITEM DISPLACEMENT                            
         DC    AL1(15)             ITEM WIDTH                                   
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(3)              1ST HEAD LINE                                
         DCDD  PP#PUB,3                                                         
SUPEXHD2 DS    0C                  NAME ENTRY                                   
         DC    AL1(SUPEXHD3-*)     ENTRY LENGTH                                 
         DC    AL1(16)             ITEM DISPLACEMENT                            
         DC    AL1(58)             ITEM WIDTH (16+58=74=MAX ON 1 LINE?)         
         DC    AL1(1)              NUMBER OF HEAD LINES                         
         DC    AL1(4)              1ST HEAD LINE                                
         DCDD  PP#NAME,4                                                        
SUPEXHD3 DS    0C                  SHORT NAME ENTRY                             
SUPEXHDZ DS    AL1(0)              TERMINATOR                                   
SUPLEXHD EQU   *-SUPEXHD                                                        
         SPACE                                                                  
***********************************************************************         
*        SUPPLIER FILTER TABLE                                        *         
***********************************************************************         
SUFILT   DS    0CL14                                                            
SUFNAME  DC    AL1(10),AL2(PP@NAME-SRCHWORK,0,0),AL1(0,0,0,0)                   
*                                                                               
SUFEDT   DC    AL1(10),AL2(PP@EDIT-SRCHWORK)                                    
         DC    AL2(RSUFEDT-SRCHPRT,RSUFEDTH-SRCHPRT)                            
         DC    AL1(0,0,0,0)                                                     
*                                                                               
SUFZON   DC    AL1(10),AL2(PP@ZONE-SRCHWORK)                                    
         DC    AL2(RSUFZON-SRCHPRT,RSUFZONH-SRCHPRT)                            
         DC    AL1(0,0,0,0)                                                     
*                                                                               
SUFFRQ   DC    AL1(10),AL2(PP@FREQ-SRCHWORK)                                    
         DC    AL2(RSUFFRQ-SRCHPRT,RSUFFRQH-SRCHPRT)                            
         DC    AL1(0,0,0,0)                                                     
*                                  **FOLIO FILTER NOT USED**                    
*SUFFOL   DC    AL1(10),AL2(PP@REP-SRCHWORK)                                    
*         DC    AL2(RSUFFOL-SRCHPRT,RSUFFOLH-SRCHPRT)                           
*         DC    AL1(0,0,0,0)                                                    
         DC    X'00'                                                            
         SPACE                                                                  
***********************************************************************         
*        SUPPLIER HELP INFO                                           *         
***********************************************************************         
RSUFFOLH DC    AL1(0,0,4),C'XXXX'                                               
RSUFFRQH DC    AL1(0,0,2),C'XX'                                                 
RSUFEDTH DC    AL1(0,0,3),C'XXX'                                                
RSUFZONH DC    AL1(0,0,2),C'NN'                                                 
         EJECT                                                                  
***********************************************************************         
*        WORK AREA DSECT                                              *         
***********************************************************************         
SRCHWORK DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    C                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
WORK2    DS    CL64                                                             
RELO     DS    A                                                                
APARMS   DS    A                   ADDRESS CALLERS PARAMETER LIST.              
*                                                                               
COMMA    DS    C                                                                
EQUALS   DS    C                                                                
OUTDD    DS    0C                                                               
         DSDDL PRINT=YES                                                        
*                                                                               
SRCHWRKL EQU   *-SRCHWORK                                                       
*                                                                               
SRCHWRKU DS    0C                  SRTN WORK SPACE IS TACKED ON HERE            
         SPACE 3                                                                
***********************************************************************         
*        FILTER TABLE DSECT                                           *         
***********************************************************************         
FILTABD  DSECT                                                                  
FILTLKWD DS    AL1                 LENGTH OF KEYWORD                            
FILTYKWD DS    AL2                 DISP TO PP@ KEYWORD IN SRCHWORK              
FILTYRTN DS    AL2                 DISP TO VALIDATOR                            
FILTYHLP DS    AL2                 DISP TO HELP TABLE                           
FILTMS   DS    X                   REQUIRED MEDSTAT BITS                        
FILTM1   DS    X                   REQUIRED MEDIND1 BITS                        
FILTM2   DS    X                   REQUIRED MEDIND2 BITS                        
FILTM3   DS    X                   REQUIRED MEDIND3 BITS                        
FILTABLQ EQU   *-FILTABD                                                        
         EJECT                                                                  
       ++INCLUDE GESRCHBLKD                                                     
         SPACE                                                                  
*        REDEFINE SAVED STORAGE ON END OF SEARCH CONTROL BLOCK                  
*        FILTERS AND SAVE AREA REDEFINED FOR EACH APPLICATION, THEN             
*        COMMON SAVE STORAGE ON END .                                           
*                                                                               
DSFILT   DS    0C                  SAVED STORAGE                                
*                                                                               
******** SUPPLIER SEARCH AREAS *************                                    
*                                                                               
         ORG   DSFILT                                                           
DSSUFILT DS    0C                  SUPPLIER SEARCH FILTERS                      
DSSUFEDT DS    XL1                 EDITION FILTER (INTERNAL, 1 BYTE)            
DSSUFZON DS    XL1                 ZONE FILTER           ''                     
DSSUFFRQ DS    CL2                 FREQ FILTER                                  
DSSUFFOL DS    CL4                 FOLIO (REP) FILTER - NOT USED                
DSSUFLNQ EQU   *-DSSUFILT          SUPPLIER FILTERS LENGTH                      
*                                                                               
DSSUSAV  DS    0C                  SUPPLIER SEARCH SAVE STORAGE                 
DSSUSAVL EQU   *-DSSUSAV           SUPPLIER SAVE STORAGE LENGTH                 
*                                                                               
*                                                                               
*********** CLIENT SEARCH AREAS ************                                    
         ORG   DSFILT                                                           
DSCLFILT DS    0C                  CLIENT SEARCH FILTER                         
         DS    X                                                                
DSCLFLNQ EQU   *-DSCLFILT          CLIENT FILTER LENGTH                         
*                                                                               
DSCLISAV DS    0C                  CLIENT SEARCH SAVE STORAGE                   
DSCLSAVL EQU   *-DSCLISAV          CLIENT SAVE STORAGE LENGTH                   
*                                                                               
*********** PRODUCT SEARCH AREAS ***********                                    
         ORG   DSFILT                                                           
DSPRFILT DS    0C                  PRODUCT SEARCH FILTER                        
         DS    X                                                                
DSPRFLNQ EQU   *-DSPRFILT          PRODUCT FILTER LENGTH                        
*                                                                               
DSPROSAV DS    0C                  PRODUCT SEARCH SAVE STORAGE                  
DSPRSAVL EQU   *-DSPROSAV          PRODUCT SAVE STORAGE LENGTH                  
*                                                                               
DSPRTDIR DS    CL8                                                              
DSPRTFIL DS    CL8                                                              
*        APPLICATION COMMON SAVE STORAGE AT END OF SAVE AREA                    
*                                                                               
         ORG   SBASAVE+L'SBASAVE-DSCOMML                                        
DSCOMMON DS    0C                                                               
       ++INCLUDE PPSRCHPARM                                                     
DSCOMML  EQU   *-DSCOMMON                                                       
         EJECT                                                                  
       ++INCLUDE GESRCHDIRD                                                     
*                                                                               
PPNSKD   DSECT                                                                  
       ++INCLUDE PPGENNSK                                                       
         EJECT                                                                  
         PRINT OFF                                                              
PAGYRECD DSECT                                                                  
       ++INCLUDE PAGYREC                                                        
PCLTRECD DSECT                                                                  
       ++INCLUDE PCLTREC                                                        
PPRDRECD DSECT                                                                  
       ++INCLUDE PPRDREC                                                        
PESTRECD DSECT                                                                  
       ++INCLUDE PESTREC                                                        
PPUBRECD DSECT                                                                  
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBNAMEL                                                       
PUBSPRED DSECT                                                                  
       ++INCLUDE PUBSPREL                                                       
PUBGENED DSECT                                                                  
       ++INCLUDE PUBGENEL                                                       
PREPRECD DSECT                                                                  
       ++INCLUDE PREPREC                                                        
         PRINT ON                                                               
*FATWA                                                                          
*DDCOMFACS                                                                      
*FAGETTXTD                                                                      
*PPDDEQUS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FATWA                                                          
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAGETTXTD                                                      
       ++INCLUDE PPDDEQUS                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'062PPSRCHDIR 05/01/02'                                      
         END                                                                    
