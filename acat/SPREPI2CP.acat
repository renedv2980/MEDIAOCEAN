*          DATA SET SPREPI2CP  AT LEVEL 053 AS OF 06/20/11                      
*CATALP GETCPAT                                                                 
         TITLE 'GETCPAT - GET COMMERCIAL PATTERNS'                              
GETCPAT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 40,**GCPT                                                        
         USING GCPW,RC                                                          
         L     R8,0(R1)                                                         
         USING GCPD,R8                                                          
         L     R9,4(R1)                                                         
         LA    RA,1(R9)                                                         
         LA    RA,4095(RA)                                                      
         USING SPWORKD,R9,RA                                                    
*                                                                               
         XC    DMCB(24),DMCB                                                    
         MVC   DMCB+4(4),=X'D9000AFE'     GET ADDRESS OF TRPACK                 
         L     RF,ACOMFACS                                                      
         L     RF,(CCALLOV-COMFACSD)(RF)                                        
         GOTO1 (RF),DMCB                                                        
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   VTRPACK,0(R1)                                                    
*                                  FIRST TIME TEST                              
         ICM   R3,15,GCPPTNO       IS PATTERN POINTER SET                       
         BZ    GP6                 NO                                           
         L     R5,GCPPTRM          YES - RESTORE REMAINDER FOR BCT              
         L     R2,GCPINEL                AND INST ELEM ADDRESS                  
*                                                                               
*                                  CLEAR ALL PATTERN DATA FROM CMLTAB           
         L     R4,GCPCPARS+4       FIRST                                        
         L     R6,GCPCPARS+8       COUNT                                        
         USING CMLTD,R4                                                         
         L     RF,GCPCMTCL         LENGTH TO CLEAR                              
         BCTR  RF,R0                                                            
*                                                                               
GP4      DS    0H                                                               
         EX    RF,GP4XC                                                         
         A     R4,GCPCPARS+12      NEXT ENTRY                                   
         BCT   R6,GP4                                                           
         B     GP19                CONTINUE                                     
*                                                                               
GP4XC    XC    CMLTROT(0),CMLTROT  CLEAR FROM ROTATION LETTER ON                
*                                  INITIALIZE BINSRCH PARS                      
GP6      DS    0H                  FOR CMML TABLE                               
         SR    R0,R0                                                            
         ST    R0,GCPCPARS+0                                                    
         L     R0,GCPCMTAB         A(TABLE)                                     
         ST    R0,GCPCPARS+4                                                    
         LA    R0,1                SET ONE ENTRY (TOTAL IS FIRST)               
         ST    R0,GCPCPARS+8       NO. S0 FAR                                   
         L     R0,GCPCMTEL                                                      
         ST    R0,GCPCPARS+12      LENGTH OF ENTRY                              
         LA    R0,16                                                            
         ST    R0,GCPCPARS+16      KEY LENGTH                                   
         L     R1,GCPCMTL                                                       
         SR    R0,R0                                                            
         L     RF,GCPCPARS+12                                                   
         DR    R0,RF                                                            
         ST    R1,GCPCPARS+20      MAXIMUM                                      
*                                                                               
         L     RF,GCPCMTAB         CLEAR TOTAL ENTRY IN CMML TAB                
         L     R1,GCPCMTEL         LENGTH                                       
         EX    R1,*+8              OK TO CLEAR 1 EXTRA                          
         B     *+10                                                             
         XC    0(0,RF),0(RF)                                                    
*                                                                               
         OC    THREE,THREE         PATTERN REF FOR NETPAK                       
         BZ    GP7                                                              
         CLC   THREE,=X'FFFFFF'    DONE WITH PATTERNS                           
         BE    GP80                                                             
         BAS   RE,NETPAT           GET PATTERN FOR NETPAK                       
         BE    GCPX                FOUND AND DONE                               
         B     GP80                                                             
*                                                                               
GP7      LA    R4,KEY              BUILD KEY                                    
         USING INSRECD,R4                                                       
         XC    KEY,KEY                                                          
         MVC   INSKID,=X'0A24'          RECORD CODE                             
         MVC   INSKAM,BAGYMD            A/M                                     
         L     RF,ADCLT                                                         
         OC    INSKCLT,CMCLTCOD-CLTHDR(RF)   USE OVERRIDE CLT                   
         BNZ   *+10                          IF ANY                             
         MVC   INSKCLT,BCLT             CLT                                     
         MVC   INSKMKT(5),BMKTSTA          MKT/STA                              
         MVI   GCPFPRD,X'FF'            SET MULTI PRD                           
         CLI   BPRD,0                                                           
         BE    GP8                                                              
         CLI   BPRD,X'FF'                                                       
         BE    GP8                                                              
*                                                                               
         MVC   GCPFPRD,BPRD             SET ONE PRD                             
         MVC   INSKPRD,BPRD                                                     
*                                                                               
GP8      DS    0H                                                               
         MVI   GCPFCPY,X'FF'      SET MULTI COPY                                
         CLI   BEST,0                                                           
         BE    GP9                                                              
         CLI   BESTEND,0                                                        
         BNE   GP9                                                              
         L     RF,ADEST                                                         
         MVC   GCPFCPY,ECOPY-ESTHDR(RF)      SET ONE COPY OR NONE               
         CLI   GCPT0PRF+10,C'E'              TEST COPY CODE=EST                 
         BNE   *+10                                                             
         MVC   GCPFCPY,EKEYEST-ESTHDR(RF)                                       
         MVC   INSKCOPY,GCPFCPY                                                 
*                                                                               
GP9      DS    0H                                                               
         MVC   GCPSVKEY,KEY                                                     
*                                                                               
GP9B     DS    0H                                                               
         GOTO1 HIGH                                                             
         B     GP12                                                             
*                                                                               
GP10     DS    0H                                                               
         MVC   KEY,GCPSVKEY                                                     
         GOTO1 HIGH                                                             
*                                                                               
GP11     DS    0H                                                               
         GOTO1 SEQ                                                              
*                                                                               
GP12     DS    0H                                                               
         CLC   KEY(5),KEYSAVE      REC/AM/CLT                                   
         BNE   GP80                                                             
         CLI   GCPFPRD,X'FF'       TEST ONE PRD                                 
         BE    GP13                                                             
         CLC   KEY+5(6),KEYSAVE+5   YES-CHECK PRD/MKT/STA                       
         BNE   GP80                                                             
         B     GP15                                                             
*                             MULTI PRODUCT                                     
GP13     DS    0H                                                               
         CLC   KEY+6(5),KEYSAVE+6  MKT/STA                                      
         BL    GP14                IF LOW, SET FROM KEYSAVE                     
         BE    GP15                                                             
*                                                                               
         LLC   RF,KEY+5            IF HIGH BUMP PRODUCT                         
         LA    RF,1(RF)                                                         
         STC   RF,KEY+5                                                         
*                                                                               
GP14     DS    0H                                                               
         MVC   KEY+6(7),KEYSAVE+6                                               
         MVI   KEY+11,0                                                         
         B     GP9B                                                             
*                                                                               
GP15     DS    0H                                                               
         CLI   GCPFCPY,X'FF'       CHECK MULTI-COPY                             
         BE    GP16                                                             
         CLC   KEY+11(1),GCPFCPY   TEST COPY                                    
         BNE   GP11                                                             
*                                                                               
GP16     DS    0H             READ INSTRUCTION RECORD                           
         MVC   GCPSVKEY,KEY                                                     
         MVC   AREC,GCPIO                                                       
         GOTO1 GET                                                              
*                                                                               
         L     R2,AREC                                                          
         LA    R2,24(R2)                                                        
         CLI   0(R2),X'10'                                                      
         BE    GP18                                                             
*                                                                               
GP17     DS    0H                                                               
         MVI   ELCODE,X'10'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GP10                EOR - NEXT REC                               
*                                                                               
GP18     DS    0H                  TEST DATES OF PATTERNS                       
         USING INSDTAEL,R2         1NST DATA ELEM                               
         CLI   GCPPIGC,C'S'        IF DOING PIGS SEPARATE                       
         BNE   GP18D                                                            
         CLC   GCPPIGF,INSPRD2     PIG PRD MUST BE RIGHT                        
         BNE   GP17                                                             
GP18D    DS    0H                                                               
         MVI   GCPPCCE,C'N'                                                     
         TM    INSFLAG,X'20'       TEST COPY CODE = ESTIMATE                    
         BZ    *+8                                                              
         MVI   GCPPCCE,C'Y'        YES                                          
*                                                                               
         ST    R2,GCPINEL                                                       
         LA    R3,INSPTTN                                                       
         LLC   R5,INSDTALN                                                      
         SHI   R5,(INSPTTN-INSDTAEL)                                            
         SR    R4,R4                                                            
         D     R4,=F'7'            R4 HAS NO. OF PTTNS                          
*                                                                               
GP18F    DS    0H                                                               
         OC    0(3,R3),0(R3)       TEST ANY PATTERN                             
         BZ    GP19                SKIP HIATUS                                  
         CLC   5(2,R3),BQSTARTP    IF ENDS BEFORE REQ START                     
         BL    GP19                REJECT                                       
         CLC   3(2,R3),BQENDP      OR IF STARTS AFTER REQ END                   
         BNH   GP19B                                                            
*                                                                               
GP19     DS    0H                  NEXT PATTERN                                 
         LA    R3,7(R3)                                                         
         BCT   R5,GP18F                                                         
*                                                                               
         L     R2,GCPINEL          RESTORE ELEM POINTER                         
         B     GP17                NEXT ELEM                                    
*                                                                               
GP19B    ST    R3,GCPPTNO          SAVE THIS PATTERN                            
         ST    R5,GCPPTRM          AND NO. REMAINING                            
*                                                                               
         XC    KEY,KEY             BUILD PATTERN REC KEY                        
         USING PATRECD,R4                                                       
         LA    R4,KEY                                                           
*                                                                               
         L     RF,GCPIO                                                         
         MVC   KEY(5),0(RF)        CODE/AM/CLT                                  
         MVC   PATKID,=X'0A22'      CODE                                        
         L     R2,GCPINEL                                                       
         USING INSDTAEL,R2                                                      
         MVC   PATKPRD(4),INSPRD1  PRD-SLN1/PRD-SLN2                            
         MVC   PATKCODE,11(RF)     COPY CODE                                    
         MVC   PATKREF,0(R3)       REF/SUBLINE                                  
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE     DID I FIND PATTERN                           
         BE    GP21                YES- OK                                      
         CLI   GCPPCCE,C'Y'         NO- IS IT COPY CODE=EST?                    
         BNE   GP19                NO- IGNORE                                   
         MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVI   PATKCODE,0          TRY FOR COPY CODE 0                          
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GP19                IGNORE IF NOT FOUND                          
*                                                                               
GP21     DS    0H                                                               
         MVC   AREC,GCPIO2         2ND IOAREA                                   
         GOTO1 GET                                                              
*                                                                               
         L     R4,GCPIO2                                                        
         USING PATRECD,R4                                                       
         MVC   GCPPPRD,PATKPRD      SET PRD                                     
         MVC   GCPPSLN,PATKSLN      SET SLN1                                    
         MVC   GCPPPRD2,PATKPRD2    SET PRD2                                    
         MVC   GCPPSLN2,PATKSLN2    SET SLN2                                    
         MVC   GCPPCPY,PATKCODE     SET COPY                                    
                                                                                
         MVC   GCPPREF,PATKREF     REFERENCE NO.                                
         XC    GCPPREF,=3X'FF'     UN-COMPLEMENT                                
         SR    RF,RF                                                            
         ICM   RF,7,GCPPREF                                                     
         SRL   RF,10               USE ONLY 14 BITS                             
         STCM  RF,7,GCPPREF                                                     
*                                                                               
         LA    R2,24(R4)                                                        
         CLI   0(R2),X'10'                                                      
         BE    GP21A                                                            
         MVI   ELCODE,X'10'                                                     
         BAS   RE,NEXTEL                                                        
         BE    GP21A                                                            
         DC    H'0'                                                             
*                                                                               
GP21A    DS    0H                                                               
         USING PATDTAEL,R2                                                      
         MVC   GCPPDESC,PATDESC    DESCRIPTION                                  
*                                                                               
         L     R3,GCPPTNO                                                       
         GOTO1 DATCON,DMCB,(2,3(R3)),(3,GCPPST)                                 
         GOTO1 (RF),(R1),(2,5(R3)),(3,GCPPEND)                                  
         DROP  R2                                                               
*                                                                               
         MVC   GCPPSTIM,PATSTIM                                                 
         CLC   GCPPSTIM,=H'2400'   START TIME 2400?                             
         BNE   *+10                NO                                           
         XC    GCPPSTIM,GCPPSTIM   YES - 12M IS X'0000'                         
         MVC   GCPPETIM,PATETIM                                                 
*                                                                               
         LA    RE,GCPCMLST                                                      
         LH    RF,=AL2(L'GCPCMLST)                                              
         XCEFL                                                                  
         XC    GCPPTTN,GCPPTTN                                                  
         XC    BPATDISP,BPATDISP                                                
*                                                                               
         L     R2,AREC                                                          
         TM    15(R2),X'02'        INCOMPLETE BPAT RECORD                       
         BO    GP19                SKIP TO NEXT PATTERN                         
         MVI   BPATREC,C'N'                                                     
         TM    15(R2),X'01'        BPAT RECORD                                  
         BNO   *+8                                                              
         MVI   BPATREC,C'Y'                                                     
*                                                                               
         LA    R2,24(R2)                                                        
GP22     DS    0H                                                               
         CLI   0(R2),0                                                          
         BE    GP27                                                             
         CLI   0(R2),X'10'         CMML DATA ELEMENT                            
         BE    GP23                                                             
         CLI   0(R2),X'30'         CMML LIST ELEM                               
         BE    GP24                                                             
         CLI   0(R2),X'31'         BPAT CMML LIST ELEM                          
         BE    GP26                                                             
         CLI   0(R2),X'32'         PATTERN ELEM                                 
         BE    GP25                                                             
*                                                                               
GP22B    DS    0H                                                               
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GP22                                                             
*                                                                               
         USING PATDTAEL,R2                                                      
GP23     MVI   PACKDCML,C'N'            COMMERCIALS NOT PACKED                  
         TM    PATSTAT1,PATSADID        ARE THE COMMERCIALS PACKED?             
         BZ    *+8                      NO                                      
         MVI   PACKDCML,C'Y'            YES - COMMERCIALS ARE PACKED            
         MVI   GCPFLAGS,0               CLEAR FLAGS                             
         TM    PATSTAT1,PATSDLY         TIMES DAILY?                            
         BZ    GP22B                    NO                                      
         OI    GCPFLAGS,GCPDAILY        YES - CHECK TIMES DAILY FLAG            
         B     GP22B                                                            
         DROP  R2                                                               
*                                                                               
GP24     DS    0H             CMML LIST ELEM                                    
         LA    RE,GCPCMLST                                                      
         LH    RF,=AL2(L'GCPCMLST)                                              
         XCEFL                                                                  
         LLC   R1,1(R2)                                                         
         SHI   R1,3                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   GCPCMLST(0),2(R2)        N 16 BYTE ENTRIES                       
         B     GP22B                                                            
*                                                                               
GP25     DS    0H             GMML PATTERN ELEM                                 
         XC    GCPPTTN,GCPPTTN                                                  
         LLC   R1,1(R2)                                                         
         SHI   R1,2                                                             
         STC   R1,GCPPTTNL         SET PATTERN LENGTH                           
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   GCPPTTN(0),2(R2)         N 1 BYTE ENTRIES                        
         B     GP22B                                                            
*                                                                               
GP26     DS    0H                  BPAT CMML LIST ELEM                          
         LA    R7,GCPCMLST         ** CAN HAVE MULTIPLE ELEMS **                
         SR    R1,R1                                                            
         ICM   R1,3,BPATDISP       PICK UP END OF LIST                          
         AR    R7,R1                                                            
*                                                                               
         LLC   R1,1(R2)                                                         
         SHI   R1,3                CODE+LEN+OVERHEAD                            
         BZ    GP22B                                                            
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,BPATDISP                                                    
         AR    R0,R1               ADD THIS ELEM'S LEN                          
         STCM  R0,3,BPATDISP                                                    
         CHI   R0,800                                                           
         BNH   *+6                                                              
         DC    H'0'                SECURITY FOR MAX 50 COMMLS                   
*                                                                               
         SHI   R1,1                ONE MORE FOR EX                              
         BM    GP22B                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R7),3(R2)              N 16 BYTE ENTRIES                     
         B     GP22B                                                            
*===================================================================            
*        FINISHED WITH PATTERN RECORD - BUILD COMML TABLE                       
*===================================================================            
*                                                                               
GP27     DS    0H                         ADD FILM ENTRIES                      
         XC    GCPWK,GCPWK                                                      
         LA    R7,GCPWK                                                         
         USING CMLTD,R7                                                         
         LA    R4,GCPPTTN          PATTERN LIST                                 
*                                                                               
         CLI   BPATREC,C'Y'                                                     
         BNE   GP29A                                                            
         LA    R5,GCPCMLST         LIST OF COMMLS                               
         AHI   R5,-16              WILL BUMP TO FIRST ENTRY BELOW               
         LA    R4,SPTRRTAB         LIST OF ROTATION LETTERS                     
*                                                                               
GP29     DS    0H                                                               
         CLI   BPATREC,C'Y'                                                     
         BE    GP29B                                                            
*                                                                               
GP29A    CLI   0(R4),0             EOL                                          
         BE    GCPX                                                             
         XC    GCPWK,GCPWK                                                      
         LLC   RF,0(R4)                                                         
         SHI   RF,193              OK FOR A-I                                   
         CLI   0(R4),C'J'                                                       
         BL    *+8                                                              
         SHI   RF,7                7 MORE FR J+                                 
         SLL   RF,4                                                             
         LA    RF,GCPCMLST(RF)                                                  
         MVC   CMLTFLM,0(RF)       FILM CODE (16)                               
         BAS   RE,UNPKCML          CONVERT ENTRY TO UNPACKED                    
         MVC   CMLTROT,0(R4)       SET ROTATION LETTER                          
         OI    CMLTACTV,X'80'      SET FILM ACTIVE                              
         MVC   CMLTOCC,=F'1'       SET 1 OCCURENCE                              
         B     GP30                                                             
*                                                                               
*        BUILD COMML TABLE FOR BPAT RECORDS                                     
*                                                                               
GP29B    CLI   0(R4),0             MAX ROTATIONS                                
         BE    GCPX                                                             
         LA    R5,16(R5)           ANY MORE COMMLS IN PAT                       
         OC    0(16,R5),0(R5)                                                   
         BZ    GCPX                                                             
         XC    GCPWK,GCPWK                                                      
         MVC   CMLTFLM,0(R5)       FILM CODE (16)                               
         BAS   RE,UNPKCML          CONVERT ENTRY TO UNPACKED                    
         MVC   CMLTROT,2(R4)       SET ROTATION LETTER/CODE                     
         OI    CMLTACTV,X'80'      SET FILM ACTIVE                              
         MVC   CMLTOCC,=F'1'       SET 1 OCCURENCE                              
*===================================================================            
*        ADD TO COMML TABLE OR BUMP COUNTER                                     
*===================================================================            
GP30     GOTO1 BINSRCH,GCPCPARS,(1,CMLTD)                                       
         L     RF,0(R1)                                                         
         CLI   0(R1),1             TEST IN TABLE                                
         BNE   GP31           YES - ADD TO COUNTER                              
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         B     GP31B                                                            
*                                                                               
GP31     DS    0H                                                               
         OC    CMLTACTV-CMLTD(1,RF),CMLTACTV   OR IN ACTIVITY SW                
         MVC   CMLTROT-CMLTD(1,RF),CMLTROT     SET ROTATION LETTER              
         L     RE,CMLTOCC-CMLTD(RF)     ADD TO OCCURENCE COUNTER                
         A     RE,CMLTOCC                                                       
         ST    RE,CMLTOCC-CMLTD(RF)                                             
*                                                                               
GP31B    DS    0H                       ADD TO TOTAL ROW OCCURENCES             
         L     RF,GCPCMTAB                                                      
         L     RE,CMLTOCC-CMLTD(RF)                                             
         LA    RE,1(RE)                                                         
         ST    RE,CMLTOCC-CMLTD(RF)                                             
*                                                                               
         LA    R4,1(R4)            NEXT PATTERN ENTRY                           
         CLI   BPATREC,C'Y'                                                     
         BNE   *+8                                                              
         LA    R4,2(R4)            NEXT ROTATION LETTER IN TABLE                
         B     GP29                                                             
*                                                                               
GP80     DS    0H                                                               
*        MVI   GCPPPRD,0           SET FINISHED                                 
         XC    GCPNPRD,GCPNPRD                                                  
*                                                                               
GCPX     DS    0H                                                               
         XIT1                                                                   
*---------------------------------------------------------                      
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
NEXTEL2  DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*---------------------------------------------------------                      
*        BUILD PATTERN RECORD KEY FOR NETPAK                                    
*---------------------------------------------------------                      
NETPAT   NTR1                                                                   
*                                                                               
NP10     XC    BIGKEY,BIGKEY       BUILD PATTERN REC KEY                        
         USING NPTXKEY,R4                                                       
         LA    R4,BIGKEY                                                        
*                                                                               
         MVC   NPTPXID,=X'0AE1'    CODE                                         
         MVC   NPTPXAM,BAGYMD      AGY/MED                                      
         MVC   NPTPXCLT,BCLT       CLIENT                                       
         MVC   NPTPXS3Q,THREE      REF SEQ NUMBER                               
         DROP  R4                                                               
*                                                                               
         MVC   BIGKYSV,BIGKEY                                                   
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'XSPDIR',BIGKYSV,BIGKEY,DMWORK         
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   BIGKEY(32),BIGKYSV  DID I FIND PATTERN                           
         BNE   NPXNO               YES- OK                                      
         MVC   AREC,GCPIO2         2ND IOAREA                                   
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'XSPFIL',BIGKEY+36,AREC,DMWORK         
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,GCPIO2                                                        
         USING NPTRECD,R2                                                       
         MVC   GCPNPRD,NPTXPRD       3 CHARACTER PRODUCT                        
         MVC   GCPPSLN,NPTXSLN       LENGTH 1                                   
         MVC   GCPNPRD2,NPTXPRD2     3 CHARACTER PRODUCT2                       
         MVC   GCPPSLN2,NPTXSLN2     LENGTH 2                                   
         MVC   GCPPCPY,NPTXR3F       COPY CODE                                  
*                                                                               
         MVC   GCPPREF,NPTXR3F       REFERENCE NO.                              
         XC    GCPPREF,=3X'FF'       UN-COMPLEMENT                              
         DROP  R2                                                               
*                                                                               
         LA    R2,42(R2)                                                        
         CLI   0(R2),X'10'                                                      
         BE    NP15                                                             
         MVI   ELCODE,X'10'                                                     
         BAS   RE,NEXTEL                                                        
         BE    NP15                                                             
         DC    H'0'                                                             
*                                                                               
NP15     DS    0H                                                               
         USING NPTDTAEL,R2                                                      
         MVC   GCPPDESC,NPTDESC    DESCRIPTION                                  
         GOTO1 DATCON,DMCB,(3,NPTSTART),(3,GCPPST)                              
         GOTO1 (RF),(R1),(3,NPTEND),(3,GCPPEND)                                 
         TM    NPTSTAT,NPTS_TIME   ELEM HAS START/END TIME?                     
         BZ    NP20                NO                                           
         MVC   GCPPSTIM,NPTSTIM    START TIME                                   
         CLC   GCPPSTIM,=H'2400'   START TIME 2400?                             
         BNE   *+10                NO                                           
         XC    GCPPSTIM,GCPPSTIM   YES - 12M IS X'0000'                         
         MVC   GCPPETIM,NPTETIM    END TIME                                     
         DROP  R2                                                               
*                                                                               
NP20     LA    RE,GCPCMLST                                                      
         LH    RF,=AL2(L'GCPCMLST)                                              
         XCEFL                                                                  
         XC    GCPPTTN,GCPPTTN                                                  
*                                                                               
         L     R2,AREC                                                          
         LA    R2,42(R2)                                                        
NP22     DS    0H                                                               
         CLI   0(R2),0                                                          
         BE    NP27                                                             
         CLI   0(R2),X'10'         PATTERN DATA ELEMENT?                        
         BE    NP23                                                             
         CLI   0(R2),X'30'         CMML LIST ELEM                               
         BE    NP24                                                             
         CLI   0(R2),X'32'         PATTERN ELEM                                 
         BE    NP25                                                             
*                                                                               
NP22B    DS    0H                                                               
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NP22                                                             
*                                                                               
         USING NPTDATA,R2                                                       
NP23     MVI   PACKDCML,C'N'            COMMERCIALS NOT PACKED                  
         TM    NPTSTAT,NPTS_ADID        ARE THE COMMERCIALS PACKED?             
         BZ    *+8                      NO                                      
         MVI   PACKDCML,C'Y'            YES - COMMERCIALS ARE PACKED            
         B     NP22B                                                            
         DROP  R2                                                               
*                                                                               
NP24     DS    0H             CMML LIST ELEM X'30'                              
         LA    RE,GCPCMLST                                                      
         LH    RF,=AL2(L'GCPCMLST)                                              
         XCEFL                                                                  
         LLC   R1,1(R2)                                                         
         SHI   R1,3                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   GCPCMLST(0),2(R2)        N 16 BYTE ENTRIES                       
         B     NP22B                                                            
*                                                                               
NP25     DS    0H             GMML PATTERN ELEM X'32'                           
         XC    GCPPTTN,GCPPTTN                                                  
         LLC   R1,1(R2)                                                         
         SHI   R1,2                                                             
         STC   R1,GCPPTTNL         SET PATTERN LENGTH                           
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   GCPPTTN(0),2(R2)         N 1 BYTE ENTRIES                        
         B     NP22B                                                            
*                                                                               
NP27     DS    0H                         ADD FILM ENTRIES                      
         XC    GCPWK,GCPWK                                                      
         LA    R7,GCPWK                                                         
         USING CMLTD,R7                                                         
         LA    R4,GCPPTTN          PATTERN LIST                                 
*                                                                               
NP29     DS    0H                                                               
         CLI   0(R4),0             EOL                                          
         BE    NPXYES                                                           
         XC    GCPWK,GCPWK                                                      
         LLC   RF,0(R4)                                                         
         SHI   RF,193              OK FOR A-I                                   
         CLI   0(R4),C'J'                                                       
         BL    *+8                                                              
         SHI   RF,7                7 MORE FR J+                                 
         SLL   RF,4                                                             
         LA    RF,GCPCMLST(RF)                                                  
         MVC   CMLTFLM,0(RF)       FILM CODE (16)                               
         BAS   RE,UNPKCML          CONVERT ENTRY TO UNPACKED                    
         MVC   CMLTROT,0(R4)       SET ROTATION LETTER                          
         OI    CMLTACTV,X'80'      SET FILM ACTIVE                              
         MVC   CMLTOCC,=F'1'       SET 1 OCCURENCE                              
*                                                                               
         GOTO1 BINSRCH,GCPCPARS,(1,CMLTD)                                       
         L     RF,0(R1)                                                         
         CLI   0(R1),1             TEST IN TABLE                                
         BNE   NP31           YES - ADD TO COUNTER                              
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         B     NP31B                                                            
*                                                                               
NP31     DS    0H                                                               
         OC    CMLTACTV-CMLTD(1,RF),CMLTACTV   OR IN ACTIVITY SW                
         MVC   CMLTROT-CMLTD(1,RF),CMLTROT     SET ROTATION LETTER              
         L     RE,CMLTOCC-CMLTD(RF)     ADD TO OCCURENCE COUNTER                
         A     RE,CMLTOCC                                                       
         ST    RE,CMLTOCC-CMLTD(RF)                                             
*                                                                               
NP31B    DS    0H                       ADD TO TOTAL ROW OCCURENCES             
         L     RF,GCPCMTAB                                                      
         L     RE,CMLTOCC-CMLTD(RF)                                             
         LA    RE,1(RE)                                                         
         ST    RE,CMLTOCC-CMLTD(RF)                                             
*                                                                               
         LA    R4,1(R4)            NEXT PATTERN ENTRY                           
         B     NP29                                                             
*                                                                               
NPXYES   SR    RC,RC                                                            
NPXNO    LTR   RC,RC                                                            
NPX      XIT1                                                                   
         EJECT                                                                  
*                                                                               
BIGKEY   DS    XL40                                                             
BIGKYSV  DS    XL40                                                             
*                                                                               
UNPKCML  NTR1                                                                   
*                                                                               
         CLI   PACKDCML,C'Y'        ARE COMMERCIALS PACKED?                     
         BNE   UCMLX                NO - EXIT                                   
*                                                                               
         XC    WORK,WORK                                                        
         GOTO1 VTRPACK,DMCB,(C'U',CMLTFLM),WORK                                 
         CLI   WORK+8,C' '          IS THIS AN AD-ID?                           
         BH    UCML05               YES                                         
         MVC   CMLTFLM(8),WORK      NO - REGULAR 8-CHAR ISCII                   
         B     UCML10                                                           
UCML05   MVC   CMLTADID(8),CMLTFLM  MOVE IN AD-ID                               
         MVC   WORK(8),CMLTFLM      LOOK UP THIS COMMERCIAL                     
         BAS   RE,RDPACKED          IS THIS FULL AD-ID?                         
         BE    UCML10               YES - KEY IS AD-ID/AD-ID                    
         MVC   CMLTFLM(8),WORK      NO - KEY IS ISCII/AD-ID                     
*                                                                               
UCML10   OC    CMLTFLM+8(8),CMLTFLM+8                                           
         BZ    UCMLX                                                            
         XC    WORK,WORK                                                        
         GOTO1 VTRPACK,DMCB,(C'U',CMLTFLM+8),WORK                               
         CLI   WORK+8,C' '          IS THIS AN AD-ID?                           
         BH    UCML15               YES                                         
         MVC   CMLTFLM+8(8),WORK    NO - REGULAR 8-CHAR ISCII                   
         B     UCMLX                                                            
UCML15   MVC   CMLTADID+8(8),CMLTFLM+8 MOVE IN AD-ID                            
         MVC   WORK(8),CMLTFLM+8    LOOK UP THIS COMMERCIAL                     
         BAS   RE,RDPACKED          IS THIS FULL AD-ID?                         
         BE    UCMLX                YES - KEY IS AD-ID/AD-ID                    
         MVC   CMLTFLM+8(8),WORK    NO - KEY IS ISCII/AD-ID                     
*                                                                               
UCMLX    B     GCPX                                                             
*                                                                               
RDPACKED NTR1                                                                   
         XC    KEY,KEY              BUILD PASSIVE AD-ID KEY                     
         USING CMLRECD,R4                                                       
         LA    R4,KEY                                                           
*                                                                               
         L     RF,GCPIO             A(INST RECAP REC)                           
         MVC   KEY(5),0(RF)         CODE/AM/CLT                                 
         MVC   CMLPIDAD,=X'0AC1'    CODE                                        
         MVC   CMLPADID,WORK        PACKED AD-ID                                
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(13),KEYSAVE      FOUND PACKED AD-ID?                         
         BNE   RDYES                NO                                          
*                                                                               
         MVC   AREC,GCPIO2          2ND IOAREA                                  
         GOTO1 GET                                                              
*                                                                               
         L     R4,AREC                                                          
         TM    CMLRSTAT,CMLKSTA_PCKD  PACKED AD-ID?                             
         BNZ   RDYES                  YES                                       
         MVC   WORK(8),CMLKCML        MOVE ISCII TO WORK                        
         B     RDNO                                                             
*                                                                               
RDYES    SR    RC,RC                                                            
RDNO     LTR   RC,RC                                                            
         B     GCPX                                                             
         DROP  R4                                                               
*                                                                               
*---------------------------------------------------------                      
*        LTORG                                                                  
*---------------------------------------------------------                      
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPTRRTAB                                                       
         DC    X'0000'                                                          
*                                                                               
CMLTD    DSECT                     DSECT FOR GCPCMTAB                           
CMLTFLM  DS    CL16                FILM CODE                                    
CMLTADID DS    CL16                AD-ID                                        
CMLTROT  DS    CL1                 ROTATION LETTER                              
CMLTACTV DS    CL1                                                              
         DS    CL2                 SPARE                                        
CMLTOCC  DS    XL4                 PATTERN OCCURENCES                           
*                                                                               
         EJECT                                                                  
*        GCP WORK AREA                                                          
*                                                                               
GCPW     DSECT                                                                  
GCPWK    DS    XL256                                                            
ELCODE   DS    X                                                                
BPATREC  DS    X                                                                
BPATDISP DS    XL2                                                              
PACKDCML DS    CL1                                                              
VTRPACK  DS    A                                                                
*                                                                               
       ++INCLUDE SPGCPD                                                         
*                                                                               
       ++INCLUDE SPTRINST                                                       
*                                                                               
       ++INCLUDE SPTRPAT                                                        
       ++INCLUDE SPTRNPAT                                                       
       ++INCLUDE SPTRCMML                                                       
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENCLT                                                       
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'053SPREPI2CP 06/20/11'                                      
         END                                                                    
