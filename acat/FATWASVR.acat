*          DATA SET FATWASVR   AT LEVEL 018 AS OF 02/25/20                      
*CATALP FATWASVR                                                                
         TITLE 'TWASVR - SET TWA RECORDS ON DISK FOR SESSION'                   
***********************************************************************         
* XL1    SESSION NUMBER  X'0S' WHERE S IS NEW SESSION NUMBER          *         
* AL3    A(SYSFACS)      OR A(0)                                      *         
*                                                                     *         
* XL1    SESSION ACTION  X'80' DISCONNECT FROM SESSION IN P1          *         
*                        X'40' TWA0 ALREADY IN TWA                    *         
*                        X'20' COMPLETE UTL/TCB CONTROLS              *         
*        RETURN VALUE    0=ACTION COMPLETED,N=ERROR NUMBER            *         
* AL3    RETURN A(TWA)   IF VALID ACTION                              *         
***********************************************************************         
         PRINT NOGEN                                                            
TWASVR   CSECT                                                                  
         NMODL WORKX-WORKD,**TWSR**,RR=RE                                       
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         LR    R2,R1               R2=A(PARAM LIST)                             
         ICM   RA,15,=V(SYSFAC)                                                 
         BNZ   TSVR1               MODULE LINKED INTO FACPAK                    
         ICM   RA,7,1(R2)          RA=A(SYSFAC LIST)                            
         JZ    *+2                 CALLER DIDNT PASS V(SYSFAC)                  
         USING SYSFACD,RA                                                       
*                                                                               
TSVR1    L     RE,VSSB             EXTRACT TEMPSTR INFO FROM SSB                
         MVC   RECLEN,SSBTWAL-SSBD(RE)                                          
         MVC   SSMAX,SSBSSMAX-SSBD(RE)                                          
         MVC   SSMXP,SSBSSMXP-SSBD(RE)                                          
         MVC   SSPGS,SSBSSPGS-SSBD(RE)                                          
         MVC   STRDATE(8),SSBSDATE-SSBD(RE)                                     
TSVR1A   CLC   RECLEN,=H'18432'    18K TEMPSTR DISPLACEMENTS                    
         BNE   TSVR1B                                                           
         MVC   GLODSP,=Y(CHKPTGLD)                                              
         MVC   CHKDSP,=Y(CHKPTDSP)                                              
         B     TSVR1E                                                           
TSVR1B   CLC   RECLEN,=H'14336'    14K TEMPSTR DISPLACEMENTS                    
         BNE   TSVR1C                                                           
         MVC   GLODSP,=H'12288'                                                 
         MVC   CHKDSP,=H'12800'                                                 
         B     TSVR1E                                                           
TSVR1C   DC    H'0'                                                             
TSVR1E   L     R4,SSBTKADR-SSBD(RE)                                             
         USING TCBD,R4             R4=A(TCB ENTRY)                              
         L     R5,TCBUTL                                                        
         USING UTLD,R5             R5=A(UTL ENTRY)                              
         L     R8,TUTLXADR                                                      
         USING XAUTLD,R8           R8=XA UTL ENTRY                              
         TM    TSTATC,TSTCXSES                                                  
         BO    *+16                                                             
         MVC   SSMAX,=AL2(4)                                                    
         MVC   SSMXP,=AL2(5)                                                    
*                                                                               
         MVC   TRMNUM,TNUM         SET TERMINAL NUMBER FROM UTL                 
         EJECT                                                                  
***********************************************************************         
* SET DMCB1 AND DMCB2 FOR I/O TO TEMPSTR                              *         
***********************************************************************         
TSVR2    LA    RE,TWA1             INITIALISE DMCB'S FOR TEMPSTR I/O            
         ST    RE,ATWA1                                                         
         MVC   WOTSES,4(R2)                                                     
         ST    RE,4(R2)            RETURN A(RESULT TWA) IN PARAM LIST           
         L     RE,=A(TWA2-WORKD)                                                
         LA    RE,WORKD(RE)                                                     
         ST    RE,ATWA2                                                         
         LA    RE,DMREAD           SET READ ACTION                              
         ST    RE,DMCB1                                                         
         ST    RE,DMCB2                                                         
*                                                                               
         LA    RE,TEMPSTR          SET TEMPSTR                                  
         ST    RE,DMCB1+4                                                       
         ST    RE,DMCB2+4                                                       
*                                                                               
         XC    DMCB1+8(2),DMCB1+8  SET TWA NUMBER                               
         XC    DMCB2+8(2),DMCB2+8                                               
         MVC   DMCB1+10(2),TNUM    SET TERMINAL NUMBER                          
         MVC   DMCB2+10(2),TNUM                                                 
*                                                                               
         MVC   DMCB1+12(4),ATWA1   SET BUFFER ADDRESS                           
         MVC   DMCB2+12(4),ATWA2                                                
         XC    DMCB1+16(8),DMCB1                                                
         XC    DMCB2+16(8),DMCB2                                                
*                                                                               
         MVC   DMCB1+20(2),=C'L='  SPECIAL PARM FOR TWA0 READ/WRITES            
         MVC   DMCB1+22(2),RECLEN                                               
         MVC   DMCB2+20(2),=C'L='                                               
         MVC   DMCB2+22(2),RECLEN                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE PARAM LIST AND INITIALISE SESSION NUMBERS                  *         
***********************************************************************         
TWASS    MVC   CURSES,TSESSION     SAVE CURRENT SESSION NUMBER                  
         MVI   STASES,0            INIT SESSION STATUS FLAGS                    
         MVC   NEWSES,0(R2)        NEW SESSION PASSED IN PARAM LIST             
         MVI   RELERR,0                                                         
         MVI   RETERR,0                                                         
         MVI   XTRSES,0                                                         
         CLC   NEWSES,SSMAX+1      TEST WITH LOGICAL MAX SESSIONS               
         BL    TWASS0                                                           
         BH    ERR8                SESSION EXCEEDS PHYSICAL MAX                 
         CLC   SSMAX,SSMXP                                                      
         BE    ERR8                SESSION EXCEEDS LOGICAL MAX                  
         MVI   XTRSES,1            SET EXTRA SESSION FLAG                       
*                                                                               
TWASS0   SR    R0,R0               GET TIME AND DATE                            
         SR    R1,R1                                                            
         TIME  BIN                                                              
         ST    R1,MVSDATE                                                       
         ST    R0,MVSTIME                                                       
         OI    MVSDATE+3,X'0F'                                                  
         EJECT                                                                  
***********************************************************************         
* READ CURRENT TWA#0 INTO TWA2 AND CHECK ITS VALIDITY                 *         
* CONVERT TO CONNECT SCREEN IF CURRENT TWA IS INVALID OR NOT CONNECTED*         
***********************************************************************         
TWASS1   LA    RE,DMREAD           READ CURRENT TWA#0 INTO TWA2                 
         ST    RE,DMCB2                                                         
         MVI   DMCB2+8,X'80'       SET ACTUAL PAGE NUMBER TO ZERO               
         MVC   DMCB2+10(2),TRMNUM                                               
         TM    WOTSES,TWATWA       TEST IF CURRENT TWA ALREADY READ             
         BZ    TWASS1A             NO                                           
         L     R0,ATWA2            YES CAN COPY TCBTWA INTO TWA2                
         LH    R1,RECLEN                                                        
         L     RE,TCBTWA                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     TWASS1X                                                          
TWASS1A  GOTO1 VDATAMGR,DMCB2                                                   
         CLI   8(R1),0                                                          
         BNE   ERR1                                                             
TWASS1X  EQU   *                                                                
*                                                                               
TWASS2   L     R3,ATWA2            POINT TO CURRENT SESSION TWA                 
         AH    R3,CHKDSP                                                        
         USING CHKPTD,R3                                                        
*                                                                               
         CLC   CHTDDATE(4),STRDATE                                              
         BL    TWASS2B             DATE IS LESS THAN FACPAKS                    
         BH    TWASS2C             DATE IS GREATER THAN FACPAKS                 
         CLC   CHTDDATE+4(4),STRDATE+4                                          
         BNL   TWASS2C             TIME IS >= FACPAK START                      
*                                                                               
TWASS2B  OI    STASES,CURINV       SET CURRENT SESSION INVALID                  
         B     TWASS2D                                                          
*                                                                               
TWASS2C  CLI   CHUTLSYS,0          TWA#0 IS VALID AND ACTIVE                    
         BE    *+14                                                             
         CLC   CHUTLSVC,=X'01FF'                                                
         BNE   TWASS2D                                                          
         OI    STASES,CURNCT       TWA#0 SAYS THAT WE ARE NOT CONNECTED         
*                                                                               
TWASS2D  LLC   R1,CURSES           CHECK UTL STATUS WITH TWA#0 STATUS           
         IC    RF,SSACBITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    TSSBITS,0                                                        
         BO    TWASS2X                                                          
TWASS2E  CLI   STASES,0            UTL SAYS THAT WE ARE NOT CONNECTED           
         BNE   TWASS2X                                                          
         OI    STASES,CURNCT                                                    
TWASS2X  EQU   *                                                                
*                                                                               
TWASS3   TM    STASES,CURINV+CURNCT TEST STATUS OF CURRENT SESS                 
         BZ    TWASS3X                                                          
         L     RE,ATWA2                                                         
         LH    RF,RECLEN           CLEAR EVERYTHING IF INVALID SESSION          
         TM    STASES,CURINV                                                    
         BO    *+8                                                              
         LH    RF,GLODSP           ELSE LEAVE GLOBALS+CHKPNT                    
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
TWASS3A  L     RE,ATWA2            LOAD S/R ROOT SCREEN INTO TWA#2              
         LA    RE,64(RE)                                                        
         ST    RE,DMCB+0                                                        
         MVC   DMCB+4(4),=X'D70110FF'                                           
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),X'FF'                                                      
         BE    ERR1                                                             
         L     RE,ATWA2            REMOVE LOGO FIELDS FROM SCREEN               
         LA    RE,64(RE)                                                        
         SR    R0,R0                                                            
         SR    R1,R1                                                            
TWASS3B  IC    R0,0(RE)            BUMP TO NEXT $CT SCREEN FIELD                
         AR    RE,R0                                                            
         LTR   R1,R1               TEST IF LOCATED S/R FIELD                    
         BNZ   TWASS3C                                                          
         L     R1,VSSB                                                          
         CLI   0(RE),X'19'         CHECK THAT S/R FIELD LOOKS OK                
         BNE   TWASS3C                                                          
         CLC   2(2,RE),=X'003E'                                                 
         BNE   TWASS3C                                                          
         MVC   8(8,RE),=C'=CT,*  *'                                             
         XC    16(9,RE),16(RE)     SET S/R FIELD TO =CT,*FS*                    
         MVC   13(1,RE),SSBSYSN1-SSBD(R1)                                       
         IC    R1,CURSES                                                        
         LA    R1,1(R1)                                                         
         STC   R1,14(RE)                                                        
         OI    14(RE),X'80'        SET LOWER CASE SESSION LETTER                
TWASS3C  CLI   0(RE),0                                                          
         BE    TWASS3D                                                          
         CLI   0(RE),9             TEST IF LOCATED ONE BYTE TAB FIELD           
         BNE   TWASS3B                                                          
         MVC   9(3,RE),=X'000101'  TRUNCATE LOGO                                
*                                                                               
TWASS3D  LLC   R1,CURSES           SET SESSION NOT ACTIVE IN UTL                
         IC    RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSBITS,0           TURN OFF SESSION ACTIVE BIT                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSXBITS,0          TURN OFF NON USABLE SESSION BIT              
         MVC   SUSER,CHUTLUSR      SET SESSION NOT ACTIVE IN CHKPT              
         MVC   SPASSWD,CHUTLPSW                                                 
         MVC   SAGY,CHUTLAGY                                                    
         MVC   SSYS,CHUTLSYS                                                    
         MVC   SPRG,CHUTLPRG                                                    
         XC    CHUTLSV(L'TCTDATA),CHUTLSV                                       
         MVI   CHUTLALI,0                                                       
         MVC   CHUTLUSR,SUSER                                                   
         MVC   CHUTLPSW,SPASSWD                                                 
         MVC   CHUTLAGY,SAGY                                                    
         LLC   R1,CURSES                                                        
         IC    RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    CHUTLSSB,0                                                       
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    CHUTLSSX,0                                                       
         XC    CHBILL,CHBILL                                                    
         TM    STASES,CURINV       CLEAR TEMPEST IF INVALID SESSION             
         BZ    TWASS3X                                                          
         XC    CHXTINF,CHXTINF                                                  
*                                                                               
TWASS3X  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* READ NEW SESSION TWA#0 INTO TWA1 AND CHECK ITS VALIDITY.            *         
* CONVERT TO CONNECT SCREEN IF NEW TWA IS INVALID OR NOT CONNECTED    *         
***********************************************************************         
TWASS4   LLC   R1,NEWSES           READ NEW TWA#0 INTO TWA1                     
         IC    RF,SSACBITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    TSSBITS,0           TEST IF CONNECTED IN THIS SESSION            
         BO    TWASS4A                                                          
         CLC   CURSES,NEWSES                                                    
         BE    TWASS4A                                                          
         OI    STASES,NEWNCT       SET NEW SESSION NOT CONNECTED                
         L     RE,ATWA1                                                         
         AH    RE,GLODSP           CLEAR GLOBALS+CHKPNT IF DONT READ            
         LH    RF,RECLEN                                                        
         SH    RF,GLODSP                                                        
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     TWASS6                                                           
TWASS4A  MH    R1,SSPGS            PAGE=2+(SESSION*PAGESPERSESSION)             
         LA    R1,2(R1)                                                         
         LA    RE,DMREAD                                                        
         ST    RE,DMCB1                                                         
         STC   R1,DMCB1+8          SET ACTUAL PAGE NUMBER                       
         OI    DMCB1+8,X'80'                                                    
         MVC   DMCB1+10(2),TRMNUM                                               
         CLC   CURSES,NEWSES       TEST IF NEW SESSION IS CURRENT               
         BNE   TWASS4B             NO                                           
         L     R0,ATWA1            YES CAN COPY TWA2 TO TWA1                    
         LH    R1,RECLEN                                                        
         L     RE,ATWA2                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     TWASS4X                                                          
TWASS4B  GOTO1 VDATAMGR,DMCB1                                                   
         CLI   8(R1),0                                                          
         BNE   ERR1                                                             
TWASS4X  EQU   *                                                                
*                                                                               
TWASS5   L     R3,ATWA1            POINT TO NEW SESSION TWA                     
         AH    R3,CHKDSP                                                        
         USING CHKPTD,R3                                                        
         CLC   CHTDDATE(4),STRDATE                                              
         BL    TWASS5B             DATE IS LESS THAN FACPAKS                    
         BH    TWASS5C             DATE IS GREATER THAN FACPAKS                 
         CLC   CHTDDATE(8),STRDATE TEST IF OLDER THAN THIS FACPAK               
         BNL   TWASS5C                                                          
*                                                                               
TWASS5B  OI    STASES,NEWINV       SET NEW SESSION INVALID                      
         B     TWASS5X                                                          
TWASS5C  CLI   CHUTLSYS,0          TEST IF CONNECTED OR ABENDED                 
         BE    *+14                                                             
         CLC   CHUTLSVC,=X'01FF'                                                
         BNE   *+8                                                              
         OI    STASES,NEWNCT       SET NEW SESSION NOT CONNECTED                
TWASS5X  EQU   *                                                                
*                                                                               
TWASS6   TM    WOTSES,DISCON       TEST IF NEED TO DISCONNECT SESSION           
         BO    *+12                                                             
         TM    STASES,NEWINV+NEWNCT                                             
         BZ    TWASS8                                                           
         L     RE,ATWA1                                                         
         LH    RF,RECLEN           CLEAR EVERYTHING IF INVALID SESSION          
         TM    STASES,NEWINV                                                    
         BO    *+8                                                              
         LH    RF,GLODSP           ELSE LEAVE GLOBALS+CHKPNT                    
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
TWASS6A  L     RE,ATWA1            LOAD S/R ROOT SCREEN INTO TWA#1              
         LA    RE,64(RE)                                                        
         ST    RE,DMCB+0                                                        
         MVC   DMCB+4(4),=X'D70110FF'                                           
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),X'FF'                                                      
         BE    ERR1                                                             
         L     RE,ATWA1            REMOVE LOGO FIELDS FROM SCREEN               
         LA    RE,64(RE)                                                        
         SR    R0,R0                                                            
         SR    R1,R1                                                            
TWASS6B  IC    R0,0(RE)            BUMP TO NEXT $CT SCREEN FIELD                
         AR    RE,R0                                                            
         LTR   R1,R1               TEST IF LOCATED S/R FIELD                    
         BNZ   TWASS6C                                                          
         L     R1,VSSB                                                          
         CLI   0(RE),X'19'         CHECK THAT S/R FIELD LOOKS OK                
         BNE   TWASS6C                                                          
         CLC   2(2,RE),=X'003E'                                                 
         BNE   TWASS6C                                                          
         MVC   8(8,RE),=C'=CT,*  *'                                             
         XC    16(9,RE),16(RE)     SET S/R FIELD TO =CT,*FS*                    
         MVC   13(1,RE),SSBSYSN1-SSBD(R1)                                       
         IC    R1,NEWSES                                                        
         LA    R1,1(R1)                                                         
         STC   R1,14(RE)                                                        
         OI    14(RE),X'80'        SET LOWER CASE SESSION LETTER                
TWASS6C  CLI   0(RE),0                                                          
         BE    TWASS7                                                           
         CLI   0(RE),9             TEST IF LOCATED ONE BYTE TAB FIELD           
         BNE   TWASS6B                                                          
         MVC   9(3,RE),=X'000101'  TRUNCATE LOGO                                
         EJECT                                                                  
***********************************************************************         
* NEW SESSION IS NOT ACTIVE OR REQUEST TO DISCONNECT NEW SESSION.     *         
* UPDATE UTL SESSION BITS TO SHOW NOT ACTIVE IN NEW SESSION           *         
* UPDATE CHKPT DATA TO SHOW NOT ACTIVE AND RELEASE SESSION RESOURCES  *         
* WRITE BACK NEW SESSION'S DISCONNECTED TWA BACK TO TEMPSTR           *         
***********************************************************************         
TWASS7   LLC   R1,NEWSES           SET SESSION NOT ACTIVE IN UTL                
         IC    RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSBITS,0           TURN OFF SESSION ACTIVE BIT                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSXBITS,0          TURN OFF NON USABLE SESSION BIT              
*                                                                               
         CLI   XTRSES,0            IGNORE IF EXTRA SESSION                      
         BNE   TWASS7A                                                          
         LLC   RE,TSSSWAP          CONVERT LAST PREV TO RE,RF                   
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CLM   RE,1,NEWSES         CLEAR RE IF WE JUST CLEARED SESSION          
         BNE   *+8                                                              
         LA    RE,15                                                            
         CLM   RF,1,NEWSES         CLEAR RF IF WE JUST CLEARED SESSION          
         BNE   *+8                                                              
         LA    RF,15                                                            
         SLL   RF,28               CONVERT BACK AND SAVE                        
         SLDL  RE,4                                                             
         STC   RE,TSSSWAP                                                       
*                                                                               
TWASS7A  TM    STASES,NEWINV+NEWNCT CLEANUP IF INVALID OR DISCONNECT            
         BNZ   TWASS7X                                                          
         TM    WOTSES,DISCON                                                    
         BZ    TWASS7X                                                          
*                                                                               
TWASS7B  OC    CHXTNDX,CHXTNDX     RELEASE TEMPEST HELD BY THIS SESSION         
         BZ    TWASS7B1                                                         
         TM    CHXTFLG,X'80'       TEST IF TEMPEST NOT OWNED BY SESSION         
         BO    TWASS7B1                                                         
         MVC   TCBXTINF,CHXTINF                                                 
         GOTO1 VDATAMGR,DMCB,(0,DMRLSE),TEMPEST,(255,0),0                       
TWASS7B1 XC    CHXTINF,CHXTINF                                                  
*                                                                               
TWASS7C  OC    CHBILL,CHBILL       TERMINATE BILLING THIS SESSION               
         BZ    TWASS7D                                                          
         MVC   TCBBILL,CHBILL                                                   
         L     RF,VSYSFAC0                                                      
         L     RF,CBILLIT-COMFACSD(RF)                                          
         XC    DMCB(8),DMCB                                                     
         MVI   DMCB,C'E'           SET END BILLING                              
         LA    R1,DMCB                                                          
         BASR  RE,RF                                                            
         XC    CHBILL,CHBILL                                                    
*                                                                               
TWASS7D  MVC   SDATE,CHTDDATE      SAVE DATE/TIME                               
         MVC   STIME,CHTDTIME                                                   
         MVC   SUSER,CHUTLUSR      SAVE SOME CONNECT DATA                       
         MVC   SPASSWD,CHUTLPSW                                                 
         MVC   SAGY,CHUTLAGY                                                    
         MVC   SSYS,CHUTLSYS                                                    
         MVC   SPRG,CHUTLPRG                                                    
         XC    CHUTLSV(L'TCTDATA),CHUTLSV                                       
         MVI   CHUTLALI,0                                                       
         MVC   CHUTLUSR,SUSER                                                   
         MVC   CHUTLPSW,SPASSWD                                                 
         MVC   CHUTLAGY,SAGY                                                    
         LLC   R1,NEWSES                                                        
         IC    RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    CHUTLSSB,0          TURN OFF SESSION ACTIVE BIT                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    CHUTLSSX,0          TURN OFF NON USABLE SESSION BIT              
*                                                                               
TWASS7E  TM    STASES,CURINV       TEST IF CURRENT SESSION VALID                
         BO    TWASS7F                                                          
         L     R6,ATWA2            POINT TO CURRENT SESSION CHKPT               
         AH    R6,CHKDSP                                                        
         MVC   CHBCDATA,CHBCDATA-CHKPTD(R6) COPY BCAST FROM CUR TO NEW          
         L     R0,ATWA1                                                         
         AH    R0,GLODSP                                                        
         LA    R1,CHKPTGLL                                                      
         L     RE,ATWA2                                                         
         AH    RE,GLODSP                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY GLOBALS FROM CUR TO NEW                 
*                                                                               
TWASS7F  LLC   R1,NEWSES           WRITE BACK NEW SESSION TWA#0                 
         MH    R1,SSPGS                                                         
         LA    R1,2(R1)            PAGE=2+(SESSION*PAGESPERSESSION)             
         LA    RE,DMWRT                                                         
         ST    RE,DMCB1                                                         
         STC   R1,DMCB1+8          SET ACTUAL PAGE NUMBER                       
         OI    DMCB1+8,X'80'                                                    
         MVC   DMCB1+10(2),TRMNUM                                               
         GOTO1 VDATAMGR,DMCB1                                                   
         CLI   8(R1),0                                                          
         BNE   ERR1                                                             
         B     TWASS9                                                           
TWASS7X  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* NEW SESSION IS ACTIVE                                               *         
* COPY CURRENT SESSION'S CHKPT DATA TO NEW SESSION                    *         
***********************************************************************         
TWASS8   TM    STASES,CURINV       TEST IF CURRENT SESSION VALID                
         BO    TWASS8X                                                          
         L     R3,ATWA1            POINT TO NEW SESSION CHKPT                   
         AH    R3,CHKDSP                                                        
         USING CHKPTD,R3                                                        
         L     R6,ATWA2            POINT TO CURRENT SESSION CHKPT               
         AH    R6,CHKDSP                                                        
         MVC   CHBCDATA,CHBCDATA-CHKPTD(R6) COPY BCAST FROM CUR TO NEW          
         L     R0,ATWA1                                                         
         AH    R0,GLODSP                                                        
         LA    R1,CHKPTGLL                                                      
         L     RE,ATWA2                                                         
         AH    RE,GLODSP                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY GLOBALS FROM CUR TO NEW                 
TWASS8X  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* IF REQUESTED DISCONNECT FROM CURRENT SESSION WRITE CURRENT          *         
* SESSIONS'S DISCONNECTED TWA BACK TO TWA#0                           *         
* UPDATE UTL TO SHOW CURRENT SESSION NOT CONNECTED                    *         
***********************************************************************         
TWASS9   TM    WOTSES,DISCON       REQUEST DISCONNECT FROM CURRENT SESS         
         BZ    TWASS10                                                          
         CLC   CURSES,NEWSES                                                    
         BNE   TWASS9X                                                          
         LA    RE,DMWRT                                                         
         ST    RE,DMCB1                                                         
         MVI   DMCB1+8,X'80'       WRITE BACK UPDATED CURRENT TWA#0             
         MVC   DMCB1+10(2),TRMNUM                                               
         GOTO1 VDATAMGR,DMCB1                                                   
         CLI   8(R1),0                                                          
         BNE   ERR1                                                             
TWASS9A  MVC   SUSER,TUSER         SAVE SOME CONNECT DATA                       
         MVC   SPASSWD,TPASSWD                                                  
         MVC   SAGY,TAGY                                                        
         MVC   SSYS,TSYS                                                        
         MVC   SPRG,TPRG                                                        
         MVC   SSVCREQ,TSVCREQ                                                  
         XC    TCTDATA,TCTDATA     SET UTL TO SHOW DISCONNECTED                 
         MVI   TPRGALI,0                                                        
         XC    TPERSON,TPERSON                                                  
         NI    TSTAT7,255-(TST7PS0+TST7PS1+TST7PS2+TST7PSWN)                    
         MVI   TPASSEXP,0                                                       
         MVI   TSTATB,0                                                         
         XC    TAGYPER,TAGYPER                                                  
         XC    TAGYSEC,TAGYSEC                                                  
         XC    TUPDFAC,TUPDFAC                                                  
         XC    TTICKET,TTICKET                                                  
         SAM31                                                                  
         XC    TTICKETN,TTICKETN                                                
         SAM24                                                                  
         XC    TXPINFO,TXPINFO                                                  
         MVC   TSVCREQ,SSVCREQ                                                  
         XC    TAPRG,TAPRG                                                      
         NI    TSTAT6,255-TST6STRO-TST6STFU-TST6STSS                            
         NI    TSTATD,255-TSTATWSP-TSTATCPV                                     
TWASS9X  B     TWAOK                                                            
         EJECT                                                                  
***********************************************************************         
* UPDATE THE CURRENT SESSION'S TWA 0 WITH THE LATEST VERSION IN TWA#0 *         
***********************************************************************         
TWASS10  LLC   R1,CURSES           WRITE BACK CURRENT SESSION TWA#0             
         MH    R1,SSPGS                                                         
         LA    R1,2(R1)            PAGE=2+(SESSION*PAGESPERSESSION)             
         LA    RE,DMWRT                                                         
         ST    RE,DMCB2                                                         
         STC   R1,DMCB2+8          SET ACTUAL PAGE NUMBER                       
         OI    DMCB2+8,X'80'                                                    
         MVC   DMCB2+10(2),TRMNUM                                               
         CLC   CURSES,NEWSES       NO NEED TO WRITE IF NEW SAME AS CUR          
         BE    TWASS10X                                                         
TWASS10A GOTO1 VDATAMGR,DMCB2                                                   
         CLI   8(R1),0                                                          
         BNE   ERR1                                                             
TWASS10X EQU   *                                                                
*                                                                               
TWASS11  TM    STASES,NEWINV       EXIT WITH NEW SESSION INVALID                
         BNZ   ERR6                                                             
         TM    STASES,NEWNCT       EXIT WITH NEW SESSION NOT ACTIVE             
         BO    ERR7                                                             
         B     TWAOK               EXIT WITH NEW SESSION TWA IN PARAM2          
         EJECT                                                                  
***********************************************************************         
* RETURN ERROR CODE AND A(ERROR MESSAGE)                              *         
***********************************************************************         
ERR1     LA    RE,1                DISK READ FAILURE                            
         B     ERRX                                                             
ERR2     LA    RE,2                DISK WRITE FAILURE                           
         B     ERRX                                                             
ERR3     LA    RE,3                MUST BE CONNECTED                            
         B     ERRX                                                             
ERR4     LA    RE,4                NO SCREEN SAVED                              
         B     ERRX                                                             
ERR5     LA    RE,5                SAVE/RESTORE INVALID FOR CUR PROGRAM         
         B     ERRX                                                             
ERR6     LA    RE,6                SAVED SCREEN TOO OLD/SESSION INVALID         
         B     ERRX                                                             
ERR7     LA    RE,7                SESSION IS NOT ACTIVE                        
         B     ERRX                                                             
ERR8     LA    RE,8                SESSION DOES NOT EXIST                       
         B     ERRX                                                             
*                                                                               
ERRX     LR    RF,RE               RE=RELATIVE ERROR NUMBER                     
         AHI   RF,119              RF=ACTUAL DISK ERROR NUM FOR SERVICE         
         STC   RE,RELERR                                                        
         STC   RF,RETERR                                                        
         STC   RF,4(R2)            RETURN ERROR NUMBER                          
         TM    WOTSES,UPDUTL                                                    
         BO    SCB                 SET CONTROL BLOCKS                           
         B     TWASVRX                                                          
*                                                                               
TWAOK    MVI   4(R2),0             RETURN OK ACTION                             
         TM    WOTSES,UPDUTL                                                    
         BO    SCB                                                              
*                                                                               
TWASVRX  XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* MOVE UPDATED/LATEST TWA#0 TO TASK'S TCBTWA AREA                     *         
* COMPLETE UTL/TWA/TCB CONTROL BLOCKS FOR NEW SESSION                 *         
***********************************************************************         
SCB      CLI   RELERR,0            TEST IF NEW SESSION VALID                    
         BE    SCB02                                                            
         CLI   RELERR,6            TEST IF NEW SESSION INVALID                  
         BE    SCB02                                                            
         CLI   RELERR,7            TEST IF NEW SESSION NOT CONNECTED            
         BE    SCB02                                                            
         B     SCBX                                                             
*                                                                               
SCB02    L     RE,ATWA1            MOVE LATEST SCREEN TO MY TWA                 
         LH    RF,RECLEN                                                        
         L     R3,TCBTWA           R3=A(TWA)                                    
         LR    R0,R3                                                            
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
         LLC   R1,64(R3)                                                        
         LA    R7,64(R3,R1)        R7=A(S/R FIELD HEADER)                       
         CLI   0(R7),X'19'         CHECK THAT IT LOOKS OK                       
         BNE   *+14                                                             
         CLC   2(2,R7),=X'003E'                                                 
         BE    SCB02A                                                           
         SR    R7,R7                                                            
*                                                                               
SCB02A   AH    R3,CHKDSP           R3=A(TWA0 CHECKPOINT AREA)                   
         USING CHKPTD,R3                                                        
SCB02B   NI    TSTAT6,255-TST6STRO-TST6STFU-TST6STSS                            
         TM    CHUTLST6,TST6STRO                                                
         BZ    *+8                                                              
         OI    TSTAT6,TST6STRO     RESTORE STEREO FLAGS FROM CHKPNT             
         TM    CHUTLST6,TST6STFU                                                
         BZ    *+8                                                              
         OI    TSTAT6,TST6STFU                                                  
         TM    CHUTLST6,TST6STSS                                                
         BZ    *+8                                                              
         OI    TSTAT6,TST6STSS                                                  
*                                                                               
SCB03    CLI   RELERR,6            FIX UTL FOR DISCONNECTED SESSION             
         BE    SCB03A                                                           
         CLI   RELERR,7                                                         
         BE    SCB03A                                                           
         TM    WOTSES,DISCON                                                    
         BZ    SCB04                                                            
SCB03A   MVC   SUSER,TUSER         SAVE USERID NUMBER                           
         MVC   SPASSWD,TPASSWD     SAVE PASSWORD NUMBER                         
         MVC   SAGY,TAGY           SAVE AGENCY ALPHA                            
         MVC   SSYS,TSYS                                                        
         MVC   SPRG,TPRG                                                        
         MVC   SSVCREQ,TSVCREQ                                                  
         XC    TCTDATA,TCTDATA     SET UTL TO SHOW DISCONNECTED                 
         MVI   TPRGALI,0                                                        
         XC    TPERSON,TPERSON                                                  
         NI    TSTAT7,255-(TST7PS0+TST7PS1+TST7PS2+TST7PSWN)                    
         MVI   TPASSEXP,0                                                       
         MVI   TSTATB,0                                                         
         XC    TAGYPER,TAGYPER                                                  
         XC    TUPDFAC,TUPDFAC                                                  
         XC    TTICKET,TTICKET                                                  
         SAM31                                                                  
         XC    TTICKETN,TTICKETN                                                
         SAM24                                                                  
         MVC   TSVCREQ,SSVCREQ                                                  
         XC    TAPRG,TAPRG                                                      
         MVC   TSESSION,NEWSES     SET NEW SESSION                              
         LLC   R1,TSESSION                                                      
         IC    RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSBITS,0           TURN OFF SESSION ACTIVE BIT                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TSSXBITS,0          TURN OFF NON USABLE SESSION BIT              
         B     SCBX                                                             
*                                                                               
SCB04    MVC   SVTSVC,TSVCREQ      RESTORE CONNECT DATA                         
         MVC   TCTDATA,CHUTLSV                                                  
         MVC   TPRGALI,CHUTLALI                                                 
         MVC   TPERSON,CHUTLPER                                                 
         MVC   TSVCREQ,SVTSVC                                                   
*                                                                               
         MVC   SVFLAG,TFLAG                                                     
         MVC   SVTRCNT,TTRCNT                                                   
         MVC   TSVDATA1,CHUTLSV1                                                
         MVC   TFLAG,SVFLAG                                                     
         MVC   TTRCNT,SVTRCNT                                                   
*                                                                               
         MVC   TAGYSEC,CHUTLASC                                                 
         MVC   TACCS2,CHUTLAC2                                                  
*                                                                               
         MVC   TXPINFO,CHUTLXPI                                                 
*                                                                               
         NI    TSTAT6,255-TST6LOGN                                              
         TM    CHUTLST6,TST6LOGN                                                
         BZ    *+8                                                              
         OI    TSTAT6,TST6LOGN                                                  
*                                                                               
         NI    TSTAT7,255-(TST7PS0+TST7PS1+TST7PS2+TST7PSWN)                    
         TM    CHUTLST7,TST7PS0                                                 
         BZ    *+8                                                              
         OI    TSTAT7,TST7PS0                                                   
         TM    CHUTLST7,TST7PSWN                                                
         BZ    *+8                                                              
         OI    TSTAT7,TST7PSWN                                                  
         MVC   TPASSEXP,CHUTLPEX                                                
         MVC   TSTATB,CHUTLSTB                                                  
         MVC   TAGYPER,CHUTLAGP                                                 
         MVC   TUPDFAC,CHUTLUPF                                                 
         MVC   TTICKET,CHUTLTKT                                                 
*                                                                               
         SAM31                                                                  
         MVC   TTICKETN,CHUTLTKN   RESTORE TICKETN TO XA UTL                    
         SAM24                                                                  
*                                                                               
*&&UK                                                                           
         NI    TSTAT9,255-TST9AGMS-TST9AGPR                                     
         TM    CHUTLST9,TST9AGMS                                                
         BZ    *+8                                                              
         OI    TSTAT9,TST9AGMS                                                  
         TM    CHUTLST9,TST9AGPR                                                
         BZ    *+8                                                              
         OI    TSTAT9,TST9AGPR                                                  
*&&                                                                             
*&&US                                                                           
         NI    TSTAT9,255-TST9SPTR                                              
         TM    CHUTLST9,TST9SPTR                                                
         BZ    *+8                                                              
         OI    TSTAT9,TST9SPTR                                                  
*&&                                                                             
SCB05    CLI   TSYS,0              TEST IF CONNECTED SESSION                    
         BE    SCBX                                                             
SCB05A   L     R1,VSELIST          RESET TASYS AND TAPRG                        
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         CLC   TSYS,SESYS-SELISTD(R1)                                           
         BE    *+10                                                             
         BXLE  R1,RE,*-10                                                       
         DC    H'0'                                                             
         STCM  R1,7,TASYS                                                       
         STCM  R1,7,TARSYS                                                      
*                                                                               
         L     R1,SEPGMS-SELISTD(R1)                                            
         LH    RE,0(R1)            SET BXLE FOR PGMLST READ                     
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         MVI   FLAG,0                                                           
SCB05B   CLC   TAGCTRY,PGMCTRY-PGMLSTD(R1)                                      
         BNE   *+8                                                              
         MVI   FLAG,1              ENTRY FOUND FOR THIS CTRY                    
         CLC   TPRG,PGMNUM-PGMLSTD(R1)                                          
         BNE   SCB05C              NO MATCH ON PGM NUMBER                       
         CLI   PGMCTRY-PGMLSTD(R1),0                                            
         BNE   *+12                IF NOT CTRY=0 IT MUST MATCH                  
         CLI   FLAG,1              IF NO ENTRIES FOUND THEN OK                  
         BNE   SCB05D                                                           
         CLC   TAGCTRY,PGMCTRY-PGMLSTD(R1)                                      
         BE    SCB05D                                                           
SCB05C   BXLE  R1,RE,SCB05B                                                     
         XR    R1,R1                                                            
         OC    TAPRG,TAPRG         TEST P=XX INPUT ON CONNECT                   
         BZ    SCB05E                                                           
         DC    H'0'                                                             
*                                                                               
SCB05D   L     RF,VSSB             TAPRG IS DISPLACEMENT?                       
         TM    SSBSTAT4-SSBD(RF),SSBPGDSP                                       
         BZ    SCB05E                                                           
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS                                                      
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         SR    R1,RF               SET DISPLACEMENT TO TABLE ENTRY              
SCB05E   STCM  R1,7,TAPRG                                                       
*                                                                               
SCB06    MVC   TCBLNSYS,CHLNSYS    RESTORE TCB SYS AND PRG                      
         MVC   TCBLNPRG,CHLNPRG                                                 
         MVC   TCBSRMSG,CHSRMSG                                                 
         MVC   TCBBILL,CHBILL      RESTORE BILLING REFERENCE                    
         MVC   TCBXTINF,CHXTINF    RESTORE TEMPEST ALLOCATION                   
*                                                                               
SCB07    SR    R0,R0               R0=N'SYSTEM SWITCH ENTRIES                   
         ICM   R0,1,CHTCBNUM                                                    
         BZ    SCB08                                                            
         CHI   R0,TCBSWMAX         DO NOT RESTORE A BAD COUNT                   
         JH    *+2                                                              
         STC   R0,TCBSWNUM                                                      
         LA    RE,TCBSWTAB                                                      
         USING TCBSWTAB,RE         RE=A(TCB SYSTEM TABLE)                       
         LA    RF,CHTCBTAB                                                      
         USING CHTCBTAB,RF         RF=A(TWA CHKPT TABLE)                        
SCB07A   MVC   TCBSWSYS,CHTCBSYS                                                
         MVC   TCBSWSOV,CHTCBSOV                                                
         MVC   TCBSWAGB,CHTCBAGB                                                
         MVC   TCBSWACS,CHTCBACS                                                
         LA    RE,TCBSWLEN(RE)                                                  
         LA    RF,CHTCBLEN(RF)                                                  
         BCT   R0,SCB07A                                                        
         DROP  RE,RF                                                            
*                                                                               
SCB08    MVC   TSESSION,NEWSES     SET NEW SESSION NUMBER                       
         LLC   R1,TSESSION                                                      
         IC    R1,SSACBITS(R1)                                                  
         EX    R1,*+8                                                           
         B     *+8                                                              
         OI    TSSBITS,0           SET SESSION ACTIVE BIT                       
         LTR   R7,R7                                                            
         BZ    SCB09                                                            
         TM    TSTAT6,TST6STRO                                                  
         BO    *+14                                                             
         MVC   8(17,R7),CHSRMSG    RESTORE *USR/SYS/PRG/FS*                     
         B     SCB09                                                            
         XC    8(17,R7),8(R7)      CLEAR FOR STEREO DATA                        
         MVI   8(R7),C'*'                                                       
*                                                                               
SCB09    NI    TFLAG,255-TFLAGRTS                                               
         LA    RF,L'CHXTNUM-1                                                   
         LA    R1,CHXTNUM                                                       
         L     RE,VSSB             NEW TEMPEST?                                 
         TM    SSBSTAT4-SSBD(RE),SSBTMPST                                       
         BZ    *+12                                                             
         LA    RF,L'CHXTNDX-1                                                   
         LA    R1,CHXTNDX                                                       
         EX    RF,SCBCMP                                                        
         BNZ   *+8                                                              
         OI    TFLAG,TFLAGRTS      SET RESERVED TEMPEST SPACE                   
*                                                                               
         MVC   CHUTLSV1+TFLAG-TSVDATA1,TFLAG                                    
         NI    TSTAT1,255-TSTATBIL                                              
         OC    CHBILL,CHBILL                                                    
         BZ    *+8                                                              
         OI    TSTAT1,TSTATBIL     SET BILLING ACTIVE                           
*                                                                               
SCBX     B     TWASVRX                                                          
*                                                                               
SCBCMP   OC    0(0,R1),0(R1)                                                    
         EJECT                                                                  
CURINV   EQU   X'01'               CURRENT SESSION INVALID                      
*URTO    EQU   X'02'               N/D WAS TIMEOUT                              
CURNCT   EQU   X'04'               CURRENT SESSION NOT CONNECTED                
NEWINV   EQU   X'10'               NEW SESSION IS INVALID                       
*EWTO    EQU   X'20'               N/D WAS TIMEOUT                              
NEWNCT   EQU   X'40'               NEW SESSION NOT CONNECTED                    
*                                                                               
DISCON   EQU   X'80'               DISCONNECT SESSION                           
TWATWA   EQU   X'40'               TWA#0 ALREADY READ INTO TWA                  
UPDUTL   EQU   X'20'               COMPLETE UTL/TCB CONTROL BLOCKS              
*                                                                               
NUMTWAS  DC    H'5'                NUM OF TWAS TO COPY                          
ACTTWAS  DC    X'000102030400'     ACTUAL TWA NUMBERS 0,1,2,3,4                 
SVDTWAS  DC    X'060708090A00'     SAVED TWA NUMBERS  6,7,8,9,10                
*                                                                               
SSACBITS DC    X'0102040810204080'                                              
SSNABITS DC    X'FEFDFBF7EFDFBF7F'                                              
*                                                                               
DMREAD   DC    CL8'DMREAD'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
DMRLSE   DC    CL8'DMRLSE'                                                      
TEMPSTR  DC    CL8'TEMPSTR'                                                     
TEMPEST  DC    CL8'TEMPEST'                                                     
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
RELO     DS    A                                                                
DMCB     DS    6F                                                               
DMCB1    DS    6F                                                               
DMCB2    DS    6F                                                               
ATWA1    DS    A                                                                
ATWA2    DS    A                                                                
*                                                                               
STRDATE  DS    PL4                 FACPAK STARTED DATE                          
STRTIME  DS    F                   FACPAK STARTED TIME                          
MVSDATE  DS    PL4                 DATE IN P'0CYYDDDS' FORMAT                   
MVSTIME  DS    F                   TIME IN 1/100 SEC UBNITS                     
*                                                                               
TRMNUM   DS    H                                                                
RECLEN   DS    H                                                                
CHKDSP   DS    H                                                                
GLODSP   DS    H                                                                
SSMAX    DS    H                                                                
SSMXP    DS    H                                                                
SSPGS    DS    H                                                                
WOTSES   DS    X                                                                
CURSES   DS    X                                                                
NEWSES   DS    X                                                                
STASES   DS    X                                                                
STIME    DS    XL4                                                              
SDATE    DS    PL4                                                              
SSVCREQ  DS    XL2                                                              
SUSER    DS    XL2                                                              
SPASSWD  DS    XL2                                                              
SAGY     DS    XL2                                                              
SSYS     DS    X                                                                
SPRG     DS    X                                                                
RELERR   DS    X                                                                
RETERR   DS    X                                                                
FLAG     DS    X                                                                
SRMSG    DS    CL17                                                             
*                                                                               
SVTSVC   DS    XL2                                                              
SVTRCNT  DS    XL2                                                              
SVFLAG   DS    XL1                                                              
SVSESS   DS    XL1                                                              
XTRSES   DS    XL1                                                              
*                                                                               
         DS    0D                                                               
TWA1     DS    18432X                                                           
TWA2     DS    18432X                                                           
*                                                                               
WORKX    DS    0C                                                               
         EJECT                                                                  
*FADSECTS                                                                       
       ++INCLUDE FADSECTS                                                       
*DDCOMFACS                                                                      
       ++INCLUDE DDCOMFACS                                                      
*FACHKPT                                                                        
       ++INCLUDE FACHKPT                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018FATWASVR  02/25/20'                                      
         END                                                                    
