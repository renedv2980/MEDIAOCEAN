*          DATA SET DDTIMBER   AT LEVEL 003 AS OF 04/14/15                      
*CATALP TIMBER                                                                  
         TITLE 'DDTIMBER - TIME INPUT VALIDATION AND OUTPUT FORMAT'             
***********************************************************************         
* XL1 X'80'=VALIDATE INPUT IN P3     RETURN NONZERO IF ERROR          *         
*     X'40'=INPUT HAS ONLY ONE TIME                                   *         
*     X'20'=COMPUTE TIME DIFFERENCE                                   *         
*     X'10'=INPUT HAS ONE/TWO TIMES                                   *         
* AL3 LENGTH OF EXTRNAL FIELD IN P3  RETURN TIME DIFFERENCE           *         
*                                                                     *         
* XL1 0=INTERNAL VALUE HHMM IN BINARY                                 *         
*     1=INTERNAL VALUE IN PWOS                                        *         
*     2=INTERNAL VALUE TOTAL MINUTES IN BINARY                        *         
*     3=INTERNAL VALUE X'HHMM'                                        *         
* AL3 A(INTERNAL 4 BYTE FIELD HOLDING TWO 2-BYTE TIMES)               *         
*                                                                     *         
* XL1 FORMAT OF TIME DISPLAY                                          *         
* AL3 A(EXTERNAL FIELD)                                               *         
*                                                                     *         
* IF ONLY ONE TIME INPUT SET TIME2=TIME1                              *         
***********************************************************************         
                                                                                
         PRINT NOGEN                                                            
TIMBER   CSECT                                                                  
         ENTRY TIMVAL                                                           
         NMOD1 TWORKX-TWORKS,**TIME**,RR=RA                                     
         USING TWORKS,RC                                                        
         SAM24                     SET 24-BIT MODE                              
*                                                                               
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         CTRY                                                                   
         STCM  R0,3,CTRY           SAVE CTRY/LANG CODES                         
         CLI   LANG,0                                                           
         BE    *+12                                                             
         CLI   LANG,7                                                           
         BNH   *+10                                                             
         MVC   CTRY(2),DEFCTRY     SET DEFAULT UK=1 OR US=2                     
*                                                                               
         LLC   RE,LANG             SAVE LANGUAGE TABLE RECORD                   
         SRL   RE,3                                                             
         LA    R1,ATABADS(RE)                                                   
         MVC   AIN(8),0(R1)                                                     
         L     R1,AIN              RELOCATE A-TYPES                             
         A     R1,RELO                                                          
         ST    R1,AIN                                                           
         L     R1,AOUT                                                          
         A     R1,RELO                                                          
         ST    R1,AOUT                                                          
*                                                                               
         CLI   DDIFORM,3           VALIDATE INPUT FORMAT                        
         BH    ERRORJ10                                                         
         TM    DDCNTRL,X'40'+X'20' IF 1 TIME AND DIFF. REQUEST                  
         BO    ERRORJ10            EXIT WITH ERROR                              
*                                                                               
         LM    R3,R5,PARM          GET INPUT PARAMETERS                         
         ICM   R3,8,=X'00'         R3=EXTERNAL LENGTH                           
         ICM   R4,8,=X'00'         R4=INTERNAL ADDRESS                          
         ICM   R5,8,=X'00'         R5=TEXTERNAL ADDRESS                         
*                                                                               
         B     ACTN                GOTO MAIN CONTROL ROUTINE                    
         EJECT                                                                  
***********************************************************************         
* VALIDATE IO CONTROL ACTION AND PASS CONTROL TO PROCESSING ROUTINE   *         
***********************************************************************         
ACTN     LTR   R3,R3                                                            
         BNZ   ACTN1              IF NULL EXTERNAL LENGTH                       
         TM    DDCNTRL,X'20'      AND DIFF. REQUEST                             
         BZ    ERRORJ11           EXIT WITH ERROR                               
         BAS   R9,TDIFF           ELSE PROCESS DIFFERENCE                       
         B     EXIT               AND EXIT                                      
*                                                                               
ACTN1    TM    DDCNTRL,X'80'      TEST FOR INPUT OR OUTPUT                      
         BNZ   TENT               CONVERT TO INTERNAL FORMAT                    
         B     TOUT               CONVERT TO EXTERNAL FORMAT                    
*                                                                               
ERRORJ   MVI   ERRVAL,C'!'        TEMPORARY ERROR ROUTINES                      
         B     ERROR                                                            
ERRORJ1  MVI   ERRVAL,C'A'                                                      
         B     ERROR                                                            
ERRORJ2  MVI   ERRVAL,C'B'                                                      
         B     ERROR                                                            
ERRORJ3  MVI   ERRVAL,C'C'                                                      
         B     ERROR                                                            
ERRORJ4  MVI   ERRVAL,C'D'                                                      
         B     ERROR                                                            
ERRORJ5  MVI   ERRVAL,C'E'                                                      
         B     ERROR                                                            
ERRORJ6  MVI   ERRVAL,C'F'                                                      
         B     ERROR                                                            
ERRORJ7  MVI   ERRVAL,C'G'                                                      
         B     ERROR                                                            
ERRORJ8  MVI   ERRVAL,C'H'                                                      
         B     ERROR                                                            
ERRORJ9  MVI   ERRVAL,C'I'                                                      
         B     ERROR                                                            
ERRORJ10 MVI   ERRVAL,C'J'                                                      
         B     ERROR                                                            
ERRORJ11 MVI   ERRVAL,C'K'                                                      
         B     ERROR                                                            
*                                                                               
ERROR    L     R1,APARM            DEAL WITH ERROR CONDITION                    
         MVC   0(L'PARM,R1),PARM                                                
         MVC   0(1,R1),ERRVAL      SET ERROR CODE IN PLIST                      
         B     EXITX                                                            
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         MVI   0(R1),0                                                          
*                                                                               
EXITX    XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* VALIDATE AND CONVERT TIME EXTERNAL FIELD TO INTERNAL FORMAT         *         
***********************************************************************         
TENT     MVI   TYPE1,0             INITIALISE TIME FIELD TYPE FLAGS             
         MVI   TYPE2,0                                                          
*                                                                               
TENTL1   CLI   0(R5),C' '          READ UPTO TIME 1 FIRST DIGIT                 
         BE    TENTL1A             IGNORE BLANK OR DASH                         
         CLI   0(R5),C'-'                                                       
         BE    TENTL1A                                                          
         CLI   0(R5),C'0'                                                       
         BL    TENTL1B                                                          
         CLI   0(R5),C'9'                                                       
         BNH   TENTL1X                                                          
*                                                                               
TENTL1B  BAS   R9,TXIN             VALIDATE NON DIGIT TEXT BLOCK                
         LTR   R3,R3               TEST EOI                                     
         BE    ERRORJ2                                                          
         B     TENTL1                                                           
TENTL1A  LA    R5,1(R5)            MOVE EXTERNAL POINTER                        
         BCT   R3,TENTL1           TEST EOI AND CONTINUE                        
         B     ERRORJ2                                                          
*                                                                               
TENTL1X  BAS   R9,CNVD             CALL SUBROUTINE TO CONVERT TIME 1            
         MVC   TIM1(3),DUB+5                                                    
         MVC   TYPE1(1),TYPE                                                    
ACTRET1  LTR   R3,R3               TEST EOI                                     
         BZ    TENTX1              TEST FOR EXIT WITH ONLY 1 TIME               
         CLI   0(R5),C' '          UNLESS BLANK OR DASH                         
         BE    TENTL2              VALIDATE FOLLOWING TEXT BLOCK                
         CLI   0(R5),C'-'                                                       
         BE    TENTL2                                                           
         BAS   R9,TXIN                                                          
         LTR   R3,R3                                                            
         BE    TENTX1                                                           
*                                                                               
TENTL2   CLI   0(R5),C' '          READ UPTO TIME 2 FIRST DIGIT                 
         BE    TENTL2A             IGNORE BLANK OR DASH                         
         CLI   0(R5),C'-'                                                       
         BE    TENTL2A                                                          
         CLI   0(R5),C'0'                                                       
         BL    TENTL2B                                                          
         CLI   0(R5),C'9'                                                       
         BNH   TENTL2X                                                          
TENTL2B  BAS   R9,TXIN             VALIDATE NON DIGIT TEXT BLOCK                
         LTR   R3,R3                                                            
         BE    ERRORJ2                                                          
         B     TENTL2                                                           
*                                                                               
TENTL2A  LA    R5,1(R5)            MOVE EXTERNAL POINTER                        
         BCT   R3,TENTL2           TEST EOI                                     
*                                                                               
TENTX1   TM    DDCNTRL,X'40'+X'10' TEST IF CAN HAVE ONLY ONE TIME               
         BZ    ERRORJ5             NO-ERROR EXIT                                
         CLI   TYPE1,C'P'                                                       
         BE    *+12                                                             
         CLI   TYPE1,C'N'                                                       
         BNE   *+10                                                             
         AP    TIM1,=P'1200'                                                    
         MVC   DUB+5(3),TIM1                                                    
         BAS   R9,CNVI                                                          
         MVC   2(2,R4),0(R4)       IF NOT SET TIME2=TIME1 INTERNAL              
         B     EXIT                RETURN TO MAIN CONTROL ROUTINE               
*                                                                               
TENTL2X  TM    DDCNTRL,X'40'       TEST IF ONLY ONE TIME REQUESTED              
         BO    ERRORJ6             YES-ERROR EXIT                               
*                                                                               
TENTD    BAS   R9,CNVD             CALL SUBROUTINE TO CONVERT                   
         MVC   TIM2(3),DUB+5                                                    
         MVC   TYPE2(1),TYPE                                                    
*                                                                               
ACTRET2  LTR   R3,R3               TEST EOI                                     
         BE    CMPT                                                             
         CLI   0(R5),C' '          UNLESS BLANK OR DASH                         
         BE    TENTL3              VALIDATE TRAILING TEXT BLOCK                 
         CLI   0(R5),C'-'                                                       
         BE    TENTL3                                                           
         BAS   R9,TXIN                                                          
         LTR   R3,R3               TEST EOI                                     
         BE    CMPT                                                             
*                                  VALIDATE REMAINDER OF STRING                 
TENTL3   CLI   0(R5),C' '          IGNORE BLANKS                                
         BNE   ERRORJ3                                                          
         LA    R5,1(R5)            MOVE EXTERNAL POINTER                        
         BCT   R3,TENTL3                                                        
         B     CMPT                                                             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO ENSURE VALIDATION OF 'USER FRIENDLY' TIME INPUTS      *         
***********************************************************************         
CMPT     CLI   TYPE1,C'N'                                                       
         BNE   *+8                                                              
         MVI   TYPE1,C'P'          CHANGE 12N TO 12P                            
         CLI   TYPE2,C'N'                                                       
         BNE   *+8                                                              
         MVI   TYPE2,C'P'                                                       
*                                                                               
         CLC   TIM1(3),TIM2        TIMES EQUAL                                  
         BNE   *+34                                                             
         CLC   TIM1(3),=X'1259F'   NOT OVER 1300                                
         BH    ERROR3                                                           
         CLC   TIM1(3),=X'1200F'   NOT OVER 1200                                
         BL    CMPT4                                                            
         SP    TIM1(3),=P'1200'                                                 
         B     CMPT5                                                            
         CLC   TIM1(3),TIM2        TIME 1 LESS THAN TIME 2                      
         BH    CMPT1                                                            
         CLC   TIM1(3),=X'1200F'                                                
         BL    CMPT3                                                            
         CLC   TIM1(3),=X'1259F'                                                
         BH    CMPT3                                                            
         B     CMPT5               BETWEEN 12 AND 1                             
CMPT1    CLC   TIM1(3),=X'1200F'   TIME 1 GREATER THAN TIME 2                   
         BL    CMPT4                                                            
         CLC   TIM1(3),=X'1259F'                                                
         BH    ERROR3                                                           
         B     CMPT6                                                            
*                                                                               
CMPT3    LA    R6,LAB1                                                          
CMPT3A   CLI   0(R6),C'X'          END OF TABLE                                 
         BE    CMPTX                                                            
         CLC   TYPE1(2),0(R6)                                                   
         BE    CMPT3B                                                           
         LA    R6,8(R6)                                                         
         B     CMPT3A                                                           
*                                                                               
CMPT3B   AP    TIM1(3),2(3,R6)     ADJUST TIME 1                                
         AP    TIM2(3),5(3,R6)     ADJUST TIME 2                                
         B     CMPTX                                                            
*                                                                               
CMPT4    LA    R6,LAB2                                                          
         B     CMPT3A                                                           
*                                                                               
CMPT5    LA    R6,LAB3                                                          
         B     CMPT3A                                                           
*                                                                               
CMPT6    LA    R6,LAB4                                                          
         B     CMPT3A                                                           
*                                                                               
CMPTX    MVC   DUB+5(3),TIM1                                                    
         BAS   R9,CNVI                                                          
         LA    R4,2(R4)                                                         
         MVC   DUB+5(3),TIM2                                                    
         BAS   R9,CNVI                                                          
         BCTR  R4,0                                                             
         BCTR  R4,0                                                             
         CLC   0(2,R4),2(R4)                                                    
         BH    ERROR3                                                           
         TM    DDCNTRL,X'20'       TEST FOR DIFFERENCE REQUEST                  
         BNO   EXIT                                                             
         BAS   R9,TDIFF            CALL TIME DIFFERENCE ROUTINE                 
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CONVERT AND OUTPUT TIMES                                            *         
***********************************************************************         
TOUT     SR    R0,R0                                                            
         ICM   R0,1,DDEFORM        LOAD EXTERNAL FORMAT VALUE                   
         BNZ   TOUT1               IF DEFAULT THEN -                            
         L     RE,AOUT             GET ADR LANGUAGE TABLE                       
         IC    R0,0(RE)            GET LANG. DEFAULT RECORD #                   
*                                                                               
TOUT1    LA    R2,EXTFORM          SET UP POINTER TO FORMAT TABLE               
         CH    R0,EFNUM            CHECK TABLE OFFSET IN RANGE                  
         BH    ERRORJ1                                                          
         BCTR  R0,0                DECREMENT TO FIGURE OFFSET                   
         MH    R0,EFRECLEN         RECORD LENGTH IN EFRECLEN                    
         AR    R2,R0                                                            
*                                                                               
         LA    R1,4                CHECK EXTERNAL LENGTH BIG ENOUGH             
         CLI   HMSEP(R2),C'0'                                                   
         BE    *+8                                                              
         LA    R1,1(R1)            ADD 1 FOR HHMM SEPARATOR                     
         TM    DDCNTRL,X'40'       IF 2 TIMES DOUBLE LENGTH                     
         BNE   *+12                                                             
         SLL   R1,1                                                             
         LA    R1,L'TSEP(R1)       ADD LENGTH OF INBETWEEN TEXT                 
         CLI   HRFLAG(R2),C'0'     TEST FOR 24 HOUR MODE                        
         BE    *+20                                                             
         CLI   HRFLAG(R2),C'1'     TEST FOR 12 HOUR SHORT MODE                  
         BE    *+8                                                              
         LA    R1,2(R1)            ADD LENGTH OF AM PM TEXT                     
         LA    R1,2(R1)                                                         
         CR    R1,R3                                                            
         BH    ERRORJ2                                                          
*                                                                               
         BAS   R9,CNVE             CALL OUTPUT ROUTINE FOR TIME1                
         CLI   HRFLAG(R2),C'0'                                                  
         BE    TOUT6               24 HOUR MODE                                 
         CLI   HRFLAG(R2),C'1'                                                  
         BE    *+18                                                             
         MVC   0(2,R5),AMPM        12 HOUR LONG MODE                            
         LA    R5,2(R5)                                                         
         B     TOUT6                                                            
         MVC   0(1,R5),AMPM        12 HOUR SHORT MODE                           
         LR    R6,R5               SAVE ADR TIME 1 TYPE                         
         LA    R5,1(R5)                                                         
*                                                                               
TOUT6    TM    DDCNTRL,X'40'       IF 1 TIME ONLY                               
         BZ    TOUT7               EXIT                                         
         B     EXIT                                                             
*                                                                               
TOUT7    MVC   0(L'TSEP,R5),TSEP   OUTPUT INBETWEEN TEXT                        
         LA    R5,L'TSEP(R5)       MOVE EXTERNAL POINTER ALONG                  
         LA    R4,2(R4)            POINT TO TIME 2 INTERNAL                     
         BAS   R9,CNVE             CALL OUTPUT ROUTINE FOR TIME2                
         BCTR  R4,0                RETURN TIME FIELD POINTER TO 1               
         BCTR  R4,0                                                             
         CLI   HRFLAG(R2),C'0'                                                  
         BE    TOUT9               24 HOUR MODE                                 
         CLI   HRFLAG(R2),C'1'                                                  
         BE    TOUT8               12 HOUR SHORT MODE                           
         MVC   0(2,R5),AMPM                                                     
         B     TOUT9                                                            
TOUT8    MVC   0(1,R5),AMPM                                                     
         CLC   0(1,R5),0(R6)       TEST TIME TYPES =                            
         BNE   TOUT9                                                            
         SR    R5,R6               REMOVE TIME 1 TYPE                           
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),1(R6)                                                    
         AR    R5,R6                                                            
         MVI   1(R5),C' '                                                       
*                                                                               
TOUT9    TM    DDCNTRL,X'20'       IF DIFFERENCE REQUEST                        
         BNO   EXIT                                                             
         CLC   0(2,R4),2(R4)       TEST TIME2 >= TIME1                          
         BH    ERRORJ3                                                          
         BAS   R9,TDIFF            CALCULATE TIME DIFFERENCE                    
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE VALIDATE BLOCK OF TEXT FROM EXTERNAL STRING              *         
* R2 ADDRESS OF TEXT BLOCK LOOKUP TABLE                               *         
***********************************************************************         
TXIN     L     R2,AIN              GET ENTRY ADR                                
         LLC   RF,0(R2)            SAVE ENTRY LENGTH                            
         LA    R2,1(R2)            POINT TO FIRST ENTRY                         
*                                                                               
TXIN1    CLI   0(R2),X'FF'         TEST END OF ENTRIES                          
         BE    TXIN2                                                            
         SR    R1,R1                                                            
         LR    RE,R1                                                            
         IC    R1,0(R2)            GET LONGEST LENGTH                           
         IC    RE,1(R2)            GET SHORTEST LENGTH                          
         CR    RE,R3               CHECK WITHIN INPUT LENGTH                    
         BNH   *+10                                                             
         AR    R2,RF               IF NOT GET NEXT ENTRY                        
         B     TXIN1                                                            
         CR    R1,R3                                                            
         BNH   *+6                                                              
         LR    R1,R3                                                            
         BCTR  R1,0                SET UP FOR EXECUTE                           
         BCTR  RE,0                                                             
*                                                                               
TXIN3    EX    R1,TABCOMP          COMPARE TEXT ENTRY                           
         BE    TXIN4                                                            
         BCTR  R1,0                DECREMENT COMPARE LENGTH                     
         CR    R1,RE               TEST MINIMUM                                 
         BNL   TXIN3                                                            
         AR    R2,RF               POINT TO NEXT ENTRY                          
         B     TXIN1                                                            
*                                                                               
TXIN2    B     ERRORJ3             GOTO ERROR ROUTINE                           
*                                                                               
TXIN4    LA    R1,1(R1)                                                         
         SR    R3,R1               UPDATE INPUT COUNT                           
         LR    RF,R5               SAVE INITIAL ADRESS INPUT TEXT               
         LA    R5,0(R1,R5)         UPDATE INPUT TEXT POINTER                    
         ICM   RE,15,2(R2)         LOAD ADDRESS OF ACTION.                      
         A     RE,RELO             ADD RELOCATION FACTOR.                       
         BR    RE                  GO DO IT.                                    
*                                                                               
TABCOMP  CLC   0(0,R5),6(R2)       EXECUTE INSTRUCTION                          
         EJECT                                                                  
***********************************************************************         
* ACTION ROUTINES TO HANDLE TEXT INPUT IN EXTERNAL STRING             *         
***********************************************************************         
ACTPRE   CLI   TYPE1,0             TEST PRECEDING TEXT                          
         BNE   ERRORJ3             OK IF NO TIME REACHED                        
         MVI   TYPE1,1             FLAG TEXT READ                               
         BR    R9                                                               
*                                                                               
ACTFOL   CLI   TYPE1,1             TEST FOLLOWING TEXT                          
         BNH   ERRORJ3             CRAP IF NO FIRST TIME                        
         CLI   TYPE2,1                                                          
         BE    ERRORJ3             CRAP IF PRECEDING TEXT                       
         BR    R9                                                               
*                                                                               
ACTSEP   CLI   TYPE1,1             TEST TIME SEPERATOR TEXT                     
         BNH   ERRORJ3             OK IF BETWEEN TIMES                          
         CLI   TYPE2,0                                                          
         BNE   ERRORJ3                                                          
         TM    DDCNTRL,X'40'       OK IF 2 TIMES REQUESTED                      
         BNZ   ERRORJ5                                                          
         MVI   TYPE2,1                                                          
         BR    R9                                                               
*                                                                               
ACTNON   CLI   TYPE1,1             TEST NOON SPECIAL WORD                       
         BNH   ACTNON1             FIRST TIME                                   
         CLI   TYPE2,1                                                          
         BLR   R9                  IGNORE IF FOLLOWS TIME                       
         BHR   R9                                                               
         TM    DDCNTRL,X'40'       OK IF 2 TIMES REQUESTED                      
         BNZ   ERRORJ6                                                          
         MVC   TIM2(3),=P'0000'    SET NOON TIME 2                              
         MVI   TYPE2,C'N'          SET 24HR TYPE                                
         B     ACTRET2                                                          
ACTNON1  MVC   TIM1(3),=P'0000'    SET NOON TIME 1                              
         MVI   TYPE1,C'N'          SET 24HR TYPE                                
         B     ACTRET1                                                          
*                                                                               
ACTMID   CLI   TYPE1,1             TEST MIDNIGHT SPECIAL WORD                   
         BNH   ACTMID1             FIRST TIME                                   
         CLI   TYPE2,1                                                          
         BLR   R9                  IGNORE IF FOLLOWS TIME                       
         BHR   R9                                                               
         TM    DDCNTRL,X'40'       OK IF 2 TIMES REQUESTED                      
         BNZ   ERRORJ6                                                          
         MVC   TIM2(3),=P'0000'    SET NOON TIME 2                              
         MVI   TYPE2,C'M'          SET 24HR TYPE                                
         B     ACTRET2                                                          
ACTMID1  MVC   TIM1(3),=P'0000'    SET NOON TIME 1                              
         MVI   TYPE1,C'M'          SET 24HR TYPE                                
         B     ACTRET1                                                          
*                                                                               
ACTNOW   CLI   TYPE2,1             TEST NOW SPECIAL WORD                        
         BH    ERRORJ3             INVALID IF BOTH TIMES IN                     
         XC    DUB,DUB                                                          
         TIME                                                                   
         SRL   R0,12               GET TIME IN R0 HHMM PWOS                     
         ST    R0,DUB+4                                                         
         OI    DUB+7,X'0F'                                                      
         LTR   R3,R3                                                            
         BE    ACTNOW2                                                          
         CLI   0(R5),C'+'          TEST FOR TIMEOFFSET                          
         BNE   ACTNOW2                                                          
         CVB   RF,DUB                                                           
         LA    R5,1(R5)                                                         
         BCT   R3,*+8                                                           
         B     ERRORJ3                                                          
         CLI   0(R5),C'0'          GET DIGITS                                   
         BL    ERRORJ3                                                          
         CLI   0(R5),C'9'                                                       
         BH    ERRORJ3                                                          
*                                                                               
         XC    WORKTIM,WORKTIM                                                  
         MVC   WORKTIM+7(1),0(R5)                                               
         LA    R5,1(R5)                                                         
         BCT   R3,*+8                                                           
         B     ACTNOW3                                                          
         CLI   0(R5),C'0'                                                       
         BL    ACTNOW3                                                          
         CLI   0(R5),C'9'                                                       
         BH    ACTNOW3                                                          
         MVC   WORKTIM+6(1),WORKTIM+7                                           
         MVC   WORKTIM+7(1),0(R5)                                               
         LA    R5,1(R5)                                                         
         BCTR  R3,0                                                             
ACTNOW3  PACK  DUB,WORKTIM                                                      
         SR    RE,RE                                                            
         LR    R0,RE                                                            
         CVB   R1,DUB                                                           
         D     RE,=F'100'                                                       
         AR    R1,RE                                                            
         MHI   RF,60                                                            
         AR    R1,RF                                                            
         D     R0,=F'60'                                                        
         MHI   R1,100                                                           
         AR    R1,R0                                                            
         CVD   R1,DUB                                                           
ACTNOW2  CLI   TYPE1,1             TIME 1 OR 2 ?                                
         BNH   ACTNOW1                                                          
         TM    DDCNTRL,X'40'       OK IF 2 TIMES REQUESTED                      
         BNZ   ERRORJ6                                                          
         MVC   TIM2(3),DUB+5                                                    
         MVI   TYPE2,C' '          SET 24HR TYPE                                
         B     ACTRET2                                                          
ACTNOW1  MVC   TIM1(3),DUB+5                                                    
         MVI   TYPE1,C' '          SET 24HR TYPE                                
         B     ACTRET1                                                          
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO VALIDATE TIME INPUT AND TURN INTO PWOS 2 BYTE FIELD   *         
* R5 POINTS TO FIRST DIGIT OF TIME                                    *         
* R4 POINTS TO WHERE TO STORE CONVERTED TIME (2BYTE FIELD)            *         
***********************************************************************         
CNVD     LA    R1,TIMSTORE                                                      
         LR    R0,R1               STORE ADDR OF TIMSTORE                       
         MVC   TIMSTORE(4),=C'0000'                                             
         BCTR  R5,0                                                             
*                                                                               
CNVDL    LA    R5,1(R5)                                                         
         CLI   0(R5),C'0'                                                       
         BL    CNVD1                                                            
         CLI   0(R5),C'9'                                                       
         BH    ERROR1              WE NOW HAVE A DIGIT                          
         MVC   0(1,R1),0(R5)       MOVE DIGIT TO STORE                          
         LA    R1,1(R1)                                                         
         BCT   R3,CNVDL                                                         
         MVI   TYPE,C' '                                                        
         B     CNVD2B              END OF INPUT STRING                          
*                                                                               
CNVD1    LA    RE,SYMST                                                         
CNVD1A   CLC   0(1,R5),0(RE)       CHECK FOR COLON ETC                          
         BE    CNVD1B                                                           
         LA    RE,1(RE)                                                         
         CLI   0(RE),C'X'                                                       
         BE    CNVD2               END OF TIME STRING                           
         B     CNVD1A                                                           
*                                                                               
CNVD1B   CLI   2(R5),C'0'          2 DIGITS AFTER COLON                         
         BL    ERROR3                                                           
         CLI   3(R5),C'Z'                                                       
         BH    ERROR3                                                           
         ICM    R8,7,DDEADR                                                     
         BCTR   R5,0               CHAR BEFORE COLON                            
         CR     R5,R8                                                           
         BL     ERROR3             COLON FIRST CHAR OF INPUT                    
         CLI    0(R5),C'0'                                                      
         BL     ERROR3                                                          
         BCTR   R5,0                                                            
         CR     R5,R8                                                           
         BL     CNVD1D                                                          
         CLI    0(R5),C'0'                                                      
         BL     CNVD1D                                                          
         BCTR   R5,0                                                            
         CR     R5,R8                                                           
         BL     CNVD1C                                                          
         CLI    0(R5),C'0'                                                      
         BL     CNVD1C                                                          
         B      ERROR3             3 NUMS BEFORE COLON                          
CNVD1C   LA    R5,1(R5)                                                         
CNVD1D   LA    R5,2(R5)                                                         
         BCT   R3,CNVDL                                                         
         B     ERROR2              END OF INPUT STR                             
*                                                                               
CNVD2    CLI   0(R5),C' '          ALL TIME DIGITS IN TIMSTORE                  
         BNE   CNVD2D                                                           
         LA    R5,1(R5)                                                         
         BCT   R3,CNVD2                                                         
         MVI   TYPE,C' '                                                        
         B     CNVD2B                                                           
CNVD2D   LA    RE,SYMST2                                                        
CNVD2A   CLC   0(1,R5),0(RE)       CHECK NOON MNIGHT ETC                        
         BE    CNVD2C                                                           
         LA    RE,1(RE)                                                         
         CLI   0(RE),C'X'                                                       
         BNE   CNVD2A              INVALID CHR AFTER TIME                       
         MVI   TYPE,C' '                                                        
         B     CNVD2B                                                           
CNVD2C   MVC   TYPE(1),0(R5)                                                    
*                                                                               
CNVD2B   SR    R1,R0               LENGTH OF TIMSTORE                           
         CHI   R1,2                                                             
         BH    *+8                                                              
         LA    R1,2(R1)            ADD '00'HOURS                                
         CHI   R1,4                TOO MANY DIGITS IN TIME                      
         BH    ERROR5                                                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,TIMSTORE(0)                                                  
*                                                                               
         CLI   TYPE,C'N'           NOON                                         
         BNE   *+20                                                             
         CLC   DUB+5(3),=X'01200F' CHECK 12NOON                                 
         BNE   ERROR3                                                           
         SP    DUB+5(3),=P'1200'                                                
         CLI   TYPE,C'M'           MIDNIGHT                                     
         BNE   *+20                                                             
         CLC   DUB+5(3),=X'1200F'  CHECK 12MIDNIGHT                             
         BNE   ERROR3                                                           
         SP    DUB+5(3),=P'1200'                                                
         CLI   TYPE,C'P'           SEE IF P.M                                   
         BNE   *+40                                                             
         CLC   DUB+5(3),=P'0100'   TOO SMALL                                    
         BL    ERROR3                                                           
         CLC   DUB+5(3),=P'1200'                                                
         BL    *+20                                                             
         CLC   DUB+5(3),=P'1300'                                                
         BNL   ERROR3                                                           
         SP    DUB,=P'1200'                                                     
         CLI   TYPE,C'A'           SEE IF A.M                                   
         BNE   CNVDX                                                            
         CLC   DUB+5(3),=P'1200'                                                
         BL    CNVDX                                                            
         CLC   DUB+5(3),=P'1300'                                                
         BH    ERROR3              TIME INVALID                                 
         SP    DUB,=P'1200'                                                     
*                                                                               
CNVDX    BR    R9                                                               
                                                                                
***********************************************************************         
* SUBROUTINE TO CONVERT PACKED TIME PASSED IN DUB INTO THE REQUIRED   *         
* INTERNAL FORMAT                                                     *         
***********************************************************************         
CNVI     CLC   DUB+5(3),=P'2400'   TOO MANY HOURS                               
         BH    ERROR3                                                           
         MVI   MINTEST,0                                                        
         MVN   MINTEST(1),DUB+6                                                 
         CLI   MINTEST,X'06'       TOO MANY MINUTES                             
         BNL   ERROR3                                                           
         L     R1,DUB+4                                                         
         CLI   DDIFORM,1           PWOS                                         
         BE    CNVI3A                                                           
         CLI   DDIFORM,0           BINARY                                       
         BE    CNVI3B                                                           
         CLI   DDIFORM,2           BIN MIN                                      
         BE    CNVI3C                                                           
         CLI   DDIFORM,3           HEX HHMM                                     
         BE    CNVI3D                                                           
         B     CNVI3B                                                           
*                                                                               
CNVI3A   SRL   R1,4                CONVERT TO PWOS                              
         B     CNVIX                                                            
*                                                                               
CNVI3B   MVC   WORKTIM,=D'0'       CONVERT TO BINARY                            
         ST    R1,WORKTIM+4                                                     
         CVB   R1,WORKTIM                                                       
         B     CNVIX                                                            
*                                                                               
CNVI3C   SR    R0,R0               CONVERT TO BIN MINS                          
         MVC   WORKTIM,=D'0'                                                    
         ST    R1,WORKTIM+4                                                     
         CVB   R1,WORKTIM                                                       
         LA    R2,100                                                           
         DR    R0,R2                                                            
         MHI   R1,60                                                            
         AR    R1,R0                                                            
         B     CNVIX                                                            
*                                                                               
CNVI3D   MVC   WORKTIM,=D'0'       CONVERT TO HEX HHMM                          
         ST    R1,WORKTIM+4                                                     
         NC    WORKTIM+5(3),=X'000FFF'                                          
         CVB   R0,WORKTIM                                                       
         STC   R0,1(R4)            R0 HAS MINUTES                               
         MVC   WORKTIM,=D'0'                                                    
         SRL   R1,12               SHIFT OUT MINUTES                            
         SLL   R1,4                                                             
         ST    R1,WORKTIM+4                                                     
         OI    WORKTIM+7,X'0F'                                                  
         CVB   R1,WORKTIM                                                       
         STC   R1,0(R4)            R1 HAS HOURS                                 
         ICM   R1,3,0(R4)                                                       
*                                                                               
CNVIX    STH   R1,0(R4)            STORE WHERE NEEDED                           
         BR    R9                  BACK TO MAIN PROGRAM                         
*                                                                               
*ERROR CODES FOR CNVR SUBROUTINE                                                
*                                                                               
ERROR1   MVI   ERRVAL,C'1'         CRAP HEX CODE INPUT                          
         B     ERROR                                                            
ERROR2   MVI   ERRVAL,C'2'         END OF INPUT STR                             
         B     ERROR                                                            
ERROR3   MVI   ERRVAL,C'3'         INCORRECT COLON/TIME                         
         B     ERROR                                                            
ERROR4   MVI   ERRVAL,C'4'         INVALID CHR AFTER TIME                       
         B     ERROR                                                            
ERROR5   MVI   ERRVAL,C'5'         TOO MANY DIGITS                              
         B     ERROR                                                            
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO RETURN TIME ELAPSED IN MINUTES                        *         
* USES TWO TIMES TIM1 TIM2 IN PWOS,BINARY JOINED OR BIN MIN FORMAT    *         
* PUTS RESULT BACK IN DDTDIFF & RETURNS TO MAIN PROGRAM               *         
***********************************************************************         
TDIFF    CLI   DDIFORM,0           BINARY                                       
         BE    TDIFF0                                                           
         CLI   DDIFORM,1           PWOS                                         
         BE    TDIFF1                                                           
         CLI   DDIFORM,2           BIN MIN                                      
         BE    TDIFF2                                                           
         CLI   DDIFORM,3           HEX HHMM                                     
         BE    TDIFF3                                                           
*                                                                               
TDIFF0   LH    R1,2(R4)            BINARY CALC                                  
         LH    RF,0(R4)                                                         
TDIFF0A  LA    R2,100                                                           
         SR    R0,R0                                                            
         SR    RE,RE                                                            
         DR    R0,R2                                                            
         MHI   R1,60                                                            
         AR    R1,R0                                                            
         DR    RE,R2                                                            
         MHI   RF,60                                                            
         AR    RF,RE                                                            
         B     TDIFF4                                                           
*                                                                               
TDIFF1   SR    RF,RF               PWOS                                         
         SR    R0,R0                                                            
         LH    R1,0(R4)                                                         
TDIFF1A  XC    WORKTIM(8),WORKTIM                                               
         SLL   R1,4                                                             
         AH    R1,=X'000F'         TURN PWOS TO PACKED                          
         ST    R1,WORKTIM+4                                                     
         CVB   R1,WORKTIM          TIME NOW IN BINARY                           
         CR    RF,R0                                                            
         BNE   TDIFF0A             BOTH IN BINARY                               
         LR    RF,R1                                                            
         LH    R1,2(R4)                                                         
         B     TDIFF1A                                                          
*                                                                               
TDIFF2   LH    R1,2(R4)            TIM1/2 IN BINARY MINUTES                     
         LH    RF,0(R4)                                                         
         B     TDIFF4                                                           
*                                                                               
TDIFF3   LLC   R0,2(R4)            TIM1/2 IN X'HHMM'                            
         MHI   R0,60                                                            
         LLC   R1,3(R4)                                                         
         AR    R1,R0                                                            
         LLC   RE,0(R4)                                                         
         MHI   RE,60                                                            
         LLC   RF,1(R4)                                                         
         AR    RF,RE                                                            
*                                                                               
TDIFF4   SR    R1,RF                                                            
         STCM  R1,7,DDTDIFF        SEND BACK IN PLIST                           
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO CONVERT TIME TO EXTERNAL FORMAT                       *         
***********************************************************************         
CNVE     LH    R1,0(R4)            LOAD TIME VALUE                              
         XC    WORKTIM(8),WORKTIM  CLEAR TIME BUFFER TO 0                       
         CLI   DDIFORM,0           CONVERT ALL FORMATS TO MILITARY              
         BE    CNVE2               UNLESS ALREADY                               
*                                                                               
         CLI   DDIFORM,1           TEST FOR PWOS FORMAT                         
         BNE   CNVE1                                                            
*                                                                               
         SLL   R1,4                                                             
         ST    R1,WORKTIM+4                                                     
         OI    WORKTIM+7,X'0F'                                                  
         CVB   R1,WORKTIM                                                       
         B     CNVE2                                                            
*                                                                               
CNVE1    SR    R0,R0               BINARY MINUTES FORMAT                        
         D     R0,=F'60'                                                        
         MHI   R1,100                                                           
         AR    R1,R0                                                            
*                                                                               
CNVE2    CLI   HRFLAG(R2),C'0'     24 HOUR MODE                                 
         BE    CNVE2A                                                           
         MVC   AMPM,=C'AM'         UPDATE FOR 24HR FORMAT                       
         CHI   R1,1200                                                          
         BL    CNVE2A                                                           
         MVC   AMPM,=C'PM'                                                      
         CHI   R1,1300                                                          
         BL    CNVE2A                                                           
         AHI   R1,-1200            ADJUST PM VALUES                             
*                                                                               
CNVE2A   CVD   R1,DUB              CONVERT TIME INTO CHARACTER BUFFER           
         UNPK  WORKTIM,DUB                                                      
         OI    WORKTIM+L'WORKTIM-1,C'0'                                         
         CLI   HRFLAG(R2),C'1'     12 HOUR SHORT MODE                           
         BNE   CNVE2B                                                           
         CLI   WORKTIM+4,C'0'      DROP LEADING ZERO HOUR < 10                  
         BNE   CNVE2B                                                           
         MVC   0(1,R5),WORKTIM+5   MOVE H TO EXTERNAL FIELD                     
         LA    R5,1(R5)                                                         
         B     *+14                                                             
CNVE2B   MVC   0(2,R5),WORKTIM+4   MOVE HH TO EXTERNAL FIELD                    
         LA    R5,2(R5)            UPDATE EXTERNAL FIELD POINTER                
         CLI   HMSEP(R2),C'0'      TEST FOR HH MM SEPERATOR                     
         BE    CNVE3B                                                           
         MVC   0(1,R5),HMSEP(R2)                                                
         LA    R5,1(R5)                                                         
CNVE3B   MVC   0(2,R5),WORKTIM+6   MOVE MM TO EXTERNAL FIELD                    
         LA    R5,2(R5)                                                         
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* DATA SECTION                                                        *         
***********************************************************************         
DEFCTRY  DS    0XL2                                                             
*&&UK*&& DC    AL1(1,1)                                                         
*&&US*&& DC    AL1(2,2)                                                         
*                                                                               
         LTORG                                                                  
*                                                                               
ATABADS  DC    A(ENGIN,ENGOUT)     0                                            
         DC    A(ENGIN,GBROUT)     1                                            
         DC    A(ENGIN,USAOUT)     2                                            
         DC    A(GERIN,GEROUT)     3                                            
         DC    A(FREIN,FREOUT)     4                                            
         DC    A(SPAIN,SPAOUT)     5                                            
         DC    A(ITAIN,ITAOUT)     6                                            
         DC    A(DUTIN,DUTOUT)     7                                            
         EJECT                                                                  
**********************************************************************          
* THE FORMAT OF THE FOLLOWING TABLES IS:                             *          
* AL1    MAXIMUM LENGTH FOR DATA                                     *          
* AL1    MINIMUM LENGTH FOR DATA (OPTIONAL)                          *          
* CLN    STRING FOR COMPARISON                                       *          
* AL1    CODE OF ACTION ROUTINE                                      *          
* AL1    ENTRY LENGTH                                                *          
**********************************************************************          
                                                                                
* TABLES OF TEXT THAT PRECEDES TIME FIELDS                                      
*                                                                               
ENGIN    DC    AL1(14)                                                          
         DC    AL1(3),AL1(3),AL4(ACTNOW),CL8'NOW'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'FROM'                              
         DC    AL1(4),AL1(2),AL4(ACTFOL),CL8'A.M.'                              
         DC    AL1(4),AL1(2),AL4(ACTFOL),CL8'P.M.'                              
         DC    AL1(2),AL1(1),AL4(ACTFOL),CL8'AM'                                
         DC    AL1(2),AL1(1),AL4(ACTFOL),CL8'PM'                                
         DC    AL1(4),AL1(1),AL4(ACTNON),CL8'NOON'                              
         DC    AL1(8),AL1(1),AL4(ACTMID),CL8'MIDNIGHT'                          
         DC    AL1(2),AL1(2),AL4(ACTSEP),CL8'TO'                                
         DC    AL1(4),AL1(4),AL4(ACTSEP),CL8'THRU'                              
         DC    AL1(5),AL1(5),AL4(ACTSEP),CL8'UNTIL'                             
         DC    X'FF'                                                            
*                                                                               
GERIN    DC    AL1(14)                                                          
         DC    AL1(2),AL1(2),AL4(ACTNOW),CL8'NU'                                
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'VON'                               
         DC    AL1(6),AL1(6),AL4(ACTNON),CL8'MITTAG'                            
         DC    AL1(6),AL1(6),AL4(ACTMID),CL8'MITTER'                            
         DC    AL1(3),AL1(3),AL4(ACTSEP),CL8'BIS'                               
         DC    X'FF'                                                            
*                                                                               
FREIN    DC    AL1(14)                                                          
         DC    AL1(3),AL1(3),AL4(ACTNOW),CL8'NOW'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(1),AL1(1),AL4(ACTPRE),CL8'A'                                 
         DC    AL1(4),AL1(3),AL4(ACTNON),CL8'MIDI'                              
         DC    AL1(6),AL1(3),AL4(ACTMID),CL8'MINUIT'                            
         DC    AL1(1),AL1(1),AL4(ACTSEP),CL8'@'                                 
         DC    AL1(2),AL1(2),AL4(ACTSEP),CL8'EN'                                
         DC    AL1(7),AL1(7),AL4(ACTSEP),CL8'JUSQU''A'                          
         DC    X'FF'                                                            
*                                                                               
SPAIN    DC    AL1(14)                                                          
         DC    AL1(3),AL1(3),AL4(ACTNOW),CL8'NOW'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'FROM'                              
         DC    AL1(4),AL1(1),AL4(ACTNON),CL8'NOON'                              
         DC    AL1(8),AL1(1),AL4(ACTMID),CL8'MIDNIGHT'                          
         DC    AL1(5),AL1(5),AL4(ACTSEP),CL8'HASTA'                             
         DC    AL1(1),AL1(1),AL4(ACTSEP),CL8'A'                                 
         DC    X'FF'                                                            
*                                                                               
ITAIN    DC    AL1(14)                                                          
         DC    AL1(3),AL1(3),AL4(ACTNOW),CL8'NOW'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'FROM'                              
         DC    AL1(4),AL1(1),AL4(ACTNON),CL8'NOON'                              
         DC    AL1(8),AL1(1),AL4(ACTMID),CL8'MIDNIGHT'                          
         DC    AL1(4),AL1(4),AL4(ACTSEP),CL8'FINO'                              
         DC    AL1(1),AL1(1),AL4(ACTSEP),CL8'A'                                 
         DC    X'FF'                                                            
*                                                                               
DUTIN    DC    AL1(14)                                                          
         DC    AL1(3),AL1(3),AL4(ACTNOW),CL8'NOW'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'POST'                              
         DC    AL1(3),AL1(3),AL4(ACTPRE),CL8'PRE'                               
         DC    AL1(4),AL1(4),AL4(ACTPRE),CL8'VAN'                               
         DC    AL1(3),AL1(3),AL4(ACTFOL),CL8'UUR'                               
         DC    AL1(6),AL1(6),AL4(ACTNON),CL8'MIDDAG'                            
         DC    AL1(6),AL1(6),AL4(ACTMID),CL8'MIDDER'                            
         DC    AL1(3),AL1(3),AL4(ACTSEP),CL8'T/M'                               
         DC    AL1(3),AL1(3),AL4(ACTSEP),CL8'TOT'                               
         DC    X'FF'                                                            
                                                                                
* TABLES SPECIFYING DEFAULT OUTPUT EXTERNAL FORMAT                              
* VALUE POINTS TO RECORD IN EXTFORM                                             
*                                                                               
ENGOUT   DC     AL1(01)                                                         
GBROUT   DC     AL1(01)                                                         
USAOUT   DC     AL1(02)                                                         
GEROUT   DC     AL1(01)                                                         
FREOUT   DC     AL1(01)                                                         
SPAOUT   DC     AL1(01)                                                         
ITAOUT   DC     AL1(01)                                                         
DUTOUT   DC     AL1(01)                                                         
         EJECT                                                                  
***********************************************************************         
* EXTERNAL FORMAT PARAMETER TABLE                                     *         
* SETS 24 HR CLOCK FLAG AND HH MM SEPARATOR                           *         
* HRFLAG VALUES:                                                      *         
*        0 = 24 HOUR CLOCK                                            *         
*        1 = 12 HOUR CLOCK SHORT FORMAT, E.G.  1000-123P              *         
*        2 = 12 HOUR CLOCK LONG FORMAT, E.G.  1000AM-0123PM           *         
* HMSEP VALUES:                                                       *         
*        0 = NO SEPARATOR                                             *         
*        OTHERWISE GIVEN CHARACTER E.G. ':' FOR 10:00AM               *         
***********************************************************************         
EXTFORM  DC     C'00'              01                                           
         DC     C'10'              02                                           
         DC     C'20'              03                                           
         DC     C'0:'              04                                           
         DC     C'1:'              05                                           
         DC     C'2:'              06                                           
         DC     C'0,'              07                                           
         DC     C'1,'              08                                           
         DC     C'2,'              09                                           
         DC     C'0H'              10                                           
         DC     C'1H'              11                                           
         DC     C'2H'              12                                           
         DC     C'0.'              13                                           
         DC     C'1.'              14                                           
         DC     C'2.'              15                                           
*                                                                               
EFRECLEN DC     H'02'                                                           
EFNUM    DC     H'15'                                                           
HRFLAG   EQU    0                                                               
HMSEP    EQU    1                                                               
OFLEN    EQU    2                                                               
TSEP     DC     CL1'-'             CHARACTERS BETWEEN OUTPUT TIMES              
         EJECT                                                                  
***********************************************************************         
* TABLES FOR CMPT ROUTINE                                             *         
***********************************************************************         
LAB1     DC    C'  ',P'0000',P'0000'                                            
         DC    C' P',P'1200',P'1200'                                            
         DC    C' A',P'0000',P'0000'                                            
         DC    C' M',P'0000',P'2400'                                            
         DC    C'A ',P'0000',P'0000'                                            
         DC    C'AP',P'0000',P'1200'                                            
         DC    C'AA',P'0000',P'0000'                                            
         DC    C'P ',P'1200',P'1200'                                            
         DC    C'PA',P'2500',P'2500'                                            
         DC    C'PP',P'1200',P'1200'                                            
         DC    C'MP',P'0000',P'1200'                                            
         DC    C'MA',P'0000',P'0000'                                            
         DC    C'M ',P'0000',P'0000'                                            
         DC    C'X'                                                             
*                                                                               
LAB2     DC    C'  ',P'0000',P'1200'                                            
         DC    C' P',P'0000',P'1200'                                            
         DC    C' A',P'2500',P'2500'                                            
         DC    C' M',P'1200',P'2400'                                            
         DC    C'A ',P'0000',P'1200'                                            
         DC    C'AP',P'0000',P'1200'                                            
         DC    C'AA',P'2500',P'2500'                                            
         DC    C'AM',P'0000',P'2400'                                            
         DC    C'P ',P'2500',P'2500'                                            
         DC    C'PA',P'2500',P'2500'                                            
         DC    C'PP',P'2500',P'2500'                                            
         DC    C'PM',P'1200',P'2400'                                            
         DC    C'MP',P'0000',P'1200'                                            
         DC    C'MA',P'0000',P'0000'                                            
         DC    C'M ',P'0000',P'0000'                                            
         DC    C'MM',P'0000',P'2400'                                            
         DC    C'X'                                                             
*                                                                               
LAB3     DC    C'  ',P'0000',P'0000'                                            
         DC    C'X'                                                             
*                                                                               
LAB4     DC    C'  ',P'0000',P'1200'                                            
         DC    C' P',P'0000',P'1200'                                            
         DC    C' A',P'-1200',P'0000'                                           
         DC    C'X'                                                             
*                                                                               
TIM1     DS    CL3                                                              
TIM2     DS    CL3                                                              
TYPE     DS    C                                                                
TYPE1    DS    C                                                                
TYPE2    DS    C                                                                
*                                                                               
SYMST    DC    CL8':.,HX   '       CHRS TWIXT HRS & MINS                        
SYMST2   DC    CL8'PANMX'          PM,AM,NOON,M'NGHT                            
         EJECT                                                                  
***********************************************************************         
* TIMVAL ENTRY POINT. CONVERT PARAMETER LIST AND CALL TIMBER          *         
*                                                                     *         
* XL1 LENGTH OF INPUT              RETURN X'FF' IF ERROR              *         
* AL3 A(INPUT/EXTERNAL)                                               *         
* XL1 N/D                                                             *         
* AL3 A(OUTPUT/INTERNAL)                                              *         
***********************************************************************         
TIMVAL   NMOD1 TVALWX-TVALWS,*TIMVAL*                                           
         USING TVALWS,RC                                                        
         SAM24                     SET 24-BIT MODE                              
*                                                                               
         ST    R1,TVPARMA          R1=A(TIMVAL PARAM LIST)                      
         LLC   R2,0(R1)            R2=L'INPUT                                   
         LM    R3,R4,0(R1)                                                      
         ICM   R3,8,=X'00'         R3=A(INPUT)                                  
         ICM   R4,8,=X'00'         R4=A(OUTPUT)                                 
*                                                                               
TIMVAL1  CLC   0(4,R3),=C'NONE'    SPECIAL NONE CONDITION                       
         BNE   TIMVAL2                                                          
         MVC   0(4,R4),=C'NONE'                                                 
         MVI   0(R1),0                                                          
         B     TIMVALX                                                          
TIMVAL2  CLC   0(3,R3),=C'VARY'    SPECIAL VARY CONDITION                       
         BNE   TIMVAL4                                                          
         MVC   0(4,R4),=C'VARY'                                                 
         MVI   0(R1),0                                                          
         B     TIMVALX                                                          
*                                                                               
TIMVAL4  ST    R2,TBPARM1          L'INPUT                                      
         MVI   TBPARM1,X'90'       SET VALIDATE FOR ONE OR TWO TIMES            
         ST    R4,TBPARM2          A(OUTPUT)                                    
         ST    R3,TBPARM3          A(INPUT)                                     
         LA    R1,TBPARM1                                                       
         LARL  RF,TIMBER                                                        
         BASR  RE,RF                                                            
*                                                                               
TIMVAL5  L     R1,TVPARMA          COPY RETURN CODE                             
         MVC   0(1,R1),TBPARM1                                                  
         CLI   TBPARM1,0                                                        
         BNE   TIMVAL6                                                          
         CLC   0(2,R4),2(R4)       ONLY ONE TIME IF TIME1=TIME2                 
         BNE   TIMVALX                                                          
         XC    2(2,R4),2(R4)       TIMVAL SETS TIME2 TO ZERO                    
         B     TIMVALX                                                          
*                                                                               
TIMVAL6  MVI   0(R1),X'FF'         SET TIMVAL ERROR RETURN CODE                 
         XC    0(4,R4),0(R4)       SET OUTPUT TIMES TO ZERO                     
*                                                                               
TIMVALX  XMOD1 1                                                                
*                                                                               
         LTORG                                                                  
*                                                                               
TVALWS   DSECT                                                                  
TVPARMA  DS    A                   TIMVAL A(PARAM LIST)                         
TBPARM1  DS    F                   TIMBER L'INPUT                               
TBPARM2  DS    A                   TIMBER A(OUTPUT)                             
TBPARM3  DS    A                   TIMBER A(INPUT)                              
TVALWX   EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
TWORKS   DSECT                                                                  
PARM     DS     0XL12              PARAMETER SAVE AREA                          
*                                                                               
DDCNTRL  DS     X                  IO CONTROL BYTE                              
DDEXLEN  DS     AL3                EXTERNAL FIELD LENGTH                        
         ORG    *-3                                                             
DDTDIFF  DS     AL3                TIME DIFFERENCE RETURN                       
*                                                                               
DDIFORM  DS     X                  INTERNAL FORMAT BYTE                         
DDIADR   DS     AL3                ADDRESS OF INTERNAL TIME FIELD               
*                                                                               
DDEFORM  DS     X                  EXTERNAL FORMAT BYTE                         
DDEADR   DS     AL3                ADDRESS OF EXTERNAL FIELD                    
*                                                                               
RELO     DS     F                  RELOCATION SAVE                              
DUB      DS     D                                                               
APARM    DS     A                  ADDRESS OF PARAMETERS                        
WORKTIM  DS     D                  TIME CONVERSION BUFFER                       
*                                                                               
AIN      DS    A                   A(INPUT TABLE)                               
AOUT     DS    A                   A(OUTPUT TABLE)                              
*                                                                               
CTRY     DS    XL1                 COUNTRY CODE                                 
LANG     DS    XL1                 LANGUAGE CODE 0-7                            
         DS    XL2                                                              
TIMSTORE DS    CL8                 IN CASE TOO MANY DIGITS                      
MINTEST  DS    CL1                 TEST BYTE                                    
ERRVAL   DS    CL1                 ERROR VALUE SAVE                             
AMPM     DS    CL2                 AM PM CHARACTER BUFFER                       
TWORKX   EQU   *                                                                
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDTIMBER  04/14/15'                                      
         END                                                                    
