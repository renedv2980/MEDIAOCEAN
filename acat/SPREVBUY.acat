*          DATA SET SPREVBUY   AT LEVEL 016 AS OF 11/29/84                      
*CATALP REVBUY                                                                  
         TITLE 'ACCUMULATE AND PRINT TODAYS REVISIONS'                          
REVBUY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,REVBUY                                                         
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         CLI   FIRST,0                                                          
         BE    REVBUY01                                                         
         LA    RE,SVCHG                                                         
         LA    RF,1024                                                          
         XCEF                                                                   
         MVI   FIRST,0                                                          
         XC    REVCNTR,REVCNTR                                                  
         SPACE 2                                                                
REVBUY01 CLI   MODE,STALAST                                                     
         BE    PRTREV                                                           
         CLI   MODE,PROCBUY                                                     
         BNE   EXIT                                                             
         L     R2,ADBUY                                                         
         USING BUYREC,R2                                                        
         MVC   LINNO,10(R2)                                                     
         CLC   TODAYB,BDCHG                                                     
         BH    EXIT                                                             
         LA    RE,SVCHG                                                         
REVBUY02 CLI   3(RE),0             OPEN SLOT                                    
         BE    REVBUY03             YES - SAVE LINE NUMBER                      
         CLC   LINNO,3(RE)         DUPLICATE                                    
         BE    EXIT                 YES - BYPASS                                
         LA    RE,4(RE)                                                         
         B     REVBUY02                                                         
         SPACE 2                                                                
REVBUY03 MVC   0(1,RE),BDWHY                                                    
         MVC   1(1,RE),BDWHY2                                                   
         MVC   2(1,RE),BDWHY3                                                   
         MVC   3(1,RE),LINNO                                                    
         B     EXIT                                                             
         SPACE 2                                                                
* PRINT REVISIONS                                                               
PRTREV   DS    0H                                                               
         OC    SVCHG(4),SVCHG                                                   
         BZ    EXIT                                                             
         MVI   P1,0                                                             
         GOTO1 REPORT                                                           
         LA    R9,P1+24                                                         
         LR    R7,R9               SAVE ADDRESS                                 
         LA    R8,21                                                            
         MVI   ACTIVE,0                                                         
         MVC   P1(20),=C'REVISIONS MADE TODAY'                                  
         LA    R6,SVCHG                                                         
         LA    R5,REASON                                                        
PRTREV1  CLI   0(R5),0             GET CAPTION AND BYTES FOR THIS PASS          
         BE    PRTREVX                                                          
         MVC   0(22,R9),2(R5)                                                   
         LA    R9,24(R9)                                                        
         LR    R7,R9                                                            
         MVC   CONTROL,0(R5)                                                    
PRTREV2  ZIC   RF,CONTROL+1                                                     
         ZIC   RE,CONTROL                                                       
         LA    RF,0(R6,RF)                                                      
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    0(RF),0             CHANGE=THIS CAPTION                          
         BO    PRTREV3              YES - PRINT IT                              
         LA    R6,4(R6)                                                         
         CLI   3(R6),0                                                          
         BE    PRTREV4                                                          
         B     PRTREV2                                                          
PRTREV3  ZIC   RE,3(R6)            PRINT LINE NUMBER                            
         MVI   ACTIVE,1                                                         
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R9),DUB+6(2)                                                 
         LA    R6,4(R6)                                                         
         MVI   3(R9),C','                                                       
         LA    R9,4(R9)                                                         
         CLI   3(R6),0                                                          
         BE    PRTREV4                                                          
         BCT   R8,PRTREV2                                                       
         BCTR  R9,0                                                             
         MVI   0(R9),C' '                                                       
         LR    R9,R7                                                            
         LA    R9,132(R9)                                                       
         LR    R7,R9                                                            
         LA    R8,21                                                            
         B     PRTREV2                                                          
PRTREV4  CLI   ACTIVE,1                                                         
         BNE   PRTREV5                                                          
         BCTR  R9,0                                                             
         MVI   0(R9),C' '                                                       
         GOTO1 REPORT                                                           
         MVI   ACTIVE,0                                                         
         SPACE 2                                                                
PRTREV5  LA    R5,24(R5)                                                        
         LA    R9,P1+24                                                         
         LA    R6,SVCHG                                                         
         LA    R8,21                                                            
         B     PRTREV1                                                          
         SPACE 2                                                                
PRTREVX  MVC   P1,SPACES                                                        
         MVC   P2,SPACES                                                        
         LA    RE,SVCHG                                                         
         LA    RF,1024                                                          
         XCEF                                                                   
         XC    REVCNTR,REVCNTR                                                  
         B     EXIT                                                             
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         LTORG                                                                  
REASON   DC    X'80',X'00',CL22'NEW BUY'                                        
         DC    X'40',X'00',CL22'BUY DESCRIPTION CHANGE'                         
         DC    X'20',X'00',CL22'POOL BRAND ALLOCATION'                          
         DC    X'10',X'00',CL22'OTO'                                            
         DC    X'08',X'00',CL22'COMMENT CHANGE'                                 
         DC    X'04',X'00',CL22'REPEAT NEW BUY'                                 
         DC    X'02',X'00',CL22'DEMO LOOKUP'                                    
         DC    X'01',X'00',CL22'PACKAGE CHANGE'                                 
         DC    X'80',X'01',CL22'AFFIDAVIT INPUT'                                
         DC    X'40',X'01',CL22'PKG STATUS CHANGE'                              
         DC    X'20',X'01',CL22'INTG COST CHANGE'                               
         DC    X'10',X'01',CL22'PCD LOOKUP'                                     
         DC    X'08',X'01',CL22'DEMO OVERRIDE'                                  
         DC    X'04',X'01',CL22'HUT CHANGE'                                     
         DC    X'02',X'01',CL22'ORBIT CHANGE'                                   
         DC    X'01',X'01',CL22'FILM NUMBER'                                    
         DC    X'80',X'02',CL22'SKED CHANGE'                                    
         DC    X'00'                                                            
FIRST    DC    X'01'                                                            
LINNO    DS    C                                                                
REVCNTR  DS    F                                                                
CONTROL  DS    H                                                                
ACTIVE   DS    C                                                                
SVCHG    DS    1024C                                                            
       ++INCLUDE SPGENBUY                                                       
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
