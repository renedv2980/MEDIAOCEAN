*          DATA SET MPDICHK    AT LEVEL 005 AS OF 05/01/02                      
*CATALP MPDICHK                                                                 
         TITLE 'MPDICHK - MPDITAB VERIFICATION UTILITY'                         
         PRINT NOGEN                                                            
MPDICHK  CSECT                                                                  
         SPACE 2                                                                
*                                                                               
*        INPUT IS THREE FULLWORDS :-                                            
*         PROG ID, A(MPDITAB), A(ERROR RETURN FIELD HEADER) - OPTIONAL          
*                                                                               
*        IF ERROR FIELD IS SPECIFIED THEN THE MODULE WILL RETURN A              
*        MESSAGE TO THE CALLER (ASSUMING THE ERROR IS TRAPPABLE).               
*        IF NO ERROR FIELD IS SPECIFIED THE MODULE WILL NOT RETURN              
*        ON ERROR BUT POINT PSW AT THE RELEVANT MESSAGE.                        
*                                                                               
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPDICHK                                              
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         EJECT                                                                  
         L     R3,4(R1)           A(MPDITAB)                                    
         A     R3,8(R3)           A(CONTAB)                                     
         ST    R3,ACONTAB                                                       
         USING MPDIPD,R3                                                        
*                                                                               
*        GET ERROR RETURN FIELD AND SPACE FILL IT                               
*                                                                               
         MVI   ERRFLD,0            INIT AS NO ERR FIELD SPECIFIED               
         L     R2,8(R1)            LOAD ADDRESS OF ERR HDR (OPTIONAL)           
         LTR   R2,R2               ANY VALUE PASSED ?                           
         BZ    DV00                NO - IGNORE                                  
         ST    R2,AERRFLD          SAVE ADDRESS                                 
         MVI   ERRFLD,X'FF'        FLAG AS PRESENT                              
         MVI   8(R2),C' '          INIT FIELD                                   
         ZIC   R1,0(R2)            GET FIELD LEN                                
         SH    R1,=H'10'           LESS HEADER - LESS 1 FOR EXECUTE             
*                                                LESS 1 FOR MVC                 
         EX    R1,*+8                                                           
         B     DV00                                                             
         MVC   9(0,R2),8(R2)       PROPAGATE SPACES                             
*                                                                               
*        LOOK FOR END OF TABLE                                                  
*                                                                               
DV00     DS    0H                                                               
         L     R3,ACONTAB                                                       
*                                                                               
DV10     DS    0H                                                               
         CLI   MPPLEN,X'FF'        FOUND END ?                                  
         BE    DV20                                                             
         CLI   MPPTYP,C'P'         PAIRING CONTROL ENTRY ???                    
         LA    RF,ERR9                                                          
         BNE   ERREX               NO -ERROR EXIT                               
         ZIC   R1,MPPLEN           GET LENGTH                                   
         AR    R3,R1               BUMP TO NEXT                                 
         B     DV10                                                             
         DC    C'**END OF TABLE ERR**'       INCASE EOT NOT FOUND               
*                                                                               
*        CHECK THAT ALL FORMULA B/Q PAIRS EXIST AS ENTRIES                      
*        AND THAT NO INVALID COMBINATIONS ARE DEFINED.                          
*                                                                               
DV20     DS    0H                                                               
         L     R3,ACONTAB                                                       
DV30     DS    0H                                                               
         CLI   MPPLEN,X'FF'                                                     
         BE    DVOK                                                             
         MVC   BQPAIR,MPPBCOD                                                   
         MVC   SUFFIX,MPPSUFF                                                   
         CLI   MPPFOP3B,0                                                       
         BNE   DV40                                                             
         CLI   MPPFOP2B,0                                                       
         BNE   DV50                                                             
         CLI   MPPFOP1B,0                                                       
         BNE   DV60                                                             
         CLI   MPPHOW,MPOPSUBR    IS A SUBROUTINE CALL SPECIFIED                
         BNE   DV100              NO FORMULA OR SUBR                            
*                                                                               
*        CHECK THAT SUBR CALL SPECIFIES AN ADDR - MAY BE A(SUBR) FOR            
*        FOR GENERAL ROUTINES OR A NUMBER FOR LOCAL SUBROUTINES.                
*                                                                               
         NC    MPPASUBR,MPPASUBR                                                
         LA    RF,12                                                            
         BZ    ERREX                                                            
         B     DV100               NO FORMULA                                   
*                                                                               
DV40     DS    0H                                                               
         SR    R5,R5                                                            
         ICM   R5,3,MPPVALF        LOAD VALIDITY FLAG BYTES                     
         MVC   BQPAIR,MPPFOP3B     LOAD BASE QUALIFIER PAIR                     
         BAS   RE,CTSCAN           LOOK FOR IT (ONLY RETURNS IF O.K.)           
         CLI   MPPFOP2B,0                                                       
         LA    RF,ERR1                                                          
         BZ    ERREX                                                            
*                                                                               
DV50     DS    0H                                                               
         SR    R5,R5                                                            
         ICM   R5,3,MPPVALF        LOAD VALIDITY FLAG BYTES                     
         MVC   BQPAIR,MPPFOP2B     LOAD BASE QUALIFIER PAIR                     
         BAS   RE,CTSCAN           LOOK FOR IT (ONLY RETURNS IF O.K.)           
         CLI   MPPFOP1B,0                                                       
         LA    RF,ERR1                                                          
         BZ    ERREX                                                            
*                                                                               
DV60     DS    0H                                                               
         SR    R5,R5                                                            
         ICM   R5,3,MPPVALF        LOAD VALIDITY FLAG BYTES                     
         MVC   BQPAIR,MPPFOP1B     LOAD BASE QUALIFIER PAIR                     
         BAS   RE,CTSCAN           LOOK FOR IT (ONLY RETURNS IF O.K.)           
*                                                                               
*        CHECK THAT ANY TABLE ENTRY THAT HAS A FORMULA IS IN THE                
*        VARIABLE PART OF THE DATA VECTOR                                       
*                                                                               
         CLI   MPPVTYP,MPVTYPV     VARIABLE PART OF DATA VECTOR ???             
         BE    DV65                                                             
         LA    RF,ERR13                                                         
         B     ERREX                                                            
*                                                                               
*        CHECK THAT ANY FORMULA CALCULATING RANK OR CUME HAS ONLY               
*        ONE OPERAND                                                            
*                                                                               
DV65     DS    0H                                                               
         CLI   MPPFO1,MPOPRNK      RANK ????                                    
         BE    DV70                                                             
         CLI   MPPFO1,MPOPCUM      CUME ????                                    
         BE    DV70                                                             
         CLI   MPPFO1,MPOPRCUM     REV CUME ???                                 
         BNE   DV100                                                            
DV70     DS    0H                                                               
         NC    MPPFOP2B(5),MPPFOP2B                                             
         LA    RF,ERR5                                                          
         BNZ   ERREX                                                            
         CLI   MPPFO1,MPOPRNK      RANK ????                                    
         BE    DV100                                                            
*                                                                               
*        CUME ENTRIES - CHECK FORMAT AND TYPE OF CUME FIELD                     
*                       EQUAL FIELD BEING CUME'D                                
         CLC   MPPINTD,DATATYPE                                                 
         LA    RF,ERR8                                                          
         BNE   ERREX                                                            
         CLC   MPPDLEN,DATALEN                                                  
         BNE   ERREX                                                            
*                                                                               
*        CHECK DATA FORMAT/LENGTH                                               
*                                                                               
DV100    DS    0H                                                               
         CLI   MPPDLEN,0           CHECK A LENGTH IS SPECIFIED                  
         LA    RF,ERR7                                                          
         BNH   ERREX                                                            
         CLI   MPPINTD,C'P'        PACKED ???                                   
         BE    DV110                                                            
         CLI   MPPINTD,C'B'        BINARY ???                                   
         LA    RF,ERR6                                                          
         BNE   ERREX                                                            
DV110    DS    0H                                                               
         CLI   MPPDLEN,X'8'                                                     
         LA    RF,ERR7                                                          
         BH    ERREX                                                            
         B     DV130                                                            
*                                                                               
*        CHECK SUFFIX.                                                          
*        REQUIRED - MUST HAVE A VALUE SET WHICH UNIQUELY DEFINES THE            
*        (CTL 40)   CALC METHOD.                                                
*        OPTIONAL - MAY HAVE A VALUE SET WHICH WILL BE THE DEFAULT IF           
*        (CTL 20)   THE USER DOES NOT SPECIFY A VALUE. THE SUFFIX DOES          
*                   NOT AFFECT THE CALC METHOD                                  
*        NO SUFFIX- THIS ENTRY IS NOT SUFFIXABLE                                
*        (CTL 01)                                                               
*                                                                               
DV120    DS    0H                                                               
         CLI   MPPDLEN,X'4'                                                     
         LA    RF,ERR7                                                          
         BH    ERREX                                                            
DV130    DS    0H                                                               
         TM    MPPCTLF,MPSUFFR    SUFFIX REQUIRED ???                           
         BZ    DV140                                                            
         CLI   MPPSUFF,0           MUST HAVE A SUFFIX                           
         LA    RF,ERR10                                                         
         BE    ERREX                                                            
DV140    TM    MPPCTLF,MPSUFFN     SUFFIX NOT ALLOWED ???                       
         BZ    DV150                                                            
         CLI   MPPSUFF,0                                                        
         LA    RF,ERR11                                                         
         BNE   ERREX                                                            
DV150    DS    0H                                                               
*                                                                               
DV900    DS    0H                                                               
         ZIC   R1,MPPLEN                                                        
         AR    R3,R1                                                            
         B     DV30                                                             
DVOK     DS    0H                                                               
         LA    RF,NOERROR          FLAG AS NO ERROR                             
         B     OKEXIT              OUTPUT MESSAGE IF REQUESTED                  
         DROP  R3                                                               
         EJECT                                                                  
**********************************************************************          
*                                                                    *          
*        ROUTINE TO LOOK FOR FORMULA B/Q PAIR IN ITS OWN RIGHT AND   *          
*        TO CHECK THAT ONLY VALID QUALIFIERS ARE DEFINED.            *          
*                                                                    *          
*                                                                    *          
**********************************************************************          
*                                                                               
CTSCAN   DS    0H                                                               
         ST    RE,CTSRET           SAVE RETURN ADDRESS                          
         USING MPDIPD,R4                                                        
         L     R4,ACONTAB                                                       
CTS010   DS    0H                                                               
         CLI   MPPLEN,X'FF'                                                     
         LA    RF,ERR2                                                          
         BE    ERREX                                                            
         CLC   BQPAIR,MPPBCOD                                                   
         BNE   CTS015                                                           
         SR    R1,R1                                                            
         ICM   R1,3,MPPVALF        VALIDITY FLAG BYTES                          
         NR    R1,R5               VALID FOR ANY OF THIS PROG SET               
         BZ    CTS015              NOT VALID FOR ANY SO IGNORE                  
         TM    MPPCTLF,MPSUFFR     SUFFIX REQUIRED ???                          
         BZ    CTS014              NO -DON'T CHECK FOR CORRECT SUFFIX           
         CLI   MPPVTYP,MPVTYPV     IS THIS A VARIABLE VECTOR ENTRY ??           
         BNE   CTS014              NO - IGNORE SFX FOR FIXED OR GLOBAL          
         CLC   SUFFIX,MPPSUFF      CORRECT SUFFIX ???                           
         BNE   CTS015                                                           
CTS014   DS    0H                                                               
         XR    R5,R1               REMOVE VALIDATED PROGS FROM MASK             
         NR    R5,R5               ANY BITS STILL TO VALIDATE ???               
         BZ    CTS020              NO -DONE                                     
CTS015   DS    0H                                                               
         ZIC   R1,MPPLEN                                                        
         AR    R4,R1               BUMP TO NEXT CONTAB ENTRY                    
         B     CTS010              CONTINUE SEARCH                              
*                                                                               
CTS020   DS    0H                                                               
         CLI   MPPQCOD,MPQRANK     RANK CANNOT BE  PART OF FORMULA              
         LA    RF,ERR3                                                          
         BE    ERREX                                                            
         CLI   MPPQCOD,MPQCPC      CUME% CANNOT BE PART OF FORMULA              
         BL    CTS030                                                           
         CLI   MPPQCOD,MPQCPCR     CUME%REACH (HIGHEST CUME%)                   
         LA    RF,ERR4                                                          
         BNH   ERREX                                                            
CTS030   DS    0H                                                               
         MVC   DATATYPE,MPPINTD                                                 
         MVC   DATALEN,MPPDLEN                                                  
*                                                                               
CTSEX    DS    0H                                                               
         L     RE,CTSRET           RESTORE RETURN ADDRESS                       
         BR    RE                                                               
         DROP  R4                                                               
         EJECT                                                                  
OKEXIT   DS    0H                                                               
         CLI   ERRFLD,0            ANY OP MESSAGE FIELD ???                     
         BE    DVXIT                                                            
         MVI   ERRFLG,0                                                         
         B     XIT01                                                            
         SPACE 5                                                                
ERREX    DS    0H                                                               
         CLI   ERRFLD,0            ANY ERROR ADDR ??                            
         BER   RF                  DIE AT APPROPRIATE PLACE                     
         MVI   ERRFLG,X'FF'                                                     
         SPACE 5                                                                
XIT01    DS    0H                                                               
         LR    R1,RF                                                            
         LA    R1,2(R1)            POINT TO LENGTH OF TEXT                      
         ZIC   R3,0(R1)            GET LEN                                      
         L     R2,AERRFLD          ADDR OF ERROR HEADER                         
         OI    6(R2),X'80'         FLAG XMT                                     
         ZIC   R1,0(R2)            LENGTH OF SCREEN FIELD                       
         SH    R1,=H'15'           LESS HEADER LENGTH AND BBQQSS                
         CR    R1,R3               CHECK TEXT NOT GREATER THAN FIELD            
         BNL   XIT02                                                            
         LR    R3,R1               ONLY MOVE LENGTH OF O/P FIELD                
XIT02    DS    0H                                                               
         BCTR  R3,0                GET AN EXECUTABLE LEN                        
         EX    R3,*+8              MOVE IN TEXT                                 
         B     *+10                                                             
         MVC   15(0,R2),3(RF)      MOVE ERR MESSAGE TO SCREEN                   
         CLI   ERRFLG,0                                                         
         BE    DVXIT                                                            
         ZIC   R1,BQPAIR           GET BASE VALUE                               
         CVD   R1,DUB                                                           
         UNPK  8(2,R2),DUB                                                      
         OI    9(R2),X'F0'                                                      
*                                                                               
         ZIC   R1,BQPAIR+1         GET QUAL VALUE                               
         CVD   R1,DUB                                                           
         UNPK  10(2,R2),DUB                                                     
         OI    11(R2),X'F0'                                                     
*                                                                               
         CLI   SUFFIX,0                                                         
         BE    DVXIT                                                            
         ZIC   R1,SUFFIX           GET SUFFIX                                   
         CVD   R1,DUB                                                           
         UNPK  12(2,R2),DUB                                                     
         OI    13(R2),X'F0'                                                     
DVXIT    DS    0H                                                               
         XMOD1 1                                                                
         EJECT                                                                  
NOERROR  DC    H'0',AL1(NOERRE-NOERROR-3)                                       
         DC    C'**TABLE VALID**'                                               
NOERRE   DS    0X                                                               
ERR1     DC    H'0',AL1(ERR1E-ERR1-3)                                           
         DC    C'**FORMULA INVALID 1ST/2ND B/Q MISSING**'                       
ERR1E    DS    0X                                                               
ERR2     DC    H'0',AL1(ERR2E-ERR2-3)                                           
         DC    C'**FORMULA INVALID B/Q NOT FOUND**'                             
ERR2E    DS    0X                                                               
ERR3     DC    H'0',AL1(ERR3E-ERR3-3)                                           
         DC    C'**FORMULA INVALID RANK QUAL**'                                 
ERR3E    DS    0X                                                               
ERR4     DC    H'0',AL1(ERR4E-ERR4-3)                                           
         DC    C'**FORMULA INVALID CUME% QUAL**'                                
ERR4E    DS    0X                                                               
ERR5     DC    H'0',AL1(ERR5E-ERR5-3)                                           
         DC    C'**FORMULA INVALID CUME/RANK OP1 WITH 2ND B/Q**'                
ERR5E    DS    0X                                                               
ERR6     DC    H'0',AL1(ERR6E-ERR6-3)                                           
         DC    C'**DATA TYPE INVALID**'                                         
ERR6E    DS    0X                                                               
ERR7     DC    H'0',AL1(ERR7E-ERR7-3)                                           
         DC    C'**DATA LENGTH INVALID**'                                       
ERR7E    DS    0X                                                               
ERR8     DC    H'0',AL1(ERR8E-ERR8-3)                                           
         DC    C'**DATA LEN/TYPE MISMATCH ON CUME**'                            
ERR8E    DS    0X                                                               
ERR9     DC    H'0',AL1(ERR9E-ERR9-3)                                           
         DC    C'**CONTAB TYPE NOT "P"**'                                       
ERR9E    DS    0X                                                               
ERR10    DC    H'0',AL1(ERR10E-ERR10-3)                                         
         DC    C'**SUFFIX REQUIRED**'                                           
ERR10E   DS    0X                                                               
ERR11    DC    H'0',AL1(ERR11E-ERR11-3)                                         
         DC    C'**SUFFIX NOT ALLOWED**'                                        
ERR11E   DS    0X                                                               
ERR12    DC    H'0',AL1(ERR12E-ERR12-3)                                         
         DC    C'**NO SUBR ADDRESS**'                                           
ERR12E   DS    0X                                                               
ERR13    DC    H'0',AL1(ERR13E-ERR13-3)                                         
         DC    C'**FORMULA FOR FXD VAL**'                                       
ERR13E   DS    0X                                                               
ERR14    DS    0X                                                               
         LTORG                                                                  
WORKD    DSECT                                                                  
BQPAIR   DS    XL2                                                              
SUFFIX   DS    X                                                                
DATATYPE DS    XL1                                                              
DATALEN  DS    XL1                                                              
ERRFLD   DS    XL1                                                              
ERRFLG   DS    X                                                                
         DS    X                                                                
DUB      DS    D                                                                
AERRFLD  DS    F                                                                
ACONTAB  DS    F                                                                
CTSRET   DS    F                                                                
WORKX    DS    0H                                                               
         SPACE 3                                                                
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*MPDIITABD,MPPCMDD,MPEQUATES                                                    
         PRINT OFF                                                              
       ++INCLUDE MPDITABD                                                       
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPEQUATES                                                      
         PRINT ON                                                               
         SPACE                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005MPDICHK   05/01/02'                                      
         END                                                                    
