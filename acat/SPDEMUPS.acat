*          DATA SET SPDEMUPS   AT LEVEL 043 AS OF 05/01/02                      
*CATALP SPDEMUPA                                                                
*                                                                               
* -----------> REMEMBER TO LINK LMSPDEMUP <-----------                          
*                                                                               
* -----------> DISABLED OPTIONS <-------------------                            
* 1. CANADIAN NSI UPGRADES                                                      
* 2. TAPE BASED DEMOS                                                           
*                                                                               
         SPACE 2                                                                
***********************************************************************         
* PARAM1 BYTE(S) 1-3 = A(SPOT UPGRADE BLOCK) (SPDEMUPD)               *         
* PARAM2         1-3 = A(INPUT DEMO LIST) OR ZERO                     *         
* PARAM3         1-3 = A(OUTPUT DEMO AREA) OR ZERO                    *         
***********************************************************************         
         TITLE 'SPDEMUP - SPOT SYSTEM DEMO UPGRADE ROUTINES'                    
SPDEMUP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,SPDEMUP,RA,CLEAR=YES,RR=R8                           
         USING WORKD,RC            RC=A(W/S)                                    
         ST    RD,SAVERD           SAVE RD FOR ERROR EXIT                       
         ST    R1,APARM                                                         
         MVI   0(R1),0             RESET ERROR BYTE                             
         L     R9,0(R1)                                                         
         USING SPDEMUPD,R9         R9=A(LOOK-UP/UPG   E EXPRESSION)             
         MVC   ADEMLST,4(R1)       SAVE A(DEMO LIST) & A(OUTPUT)                
         MVC   ADEMOUT,8(R1)                                                    
         ST    R8,RELO                                                          
         LH    RF,=Y(IOAREA2-WORKD)                                             
         AR    RF,RC                                                            
         ST    RF,AIO2             GET ADDRESSABILITY TO IO2                    
         LH    RF,=Y(IOAREA1-WORKD)                                             
         AR    RF,RC                                                            
         ST    RF,AIO1             GET ADDRESSABILITY TO IO2                    
*                                                                               
         L     RF,SPUPAFAC                                                      
         ST    RF,VCOMFACS                                                      
         USING COMFACSD,RF         RF=A(COMFACS)                                
         MVC   VCALLOV,CCALLOV                                                  
         MVC   VDEMADDR,CDEMADDR                                                
         MVC   VDEMAND,CDEMAND                                                  
         MVC   VDEMOMTH,CDEMOMTH                                                
         MVC   VDEMOUT,CDEMOUT                                                  
         MVC   VDEMAINT,CDEMAINT                                                
         MVC   VHELLO,CHELLO                                                    
         LA    RF,TPTVALS                                                       
         ST    RF,ATPTVALS                                                      
         L     RF,=V(SUBR01)                                                    
         A     RF,RELO                                                          
         ST    RF,VSUBR01                                                       
         MVI   Y4ASW,X'00'                                                      
         MVI   Y4MSW,X'00'                                                      
         MVI   Y4SSW,C'P'                                                       
         MVI   Y4SYRS,2                                                         
         MVI   SBKCTRL,0                                                        
*                                                                               
         MVI   TAPEOPT,C'N'        NORMAL PRECISION IS BOOK                     
         MVI   NORMHPT,C'N'        NORMALIZE HPT VALUES                         
         MVI   PRECUPG,C'N'        1 DECIMAL UPGRADE VALUES                     
         TM    SPUPTYPE,X'C0'                                                   
         BNM   *+12                                                             
         MVI   PRECUPG,C'Y'                                                     
         XI    SPUPTYPE,X'40'                                                   
         CLI   SPUPMED,C'T'        USTV                                         
         BNE   FIXARBX                                                          
         CLI   SPUPSRC,C'A'        AND ARB                                      
         BNE   FIXARBX                                                          
         MVI   SPUPSRC,C'N'        SWITCH TO NSI                                
FIXARBX  DS    0C                                                               
*                                                                               
         CLI   SPUPMED,C'N'        DO EXTENDED PREC FOR NET ALSO                
         BE    ENSICHKX                                                         
         CLI   SPUPMED,C'T'        EXTENDED PRECISION NSI/USTV ONLY             
         BNE   ENSINO                                                           
         CLI   SPUPSRC,C'N'                                                     
         BNE   ENSINO                                                           
         OC    SPUPFBKL,SPUPFBKL   FOR MULTI-BOOK REQUESTS,                     
         BNZ   ENSINO               FORCE RTG-BASED TO PREVENT OVERFLOW         
ENSICHKX EQU   *                                                                
         TM    SPUPOPTS,SPOPDMAI                                                
         BZ    *+8                                                              
         MVI   TAPEOPT,C'Y'                                                     
         CLI   SPUPMED,C'N'        NET ALWAYS NORMALIZED                        
         BE    *+12                                                             
         TM    SPUPOPTS,SPOPDMAI+SPOPNORM                                       
         BNO   *+8                                                              
         MVI   NORMHPT,C'Y'        NORMALIZE HPT VALUES                         
*                                                                               
ENSINO   CLI   TAPEOPT,C'Y'        IF TAPE PRECISION                            
         BNE   *+8                                                              
         MVI   PERSHR,C'Y'         FORCE PERSONS SHARE                          
*                                                                               
         MVC   LOCALMED,SPUPMED    SETUP FOR CSI UPGRADES                       
         CLI   LOCALMED,MEDCAN                                                  
         BNE   *+8                                                              
         CLI   SPUPSRC,C'N'                                                     
         BNE   *+8                                                              
         MVI   LOCALMED,C'T'                                                    
         MVC   LOCALMED,SPUPMED    DISABLE OPTION FOR NOW                       
         DROP  RF                                                               
         SPACE 1                                                                
         L     R0,=V(SPGETIUN)                                                  
         A     R0,RELO                                                          
         ST    R0,VGETIUN                                                       
         L     R0,=V(DEINMKT)                                                   
         A     R0,RELO                                                          
         ST    R0,VDEINMKT                                                      
         SPACE 1                                                                
         ICM   R0,15,=V(DEFINE)    OFFLINE HAS DEFINE LINKED                    
         BZ    *+12                                                             
         ST    R0,VDEFINE                                                       
         B     UP2                                                              
         GOTO1 VCALLOV,DMCB,0,X'D9000A26'                                       
         MVC   VDEFINE,0(R1)                                                    
*                                                                               
UP2      OC    ADISPTAB+1(3),ADISPTAB+1                                         
         BNZ   UP4                                                              
         GOTO1 VDEMADDR,DMCB,(X'FF',ADISPTAB),VCOMFACS                          
*                                                                               
UP4      CLI   SPUPSTA,C'Z'        NO DEMOS FOR CABLE                           
         BH    UPX                                                              
         CLI   LOCALMED,MEDCAN                                                  
         BE    UP8                                                              
         LA    R7,DBLOCK1          GET US AGENCY CONTROL VALUES                 
         USING DBLOCKD,R7                                                       
         BAS   RE,BLDBLK                                                        
         MVC   DBFILE,TPTVALS+1                                                 
         MVI   DBFUNCT,DBGETCTL                                                 
         GOTO1 VDEMAND,DMCB,DBLOCK,0                                            
         TM    DBCSHR,DBOTOSHR     TEST COMPUTE HOMES SHARES USING R/P          
         BZ    *+8                                                              
         MVI   PERSHR,YES                                                       
*                                                                               
         ICM   R1,15,ADISPTAB      LOCATE IUN DISP. TABLE ENTRY                 
         USING DSPHDRD,R1          R1=A(MASTER DISP. TABLE)                     
UP6      OC    0(2,R1),0(R1)       TEST E-O-T                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   DSPFILE,IUNVALS+1   MATCH ON INTERNAL FILE CODE                  
         BE    *+16                                                             
         ICM   RE,7,DSPAET                                                      
         LA    R1,1(RE,R1)         BUMP TO NEXT TABLE HEADER                    
         B     UP6                                                              
*                                                                               
         CLI   TAPEOPT,C'Y'        TAPE BASED OPTION                            
         BE    UP7                                                              
         CLC   DSPSBOOK,=X'B0F4'   BOOK BASE DISP TABLE                         
         BE    UP7                                                              
         ICM   RE,7,DSPAET         NO - USE NEXT DISPLACEMENT TABLE             
         LA    R1,1(RE,R1)                                                      
*                                                                               
UP7      LA    R1,DSPHDRLN(R1)     BUMP PAST HEADER                             
         ST    R1,ADISPDTA         SAVE A(FIRST DEMO ENTRY)                     
*                                                                               
UP8      LA    R7,DBLOCK1                                                       
         BAS   RE,BLDBLK           BUILD DBLOCK FOR RATING LOOK-UPS             
         CLC   SPUPFIL,PAVVALS     TEST FOR PAV FILE LOOK-UP                    
         BNE   UP10                                                             
         XC    DBSELBK,DBSELBK     YES - GET LATEST T/P BOOK                    
         MVC   DBFILE,TPTVALS+1                                                 
         MVI   DBFUNCT,DBGETTLB                                                 
         GOTO1 VDEMAND,DMCB,DBLOCK,0,0                                          
         L     R1,APARM                                                         
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BE    *+14                                                             
         MVC   0(1,R1),DBERROR     YES - RETURN DEMAND ERROR NUMBER             
         B     UPX                                                              
         OC    SPUPFBK,SPUPFBK     TEST BOOK PASSED                             
         BZ    *+14                                                             
         CLC   SPUPFBK,DBACTBK     YES - TEST GR LATEST BOOK                    
         BNH   *+10                                                             
         MVC   SPUPFBK,DBACTBK     SET BOOK FROM LATEST T/P BOOK                
         LA    R1,SPUPFBKL                                                      
         LA    RF,L'SPUPFBKL/2     DO THE SAME FOR BOOK LIST                    
*                                                                               
UP9      OC    0(2,R1),0(R1)                                                    
         BZ    UP9A                                                             
         CLC   0(2,R1),DBACTBK                                                  
         BNH   *+10                                                             
         MVC   0(2,R1),DBACTBK                                                  
         LA    R1,2(R1)                                                         
         BCT   RF,UP9                                                           
*                                                                               
UP9A     BAS   RE,BLDBLK           RE-BUILD DBLOCK WITH BOOK VALUE              
         B     UP10                                                             
         EJECT                                                                  
***********************************************************************         
* GET ORIGINAL DEMO VALUES                                            *         
***********************************************************************         
         SPACE 1                                                                
UP10     MVI   DBFUNCT,DBTSTACS    TEST IF USER HAS ACCESS TO FILE              
         GOTO1 VDEMAND,DMCB,DBLOCK,0,0                                          
         L     R1,APARM                                                         
         CLI   DBERROR,0           TEST ACCESS IS VALID                         
         BE    *+14                                                             
         MVC   0(1,R1),DBERROR     NO - RETURN WITH ERROR                       
         B     UPX                                                              
         MVI   DBFUNCT,DBGETDEM    SET TO LOOK-UP DEMOS                         
*                                                                               
         CLI   LOCALMED,MEDCAN     BUILD DUMMY RECORD AT IO2 FOR CANADA         
         BNE   UP11                                                             
         L     R1,AIO2                                                          
         XC    0(50,R1),0(R1)                                                   
         MVI   0(R1),C'R'                                                       
         LA    R0,PRFRSTEL+1-PRKEY                                              
         STCM  R0,3,PRRLEN-PRKEY(R1)                                            
*                                                                               
UP11     MVC   DBTAPEP,TAPEOPT                                                  
         GOTO1 VDEMAND,DMCB,DBLOCK,DEMHK                                        
         L     R1,APARM                                                         
         OC    DBDIVSOR,DBDIVSOR   TEST DEMOS FOUND                             
         BNZ   *+12                                                             
         MVI   0(R1),X'FF'         RETURN NO DEMOS FOUND ERROR                  
         B     UPX                                                              
         CLI   DBERROR,X'80'       TEST FOR E-O-F                               
         BE    *+14                                                             
         MVC   0(1,R1),DBERROR     RETURN DEMAND ERROR NUMBER                   
         B     UPX                                                              
*                                                                               
         MVC   SPUPACTS,DBACTSRC   SET ACTUAL RATING SERVICE                    
         MVC   DBSELSRC,DBACTSRC                                                
*                                                                               
         CLI   LOCALMED,MEDCAN     CANADIAN LOOK-UPS                            
         BE    UP11A                                                            
         OC    DBSELBK,DBSELBK     LATEST IS AFTER X'5801'                      
         BZ    *+14                                                             
         CLC   DBSELBK,=X'5801'    IF AFTER DEC/87                              
         BL    *+8                                                              
         MVI   SBKCTRL,1           SET FOR NEW PRECISION                        
         B     UP12                                                             
UP11A    L     R1,AIO2                                                          
         STCM  R1,15,DBAREC                                                     
         LA    R1,PRFRSTEL-PRKEY(R1)                                            
         STCM  R1,15,DBAQUART                                                   
         XC    WORK,WORK                                                        
         LA    R0,DBLOCKD                                                       
         STCM  R0,15,WORK                                                       
         MVC   WORK+06(2),DBDIVSOR                                              
         MVC   WORK+08(3),DBFILE                                                
         MVC   WORK+11(3),DBFILE                                                
         GOTO1 VDEMOMTH,DMCB,DIV,AIO2,AIO2,WORK                                 
         CLI   SPUPTYPE,SPUPTNDX   TEST INDEX UPGRADE                           
         BNE   UP18                NO - OTHER UPGRADES NOT SUPPORTED            
         MVC   WORK+6(2),SPUPFLD1                                               
*                                  MULTIPLY RECORD BY UPGRADE INDEX             
         GOTO1 (RF),(R1),MUL,AIO2,AIO2,WORK                                     
         LA    R0,100                                                           
         STCM  R0,3,WORK+6                                                      
*                                  DIVIDE RECORD BY 100                         
         GOTO1 (RF),(R1),DIV,AIO2,AIO2,WORK                                     
         B     UP18                                                             
*                                                                               
UP12     CLI   SPUP2YRR,C'Y'       TEST 2 YEAR RATINGS REQUIRED                 
         BNE   UP13                                                             
         MVC   DBSELBK,SPUPFBK     YES - DECREMENT ACTUAL BOOK                  
         NI    DBSELBK+1,X'0F'     AND OFF WEEK BITS                            
         ZIC   R1,DBSELBK                                                       
         BCTR  R1,0                                                             
         STC   R1,DBSELBK                                                       
         PACK  DBSELWKN,SPUPFBK+1(1)                                            
         NI    DBSELWKN,X'0F'      AND OFF MONTH BITS                           
         XC    DBACTBK,DBACTBK                                                  
         MVC   SVDIVSOR,DBDIVSOR   SAVE DBDIVSOR VALUE                          
         XC    DBDIVSOR,DBDIVSOR                                                
         MVC   DBTAPEP,TAPEOPT                                                  
         GOTO1 VDEMAND,DMCB,DBLOCK,DEMHK                                        
         SR    R0,R0                                                            
         ICM   R0,3,DBDIVSOR       ADD DBDIVSORS FOR CALLS TOGETHER             
         AH    R0,SVDIVSOR                                                      
         STCM  R0,3,DBDIVSOR                                                    
*                                  CLEAR WORK AREA VALUES                       
UP13     XC    OLDRTG(LENVALS*2),OLDRTG                                         
         XC    OLDHPT(LENVALS*2),OLDHPT                                         
*                                                                               
         LA    R0,3                UNWEIGHT VUTS                                
         LA    R1,HOMEVUTS                                                      
         ST    R1,DMCB+8                                                        
         SR    R2,R2                                                            
         GOTO1 VSUBR01,DMCB,('UNWGHTQ',(RC)),(R0),,(R2)                         
*        BAS   RE,UNWGHT                                                        
*                                                                               
         LA    R0,NUMVALS*4        UNWEIGHT DEMO VALUES                         
         LA    R1,NEWRTG                                                        
         ST    R1,DMCB+8                                                        
         LA    R2,OLDRTG                                                        
         GOTO1 VSUBR01,DMCB,('UNWGHTQ',(RC)),(R0),,(R2)                         
*        BAS   RE,UNWGHT                                                        
*                                                                               
         LA    RF,3                RF=N'HOMES SHARE VALUES                      
         SR    RE,RE               RE=INDEX REGISTER                            
UP15     L     R1,UORHOMES(RE)     HOMES SHARE=RATING/VUT                       
         M     R0,=F'2000'         RATING + PUT LINES ARE 1 DECIMAL             
         L     R2,HOMEVUTS(RE)                                                  
         CLI   PERSHR,YES          TEST FOR HOMES SHARE=R/P                     
         BNE   *+8                                                              
         L     R2,UOPHOMES(RE)                                                  
         LTR   R2,R2               TEST FOR ZERO PUT                            
         BNZ   *+10                                                             
         SR    R1,R1               YES-SET SHARE TO ZERO                        
         B     *+14                                                             
         DR    R0,R2                                                            
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         ST    R1,HOMSHR(RE)                                                    
         LA    RE,4(RE)                                                         
         BCT   RF,UP15                                                          
*                                                                               
         LA    RF,NUMVALS*2        RF=N'SHARE VALUES                            
         SR    RE,RE               RE=INDEX REGISTER                            
UP16     L     R1,OLDRTG(RE)       SHARE=RATING/PUT                             
         M     R0,=F'2000'         RATING + PUT LINES ARE 1 DECIMAL             
         L     R2,OLDHPT(RE)                                                    
         LTR   R2,R2               TEST FOR ZERO PUT                            
         BNZ   *+10                                                             
         SR    R1,R1               YES-SET SHARE TO ZERO                        
         B     *+14                                                             
         DR    R0,R2                                                            
         AH    R1,=H'1'            ROUNDED DIVIDE                               
         SRA   R1,1                SHARE PRECISION IS XX.X                      
         ST    R1,OLDSHR(RE)                                                    
         LA    RE,4(RE)                                                         
         BCT   RF,UP16                                                          
         CLI   PERSHR,YES          TEST HOMES SHARES=R/P                        
         BE    *+10                YES-USE COMPUTED R/P IN UPGRADE              
         MVC   OLDSHR+(UORHOMES-OLDRTG)(HOMSHRLN),HOMSHR                        
*                                                                               
         MVC   NEWRTG(LENVALS*2),OLDRTG                                         
         MVC   NEWHPT(LENVALS*2),OLDHPT                                         
         BAS   RE,UPGRADE          CALL DEMO UPGRADE ROUTINE                    
*                                                                               
         OC    ADEMLST,ADEMLST     TEST DEMO LOOK-UP REQUIRED                   
         BZ    UP28                                                             
         XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR LOOK-UPS                    
         MVC   DBFILE,PAVVALS+1    FORCE OVERRIDE ELEMENT LOOK-UP               
         L     RE,SPUPAREC                                                      
         ST    RE,DBAREC                                                        
         LA    RE,DRFRSTEL-DRKEY(RE)                                            
         ST    RE,DBAQUART                                                      
         MVC   DBCOMFCS,SPUPAFAC                                                
*                                                                               
UP18     ICM   R0,15,ADEMLST       TEST DEMO LOOK-UP REQUIRED                   
         BZ    UP28                                                             
         TM    SPUPOPTS,X'80'      1 DECIMAL PRECISION                          
         BZ    UP19                                                             
         LA    RF,DEXTRA1          SET FOR 1 DEC AND HUNDREDS                   
         ST    RF,DBEXTEND                                                      
UP19     GOTO1 VDEMOUT,DMCB,(C'L',(R0)),DBLOCK,ADEMOUT                          
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         XC    DBEXTEND,DBEXTEND                                                
*                                                                               
         ICM   R2,15,SPUPAOVR      POINT TO USER DEMO OVERRIDE LIST             
         BZ    UP28                FLAG USER OVERRIDES IN DEMO LIST             
UP20     CLI   0(R2),0             TEST E-O-R                                   
         BE    UP28                                                             
         CLI   0(R2),OVERELEM      TEST DEMO OVERRIDE ELEMENT                   
         BNE   UP24                                                             
         L     RF,ADEMLST          LOOK-UP DEMO IN LIST                         
         L     R1,ADEMOUT                                                       
UP22     CLI   0(RF),X'FF'         TEST E-O-L                                   
         BE    UP24                                                             
         CLC   2(1,RF),3(R2)       MATCH ON DEMO                                
         BE    *+16                                                             
         LA    RF,3(RF)                                                         
         LA    R1,4(R1)                                                         
         B     UP22                                                             
         BAS   RE,SETOVER          GO SET OTHER OVERRIDE VALUES                 
UP24     ZIC   R0,1(R2)            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         B     UP20                                                             
*                                                                               
UP28     MVI   SPUPPRG,C' '        RETURN PROGRAM NAME(S)                       
         MVC   SPUPPRG+1(L'SPUPPRG-1),SPUPPRG                                   
         MVC   SPUPPRG(L'STPROG),STPROG                                         
         CLC   STPROG,NDPROG                                                    
         BE    *+14                                                             
         MVI   SPUPPRG+7,C'/'                                                   
         MVC   SPUPPRG+8(7),NDPROG                                              
         B     UPX                                                              
*                                                                               
UPERR    L     RD,SAVERD           ENTER HERE ON ERROR                          
UPX      XIT1  ,                   EXIT FROM MODULE                             
XIT      EQU   UPX                                                              
         EJECT                                                                  
***********************************************************************         
* HOOK ROUTINE TO PROCESS DEMO ELEMENTS FOR OLD DATA LOOK-UPS         *         
***********************************************************************         
         SPACE 1                                                                
DEMHK    ST    RE,SAVERE           SAVE RETURN ADDRESS                          
*                                  EXTRACT ACTUAL BOOK VALUE                    
         OC    SPUPFBK,SPUPFBK                                                  
         BNZ   *+10                                                             
         MVC   SPUPFBK,DBACTBK                                                  
*                                  EXTRACT PROGRAM NAMES                        
         GOTO1 VDEFINE,DMCB,PROGPLUS,DBLOCKD,NDPROG                             
         OC    STPROG,STPROG                                                    
         BNZ   *+10                                                             
         MVC   STPROG,NDPROG                                                    
*                                  EXTRACT MARKET NUMBER                        
         OC    MARKET,MARKET       TEST MARKET NUMBER SET                       
         BNZ   DEMHK1                                                           
         L     R1,DBAREC           POINT TO FIRST ELEMENT                       
         LA    R1,DRFRSTEL-DRKEY(R1)                                            
         CLI   0(R1),MARCODEQ      EXTRACT MARKET NUMBER                        
         BNE   DEMHK1                                                           
         MVC   MARKET,MARNO-MARELEM(R1)                                         
*                                                                               
DEMHK1   CLI   LOCALMED,MEDCAN     TEST CANADIAN LOOK-UPS                       
         BNE   DEMHK2                                                           
         XC    WORK,WORK           MAD RECORD INTO IOAREA2                      
         LA    R0,DBLOCKD                                                       
         ST    R0,WORK                                                          
         MVC   WORK+6(2),DBFACTOR                                               
         MVC   WORK+8(3),DBFILE                                                 
         MVC   WORK+11(3),DBFILE                                                
         GOTO1 VDEMOMTH,DMCB,MAD,DBAREC,AIO2,WORK                               
         B     DEMHKX                                                           
*                                                                               
DEMHK2   CLI   PUREFLAG,YES        TEST PURE PAV RECORD                         
         BNE   DEMHK3                                                           
         L     R1,DBAREC           EXTRACT PROGRAM DAY/WEEK                     
         MVC   PURESTIM,PRSTIM-PRKEY(R1)                                        
         MVC   PUREDW,PRDW-PRKEY(R1)                                            
         L     R1,DBAQUART         EXTRACT PROGRAM DURATION                     
         ICM   R1,1,PHDUR-PHELEM(R1)                                            
         BNZ   *+8                                                              
         LA    R1,2                                                             
         SRL   R1,1                ROUND TO NEXT LOWEST EVEN QTR HOUR           
         SLL   R1,1                                                             
         STC   R1,PUREDUR                                                       
*                                                                               
DEMHK3   DS    0H                                                               
         SPACE 1                                                                
* EXTRACT OLD UNVS/RTGS/IMPS/PUTS/TOTS                                          
         SPACE 1                                                                
         GOTO1 VGETIUN,DMCB,(4,DBLOCK),NEWUNV                                   
*                                                                               
         OC    OLDUNV(LENVALS),OLDUNV   TEST OLD UNVS SET YET                   
         BNZ   *+10                                                             
         MVC   OLDUNV(LENVALS),NEWUNV                                           
*                                  EXTRACT VUTS (FOR HOMES SHARES)              
         CLI   PERSHR,YES                                                       
         BE    DEMHKX                                                           
         XC    OVERDEMS(12),OVERDEMS                                            
         CLI   DBSELMED,C'N'                                                    
         BNE   DEMHK4                                                           
         MVC   DEMOLST(10),NETPUTS                                              
         GOTO1 VDEMOUT,DMCB,(C'L',DEMOLST),DBLOCK,OVERDEMS                      
         B     DEMHK5                                                           
*                                                                               
DEMHK4   GOTO1 VDEMOUT,(R1),(C'L',HOMEVUT),DBLOCK,OVERDEMS                      
         OC    OVERDEMS(4),OVERDEMS   ANY VUTS (RTG/SHR)                        
         BNZ   DEMHK5                                                           
         GOTO1 (RF),(R1),(C'L',HOMEPUT)  NO - USE THE PUT                       
*                                                                               
DEMHK5   LA    R0,3                WEIGHT & ACCUMULATE VUTS                     
         LA    R1,OVERDEMS                                                      
         ST    R1,DMCB+8                                                        
         LA    R2,HOMEVUTS                                                      
         GOTO1 VSUBR01,DMCB,('WGHTUPQ',(RC)),(R0),,(R2)                         
*        BAS   RE,WGHTUP                                                        
*                                                                               
DEMHKX   L     RE,SAVERE           RETURN TO DEMAND FOR NEXT RECORD             
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO DO UPGRADES                                             *         
***********************************************************************         
         SPACE 1                                                                
UPGRADE  NTR1  ,                                                                
         MVI   PUTUPGD,NO                                                       
         L     R1,SPUPAREC                                                      
         LA    RE,DRFRSTEL-DRKEY(R1)                                            
         ST    RE,AFRSTEL                                                       
         MVC   FILEVALS,TPTVALS                                                 
         CLC   SPUPFIL,TPTVALS                                                  
         BE    *+10                                                             
         MVC   FILEVALS,PAVVALS                                                 
*                                                                               
         LA    R7,DBLOCK2                                                       
         XC    DBLOCK,DBLOCK       EXPLODE RECORD INTO IUNREC                   
         MVC   DBFILE,IUNVALS+1    BUILD DBLOCK FOR LOOKUP/EXPLODE              
         MVC   DBAREC,SPUPAREC                                                  
         MVC   DBAQUART,AFRSTEL                                                 
         MVC   DBCOMFCS,VCOMFACS                                                
         MVC   DBTAPEP,TAPEOPT                                                  
*                                                                               
         MVI   HPT,OLD             SET TO GET OLD H/P/T LINE                    
         LA    R1,BOOK                                                          
         OC    UOQHOMES,UOQHOMES   ONLY IF QHOMES NOT PRESENT                   
         BNZ   *+8                                                              
         BAS   RE,GETHPT           GET OLD H/P/T VALUES                         
         MVI   HPT,NEW             SET TO GET NEW H/P/T VALUES                  
         B     UPRTG                                                            
         EJECT                                                                  
UPRTG    CLI   SPUPTYPE,SPUPTRTG                                                
         BNE   UPHPT                                                            
         OC    SPUPFLD2,SPUPFLD2   TEST IF SHARE PRESENT                        
         BZ    *+14                                                             
         CLC   SPUPFLD2,BOOK       TEST SHARE VALUE PRESENT                     
         BL    UPRTGSHR            HIGH IS A BOOK -- NOT A SHARE                
         SPACE 1                                                                
***********************************************************************         
* RATING ONLY UPGRADE                                                 *         
* ADJRTG = RTGINX * OLDRTG                                            *         
* OUTPUT = OLDRTG / OLDHPT / OLDRTG / NEWHPT                          *         
* OVERRIDE RATING - CALCULATE SHARE OVERRIDE                          *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 GETHPT,SPUPFLD1                                                  
*                                                                               
         SR    R1,R1               CREATE HOMES OVERRIDE                        
         ICM   R1,3,SPUPFLD1       GET NEW RHOMES                               
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         MVC   DUB(2),RHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,UORHOMES         COMPUTE HOMES INDEX                          
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPRTG2                                                           
         LR    RF,R1               R1 CONTAINS NEW RHOMES PERCENT               
         SR    RE,RE                                                            
         M     RE,UOUHOMES                                                      
         A     RF,=F'5'                                                         
         D     RE,=F'10'                                                        
         LR    R1,RF                                                            
UPRTG2   BAS   RE,GETINDEX                                                      
*                                                                               
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
*                                                                               
         SR    R1,R1               CALCULATE SHARE OVERRIDE                     
         ICM   R1,3,SPUPFLD1       GET NEW RATING HOMES                         
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALING FOR DIVISION TO DERIVE SHARE         
         L     R0,UOPHOMES         OLD HUT                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPRTG3                                                           
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPRTG3   BAS   RE,GETVALUE                                                      
         MVC   DUB(2),SHOMES                                                    
         MH    R1,=H'10'           SCALING FOR OVERRIDE ELEMENT                 
         STH   R1,DUB+2                                                         
*        BAS   RE,ADDOVER                                                       
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         MVI   INDEXUPG,C'Y'                                                    
         B     UPEND                                                            
         EJECT                                                                  
***********************************************************************         
* RATING/SHARE UPGRADE                                                *         
* ADJRTG = RTGINX * OLDRTG                                            *         
* OUTPUT = ADJRTG / ADJHPT / ADJRTG / NEWHPT                          *         
* OVERRIDE RATING/SHARE - CALCULATE HUT OVERRIDE                      *         
***********************************************************************         
         SPACE 1                                                                
UPRTGSHR GOTO1 GETHPT,SPUPFLD1                                                  
*                                                                               
         SR    R1,R1               CREATE HOMES OVERRIDE                        
         ICM   R1,3,SPUPFLD1       GET NEW RHOMES                               
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         MVC   DUB(2),RHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,UORHOMES         COMPUTE HOMES INDEX                          
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPRTSH2                                                          
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPRTSH2  BAS   RE,GETINDEX                                                      
*                                                                               
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
*                                                                               
         SR    R1,R1               CREATE SHARE OVERRIDE                        
         ICM   R1,3,SPUPFLD2       GET NEW SHARE                                
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         MVC   DUB(2),SHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         SR    R1,R1               CALCULATE NEW HUT OVERRIDE                   
         ICM   R1,3,SPUPFLD1       NEW RATING                                   
         CLI   PRECUPG,C'Y'                                                     
         BNE   *+12                                                             
         MH    R1,=H'10'                                                        
         B     *+8                                                              
         MH    R1,=H'100'                                                       
         LH    R0,DUB+2            NEW SHARE                                    
         BAS   RE,GETVALUE                                                      
         MVC   DUB(2),PHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
* ADJUST THE PUTS/TOTS                                                          
         LH    R1,DUB+2            NEW HUT                                      
         MVC   DUB(4),INDEX        SAVE CURRENT INDEX                           
         SR    R0,R0                                                            
         CLI   TAPEOPT,C'Y'        IMP BASED                                    
         BNE   UPRTSH3                                                          
         L     R1,UOPHOMES         GETS ADJUSTED BY UNIVERSE                    
         MH    R1,=H'100'                                                       
         D     R0,UOUHOMES                                                      
         SR    R0,R0                                                            
         AHI   R1,5                                                             
         D     R0,=F'10'                                                        
         LR    R0,R1               SLOT PROPERLY                                
         L     R1,DUB+4            RESTORE NEW                                  
         B     *+8                                                              
UPRTSH3  L     R0,UOPHOMES                                                      
         BAS   RE,GETINDEX                                                      
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDHPT),0,NEWHPT                                 
         GOTO1 CALC,DMCB,((R0),OLDTOT),0,NEWTOT                                 
         MVC   INDEX(4),DUB                                                     
         MVI   INDEXUPG,C'Y'                                                    
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
UPHPT    CLI   SPUPTYPE,SPUPTHPT   TEST HPT UPGRADE                             
         BE    UPHPT2                                                           
         CLI   SPUPTYPE,SPUPTHUT                                                
         BNE   UPNDX                                                            
         CLI   SPUPSTYP,C'P'       TEST PUT UPGRADE                             
         BNE   UPHUT                                                            
         SPACE 1                                                                
***********************************************************************         
* PUT UPGRADE                                                         *         
* NEWHPT = BOOK VALUES                                                *         
* ADJRTG = OLDSHR * NEWHPT                                            *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / NEWHPT                          *         
***********************************************************************         
         SPACE 1                                                                
UPHPT2   GOTO1 GETHPT,SPUPFLD1                                                  
*                                                                               
         CLI   SPUPTYPE,SPUPTHPT   TEST HPT UPGRADE                             
         BNE   UPPUT                                                            
*                                                                               
         SR    R0,R0               INDEX HPTS BY VALUE IN RAVLNOP3              
         ICM   R0,3,SPUPFLD3                                                    
         CLI   PRECUPG,C'Y'                                                     
         BNE   *+12                                                             
         MH    R0,=H'10'                                                        
         B     *+8                                                              
         MH    R0,=H'100'          SCALE TO 2 DEC PLACES                        
         ST    R0,INDEX                                                         
         LA    R0,NUMVALS*2                                                     
         GOTO1 CALC,DMCB,((R0),NEWHPT),0,NEWHPT                                 
*                                                                               
UPPUT    BAS   RE,NORMU                                                         
*                                                                               
UPPUTXX  LA    R0,NUMVALS*2        CALCULATE NEW RATINGS                        
*                                                                               
         CLC   SPUPFLD2,BOOK       IF IT'S A BOOK IGNORE                        
         BL    *+10                THAT'S WHAT HAPPENED WITH ANY VALUE          
         XC    SPUPFLD2,SPUPFLD2   BEFORE CODE BELOW                            
*                                                                               
         OC    SPUPFLD2,SPUPFLD2   ANY NEW SHARE                                
         BZ    UPPSH8              NO - ADJUST RATINGS/IMPS ONLY                
*                                                                               
         OC    HOMSHR,HOMSHR       ANY OLD SHARE                                
         BZ    UPPSH8              NO - ADJUST RATINGS/IMPS ONLY                
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,SPUPFLD2       GET NEW SHARE                                
         SR    RE,RE                                                            
         M     RE,=F'2000'                                                      
         D     RE,HOMSHR           INDEX TO OLD SHARE                           
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         LR    R1,RF                                                            
         LA    R0,NUMVALS*2        INDEX ALL OLD SHARES                         
         LA    R4,OLDSHR                                                        
UPPSH    SR    RE,RE                                                            
         L     RF,0(R4)            OLD SHARE                                    
         SLDA  RE,1                                                             
         MR    RE,R1               TIMES INDEX                                  
         D     RE,=F'1000'                                                      
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,0(R4)            EQUAL NEW SHARE                              
         LA    R4,4(R4)                                                         
         BCT   R0,UPPSH                                                         
         LA    R0,3                INDEX ALL OLD HOMES SHARES                   
         LA    R4,HOMSHR                                                        
UPPSH2   SR    RE,RE                                                            
         L     RF,0(R4)            OLD SHARE                                    
         SLDA  RE,1                                                             
         MR    RE,R1               TIMES INDEX                                  
         D     RE,=F'1000'                                                      
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,0(R4)            EQUAL NEW SHARE                              
         LA    R4,4(R4)                                                         
         BCT   R0,UPPSH2                                                        
*                                                                               
         ICM   RF,3,SPUPFLD2       NOW REPLACE OVERRIDEN SHARE                  
         ST    RF,HOMSHR                                                        
         LA    R0,NUMVALS*2        RESET FOR CALCS                              
*                                                                               
UPPSH8   GOTO1 CALC,DMCB,((R0),NEWHPT),OLDSHR,NEWRTG                            
         XC    INDEX,INDEX                                                      
         MVI   PUTUPGD,YES                                                      
         OC    SPUPFLD2,SPUPFLD2   ANY NEW SHARE                                
         BZ    UPEND               NO - EXIT                                    
*                                                                               
UPPSH9   MVC   OLDRTG(NUMVALS*4*2),NEWRTG    UPDATE THE SHARES                  
         MVC   OLDHPT(NUMVALS*4*2),NEWHPT                                       
         B     UPEND                                                            
         EJECT                                                                  
UPHUT    OC    SPUPFLD2,SPUPFLD2   TEST IF SHARE PRESENT                        
         BZ    *+14                                                             
         CLC   SPUPFLD2,BOOK       TEST SHARE GIVEN                             
         BL    UPHUTSHR            YES                                          
         SPACE 1                                                                
***********************************************************************         
* HUT ONLY UPGRADE                                                    *         
* NEWRTG(HMS) = OLDSHR * NEWHUT                                       *         
* ADJRTG      = RTGINX * OLDRTG                                       *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / OLDHPT                          *         
* OVERRIDE HUT - CALCULATE RATING OVERRIDE                            *         
***********************************************************************         
         SPACE 1                                                                
         SR    R1,R1                                                            
         ICM   R1,3,SPUPFLD1       GET HUT INDEX                                
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         CLC   SPUPFLD1,BOOK       TEST HUT = INDEX OR BOOK                     
         BL    UPHUT2                                                           
         GOTO1 GETHPT,SPUPFLD1     BOOK - GET NEW HUT VALUE                     
*                                                                               
         BAS   RE,NORMU                                                         
*                                                                               
         L     R1,UNPHOMES                                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPHUT2                                                           
         SR    R0,R0                                                            
         MH    R1,=H'100'                                                       
         D     R0,UOUHOMES                                                      
         AH    R1,=H'5'                                                         
         SR    R0,R0                                                            
         D     R0,=F'10'                                                        
*                                                                               
UPHUT2   MVC   DUB(2),PHOMES       CREATE OVERRIDE ELEMENT                      
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                  CALCULATE NEW RTGHMS AND OVRD ELEM           
         L     R0,HOMSHR           GET OLD HOMES SHARE                          
         MR    R0,R0                                                            
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         STH   R1,DUB+2            STORE NEW RATING HOMES                       
         MVC   DUB(2),RHOMES                                                    
*        BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,UORHOMES         GET OLD RATING HOMES                         
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPHUT3                                                           
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPHUT3   BAS   RE,GETINDEX                                                      
*                                                                               
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
         GOTO1 CALC,DMCB,((R0),OLDHPT),0,NEWHPT                                 
         GOTO1 CALC,DMCB,((R0),OLDTOT),0,NEWTOT                                 
         MVI   INDEXUPG,C'Y'                                                    
         CLI   TAPEOPT,C'Y'             IF IMP-BASED, KEEP                      
         BE    *+10                      ORIG HOME SHRS FOR HUT UPGD            
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
UPHUTSHR OC    SPUPFLD1,SPUPFLD1   TEST HUT PRESENT                             
         BZ    UPSHR                                                            
         SPACE 1                                                                
***********************************************************************         
* HUT/SHARE UPGRADE                                                   *         
* NEWRTG = NEWHUT * NEWSHR(HMS)                                       *         
* ADJRTG = RTGINX * OLDRTG                                            *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / NEWHPT                          *         
* OVERRIDE HUT/SHARE - CALCULATE RATING OVERRIDE                      *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 GETHPT,SPUPFLD1                                                  
*                                                                               
         SR    R1,R1               CREATE HUT OVERRIDE ELEMENT                  
         ICM   R1,3,SPUPFLD1       GET HUT INDEX                                
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         CLC   SPUPFLD1,BOOK       TEST HUT = INDEX OR BOOK                     
         BL    UPHUT7                                                           
         L     R1,UNPHOMES         BOOK - GET NEW HUT VALUE                     
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPHUT7                                                           
         SR    R0,R0                                                            
         MH    R1,=H'100'                                                       
         D     R0,UOUHOMES                                                      
         AH    R1,=H'5'                                                         
         SR    R0,R0                                                            
         D     R0,=F'10'                                                        
UPHUT7   MVC   DUB(2),PHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
         ST    R1,DUB+4            SAVE NEW HUT                                 
*                                                                               
         SR    R1,R1               CREATE SHARE OVERRIDE ELEMENT                
         ICM   R1,3,SPUPFLD2       GET NEW SHARE                                
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         MVC   DUB(2),SHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,DUB+4            RETRIEVE NEW HUT                             
         MR    R0,R0               COMPUTE NEW RATING HOMES                     
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         MVC   DUB(2),RHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,UORHOMES                                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPHUT8                                                           
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPHUT8   BAS   RE,GETINDEX                                                      
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
* ADJUST THE PUTS/TOTS                                                          
         MVC   DUB(4),INDEX        SAVE CURRENT INDEX                           
         SR    R0,R0                                                            
         L     R1,DUB+4            NEW HUT                                      
         CLI   TAPEOPT,C'Y'        IMP BASED                                    
         BNE   UPHUT9                                                           
         L     R1,UOPHOMES         GETS ADJUSTED BY UNIVERSE                    
         MH    R1,=H'100'                                                       
         D     R0,UOUHOMES                                                      
         SR    R0,R0                                                            
         AHI   R1,5                                                             
         D     R0,=F'10'                                                        
         LR    R0,R1               SLOT PROPERLY                                
         L     R1,DUB+4            RESTORE NEW                                  
         B     *+8                                                              
UPHUT9   L     R0,UOPHOMES                                                      
         BAS   RE,GETINDEX                                                      
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDHPT),0,NEWHPT                                 
         GOTO1 CALC,DMCB,((R0),OLDTOT),0,NEWTOT                                 
         MVC   INDEX(4),DUB                                                     
         MVI   INDEXUPG,C'Y'                                                    
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
***********************************************************************         
* SHARE ONLY UPGRADE                                                  *         
* NEWRTG = NEWSHR * OLDHUT                                            *         
* ADJRTG = RTGINX * OLDRTGS                                           *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / NEWHPT                          *         
* OVERRIDE SHARE - CALCULATE RATING OVERRIDE                          *         
***********************************************************************         
         SPACE 1                                                                
UPSHR    GOTO1 GETHPT,SPUPFLD1                                                  
*                                                                               
         SR    R1,R1               CREATE SHARE OVERRIDE                        
         ICM   R1,3,SPUPFLD2       GET NEW SHARE                                
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'           SCALE                                        
         MVC   DUB(2),SHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                  CALCULATE RATING HOMES OVERRIDE              
         L     R0,UOPHOMES         OLD HUT                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPSHR2                                                           
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPSHR2   MR    R0,R0               DEVELOP NEW RATING HOMES                     
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         MVC   DUB(2),RHOMES                                                    
         STH   R1,DUB+2            NEW RATING HOMES                             
         BAS   RE,ADDOVER                                                       
*                                                                               
         L     R0,UORHOMES         OLD RATING                                   
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPSHR4                                                           
         LR    RF,R0               CONVERT IMPS TO RATING XX.X                  
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         LR    R0,RF                                                            
UPSHR4   BAS   RE,GETINDEX         RATING INDEX                                 
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
         MVI   INDEXUPG,C'Y'                                                    
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
UPNDX    CLI   SPUPTYPE,SPUPTNDX                                                
         BNE   UPDEM                                                            
         SPACE 1                                                                
***********************************************************************         
* INDEX UPGRADE                                                       *         
* ADJRTG = INDEX * OLDRTG                                             *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / NEWHPT                          *         
* CALCULATE SHARE OVERRIDE                                            *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 GETHPT,SPUPFLD1                                                  
         CLC   SPUPFLD1,=H'100'    TEST INDEX=100                               
         BNE   UPNDX2                                                           
         MVC   NEWRTG(LENVALS*2),OLDRTG                                         
         XC    INDEX,INDEX                                                      
         B     UPEND                                                            
*                                                                               
UPNDX2   SR    R1,R1                                                            
         ICM   R1,3,SPUPFLD1       GET INDEX VALUE                              
         MH    R1,=H'100'          INDEX VALUE MUST BE TO 2 DEC. PLCS.          
         ST    R1,INDEX                                                         
*                                                                               
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
         MVI   INDEXUPG,C'Y'                                                    
*                                                                               
         L     R1,UNRHOMES         NEW RATING HOMES                             
         MH    R1,=H'10'           SCALING                                      
         L     R0,UOPHOMES         OLD HUT                                      
         BAS   RE,GETVALUE         CALCULATE NEW HOMES SHARE                    
         MVC   DUB(2),SHOMES                                                    
         STH   R1,DUB+2                                                         
*        BAS   RE,ADDOVER                                                       
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
UPDEM    CLI   SPUPTYPE,X'09'                                                   
         BE    UPMMU                                                            
         CLI   SPUPTYPE,X'0B'                                                   
         BE    UPVIX                                                            
         CLI   SPUPTYPE,X'0C'                                                   
         BE    UPVIX                                                            
         CLI   SPUPTYPE,X'0D'                                                   
         BE    UPVAX                                                            
         CLI   SPUPTYPE,X'0E'                                                   
         BE    UPVAX                                                            
         CLI   SPUPTYPE,C'A'                                                    
         BNL   *+6                                                              
         DC    H'0'                                                             
         CLI   SPUPTYPE,C'P'       TEST FOR PUT                                 
         BNE   UPDEM2              NO                                           
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,SPUPFLD1       R0=PUT                                       
         SR    R1,R1                                                            
         ICM   R1,3,SPUPFLD2       R1=SHARE                                     
         MR    R0,R0               RATING=PUT*SHARE                             
         CLI   PRECUPG,C'Y'                                                     
         BE    *+16                                                             
         AH    R1,=H'50'                                                        
         D     R0,=F'100'          SCALE THE RATING                             
         B     *+12                                                             
         AH    R1,=F'500'                                                       
         D     R0,=F'1000'         SCALE THE RATING                             
         STCM  R1,3,SPUPFLD1       PROCEED AS THOUGH RATING                     
         MVI   SPUPTYPE,C'R'       WERE INPUT FOR CELL                          
         SPACE 1                                                                
***********************************************************************         
* DEMO UPGRADE                                                        *         
* INDEX  = NEWDEMO / OLDDEMO                                          *         
* ADJRTG = INDEX * OLDRTG                                             *         
* OUTPUT = OLDRTG / OLDHPT / ADJRTG / NEWHPT                          *         
* OVERRIDE NEWDEMO - CALCULATE SHARE/HOMES RATING OVERRIDE            *         
***********************************************************************         
         SPACE 1                                                                
UPDEM2   XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR DEMAINT CALL                
         MVC   DBFILE,IUNVALS+1                                                 
         MVC   DBTAPEP,TAPEOPT                                                  
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         XC    0(50,R1),0(R1)                                                   
         LA    R0,DRFRSTEL+1-DRKEY                                              
         STCM  R0,3,DRRLEN-DRKEY(R1)                                            
         LA    R1,DRFRSTEL-DRKEY(R1)                                            
         ST    R1,DBAQUART                                                      
         MVC   DBCOMFCS,VCOMFACS                                                
         LA    R1,(UPRECX-UPREC)/4                                              
         STCM  R1,3,DBNUMVLS       SET DBNUMVLS TO MAXIMUM                      
         SLL   R1,2                                                             
         L     RF,AIO2                                                          
         MOVE  ((RF),(R1)),UPREC                                                
*                                                                               
         MVC   WORK(10),OFORMAT    BUILD DEMAINT OUTPUT FORMAT BLOCK            
         CLI   TAPEOPT,C'Y'        TAPE BASED ?                                 
         BNE   *+10                                                             
         MVC   WORK+7(2),BOOK9011  SWITCH FORMULA                               
*                                                                               
         GOTO1 VDEMAINT,DMCB,REP,DBLOCK,AIO2,WORK                               
         CLI   DBERROR,0                                                        
         BNE   UPERR                                                            
*                                  GET OLD DEMO VALUE INTO INDEX                
         GOTO1 VDEMOUT,DMCB,(C'D',SPUPTYPE-1),DBLOCK,INDEX                      
         CLI   DBERROR,0                                                        
         BNE   UPERR                                                            
*                                                                               
         GOTO1 GETHPT,BOOKZ                                                     
*                                                                               
         L     R0,INDEX            GET INDEX VALUE                              
         SR    R1,R1                                                            
         ICM   R1,3,SPUPFLD1                                                    
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'                                                        
         BAS   RE,GETINDEX                                                      
*                                                                               
         SR    R1,R1               CREATE DEMO OVERRIDE ELEMENT                 
         ICM   R1,3,SPUPFLD1                                                    
         CLI   PRECUPG,C'Y'                                                     
         BE    *+8                                                              
         MH    R1,=H'10'                                                        
         MVC   DUB(2),SPUPTYPE                                                  
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
*                                                                               
         LA    R0,NUMVALS          CALCULATE NEW IMPRESSIONS                    
         GOTO1 CALC,DMCB,((R0),OLDIMP),0,NEWIMP                                 
         MVC   NEWRTG(LENVALS),OLDRTG                                           
         MVI   INDEXUPG,C'Y'                                                    
*                                                                               
         L     R1,HOMSHR           CREATE SHARE OVERRIDE                        
         M     R0,INDEX                                                         
         A     R1,=F'5000'                                                      
         D     R0,=F'10000'                                                     
         MVC   DUB(2),SHOMES                                                    
         STH   R1,DUB+2                                                         
*        BAS   RE,ADDOVER                                                       
*                                                                               
         CLC   SPUPTYPE(2),RHOMES  CREATE RATING OVERRIDE                       
         BE    UPEND               UNLESS RHOMES WAS INPUT DEMO                 
         L     RF,UOPHOMES                                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   UPDEM3                                                           
         SR    RE,RE                                                            
         MH    RF,=H'100'                                                       
         D     RE,UOUHOMES                                                      
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
UPDEM3   MR    R0,RF                                                            
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         MVC   DUB(2),RHOMES                                                    
         STH   R1,DUB+2                                                         
         BAS   RE,ADDOVER                                                       
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         B     UPEND                                                            
         EJECT                                                                  
UPVIX    DS    0C                  VARIOUS PUT/SHARE INDEX TRENDS               
         MVI   Y4SSW,C'S'                                                       
         CLI   SPUPTYPE,12                                                      
         BE    *+8                                                              
         MVI   Y4SSW,C'P'                                                       
         B     UPVX                                                             
         SPACE 1                                                                
UPVAX    DS    0C                  VARIOUS PUT/SHARE AVERAGES                   
         MVI   Y4SSW,C'S'                                                       
         MVI   Y4ASW,C'Y'                                                       
         CLI   SPUPTYPE,14                                                      
         BE    *+8                                                              
         MVI   Y4SSW,C'P'                                                       
         SPACE 1                                                                
UPVX     GOTO1 GETHPT,BOOKZ                                                     
         GOTO1 VSUBR01,DMCB,('GETYRSE',(RC))                                    
         B     UPEND                                                            
*                                                                               
* METERED MARKET UPGRADE                                                        
* PUTS FROM A BOOK(UPFLD1) MULTIPLIED BY FACTOR FROM                            
* INDEX OF UPFLD2 AND THE PRIOR YEAR                                            
UPMMU    GOTO1 GETHPT,SPUPFLD1                                                  
         BAS   RE,NORMU                                                         
         MVC   SVUPFLD(6),SPUPFLD1                                              
         MVI   Y4ASW,C'N'                                                       
         MVI   Y4SSW,C'P'                                                       
         MVC   SPUPFLD1,SPUPFLD2                                                
         MVC   SPUPFLD2,=H'2'                                                   
         MVI   SPUPSTYP,C'Y'                                                    
         GOTO1 VSUBR01,DMCB,('GETYRSE',(RC))                                    
         MVC   SPUPFLD1(6),SVUPFLD                                              
         B     UPEND                                                            
         EJECT                                                                  
***********************************************************************         
* IUN RECORD VALUES HAVE NOW BEEN ADJUSTED BY UPGRADE ROUTINES.       *         
* REPLACE OLD DEMO ELEMENTS WITH IUN FORMAT DEMO ELEMENTS.            *         
* CREATE OVERRIDE ELEMENTS FOR ANY VALUE THAT WAS PASSED FROM CALLER. *         
* BOOK ELEMENT IS FORCED TO JUN/83 IF PUT UPGRADE TOOK PLACE.         *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
UPEND    OC    SPUPSPL,SPUPSPL     CLEAR IMPS/TOTS IF SPILL                     
         BZ    UPEND4                                                           
         XC    OLDIMP(LENVALS),OLDIMP                                           
         XC    OLDTOT(LENVALS),OLDTOT                                           
         XC    NEWIMP(LENVALS),NEWIMP                                           
         XC    NEWTOT(LENVALS),NEWTOT                                           
*                                                                               
UPEND4   XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR DEMAINT CALL                
         MVC   DBFILE,PAVVALS+1                                                 
         MVC   DBAREC,SPUPAREC                                                  
         MVC   DBAQUART,AFRSTEL                                                 
         MVC   DBCOMFCS,VCOMFACS                                                
         LA    R1,(UPRECX-UPREC)/4                                              
         STCM  R1,3,DBNUMVLS       SET DBNUMVLS TO MAXIMUM                      
         MVC   WORK(10),OFORMAT    BUILD DEMAINT OUTPUT FORMAT BLOCK            
*                                                                               
         CLI   INDEXUPG,C'Y'                                                    
         BNE   *+10                                                             
         MVC   WORK+7(2),BOOK8212  YES - SET BOOK TO DEC/82                     
*                                                                               
         CLI   PUTUPGD,YES         TEST IF PUT UPGRADE DONE                     
         BNE   *+10                                                             
         MVC   WORK+7(2),BOOK8306  YES - SET BOOK TO JUN/83                     
*                                                                               
         CLI   TAPEOPT,C'Y'        TAPE BASED ?                                 
         BNE   UPEND5                                                           
         MVC   WORK+7(2),BOOK9011  SWITCH FORMULA                               
*                                                                               
         CLI   INDEXUPG,C'Y'                                                    
         BNE   *+10                                                             
         MVC   WORK+7(2),BOOK9012  SWITCH TO INDEX FORMULA                      
*                                                                               
         CLI   PUTUPGD,YES         TEST IF PUT UPGRADE DONE                     
         BNE   *+10                                                             
         MVC   WORK+7(2),BOOK9106  YES - SET BOOK TO JUN/91                     
         B     UPEND6                                                           
*                                                                               
UPEND5   ZIC   RE,WORK+7                                                        
         ZIC   RF,SBKCTRL                                                       
         AR    RE,RF                                                            
         STC   RE,WORK+7                                                        
*                                                                               
*        UPGRADES DO A REP ON A TPT RECORD WHICH IS TOTALLY ILLEGAL             
*        THIS SECTION DEALS WITH THAT                                           
UPEND6   L     R1,DBAREC                                                        
         CLI   0(R1),C'R'          POSSIBLE TPT RECORD                          
         BNE   UPEND12             NO - LEAVE IT ALONE                          
         L     R1,DBAQUART         FORCE DEMEL TO INSERT DEMOS AT               
*                                  AT THIS QH                                   
UPEND7   CLI   0(R1),0                                                          
         BE    UPEND12                                                          
         ZIC   RE,1(R1)                                                         
         LA    RF,0(RE,R1)                                                      
         CLI   0(RF),X'5F'         60 AND ABOVE WILL STOP DEMAINT               
         BH    UPEND12             AT THE CORRECT SPOT                          
         CLC   0(1,RF),0(R1)       NEW SECTION                                  
         BL    UPEND8                                                           
         LR    R1,RF                                                            
         B     UPEND7                                                           
*                                                                               
UPEND8   CLI   0(RF),X'20'         ENSURE I HAVE ANOTHER QH                     
         BNE   UPEND12              NO-DON'T ZAP RECORD                         
UPEND9   MVI   0(RF),X'FF'         DELETE REMAINING ELEMENTS                    
         ZIC   RE,1(RF)                                                         
         AR    RF,RE                                                            
         CLI   0(RF),0                                                          
         BNE   UPEND9                                                           
*                                                                               
UPEND12  GOTO1 VDEMAINT,DMCB,REP,DBLOCK,UPREC,WORK                              
         CLI   DBERROR,0                                                        
         BNE   UPERR                                                            
*                                                                               
         MVC   DUB(2),NDXDEMO      ADD INDEX VALUE OVERRIDE ELEMENT             
         MVC   DUB+2(2),INDEX+2                                                 
         OC    INDEX,INDEX                                                      
         BZ    *+8                                                              
         BAS   RE,ADDOVER                                                       
*                                                                               
         SR    R0,R0               CREATE DEMO OVERRIDE ELEMENTS                
         ICM   R0,1,OVERLIST       R0=NUMBER OF OVERRIDE VALUES                 
         BZ    UPX                                                              
         LA    R2,OVERLIST+1       R2=A(OVERRIDE LIST)                          
         MVI   WORK+0,OVERELEM     DELETE OVERRIDE ELEMENTS                     
         MVI   WORK+1,6                                                         
*                                                                               
UPEND14  MVC   WORK+2(4),0(R2)     ADD OVERRIDE ELEMENTS                        
         GOTO1 VHELLO,DMCB,(C'P',DEMFILE),SPUPAREC,WORK,0                       
         LA    R2,4(R2)                                                         
         BCT   R0,UPEND14                                                       
         B     UPX                                                              
         EJECT                                                                  
NORMU    NTR1                                                                   
         CLI   TAPEOPT,C'Y'                                                     
         BNE   NORMUX                                                           
         CLI   NORMHPT,C'Y'        NORMALIZE HPT                                
         BNE   NORMUX                                                           
         LA    R0,NUMVALS          CALCULATE UNIVERSE INDEX                     
         LA    R1,LUNV                                                          
         LA    R7,UPUIDX                                                        
         LA    R8,OLDUNV                                                        
NORMU1   L     RF,0(R8)                                                         
         M     RE,=F'20000'                                                     
         OC    0(4,R1),0(R1)                                                    
         BZ    *+12                                                             
         D     RE,0(R1)                                                         
         B     *+8                                                              
         L     RF,=F'20000'                                                     
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         ST    RF,0(R7)                                                         
         LA    R8,4(R8)                                                         
         LA    R7,4(R7)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,NORMU1                                                        
*                                                                               
         LA    R0,NUMVALS          CALCULATE ADJUSTED HUTS/PUTS                 
         LA    R1,NEWHPT                                                        
         LA    R7,UPUIDX                                                        
NORMU2   L     RF,0(R1)                                                         
         M     RE,0(R7)                                                         
         SLDL  RE,1                                                             
         D     RE,=F'10000'                                                     
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         ST    RF,0(R1)                                                         
         LA    R7,4(R7)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,NORMU2                                                        
NORMUX   B     UPX                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET OLD OR NEW H/P/T VALUES FROM TIME PERIOD FILE        *         
* ON ENTRY HPT=OLD FOR OLD H/P/T OR NEW FOR NEW H/P/T WITH            *         
* R1=A(BOOK OPTION)                                                   *         
*                                                                     *         
* EXIT WITH H/P/T VALUES AT OLDHPT OR NEWHPT                          *         
***********************************************************************         
         SPACE 1                                                                
GETHPT   NTR1  WORK=(R8,GETHPTLN)                                               
         USING GETHPTWK,R8         R8=A(LOCAL W/S)                              
         TM    SPUPOPTS,SPOPEXT                                                 
         BZ    GETHPT0             EXTEND BLOCK NOT IN USE --NO IMS             
         ICM   RE,15,SPUPEXTN      IN MKT SHARE REQUEST?                        
         LTR   RE,RE                                                            
         BZ    GETHPT0             NO, REGULAR HUTS/PUT HANDLING                
         USING SPIMSD,RE                                                        
         CLC   SPIMSID,=C'IMS '    IN MKT SHARE REQUESTED?                      
         BE    *+12                                                             
         L     RE,SPIMSNXT         BUMP TO NEXT EXTN BLOCK                      
         B     *-20                                                             
         LA    RE,SPIMSAFF                                                      
         ST    RE,STNAFFL          SAVE ADDR OF STATION AFFL LIST               
         DROP  RE                                                               
         LR    R2,R1               PASS GETHPT PARM LIST                        
         GOTO1 VSUBR01,DMCB,('STNHPTS',(RC)),(R2)                               
         CLI   DBERROR,0           IF NO ERRORS, EXIT ELSE DEFAULT              
         BE    UPX                  TO REG LK UP W/OUT AN IMS                   
         LR    R1,R2               RESTORE PARM LIST                            
*                                                                               
GETHPT0  MVI   GETHIND,0           SET INDICATOR BYTE                           
         CLI   HPT,OLD             CLEAR REQUIRED H/P/T VALUES                  
         BNE   *+14                                                             
         XC    OLDHPT(LENVALS*2),OLDHPT                                         
         B     *+10                                                             
         XC    NEWHPT(LENVALS*2),NEWHPT                                         
*                                                                               
GETHPT1  XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR NEW H/P/T LOOKUP            
         MVC   DBFILE,TPTVALS+1                                                 
         MVC   DBTAPEP,TAPEOPT                                                  
         MVI   DBFUNCT,DBVLSTBK    VALIDATE STATION TO GET MARKET               
         CLC   0(2,R1),BOOK        TEST IF BOOK PASSED                          
         BNH   *+10                                                             
         MVC   DBSELBK,0(R1)       YES - SET SELECTED BOOK                      
         NI    DBSELBK+1,X'0F'     AND OFF WEEK BITS                            
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,VCOMFACS   **MUST BE DONE FOR MKT OVERRIDES**           
         MVC   DBSELAGY,SPUPAGY    RESTORE AGENCY                               
         MVC   DBSELUMK,SPUPMKT    SET AGENCY MKT CODE                          
         MVC   DBSELCLI,SPUPCLI    SET AGENCY CLIENT CODE                       
         MVC   DBSELMED,SPUPMED                                                 
         TM    SPUPOPTS,SPOPEXT                                                 
*&&DO                                                                           
         BZ    *+10                EXTEND BLOCK NOT IN USE                      
         MVC   DBEXTEND,SPUPEXTN    IN USE - SET IT                             
*&&                                                                             
         BZ    *+8                 EXTEND BLOCK NOT IN USE                      
         BAS   RE,INXTND            IN USE - INSERT IT                          
         TM    SPUPOPTS,SPOANGFR   CANADIAN ANGLO/FRANCO OPTION                 
         BZ    *+8                                                              
         MVI   DBFRANCO,C'Y'                                                    
         TM    SPUPOPT2,SPO2CMON   CANADIAN WEEKLIES AS MONTH                   
         BZ    *+8                                                              
         MVI   DBBEST,C'M'                                                      
         TM    SPUPOPT2,SPO2UBBM   CANADIAN USE BBM WEEKLIES                    
         BZ    *+8                                                              
         MVI   DBUSEBBM,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTY   SPORTS ONLY OPTION                           
         BZ    *+8                                                              
         MVI   DBSELSPO,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTN   EXCLUDE SPORTS OPTION                        
         BZ    *+8                                                              
         MVI   DBSELSPO,C'N'                                                    
*                                                                               
         OC    SPUPPUR,SPUPPUR     TEST DAY/TIME LOOK-UP                        
         BNZ   GETHPT2                                                          
         LA    R1,DBLOCK1          YES - GET VALUES FROM USER DBLOCK            
         MVC   DBSELSTA,DBSELSTA-DBLOCK(R1)                                     
         MVC   DBSELSRC,DBSELSRC-DBLOCK(R1)                                     
         MVC   DBBTYPE,DBBTYPE-DBLOCK(R1)                                       
         MVC   DUB(2),SPUPFBK                                                   
         NI    DUB+1,X'0F'         AND OFF WEEK BITS                            
         B     GETHPT4                                                          
*                                                                               
GETHPT2  L     R1,SPUPAREC                                                      
         MVC   DBSELSTA,PRSTAT-PRKEY(R1)                                        
         MVC   DBSELSRC,PRSRC-PRKEY(R1)                                         
         CLI   DBSELMED,C'N'                                                    
         BNE   *+10                                                             
         MVC   DBSELSRC,SPUPSRC                                                 
         MVC   DBBTYPE,PRBTYP-PRKEY(R1)                                         
         MVC   DUB(2),PRBOOK-PRKEY(R1)                                          
*                                                                               
GETHPT4  MVC   SVBTYPE,DBBTYPE     SAVE INITIAL BOOK TYPE                       
         CLI   DBBTYPE,C'O'        OLYMPIC BOOK                                 
         BNE   *+8                                                              
         CLI   HPT,NEW             AND NEW HPT                                  
         BNE   *+8                                                              
         MVI   DBBTYPE,0           KILL BOOK TYPE                               
         OC    DBSELBK,DBSELBK     TEST IF BOOK PASSED                          
         BNZ   *+10                                                             
         MVC   DBSELBK,DUB         NO - SET FROM INPUT RECORD                   
         MVC   DUB(2),DBSELBK      SAVE SELECTED BOOK VALUE                     
         LA    R0,2                                                             
         TM    GETHIND,X'80'       DON'T BACK-UP IF 2ND PUT LOOK-UP             
         BZ    *+8                                                              
         LA    R0,1                                                             
         OC    SPUPSPL,SPUPSPL     TEST SPILL MARKET GIVEN                      
         BZ    *+14                                                             
         MVC   DBSELRMK,SPUPSPL    YES - GET H/P/T'S FROM HERE                  
         B     GETHPT10                                                         
         OC    DBSELRMK,MARKET     TEST MARKET NUMBER KNOWN                     
         BNZ   GETHPT10            YES - GO GET DEMOS                           
*                                                                               
GETHPT6  GOTO1 VDEMAND,DMCB,DBLOCK,0 VALIDATE STATION/BOOK                      
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BNE   *+14                NO - EXTRACT MARKET NUMBER                   
         MVC   DBSELRMK,DBKEY+(BSRMKT-BSKEY)                                    
         B     GETHPT10                                                         
         CLI   HPT,OLD             TEST OLD H/P/T LINE REQUIRED                 
         BE    UPX                                                              
         CLI   DBERROR,X'10'       TEST FOR NOT FOUND                           
         BNE   GETHPT8                                                          
         BCT   R0,*+8              YES - TEST EST BOOK PROCESSED                
         B     GETHPT8                                                          
         ZIC   R1,DBSELBK          NO - DECREMENT BOOK & TRY AGAIN              
         BCTR  R1,0                                                             
         STC   R1,DBSELBK                                                       
         B     GETHPT6                                                          
*                                                                               
GETHPT8  MVI   DBFUNCT,DBVLST      STA/BOOK NOT FOUND TRY STATION ONLY          
         GOTO1 VDEMAND,DMCB,DBLOCK,0                                            
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BNE   GETHPT20                                                         
         MVC   DBSELRMK,DBACTRMK   NO - EXTRACT MARKET NUMBER                   
         MVC   DBSELBK,DUB         RESTORE SELECTED BOOK VALUE                  
*                                                                               
*ETHPT10 MVI   DBFUNCT,DBGETTTL   (DEFAULT CHANGED) <----DELETED---             
*        CLI   DBSELSRC,C'N'       TEST FOR NSI     <--------------             
*        BE    *+8                                  <--------------             
GETHPT10 MVI   DBFUNCT,DBGETTOT                                                 
*                                                                               
         CLC   DBSELSTA(4),=C'WWWB' FORCE TO LOUISVILLE                         
         BNE *+10                                                               
         MVC   DBACTRMK,=H'129'                                                 
*                                                                               
         CLI   PUREFLAG,YES        SET DAY/TIME FOR LOOK-UP                     
         BE    GETHPT12                                                         
         MVC   DBSELDAY,SPUPDAY    SET DAY/TIME FROM UPGRADE BLOCK              
         MVC   DBSELTIM,SPUPTIM                                                 
         CLI   HPT,NEW                                                          
         BE    GETHPT14                                                         
         LA    R1,DBLOCK1          SET DAY/TIME FROM DBLOCK1                    
         MVC   DBSELDAY,DBSELDAY-DBLOCK(R1)                                     
         MVC   DBSELTIM,DBSELTIM-DBLOCK(R1)                                     
         B     GETHPT14                                                         
*                                                                               
GETHPT12 MVC   DUB+0(1),PUREDW     EXTRACT DAY/TIME FROM PAV RECORD             
         GOTO1 VSUBR01,DMCB,('GETDAYQ',(RC))  PUT  DAY IN DEMAND FMT            
*        BAS   RE,GETDAY2          CONVERT DAY TO DEMAND FORMAT                 
         MVC   DBSELDAY,DUB+4                                                   
         MVC   DUB+0(1),PURESTIM                                                
         MVC   DUB+1(1),PUREDUR                                                 
         GOTO1 VSUBR01,DMCB,('GETTIMQ',(RC))  PUT  DAY IN DEMAND FMT            
*        BAS   RE,GETIME2          CONVERT TIME TO DEMAND FORMAT                
         MVC   DBSELTIM,DUB+4                                                   
*                                                                               
GETHPT14 DS    0H                                                               
*&&DO                                                                           
         CLI   DBBTYPE,C'O'        OLYMPIC BOOK                                 
         BNE   GETHPT16                                                         
         CLC   DBSELBK,=X'6202'    EXCEPT FOR FEB/98                            
         BE    GETHPT16                                                         
         CLC   DBSELBK,=X'6007'    AND JULY/96                                  
         BE    GETHPT16                                                         
         MVI   DBBTYPE,0           THERE IS NO OLYMPIC BOOK SCHMUCK             
*&&                                                                             
GETHPT16 MVC   DBTAPEP,TAPEOPT                                                  
         GOTO1 VDEMAND,DMCB,DBLOCK,GETHPTHK                                     
         MVC   DBBTYPE,SVBTYPE                                                  
         CLI   DBERROR,X'80'       TEST FOR E-O-F                               
         BNE   GETHPT20            NO - EXIT ON ANY ERROR SETTING               
         MVI   DBERROR,0                                                        
         OC    DBDIVSOR,DBDIVSOR                                                
         BZ    GETHPT20                                                         
*                                                                               
         CLI   HPT,OLD             TEST OLD HPT'S REQUIRED                      
         BE    GETHPT22            YES                                          
         CLI   SPUP2YRP,C'Y'       CALLER CAN SPECIFY 2 YEAR PUTS               
         BE    *+12                                                             
         TM    DBCPUTS,DBO2YEAR    TEST 2 YEAR AVERAGE PUT REQUIRED             
         BZ    GETHPT22            NO                                           
         TM    GETHIND,X'80'       TEST BOTH YEARS DONE                         
         BNZ   GETHPT18            YES                                          
         ZIC   R1,GETHBK           DECREMENT ACTUAL BOOK YEAR                   
         BCTR  R1,0                                                             
         STC   R1,GETHBK                                                        
         MVC   GETHDIV,DBDIVSOR    SAVE DBDIVSOR                                
         OI    GETHIND,X'80'                                                    
         LA    R1,GETHBK           POINT TO DECREMENTED BOOK VALUE              
         B     GETHPT1             GET PREVIOUS YEAR HPT VALUES                 
*                                                                               
GETHPT18 SR    R0,R0               CALCULATE TOTAL DBDIVSOR VALUE               
         ICM   R0,3,DBDIVSOR                                                    
         SR    R1,R1                                                            
         ICM   R1,3,GETHDIV                                                     
         AR    R0,R1                                                            
         STCM  R0,3,DBDIVSOR                                                    
         B     GETHPT22                                                         
*                                                                               
GETHPT20 MVC   DBDIVSOR,GETHDIV                                                 
         TM    GETHIND,X'80'                                                    
         BNZ   GETHPT22                                                         
         L     R1,APARM                                                         
         MVI   0(R1),X'FE'         RETURN NO H/P/T'S FOUND                      
         B     UPERR                                                            
*                                                                               
GETHPT22 LA    R0,NUMVALS*2        UNWEIGHT HPT VALUES                          
         LA    R1,OLDHPT           R1=A(OLD OR NEW HPT LINE)                    
         CLI   HPT,OLD                                                          
         BE    *+8                                                              
         LA    R1,NEWHPT                                                        
         ST    R1,DMCB+8                                                        
         SR    R2,R2                                                            
         GOTO1 VSUBR01,DMCB,('UNWGHTQ',(RC)),(R0),,(R2)                         
*        BAS   RE,UNWGHT           UNWEIGHT DEMO VALUES                         
         B     UPX                                                              
         SPACE 2                                                                
***********************************************************************         
* SUBROUTINE TO PROCESS HPT RECORDS FROM DEMAND                       *         
***********************************************************************         
         SPACE 1                                                                
GETHPTHK ST    RE,SAVERE           SAVE RETURN ADDRESS                          
         XC    GETHPTRT(LENVALS*2),GETHPTRT CLEAR RTG/IMPS                      
         XC    GETHPTS(LENVALS*2),GETHPTS   CLEAR HPTS                          
         XC    GETHPTUN(LENVALS),GETHPTUN  CLEAR UNIVERSES                      
*                                                                               
         TM    GETHIND,X'40'       TEST ACTUAL BOOK SET                         
         BNZ   *+14                                                             
         OI    GETHIND,X'40'                                                    
         MVC   GETHBK,DBACTBK                                                   
*                                                                               
         LH    R0,DBFACTOR         FORCE NO WEIGHTING IN GETIUN                 
         MVC   DBFACTOR,=H'1'                                                   
         GOTO1 VGETIUN,DMCB,(4,DBLOCK),GETHPTUN                                 
         STH   R0,DBFACTOR                                                      
*                                                                               
         LA    R0,NUMVALS*2                                                     
         LA    R1,GETHPTS          R1=A(UNWEIGHTED DEMO VALUES)                 
         LA    R2,OLDHPT           R2=A(WEIGHTED DEMO ACCUMULATORS)             
         CLI   HPT,OLD                                                          
         BE    GETHPTH2                                                         
         LA    R2,NEWHPT                                                        
         OC    LUNV(LENVALS),LUNV  TEST IF LOONEYVERSES THERE                   
         BNZ   *+10                YES                                          
         MVC   LUNV(LENVALS),GETHPTUN                                           
*                                                                               
GETHPTH2 DS    0H                                                               
         ST    R1,DMCB+8                                                        
         GOTO1 VSUBR01,DMCB,('WGHTUPQ',(RC)),(R0),,(R2)                         
*        BAS   RE,WGHTUP           WEIGHT & ACCUMULATE DEMOS                    
*        LA    RE,31*4(R2)         ZAP AD3564                                   
*        XC    0(4,RE),0(RE)                                                    
         L     RE,SAVERE           RETURN TO DEMAND FOR NEXT RECORD             
         BR    RE                                                               
*******  DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO INDEX 4 YEARS SHARES                                  *         
***********************************************************************         
         SPACE 1                                                                
***********************************************************************         
* SUBROUTINE TO CALCULATE NEW ROW VALUES                              *         
* P1=IN1,P2=IN2,P3=OUT                                                *         
* IF P2=0, P1 IS MULTIPLIED BY 'INDEX'                                *         
* IF P2 NOT 0, P1 VALUES ARE MULTIPLIED BY P2 VALUES                  *         
***********************************************************************         
         SPACE 1                                                                
CALC     NTR1                                                                   
         LM    R2,R4,0(R1)         R2=IN1,R3=IN2,R4=OUT                         
         SR    RE,RE               CLEAR INDEX REG                              
         ZIC   RF,0(R1)            RF=N'VALUES                                  
         LTR   R3,R3               TEST 2 ROWS SPECIFIED                        
         BNZ   CALC2                                                            
         SPACE 1                                                                
CALC1    L     R1,0(R2,RE)         ONE ROW SPECIFIED                            
         M     R0,INDEX            MULTIPLY IT BY VALUE IN INDEX                
         AH    R1,=H'5000'                                                      
         D     R0,=F'10000'                                                     
         ST    R1,0(R4,RE)                                                      
         LA    RE,4(RE)                                                         
         BCT   RF,CALC1                                                         
         B     UPX                                                              
         SPACE 1                                                                
CALC2    L     R1,0(R2,RE)         TWO ROWS SPECIFIED                           
         M     R0,0(R3,RE)         MULTIPLY CORRESPONDING VALUES                
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         ST    R1,0(R4,RE)                                                      
         LA    RE,4(RE)                                                         
         BCT   RF,CALC2                                                         
         B     UPX                                                              
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO COMPUTE INTEGER INDEX VALUE                           *         
* R0 HAS OLD VALUE - R1 HAS NEW VALUE                                 *         
* RETURN RESULT IN R1.                                                *         
***********************************************************************         
         SPACE 1                                                                
GETVALUE MH    R1,=H'100'                                                       
         LR    RF,R0               SAVE OLD VALUE                               
         SRL   R0,1                HALVE                                        
         AR    R0,R1                                                            
         SRDA  R0,32                                                            
         DR    R0,RF                                                            
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* SUBROUTINE TO COMPUTE INDEX VALUE TO 2 DECIMAL PLACES               *         
*                                                                     *         
* R0 HAS OLD VALUE - R1 HAS NEW VALUE                                 *         
* RETURN RESULT IN R1 AND IN 'INDEX'.                                 *         
***********************************************************************         
         SPACE 1                                                                
GETINDEX XC    INDEX,INDEX                                                      
         MH    R1,=H'10000'                                                     
         LTR   RF,R0               SAVE OLD VALUE                               
         BZR   RE                                                               
         SRL   R0,1                HALVE                                        
         AR    R0,R1                                                            
         SRDA  R0,32                                                            
         DR    R0,RF                                                            
         ST    R1,INDEX                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET OUTPUT VALUES FOR OVERRIDE DEMOS                     *         
*                                                                     *         
* NTRY - R2=A(OVERRIDE ELEMENT)                                       *         
*        RF=A(DEMO LIST ENTRY OF FIRST MODIFIER FOR OVERRIDE DEMO)    *         
*        R1=A(CORRESPONDING OUTPUT VALUE OF RF)                       *         
***********************************************************************         
         SPACE 1                                                                
SETOVER  NTR1  ,                                                                
         ST    RF,DUB+0                                                         
         ST    R1,DUB+4                                                         
         LA    R4,WORK             BUILD OVERRIDE ENTRY IN WORK                 
         MVC   0(1,R4),2(R2)                                                    
         XC    1(2,R4),1(R4)                                                    
         MVC   3(2,R4),4(R2)                                                    
         MVI   5(R4),X'FF'                                                      
         CLI   2(R2),C'S'          TEST 'SHARE' OVERRIDE                        
         BNE   SETOVERA                                                         
*                                  CELL UPGRADE FOR SHARE OVERRIDE              
         MVC   OVERDEM(OVERSHRL),OVERSHRS                                       
         LA    R1,OVERDEM-L'OVERDEM                                             
SETOVER2 LA    R1,3(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BE    *+14                                                             
         MVC   2(1,R1),3(R2)       INSERT DEMO NUMBER                           
         B     SETOVER2                                                         
         GOTO1 VDEMOUT,DMCB,(C'L',OVERDEM),DBLOCK,OVERDEMS                      
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BNE   SETOVERA                                                         
         L     R0,OVERSHR          R0=OLD SHARE VALUE                           
         SR    R1,R1                                                            
         ICM   R1,3,4(R2)          R1=NEW SHARE VALUE                           
         BAS   RE,GETINDEX         CALCULATE INDEX VALUE                        
         LA    R4,5(R4)            POINT TO NEXT OVERRIDE ENTRY                 
*                                                                               
         L     R1,OVERRTG          CALCULATE NEW RATING                         
         M     R0,INDEX                                                         
         AH    R1,=H'5000'                                                      
         D     R0,=F'10000'                                                     
         MVI   0(R4),C'R'                                                       
         STCM  R1,15,1(R4)                                                      
         LA    R4,5(R4)                                                         
*                                                                               
         L     R1,OVERIMP          CALCULATE NEW IMPRESSION                     
         M     R0,INDEX                                                         
         AH    R1,=H'5000'                                                      
         D     R0,=F'10000'                                                     
         MVI   0(R4),C'I'                                                       
         STCM  R1,15,1(R4)                                                      
         LA    R4,5(R4)                                                         
         MVI   0(R4),X'FF'                                                      
         B     SETOVERA                                                         
*                                                                               
SETOVERA LM    RE,RF,DUB           SLOT OVERRIDES INTO OUTPUT AREA              
SETOVERC CLI   0(RE),X'FF'         TEST E-O-L                                   
         BE    SETOVERX                                                         
         CLC   2(1,RE),3(R2)       MATCH ON DEMO                                
         BNE   SETOVERG                                                         
         CLI   0(RE),OVERELEM      IGNORE IF ALREADY AN OVERRIDE                
         BE    SETOVERG                                                         
         LA    R1,WORK             FIND OVERRIDE VALUE IN TABLE                 
SETOVERE CLI   0(R1),X'FF'                                                      
         BE    SETOVERG                                                         
         CLC   0(1,R1),1(RE)       MATCH ON MODIFIER                            
         BE    *+12                                                             
         LA    R1,5(R1)                                                         
         B     SETOVERE                                                         
         MVC   0(4,RF),1(R1)                                                    
         MVI   0(RE),OVERELEM      INDICATE THIS IS AN OVERRIDE                 
SETOVERG LA    RE,3(RE)            BUMP TO NEXT DEMO                            
         LA    RF,4(RF)                                                         
         B     SETOVERC                                                         
SETOVERX B     UPX                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALIZE DBLOCK FOR OLD DATA LOOK-UPS                  *         
***********************************************************************         
         SPACE 1                                                                
BLDBLK   NTR1                                                                   
         XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR RATING LOOK-UP              
         MVC   DBAREC,SPUPAREC                                                  
         MVC   DBCOMFCS,SPUPAFAC                                                
         MVC   DBFILE,TPTVALS+1    SET FILE                                     
         MVC   DBTAPEP,TAPEOPT                                                  
         CLC   SPUPFIL,PAVVALS                                                  
         BNE   *+10                                                             
         MVC   DBFILE,PAVVALS+1                                                 
         MVC   DBSELMED,SPUPMED                                                 
         MVC   DBSELSRC,SPUPSRC                                                 
         MVC   DBSELBK,SPUPFBK                                                  
         NI    DBSELBK+1,X'0F'     AND OFF WEEK BITS                            
         TM    SPUPOPTS,SPOANGFR   CANADIAN ANGLO/FRANCO OPTION                 
         BZ    *+8                                                              
         MVI   DBFRANCO,C'Y'                                                    
         TM    SPUPOPT2,SPO2CMON   CANADIAN WEEKLIES AS MONTH                   
         BZ    *+8                                                              
         MVI   DBBEST,C'M'                                                      
         TM    SPUPOPT2,SPO2UBBM   CANADIAN USE BBM WEEKLIES                    
         BZ    *+8                                                              
         MVI   DBUSEBBM,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTY   SPORTS ONLY OPTION                           
         BZ    *+8                                                              
         MVI   DBSELSPO,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTN   EXCLUDE SPORTS OPTION                        
         BZ    *+8                                                              
         MVI   DBSELSPO,C'N'                                                    
*                                                                               
         OC    SPUPFBKL,SPUPFBKL   TEST BOOK LIST                               
         BZ    BLDBLK1                                                          
         XC    MBEXTEND,MBEXTEND   YES-BUILD EXTENSION FOR MULTIPLE BKS         
         LA    R1,MBEXTEND                                                      
         ST    R1,DBEXTEND         A(EXTENDED DBLOCK)                           
         USING DBXMBD,R1                                                        
         MVC   DBXMBID,=C'MBKS'                                                 
         MVC   DBXMBKS(2),DBSELBK                                               
         MVC   DBXMBKS+2(L'SPUPFBKL),SPUPFBKL                                   
         XC    DBSELBK,DBSELBK                                                  
         DROP  R1                                                               
BLDBLK1  DS    0H                                                               
*                                                                               
         TM    SPUPOPTS,SPOP1DEC    ONE DECIMAL OPTION SET                      
         BZ    BLDBLK1Z             NO                                          
         XC    SPEXTEND,SPEXTEND                                                
         LA    R1,SPEXTEND                                                      
         OC    DBEXTEND,DBEXTEND    1ST EXTENDED ADDR LOADED?                   
         BNZ   BLDBLK1A                                                         
         ST    R1,DBEXTEND                                                      
         B     BLDBLK1D                                                         
BLDBLK1A DS    0H                                                               
         L     R2,DBEXTEND          POINT TO THE FIRST BLOCK                    
BLDBLK1B DS    0H                                                               
         OC    4(4,R2),4(R2)        LOOK AT ITS POINTER                         
         BZ    BLDBLK1C             ZERO MEANS WE CAN USE IT                    
         L     R2,4(R2)             ELSE, POINT TO NEXT                         
         B     BLDBLK1B             AND LOOP                                    
BLDBLK1C DS    0H                                                               
         ST    R1,4(R2)             POINT TO IT                                 
BLDBLK1D DS    0H                                                               
         USING DBXTTID,R1           SET VALUES FOR 1 DEC RETURN                 
         MVC   DBXTID(4),=C'SPOT'                                               
         MVI   DBXTTRP,X'01'                                                    
         MVI   DBXTTSP,X'01'                                                    
         MVI   DBXTTIP,X'02'                                                    
BLDBLK1Z DS    0H                                                               
*                                                                               
         PACK  DBSELWKN,SPUPFBK+1(1)                                            
         NI    DBSELWKN,X'0F'      AND OFF MONTH BITS                           
         MVC   DBSELSTA,SPUPSTA                                                 
         MVC   DBSELUMK,SPUPMKT    SET AGY MKT CODE FOR RTGSVC OVRD             
         MVC   DBSELMK,SPUPSPL                                                  
         MVC   DBSELAGY,SPUPAGY                                                 
         MVC   DBSELCLI,SPUPCLI                                                 
         MVC   DBBTYPE,SPUPBTYP                                                 
         MVC   DBSELDAT,=X'7F0C'   DEC/2027                                     
         TM    SPUPOPTS,SPOPEXT                                                 
*&&DO                                                                           
         BZ    *+10                EXTEND BLOCK NOT IN USE                      
         MVC   DBEXTEND,SPUPEXTN    IN USE - SET IT                             
*&&                                                                             
         BZ    *+8                 EXTEND BLOCK NOT IN USE                      
         BAS   RE,INXTND            IN USE - INSERT IT                          
         MVI   PUREFLAG,NO                                                      
         CLC   SPUPFIL,PAVVALS                                                  
         BNE   BLDBLK2                                                          
         MVC   DBSELPUR,SPUPPUR    SET PURE NUMBER                              
         MVC   DBBEST,SPUPBEST                                                  
         OC    DBSELPUR,DBSELPUR                                                
         BZ    BLDBLK2                                                          
         MVI   PUREFLAG,YES                                                     
         B     BLDBLKX                                                          
BLDBLK2  MVC   DBSELDAY,SPUPDAY    SET DAY/TIME VALUES                          
         MVC   DBSELTIM,SPUPTIM                                                 
         CLI   SPUPTPTT,C'P'       SET TP OR 4WK AVG                            
         BNE   *+8                                                              
         MVI   DBTPTT,C'P'                                                      
         OC    SPUPUDAY(5),SPUPUDAY TEST OVERRIDE DAY/TIME GIVEN                
         BZ    BLDBLKX                                                          
         MVC   DBSELDAY,SPUPUDAY   YES - USE THEM                               
         MVC   DBSELTIM,SPUPUTIM                                                
*                                                                               
BLDBLKX  DS    0H                                                               
         J     UPX                                                              
         EJECT                                                                  
* ADD AN OVERRIDE VALUE TO LIST OF DEMO OVERRIDE VALUES                         
*                                                                               
ADDOVER  ZIC   RF,OVERLIST         RF=NUMBER OF VALUES SO FAR                   
         LA    RF,1(RF)                                                         
         STC   RF,OVERLIST         SET NEW VALUE COUNT                          
         SLL   RF,2                                                             
         LA    RF,OVERLIST-3(RF)                                                
         MVC   0(4,RF),DUB         INSERT NEW ENTRY INTO LIST                   
         MVI   4(RF),X'FF'                                                      
         BR    RE                                                               
         DROP  R8                                                               
         EJECT                                                                  
* HELPER ROUTINE TO CALL THE ACTUAL INSERTING ROUTINE                           
                                                                                
INXTND   NTR1                                                                   
         GOTO1 VSUBR01,DMCB,('INXTNDQ',(RC))                                    
         J     XIT                                                              
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
MEDCAN   EQU   C'C'                                                             
NO       EQU   C'N'                                                             
YES      EQU   C'Y'                                                             
OLD      EQU   C'O'                                                             
NEW      EQU   C'N'                                                             
OVERELEM EQU   X'DE'                                                            
NUMVALS  EQU   32                                                               
DISPHOM  EQU   20                  FULLWORD DISPLACEMENT TO HOMES CELL          
LENVALS  EQU   NUMVALS*4                                                        
         SPACE 1                                                                
TPTVALS  DC    C'TTP '                                                          
PAVVALS  DC    C'PPAV'                                                          
IUNVALS  DC    C' IUN'                                                          
         SPACE 1                                                                
HOMEVUT  DC    X'81',C'V',AL1(1)   LIST OF HOMES VUTS (RTG/SHR)                 
         DC    X'81',C'V',AL1(2)                                                
         DC    X'81',C'V',AL1(3)                                                
         DC    X'FF'                                                            
         SPACE 1                                                                
HOMEPUT  DC    X'81',C'P',AL1(1)   LIST OF HOMES PUTS                           
         DC    X'81',C'P',AL1(2)                                                
         DC    X'81',C'P',AL1(3)                                                
         DC    X'FF'                                                            
         SPACE 1                                                                
NETPUTS  DC    X'00',C'P',AL1(1)   LIST OF HOMES PUTS (NETWORK)                 
         DC    X'00',C'P',AL1(2)                                                
         DC    X'00',C'P',AL1(3)                                                
         DC    X'FF'                                                            
         SPACE 1                                                                
OVERSHRS DC    X'00',C'S',X'00'    LIST OF DEMOS FOR SHARE OVERRIDES            
         DC    X'00',C'R',X'00'                                                 
         DC    X'00',C'I',X'00'                                                 
         DC    X'FF'                                                            
OVERSHRL EQU   *-OVERSHRS                                                       
         SPACE 1                                                                
RHOMES   DC    C'R',AL1(1)                                                      
PHOMES   DC    C'P',AL1(1)                                                      
SHOMES   DC    C'S',AL1(1)                                                      
         SPACE 1                                                                
DIV      DC    C'DIV'                                                           
MAD      DC    C'MAD'                                                           
MUL      DC    C'MUL'                                                           
REP      DC    C'REP'                                                           
PROGPLUS DC    C'PROG+'                                                         
         SPACE 1                                                                
DEMFILE  DC    C'DEMFILE '                                                      
OFORMAT  DC    C'PAVUIUN',AL1(82,11,00)                                         
BOOK8306 DC    AL1(83,06)          BOOK UNROUNDED BOTTOM LINE                   
BOOK9011 DC    AL1(90,11)          TAPE BASED NORMAL                            
BOOK9106 DC    AL1(91,06)          TAPE BASED UPGRADE                           
BOOK9012 DC    AL1(90,12)          TAPE BASED INDEXED                           
BOOK8212 DC    AL1(82,12)          BOOK BASED INDEXED                           
BOOK     DC    XL2'2000'                                                        
BOOKZ    DC    XL2'0000'           DUMMY ZERO BOOK                              
NDXDEMO  DC    C'&&',AL1(254)      INDEX DEMO MODIFIER/NUMBER                   
ADISPTAB DC    X'D0',3X'00',X'FF'                                               
         DS    0F                                                               
DEXTRA1  DC    C'SPOT',AL4(0),X'01',X'01',X'02',AL4(0)                          
         EJECT                                                                  
SUBR01   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**SR01**,RA,RR=RE                                              
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
         ST    R1,PARM01                                                        
         ST    RE,RELO2            SAVE PROGRAM RELOCATION FACTOR               
         ST    RB,MYBASE2                                                       
         L     R1,0(R1)                                                         
         SRL   R1,24               GET THE ROUTINE NUMBER                       
         SLL   R1,2                AND BRANCH ADDRESS                           
         LA    RF,*+2(R1)                                                       
         BR    RF                                                               
GETYRSE  EQU   (GETYRS#-*)/4+1                                                  
GETDAYQ  EQU   (GETDAY#-*)/4+1                                                  
GETTIMQ  EQU   (GETTIM#-*)/4+1                                                  
UNWGHTQ  EQU   (UNWGHT#-*)/4+1                                                  
WGHTUPQ  EQU   (WGHTUP#-*)/4+1                                                  
STNHPTS  EQU   (STNHPT#-*)/4+1                                                  
INXTNDQ  EQU   (INXTND#-*)/4+1                                                  
         SPACE 1                                                                
GETYRS#  B     GETYRS              GET MULTI BOOK INDEXES                       
GETDAY#  B     GETDAY              GET DAY IN DEMAND FORMAT                     
GETTIM#  B     GETTIME             CONVERT QHR TO MILITARY ST & END             
UNWGHT#  B     UNWGHT              UNWEIGHT AND ACCUM DEMOS                     
WGHTUP#  B     WGHTUP              WEIGHT AND ACCUM DEMOS                       
STNHPT#  B     STNHPT              PROCESS IN MARKET SHARES                     
INXTND#  B     INSXTND             INSERT INTO EXTENSION LIST                   
         EJECT                                                                  
ERR01    L     RD,SAVERD           ENTER HERE ON ERROR                          
XIT01    XIT1                      EXIT SUBR01  -- RTN TO CALLER                
*                                                                               
GETYRS   DS    0C                                                               
         L     RE,AIO2                                                          
         LH    RF,=Y(IOAREA2X-IOAREA2)                                          
         XCEF                                                                   
         XC    BOOKLIST,BOOKLIST                                                
         SR    R0,R0                                                            
         ICM   R0,3,SPUPFLD2                                                    
         STC   R0,Y4SYRS                                                        
*                                                                               
         CLI   SPUPSTYP,C'M'       INDEX MULTIPLY                               
         BNE   *+12                                                             
         MVI   Y4MSW,C'Y'                                                       
         B     *+12                                                             
         CLI   SPUPSTYP,C'N'       INDEX AVERAGE                                
         BNE   GETYRS0                                                          
         MVI   Y4SYRS,2                                                         
         MVC   BOOKLIST(6),SPUPFLD1                                             
         OC    BOOKLIST+2(2),BOOKLIST+2                                         
         BZ    UPX2                                                             
         OC    BOOKLIST+4(2),BOOKLIST+4                                         
         BZ    GETYRS0                                                          
         MVI   Y4SYRS,3                                                         
*                                                                               
GETYRS0  XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR NEW H/P/T LOOKUP            
         XC    DUB,DUB             CLEAR BOOK VALUES AREA                       
GETYRS1  XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR NEW H/P/T LOOKUP            
*        MVC   DBFILE,TPTVALS+1                                                 
         L     RF,ATPTVALS                                                      
         MVC   DBFILE(L'DBFILE),1(RF)                                           
         MVC   DBTAPEP,TAPEOPT                                                  
         MVI   DBFUNCT,DBVLSTBK    VALIDATE STATION TO GET MARKET               
*        CLC   0(2,R1),BOOK        TEST IF BOOK PASSED                          
*        BNH   *+10                                                             
*        MVC   DBSELBK,0(R1)       YES - SET SELECTED BOOK                      
*        NI    DBSELBK+1,X'0F'     AND OFF WEEK BITS                            
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         TM    SPUPOPTS,SPOPEXT                                                 
*&&DO                                                                           
         BZ    *+10                EXTEND BLOCK NOT IN USE                      
         MVC   DBEXTEND,SPUPEXTN    IN USE - SET IT                             
*&&                                                                             
         BZ    GETYRSG             EXTEND BLOCK NOT IN USE                      
         GOTO1 VSUBR01,DMCB,('INXTNDQ',(RC))   IN USE - INSERT IT               
GETYRSG  EQU   *                                                                
         MVC   DBCOMFCS,VCOMFACS   **MUST BE DONE FOR MKT OVERRIDES**           
         MVC   DBSELAGY,SPUPAGY    RESTORE AGENCY                               
         MVC   DBSELUMK,SPUPMKT    SET AGENCY MKT CODE                          
         MVC   DBSELCLI,SPUPCLI    SET AGENCY CLIENT CODE                       
         MVC   DBSELMED,SPUPMED                                                 
         TM    SPUPOPTS,SPOANGFR   CANADIAN ANGLO/FRANCO OPTION                 
         BZ    *+8                                                              
         MVI   DBFRANCO,C'Y'                                                    
         TM    SPUPOPT2,SPO2CMON   CANADIAN WEEKLIES AS MONTH                   
         BZ    *+8                                                              
         MVI   DBBEST,C'M'                                                      
         TM    SPUPOPT2,SPO2UBBM   CANADIAN USE BBM WEEKLIES                    
         BZ    *+8                                                              
         MVI   DBUSEBBM,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTY   SPORTS ONLY OPTION                           
         BZ    *+8                                                              
         MVI   DBSELSPO,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTN   EXCLUDE SPORTS OPTION                        
         BZ    *+8                                                              
         MVI   DBSELSPO,C'N'                                                    
         CLI   SPUPTPTT,C'P'       SET TP/TT                                    
         BNE   *+8                                                              
         MVI   DBTPTT,C'P'                                                      
         MVC   DBSELBK,SPUPFLD1                                                 
*                                                                               
         OC    SPUPPUR,SPUPPUR     TEST DAY/TIME LOOK-UP                        
         BNZ   GETYRS2                                                          
         LA    R1,DBLOCK1          YES - GET VALUES FROM USER DBLOCK            
         MVC   DBSELSTA,DBSELSTA-DBLOCK(R1)                                     
         MVC   DBSELSRC,DBSELSRC-DBLOCK(R1)                                     
         MVC   DBBTYPE,DBBTYPE-DBLOCK(R1)                                       
         MVC   DUB(2),SPUPFBK                                                   
         NI    DUB+1,X'0F'         AND OFF WEEK BITS                            
         MVC   DBSELDAY,DBSELDAY-DBLOCK(R1)                                     
         MVC   DBSELTIM,DBSELTIM-DBLOCK(R1)                                     
         B     GETYRS4                                                          
*                                                                               
GETYRS2  L     R1,SPUPAREC                                                      
         MVC   DBSELSTA,PRSTAT-PRKEY(R1)                                        
         MVC   DBSELSRC,PRSRC-PRKEY(R1)                                         
         MVC   DBBTYPE,PRBTYP-PRKEY(R1)                                         
         MVC   DUB(2),PRBOOK-PRKEY(R1)                                          
*                                                                               
GETYRS4  OC    DBSELBK,DBSELBK     TEST IF BOOK PASSED                          
         BNZ   *+10                                                             
         MVC   DBSELBK,DUB         NO - SET FROM INPUT RECORD                   
         MVC   DUB(2),DBSELBK      SAVE SELECTED BOOK VALUE                     
*        LA    R0,2                                                             
*        TM    GETHIND,X'80'       DON'T BACK-UP IF 2ND PUT LOOK-UP             
*        BZ    *+8                                                              
*        LA    R0,1                                                             
         OC    SPUPSPL,SPUPSPL     TEST SPILL MARKET GIVEN                      
         BZ    *+14                                                             
         MVC   DBSELRMK,SPUPSPL    YES - GET H/P/T'S FROM HERE                  
         B     GETYRS10                                                         
         OC    DBSELRMK,MARKET     TEST MARKET NUMBER KNOWN                     
         BNZ   GETYRS10            YES - GO GET DEMOS                           
*                                                                               
*                                                                               
GETYRS7  DS    0C                                                               
GETYRS10 DS    0C                                                               
*                                                                               
GETYRS11 MVI   DBFUNCT,DBGETDEM                                                 
         L     R1,AIO2             BUILD A DUMMY DEMO RECORD IN IO2             
         XC    0(50,R1),0(R1)                                                   
*                                                                               
GETYRS16 XC    WORK(20),WORK       BUILD DEMOMATH FORMAT BLOCK                  
         ST    R7,SAVER7                                                        
         LA    R1,DBLOCK                                                        
         ST    R1,WORK                                                          
         MVC   WORK+8(3),DBFILE                                                 
         MVC   WORK+11(3),WORK+8                                                
         MVC   WORK+14(1),DBACTSRC                                              
         MVC   DBTAPEP,TAPEOPT                                                  
*        LA    R0,4                SET FOR 4 BOOKS                              
         ZIC   R0,Y4SYRS           SET FOR N BOOKS                              
         CLI   Y4SYRS,2                                                         
         BL    UPX2                                                             
         XC    DUB(4),DUB          DBDIVSOR COUNTER                             
*                                  CALL DEMAND TO GET H/P/T DATA                
*                                  RECORDS ARE PROCESSED AT GETYRS18            
GETYRS17 OC    BOOKLIST(2),BOOKLIST BOOK LIST GIVEN                             
         BZ    GTYRS17B                                                         
         ZIC   RE,BOOKI             YES - INDEX THROUGH IT                      
         LR    RF,RE                                                            
         LA    RF,1(RF)                                                         
         STC   RF,BOOKI                                                         
         SLL   RE,1                                                             
         LA    RE,BOOKLIST(RE)                                                  
         OC    0(2,RE),0(RE)                                                    
         BZ    GETYRS20                                                         
         MVC   DBSELBK,0(RE)                                                    
GTYRS17B GOTO1 VDEMAND,DMCB,DBLOCK,GETYRS18                                     
         CLI   DBERROR,X'80'       TEST FOR E-O-F                               
         BNE   UPX2                NO - EXIT ON ANY ERROR SETTING               
         OC    DUB(4),DUB          NOTHING FOUND                                
         BZ    UPX2                                                             
         MVI   DBERROR,0                                                        
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         LA    RF,Y2SHR-Y1SHR                                                   
         MR    RE,RE                                                            
         L     RE,AIO2                                                          
         LA    RE,Y1SHR-IOAREA2(RE)                                             
         AR    RE,RF               POINT TO SHARE SLOT                          
         LA    R4,NEWRTG-OLDUNV                                                 
         LA    R5,NEWHPT-OLDUNV                                                 
         L     R6,AIO2                                                          
         AR    R4,R6               RATINGS                                      
         AR    R5,R6               PUTS                                         
         LA    R1,NUMVALS*2                                                     
*                                                                               
         CLI   Y4SSW,C'S'          SHARES NEED PRECISION                        
         BE    SLOTSHR              THIS IS CONFUSING BUT NEEDED                
*                                                                               
         CLI   DBTAPEP,C'Y'        IF RATING BASED ITS OK                       
         BNE   SLOTSHR                                                          
         LA    R1,NUMVALS                                                       
         LR    RF,R6                                                            
CALCPCT  DS    0H                  GET RATING/PUT PERCENTS                      
         OC    0(4,RF),0(RF)        SO USER CAN TRACK WHATS GOING ON            
         BZ    CALCPCT2                                                         
*                                                                               
         L     R7,0(RF)                *                                        
         SR    R6,R6                   *                                        
         A     R7,=F'5'                *                                        
         D     R6,=F'10'               *                                        
         MH    R7,=H'10'               *                                        
         ST    R7,0(RF)                *                                        
*                                                                               
         L     R7,0(R4)            RATINGS                                      
         M     R6,=F'10'                                                        
         SLDA  R6,1                                                             
         D     R6,0(RF)                                                         
         A     R7,=F'1'                                                         
         SRL   R7,1                                                             
         ST    R7,0(R4)                                                         
*                                                                               
         L     R7,0(R5)            PUTS                                         
         M     R6,=F'10'                                                        
         SLDA  R6,1                                                             
         D     R6,0(RF)                                                         
         A     R7,=F'1'                                                         
         SRL   R7,1                                                             
         ST    R7,0(R5)                                                         
CALCPCT2 LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         LA    RF,4(RF)                                                         
         BCT   R1,CALCPCT                                                       
*                                                                               
         LA    R4,NEWRTG-OLDUNV    RESET FOR AVERAGING                          
         LA    R5,NEWHPT-OLDUNV                                                 
         L     R6,AIO2                                                          
         AR    R4,R6               RATINGS                                      
         AR    R5,R6               PUTS                                         
         LA    R1,NUMVALS*2                                                     
*                                                                               
SLOTSHR  L     R7,0(R4)            DO THE AVERAGES FOR RTGS                     
         SR    R6,R6                SO I USE THE VALUES SHOWN BY APPS           
         SLDA  R6,1                                                             
         D     R6,DUB                                                           
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,0(R4)                                                         
*                                                                               
         L     R7,0(R5)            DO THE AVERAGES FOR PUTS                     
         SR    R6,R6                                                            
         SLDA  R6,1                                                             
         D     R6,DUB                                                           
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,0(R5)                                                         
*                                                                               
         L     R7,0(R4)            CALC THE SHARE AND TSA SHARE                 
         LA    R6,2000                                                          
         MR    R6,R6                                                            
         ICM   R8,15,0(R5)                                                      
         BNZ   *+10                                                             
         SR    R7,R7                                                            
         B     *+6                                                              
         DR    R6,R8                                                            
         A     R7,=F'1'                                                         
         SRA   R7,1                                                             
         ST    R7,0(RE)            SAVE IN YEAR SLOT                            
         CLI   Y4SSW,C'P'          DOING A PUT INDEX                            
         BNE   *+10                                                             
         MVC   0(4,RE),0(R5)       SAVE THE PUTS - NOT THE SHARES               
         LA    R7,4                SET UP NEXT SLOT                             
         AR    R4,R7                                                            
         AR    R5,R7                                                            
         AR    RE,R7                                                            
         BCT   R1,SLOTSHR          DO 'EM ALL                                   
*                                                                               
         CLI   Y4SSW,C'S'          ONLY IF WE'RE SAVING THE SHARES              
         BNE   SLOTSHR3X                                                        
         SHI   RE,OLDRTGLN         GO BACK AND SEED ORIG HOME SHARES            
         AHI   RE,(DISPHOM*4)       RE-->SLOTS FOR HOMES VALUES                 
         LA    R5,TOTHMSHR          ACCUMULATED ORIG HOME SHARES                
         LHI   R1,(HOMSHRLN/4)      LOOP COUNTER                                
SLOTSHR3 L     R7,0(R5)                                                         
         SR    R6,R6                                                            
         SLDA  R6,1                                                             
         D     R6,DUB               UNWEIGH                                     
         AHI   R7,1                                                             
         SRA   R7,1                                                             
         ST    R7,0(RE)                                                         
         AHI   R5,4                                                             
         AHI   RE,4                                                             
         BCT   R1,SLOTSHR3                                                      
SLOTSHR3X EQU  *                                                                
*                                                                               
         L     R7,SAVER7                                                        
         L     RE,AIO2             RESET FOR NEXT YEAR                          
         LA    RF,IOAREA2X-IOAREA2                                              
         XCEF                                                                   
         XC    DUB,DUB                                                          
         XC    TOTHMSHR,TOTHMSHR                                                
         ZIC   RE,DBSELBK          DECREMENT THE YEAR                           
         BCTR  RE,0                                                             
         STC   RE,DBSELBK                                                       
         BCT   R0,GETYRS17         REPEAT FOR N PREV YEARS                      
         B     GETYRS20                                                         
*                                  ADD DEMO RECORD INTO COMPOSITE               
GETYRS18 NTR1                                                                   
*---->   L     R1,DBAQUART         IGNORE 2 WEEK ONLY DATA                      
*---->   TM    QHWKS-QHELEM(R1),X'10'                                           
*---->   BNZ   UPX2                                                             
         GOTO1 VGETIUN,DMCB,(4,DBLOCK),AIO2                                     
         LA    R0,(OLDHPTX-OLDRTG)/4                                            
         L     R4,AIO2                                                          
         LA    R4,OLDRTG-OLDUNV(R4)    POINT TO CURR RATINGS                    
         LA    R5,NEWRTG-OLDRTG        AND OFFSET TO SUMMED RATINGS             
         AR    R5,R4                                                            
GETYRSML L     RF,0(R4)                                                         
         ST    RF,0(R5)                                                         
         LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         BCT   R0,GETYRSML                                                      
         LH    R1,DBFACTOR                                                      
         A     R1,DUB                                                           
         ST    R1,DUB                                                           
*                                                                               
         DS    0H                  GET ORIGINAL HOME SHARES FROM RECD           
         MVC   SVDBACBK,DBACTBK                                                 
         L     R4,AIO2                                                          
         AHI   R4,(HOMSHR-UPREC)    R4-->OUTPUT AREA                            
         XC    0(HOMSHRLN,R4),0(R4)                                             
         OC    DBAQUART,DBAQUART                                                
         BZ    GETYRSSHX                                                        
         GOTO1 VDEMOUT,DMCB,(C'P',DEMOSHR),DBLOCK,(R4),0                        
         MVC   DBACTBK,SVDBACBK                                                 
         LHI   R0,3                                                             
         LA    R5,TOTHMSHR                                                      
GETYRSSHG DS   0H                                                               
         L     RF,0(R4)                                                         
         MH    RF,DBFACTOR                                                      
         A     RF,0(R5)                                                         
         ST    RF,0(R5)                                                         
         AHI   R4,4                                                             
         AHI   R5,4                                                             
         BCT   R0,GETYRSSHG                                                     
GETYRSSHX EQU  *                                                                
         B     UPX2                                                             
         SPACE 2                                                                
*       CALCULATE THE YEAR TO YEAR INDEXES                                      
GETYRS20 L     RE,AIO2                                                          
         LA    RE,Y1SHR-IOAREA2(RE)                                             
         ST    RE,DMCB                                                          
         ST    RE,DMCB+8           SAVE SUM AREA ADDR                           
         LR    R5,RE               SET SUM AREA                                 
         LA    RF,Y2SHR-Y1SHR(RE)                                               
         MVI   DMCB+4,1            FIRST TIME SWITCH                            
         ZIC   R0,Y4SYRS                                                        
         BCTR  R0,0                                                             
         LA    R1,(Y2SHR-Y1SHR)/4                                               
GETYRS21 LA    R6,2000             CALC THE YR TO YR INDEXES                    
         L     R7,0(RF)                                                         
         CLI   Y4ASW,C'Y'          DOING AVERAGE NOT INDEX                      
         BE    GETYRA21                                                         
         MR    R6,R6                                                            
         OC    0(4,RE),0(RE)                                                    
         BNZ   *+12                                                             
         LA    R7,2000                                                          
         B     *+8                                                              
         D     R6,0(RE)                                                         
         A     R7,=F'1'                                                         
         SRA   R7,1                                                             
         CLI   DMCB+4,1            IF NOT FIRST TIME                            
         BE    GETYRM21                                                         
GETYRA21 DS    0C                  SUM THE INDEXES OR VALUES                    
         CLI   Y4MSW,C'Y'          OR MULIPLY THEM                              
         BNE   *+12                                                             
         MH    R7,2(R5)            MULT FOR GEOMETRIC AVERAGES                  
         B     *+8                                                              
         A     R7,0(R5)            SUM THE INDEXES OR VALUES                    
GETYRM21 ST    R7,0(R5)                                                         
         LA    R5,4(R5)            SUM AREA                                     
         LA    RE,4(RE)            DENOMINATOR                                  
         LA    RF,4(RF)            NUMERATOR                                    
         BCT   R1,GETYRS21                                                      
         L     R7,SAVER7                                                        
         L     R5,DMCB+8           SUM AREA                                     
         MVI   DMCB+4,0            RESET FIRST TIME                             
         LA    R1,(Y2SHR-Y1SHR)/4  SET UP FOR THE NEXT SET                      
         L     RE,DMCB                                                          
         LA    RE,Y2SHR-Y1SHR(RE)                                               
         ST    RE,DMCB                                                          
         LA    RF,Y2SHR-Y1SHR(RE)                                               
         BCT   R0,GETYRS21                                                      
         SPACE 2                                                                
*        NOW AVERAGE THE INDEXES                                                
         L     RE,DMCB+8           SUM AREA                                     
         LA    R1,(Y2SHR-Y1SHR)/4  SET UP FOR THE NEXT SET                      
         ZIC   R7,Y4SYRS                                                        
         CLI   Y4ASW,C'Y'          AVERAGES                                     
         BE    *+6                                                              
         BCTR  R7,0                INDEXES 1 LESS                               
         ST    R7,DMCB                                                          
*                                                                               
         CLI   Y4MSW,C'Y'          GEOMETRIC INDEX                              
         BNE   GETYRS22                                                         
         LA    R6,1                                                             
         B     *+8                                                              
         MH    R6,=H'1000'         DETERMINE THE DIVISOR                        
         BCT   R7,*-4                                                           
         ST    R6,DMCB                                                          
*                                                                               
GETYRS22 L     R7,0(RE)            SUM OF INDEXES/AVERAGES                      
         SR    R6,R6                                                            
         SLDA  R6,1                                                             
         D     R6,DMCB             DIVIDE BY YEARS                              
         A     R7,=F'1'                                                         
         SRA   R7,1                                                             
         ST    R7,0(RE)                                                         
         LA    RE,4(RE)                                                         
         BCT   R1,GETYRS22                                                      
         L     R7,SAVER7                                                        
         CLI   Y4SSW,C'P'                                                       
         BE    GETYRS30                                                         
         SPACE 2                                                                
*        NOW INDEX THE CURRENT VALUES TO THIS                                   
*        1. CALCULATE THE OLD SHARE                                             
*        2. INDEX THE OLD SHARE                                                 
*        3. OLD PUT X INDEXED SHARE = OLD RATING/IMP                            
*        3. NEW PUT X INDEXED SHARE = NEW RATING/IMP                            
*                                                                               
         L     RE,AIO2                                                          
         LA    RE,Y1SHR-IOAREA2(RE)                                             
         L     RF,AIO2                                                          
         AHI   RF,(OLDRTG-UPREC)                RF-->OLD RTG WORK AREA          
         LA    R8,(OLDHPT-OLDRTG)(RF)           R8-->OLD HPT WORK AREA          
         MVC   0(OLDRTGLN,RF),OLDRTG                                            
         MVC   0(OLDHPTLN,R8),OLDHPT                                            
         MVC   (DISPHOM*4)(HOMSHRLN,RF),HOMSHR  FUDGE TO USE ORIGINAL           
         LHI   R1,1000                                                          
         STCM  R1,15,((DISPHOM+0)*4)(R8)         HOMES SHARES IN                
         STCM  R1,15,((DISPHOM+1)*4)(R8)         INDEXING                       
         STCM  R1,15,((DISPHOM+2)*4)(R8)         AND CALCULATING                
         LA    R0,(OLDRTGX-OLDRTG)/4                                            
         SR    R1,R1                                                            
*                                                                               
GETYRS23 CLI   Y4ASW,C'Y'          AVERAGE NOT INDEX                            
         BNE   *+12                                                             
         L     R7,0(RE)                                                         
         B     GETYRA23                                                         
*                                                                               
         SR    R6,R6                                                            
         LHI   R7,2000                                                          
         M     R6,0(RF)            OLDRTG                                       
         OC    0(4,R8),0(R8)                                                    
         BNZ   *+12                                                             
         LA    R7,1                                                             
         B     *+8                                                              
         D     R6,0(R8)            OLDHPT (SHARE CALC)                          
         AH    R7,=H'1'                                                         
         SRL   R7,1                                                             
         SR    R6,R6                                                            
         M     R6,0(RE)            NOW INDEX IT                                 
         SLDA  R6,1                                                             
         D     R6,=F'1000'                                                      
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
GETYRA23 ST    R7,DMCB             SAVE INDEXED OR AVG SHARE                    
         SR    R6,R6                                                            
         M     R6,OLDHPT(R1)       OLDHPT X INDEXED SHARE                     
         SLDA  R6,1                                                             
         D     R6,=F'1000'                                                      
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,OLDRTG(R1)       OLDRTG (BASED ON INDEXED SHARE)              
         SR    R6,R6                                                            
         L     R7,DMCB             INDEXED SHARE                                
         M     R6,NEWHPT(R1)       NEWHPT X INDEXED SHARE                     
         SLDA  R6,1                                                             
         D     R6,=F'1000'                                                      
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,NEWRTG(R1)       NEW RATING (BASED ON INDEXED SHR)            
         LA    R7,4                BUMP TO NEXT FIELDS                          
         AR    RE,R7                                                            
         AR    RF,R7                                                            
         AR    R8,R7                                                            
         AR    R1,R7                                                            
         BCT   R0,GETYRS23                                                      
*&&DO                                                                           
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
*&&                                                                             
         L     RE,AIO2                                                          
         LA    RE,(Y1SHR-IOAREA2)(RE)                                           
         AHI   RE,(DISPHOM*4)       RE-->INDICES FOR HOMES SHARES               
         LA    RF,HOMSHR            RF-->ORIG HOMES SHARE NEED INDEXING         
         LHI   R0,HOMSHRLN/4                                                    
                                                                                
GETYRS23SH DS  0H                                                               
         L     R7,0(RE)             ASSUME 0(RE) = AVG SHR VALUE                
         CLI   Y4ASW,C'Y'                                                       
         BE    GETYRA23SH           IF IT IS, THEN JUST SEED IT IN              
                                                                                
         L     R7,0(RF)             R7 = ORIGINAL HOME SHARE                    
         SR    R6,R6                                                            
         M     R6,0(RE)             INDEX IT                                    
         SLDA  R6,1                                                             
         D     R6,=F'1000'                                                      
         A     R7,=F'1'                                                         
         SRL   R7,1                                                             
GETYRA23SH ST  R7,0(RF)             AND PUT IT BACK                             
         AHI   RE,4                                                             
         AHI   RF,4                                                             
         BCT   R0,GETYRS23SH                                                    
*                                                                               
         L     R7,SAVER7           RESET DBLOCK POINTER                         
*                                  RESET ORIGINAL DBLOCK VALUES                 
GETYRS24 XC    DBLOCK,DBLOCK                                                    
         MVC   DBAREC,SPUPAREC                                                  
         MVC   DBAQUART,AFRSTEL                                                 
         MVC   DBFILE,FILEDEM                                                   
         MVC   DBTAPEP,TAPEOPT                                                  
         STCM  R9,15,DBCOMFCS                                                   
         B     UPX2                                                             
*                                                                               
GETYRS30 L     RE,AIO2                                                          
         LA    RE,Y1SHR-IOAREA2(RE)                                             
         LA    R3,OLDUNV           THESE POINT TO SHARE BOOK VALUES             
         LA    RF,OLDRTG                                                        
         LA    R8,OLDHPT                                                        
         LA    R1,NEWHPT                                                        
         LA    R4,NEWRTG                                                        
         LA    R0,(OLDRTGX-OLDRTG)/4                                            
GETYRS33 L     R7,0(RF)            OLDRTG                                       
         LA    R6,2000                                                          
         MR    R6,R6                                                            
         OC    0(4,R8),0(R8)                                                    
         BNZ   *+12                                                             
         LA    R7,1                                                             
         B     *+8                                                              
         D     R6,0(R8)            OLDHPT (SHARE CALC)                          
         AH    R7,=H'1'                                                         
         SRL   R7,1                                                             
         ST    R7,DMCB             SAVE  SHARE                                  
*                                                                               
         CLI   Y4ASW,C'Y'          DOING AVERAGE                                
         BNE   *+12                                                             
         L     R7,0(RE)            YES JUST REPLACE IT                          
         B     GETYRA33                                                         
*                                                                               
         L     R7,0(R1)            NEWHPT                                       
         SR    R6,R6                                                            
         BAS   R2,GYRSITOR                                                      
         M     R6,0(RE)            NOW INDEX IT                                 
         AH    R7,=H'500'                                                       
         D     R6,=F'1000'                                                      
GETYRA33 BAS   R2,GYRSRTOI                                                      
         ST    R7,0(R1)            INDEXED HPT                                  
         L     R6,DMCB             SHARE                                        
         MR    R6,R6                                                            
         SLDA  R6,1                                                             
         D     R6,=F'1000'                                                      
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,0(R4)            NEW RATING (BASED ON INDEXED PUT)            
         LA    R7,4                BUMP TO NEXT FIELDS                          
         AR    RE,R7                                                            
         AR    RF,R7                                                            
         AR    R8,R7                                                            
         AR    R1,R7                                                            
         AR    R4,R7                                                            
         AR    R3,R7                                                            
         BCT   R0,GETYRS33                                                      
         L     R7,SAVER7                                                        
*                                                                               
*        MVC   SAVRTG(OLDRTGLN),OLDRTG BECAUSE IT GETS RESET                    
*                                                                               
         B     GETYRS24                                                         
         SPACE 2                                                                
UPX2     XIT1  ,                   EXIT FROM MODULE                             
         SPACE 2                                                                
GYRSITOR CLI   TAPEOPT,C'Y'        CONVERT IMPS TO PERCENT                      
*        BR    R2                                                               
         BNER  R2                                                               
         CH    R0,=H'33'                                                        
         BLR   R2                                                               
         OC    0(4,R3),0(R3)                                                    
         BZR   R2                                                               
         STM   RE,RF,SAVEMORE                                                   
         L     RF,0(R3)                                                         
         SR    RE,RE                                                            
         AH    RF,=H'5'                                                         
         D     RE,=F'10'                                                        
         MH    RF,=H'10'                                                        
         M     R6,=F'20'                                                        
         DR    R6,RF                                                            
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         SR    R6,R6                                                            
         LM    RE,RF,SAVEMORE                                                   
         BR    R2                                                               
         SPACE 1                                                                
GYRSRTOI CLI   TAPEOPT,C'Y'        CONVERT PECENT TO IMPS                       
*        BR    R2                                                               
         BNER  R2                                                               
         CH    R0,=H'33'                                                        
         BLR   R2                                                               
         STM   RE,RF,SAVEMORE                                                   
         L     RF,0(R3)                                                         
         SR    RE,RE                                                            
         AH    RF,=H'5'                                                         
         D     RE,=F'10'                                                        
         MH    RF,=H'10'                                                        
         SR    R6,R6                                                            
         MR    R6,RF                                                            
         AH    R7,=H'5'                                                         
         D     R6,=F'10'                                                        
         SR    R6,R6                                                            
         LM    RE,RF,SAVEMORE                                                   
         BR    R2                                                               
         EJECT                                                                  
*********************************************************************           
* GETDAY - CONVERT INTERNAL DAY CODE INTO DEMAND DAY CODE FORMAT                
*********************************************************************           
*                                                                               
GETDAY   DS    0H                                                               
         LA    R1,DAYTAB           LOOKUP DAY IN TABLE                          
         NI    DUB,X'F0'                                                        
GETDAY22 CLI   0(R1),X'FF'                                                      
         BE    GETDAY24                                                         
         CLC   0(1,R1),DUB                                                      
         BE    GETDAY24                                                         
         LA    R1,2(R1)                                                         
         B     GETDAY22                                                         
GETDAY24 MVC   DUB+4(1),1(R1)                                                   
         B     XIT01               RETURN TO CALLER                             
*                                                                               
DAYTAB   DC    X'007C1040202030104008500460027001FF7F'                          
*                                                                               
         SPACE 1                                                                
*********************************************************************           
* CONVERT START QUARTER HOUR & DURATION INTO MILITARY START & END TIMES         
*********************************************************************           
*                                                                               
GETTIME  DS    0H                                                               
         ZIC   R1,DUB              R1=START QUARTER HOUR                        
         ZIC   RF,DUB+1                                                         
         AR    RF,R1                                                            
         BAS   RE,GETIME22         CONVERT START TO MILITARY                    
         STH   R1,DUB+4                                                         
         LR    R1,RF                                                            
         BAS   RE,GETIME22         CONVERT END TO MILITARY                      
         STH   R1,DUB+6                                                         
         B     XIT01               RETURN TO CALLER                             
         SPACE 1                                                                
*                                                                               
GETIME22 DS    0H                  CONVERT REL QHR INTO MILIT TIME              
         SR    R0,R0                                                            
         D     R0,=F'4'            R1=HOURS, R0=QUARTER HOURS                   
         MH    R1,=H'100'          CALCULATE MILITARY HOUR                      
         MH    R0,=H'15'           CALCULATE MILITARY QUARTER HOUR              
         AR    R1,R0               R1=MILITARY TIME                             
         AH    R1,=H'600'          ADD BASE TIME (6AM)                          
         CH    R1,=H'2400'         TEST IF GR MIDNIGHT                          
         BNH   *+8                                                              
         SH    R1,=H'2400'         YES - SUBTRACT 24 HOURS                      
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* WGHTUP - WEIGHT AND ACCUMULATE DEMO VALUES                          *         
* RESTORE REGS FROM PARAM LIST:                                       *         
* R0 = NUMBER OF ENTRIES TO WEIGHT & ACCUMULATE                       *         
* R1 = A(INPUT VALUES)                                                *         
* R2 = A(ACCUMULATOR VALUES)                                          *         
***********************************************************************         
         SPACE 1                                                                
WGHTUP   DS    0H                                                               
         L     R4,PARM01                                                        
         LM    R0,R2,4(R4)         RESTORE REGS FROM PARM LIST                  
         SR    R4,R4                                                            
         ICM   R4,3,DBFACTOR                                                    
         SR    R3,R3                                                            
WGHTUP2  L     RF,0(R1,R3)         RF=DEMO VALUE                                
         CLI   TAPEOPT,C'Y'        TAPE BASED                                   
         BE    WGHTUP3              NO ROUNDING                                 
         AH    RF,=H'5'                                                         
         SR    RE,RE                                                            
         D     RE,=F'10'                                                        
         M     RE,=F'10'                                                        
WGHTUP3  MR    RE,R4               RF=(VALUE+5/10*10)*WEIGHT                    
         ST    RF,0(R1,R3)         SET WEIGHTED VALUE IN INPUT                  
*&&DO                                                                           
         A     RF,0(R2,R3)         AND ACCUMULATE WEIGHTED VALUE                
*&&                                                                             
         AL    RF,0(R2,R3)         AND ACCUMULATE WEIGHTED VALUE                
         BC    12,*+6              IF RESULTED IN A CARRY,                      
         DC    H'0'                 DIE (FOR NOW)                               
         ST    RF,0(R2,R3)                                                      
         LA    R3,4(R3)                                                         
         BCT   R0,WGHTUP2                                                       
         B     XIT01                                                            
         SPACE 1                                                                
***********************************************************************         
* SUBROUTINE TO UNWEIGHT ACCUMULATED DEMO VALUES                      *         
* R0 = NUMBER OF VALUES TO UNWEIGHT                                   *         
* R1 = A(ACCUMULATOR VALUES)                                          *         
* R2 = A(UNWEIGHTED ACCUMULATOR VALUES) OR ZERO IF SAME AS INPUT      *         
***********************************************************************         
         SPACE 1                                                                
UNWGHT   DS    0H                                                               
         L     R4,PARM01                                                        
         LM    R0,R2,4(R4)         RESTORE REGS FROM PARM LIST                  
         SR    R4,R4                                                            
         ICM   R4,3,DBDIVSOR                                                    
         SR    R3,R3                                                            
UNWGHT2  L     RE,0(R1,R3)                                                      
         SR    RF,RF                                                            
         SRDA  RE,31                                                            
         DR    RE,R4               RF=VALUE/DBDIVSOR (ROUNDED)                  
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         LTR   R2,R2               TEST ACCUMULATORS GIVEN                      
         BNZ   *+12                                                             
         ST    RF,0(R1,R3)         NO - STORE UNWEIGHTED VALUE                  
         B     *+12                                                             
         A     RF,0(R2,R3)         YES - ACCUMULATE UNWEIGHTED VALUE            
         ST    RF,0(R2,R3)                                                      
         LA    R3,4(R3)                                                         
         BCT   R0,UNWGHT2                                                       
         B     XIT01                                                            
         EJECT                                                                  
***********************************************************************         
* PROCESS IN MARKET SHARES:                                           *         
* COMPUTE 'PUT' FROM RTGS OF EACH STATION IN DEFINED 'MKT'            *         
* ROUTINE TO GET OLD OR NEW H/P/T VALUES FROM TIME PERIOD FILE        *         
* ON ENTRY HPT=OLD FOR OLD H/P/T OR NEW FOR NEW H/P/T WITH            *         
* R1=A(BOOK OPTION)                                                   *         
*                                                                     *         
* EXIT WITH H/P/T VALUES AT OLDHPT OR NEWHPT                          *         
***********************************************************************         
         SPACE 1                                                                
STNHPT   DS    0H                                                               
         USING GETHPTWK,R8         R8=A(LOCAL W/S)                              
         L     R1,PARM01                                                        
         MVI   GETHIND,0           SET INDICATOR BYTE                           
         CLI   HPT,OLD             CLEAR REQUIRED H/P/T VALUES                  
         BNE   *+14                                                             
         XC    OLDHPT(LENVALS*2),OLDHPT                                         
         B     *+10                                                             
         XC    NEWHPT(LENVALS*2),NEWHPT                                         
*                                                                               
STNINIT  DS    0H                                                               
         XC    STNBK,STNBK                                                      
         ICM   RE,15,4(R1)                                                      
         BZ    STNHPT1                                                          
         CLC   0(2,RE),=X'2000'    TEST IF BOOK PASSED                          
         BNH   *+10                                                             
         MVC   STNBK,0(RE)         YES - SET SELECTED BOOK                      
*                                                                               
STNHPT1  XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR NEW H/P/T LOOKUP            
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBTAPEP,TAPEOPT                                                  
         MVI   DBFUNCT,DBGETDEM    LOOK UP DEMOS FOR EA STN IN LIST             
         MVC   DBSELBK,STNBK                                                    
*                                                                               
         NI    DBSELBK+1,X'0F'     AND OFF WEEK BITS                            
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,VCOMFACS   **MUST BE DONE FOR MKT OVERRIDES**           
         TM    SPUPOPTS,SPOPEXT                                                 
*&&DO                                                                           
         BZ    *+10                EXTEND BLOCK NOT IN USE                      
         MVC   DBEXTEND,SPUPEXTN    IN USE - SET IT                             
*&&                                                                             
         BZ    STNHPTG             EXTEND BLOCK NOT IN USE                      
         GOTO1 VSUBR01,DMCB,('INXTNDQ',(RC))   IN USE - INSERT IT               
STNHPTG  EQU   *                                                                
         MVC   DBSELAGY,SPUPAGY    RESTORE AGENCY                               
         MVC   DBSELUMK,SPUPMKT    SET AGENCY MKT CODE                          
         MVC   DBSELCLI,SPUPCLI    SET AGENCY CLIENT CODE                       
         MVC   DBSELMED,SPUPMED                                                 
         TM    SPUPOPTS,SPOANGFR   CANADIAN ANGLO/FRANCO OPTION                 
         BZ    *+8                                                              
         MVI   DBFRANCO,C'Y'                                                    
         TM    SPUPOPT2,SPO2CMON   CANADIAN WEEKLIES AS MONTH                   
         BZ    *+8                                                              
         MVI   DBBEST,C'M'                                                      
         TM    SPUPOPT2,SPO2UBBM   CANADIAN USE BBM WEEKLIES                    
         BZ    *+8                                                              
         MVI   DBUSEBBM,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTY   SPORTS ONLY OPTION                           
         BZ    *+8                                                              
         MVI   DBSELSPO,C'Y'                                                    
         TM    SPUPOPTS,SPOSPRTN   EXCLUDE SPORTS OPTION                        
         BZ    *+8                                                              
         MVI   DBSELSPO,C'N'                                                    
*                                                                               
         OC    SPUPPUR,SPUPPUR     TEST DAY/TIME LOOK-UP                        
         BNZ   STNHPT2                                                          
         LA    R1,DBLOCK1          YES - GET VALUES FROM USER DBLOCK            
         MVC   DBSELSTA,DBSELSTA-DBLOCK(R1)                                     
         MVC   DBSELSRC,DBSELSRC-DBLOCK(R1)                                     
         MVC   DBBTYPE,DBBTYPE-DBLOCK(R1)                                       
         MVC   DUB(2),SPUPFBK                                                   
         NI    DUB+1,X'0F'         AND OFF WEEK BITS                            
         B     STNHPT4                                                          
*                                                                               
STNHPT2  L     R1,SPUPAREC                                                      
         MVC   DBSELSTA,PRSTAT-PRKEY(R1)                                        
         MVC   DBSELSRC,PRSRC-PRKEY(R1)                                         
         CLI   DBSELMED,C'N'                                                    
         BNE   *+10                                                             
         MVC   DBSELSRC,SPUPSRC                                                 
         MVC   DBBTYPE,PRBTYP-PRKEY(R1)                                         
         MVC   DUB(2),PRBOOK-PRKEY(R1)                                          
*                                                                               
STNHPT4  MVC   SVBTYPE,DBBTYPE     SAVE INITIAL BOOK TYPE                       
         CLI   DBBTYPE,C'O'        OLYMPIC BOOK                                 
         BNE   *+8                                                              
         CLI   HPT,NEW             AND NEW HPT                                  
         BNE   *+8                                                              
         MVI   DBBTYPE,0           KILL BOOK TYPE                               
         OC    DBSELBK,DBSELBK     TEST IF BOOK PASSED                          
         BNZ   *+10                                                             
         MVC   DBSELBK,DUB         NO - SET FROM INPUT RECORD                   
         MVC   DUB(2),DBSELBK      SAVE SELECTED BOOK VALUE                     
*                                                                               
STNHPT10 CLI   PUREFLAG,YES        SET DAY/TIME FOR LOOK-UP                     
         BE    STNHPT12                                                         
         MVC   DBSELDAY,SPUPDAY    SET DAY/TIME FROM UPGRADE BLOCK              
         MVC   DBSELTIM,SPUPTIM                                                 
         CLI   HPT,NEW                                                          
         BE    STNHPT16                                                         
         LA    R1,DBLOCK1          SET DAY/TIME FROM DBLOCK1                    
         MVC   DBSELDAY,DBSELDAY-DBLOCK(R1)                                     
         MVC   DBSELTIM,DBSELTIM-DBLOCK(R1)                                     
         B     STNHPT16                                                         
*                                                                               
STNHPT12 MVC   DUB+0(1),PUREDW     EXTRACT DAY/TIME FROM PAV RECORD             
         GOTO1 VSUBR01,DMCB,('GETDAYQ',(RC))       DAY IN DEMAND FMT            
         MVC   DBSELDAY,DUB+4                                                   
         MVC   DUB+0(1),PURESTIM                                                
         MVC   DUB+1(1),PUREDUR                                                 
         GOTO1 VSUBR01,DMCB,('GETTIMQ',(RC))       TIME IN DEMAND FMT           
         MVC   DBSELTIM,DUB+4                                                   
*                                                                               
STNHPT16 DS    0H                                                               
         MVC   DBTAPEP,TAPEOPT                                                  
         LA    RE,GETSTLST                                                      
         USING DBXMSD,RE                                                        
         XC    DBXMSD(20),DBXMSD                                                
         MVC   DBXMSID,=C'MSTA'                                                 
         LA    R0,DBXMSTA                                                       
         GOTO1 VDEINMKT,DMCB,DBLOCKD,STNAFFL,(R0)                               
         LA    RE,GETSTLST                                                      
         OC    DBXMSTA,DBXMSTA                                                  
         BZ    STNHPTNX            NO STATIONS TO LOOK UP-- ERROR               
         ST    RE,DBEXTEND                                                      
         DROP  RE                                                               
*                                                                               
         XC    GETSTNBK(9),GETSTNBK                                             
         MVI   GETSTCNT,0          AND # STN'S COUNTER                          
         GOTO1 VDEMAND,DMCB,DBLOCK,STNHK                                        
         MVC   DBBTYPE,SVBTYPE                                                  
         CLI   DBERROR,X'80'       TEST FOR E-O-F                               
         BNE   STNHPTNX            NO - EXIT ON ANY ERROR SETTING               
         MVI   DBERROR,0                                                        
         OC    DBDIVSOR,DBDIVSOR                                                
         BZ    STNHPTNX                                                         
*                                                                               
         XC    HOMSHR(HOMSHRLN),HOMSHR                                          
         CLI   HPT,OLD             TEST OLD HPT'S REQUIRED                      
         BE    STNHPT21            YES                                          
         CLI   SPUP2YRP,C'Y'       CALLER CAN SPECIFY 2 YEAR PUTS               
         BE    *+12                                                             
         TM    DBCPUTS,DBO2YEAR    TEST 2 YEAR AVERAGE PUT REQUIRED             
         BZ    STNHPT21            NO                                           
         TM    GETHIND,X'80'       TEST BOTH YEARS DONE                         
         BNZ   STNHPT18            YES                                          
         ZIC   R1,GETHBK           DECREMENT ACTUAL BOOK YEAR                   
         BCTR  R1,0                                                             
         STC   R1,GETHBK                                                        
         MVC   GETHDIV,DBDIVSOR    SAVE DBDIVSOR                                
         OI    GETHIND,X'80'                                                    
         LA    R1,GETHBK           POINT TO DECREMENTED BOOK VALUE              
         B     STNHPT1             GET PREVIOUS YEAR HPT VALUES                 
*                                                                               
STNHPT18 SR    R0,R0               CALCULATE TOTAL DBDIVSOR VALUE               
         ICM   R0,3,DBDIVSOR                                                    
         SR    R1,R1                                                            
         ICM   R1,3,GETHDIV                                                     
         AR    R0,R1                                                            
         STCM  R0,3,DBDIVSOR                                                    
         B     STNHPT21                                                         
*                                                                               
STNHPT21 CLI   GETSTCNT,0                                                       
         BE    STNHPT22                                                         
         SR    R1,R1                                                            
         ICM   R1,3,DBDIVSOR       DIVIDE DBDIVSOR BY # STATIONS HIT            
         SR    R0,R0                                                            
         SR    R2,R2                                                            
         IC    R2,GETSTCNT                                                      
         DR    R0,R2                                                            
         STCM  R1,3,DBDIVSOR                                                    
*                                                                               
STNHPT22 LA    R0,NUMVALS*2        UNWEIGHT HPT VALUES                          
         LA    R1,OLDHPT           R1=A(OLD OR NEW HPT LINE)                    
         CLI   HPT,OLD                                                          
         BE    *+8                                                              
         LA    R1,NEWHPT                                                        
         SR    R2,R2                                                            
         ST    R1,DMCB+8                                                        
         SR    R2,R2                                                            
         GOTO1 VSUBR01,DMCB,('UNWGHTQ',(RC)),(R0),,(R2)                         
         CLI   HPT,OLD                                                          
         BE    *+10                                                             
         MVC   OLDHPT(LENVALS*2),NEWHPT                                         
         MVI   DBERROR,0                                                        
         B     STNHPTX                                                          
*                                                                               
STNHPTNX MVI   DBERROR,X'10'      SET ERROR TO NOT FOUND                        
*                                                                               
STNHPTX  B     XIT01                                                            
         EJECT                                                                  
***********************************************************************         
* STNHPT DEMAND HOOK TO ACCUM RTGS FROM EACH RECD RETURNED                      
***********************************************************************         
         SPACE 1                                                                
STNHK    ST    RE,SAVERE           SAVE RETURN ADDRESS                          
         XC    GETHPTUN(LENVALS),GETHPTUN  CLEAR UNIVERSES                      
         XC    GETHPTRT(LENVALS*2),GETHPTRT CLEAR RTG/IMPS                      
         XC    GETHPTS(LENVALS*2),GETHPTS   CLEAR HPTS                          
*                                                                               
         TM    GETHIND,X'40'       TEST ACTUAL BOOK SET                         
         BNZ   *+14                                                             
         OI    GETHIND,X'40'                                                    
         MVC   GETHBK,DBACTBK                                                   
*                                                                               
         LH    R0,DBFACTOR         FORCE NO WEIGHTING IN GETIUN                 
         MVC   DBFACTOR,=H'1'                                                   
         GOTO1 VGETIUN,DMCB,(4,DBLOCK),GETHPTUN                                 
         STH   R0,DBFACTOR                                                      
*                                                                               
         MVC   GETHPTS(LENVALS*2),GETHPTRT  COPY RTS-IMPS TO PUT-TOTS           
*                                                                               
         LA    R0,NUMVALS*2                                                     
         LA    R1,GETHPTS          R1=A(UNWEIGHTED DEMO VALUES)                 
         LA    R2,OLDHPT           R2=A(WEIGHTED DEMO ACCUMULATORS)             
         CLI   HPT,OLD                                                          
         BE    STNHK20                                                          
         LA    R2,NEWHPT                                                        
         OC    LUNV(LENVALS),LUNV  TEST IF LOONEYVERSES THERE                   
         BNZ   *+10                YES                                          
         MVC   LUNV(LENVALS),GETHPTUN                                           
*                                                                               
STNHK20  ST    R1,DMCB+8                                                        
         GOTO1 VSUBR01,DMCB,('WGHTUPQ',(RC)),(R0),,(R2)                         
*                                                                               
         LA    RE,GETSTNBK         UPDATE STN-HIT TABLE                         
         LA    R0,GETSTNQ          MAX TABLE SIZE                               
STNHK25  CLI   0(RE),0             EOT?                                         
         BE    STNHK28             UPDATE TABLE IF NOT FOUND                    
         CLC   DBSELSTA,0(RE)      MATCH STATION AND BOOK                       
         BNE   *+14                                                             
         CLC   DBACTBK,5(RE)       SAME BOOK?                                   
         BE    STNHK30             MATCH, NO UPDATE NECC                        
         LA    RE,7(RE)                                                         
         BCT   R0,STNHK25                                                       
         DC    H'0'                REACHED MAX STN SIZE                         
STNHK28  MVC   0(5,RE),DBSELSTA                                                 
         MVC   5(2,RE),DBACTBK                                                  
         XC    7(9,RE),7(RE)       CLEAR FOR NEXT ENTRY                         
         ZIC   RE,GETSTCNT         UPDATE NUMBER STATIONS IN TABLE              
         LA    RE,1(RE)                                                         
         STC   RE,GETSTCNT                                                      
*                                                                               
STNHK30  L     RE,SAVERE           RETURN TO DEMAND FOR NEXT RECORD             
         BR    RE                                                               
         EJECT                                                                  
* INSERT  SPUPEXTN  LINKS AT THE BEGINNING OF  DBEXTEND                         
* TWO THINGS WE NEED TO CHECK BEFORE DOING ANY INSERTIONS:                      
*  1) MAKE SURE  SPUPEXTN  NOT POINTING TO ANY NODE IN  DBEXTEND  LIST          
*  2) MAKE SURE NONE OF THE NODES OF THE  SPUPEXTN  LIST POINTS INTO            
*      THE  DBEXTEND  LIST                                                      
                                                                                
INSXTND  DS    0H                                                               
         LA    RF,DBEXTEND-4       CHECK #1                                     
*                                                                               
INSXTN05 DS    0H                   LOOP TO END OF LIST                         
         ICM   R1,15,4(RF)                                                      
         BZ    INSXTN09                                                         
         CLM   R1,15,SPUPEXTN                                                   
         BE    INSXTNX                                                          
         LR    RF,R1                                                            
         B     INSXTN05                                                         
INSXTN09 EQU   *                                                                
                                                                                
*                                                                               
         DS    0H                                                               
         LA    RE,SPUPEXTN-4       CHECK #2                                     
*                                                                               
INSXTN12 DS    0H                   LOOP TO END OF LIST TO BE INSERTED          
         ICM   R0,15,4(RE)                                                      
         BZ    INSXTN29                                                         
*                                                                               
         LA    RF,DBEXTEND-4                                                    
INSXTN16 DS    0H                   LOOP TO END OF TARGET LIST                  
         ICM   R1,15,4(RF)                                                      
         BNZ   *+10                                                             
         LR    RE,R0                                                            
         B     INSXTN12                                                         
*                                                                               
         DS    0H                                                               
         CR    R0,R1                 IF EQUAL TO A(NODE) ON TRGT LIST,          
         BE    *+10                   EXIT LOOP                                 
         LR    RF,R1                 ELSE, BUMP TO NEXT NODE                    
         B     INSXTN16                                                         
*                                                                               
         XC    4(4,RE),4(RE)                                                    
INSXTN29 EQU   *                                                                
                                                                                
*                                                                               
         DS    0H                  RE-->LAST NODE OF LIST T.B. INSERTED         
         MVC   4(4,RE),DBEXTEND                                                 
         MVC   DBEXTEND,SPUPEXTN                                                
                                                                                
*                                                                               
INSXTNX  DS    0H                                                               
         J     XIT01                                                            
         EJECT                                                                  
DEMOSHR  DS    0XL3                                                             
         DC    X'81',C'S',AL1(1)                                                
         DC    X'81',C'S',AL1(2)                                                
         DC    X'81',C'S',AL1(3)                                                
         DC    X'FF'                                                            
         EJECT                                                                  
*--------------------------------------------------------------------           
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                     ** SPDEMUP GLOBAL W/S **                     
ATPTVALS DS    F                                                                
VSUBR01  DS    F                                                                
RELO2    DS    F                                                                
MYBASE2  DS    F                                                                
PARM01   DS    F                   A(PARM LIST) COMING INTO SUBR01              
SAVER7   DS    F                                                                
SAVEMORE DS    D                                                                
Y4ASW    DS    C                   INDICATE AVERAGES                            
Y4MSW    DS    C                   INDICATE MULTIPLY INDEX(GEOMETRIC)           
Y4SSW    DS    C                   INDICATE PINDEX/SINDEX                       
Y4SYRS   DS    C                   NUMBER OF IDX/AVG BOOKS                      
SVUPFLD  DS    6C                  SAVE UPGRADE FIELDS                          
BOOKI    DS    C                   BOOK INDEX                                   
INDEXUPG DS    C                   INDEXED UPGRADE                              
BOOKLIST DS    CL10                4 BOOKS + 00 END                             
STNBK    DS    XL2                 BOOK PARAM PASSED                            
DUB      DS    D                                                                
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
VCOMFACS DS    V                                                                
VCALLOV  DS    V                                                                
VDEMADDR DS    V                                                                
VDEMAND  DS    V                                                                
VDEMOMTH DS    V                                                                
VDEMOUT  DS    V                                                                
VDEMAINT DS    V                                                                
VHELLO   DS    V                                                                
VDEFINE  DS    V                                                                
VGETIUN  DS    V                                                                
VDEINMKT DS    V                                                                
AIO2     DS    A                                                                
AIO1     DS    A                                                                
RELO     DS    A                                                                
APARM    DS    A                   A(USER PARAMETER LIST)                       
ADISPDTA DS    A                   A(IUN MASTER DISP. TABLE)                    
ADEMLST  DS    A                   A(INPUT DEMO LIST)                           
ADEMOUT  DS    A                   A(OUTPUT DEMO AREA)                          
AFRSTEL  DS    A                   A(FIRST ELEMENT)                             
SAVERD   DS    A                   SAVED RD VALUE FOR ERROR EXIT                
SAVERE   DS    A                   SAVED RE VALUE IN DEMAND HOOK                
INDEX    DS    F                   CALCULATION INDEX FACTOR                     
SVDIVSOR DS    H                   SAVED DBDIVSOR VALUE                         
DBLOCK1  DS    264X                DBLOCK FOR DEMO LOOK-UPS                     
DBLOCK2  DS    264X                DBLOCK FOR UPGRADES                          
FILEVALS DS    0CL4                INPUT FILE VALUES                            
FILETYPE DS    C                   INTERNAL FILE (I=INV,P=PAV)                  
FILEDEM  DS    CL3                 DBLOCK FILE NAME                             
SBKCTRL  DS    C                   SHARE BOOK CONTROL                           
PUTUPGD  DS    C                   Y=THIS IS A PUT UPGRADE                      
HPT      DS    C                   O=GET OLD H/P/T, N=GET NEW H/P/T             
PERSHR   DS    C                   Y=COMPUTE SHARES FROM PERIOD R/P             
PUREFLAG DS    C                   Y=PURE PAV NUMBER LOOK-UP                    
PUREDUR  DS    X                   DURATION OF PURE PROGRAM                     
PUREDW   DS    X                   PROGRAM DAY/WEEK                             
PURESTIM DS    X                   PROGRAM START QTR HOUR                       
TAPEOPT  DS    C                   CALCULATE BASED ON TAPE PRECISION            
PRECUPG  DS    C                   BASED ON DECIMAL PREC. UPGRADE VALS          
NORMHPT  DS    C                   NOMALIZE HPT TAPE PRECISION                  
LOCALMED DS    C                   SO I CAN DO CSI UPGRADES                     
STNAFFL  DS    A                   A(AFFIL) LIST FOR IN MKT SHARES              
SVBTYPE  DS    C                   SAVE THE BOOK TYPE                           
SVDBACBK DS    XL(L'DBACTBK)       SAVE AREA FOR DBACTBK                        
OVERLIST DS    XL31                LIST OF DEMO OVERRIDE VALUES                 
MARKET   DS    XL2                 RATING SERVICE MARKET NUMBER                 
STPROG   DS    CL16                START QHR PROGRAM NAME                       
NDPROG   DS    CL16                END QHR PROGRAM NAME                         
OVERDEM  DS    3XL3,X              USER SHARE DEMO LIST                         
OVERDEMS DS    0F                  USER SHARE DEMO VALUES                       
OVERSHR  DS    F                                                                
OVERRTG  DS    F                                                                
OVERIMP  DS    F                                                                
HOMEVUTS DS    3F                  HOME VUTS (SHR/RTG)                          
         DS    0F                                                               
TOTHMSHR DS     XL(HOMSHRLN)       COMPOSITE AREA FOR HOME SHARES               
DEMOLST  DS    XL20                                                             
OLDSHR   DS    (NUMVALS*2)F        ORIGINAL SHARES                              
MBEXTEND DS    CL(DBXMBKS-DBXMBD+L'SPUPFBKL+4) EXTENSION FOR MULT BOOKS         
SPEXTEND DS    CL(DBXTTIDX-DBXTTID)            EXTENSION FOR SPOT DATA          
MSEXTEND DS    CL(DBXMSTA-DBXMSD+50)   MULT STNS EXTND - 10 STN MAX             
         EJECT                                                                  
UPREC    DS    0F                                                               
***********************************************************************         
*                                  ORIGINAL BOOK VALUES               *         
***********************************************************************         
OLDUNV   DS    (NUMVALS)F          UNIVERSES                          *         
         ORG   OLDUNV+(DISPHOM*4)                                               
UOUHOMES DS    F                                                      *         
         ORG                                                                    
OLDUNVX  EQU   *                                                      *         
***********************************************************************         
OLDRTG   DS    (NUMVALS)F          RATINGS                            *         
         ORG   OLDRTG+(DISPHOM*4)                                               
UORHOMES DS    F                                                      *         
         ORG                                                                    
OLDIMP   DS    (NUMVALS)F          IMPRESSIONS                        *         
OLDRTGX  EQU   *                                                      *         
OLDRTGLN EQU   OLDRTGX-OLDRTG                                                   
***********************************************************************         
OLDHPT   DS    (NUMVALS)F          HUTS/PUTS                          *         
         ORG   OLDHPT+(DISPHOM*4)                                               
UOPHOMES DS    F                                                      *         
         ORG                                                                    
OLDTOT   DS    (NUMVALS)F          TSA TOTALS                         *         
         ORG   OLDTOT+(DISPHOM*4)                                               
UOQHOMES DS    F                                                      *         
         ORG                                                                    
OLDHPTX  EQU   *                                                      *         
OLDHPTLN EQU   OLDHPTX-OLDHPT                                                   
***********************************************************************         
*                                  NEW VALUES                         *         
NEWUNV   EQU   OLDTOT              DEFINE ORIGIN FOR SPGETIUN CALL    *         
*                                                                     *         
***********************************************************************         
NEWRTG   DS    (NUMVALS)F          RATINGS                            *         
         ORG   NEWRTG+(DISPHOM*4)                                               
UNRHOMES DS    F                                                      *         
         ORG                                                                    
NEWIMP   DS    (NUMVALS)F          IMPRESSIONS                        *         
NEWRTGX  EQU   *                                                      *         
***********************************************************************         
NEWHPT   DS    (NUMVALS)F          HUTS/PUTS                          *         
         ORG   NEWHPT+(DISPHOM*4)                                               
UNPHOMES DS    F                                                      *         
         ORG                                                                    
NEWTOT   DS    (NUMVALS)F          TSA TOTALS                         *         
NEWHPTX  EQU   *                                                      *         
***********************************************************************         
*                                  OTHER VALUES                       *         
***********************************************************************         
HOMSHR   DS    3F                  ORIGINAL HOMES SHARES              *         
HOMSHRX  EQU   *                                                      *         
HOMSHRLN EQU   *-HOMSHR                                               *         
***********************************************************************         
LUNV     DS    (NUMVALS)F          LOONEYVERSES                       *         
LUNVX    EQU   *                                                      *         
***********************************************************************         
UPRECX   DS    0F                                                               
*                                                                               
UPUIDX   DS    (NUMVALS)F          INDEX FOR DMA IMPS                           
IOAREA1  DS    2000C               ALLLOW FOR 2000 BYTE NETWORK RECDS           
IOAREA2  DS    (UPRECX-UPREC)C                                                  
IOAREA2X DS    0C                                                               
*                                                                               
Y1SHR    DS    (NUMVALS*2)F        YEAR 1 SHARES FOR INDEXING                   
Y2SHR    DS    (NUMVALS*2)F        YEAR 2 SHARES FOR INDEXING                   
Y3SHR    DS    (NUMVALS*2)F        YEAR 3 SHARES FOR INDEXING                   
Y4SHR    DS    (NUMVALS*2)F        YEAR 4 SHARES FOR INDEXING                   
WORKX    EQU   *                                                                
         EJECT                                                                  
GETHPTWK DSECT                     ** GETHPT S/R LOCAL W/S **                   
GETHIND  DS    X                   INDICATOR BYTE                               
GETHBK   DS    XL2                 SAVED DBACTBK                                
GETHDIV  DS    XL2                 SAVED DBDIVSOR                               
*                                                                               
GETSTNQ  EQU   50                  MAX STNS TABLES CAN HANDLE                   
GETSTLST DS    XL((5*GETSTNQ)+10)                                               
GETSTNBK DS    XL((7*GETSTNQ)+10)  STN/BK FOUND IN STNHK                        
GETSTCNT DS    XL1                 TOT NUMBER STATIONS FOUND                    
GETHPTUN DS    (NUMVALS*1)F        UNVS                                         
GETHPTRT DS    (NUMVALS*2)F        RTGS/IMPS                                    
GETHPTS  DS    (NUMVALS*2)F        PUTS/TOTS                                    
GETHPTLN EQU   *-GETHPTWK                                                       
         SPACE 1                                                                
       ++INCLUDE SPDEMUPD                                                       
         EJECT                                                                  
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 2                                                                
* DEDBEXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DEDBEXTRAD                                                     
         PRINT ON                                                               
         SPACE 2                                                                
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'043SPDEMUPS  05/01/02'                                      
         END                                                                    
