*          DATA SET SDEDATE    AT LEVEL 009 AS OF 05/01/02                      
*CATALP DEISDATE                                                                
         TITLE 'DEISDATE - TEST CALLING LOADABLE DATE MODULE'                   
         PRINT NOGEN                                                            
DEISDATE CSECT                                                                  
         ENTRY DATESUB                                                          
         REQUS                                                                  
         DC    C'DATELINK'                                                      
*                                                                               
         ENTRY DATCON                                                           
         USING *,RF                                                             
DATCON   MVI   ROUTINE,0           DATCON = ROUTINE 0                           
         B     MAIN                                                             
*                                                                               
         ENTRY PERVAL                                                           
         USING *,RF                                                             
PERVAL   MVI   ROUTINE,1           PERVAL = ROUTINE 1                           
         B     MAIN                                                             
*                                                                               
         DROP  RF                                                               
         EJECT                                                                  
MAIN     STM   RE,RC,12(RD)        SAVE CALLERS REGISTERS, CALL DATESUB         
         LA    RC,72(RD)                                                        
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
*                                                                               
         BASR  RB,0                ESTABLISH ADDRESSABILITY                     
         USING *,RB                                                             
         MVC   0(4,RD),=C'DATE'                                                 
         LR    RD,RC                                                            
         ST    R1,APARM                                                         
*                                                                               
         ICM   RF,15,ADATESUB      LOAD DATESUB ON FIRST CALL                   
         BNZ   GETCTRY                                                          
         LOAD  EPLOC=DATESUB                                                    
         ST    R0,ADATESUB                                                      
         LR    RF,R0               RF = A(DATESUB)                              
*                                                                               
GETCTRY  CTRY                                                                   
         LTR   R1,R1               R1 = ZERO IF OFFLINE                         
         BZ    *+6                                                              
         DC    H'0'                CAN'T USE THIS ROUTINE ONLINE                
         ST    R0,CTRYDATA         DATA FROM CTRY MACRO                         
*                                                                               
         LM    R1,R3,APARM         PASS PARAMETERS TO DATESUB                   
         BASR  RE,RF               GO TO DATESUB                                
         DROP  RB                                                               
*                                                                               
         L     RD,4(RD)            RESTORE CALLERS REGISTERS & RETURN           
         LM    RE,RC,12(RD)                                                     
         BR    RE                                                               
         SPACE 3                                                                
APARM    DC    A(0)                CALLER'S R1                                  
CTRYDATA DS    F                   FROM CTRY MACRO (OOAACCLL)                   
ROUTINE  DS    X                   ROUTINE NUMBER                               
         DC    XL3'00'             SPARE                                        
         SPACE 2                                                                
ADATESUB DC    A(0)                FILLED IN AFTER LOAD                         
DATESUB  DC    CL8'DEISSUB'        DEFAULT NAME FOR DATE SUBROUTINES            
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009SDEDATE   05/01/02'                                      
         END                                                                    
