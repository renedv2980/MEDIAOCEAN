*          DATA SET CXSPWC     AT LEVEL 034 AS OF 08/23/96                      
*CATALP CXSPWC                                                                  
         TITLE 'CXSPWC - CONTROL SECURITY EXTRACT - PASSWORD'                   
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
* AL4 0 OR A(PASSWORD ENCRYPTION KEY)                                *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSPWC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSPWC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSPED,R2           R2=A(EXTRACT RECORD)                         
         USING SA0REC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSPELEN,CXSPELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSPELEN(2),=AL2(CXSPEDL)                                        
         MVC   CXSPETYP,CXSPEDQ                                                 
         MVI   CXSPEDX,MXTRTQ                                                   
         MVI   CXSPEDX+1,MXTRTQ                                                 
*                                                                               
         LA    R5,SA0DATA          R4=A(FIRST ELEMENT)                          
         USING SAPEFD,R5                                                        
CXEF010  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXEFNO                                                           
         CLI   0(R5),SAPEFELQ      GET SYSTEM ELEMENT                           
         BE    CXEF030                                                          
CXEF020  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BNH   CXEFNO                                                           
         AR    R5,RF                                                            
         B     CXEF010                                                          
*                                                                               
CXEF030  EQU   *                                                                
         CLC   SAPEFEND,=XL2'FFFF'                                              
         BE    DELREC                                                           
         B     CXEFOK                                                           
         DROP  R5                                                               
*                                                                               
CXEFNO   EQU   *                                                                
         B     DELREC                                                           
*                                                                               
CXEFOK   EQU   *                                                                
         MVI   CXSPEACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
*                                                                               
         MVI   CXSPEAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSPEAID,SA0KAGY                                                 
*                                                                               
CXSPWD   EQU   *                                                                
         MVI   CXSPEPWD-1,MXTRTQ   PASSWORD CODE                                
         MVC   CXSPEPWD,SA0KCODE                                                
         ICM   RF,15,AENCKEY                                                    
         BZ    CXSPWDX                                                          
         LA    R5,CXSPEPWD                                                      
         GOTO1 VCRYPT,DMCB,L'CXSPEPWD,(R5),(RF)                                 
         CLI   0(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CXSPWDX  EQU   *                                                                
*                                                                               
         MVI   CXSPENUM-1,MXTRTQ                                                
         MVI   CXSPEPID-1,MXTRTQ                                                
         MVI   CXSPEAGC-1,MXTRTQ                                                
         MVI   CXSPE1RL-1,MXTRTQ                                                
         MVI   CXSPEFNA-1,MXTRTQ                                                
         MVI   CXSPEMNA-1,MXTRTQ                                                
         MVI   CXSPELNA-1,MXTRTQ                                                
         MVI   CXSPEOFF-1,MXTRTQ                                                
         MVI   CXSPEDEP-1,MXTRTQ                                                
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         SPACE 1                                                                
EXIT     CLI   SAVEACTN,C' '                                                    
         BE    EOFEXIT                                                          
         CLI   SAVEACTN,C'D'                                                    
         BE    EOFEXIT                                                          
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         XMOD1 1                                                                
*                                                                               
EOFEXIT  MVI   SAVEACTN,C' '                                                    
         XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    32C' '                                                           
SAVEACTN DC    CL1' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         DS    0D                                                               
         DC    C'CXSPECC '                                                      
CXSPECC  DS    0XL4                CLIENT SAVE DATA AREA                        
         DC    1000XL4'00'         A/M,CLI                                      
         DC    8X'FF'                                                           
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
AENCKEY  DS    A                   A(PASSWORD ENCRYPTION KEY, 10 BYTES)         
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSPED                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'034CXSPWC    08/23/96'                                      
         END                                                                    
