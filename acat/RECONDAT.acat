*          DATA SET RECONDAT   AT LEVEL 018 AS OF 05/01/02                      
*CATALP RECONDAT                                                                
         TITLE 'CONVERT 8 BYTE BIT PATTERN TO STRING OF DATES'                  
RECONDAT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,RECONDAT,CLEAR=YES                                   
         USING WORKD,RC                                                         
         SPACE                                                                  
INIT     ST    R1,VPARAM           SAVE A(PARAM LIST)                           
         LM    R2,R5,0(R1)         P1=A(FACILS)                                 
*                                  P2=A(RCONDATE)                               
*                                  P3=A(8 BYTE FIELD)                           
*                                  P4=A(FIELD HEADER)                           
         STM   R2,R5,VFACILS       SAVE PARAMS                                  
         MVC   DATA,0(R4)          P3                                           
         L     R7,16(R2)                                                        
         USING COMFACSD,R7                                                      
         ST    R7,VCOMFACS                                                      
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         MVC   VDATCON,CDATCON                                                  
         DROP  R7                                                               
         SPACE                                                                  
INIT2    EQU   *                   CONVERT CONTRACT START/END DATES             
         L     R6,VCONDATE                                                      
         GOTO1 VDATCON,DMCB,(3,(R6)),(0,STDAT)                                  
         GOTO1 (RF),(R1),(3,3(R6)),(0,ENDAT)                                    
         SPACE                                                                  
* FIND MONDAY OF START WEEK                                                     
*                                                                               
INIT4    GOTO1 VGETDAY,DMCB,STDAT,DMCB+8                                        
         CLI   DMCB,MONDAY                                                      
         BE    INIT6               START DATE IS A MONDAY                       
         SPACE                                                                  
         ZIC   R5,DMCB             DAY OF WEEK                                  
         BCTR  R5,0                LESS ONE                                     
         LNR   R5,R5               CHANGE SIGN TO PUSH BACK TO MONDAY           
         GOTO1 VADDAY,DMCB,STDAT,MONDAT,(R5)                                    
         B     BLDTAB                                                           
         SPACE                                                                  
INIT6    MVC   MONDAT,STDAT                                                     
         EJECT                                                                  
* BUILD CONTRACT DATE TABLE - MONDAY,SUNDAY,WEEK NUMBER - AND WEEK              
* NUMBERS RUN BACKWARDS FROM 63                                                 
*                                                                               
BLDTAB   LA    R2,TABLE                                                         
         USING TABENTD,R2                                                       
         LA    R3,63                                                            
         MVC   MON,MONDAT          FIRST MONDAY                                 
         SPACE                                                                  
BLDTAB2  GOTO1 VADDAY,DMCB,MON,SUN,6                                            
         STH   R3,WKNUM                                                         
         SPACE                                                                  
         GOTO1 (RF),(R1),SUN,WRKDAT,1                                           
         CLC   WRKDAT,ENDAT                                                     
         BH    BLDTAB4             TABLE COMPLETED                              
         LA    R2,LTABENT(R2)      POINT TO NEXT ENTRY                          
         MVC   MON,WRKDAT                                                       
         BCT   R3,BLDTAB2                                                       
         SPACE                                                                  
BLDTAB4  STH   R3,LASTWK           SAVE LOWEST WEEK IN CONTRACT                 
         DROP  R2                                                               
         EJECT                                                                  
INITSCAN LA    R2,63                                                            
         STH   R2,STARTSC                                                       
         L     R5,DATA                                                          
         LA    R8,TABLE                                                         
         USING TABENTD,R8                                                       
         LA    R9,WORK1            A(WORK AREA FOR SCREEN OUTPUT)               
         SPACE                                                                  
* CONVENTIONS FOR REST OF CODE                                                  
* R2 POINTS TO NEXT BIT TO BE EXAMINED                                          
* R4 + R5 USED IN SHIFTING BIT PATTERN                                          
* R8 POINTS TO TABLE                                                            
* R9 POINTS TO WORK AREA FOR OUTPUT                                             
         SPACE                                                                  
SCAN     LH    R2,STARTSC          START BIT FOR SCAN                           
         BAS   RE,CHECKST                                                       
         SR    R4,R4                                                            
         XC    FIRSTON(10),FIRSTON CLEAR OUT AREA TO SAVE BIT NUMBERS           
         SPACE                                                                  
LOOKFST  BAS   RE,CHECKST          LOOK FOR FIRST ON BIT                        
         SLDL  R4,1                                                             
         LTR   R4,R4                                                            
         BP    CONSEC              FOUND ONE - TEST FOR 2 IN A ROW              
         BCT   R2,LOOKFST                                                       
         SPACE                                                                  
CONSEC   STH   R2,FIRSTON                                                       
         BCTR  R2,0                POINT TO BIT AFTER FIRST ON                  
         CH    R2,LASTWK                                                        
         BL    STRDAT              THIS BIT WAS LAST ONE-GO TO DATE TRN         
         CH    R2,=H'31'           WHEN NEXT BIT IS 31, NEXT WORD               
         BNE   *+8                 MUST BE LOADED                               
         L     R5,DATA+4                                                        
         SLDL  R4,1                                                             
         CH    R4,=H'3'            TEST FOR TWO ONS                             
         BNE   ALTTST              NO-TEST FOR ALTERNATION PATTERN              
         SPACE                                                                  
         BCTR  R2,0                                                             
         SPACE                                                                  
CONSEC2  CH    R2,=H'31'                                                        
         BNE   *+8                                                              
         L     R5,DATA+4                                                        
         SR    R4,R4                                                            
         SLDL  R4,1                COUNT NUMBER OF CONSECUTIVE                  
         LTR   R4,R4               ON BITS                                      
         BZ    CONSEC4             FOUND A ZERO BIT                             
         BCT   R2,CONSEC2                                                       
         SPACE                                                                  
CONSEC4  LR    R1,R2               SAVE ZERO BIT WEEK NUMBER                    
         LA    R2,1(R2)            BACK UP TO LAST ON BIT                       
         STH   R2,LASTON                                                        
         LH    R0,FIRSTON                                                       
         SR    R0,R1               COMPUTES NUMBER OF CONSECUTIVE WKS           
         STH   R0,NUMWKS                                                        
         LR    R2,R1               RESTORE SCAN START POINT                     
         B     STRDAT              PUT OUT DATES FOR STRING                     
         EJECT                                                                  
* ROUTINE TO TEST FOR ALTERNATION PATTERN - AT ENTRY R4 HAS                     
* BL4'10' AND R2 POINTS TO THE ZERO BIT IN LOW ORDER POSITION                   
*                                                                               
ALTTST   STM   R4,R7,SAVEREG                                                    
         LA    RE,63                                                            
         SH    RE,FIRSTON          SHIFT LENGTH TO REMOVE BITS TO LEFT          
         SH    R2,=H'2'            OF FIRST ON BIT                              
         CH    R2,LASTWK           FURTHER TESTING WOULD RUN PAST LAST          
         BL    ENDALT              BIT-GO TO ENDALT TO RESTORE R2               
         LR    RF,R2               GIVES SHIFT LEN FOR BITS TO R7 OF R2         
         SPACE                                                                  
SETMASK  LM    R6,R7,ALTMASK       IF FIRSTON IS EVEN, THE MASK MUST            
         TM    FIRSTON+1,X'01'     BE SHIFTED ONE POSITION TO THE               
         BO    *+8                 RIGHT SO AN ON IN MASK MATCHES               
         SLDL  R6,1                FIRSTON.                                     
         SPACE                                                                  
*                                  MASK IS INCREASED BY TWO BITS                
         SLDL  R6,0(RE)            EACH TIME THROUGH AS LONG AS IT              
         SRDL  R6,0(RE)            MATCHES THE DATA                             
         SRDL  R6,0(RF)                                                         
         SLDL  R6,0(RF)                                                         
         SPACE                                                                  
SETDATA  LM    R4,R5,DATA                                                       
         SLDL  R4,0(RE)            BITS TO LEFT OF FIRSTON ARE ZEROED           
         SRDL  R4,0(RE)            FOR COMPARISON TO MASK.                      
         SRDL  R4,0(RF)            BITS TO RIGHT OF BIT POINTED TO BY           
         SLDL  R4,0(RF)            RF ARE ZEROED.                               
         SPACE                                                                  
COMPMASK CR    R4,R6                                                            
         BNE   ENDALT                                                           
         CR    R5,R7                                                            
         BNE   ENDALT                                                           
         SPACE                                                                  
RESETM   SH    R2,=H'2'                                                         
         CH    R2,LASTWK                                                        
         BL    ENDALT                                                           
         LR    RF,R2                                                            
         B     SETMASK                                                          
         SPACE                                                                  
ENDALT   LH    RE,FIRSTON                                                       
         LA    R2,2(R2)            ADD 2 TO R2 SO R2 POINTS TO LAST BIT         
         SR    RE,R2               TO FIND NUMBER OF WEEKS IN PATTERN,          
         LA    RE,1(RE)            SUBTRACT AND ADD 1.                          
         CH    RE,=H'2'            IF ITS 2, NO ALTERNATION PATTERN             
         BE    ALTEXIT             HAS BEEN FOUND.                              
         STH   R2,LASTALT                                                       
         STH   RE,NUMALT                                                        
         SPACE                                                                  
ALTEXIT  LM    R4,R7,SAVEREG       RESTORE REGISTERS                            
         OC    LASTALT,LASTALT                                                  
         BZ    STRDAT              NO 'A' PATTERN                               
         SPACE                                                                  
* RESTORE R5 SO ITS LEFTMOST BIT IS THE ONE AFTER LASTALT                       
*                                                                               
         LH    R6,LASTALT                                                       
         LA    R7,32                                                            
         CR    R6,R7               IS LASTALT G.T. 32                           
         BH    ALTEXIT2            YES                                          
         SPACE                                                                  
         SR    R7,R6                                                            
         L     R5,DATA+4                                                        
         SLDL  R4,0(R7)                                                         
         B     ALTEXIT4                                                         
         SPACE                                                                  
ALTEXIT2 SR    R6,R7                                                            
         L     R4,DATA                                                          
         SRDL  R4,0(R6)                                                         
         SPACE                                                                  
ALTEXIT4 B     ALTDAT                                                           
         EJECT                                                                  
STRDAT   BAS   RE,EXDAT            EXTRACT NUMER START DATE FOR SCREEN          
         OC    LASTON,LASTON                                                    
         BZ    STRDAT2             ONLY A DATE                                  
         BAS   RE,EDWKS            EDIT A WEEK EXPRESSION FOR SCREEN            
         SPACE                                                                  
STRDAT2  MVI   0(R9),ASTER                                                      
         LA    R9,1(R9)                                                         
         CH    R2,LASTWK                                                        
         BH    RESET                                                            
         B     SCANEND                                                          
         SPACE                                                                  
ALTDAT   BAS   RE,EXDAT                                                         
         MVI   ALTFLAG,YES                                                      
         BAS   RE,EDWKS                                                         
         MVI   ALTFLAG,NO                                                       
         MVI   0(R9),ASTER                                                      
         LA    R9,1(R9)                                                         
         SPACE                                                                  
RESET    BCTR  R2,0                POINT TO NEXT BIT                            
         STH   R2,STARTSC                                                       
         B     SCAN                                                             
         SPACE                                                                  
SCANEND  BCTR  R9,0                BACK UP POINTER TO LAST BYTE                 
         CLI   0(R9),ASTER                                                      
         BNE   *+8                                                              
         MVI   0(R9),SPACE         CLEAR AN ASTERISK ENDING STRING              
         L     R1,VFLDHDR                                                       
         ZIC   R2,0(R1)            L'HEADER + MAXIMUM L'DATA                    
         SH    R2,=H'9'                                                         
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R1),WORK1       MOVE TO SCREEN                               
         XMOD1 1                                                                
         EJECT                                                                  
* SUB-ROUTINE TO EXTRACT NUMERIC DATES FOR SCREEN                               
*                                                                               
EXDAT    ST    RE,SAVEREG                                                       
         LH    RE,FIRSTON                                                       
         SPACE                                                                  
EXLOOP   CH    RE,WKNUM                                                         
         BE    EXDAT2              FOUND TABLE ENTRY                            
         LA    R8,LTABENT(R8)      POINT TO NEXT ENTRY                          
         B     EXLOOP                                                           
         SPACE                                                                  
EXDAT2   CLC   MON,MONDAT          IS IT FIRST WEEK                             
         BNE   EXDAT4              NO                                           
         OC    LASTALT,LASTALT                                                  
         BNZ   *+14                                                             
         OC    LASTON,LASTON                                                    
         BZ    EXDAT4              PUT OUT A DATE INSTEAD OF JUST 'S'           
         MVI   0(R9),C'S'                                                       
         LA    R9,1(R9)                                                         
         B     EXDAT6                                                           
         SPACE                                                                  
EXDAT4   PACK  FIELD,MON+2(2)      MONTH                                        
         EDIT  (P2,FIELD),(2,(R9)),ALIGN=LEFT                                   
         AR    R9,R0               UPDATE POINT BY OUTPUT LENGTH                
         MVI   0(R9),SLASH                                                      
         LA    R9,1(R9)                                                         
         PACK  FIELD,MON+4(2)      DAY                                          
         EDIT  (P2,FIELD),(2,(R9)),ALIGN=LEFT                                   
         AR    R9,R0                                                            
         SPACE                                                                  
EXDAT6   L     RE,SAVEREG                                                       
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE USED TO CHECK START OF SCAN                                       
*                                                                               
CHECKST  CH    R2,LASTWK                                                        
         BL    SCANEND                                                          
         CH    R2,=H'31'                                                        
         BNE   *+8                                                              
         L     R5,DATA+4                                                        
         BR    RE                                                               
         EJECT                                                                  
* ROUTINE TO PREPARE WEEKS OR END EXPRESSION FOR SCREEN                         
*                                                                               
EDWKS    ST    RE,SAVEREG                                                       
         MVI   0(R9),DASH                                                       
         LA    R9,1(R9)                                                         
         CLI   ALTFLAG,YES                                                      
         BE    EDWKS1                                                           
         LH    RE,LASTON                                                        
         LH    RF,NUMWKS                                                        
         B     EDWKS2                                                           
         SPACE                                                                  
EDWKS1   LH    RE,LASTALT                                                       
         LH    RF,NUMALT                                                        
         LA    RF,1(RF)            TAKE TOTAL WEEKS IN PATTERN, AND             
         SRL   RF,1                DIVIDE BY 2 TO FIND NUM OF ACTIVEWKS         
         B     EDWKS2                                                           
         SPACE                                                                  
EDWKS2   EQU   *                                                                
         CH    RE,LASTWK           CONTRACT SHORTENED AFTER SAR                 
         BNL   EDWKS3              DATA HAS BEEN ADDED MAY CAUSE                
         MVI   PASTFLAG,YES        OVERRUN PAST CONTRACT END.                   
         B     EDWKS5              DEFEND BY PUTTING OUT 'E'.                   
         SPACE                                                                  
EDWKS3   CH    RE,WKNUM            UPDATE TABLE POINTER                         
         BE    EDWKS4                                                           
         LA    R8,LTABENT(R8)                                                   
         B     EDWKS3                                                           
         SPACE                                                                  
EDWKS4   CH    RE,LASTWK           IS THE LAST WEEK INVOLVED                    
         BNE   EDWKS6              NO                                           
EDWKS5   MVI   0(R9),C'E'                                                       
         LA    R9,1(R9)                                                         
         B     EDWKS8                                                           
         SPACE                                                                  
EDWKS6   EDIT  (RF),(3,(R9)),ALIGN=LEFT,TRAIL=C'W'                              
         AR    R9,R0               UPDATE POINTER BY LEN OF OUTPUT              
         SPACE                                                                  
EDWKS8   CLI   ALTFLAG,YES                                                      
         BNE   EDWKS10                                                          
         MVI   0(R9),C'A'          ALTERNATION SUFFIX                           
         LA    R9,1(R9)                                                         
         SPACE                                                                  
         SPACE                                                                  
EDWKS10  CLI   PASTFLAG,YES        EXIT RIGHT AWAY FOR OVERRUN                  
         BE    SCANEND             PAST CONTRACT END.                           
         L     RE,SAVEREG                                                       
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE                                                                  
         DS    0D                                                               
ALTMASK  DC    8X'AA'                                                           
         SPACE                                                                  
* EQUATES                                                                       
*                                                                               
SPACE    EQU   C' '                                                             
DASH     EQU   C'-'                                                             
ASTER    EQU   C'*'                                                             
MONDAY   EQU   X'01'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
SLASH    EQU   C'/'                                                             
         SPACE                                                                  
* DSECT TO COVER CONTRACT DATE TABLE ENTRIES                                    
*                                                                               
TABENTD  DSECT                                                                  
MON      DS    CL6                                                              
SUN      DS    CL6                                                              
WKNUM    DS    H                                                                
LTABENT  EQU   (*-MON)                                                          
         SPACE                                                                  
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
VPARAM   DS    V                                                                
VFACILS  DS    V                                                                
VCONDATE DS    V                                                                
VDATA    DS    V                                                                
VFLDHDR  DS    V                                                                
VCOMFACS DS    V                                                                
         SPACE                                                                  
VDATCON  DS    V                                                                
VADDAY   DS    V                                                                
VGETDAY  DS    V                                                                
         SPACE                                                                  
DATA     DS    D                                                                
DUB      DS    D                                                                
         SPACE                                                                  
DMCB     DS    6F                                                               
SAVEREG  DS    15F                                                              
         SPACE                                                                  
STARTSC  DS    H                                                                
LASTWK   DS    H                                                                
         SPACE                                                                  
FIRSTON  DS    H                   THIS AND NEXT FOUR FIELDS ZEROED             
LASTON   DS    H                   EACH TIME SCAN IS ENTERED                    
LASTALT  DS    H                                                                
NUMWKS   DS    H                                                                
NUMALT   DS    H                                                                
         SPACE                                                                  
STDAT    DS    CL6                 CONTRACT START DATE                          
ENDAT    DS    CL6                          END   DATE                          
MONDAT   DS    CL6                 MONDAY OF CONTRACT'S FIRST WEEK              
WRKDAT   DS    CL6                                                              
FIELD    DS    CL2                                                              
         SPACE                                                                  
ALTFLAG  DS    C                                                                
PASTFLAG DS    C                                                                
WORK     DS    CL17                                                             
         SPACE                                                                  
WORK1    DS    CL80                                                             
         DS    0H                                                               
TABLE    DS    64CL14                                                           
WORKX    EQU   *                                                                
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018RECONDAT  05/01/02'                                      
         END                                                                    
