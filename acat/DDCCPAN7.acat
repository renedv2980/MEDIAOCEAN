*          DATA SET DDCCPAN7   AT LEVEL 151 AS OF 05/09/14                      
*PROCESS USING(WARN(15))                                                        
*&&      SET   NOP=N                                                            
*CATALP CCPAN7                                                                  
         TITLE 'CCPAN7 - XREF FROM PAN TAPE'                                    
CCPAN7   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE WORKX-WORKD,**PAN7**,R9,WORK=V(REGSAVE)                          
*                                                                               
         USING WORKD,RC            SET UP WORKING STORAGE USINGS                
         ST    RD,SAVERD                                                        
*                                                                               
         GOTO1 VDATCON,DMCB,(5,0),(13,DATE)                                     
         GOTO1 VDATCON,DMCB,(5,0),(15,JDATE)                                    
*                                                                               
         BAS   RE,PRINTI           INIT PRINTING                                
*                                                                               
MAIN010  BAS   RE,PANBLD           BUILD A DRECTORY LIST                        
*                                                                               
         OPEN  (TMPFILE,INPUT)     OPEN PAN LIST FILE                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OPEN  (PANFILE,OUTPUT)    OPEN PAN LIST FILE                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    HEADER(255),HEADER  INIT HEADER                                  
         MVC   HEADER(6),=X'0000000002C8'                                       
         MVC   HEADER+48(2),=X'010F'                                            
         MVC   HEADER+50(9),DATE                                                
         MVC   HEADER+59(4),JDATE                                               
         LA    R1,HEADER+63                                                     
         ST    R1,AHEAD                                                         
*                                                                               
         GOTO1 =V(SORTER),DMCB,SRTCARD,RECCARD                                  
*                                                                               
MAIN023  BAS   RE,PANDIR           READ AN ENTRY                                
         CLI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         BE    MAIN200                                                          
*                                                                               
MAIN050  BAS   RE,PANLIN           GET A LINE OF PAN INPUT                      
         B     MAIN023                                                          
*                                                                               
MAIN200  XR    R5,R5                                                            
         XC    IOAREA(20),IOAREA                                                
*                                                                               
MAIN201  GOTO1 =V(SORTER),DMCB,GET GET BACK FROM SORT                           
         ICM   R2,15,4(R1)                                                      
         BZ    MAIN900                                                          
*                                                                               
         PUT   PANFILE,(R2)                                                     
         B     MAIN201                                                          
*                                                                               
*        CLC   4(13,R2),IOAREA+4   SAME LABLE                                   
*        BNE   MAIN210                                                          
*                                                                               
*        ICM   R1,15,0(R2)         SET R1 TO LENGTH                             
*        SRL   R1,16                                                            
*        SH    R1,=H'17'           CUT OFF HEADER                               
*        LA    RE,17(R2)                                                        
*        LR    RF,R1                                                            
*        LR    R0,R5                                                            
*        AR    R5,R1               SET R5 FOR APPEND                            
*        C     R5,FULL                                                          
*        BH    MAIN250             EXCEEDED UPPER LIMIT                         
*        MVCL  R0,RE                                                            
*        B     MAIN201                                                          
*                                                                               
*AIN210  LTR   R5,R5               ANY RECORD TO WRITE                          
*        BZ    MAIN220                                                          
*        LA    R1,IOAREA                                                        
*        SR    R5,R1                                                            
*        C     R5,=F'8100'         TOO BIG                                      
*        BH    MAIN220                                                          
*        SLL   R5,16                                                            
*        STCM  R5,15,IOAREA                                                     
*        PUT   PANFILE,IOAREA      WRITE THIS RECORD                            
*        LTR   R2,R2                                                            
*        BZ    MAIN900                                                          
*                                                                               
*AIN220  LA    R5,IOAREA           COPY RECORD TO IOAREA                        
*        LR    R1,R5                                                            
*        A     R1,=F'8100'         PUT THE UPPER LIMIT IN FULL                  
*        ST    R1,FULL                                                          
*        ICM   R1,15,0(R2)                                                      
*        SRL   R1,16                                                            
*        LR    RF,R1                                                            
*        LR    RE,R2                                                            
*        LR    R0,R5                                                            
*        AR    R5,R1               SET R5 FOR APPEND                            
*        MVCL  R0,RE                                                            
*        B     MAIN201                                                          
*                                                                               
*AIN250  CLC   IOAREA+4(10),OLDBIG                                              
*        BE    MAIN201                                                          
*        MVC   PLINE+1(10),IOAREA+4                                             
*        MVC   PLINE+12(10),=C'TOO BIG   '                                      
*        BAS   RE,PRINTL                                                        
*        MVC   OLDBIG,IOAREA+4                                                  
*        B     MAIN201                                                          
*                                                                               
MAIN900  LA    RF,HEADER                                                        
         L     R1,AHEAD                                                         
         SR    R1,RF                                                            
         LA    R1,1(R1)                                                         
         STH   R1,HEADER                                                        
         SHI   R1,4                                                             
         STH   R1,HEADER+40                                                     
         PUT   PANFILE,HEADER                                                   
*                                                                               
MAIN999  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         CLOSE PANFILE                                                          
         CLOSE SYSPRINT                                                         
*                                                                               
XBASE    XBASE                                                                  
         EJECT                                                                  
*************************************************************                   
*        BUILD FILE OF DIR ENTRIES                          *                   
*************************************************************                   
         SPACE 1                                                                
PANBLD   NTR1                      FIRST READ ALL DIR ENTRIES                   
         OPEN  (TMPFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PANBK,=CL10'A'      START FROM A                                 
*                                                                               
PANBLD3  GOTO1 VPANIC,DMCB,(X'40',READ),(C'T',DIR),PANBK,PANCRD                 
*                                                                               
         CLC   PANCRD(2),=C'/*'    END OF DIRECTORY?                            
         BE    PANBLD4             YES                                          
*                                                                               
         PUT   TMPFILE,PANCRD      SAVE DIR TO TMPFILE                          
         B     PANBLD3                                                          
*                                                                               
PANBLD4  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A DIRECTORY ENTRY FROM THE LIST IN THE FILE   *                   
*************************************************************                   
         SPACE 1                                                                
PANDIR   NTR1                                                                   
PANDIR1  GET   TMPFILE,PANCRD      READ IN DIR ENTRY                            
         MVC   PANBK,PANCRD        SET PANBK TO NAME OF BOOK READ               
         MVC   PANLEV,PANCRD+10                                                 
         MVC   PANDATE,PANCRD+26                                                
         MVC   PANLANG,PANCRD+18                                                
         CLC   PANBK(2),=C'RM'     IGNORE                                       
         BE    PANDIR1                                                          
         CLC   PANBK(2),=C'DC'     IGNORE                                       
         BE    PANDIR1                                                          
         CLC   PANBK(2),=C'XX'     IGNORE                                       
         BE    PANDIR1                                                          
         CLC   PANBK(8),=C'DDLSRC  ' EXCEPTION                                  
         BE    PANDIR1                                                          
*        CLC   PANBK(2),=C'ME'     TEST ONLY                                    
*        BNE   PANDIR1                                                          
*                                                                               
PANDIR2  B     XIT1                                                             
*                                                                               
PANDIRX  DS    0H                  NO MORE ITEMS IN DIRECTORY                   
         MVI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ THE PANBOOK                                   *                   
*************************************************************                   
         SPACE 1                                                                
PANLIN   NTR1                                                                   
         CLC   OLDBOOK(2),PANBK    REPORT ON EACH NEW PROG                      
         BE    PANLIN01                                                         
         LA    R4,MESSAGE                                                       
         MVC   MESSAGE(2),=X'0011'                                              
         MVC   MESSAGE+2(10),PANBK                                              
         MVC   OLDBOOK,PANBK                                                    
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
*                                                                               
PANLIN01 L     R6,=A(BIGWORK)      SORT AREA                                    
         SR    R7,R7               SORT COUNTER                                 
         XC    LINENUM,LINENUM                                                  
         XC    PANLINE,PANLINE                                                  
*                                                                               
PANC000  XC    PANLINES,PANLINES                                                
         MVI   CODE,C'N'                                                        
*                                                                               
PANC001  GOTO1 VPANIC,DMCB,READ,(C'T',PAN),PANBK,PANCRD                         
         CLI   8(R1),0                                                          
         BNE   PANC090                                                          
*                                                                               
         LH    R1,LINENUM                                                       
         LA    R1,1(R1)                                                         
         STH   R1,LINENUM                                                       
*                                                                               
PANC010  CLC   PANCRD(8),=C'*INCLUDE'                                           
         BE    PANC015                                                          
         CLC   PANCRD(7),=C'*CATALP'                                            
         BE    PANC015                                                          
         CLC   PANCRD(6),=C'*PHASE'                                             
         BE    PANC015                                                          
         CLC   PANCRD(3),=C'*&&&&'                                              
         BE    PANC015                                                          
         CLI   PANCRD,C'*'                                                      
         BE    PANC050                                                          
*                                                                               
PANC015  CLC   PANCRD+9(5),=C'EJECT'                                            
         BE    PANC050                                                          
         CLC   PANCRD+9(5),=C'SPACE'                                            
         BE    PANC050                                                          
         CLC   PANCRD(20),SPACES                                                
         BE    PANC050                                                          
*                                                                               
         LH    R1,LINENUM          DO NOT ALLOW MORE THAN 10 LINES              
         SH    R1,PANLINE                                                       
         CHI   R1,10                                                            
         BL    PANC020                                                          
         MVC   PANLINE,LINENUM                                                  
*                                                                               
PANC020  MVI   CODE,C'Y'           MUST BE A LINE OF CODE                       
         TR    PANCRD,TRANTAB                                                   
         GOTO1 =V(SQUASHER),DMCB,PANCRD,80                                      
*                                                                               
         LA    R1,PANCRD+80                                                     
         ST    R1,FULL                                                          
         LA    R1,PANCRD                                                        
PANC021  CLI   0(R1),C'0'          IGNORE IF NUMERIC                            
         BNL   PANC025                                                          
*NOP     CLI   0(R1),C'+'          IGNORE IF +                                  
*NOP     BE    PANC025                                                          
*                                                                               
         LA    RF,WORK             COPY TO WORK                                 
         XC    WORK(16),WORK                                                    
PANC022  MVC   0(1,RF),0(R1)                                                    
         LA    RF,1(RF)                                                         
         LA    R1,1(R1)                                                         
         C     R1,FULL             CHECK FOR END OF CARD                        
         BNL   PANC040                                                          
         CLI   0(R1),C' '                                                       
         BNE   PANC022             KEEP COPYING TIL SPACE                       
*                                                                               
         LA    R1,1(R1)                                                         
         LA    RE,WORK             TEST LENGTH NOT > 12  < 2                    
         SR    RF,RE                                                            
         CHI   RF,12                                                            
         BH    PANC021             IGNORE IF IT IS > 12                         
         CHI   RF,3                                                             
         BL    PANC021             IGNORE IF IT IS < 3                          
*                                                                               
         MVC   WORK+12(2),PANLINE  PUT TO WORK AREA                             
         MVC   0(14,R6),WORK                                                    
         LA    R7,1(R7)                                                         
         C     R7,=F'50000'                                                     
         BH    PANCBIG                                                          
         LA    R6,14(R6)           BUMP R6                                      
         B     PANC021                                                          
*                                                                               
PANC025  LA    R1,1(R1)            NEXT CHR                                     
         C     R1,FULL                                                          
         BNL   PANC040             END OF CARD                                  
         CLI   0(R1),C' '                                                       
         BNE   PANC025                                                          
         LA    R1,1(R1)            SKIP OVER SPACE                              
         B     PANC021                                                          
*                                                                               
PANC040  LH    R1,PANLINES                                                      
         LA    R1,1(R1)                                                         
         STH   R1,PANLINES                                                      
         B     PANC001                                                          
*                                                                               
PANC050  CLI   CODE,C'Y'                                                        
         BNE   PANC040                                                          
         MVC   PANLINE,LINENUM                                                  
         B     PANC000                                                          
*                                                                               
PANC090  L     R6,=A(BIGWORK)                                                   
         LTR   R7,R7                                                            
         BZ    XIT1                                                             
         GOTO1 =V(XSORT),DMCB,(R6),(R7),14,12,0                                 
*                                                                               
PANC091  LA    R5,IOAREA+48        R5=DATA AREA                                 
         MVI   IOAREA+4,X'02'      RECORD TYPE                                  
         MVI   IOAREA+5,C'L'       LABLES                                       
         MVC   IOAREA+6(12),0(R6)  LABLE                                        
         XC    IOAREA+18(12),IOAREA+18                                          
         MVC   IOAREA+30(10),PANBK BOOKNAME                                     
         XR    RE,RE                                                            
*                                                                               
PANC091A LR    R4,R5                                                            
         MVC   0(2,R5),=X'0100'    SET ELEMENT STRUCTURE                        
         LA    R5,2(R5)                                                         
         B     PANC092A                                                         
*                                                                               
PANC092  LA    R6,14(R6)           NEXT TABLE ENTRY                             
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BZ    PANC093             EOT                                          
*                                                                               
         CLC   IOAREA+6(12),0(R6)  SAME LABLE                                   
         BNE   PANC093                                                          
*                                                                               
         LR    R1,R5                                                            
         SH    R1,=H'2'                                                         
         CLC   0(2,R1),12(R6)      IGNORE MULTIPLE HITS                         
         BE    PANC092                                                          
*                                                                               
PANC092A MVC   0(2,R5),12(R6)      COPY IN LINENUM                              
         LA    R5,2(R5)                                                         
         LA    RE,1(RE)            COUNT ENTRIES                                
         MVI   0(R5),0             BE SURE TO HAVE A ZERO ON THE END            
         LR    R1,R5                                                            
         SR    R1,R4                                                            
         CHI   R1,254              CHECK MAX ELEMENT LEN                        
         BNE   PANC092                                                          
         STC   R1,1(R4)            STORE ELEMENT LEN                            
         B     PANC091A                                                         
*                                                                               
PANC093  LR    R1,R5                                                            
         SR    R1,R4                                                            
         STC   R1,1(R4)            STORE ELEMENT LEN                            
         LA    R1,IOAREA                                                        
         SR    R5,R1                                                            
         CHI   R5,2000                                                          
         BH    PANCBIG                                                          
         STH   RE,IOAREA+42                                                     
*                                                                               
         SH    R5,=H'3'            -4 FOR HDR +1 FOR TRAILING ZERO              
         STCM  R5,3,IOAREA+40      SET RECORD LEN                               
         LA    R5,4(R5)                                                         
         SLL   R5,16                                                            
         STCM  R5,15,IOAREA                                                     
         GOTO1 =V(SORTER),DMCB,PUT,IOAREA                                       
         LTR   R7,R7                                                            
         BNZ   PANC091                                                          
         B     XIT1                                                             
*                                                                               
PANCBIG  MVC   PLINE(10),PANBK                                                  
         MVC   PLINE+12(8),=C'TOO BIG '                                         
         L     R1,AHEAD            FILL IN HEADER                               
         MVC   0(2,R1),=X'020C'                                                 
         MVC   2(10,R1),PANBK                                                   
         LA    R1,12(R1)                                                        
         MVI   0(R1),0                                                          
         ST    R1,AHEAD                                                         
         BAS   RE,PRINTL                                                        
                                                                                
XIT1     XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        PRINT ROUTINES                                     *                   
*************************************************************                   
         SPACE 1                                                                
PRINTI   ST    RE,SAVERE                                                        
         OPEN  (SYSPRINT,OUTPUT)   PRINT INIT                                   
         ZAP   LINE,=P'0'                                                       
         ZAP   PAGE,=P'1'                                                       
         MVC   PLINE,SPACES                                                     
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTT   ST    RE,SAVERE           PRINT TITLES                                 
         ZAP   LINE,=P'0'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
         PUT   SYSPRINT,TITLE      PRINT TITLE                                  
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTL   ST    RE,SAVERE           PRINT LINE                                   
         AP    LINE,=P'1'          BUMP LINECOUNT                               
         CP    LINE,MAXLINE        TEST FOR MAX LINES                           
         BL    PRINTL2                                                          
*                                                                               
PRINTL1  ZAP   LINE,=P'1'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
         PUT   SYSPRINT,TITLE      PRINT TITLE                                  
*                                                                               
PRINTL2  PUT   SYSPRINT,PLINE      PRINT LINE                                   
         MVC   PLINE,SPACES                                                     
         L     RE,SAVERE                                                        
         BR    RE                  EXIT                                         
*                                                                               
PRINTX   ST    RE,SAVERE           CLOSE PRINT                                  
         CLOSE SYSPRINT                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        PRINT TITLE                                        *                   
*************************************************************                   
         SPACE 1                                                                
TITLE    DC    CL166' '                                                         
         ORG   TITLE                                                            
         DC    C'1',X'40'                                                       
         DC    C'END TIME       TERMINAL  SYS  SE SYS   PROGRAM  TK'            
         DC    C'    SIN     START TIME       CPU     QUE    ET    I/P'         
         DC    C'   O/P   #IO   #OV'                                            
         ORG                                                                    
TITLE1   DC    CL166' '                                                         
         ORG   TITLE1                                                           
         DC    C'1',X'40'                                                       
         DC    C'-----------------------------------------------------'         
         DC    C'------------------ PARAMETER CARDS ------------------'         
         DC    C'-----------------------------------------------------'         
         ORG                                                                    
         EJECT                                                                  
*************************************************************                   
*        ADDCONS                                            *                   
*************************************************************                   
         SPACE 1                                                                
VPANIC   DC    V(PANIC)                                                         
VPANIC2  DC    V(PANIC2)                                                        
VSORTER  DC    V(SORTER)                                                        
VDATCON  DC    V(DATCON)                                                        
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS TABLES AND DCBS                          *                   
*************************************************************                   
         SPACE 1                                                                
TMPFILE  DCB   DDNAME=TMPFILE,DSORG=PS,RECFM=FB,MACRF=(GM,PM),         X        
               BLKSIZE=3120,LRECL=80,EODAD=PANDIRX                              
PANFILE  DCB   DDNAME=PANFILE,DSORG=PS,RECFM=VB,MACRF=(GM,PM),         X        
               BLKSIZE=32760,LRECL=8190                                         
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=(166)          
*                                                                               
PUT      DC    CL8'PUT'                                                         
GET      DC    CL8'GET'                                                         
CLOSE    DC    CL8'CLOSE'                                                       
READ     DC    CL8'READ'                                                        
PAN      DC    CL8'PAN'                                                         
DIR      DC    CL8'DIR'                                                         
SPACES   DC    CL166' '                                                         
MAXLINE  DC    P'60'                                                            
         EJECT                                                                  
TRANTAB  DC    C'                                '                              
         DC    C'                                '                              
         DC    C'          Â›                $    '                              
         DC    C'             _             #@   '                              
         DC    C'                                '                              
         DC    C'                                '                              
         DC    C' ABCDEFGHI       JKLMNOPQR      '                              
         DC    C'  STUVWXYZ      0123456789      '                              
         LTORG                                                                  
SRTCARD  DC    C'SORT FIELDS=(4,13,BI,A) '                                      
RECCARD  DC    C'RECORD TYPE=V,LENGTH=8190 '                                    
         EJECT                                                                  
*************************************************************                   
*        BIG WORK AREA                                      *                   
*************************************************************                   
         SPACE 1                                                                
BIGWORK  CSECT                                                                  
         DS    90000D                                                           
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE                                    *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
SAVERE   DS    A                                                                
SAVERD   DS    A                                                                
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
LINES    DS    F                                                                
DMCB     DS    6F                                                               
PARMS    DS    6F                                                               
AHEAD    DS    A                                                                
WORK     DS    CL132                                                            
CARD     DS    CL80                                                             
LINENUM  DS    H                                                                
DATE     DS    CL9                                                              
JDATE    DS    CL4                                                              
*                                                                               
TAPBK    DS    CL10                                                             
TAPLEV   DS    CL3                                                              
TAPDATE  DS    CL8                                                              
TAPDAT1  DS    CL7                                                              
TAPLANG  DS    CL5                                                              
*                                                                               
PANNUM   DS    H                                                                
PANLINE  DS    H                                                                
PANLINES DS    H                                                                
CODE     DS    C                                                                
PANID    DS    XL6                                                              
*                                                                               
OLDBIG   DS    CL10                                                             
*                                                                               
OLDBOOK  DS    CL10                                                             
*                                                                               
PANBK    DS    CL10                                                             
PANLEV   DS    CL3                                                              
PANDATE  DS    CL8                                                              
PANDAT1  DS    CL7                                                              
PANLANG  DS    CL5                                                              
*                                                                               
PANCRD   DS    CL80                                                             
TAPCRD   DS    CL80                                                             
WORK1    DS    CL132                                                            
*                                                                               
LINE     DS    PL2                                                              
PAGE     DS    PL4                                                              
PLINE    DS    CL166                                                            
*                                                                               
MESSAGE  DS    CL132                                                            
*                                                                               
HEADER   DS    2000C                                                            
*                                                                               
IOAREA   DS    8192C                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'151DDCCPAN7  05/09/14'                                      
         END                                                                    
