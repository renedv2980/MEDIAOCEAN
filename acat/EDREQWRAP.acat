*          DATA SET EDREQWRAP  AT LEVEL 007 AS OF 03/21/17                      
*CATALP REQWRAP                                                                 
         TITLE 'REQWRAP - WRAP JCL ROUND REQUESTS'                              
         PRINT NOGEN                                                            
REQWRAP  CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE 000,*REQWRAP,R8,R9,RA,WORK=A(REQWRK)                             
*                                                                               
         L     R6,=V(CPRINT)       R6 FOR PRINTING                              
         USING DPRINT,R6                                                        
         L     RF,=A(INIWRP)       INITIALIZE                                   
         BASR  RE,RF                                                            
         L     R7,=A(JOBIO)                                                     
         LA    R7,4(R7)                                                         
         USING EODREQD,R7                                                       
         EJECT                                                                  
***********************************************************************         
*READ CONTROL CARDS OR CONSOLE DIRECTIONS - UPSI=1 FOR CARDS          *         
***********************************************************************         
INPUT    L     RF,=A(SELCLEAR)     CLEAR SELECTION POOL                         
         BASR  RE,RF                                                            
         TM    UPSI,X'80'                                                       
         BO    INPCRD                                                           
         L     RF,=A(LOGMSG1)      ASK FOR CONSOLE INPUT                        
         GOTO1 =V(LOGIO),PARA,1,(40,(RF))                                       
*                                                                               
INPLOG   DS    0H                  GET NEXT PARAM CARD FROM CONSOLE             
         MVC   C,SPACES                                                         
         GOTO1 =V(LOGIO),PARA,0,(72,C)                                          
         B     INP2                                                             
*                                                                               
INPCRD   DS    0H                  GET NEXT PARAM CARD FROM SYSIN               
         CLI   CARDSW,C'N'         TEST IF ALREADY READ THE CARD                
         BE    INP2                                                             
         GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
*                                                                               
INP2     MVI   CARDSW,C'Y'         SET CARD HAS BEEN READ                       
         CLC   C(2),=C'/*'                                                      
         BE    INITIAL                                                          
         CLC   C(3),=C'EOJ'        ALLOW GET OUT OPTION                         
         BE    EOJ                                                              
         L     RF,=A(CONTROL)      VALIDATE CONTROL CARD                        
         BASR  RE,RF                                                            
         BNE   INPERR                                                           
         MVC   P(80),C             PRINT CONTROL CARD                           
         GOTO1 =V(PRINTER)                                                      
         BASR  RE,RF                                                            
         TM    UPSI,X'80'          TEST READING FROM CARDS                      
         BZ    INPLOG                                                           
         B     INPCRD                                                           
*                                                                               
INPERR   DS    0H                  ALLOW REINPUT OF LOG INPUT CARD              
         TM    UPSI,X'80'                                                       
         BO    INPERR2                                                          
         L     RF,=A(LOGMSG2)                                                   
         GOTO1 =V(LOGIO),PARA,1,(40,RF)                                         
         B     INPLOG                                                           
*                                                                               
INPERR2  MVC   P(80),C             CANCEL JOB IF CARD INPUT ERROR               
         MVC   P+80(9),=C'**ERROR**'                                            
         GOTO1 =V(PRINTER)                                                      
         ABEND 300                 INVALID CONTROL CARD                         
         EJECT                                                                  
***********************************************************************         
*ALL CONTROL CARDS HAVE BEEN READ                                     *         
***********************************************************************         
INITIAL  DS    0H                                                               
         GOTO1 =V(DATCON),PARA,(4,TODAY),(1,TODPAK)                             
         GOTO1 (RF),(R1),(4,NPDATE),(1,NPDPAK)                                  
*&&UK                                                                           
         GOTOR (RF),(R1),(1,TODPAK),(0,WORK)                                    
         GOTOR =V(ADDAY),(R1),WORK,WORK+6,1                                     
         GOTOR (RF),(R1),WORK,WORK+12,F'-1'                                     
         GOTOR =V(DATCON),(R1),(0,WORK+6),(10,TODAYP1)                          
         GOTOR (RF),(R1),(0,WORK+12),(10,TODAYM1)                               
*&&                                                                             
         USING DTFPHD,RF                                                        
         L     RF,=A(EODREQS)                                                   
         OI    DTFOPEN,DTF_RO      OPEN AS READ-ONLY                            
         L     RF,=A(EODJOBS)                                                   
         OI    DTFOPEN,DTF_RO      OPEN AS READ-ONLY                            
*                                                                               
INICHK1  CLC   SYSINP,=H'1'        TEST HOW MANY SYSTEMS INPUT                  
         BE    INICHK1X                                                         
         BH    INICHK1A                                                         
         L     RE,=A(LOGMSG3)      MUST HAVE AT LEAST ONE SYSTEM                
         MVC   C(80),0(RE)                                                      
         B     INIERR                                                           
*                                                                               
INICHK1A CLC   JOBSTART,SPACES     CANT HAVE START/END JOB FOR MULTI            
         BNE   INIERR                                                           
         CLC   JOBEND,SPACES                                                    
         BNE   INIERR                                                           
INICHK1X DS    0H                                                               
*                                                                               
INICHK2  CLI   SINGLE,1            CHECK SINGLE JOB OPTION                      
         BNE   INICHK2X                                                         
         L     RE,=A(JCLIN)        MUST INPUT $$ JOB FOR MVS SINGLE             
         CLC   0(2,RE),=C'//'                                                   
         BNE   INICHK2W                                                         
         LR    RF,RE                                                            
         LA    R0,16                                                            
INICHK2A CLC   0(5,RF),=C' JOB '                                                
         BE    INICHK2C                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,INICHK2A                                                      
         B     INICHK2W                                                         
INICHK2C CP    MAXSTEPS,=P'255'    EXTRACT JOB ID FOR MAXSTEPS                  
         BH    INICHK2X                                                         
         LA    RF,JCLINJID                                                      
         LA    RE,2(RE)                                                         
         SR    R1,R1                                                            
INICHK2D CLI   0(RE),C' '                                                       
         BE    INICHK2E                                                         
         MVC   0(1,RF),0(RE)                                                    
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         LA    R1,1(R1)                                                         
         CHI   R1,8                                                             
         BH    INICHK2W                                                         
         B     INICHK2D                                                         
INICHK2E CHI   R1,6                LEAVE SPACE FOR JOB NUM                      
         BNH   *+8                                                              
         LHI   R1,6                                                             
         STC   R1,JCLINJLN                                                      
         B     INICHK2X                                                         
INICHK2W L     RE,=A(LOGMSG5)                                                   
         MVC   C(80),0(RE)                                                      
         B     INIERR                                                           
INICHK2X DS    0H                                                               
*                                                                               
INICHK3  B     INIOPN                                                           
*                                                                               
INIERR   B     INPERR2             TREAT AS INVALID CONTROL CARD                
*                                                                               
INIOPN   CLI   SPOOL,C'T'          OPEN OUTPUT FILES AS REQUIRED                
         BNE   INIOPN1                                                          
         L     R2,=A(JCLTAPE)                                                   
         OPEN  ((2),(OUTPUT))                                                   
*                                                                               
INIOPN1  CLI   WJCLID,C' '                                                      
         BE    INIOPN2                                                          
         L     R2,=A(WJCL)                                                      
         MVC   40(8,R2),WJCLID                                                  
         OPEN  ((2),(OUTPUT))                                                   
*                                                                               
INIOPN2  CLI   WLOGID,C' '                                                      
         BE    INIOPN3                                                          
         L     R2,=A(WLOG)                                                      
         MVC   40(8,R2),WLOGID                                                  
         OPEN  ((2),(OUTPUT))                                                   
         MVC   LOGREC,SPACES       SET HEADER RECORD                            
         MVC   LOGSYSID,SYSTEM     SET SYSTEM ID                                
         MVC   LOGRECNO,=C'0000'   SET RECORD NUMBER TO ZERO FOR HDR            
         MVC   LOGJESID,TODAY      SET TODAYS DATE                              
         THMS                                                                   
         ST    R1,FUL                                                           
         OI    FUL+3,X'0F'                                                      
         UNPK  DUB(6),FUL                                                       
         MVC   LOGJOBID+0(2),DUB   SET TIME NOW                                 
         MVI   LOGJOBID+2,C'.'                                                  
         MVC   LOGJOBID+3(2),DUB+2                                              
         MVI   LOGJOBID+5,C'.'                                                  
         MVC   LOGJOBID+6(2),DUB+4                                              
*                                                                               
INIOPN3  DS    0H                                                               
*                                                                               
INI1     ZAP   LINE,=P'75'         JCL STATEMENTS AND REQUESTS                  
         L     RF,=A(HDRMSG3)                                                   
         MVC   SUB1(27),0(RF)                                                   
         L     RF,=A(HDRMSGU)      UNDERLINE                                    
         MVC   SUB2(27),0(RF)                                                   
*                                                                               
IN2      CLC   BEFJCL,SPACES       OPTIONAL JCL BEFORE PRODUCTION               
         BE    IN3                                                              
         MVC   THISJCL,BEFJCL                                                   
         MVI   OUTSIN,C'Y'         SET NOT TO LOOK FOR JOBS/EXECS ETC           
         MVI   JCLPARM,0           SET BEFORE PRODUCTION JCL                    
         BAS   RE,JCLOUT                                                        
         MVI   OUTSIN,C'X'                                                      
*                                                                               
IN3      LH    RE,SYSNUM           SET UP FOR CURRENT SYSTEM                    
         BCTR  RE,0                                                             
         MHI   RE,5                                                             
         L     RF,=A(SYSLST)                                                    
         AR    RF,RE                                                            
         MVC   SYSTEM(5),0(RF)     SET SYSTEM/JCLASS/DISP                       
*                                                                               
IN3A     MVC   DUB(2),=C'S='       SET UP S=SXX TO SEARCH FOR SYSTEM            
         MVC   DUB+2(3),SYSTEM                                                  
         GOTO1 =V(DMDDNAME),DMCB,=C'DDNAME',DUB                                 
         L     RF,DMCB+8                                                        
         CLI   DMCB+8,0                                                         
         BE    IN3B                                                             
         MVI   1(RF),X'FF'         SET INVALID SE NUMBER IN FILE INFO           
IN3B     MVC   SYSKEY(1),SYSTEM    SYSTEM LETTER (SAME AS INPUT VALUE)          
         MVC   SYSKEY+1(1),1(RF)   SE NUMBER                                    
*                                                                               
         CLI   WLOGID,C' '                                                      
         BE    IN4                                                              
         CLC   SYSNUM,=H'1'                                                     
         BNE   IN4                                                              
         MVC   LOGSYSID,SYSTEM     SET LOG SYSTEM ID                            
         L     R1,=A(WLOG)         WRITE HEADER RECORD TO LOG FILE              
         PUT   (1),LOGREC                                                       
*                                                                               
IN4      MVC   QDKEY+5(2),SYSKEY   INITIALISE WORKER FILES                      
         XC    QD,QD                                                            
         MVC   JDKEY+5(2),SYSKEY                                                
         XC    JD,JD                                                            
IN4A     BAS   RE,INDXREQ          LOOP LOOKING FOR SYSTEM                      
         BNE   EOF                                                              
         CLC   QD(8),QDKEY                                                      
         BNE   IN4A                                                             
IN4B     BAS   RE,INDXJOB                                                       
         BNE   EOF                                                              
         CLC   JD(8),JDKEY                                                      
         BNE   IN4B                                                             
IN4C     BAS   RE,READREQ          POSITION TO FIRST RECORDS                    
         BNE   EOF                                                              
IN4D     BAS   RE,READJOB                                                       
         BNE   EOF                                                              
         CLC   JOBSTART,SPACES     WAS STARTING JOB SPECIFIED                   
         BE    LOOP                                                             
*                                                                               
IN6      MVC   WORK(8),QJOBMVS     ALLOW MATCH ON JES JOB NAME                  
         CLC   WORK(8),JOBSTART                                                 
         BE    IN8                                                              
         CLC   QJOBJES,JOBSTART                                                 
         BE    IN8                                                              
         BAS   RE,READJOB          POSITION JOB FILE                            
         BNE   EOF                                                              
         B     IN6                                                              
IN8      L     R2,=A(REQIO)        NOW POSITION REQUESTS TO SAME                
         CLC   QJOBKEY,4(R2)         JOB KEY OF RECORD                          
         BE    LOOP                                                             
         BAS   RE,READREQ                                                       
         BNE   EOF                                                              
         B     IN8                                                              
         EJECT                                                                  
***********************************************************************         
*MAIN MERGE LOGIC                                                     *         
***********************************************************************         
LOOP     ZAP   REQCOUNT,=P'0'      INITITIALISE FOR NEXT JOB                    
         ZAP   REQSTEP,=P'0'                                                    
         L     R2,=A(REQIO)                                                     
         LHI   RF,2000                                                          
         XCEF  (R2),(RF)                                                        
*                                                                               
         OC    QDEST,QDEST         IGNORE JOBS IF QORIGIN=QDEST=ZERO            
         BNZ   LOOP1                                                            
         OC    QORIGIN,QORIGIN                                                  
         BNZ   LOOP1                                                            
         MVI   JOBIND,C'E'                                                      
         B     LOOP2                                                            
*                                                                               
LOOP1    CLI   QPRIPT,C'Z'         PROGTYPE=Z                                   
         BNE   LOOP1A                                                           
         CLI   PROGTYZ,C'I'        ARE WE INCLUDING THEM                        
         BE    LOOP1A                                                           
         MVI   JOBIND,C'E'                                                      
         B     LOOP2                                                            
LOOP1A   L     RF,=A(SELFILT)                                                   
         BASR  RE,RF                                                            
         CLI   JOBIND,C'E'         TEST IF EXCLUDING THIS JOB                   
         BE    LOOP2               YES BYPASS                                   
*                                                                               
         MVC   THISJCL,SPACES      SET UP JCL KEY FOR THIS JOB                  
         MVC   THISJCL(2),QJOBSYS                                               
         CLC   THISJCL(2),=C'PR'   PRINT USES PP NOT PR                         
         BNE   *+10                                                             
         MVC   THISJCL(2),=C'PP'                                                
         CLC   THISJCL(2),=C'NE'   NETWORK USES SP                              
         BNE   *+10                                                             
         MVC   THISJCL(2),=C'SP'                                                
         MVI   THISJCL+2,C'0'                                                   
         MVC   THISJCL+3(2),QPROGRAM                                            
         MVC   THISJCL+5(1),JCLOPT                                              
         CLC   QJOBJCL,SPACES      OPTIONAL JCL SPECIFIED                       
         BE    *+10                                                             
         MVC   THISJCL,QJOBJCL                                                  
*                                                                               
         CLI   QSUBUSER,X'FF'      JCL THAT PRECEDES REQUESTS                   
         BE    LOOP2                                                            
         MVI   JCLPARM,1           SET BEFORE REQUESTS JCL                      
         BAS   RE,JCLOUT                                                        
*                                                                               
LOOP2    BAS   RE,READREQ          GET A REQUEST                                
         BNE   LOOP4               EOF                                          
         AP    REQCOUNT,=P'1'                                                   
         CP    REQCOUNT,QJOBREQS   HAVE WE READ ALL THE REQUESTS                
         BH    LOOP4               FOR THIS JOB                                 
         CP    REQCOUNT,REQSTART   CHECK START                                  
         BL    LOOP2                                                            
         CP    REQCOUNT,REQEND     AND END OPTIONS                              
         BH    LOOP2                                                            
         CLI   QSUBUSER,X'FF'                                                   
         BE    LOOP2                                                            
*                                                                               
LOOP3    CLI   JOBIND,C'E'         TEST IF EXCLUDING THIS JOB                   
         BE    LOOP2               YES                                          
         L     R2,=A(REQIO)                                                     
         CLC   0(4,R2),=C'JOB='                                                 
         JE    *+2                 DIE IF GET OUT OF STEP                       
         BAS   RE,PUTREQ           NO OUTPUT REQUEST CARD                       
         AP    REQSTEP,=P'1'                                                    
         CP    REQSTEP,MAXREQS     TEST IF REACHED MAX REQUESTS IN STEP         
         BNE   LOOP2               NO                                           
         CP    REQCOUNT,QJOBREQS   YES OK IF THIS IS ALSO LAST REQUEST          
         BE    LOOP2                                                            
         ZAP   REQSTEP,=P'0'       RESET REQUEST STEP COUNT                     
         MVI   JCLPARM,2                                                        
         BAS   RE,JCLOUT                                                        
         B     LOOP1               BACK TO RESTART A NEW JOB                    
*                                                                               
LOOP4    CLI   QSUBUSER,X'FF'      JCL THAT FOLLOWS REQUESTS                    
         BE    LOOP5                                                            
         CLI   JOBIND,C'E'         TEST IF EXCLUDING THIS JOB                   
         BE    LOOP5                                                            
         MVI   JCLPARM,2           SET AFTER REQUESTS JCL                       
         BAS   RE,JCLOUT                                                        
*                                                                               
LOOP5    L     R2,=A(REQIO)                                                     
         CLC   0(4,R2),=C'JOB='                                                 
         BE    LOOP10                                                           
         CLI   EOFSW,C'Y'                                                       
         BE    EOF                                                              
         ZAP   REQCOUNT,=P'1'      WRITE OUT REQUESTS FOR PHASE 2               
*                                                                               
LOOP6    BAS   RE,PUTREQ                                                        
         BAS   RE,READREQ                                                       
         BNE   LOOP8                                                            
         L     R2,=A(REQIO)                                                     
         CLC   0(4,R2),=C'JOB='                                                 
         BE    LOOP8                                                            
         AP    REQCOUNT,=P'1'                                                   
         B     LOOP6                                                            
*                                                                               
LOOP8    MVI   JCLPARM,3                                                        
         BAS   RE,JCLOUT                                                        
*                                                                               
LOOP10   CLI   EOFSW,C'Y'                                                       
         BE    EOF                                                              
         CLC   QJOBMVS,JOBEND      CHECK END JOB OPTION                         
         MVC   WORK(8),QJOBMVS                                                  
         CLC   WORK(8),JOBEND                                                   
         BE    EOF                                                              
         CLC   QJOBJES,JOBEND                                                   
         BE    EOF                                                              
         ZAP   REQSTART,=P'1'      RESET OPTIONS                                
         ZAP   REQEND,=P'999999'                                                
         BAS   RE,READJOB          GET EQUIVALENT JOB RECORD                    
         BNE   EOF                                                              
         MVC   WORK(8),QJOBMVS     WAS IGNORE OPTION SPECIFIED                  
         CLC   IGNORE,WORK                                                      
         BE    LOOP12                                                           
         CLC   IGNORE,QJOBJES                                                   
         BE    LOOP12                                                           
         CLI   TRACE,C'Y'          SEPERATE EXEC GROUPS ON PRINTER              
         BNE   LOOP                FOR MVS SINGLE JOB OPTION                    
         CLI   SINGLE,0                                                         
         BE    LOOP                                                             
         CLI   QSUBUSER,X'FF'                                                   
         BE    LOOP                                                             
         CLI   JOBIND,C'E'                                                      
         BE    LOOP                                                             
         GOTO1 =V(PRINTER)                                                      
         B     LOOP                                                             
*                                                                               
LOOP12   BAS   RE,READJOB          BYPASS THIS JOB                              
         BNE   EOF                                                              
*                                                                               
LOOP14   L     R2,=A(REQIO)        AND THIS SET OF REQUESTS                     
         CLC   QJOBKEY,4(R2)                                                    
         BE    LOOP                WHEN IGNORE WAS SPECIFIED                    
         BAS   RE,READREQ                                                       
         BNE   EOF                                                              
         B     LOOP14                                                           
*                                                                               
EOF      LH    RE,SYSNUM           BUMP TO NEXT SYSTEM                          
         LA    RE,1(RE)                                                         
         STH   RE,SYSNUM                                                        
         CH    RE,SYSINP                                                        
         BH    EOF0                                                             
         MVI   EOFSW,C'N'          RESET EOF SWITCH                             
         B     IN3                                                              
*                                                                               
EOF0     CLC   AFTJCL,SPACES       OPTION TO WRITE OUT MORE JCL                 
         BE    EOF2                                                             
         CLI   SINGLE,0                                                         
         BE    EOF1                                                             
         TM    PUTTYP,X'02'        TEST IF LAST CARD WAS EOJ CARD               
         BO    EOF1                                                             
         MVI   JOBIND,C'I'                                                      
         L     RE,=A(MVSEOJ)                                                    
         MVC   JCL,0(RE)                                                        
         BAS   RE,PUTJCL                                                        
EOF1     MVI   JOBIND,C'I'                                                      
         MVC   THISJCL,AFTJCL                                                   
         MVI   OUTSIN,C'Y'         SET NOT TO LOOK FOR JOBS/EXECS ETC           
         MVI   JCLPARM,0           SET JCL THAT FOLLOWS PRODUCTION              
         BAS   RE,JCLOUT                                                        
         MVI   OUTSIN,C'X'                                                      
*                                                                               
EOF2     CLI   ANYOUT,C'Y'         TEST IF ANY OUTPUT                           
         BNE   EOF5                                                             
         CLI   SINGLE,0                                                         
         BE    EOF3                                                             
         TM    PUTTYP,X'02'        TEST IF LAST CARD WAS EOJ CARD               
         BO    EOF3                                                             
         MVI   JOBIND,C'I'                                                      
         L     RE,=A(MVSEOJ)                                                    
         MVC   JCL,0(RE)                                                        
         BAS   RE,PUTJCL                                                        
*                                                                               
EOF3     CLI   NETID,C' '          TEST FOR LAST NETWORK                        
         BE    EOF4                                                             
         OI    NETFRST,X'02'       SET LAST JOB                                 
         BAS   RE,PUTJCL                                                        
*                                                                               
EOF4     MVI   LASTOUT,C'Y'                                                     
*                                                                               
EOF5     CLI   SPOOL,C'T'                                                       
         BNE   EOF5A                                                            
         L     R2,=A(JCLTAPE)                                                   
         CLOSE ((2))                                                            
EOF5A    CLI   WJCLID,C' '                                                      
         BE    EOF5B                                                            
         L     R2,=A(WJCL)                                                      
         CLOSE ((2))                                                            
EOF5B    CLI   WLOGID,C' '                                                      
         BE    EOF6                                                             
         L     R2,=A(WLOG)                                                      
         CLOSE ((2))                                                            
*                                                                               
EOJ      DS    0H                                                               
EOF6     GOTO1 =V(PRINT),PARA,=C'CLOSE'                                         
         XBASE                                                                  
         EJECT                                                                  
***********************************************************************         
*ADD ON SOME EXTRA CONTROLLER CARDS TO JCL WHEN REQUESTS HERE FOUND   *         
***********************************************************************         
EXTRA    BAS   RE,JCLCHK           CHECK MVS JCL CARD                           
*&&UK*&& BAS   RE,REQHCARD         TEST TO OUTPUT DDSIO/DSPACE CARDS            
*                                                                               
EXTRA0D  MVC   JCL,SPACES          ALWAYS OUTPUT A DATE= CARD                   
         MVC   JCL(5),=C'DATE='                                                 
         MVC   JCL+5(8),TODAY      USE TODAYS DATE OR DATE= CARD VALUE          
         CLI   TODAYJOB,C'Y'       TEST IF DATE=CREATE INPUT                    
         BNE   EXTRA0D1            NO                                           
         CLI   QJOBDATE,C'0'       TEST IF JOB RECORD HAS CREATION DATE         
         BL    EXTRA0D1            NO                                           
         MVC   JCL+5(8),QJOBDATE   YES USE JOB CREATION DATE                    
EXTRA0D1 BAS   RE,PUTJCL                                                        
*                                                                               
EXTRA0E  CLI   FLASHCPY,C' '       FLASH=X FOR FLASH COPY DISK FILES            
         BE    EXTRA0F                                                          
         MVC   JCL,SPACES                                                       
         MVC   JCL(6),=C'FLASH='                                                
         MVC   JCL+6(1),FLASHCPY                                                
         BAS   RE,PUTJCL                                                        
*                                                                               
EXTRA0F  CLI   ACCEMU,C'E'         TEST IF EMULATING ACCOUNT FILE               
         BNE   EXTRA0G                                                          
         MVC   JCL,SPACES                                                       
         MVC   JCL(10),=C'DMPARM=EMU'                                           
         BAS   RE,PUTJCL                                                        
*                                                                               
EXTRA0G  CLI   PORFDEFS,0          TEST IF PAGEDEF OR FORMDEF OUTPUT            
*&&UK*&& BE    EXTRA0GX                                                         
*&&US*&& B     EXTRA0GX                                                         
         CLC   QJOBFMD(5),=C'BASIC'                                             
         BE    EXTRA0GX                                                         
         MVC   JCL,SPACES                                                       
         MVC   JCL(9),=C'FORMDEF=Y'                                             
         BAS   RE,PUTJCL                                                        
EXTRA0GX DS    0H                                                               
*&&UK                                                                           
EXTRA0H  CP    CDPAGES,=P'0'       RTPPAGES= FOR CD OUTPUT TYPES                
         BE    EXTRA0HX                                                         
         CLC   QOUTTYPE(5),=C'CDACC'                                            
         BE    EXTRA0H1                                                         
         CLC   QOUTTYPE(5),=C'CDMED'                                            
         BE    EXTRA0H1                                                         
         B     EXTRA0HX                                                         
EXTRA0H1 MVC   JCL,SPACES                                                       
         MVC   JCL(9),=C'RTPPAGES='                                             
         UNPK  JCL+9(6),CDPAGES                                                 
         OI    JCL+14,C'0'                                                      
         BAS   RE,PUTJCL                                                        
EXTRA0HX DS    0H                                                               
*&&                                                                             
EXTRA0J  CLI   TAPEUNIT,C' '       TAPE= FOR TAPE UNIT TYPE                     
         BE    EXTRA0JX                                                         
         MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'TAPE='                                                 
         MVC   JCL+5(1),TAPEUNIT                                                
         BAS   RE,PUTJCL                                                        
EXTRA0JX DS    0H                                                               
*                                                                               
EXTRA1   MVC   JCL,SPACES                                                       
         MVC   JCL(2),QJOBSYS                                                   
         CLC   JCL(2),=C'PR'                                                    
         BNE   *+10                                                             
         MVC   JCL(2),=C'PP'                                                    
         MVI   JCL+2,X'F0'                                                      
         MVC   JCL+3(2),QPROGRAM                                                
         CLC   JCL(2),=C'CO'       CONTROL USES CTNN                            
         BNE   *+16                                                             
         MVC   JCL(2),=C'CT'                                                    
         MVC   JCL+2(3),JCL+3                                                   
         L     R1,=A(PGMEQTAB)                                                  
EXTRA1A  CLI   0(R1),X'FF'                                                      
         BE    EXTRA1X                                                          
         CLC   JCL(5),0(R1)                                                     
         BE    *+12                                                             
         LA    R1,L'PGMEQTAB(R1)                                                
         B     EXTRA1A                                                          
         MVC   JCL(5),5(R1)                                                     
EXTRA1X  DS    0H                                                               
         MVC   JCL+5(3),QUSER                                                   
         CLI   JCL+7,C' '                                                       
         BH    *+8                                                              
         MVI   JCL+7,C' '                                                       
         CLI   JCL,C'P'            PRINT DOESNT NEED MEDIA FILTER               
         BNE   *+8                                                              
         MVI   JCL+7,C' '                                                       
         CLC   QJOBSYS(2),=C'ME'   MEDLINE USES SSPP CARD (NOT SS0PP)           
         BE    EXTRA2                                                           
         CLC   QJOBSYS(2),=C'PE'   PERSON USES SSPP CARD (NOT SS0PP)            
         BE    EXTRA2                                                           
         BAS   RE,PUTJCL                                                        
EXTRA2   CLI   ANYLOGO,C'N'        OPTION TO SKIP LOGOS                         
         BE    EXTRAB                                                           
         B     EXTRA4                                                           
*                                                                               
EXTRA3   BAS   RE,JCLCHK           CHECK MVS CARD                               
EXTRA4   MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'LOGO='                                                 
         MVC   JCL+5(14),QJOBLOG1                                               
         MVC   JCL+19(8),QJOBJES                                                
         EDIT  (2,QDEST),(5,JCL+27),FILL=0                                      
         MVC   JCL+33(7),=C'ORIGIN='                                            
         EDIT  (2,QORIGIN),(5,JCL+40),FILL=0                                    
         L     RE,=A(LLOGO)        SAVE LAST LOGO=CARD                          
         MVC   0(80,RE),JCL                                                     
         BAS   RE,PUTJCL                                                        
*                                                                               
EXTRA5   CLI   LOGOADR,C' '                                                     
         BE    EXTRA6                                                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(8),=C'LOGOADR='                                              
         MVC   JCL+8(1),LOGOADR                                                 
         BAS   RE,PUTJCL                                                        
*                                                                               
EXTRA6   MVC   JCL(12),=C'DESTINATION='                                         
         MVC   JCL+12(33),QJOBDNAM                                              
         MVC   JCL+45(33),QJOBDADD                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(6),=C'DEST2='                                                
         MVC   JCL+6(33),QJOBDAD2                                               
         MVC   JCL+39(33),QJOBDAD3                                              
         CLC   JCL+6(66),SPACES                                                 
         BE    *+8                                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'SHIP='                                                 
         MVC   JCL+5(75),QJOBSHIP                                               
         CLI   RCMSW,C'O'                                                       
         BNE   *+14                                                             
         MVI   JCL+7,C' '                                                       
         MVC   JCL+8(72),QJOBPACK                                               
         CLC   JCL+5(75),SPACES                                                 
         BE    *+8                                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'SHP2='                                                 
         MVC   JCL+5(75),QJOBSHIP+75                                            
         CLC   JCL+5(75),SPACES                                                 
         BE    *+8                                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'BRK1='                                                 
         MVC   JCL+5(75),QJOBPACK                                               
         CLC   JCL+5(75),SPACES                                                 
         BE    *+8                                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(5),=C'BRK2='                                                 
         MVC   JCL+5(75),QJOBPACK+75                                            
         CLC   JCL+5(75),SPACES                                                 
         BE    *+8                                                              
         BAS   RE,PUTJCL                                                        
*                                                                               
         L     R1,=A(MVSSPOOL)     OPTIONAL SPOOL TO TAPE                       
         MVC   JCL(80),0(R1)                                                    
         CLI   ANYSPLTP,C'Y'                                                    
         BNE   *+8                                                              
         BAS   RE,PUTJCL                                                        
         MVI   ANYSPLTP,C'N'                                                    
*                                                                               
EXTRA8   MVC   JCL,SPACES          RETAIN CLASS                                 
         MVC   JCL(7),=C'RETAIN='                                               
*                                                                               
EXTRA8A  CLC   QOUTTYPE(3),=C'CDQ' CDROM VIA PQ GETS PERM RETAIN CLASS          
         BE    *+14                                                             
         CLC   QOUTTYPE(2),=C'C$'                                               
         BNE   EXTRA8B                                                          
         MVI   JCL+7,C'P'                                                       
         B     EXTRA8W                                                          
*                                                                               
EXTRA8B  MVC   JCL+7(1),QJOBRETC   SET JOB PQ RETAIN CLASS                      
         CLI   QJOBRETC,X'41'                                                   
         BL    *+8                                                              
EXTRA8W  BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES          COUNTRY                                      
         MVC   JCL(5),=C'CTRY='                                                 
         LLC   R0,QCTRY                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JCL+5(3),DUB+6(2)                                                
         CLC   JCL+5(3),=C'000'                                                 
         B     *+8                 ***NOP*** BE                                 
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES          LANGUAGE                                     
         MVC   JCL(5),=C'LANG='                                                 
         LLC   R0,QLANG                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JCL+5(3),DUB+6(2)                                                
         CLC   JCL+5(3),=C'000'                                                 
         B     *+8                 ***NOP*** BE                                 
         BAS   RE,PUTJCL                                                        
*                                                                               
         CLI   QSYSTEM,C'S'                                                     
         BE    EXTRAA                                                           
         CLI   QSYSTEM,C'P'                                                     
         BE    EXTRAA                                                           
         CLI   QSYSTEM,C'A'                                                     
         BNE   EXTRAB                                                           
*                                                                               
*NOP*    MVC   JCL,SPACES          HEXCOMP= FOR ACCPAK                          
*NOP*    MVC   JCL(8),=C'HEXCOMP='                                              
*NOP*    GOTO1 =V(HEXOUT),PARA,QUSER,JCL+8,1                                    
*NOP*    BAS   RE,PUTJCL                                                        
         B     EXTRAB                                                           
*                                                                               
EXTRAA   MVC   JCL,SPACES                                                       
         MVC   JCL(7),=C'ORIGIN='                                               
         MVC   JCL+7(33),QJOBONAM                                               
         MVC   JCL+40(33),QJOBOADD                                              
         CLC   JCL+7(66),SPACES                                                 
         BE    EXTRAA1                                                          
         L     RE,=A(LORIGIN)      SAVE LAST ORIGIN=CARD                        
         MVC   0(80,RE),JCL                                                     
         BAS   RE,PUTJCL                                                        
EXTRAA1  CLI   RCMSW,C'O'                                                       
         BE    EXTRAS                                                           
*                                                                               
EXTRAB   MVC   JCL,SPACES                                                       
         MVC   JCL(10),=C'INPUT=CARD'                                           
         BAS   RE,PUTJCL                                                        
         MVI   BYTE1,1             SET ACTIVE CALL                              
         L     RF,=A(ANYARC)                                                    
         BASR  RE,RF                                                            
         MVI   BYTE1,1             SET ACTIVE CALL                              
         L     RF,=A(ANYDIR)                                                    
         BASR  RE,RF                                                            
         MVC   JCL,SPACES                                                       
         LA    R2,QJOBPHS                                                       
         LA    R3,1                                                             
*                                                                               
EXTRAB2  CLI   0(R2),X'41'                                                      
         BL    EXTRAB4                                                          
         MVC   JCL(8),=C'TEST=00X'                                              
         STC   R3,JCL+6                                                         
         OI    JCL+6,X'F0'                                                      
         MVC   JCL+7(1),0(R2)                                                   
         BAS   RE,PUTJCL                                                        
         MVC   JCL,SPACES                                                       
*                                                                               
EXTRAB4  LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         CHI   R3,5                                                             
         BL    EXTRAB2                                                          
*                                                                               
         CLC   QJOBSYS(2),=C'ME'                                                
         BE    *+14                                                             
         CLC   QJOBSYS(2),=C'PE'                                                
         BNE   EXTRAB6                                                          
*                                  MEDIA/PERSON SYSTEMS EXTRA CARDS             
         MVC   JCL,SPACES                                                       
         MVC   JCL(2),QJOBSYS                                                   
         MVC   JCL+2(2),QPROGRAM                                                
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(7),=C'AGENCY='                                               
         MVC   JCL+7(2),QUSER                                                   
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         CLI   QSUBUSER,X'41'                                                   
         BL    EXTRAB10                                                         
         MVC   JCL(6),=C'MEDIA='                                                
         MVC   JCL+6(1),QSUBUSER                                                
         BAS   RE,PUTJCL                                                        
*                                                                               
EXTRAB6  EQU   *                                                                
*&&UK*&& B     EXTRAB10                                                         
*&&US                                                                           
         TM    QJOBCTRL,X'01'      TEST IF JOB HAS COMSCORE REQUESTS            
         BZ    EXTRAB10                                                         
         MVC   JCL,SPACES                                                       
         MVC   JCL(10),=C'COMSCORE=1'                                           
         BAS   RE,PUTJCL                                                        
         MVC   JCL,SPACES                                                       
         MVC   JCL(07),=C'MVSDSN='                                              
         BAS   RE,PUTJCL                                                        
*&&                                                                             
EXTRAB10 MVC   JCL,SPACES                                                       
         MVC   JCL(2),=C'/*'                                                    
         BAS   RE,PUTJCL                                                        
         MVC   JCL(13),=C'REQUESTS HERE'                                        
         B     OK                                                               
         EJECT                                                                  
***********************************************************************         
*ADD ON SOME EXTRA CONTROLLER CARDS FOR OLD SPOT                      *         
***********************************************************************         
EXTRAS   MVC   JCL,SPACES                                                       
         MVI   BYTE1,1             SET ACTIVE CALL                              
         L     RF,=A(ANYARC)                                                    
         BASR  RE,RF                                                            
         MVC   JCL,SPACES                                                       
         MVI   BYTE1,1             SET ACTIVE CALL                              
         L     RF,=A(ANYDIR)                                                    
         BASR  RE,RF                                                            
         MVC   JCL(4),=C'CARD'                                                  
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL(7),=C'PRINTER'                                               
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL,SPACES                                                       
         MVC   JCL(2),QJOBSYS                                                   
         MVI   JCL+2,C'0'                                                       
         MVC   JCL+3(2),QPROGRAM                                                
         MVC   JCL+5(2),QUSER                                                   
         BAS   RE,PUTJCL                                                        
*                                                                               
         MVC   JCL(13),=C'REQUESTS HERE'                                        
         B     OK                                                               
         EJECT                                                                  
***********************************************************************         
*CHECK IF/OR CONDITIONS                                               *         
***********************************************************************         
IF       CLC   JCL+3(7),=C'AGENCY='                                             
         BE    IF2                                                              
         CLC   JCL+3(8),=C'AGENCY!='                                            
         BE    IF3                                                              
         CLC   JCL+3(6),=C'MEDIA='                                              
         BE    IF4                                                              
         CLC   JCL+3(8),=C'COMPANY='                                            
         BE    IF6                                                              
         CLC   JCL+3(7),=C'OUTPUT='                                             
         BE    IF8                                                              
         CLC   JCL+3(12),=C'DESTINATION='                                       
         BE    IF10                                                             
         CLC   JCL+3(08),=C'PROGRAM='                                           
         BE    IF12                                                             
         CLC   JCL+3(8),=C'SPECIAL='                                            
         BE    IF14                                                             
         CLC   JCL+3(7),=C'SYSTEM='                                             
         BE    IF16                                                             
         CLC   JCL+3(8),=C'SYSTEM!='                                            
         BE    IF17                                                             
         CLC   JCL+3(8),=C'HEXCOMP='                                            
         BE    IF18                                                             
         CLC   JCL+3(7),=C'LEDGER='                                             
         BE    IF20                                                             
         CLC   JCL+3(7),=C'ORIGIN='                                             
         BE    IF22                                                             
         B     JCLOUT30                                                         
*                                                                               
IF2      CLC   JCL+10(2),QUSER     MATCH ON AGENCY                              
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
IF3      CLC   JCL+11(2),QUSER     MATCH ON NOT AGENCY                          
         BE    *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF4      CLC   JCL+9(1),QSUBUSER   MATCH ON MEDIA                               
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF6      L     RE,=A(SOFTCOMP)     MATCH ON COMPANY                             
         CLC   JCL+11(1),0(RE)                                                  
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF8      CLC   JCL+10(6),QOUTTYPE  MATCH ON OUTPUT TYPE                         
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF10     MVC   DUB(2),QDEST        DESTINATION=NNNNNN                           
         LH    R0,DUB                                                           
         CVD   R0,DUB                                                           
         UNPK  WORK(6),DUB                                                      
         OI    WORK+5,X'F0'                                                     
         CLC   WORK(6),JCL+15                                                   
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF12     CLC   JCL+11(2),QPROGRAM                                               
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF14     CLC   JCL+11(1),QOUTSPEC                                               
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF16     CLC   JCL+10(3),SYSTEM    MATCH ON SYSTEM                              
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
IF17     CLC   JCL+11(3),SYSTEM    MATCH ON NOT SYSTEM                          
         BE    *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF18     L     RE,=A(SOFTCOMP)                                                  
         ST    RE,PARA                                                          
         GOTO1 =V(HEXOUT),PARA,,DUB,1                                           
         CLC   JCL+11(2),DUB                                                    
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF20     CLC   JCL+10(1),QSUBUSER                                               
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
IF22     MVC   DUB(2),QORIGIN      ORIGIN=NNNNNN                                
         LH    R0,DUB                                                           
         CVD   R0,DUB                                                           
         UNPK  WORK(6),DUB                                                      
         OI    WORK+5,X'F0'                                                     
         CLC   WORK(6),JCL+10                                                   
         BNE   *+8                                                              
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO WRITE OUT SUBSIDIARY MVS JCL CARDS                        *         
***********************************************************************         
IMVSJCL  NTR1                                                                   
         L     R2,=A(JJ1)          CLEAR ALL JCL CARDS                          
IMVSJCL1 CLC   0(8,R2),=8X'FF'                                                  
         BE    IMVSJCL2                                                         
         MVC   0(80,R2),SPACES                                                  
         LA    R2,80(R2)                                                        
         B     IMVSJCL1                                                         
*                                                                               
IMVSJCL2 CLI   JOBIND,C'E'                                                      
         BE    XIT                                                              
         MVC   JCL,SPACES                                                       
IMVJC    L     R3,=A(JJ1)          JOB-CARD-1=JOBNAME/ACCINFO                   
         MVC   0(2,R3),=C'//'                                                   
         MVC   2(8,R3),QJOBJES     JES JOB NAME                                 
         CLI   QJOBDUPS,C' '       TEST IF DUPLICATE SYSTEMS LETTER             
         BNH   *+10                                                             
         MVC   6(1,R3),QJOBDUPS                                                 
         MVC   11(3,R3),=C'JOB'                                                 
         LA    R2,15(R3)                                                        
*                                                                               
IMVJC1A  MVC   0(5,R2),QJOBJES     ACCT INFO UUUUS0PPXX                         
         MVI   5(R2),C'0'                                                       
         MVC   6(2,R2),QJOBJES+5                                                
         MVI   8(R2),C'1'          1/2 CHR SUB SYSTEM                           
         MVI   9(R2),C' '                                                       
         CLI   SYSTEM+1,C' '                                                    
         BE    *+10                                                             
         MVC   8(2,R2),SYSTEM+1                                                 
*&&UK                                                                           
IMVJC1A1 CLI   4(R2),C'M'          TEST MEDIA SYSTEM                            
         BNE   IMVJC1A4                                                         
         LA    RE,IMVJC1A3                                                      
IMVJC1A2 CLI   0(RE),C' '          SEARCH MEDIA SYS SPECIALS                    
         BE    IMVJC1A4                                                         
         CLC   6(2,R2),0(RE)       TEST IF PROGRAM PP IN LIST                   
         BE    *+12                                                             
         LA    RE,3(RE)                                                         
         B     IMVJC1A2                                                         
         MVC   5(1,R2),2(RE)       SET MEDIA SUB CHR AFTER THE M                
         B     IMVJC1A4                                                         
IMVJC1A3 DC    C'CR1CT1FC1FG1FQ1FT1NC1NG1NT1YQ1' CRAFT REQUESTS                 
         DC    C'   '                                                           
IMVJC1A4 DS    0H                                                               
*&&                                                                             
*&&US                                                                           
         MVC   SPOTSNUM(3),SYSTEM  TEST IF SPOT SYS IS NETWORK                  
         BAS   RE,SPOTTY                                                        
         BE    *+10                                                             
         MVC   8(2,R2),NETSNUM+1   SET EQUIVALENT NETWORK SYSTEM NUM            
*&&                                                                             
         LA    R2,9(R2)            ADJUST FOR ONE/TWO CHR SUB SYSTEM            
         CLI   0(R2),C' '                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
*                                                                               
IMVJC1B  MVI   0(R2),C','          PROGRAMMERS NAME                             
         MVI   1(R2),C','                                                       
*                                                                               
IMVJC3   L     R3,=A(JJ2)          JOB-CARD-2=CLASS/TYPRUN/PRTY                 
         L     RF,=A(MVSJMS)                                                    
         MVC   0(80,R3),0(RF)                                                   
         MVC   25(1,R3),MSGLEV1    SET MSGLEVEL                                 
         MVC   27(1,R3),MSGLEV2                                                 
         MVC   39(1,R3),MSGCLA     SET MSGCLASS                                 
         CLI   QJOBCLAS,C' '                                                    
         BE    *+10                                                             
         MVC   39(1,R3),QJOBCLAS                                                
         MVI   40(R3),C','                                                      
         LA    R2,41(R3)                                                        
*                                                                               
IMVJC3A  MVC   0(7,R2),=C'CLASS=C'                                              
         MVC   6(1,R2),JCLASS                                                   
         CLI   QJOBCLAS,C' '                                                    
         BE    *+10                                                             
         MVC   6(1,R2),QJOBCLAS                                                 
         LA    R2,7(R2)                                                         
*                                                                               
IMVJC3B  CLI   DISP,C'H'                                                        
         BE    IMVJC3B1                                                         
         CLI   TYPRUN,C' '                                                      
         BE    IMVJC3C                                                          
IMVJC3B1 MVC   0(12,R2),=C',TYPRUN=HOLD'                                        
         CLI   TYPRUN,C' '                                                      
         BE    *+10                                                             
         MVC   8(8,R2),TYPRUN                                                   
         LA    R2,8(R2)                                                         
         CLI   0(R2),C' '                                                       
         BE    IMVJC3C                                                          
         LA    R2,1(R2)                                                         
         B     *-12                                                             
*                                                                               
IMVJC3C  CLI   QOUTPRI,C'1'                                                     
         BL    IMVJC3D                                                          
         LA    RE,10               INVERT PRIORITY                              
         IC    RF,QOUTPRI                                                       
         SLL   RF,28                                                            
         SRL   RF,28                                                            
         SR    RE,RF                                                            
         MVC   0(7,R2),=C',PRTY=N'                                              
         STC   RE,6(R2)                                                         
         OI    6(R2),X'F0'                                                      
         LA    R2,7(R2)                                                         
*                                                                               
IMVJC3D  CLI   SINGLE,1            SINGLE MVS JOB JCL                           
         BL    IMVPC                                                            
         BH    IMVJC3E                                                          
         MVI   SINGLE,2                                                         
         BAS   RE,PUTSGLC          PUT INPUT $$ JCL CARDS                       
*                                                                               
IMVJC3E  L     RE,=A(JJ1)                                                       
         LA    R0,3                                                             
         MVC   0(2,RE),SPACES      SET GENERATED JOB CARDS TO UNWANTED          
         LA    RE,80(RE)                                                        
         BCT   R0,*-10                                                          
IMVJC3F  CLI   JCLINJLN,0          IS MAXSTEPS IN OPERATION                     
         BE    IMVPC               NO                                           
         CLI   MAXTAPES,0          IS MAXTAPES IN OPERATION                     
         BE    IMVJC3G             NO                                           
         CLI   JOBTAPES,0          YES TEST IF OUTPUT TAPES MATCH               
         BE    IMVJC3G                                                          
         MVC   TSTTAPES,MAXTAPES                                                
         NC    TSTTAPES,JOBTAPES                                                
         BNZ   IMVJC3H                                                          
IMVJC3G  CP    JOBEXEC,MAXSTEPS    IS MAXSTEPS EXCEEDED                         
         BL    IMVPC                                                            
IMVJC3H  ZAP   JOBEXEC,=P'0'       RESET STEPS IN JOB COUNT                     
         MVI   JOBTAPES,0          RESET TAPES IN JOB COUNT                     
         L     RE,=A(SPLPOOL)      RESET SPOOL TAPE POOL                        
         XC    4(4,RE),4(RE)             TO ZERO ENTRIES                        
         AP    JCLINJNO,=P'1'      BUMP NUM OF JOBS COUNT                       
         L     RE,=A(JCLIN)                                                     
         LLC   RF,JCLINJLN         GET LENGTH OF INPUT JOB ID                   
         LA    RE,2(RE,RF)                                                      
         UNPK  0(2,RE),JCLINJNO    SET JOB NO IN JOB ID                         
         OI    1(RE),X'F0'                                                      
         BAS   RE,PUTSGLC          OUTPUT NEXT SET OF JCL CARDS                 
*                                                                               
IMVPC    MVC   FLASH,=C'NONE'      INITIALISE FOR SYSPRINT                      
         MVC   FORMS,QJOBPOWO                                                   
         MVC   CLASS,QCLASS                                                     
         MVI   PORFDEFS,0                                                       
         MVI   ARCFLAG,0                                                        
*&&UK                                                                           
         CLC   QJOBPOWO,=C'1SML'   USE FLASH=STRP TO GET STRIPES                
         BNE   *+16                                                             
         MVC   FORMS,=C'1PLN'                                                   
         MVC   FLASH,=C'STRP'                                                   
*&&                                                                             
IMVPC0A1 CLI   QJOBPTYP,C'Q'       FORCE REMOTES TO CLASS Q                     
         BNE   *+8                                                              
         MVI   CLASS,C'Q'                                                       
         CLC   QJOBPROC(20),=CL20'PRINTING DIRECT'                              
         BNE   *+8                                                              
         MVI   CLASS,C'Q'                                                       
*                                                                               
IMVPC0A2 MVC   IDIOTOTY,QJOBORUT   TEST FOR IDI OUTPUT TYPE OVERRIDE            
         MVC   IDIOTSYS,QSYSTEM                                                 
         MVC   IDIOTPRG,QPROGRAM                                                
         BAS   RE,IDIOT                                                         
         BNE   IMVPC0A9                                                         
IMVPC0A6 CLI   AFP,C' '            IGNORE IF AFP STYLE OUTPUT                   
*&&US*&& BNE   IMVPC0A9                                                         
*&&UK*&& NOP   IMVPC0A9            BUT NOT IN UK                                
         CLI   QSYSTEM,C'A'        ACCOUNT SYSTEM IN LIST                       
         BNE   IMVPC0A8                                                         
         L     R1,=A(ACCXLST)      SKIP POSTING AND CONFIDENTIAL JOBS           
IMVPC0A7 CLI   0(R1),X'FF'                                                      
         BE    IMVPC0A8            GO AHEAD IF NOT FOUND                        
         CLC   QPROGRAM,0(R1)                                                   
         BE    IMVPC0A9            SKIP IT                                      
         LA    R1,2(R1)                                                         
         B     IMVPC0A7                                                         
IMVPC0A8 CLI   QJOBPTYP,C'B'       IF NOT BOTH                                  
         BE    *+8                                                              
         MVI   CLASS,C'Q'          SET CLASS TO REMOTE                          
*                                                                               
IMVPC0A9 CLI   SYSPMODE,C'D'       TEST IF SYSPRINT=DUMMY INPUT                 
         BNE   IMVPC0AA                                                         
         L     R3,=A(JP1)          //SYSPRINT DD                                
         L     RF,=A(MVSSYP)                                                    
         MVC   0(15,R3),0(RF)                                                   
         MVC   15(5,R3),=C'DUMMY'                                               
         B     XIT                                                              
*                                                                               
IMVPC0AA MVI   BYTE1,0             SET QUERY CALL TO SET ANYARCRC               
         L     RF,=A(ANYARC)                                                    
         BASR  RE,RF                                                            
         MVI   BYTE1,0             SET QUERY CALL TO SET ANYDIRRC               
         L     RF,=A(ANYDIR)                                                    
         BASR  RE,RF                                                            
         CLI   QJOBDISP,C'T'       TEST IF SYSPRINT/OUTPUT CARDS                
         BE    *+12                                                             
         CLI   SYSPMODE,C'O'       TEST IF WANT // OUTPUT CARDS                 
         BNE   IMVPC1                                                           
*                                                                               
         L     RF,=A(IMVOC)        GENERATE OUTPUT CARDS AND SYSPRINT           
         BASR  RE,RF                                                            
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*//SYSPRINT  DD SYSOUT=(,),HOLD=Y,OUTPUT=(*.OU001)                    *         
***********************************************************************         
IMVPC1   L     R3,=A(JP1)          //SYSPRINT DD                                
         L     RF,=A(MVSSYP)                                                    
         MVC   0(15,R3),0(RF)                                                   
         LA    R2,15(R3)                                                        
*                                                                               
IMVPC1A  MVC   0(11,R2),=C'SYSOUT=(C,,'                                         
         MVC   8(1,R2),CLASS                                                    
         MVC   11(4,R2),FORMS                                                   
         LA    R2,11(R2)                                                        
         CLI   0(R2),C' '          TERMINATE SYSOUT WITH )                      
         BE    *+12                                                             
         LA    R2,1(R2)                                                         
         B     *-12                                                             
         MVI   0(R2),C')'                                                       
         LA    R2,1(R2)                                                         
*                                                                               
IMVPC1B  CLI   CLASS,C'Q'          IS OUTPUT CLASS Q                            
         BNE   *+12                NO                                           
         CLI   QJOBDISP,C'T'       YES SET DISP HOLD EXCEPT FOR TAPE            
         BNE   IMVPC1BH                                                         
         CLI   QJOBDISP,C'H'       TEST OUTPUT DISP HOLD                        
         BE    IMVPC1BH                                                         
         CLI   QJOBDISP,C'T'       TEST OUTPUT DISP SPOOL TO TAPE               
         BE    IMVPC1BT                                                         
         CLI   QJOBDISP,C'K'       TEST OUTPUT DISP KEEP                        
         BE    IMVPC1BK                                                         
         CLI   QJOBKEEP,C'Y'       TEST IF KEEP SET                             
         BE    IMVPC1BK                                                         
         B     IMVPC1BX            IGNORE OTHER OUTPUT DISPOSITIONS             
*                                                                               
IMVPC1BH MVC   0(7,R2),=C',HOLD=Y'                                              
         LA    R2,7(R2)                                                         
         B     IMVPC1BX                                                         
*                                                                               
IMVPC1BT MVI   ANYSPLTP,C'Y'       WE'LL NEED A SPOOL CONTROL LATER             
         L     R1,=A(SYSEQTAB)     GET SYSTEM NAME ENTRY                        
IMVPC1BU CLI   0(R1),X'FF'                                                      
         JE    *+2                                                              
         CLC   QSYSTEM,0(R1)                                                    
         BE    *+12                                                             
         LA    R1,L'SYSEQTAB(R1)                                                
         B     IMVPC1BU                                                         
         L     R3,=A(MVSSPOOL)                                                  
         LA    RE,6(R3)            POINT TO SYSID FIELD OF SPOOL                
         MVC   0(3,RE),3(R1)       SPOOL=SSSTAPE.SS0PPAA                        
*&&UK                                                                           
         CLC   0(3,RE),=C'MED'     SPOOL=MEFTAPE.SS0PPAA                        
         BNE   *+8                                                              
         MVI   2(RE),C'F'                                                       
         CLC   0(3,RE),=C'ACC'     SPOOL=ACFTAPE.SS0PPAA                        
         BNE   *+8                                                              
         MVI   2(RE),C'F'                                                       
*&&                                                                             
*&&US                                                                           
         MVC   SPOTSNUM(3),SYSTEM  ADJUST TO NETTAPE IF NETWORK                 
         ST    RE,SAVERE                                                        
         BAS   RE,SPOTTY                                                        
         L     RE,SAVERE                                                        
         BE    *+10                                                             
         MVC   0(3,RE),NETSNAME    SET TO NETTAPE                               
*&&                                                                             
         LA    RE,14(R3)           POINT TO JOBID FIELD                         
         MVC   0(2,RE),1(R1)       BUILD SS0PPAA - (AA=AGYALPH)                 
*&&US                                                                           
         MVC   SPOTSNUM(3),SYSTEM  ADJUST TO NETTAPE IF NETWORK                 
         ST    RE,SAVERE                                                        
         BAS   RE,SPOTTY                                                        
         L     RE,SAVERE                                                        
         BE    *+10                                                             
         MVC   0(2,RE),NETSNAME    SET SS TO NE FOR NETWORK                     
*&&                                                                             
         MVI   2(RE),C'0'                                                       
         MVC   3(2,RE),QPROGRAM                                                 
         MVC   5(2,RE),QUSER                                                    
*&&UK                                                                           
         LA    RE,6(R3)            FIX DSNS FOR SPOOL TAPES IN UK               
         CLC   0(3,RE),=C'MEF'     SPOOL=MEFTAPE.MEDIA                          
         BNE   *+10                                                             
         MVC   8(7,RE),=C'MEDIA  '                                              
         CLC   0(3,RE),=C'ACF'     SPOOL=ACFTAPE.ACCOUNT                        
         BNE   *+10                                                             
         MVC   8(7,RE),=C'ACCOUNT'                                              
*&&                                                                             
         BRAS  RE,GETSPLCT         DIG OUT GEN NUMBER                           
         LA    RE,40(R3)           SHOW GEN=NNN IN COL 41                       
         MVC   0(4,RE),=C'GEN='                                                 
         EDIT  (1,SPLCOUNT),(3,4(RE)),FILL=0                                    
*                                                                               
         CLC   QJOBCC,=C'NONE'     SHOW ORIGINAL FCB                            
         BE    IMVPC5                                                           
         CLC   QJOBCC,SPACES                                                    
         BE    IMVPC5                                                           
         LA    RE,50(R3)                                                        
         MVC   0(5,RE),=C'*FCB='                                                
         MVC   5(4,RE),QJOBCC                                                   
         B     IMVPC5                                                           
*                                                                               
IMVPC1BK L     RE,=A(JES1)         OUTPUT JES CARDS FOR KEEP                    
         L     RF,=A(JESKEEP1)                                                  
         MVC   0(80,RE),0(RF)                                                   
         L     RE,=A(JES2)                                                      
         L     RF,=A(JESKEEP2)                                                  
         MVC   0(80,RE),0(RF)                                                   
IMVPC1BX DS    0H                                                               
*                                                                               
IMVPC1C  MVC   0(11,R2),=C',COPIES=001'                                         
         CLI   QJOBCOPY,C' '       TEST IF DEFINED AS SPACE CHR                 
         BE    IMVPC1C2                                                         
         CLI   QJOBCOPY,C'0'       TEST IF DEFINED AS A CHARACTER               
         BL    IMVPC1C1                                                         
         MVC   10(1,R2),QJOBCOPY                                                
         B     IMVPC1C2                                                         
IMVPC1C1 LLC   R1,QJOBCOPY         DEFINED AS BINARY VALUE                      
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  8(3,R2),DUB                                                      
IMVPC1C2 LA    R2,11(R2)                                                        
*                                                                               
IMVPC1D  CLC   QJOBCC,=C'NONE'                                                  
         BE    IMVPC1D2                                                         
         CLC   QJOBCC,SPACES                                                    
         BE    IMVPC1D2                                                         
IMVPC1D1 MVC   0(5,R2),=C',FCB='                                                
         MVC   5(4,R2),QJOBCC                                                   
         LA    R2,5(R2)                                                         
         CLI   0(R2),C' '                                                       
         BE    IMVPC1D2                                                         
         LA    R2,1(R2)                                                         
         B     *-12                                                             
IMVPC1D2 DS    0H                                                               
*                                                                               
IMVPC1E  MVC   WORK,RJEOPT         RJE SUPPORT                                  
         CLC   QJOBORUT(3),=C'RJE'                                              
         BNE   *+10                                                             
         MVC   WORK(3),QJOBORUT+3                                               
         CLC   WORK(3),SPACES                                                   
         BE    IMVPC1E1                                                         
         CLC   QJOBJES+4(3),=C'A55'                                             
         BE    IMVPC1E1                                                         
         MVC   0(6,R2),=C',DEST='                                               
         MVC   6(3,R2),WORK                                                     
         LA    R2,9(R2)                                                         
IMVPC1E1 DS    0H                                                               
*                                                                               
IMVPC1F  BAS   RE,FREEZE           TEST IF OK TO OUTPUT FREE=CLOSE              
         BNE   IMVPC1F1            NO                                           
         L     RF,=A(JP1)          YES TEST IF ROOM                             
         LA    RF,71(RF)                                                        
         SR    RF,R2                                                            
         CHI   RF,12                                                            
         BL    IMVPC1F1                                                         
         MVC   0(11,R2),=C',FREE=CLOSE'                                         
         LA    R2,11(R2)                                                        
IMVPC1F1 DS    0H                                                               
*                                                                               
IMVPC2   CLC   QJOBCHRS,SPACES     SYSPRINT-2=LASER PRINTER PARMS               
         BNE   IMVPC2A                                                          
         CLC   FLASH,=C'NONE'                                                   
         BE    IMVPC3                                                           
         CLI   FLASH,C' '                                                       
         BE    IMVPC3                                                           
IMVPC2A  MVI   0(R2),C','                                                       
         L     R3,=A(JP2)                                                       
         MVC   0(2,R3),=C'//'                                                   
         LA    R2,15(R3)                                                        
*                                                                               
IMVPC2B  CLI   FLASH,C' '                                                       
         BE    IMVPC2C                                                          
         CLC   FLASH,=C'NONE'                                                   
         BE    IMVPC2C                                                          
         MVC   0(6,R2),=C'FLASH='                                               
         MVC   6(4,R2),FLASH                                                    
         MVI   10(R2),C','                                                      
         LA    R2,11(R2)                                                        
*                                                                               
IMVPC2C  CLC   QJOBCHRS,SPACES     SINGLE CHARS=                                
         BE    IMVPC2X                                                          
         CLC   QJOBCHR2,SPACES                                                  
         BNE   IMVPC2D                                                          
         MVC   0(10,R2),=C'CHARS=    '                                          
         MVC   6(4,R2),QJOBCHRS                                                 
         LA    R2,10(R2)                                                        
         B     IMVPC2X                                                          
*                                                                               
IMVPC2D  MVC   0(7,R2),=C'CHARS=(' CHARS=(CHR1,CHR2,CHR3,CHR4)                  
         MVC   7(4,R2),QJOBCHRS                                                 
         LA    R2,11(R2)                                                        
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR2                                                 
         LA    R2,5(R2)                                                         
         CLC   QJOBCHR3,SPACES                                                  
         BE    IMVPC2E                                                          
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR3                                                 
         LA    R2,5(R2)                                                         
         CLC   QJOBCHR4,SPACES                                                  
         BE    IMVPC2E                                                          
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR4                                                 
         LA    R2,5(R2)                                                         
IMVPC2E  MVI   0(R2),C')'                                                       
*                                                                               
IMVPC2X  BCTR  R2,0                REMOVE TRAILING COMMA                        
         CLI   0(R2),C','                                                       
         BNE   *+8                                                              
         MVI   0(R2),C' '                                                       
*                                                                               
IMVPC3   CLI   QJOBFCH,C' '        TEST FICHE CLASS DEFINED                     
         BE    IMVPC4                                                           
         L     RE,=A(JES1)                                                      
         CLI   0(RE),C' '                                                       
         BE    *+8                                                              
         L     RE,=A(JES3)                                                      
         L     RF,=A(JESFICHE)                                                  
         MVC   0(80,RE),0(RF)      //*FORMAT  PR,DDNAME=SYSPRINT,....           
*                                                                               
IMVPC4   TM    ANYARCRC,X'02'      TEST IF ARC=CARD                             
         BZ    IMVPC4X                                                          
         TM    ANYARCRC,X'40'      TEST IF ELIGIBLE                             
         BO    IMVPC4X                                                          
*NOP*    CLI   CLASS,C'Q'          BUT NOT IF GOING DIRECT                      
*NOP*    BE    IMVPC4X                                                          
         L     RE,=A(JES1)                                                      
         CLI   0(RE),C' '                                                       
         BE    *+8                                                              
         L     RE,=A(JES3)                                                      
         L     RF,=A(JESARC)                                                    
         MVC   0(80,RE),0(RF)      //*FORMAT  PR,DDNAME=SYSPRINT,....           
         OI    ARCFLAG,X'08'       SET ARCHIVE SYSPRINT OUTPUT                  
IMVPC4X  DS    0H                                                               
*                                                                               
IMVPC5   B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*PUT MVS EOJ CARD                                                     *         
***********************************************************************         
FMVSJCL  NTR1                      PUT MVS EOJ CARD                             
         MVC   JCL,SPACES                                                       
         CLI   SINGLE,0                                                         
         BNE   FMVSJCL2                                                         
FMVSJCL1 MVC   JCL(2),=C'//'                                                    
         BAS   RE,PUTJCL                                                        
         B     XIT                                                              
*                                                                               
FMVSJCL2 CLI   JCLINJLN,0          TEST IF MAXSTEPS SPECIFIED                   
         BE    XIT                                                              
         CLI   MAXTAPES,0          IS MAXTAPES IN OPERATION                     
         BE    FMVSJCL3            NO                                           
         CLI   JOBTAPES,0          YES TEST IF OUTPUT TAPES MATCH               
         BE    FMVSJCL3                                                         
         MVC   TSTTAPES,MAXTAPES                                                
         NC    TSTTAPES,JOBTAPES                                                
         BNZ   FMVSJCL1                                                         
FMVSJCL3 CP    JOBEXEC,MAXSTEPS    TEST IF MAXSTEPS EXCEEDED                    
         BL    XIT                                                              
         B     FMVSJCL1                                                         
*                                                                               
PUTSGLC  NTR1                      PUT MVS SINGLE INPUT JCL CARDS               
         MVI   HALF1,0             SET IF PRTY=X DETECTED                       
         L     R1,=A(JCLIN)                                                     
         MVI   OUTSIN,C'Y'                                                      
         CP    MAXSTEPS,=P'1'      IF SINGLE AND MAXSTEPS=1 THEN....            
         BNE   PUTSGLC1                                                         
         L     RF,=A(JJ1)                                                       
         MVC   2(8,R1),2(RF)       OVERRIDE INPUT JOB NAME                      
         LA    R2,8(R1)                                                         
         LA    R0,72               SCAN FOR PRTY=X                              
*                                                                               
PUTSGLCA CLC   0(7,R2),=C',PRTY=X'                                              
         BE    PUTSGLCB                                                         
         LA    R2,1(R2)                                                         
         BCT   R0,PUTSGLCA                                                      
         B     PUTSGLC1                                                         
*                                                                               
PUTSGLCB MVC   HALF1(1),6(R2)      MOVE IN ORIGNINAL CHR (X)                    
         LA    RE,X'FA'            INVERT PRIORITY                              
         LLC   RF,QOUTPRI                                                       
         SR    RE,RF                                                            
         STC   RE,6(R2)                                                         
         OI    6(R2),X'F0'                                                      
*                                                                               
PUTSGLC1 CLI   0(R1),C' '          EXIT ON FIRST BLANK CARD                     
         BE    PUTSGLC2                                                         
         MVC   JCL,0(R1)                                                        
         BAS   RE,PUTJCL                                                        
         LA    R1,80(R1)                                                        
         B     PUTSGLC1                                                         
*                                                                               
PUTSGLC2 MVI   OUTSIN,C'X'         SET END OF SPECIALS                          
         CLI   HALF1,0             TEST PRTY=X OVERWITTEN                       
         BE    *+10                                                             
         MVC   6(1,R2),HALF1       RESTORE                                      
         CLI   TRACE,C'Y'                                                       
         BNE   PUTSGLCX                                                         
         GOTO1 =V(PRINTER)                                                      
PUTSGLCX B     XIT                                                              
*                                                                               
PUTJOBC  NTR1                      PUT MVS JOB CARD                             
         L     R1,=A(JJ1)                                                       
         MVI   OUTJOB,C'Y'                                                      
         B     PUTMVSC                                                          
*                                                                               
PUTJESC  NTR1                      PUT MVS JES CARD                             
         L     R1,=A(JES1)                                                      
         MVI   OUTJES,C'Y'                                                      
         B     PUTMVSC                                                          
*                                                                               
PUTDDC   NTR1                      PUT INPUT DD CARDS                           
         L     R1,=A(DDIN)                                                      
         MVI   OUTDD,C'Y'                                                       
         B     PUTMVSC                                                          
*                                                                               
PUTAFPC  NTR1                      PUT MVS SYSPRINT CARD                        
         L     R1,=A(AFP1)                                                      
         MVI   OUTAFP,C'Y'                                                      
         B     PUTMVSC                                                          
*                                                                               
PUTSPRC  NTR1                      PUT MVS SYSPRINT CARD                        
         L     R1,=A(JP1)                                                       
         MVI   OUTSYP,C'Y'                                                      
*                                                                               
PUTMVSC  CLI   0(R1),C' '          EXIT ON FIRST BLANK CARD                     
         BE    PUTMVSCX                                                         
         MVC   JCL,0(R1)                                                        
         BAS   RE,PUTJCL                                                        
         LA    R1,80(R1)                                                        
         B     PUTMVSC                                                          
*                                                                               
PUTMVSCX B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINES TO READ WORKER FILES                                        *         
***********************************************************************         
INDXJOB  NTR1                                                                   
         L     RF,=A(EODJOBS)                                                   
         GOTO1 =V(WORKER),DMCB,=C'INDEX',(RF),JD                                
         B     INDXCHK                                                          
*                                                                               
INDXREQ  NTR1                                                                   
         L     RF,=A(EODREQS)                                                   
         GOTO1 =V(WORKER),DMCB,=C'INDEX',(RF),QD                                
         B     INDXCHK                                                          
*                                                                               
INDXCHK  SR    R2,R2               TEST FOR ERRORS                              
         ICM   R2,1,DMCB+8         EXIT WITH CC=EQL IF GOOD INDEX READ          
         B     XIT                                                              
*                                                                               
READJOB  NTR1                                                                   
         L     RF,=A(EODJOBS)                                                   
         LR    R2,R7                                                            
         AHI   R2,-4                                                            
         GOTO1 =V(WORKER),DMCB,=C'READ',(RF),JD,(R2)                            
         SR    R2,R2                                                            
         ICM   R2,1,DMCB+8         EXIT WITH CC=EQL IF VALID FILE READ          
         BZ    XIT                                                              
         TM    DMCB+8,X'80'        TEST FOR EOF                                 
         JZ    *+2                                                              
         MVI   EOFSW,C'Y'          SET END OF DATA FLAG                         
         ICM   R2,1,DMCB+8                                                      
         B     XIT                                                              
*                                                                               
READREQ  NTR1                                                                   
         XC    REQIOMAP,REQIOMAP   CLEAR MAP OF CARDS                           
         LA    RF,REQIOMAP                                                      
         ST    RF,REQIOADR         POINT TO FIRST CARD IN MAP                   
         MVI   QCTR,1              INITIALISE MULTI-CARD COUNT                  
*                                                                               
READREQA L     RF,=A(EODREQS)                                                   
         L     R2,=A(REQLEN)                                                    
         GOTO1 =V(WORKER),DMCB,=C'READ',(RF),QD,(R2)                            
*                                                                               
READREQB SR    R2,R2               TEST FOR ERRORS                              
         ICM   R2,1,DMCB+8                                                      
         BZ    READREQC                                                         
         TM    DMCB+8,X'80'        TEST FOR EOF                                 
         JZ    *+2                                                              
         MVI   EOFSW,C'Y'          SET END OF DATA FLAG                         
         B     READREQX                                                         
*                                                                               
READREQC L     R2,=A(REQIO)                                                     
         CLC   0(4,R2),=C'JOB='    TEST JOB= RECORD                             
         BNE   READREQD                                                         
         MVC   REQWHY,SPACES                                                    
         L     RE,=A(REQLEN)                                                    
         CHI   RE,38               TEST IF JOB=RECORD HAS WHY TEXT              
         BNH   READREQX                                                         
         MVC   REQWHY,4+L'QJOBKEY(R2)                                           
         B     READREQX                                                         
*                                                                               
READREQD L     R2,=A(REQIO)                                                     
         LR    RF,R2               FIX FOR A BUG WITH BAD RFHDR=CARD            
         AHI   RF,-4                                                            
         CLC   0(2,RF),=X'0055'    TEST FOR RFHDR LENGTH                        
         BNE   *+10                NO, CANT BE RFHDR                            
         MVC   4(6,RF),=C'RFHDR='  YES,MUST BE RFHDR,MAY BE CORRUPT             
*                                                                               
         CLC   0(6,R2),=C'RFHDR='  TEST RFHDR= RECORD                           
         BNE   READREQE                                                         
         L     RE,=A(REQIOH)                                                    
         ST    RE,REQIOMAP+0                                                    
         MVC   0(80,RE),0(R2)                                                   
         B     READREQA                                                         
*                                                                               
READREQE L     R2,=A(REQLEN)                                                    
         LH    RF,0(R2)                                                         
         SH    RF,=Y(L'QREQINFO+4)                                              
         LA    R2,4(R2)                                                         
         L     RE,=A(REQIO1)                                                    
         LA    R1,REQIOMAP+4                                                    
         LA    R0,QDATAMAX+1                                                    
*                                                                               
READREQF LTR   RF,RF                                                            
         BZ    READREQX                                                         
         ST    RE,0(R1)                                                         
         MVC   0(80,RE),0(R2)                                                   
         LA    R1,4(R1)                                                         
         LA    R2,80(R2)                                                        
         LA    RE,80(RE)                                                        
         AHI   RF,-80                                                           
         BCT   R0,READREQF                                                      
         B     READREQY                                                         
*                                                                               
READREQY DC    H'0'                REQUESTS ALL OUT OF STEP                     
*                                                                               
READREQX ICM   R2,1,DMCB+8         EXIT WITH CC=EQL IF VALID FILE READ          
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*//SYSIN CARD OUTPUT - SEE IF ANY EXTRA DDSIO/DSPACE CARDS TO ADD     *         
*XTRAFLAG X'01'=HAVE OUTPUT DDSIO=.. CARD                             *         
*XTRAFLAG X'02'=HAVE OUTPUT DSPACE=X CARD                             *         
*XTRAFLAG X'10'=HAVE OUTPUT DDSIO=.. CARD THIS CALL                   *         
*XTRAFLAG X'20'=HAVE OUTPUT DSPACE=X CARD THIS CALL                   *         
***********************************************************************         
SYSICARD NTR1                                                                   
         MVI   OUTDD,C'N'          RESET TO PUT OUT DD STATEMENT                
         NI    XTRAFLAG,255-X'0F'  SET NOTHING ADDED THIS CALL                  
         LA    RE,JCL+7                                                         
         LA    R0,20                                                            
SYSIC1   CLC   0(5,RE),=C'DUMMY'   EXIT IF SYSIN DD DUMMY CARD                  
         BE    SYSICX                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,SYSIC1                                                        
*                                                                               
SYSIC2   CLI   SYSDDSIO,C' '       USE SYSDDSIO=CARD IF INPUT                   
         BNH   SYSIC3                                                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(6),=C'DDSIO='                                                
         MVC   JCL+6(8),SYSDDSIO                                                
         BAS   RE,PUTJCL                                                        
         B     SYSIC4                                                           
*                                                                               
SYSIC3   L     RE,=V(DDSIO)        TEST IF USING NON STANDARD DDSIO             
         CLC   0(8,RE),=CL8'DDSIO'                                              
         BE    SYSIC5                                                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(6),=C'DDSIO='                                                
         MVC   JCL+6(8),0(RE)      USE DDSIO=INPUT VALUE                        
         BAS   RE,PUTJCL                                                        
*                                                                               
SYSIC4   OI    XTRAFLAG,X'11'      HAVE OUTPUT A DDSIO=CARD THIS CALL           
         L     RE,=A(LDDSIO)       SAVE LAST GENERATED DDSIO=.. CARD            
         MVC   0(80,RE),JCL                                                     
*                                                                               
SYSIC5   MVC   JCL,SPACES          CREATE DSPACE=X CARD                         
         MVC   JCL(7),=C'DSPACE='                                               
         MVC   JCL+7(1),INDSPACE   USE INPUT DSPACE=X VALUE                     
         CLI   INDSPACE,C' '                                                    
         BNE   SYSIC6                                                           
         MVC   JCL+7(1),OTDSPACE   NO INPUT VALUE SO USE LAST OUTPUT            
*                                                                               
SYSIC6   SR    RE,RE               GET FACPAK ID NUMBER                         
         ICM   RE,1,QJOBFACI                                                    
         BZ    SYSIC7                                                           
         CHI   RE,L'SYSIFACI       TEST VALID FACPAK ID NUMBER                  
         BNL   SYSIC7                                                           
         LA    RE,SYSIFACI(RE)                                                  
         MVC   JCL+7(1),0(RE)      MOVE IN DSPACE FOR FACPAK                    
*                                                                               
SYSIC7   CLI   JCL+7,C'T'                                                       
         BE    SYSIC8              OUTPUT IF TST                                
         CLI   JCL+7,C'C'                                                       
         BE    SYSIC8              OUTPUT IF CSC                                
         CLI   JCL+7,C'Q'                                                       
         BE    SYSIC8              OUTPUT IF FQA                                
*&&US*&& CLI   JCL+7,C'R'                                                       
*&&US*&& BE    SYSIC8              OUTPUT IF REP                                
         B     SYSICX                                                           
*                                                                               
SYSIC8   MVC   OTDSPACE,JCL+7      SAVE OUTPUT DSPACE CHR                       
         BAS   RE,PUTJCL                                                        
         OI    XTRAFLAG,X'22'      HAVE OUTPUT A DSPACE=X THIS CALL             
         L     RE,=A(LDSPACE)      SAVE LAST GENERATED DSPACE=X CARD            
         MVC   0(80,RE),JCL                                                     
*&&US                                                                           
         CLI   JCL+7,C'T'          TEST IF DSPACE=T OUTPUT                      
         BNE   SYSICX                                                           
         TM    XTRAFLAG,X'11'      TEST IF ALSO OUTPUT DDSIO=.. CARD            
         BO    SYSICX                                                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(12),=C'DDSIO=DDSION'                                         
         BAS   RE,PUTJCL                                                        
         OI    XTRAFLAG,X'11'      HAVE OUTPUT A DDSIO=.. CARD                  
         L     RE,=A(LDDSIO)       SAVE LAST GENERATED DDSIO=.. CARD            
         MVC   0(80,RE),JCL                                                     
*&&                                                                             
SYSICX   XIT1                                                                   
*                                                                               
SYSIFACI DS    0CL16               FACPAK ID NUMBER - SEE FACIDTAB              
*&&UK*&& DC    C' TATATATATACAQA '                                              
*&&US*&& DC    C' TAARATAARACAARQ'                                              
         EJECT                                                                  
***********************************************************************         
*REQUESTS HERE CARD - SEE IF ANY EXTRA DDSIO/DSPACE CARDS TO ADD      *         
***********************************************************************         
REQHCARD NTR1                                                                   
         TM    XTRAFLAG,X'03'      EXIT IF ADDED AFTER //SYSIN CARD             
         BNZ   REQHC9                                                           
*                                                                               
REQHC2   CLI   SYSDDSIO,C' '       USE SYSDDSIO=CARD IF INPUT                   
         BNH   REQHC3                                                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(6),=C'DDSIO='                                                
         MVC   JCL+6(8),SYSDDSIO                                                
         BAS   RE,PUTJCL                                                        
         B     REQHC4                                                           
*                                                                               
REQHC3   L     RE,=V(DDSIO)        TEST IF USING NON-STANDARD DDSIO             
         CLC   0(8,RE),=CL8'DDSIO'                                              
         BE    REQHC5                                                           
         MVC   JCL,SPACES          THEN GENERATED JOBS GET THE SAME             
         MVC   JCL(6),=C'DDSIO='                                                
         MVC   JCL+6(8),0(RE)                                                   
         BAS   RE,PUTJCL                                                        
*                                                                               
REQHC4   L     RE,=A(LDDSIO)       SAVE LAST GENERATED DDSIO=.. CARD            
         MVC   0(80,RE),JCL                                                     
*                                                                               
REQHC5   MVC   JCL,SPACES          CREATE DSPACE=X CARD                         
         MVC   JCL(7),=C'DSPACE='                                               
         MVC   JCL+7(1),INDSPACE   USE INPUT DSPACE=X VALUE                     
         CLI   INDSPACE,C' '                                                    
         BNE   REQHC6                                                           
         MVC   JCL+7(1),OTDSPACE   NO INPUT VALUE SO USE LAST OUTPUT            
*                                                                               
REQHC6   SR    RE,RE               GET FACPAK ID NUMBER                         
         ICM   RE,1,QJOBFACI                                                    
         BZ    REQHC7                                                           
         CHI   RE,L'REQHFACI       TEST VALID FACPAK ID NUMBER                  
         BNL   REQHC7                                                           
         LA    RE,REQHFACI(RE)                                                  
         MVC   JCL+7(1),0(RE)      MOVE IN DSPACE FOR FACPAK                    
*                                                                               
REQHC7   CLI   JCL+7,C'T'                                                       
         BE    REQHC8              OUTPUT FOR TEST SYSTEM                       
         CLI   JCL+7,C'C'                                                       
         BE    REQHC8              OUTPUT FOR CSC SYSTEM                        
         CLI   JCL+7,C'Q'                                                       
         BE    REQHC8              OUTPUT FOR FQA SYSTEM                        
*&&US*&& CLI   JCL+7,C'R'                                                       
*&&US*&& BE    REQHC8              OUTPUT FOR REP SYSTEM                        
         B     REQHC9                                                           
*                                                                               
REQHC8   MVC   OTDSPACE,JCL+7      SAVE OUTPUT DSPACE CHR                       
         BAS   RE,PUTJCL                                                        
         L     RE,=A(LDSPACE)      SAVE LAST GENERATED DSPACE=X CARD            
         MVC   0(80,RE),JCL                                                     
*                                                                               
REQHC9   EQU   *                   EXTRA DATASPACE CARDS                        
*&&UK                                                                           
REQHC9A  CLI   INMEDDSP,C' '       TEST IF MEDDSPC=XXXX CARD INPUT              
         BNH   REQHC9X                                                          
         CLI   QSYSTEM,C'M'        ONLY ACTIVE FOR MED AND MPL                  
         BE    REQHC9B                                                          
         CLI   QSYSTEM,C'L'                                                     
         BNE   REQHC9X                                                          
REQHC9B  CLI   INDSPACE,C'T'       ONLY ACTIVE FOR TEST SYSTEMS                 
         BE    REQHC9C                                                          
         CLI   INDSPACE,C'Q'                                                    
         BE    REQHC9C                                                          
         CLI   INDSPACE,C'C'                                                    
         BNE   REQHC9X                                                          
REQHC9C  MVC   JCL,SPACES                                                       
         MVC   JCL(8),=C'MEDDSPC='                                              
         MVC   JCL+8(4),INMEDDSP                                                
         BAS   RE,PUTJCL                                                        
REQHC9X  EQU   *                                                                
*&&                                                                             
REQHCX   XIT1                                                                   
REQHFACI DS    0CL16               FACPAK ID NUMBER - SEE FACIDTAB              
*&&UK*&& DC    C' TATATATATACAQA '                                              
*&&US*&& DC    C' TAARATAARACATRQ'                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO READ JCL STATEMENTS FROM CONTROL FILE/PAN LIBRARY         *         
***********************************************************************         
JCLOUT   NTR1                                                                   
         LLC   R2,JCLPARM          SET TO 0,1,2,3                               
         MVI   JCLIF,C'Y'          PRESET CONDITIONAL SWITCH                    
         MVC   NOTFJCL,SPACES                                                   
         BAS   RE,SETSUB                                                        
         CLI   JCLPARM,1           TEST JCL THAT PRECEEDS REQUESTS              
         BNE   JCLOUT10                                                         
*                                                                               
         MVI   SYSPMODE,C' '       SET SYSPRINT MODE TO NORMAL                  
         CLI   SYSPOUT,C'O'                                                     
         BNE   *+8                                                              
         MVI   SYSPMODE,C'O'       SET IF SYSPRINT=OUTPUT                       
         CLI   AFP,C' '                                                         
         BE    *+8                                                              
         MVI   SYSPMODE,C'O'       SET IF AFP=XXXXXXXX                          
         CLI   SYSPOUT,C'D'                                                     
         BNE   *+8                                                              
         MVI   SYSPMODE,C'D'       SET IF SYSPRINT=DUMMY                        
*                                                                               
         XC    DIRDEST,DIRDEST     CLEAR DIRECT DEST OVER-RIDE                  
         XC    ARCDEST,ARCDEST     CLEAR ARC    DEST OVER-RIDE                  
         MVI   OUTJOB,C'N'         SET MVS JOB CARD NOT OUTPUT                  
         MVI   OUTDD,C'N'          SET MVS ## DD CARDS NOT OUTPUT               
         MVI   OUTAFP,C'N'         SET MVS AFP CARD NOT OUTPUT                  
         MVI   OUTSYP,C'N'         SET MVS SYSPRINT CARD NOT OUTPUT             
         ZAP   FSTEXEC,=P'0'       SET MVS FIRST EXEC CARD                      
         ZAP   OUTCRDN,=P'0'       SET MVS FIRST OUTPUT CARD NUMBER             
         ZAP   OUTCRDF,=P'0'                                                    
         ZAP   OUTCRDL,=P'0'                                                    
         MVI   THSJCL,0            SET MVS THIS JCL CARD FLAG                   
         MVI   LSTJCL,0            SET MVS LAST JCL CARD FLAG                   
         MVI   XTRAFLAG,0          SET EXTRA DDSIO/DSPACE CARD FLAG             
         MVC   STEPNAME,SPACES     SET MVS STEPNAME                             
         MVC   STEPNAMF,SPACES     SET MVS STEPNAME FIRST                       
         MVC   STEPNAMP,SPACES     SET MVS STEPNAME PREVIOUS                    
         L     RE,=A(LDIRECT)      SET LAST DIRECT=... CARD TO NONE             
         MVI   0(RE),C' '                                                       
         L     RE,=A(LORIGIN)      SET LAST ORIGIN=... CARD TO NONE             
         MVI   0(RE),C' '                                                       
         L     RE,=A(LLOGO)        SET LAST LOGO=... CARD TO NONE               
         MVI   0(RE),C' '                                                       
         L     RE,=A(LDDSIO)       SET LAST DDSIO=.. CARD TO NONE               
         MVI   0(RE),C' '                                                       
         L     RE,=A(LDSPACE)      SET LAST DSPACE=. CARD TO NONE               
         MVI   0(RE),C' '                                                       
         BAS   RE,IMVSJCL          INITIALISE MVS JCL                           
         BAS   RE,PUTJOBC          RELEASE MVS // JOB CARDS                     
*                                                                               
JCLOUT10 CLC   JCL(13),=C'REQUESTS HERE'                                        
         BNE   JCLOUT20                                                         
         MVC   JCL,SPACES          IF PREVIOUSLY MERGING REQUESTS               
         MVC   JCL(2),=C'**'                                                    
         BAS   RE,PUTJCL           SET END OF REQUESTS                          
*                                                                               
JCLOUT20 L     R4,=A(BOOKBUFF)                                                  
         USING CTJREC,R4                                                        
         CLI   JCLPARM,1           TEST IF NEED TO SET UP JCL KEY               
         BH    JCLOUT30                                                         
         MVC   PANBOOK(2),=C'JC'   MVS SET KEY FOR PAN LIBRARY                  
         MVC   PANBOOK+2(8),THISJCL                                             
*                                                                               
JCLOUT30 MVC   JCL,SPACES                                                       
         GOTO1 =V(PANIC),PARA,(X'80',=C'READ'),=C'PAN',PANBOOK,JCL              
         TM    PARA+8,X'10'        WAS BOOK FOUND OK                            
         BZ    JCLOUT36                                                         
         CLC   NOTFJCL,SPACES      NO SAVE NAME OF FIRST ATTEMPT                
         BNE   *+10                                                             
         MVC   NOTFJCL,THISJCL                                                  
         CLI   JCLPARM,1           TEST IF JCL FOR REQUEST                      
         BNE   JCLOUT36                                                         
         CLC   THISJCL+3(2),=C'00' TEST IF DEFAULT PROGRAM                      
         BE    JCLOUT36                                                         
         MVC   THISJCL+3(2),=C'00' TRY DEFAULT PROGRAM                          
         B     JCLOUT20                                                         
*                                                                               
JCLOUT36 MVC   JCL+72(8),SPACES    SEQUENCE NUMBERS LOOK MESSY                  
         TM    PARA+8,X'80'        CHECK END OF BOOK                            
         BO    JCLOUT38                                                         
         CLI   PARA+8,0            ANY ERROR CONDITIONS                         
         BE    JCLOUT40                                                         
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         MVC   P(15),=C'PAN ERROR BOOK='                                        
         MVC   P+15(10),NOTFJCL                                                 
         BASR  RE,RF                                                            
         MVC   P(10),=C'NOT FOUND '                                             
         TM    PARA+8,X'10'                                                     
         BO    *+10                                                             
         MVC   P(10),=C'DISK ERROR'                                             
         BASR  RE,RF                                                            
         BASR  RE,RF                                                            
*                                                                               
JCLOUT38 CLI   JCLPARM,0           END OF JCL BOOK                              
         BE    NOTOK                                                            
         LA    RF,FMVSJCL          POINT TO FINAL JCL ROUTINE                   
         BASR  RE,RF               WRAP UP JCL FOR 1,2,3                        
         B     NOTOK                                                            
*                                                                               
JCLOUT40 OC    JCL,SPACES          PROCESS JCL CARD                             
         CLC   JCL,SPACES                                                       
         BE    JCLOUT30            IGNORE BLANK CARD                            
         CLC   JCL(7),=C'SPACEND'  TEST IF REP SPACEND CARD                     
         BNE   JCLOUT44                                                         
         CLI   REPSPACE,C'N'       IF SO, WRAP CONTROL CARD CAN STOP IT         
         BE    JCLOUT30                                                         
*                                                                               
JCLOUT44 MVI   RCMSW,C'N'                                                       
         CLC   JCL(13),=C'REQUESTS HERE'                                        
         BE    EXTRA               SPECIAL CARD TELLS US TO GET OUT             
         MVI   RCMSW,C'O'                                                       
         CLC   JCL(8),=C'OLD SPOT REQUESTS'                                     
         BE    EXTRA3                                                           
*                                                                               
         CLC   JCL(12),=C'DESTINATION='                                         
         BNE   JCLOUT46                                                         
         MVC   JCL+12(33),QJOBDNAM SUBSTITUTION FOR DESTINATION CARD            
         MVC   JCL+45(33),QJOBDADD                                              
*                                                                               
JCLOUT46 CLC   JCL(6),=C'DEST2='                                                
         BNE   JCLOUT48                                                         
         MVC   JCL+6(33),QJOBDAD2  SUBSTITUTION FOR DEST2 CARD                  
         MVC   JCL+39(33),QJOBDAD3                                              
*                                                                               
JCLOUT48 CLC   JCL(05),=C'LOGO='                                                
         BNE   JCLOUT50                                                         
         MVC   JCL+5(7),QJOBLOG1   SUBSTITUTION FOR LOGO CARD                   
         MVC   JCL+12(7),QJOBLOG2                                               
         MVC   JCL+19(8),QJOBJES                                                
         CLI   QJOBDUPS,C' '       TEST IF DUPLICATE SYSTEMS LETTER             
         BNH   *+10                                                             
         MVC   JCL+23(1),QJOBDUPS                                               
         EDIT  (2,QDEST),(5,JCL+27),FILL=0                                      
         MVC   JCL+33(7),=C'ORIGIN='                                            
         EDIT  (2,QORIGIN),(5,JCL+40),FILL=0                                    
*                                                                               
JCLOUT50 CLC   JCL(07),=C'DITTOD=' DITTO LAST GENERATED DIRECT=... CARD         
         BNE   JCLOUT51                                                         
         L     RE,=A(LDIRECT)                                                   
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   DUB(3),JCL+7        SAVE PQ REPORT NAME                          
         MVC   JCL,0(RE)                                                        
         CLI   DUB,C' '                                                         
         BE    *+10                                                             
         MVC   JCL+7(3),DUB        SET REPORT ID FROM DITTOD=XYZ CARD           
         B     JCLOUT60                                                         
*                                                                               
JCLOUT51 CLC   JCL(07),=C'DITTOA=' DITTO LAST GENERATED ARC=... CARD            
         BNE   JCLOUT52                                                         
         L     RE,=A(LARC)                                                      
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   DUB(3),JCL+7        SAVE ARC SPP VALUE                           
         MVC   JCL,0(RE)                                                        
         CLI   DUB,C'*'            TEST IF WANT SAME SYSTEM LETTER              
         BNE   *+14                                                             
         MVC   JCL+8(2),DUB+1      SET ARC PP FROM DITTO=*PP CARD               
         B     JCLOUT60                                                         
         CLI   DUB,C' '                                                         
         BE    *+10                                                             
         MVC   JCL+7(3),DUB        SET ARC SPP FROM DITTOA=SPP CARD             
         B     JCLOUT60                                                         
*                                                                               
JCLOUT52 CLC   JCL(07),=C'DITTOO=' DITTO LAST GENERATED ORIGIN=... CARD         
         BNE   JCLOUT53                                                         
         L     RE,=A(LORIGIN)                                                   
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   JCL,0(RE)                                                        
         B     JCLOUT60                                                         
*                                                                               
JCLOUT53 CLC   JCL(07),=C'DITTOL=' DITTO LAST GENERATED LOGO=... CARD           
         BNE   JCLOUT54                                                         
         L     RE,=A(LLOGO)                                                     
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   JCL,0(RE)                                                        
         B     JCLOUT60                                                         
*                                                                               
JCLOUT54 CLC   JCL(07),=C'DITTOI=' DITTO LAST GENERATED DDSIO=. CARD            
         BNE   JCLOUT55                                                         
         L     RE,=A(LDDSIO)                                                    
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   JCL,0(RE)                                                        
         B     JCLOUT60                                                         
*                                                                               
JCLOUT55 CLC   JCL(07),=C'DITTOS=' DITTO LAST GENERATED DSPACE=. CARD           
         BNE   JCLOUT57                                                         
         L     RE,=A(LDSPACE)                                                   
         CLI   0(RE),C' '                                                       
         BE    JCLOUT30            IGNORE IF NOT GENERATED PREVIOUSLY           
         MVC   JCL,0(RE)                                                        
         B     JCLOUT60                                                         
*                                                                               
JCLOUT57 CLC   JCL(08),=C'DIRDEST='   OVERRIDE DEST IN DIRECT= CARD             
         BNE   JCLOUT58                                                         
         CLI   JCLIF,C'N'          PUT OUT JCL                                  
         BE    JCLOUT30            NO                                           
         LA    R2,JCL+8                                                         
         LA    R3,TEMPPACK                                                      
         BRAS  RE,NUMBER                                                        
         BNE   *+8                 IGNORE THIS - BAD DIRDEST=                   
         STCM  RF,3,DIRDEST        SAVE OVER-RIDE FOR ANYDIR CALL               
         MVC   JCLSAVE,JCL                                                      
         MVI   JCL,C'*'            COMMENT IT OUT                               
         MVC   JCL+1(L'JCL-1),JCLSAVE                                           
         B     JCLOUT60            SET DIRDEST, SHOW BUT COMMENT                
*                                                                               
JCLOUT58 CLC   JCL(08),=C'ARCDEST='   OVERRIDE DEST IN DIRECT= CARD             
         BNE   JCLOUT59                                                         
         CLI   JCLIF,C'N'          PUT OUT JCL                                  
         BE    JCLOUT30            NO                                           
         LA    R2,JCL+8                                                         
         LA    R3,TEMPPACK                                                      
         BRAS  RE,NUMBER                                                        
         BNE   *+8                 IGNORE THIS - BAD ARCDEST=                   
         STCM  RF,3,ARCDEST        SAVE OVER-RIDE FOR ANYDIR CALL               
         MVC   JCLSAVE,JCL                                                      
         MVI   JCL,C'*'            COMMENT IT OUT                               
         MVC   JCL+1(L'JCL-1),JCLSAVE                                           
         B     JCLOUT60            SET ARCDEST, SHOW BUT COMMENT NOW            
*                                                                               
JCLOUT59 DS    0H                                                               
*&&US                                                                           
         CLC   JCL(9),=C'LOAD=AC21'                                             
         BNE   JCLOUT60                                                         
         TM    ACSTA,CPYSADBL      TEST USES NEW BILLING                        
         BNO   JCLOUT60                                                         
         MVC   JCL(9),=C'LOAD=ACNB'                                             
*&&                                                                             
JCLOUT60 L     RF,=A(SUBTABLE)     CHECK FOR USER SELECTED SCAN/REPS            
         OC    0(4,RF),0(RF)       TEST NO VALUES IN TABLE                      
         BZ    JCLOUT62                                                         
         GOTO1 =V(REPLACE),PARA,(80,JCL),('SUBTABL',(RF))                       
*                                                                               
JCLOUT62 L     RF,=A(SOFTABLE)     NO LOOK FOR SOFT JCL                         
         OC    0(4,RF),0(RF)       TEST NO VALUES IN TABLE                      
         BZ    JCLOUT64                                                         
         GOTO1 =V(REPLACE),PARA,(80,JCL),(RF)                                   
*                                                                               
JCLOUT64 L     RF,LOADADDR                                                      
         GOTO1 (RF),PARA,JCL       GO TO OPTIONAL LOADED FIX                    
         CLC   JCL(6),=C'IF END'   CHECK FOR CONDITIONAL END                    
         BNE   JCLOUT66                                                         
         MVI   JCLIF,C'Y'                                                       
         B     JCLOUT30                                                         
*                                                                               
JCLOUT66 CLC   JCL(3),=C'IF '      CHECK FOR IF CONDITIONAL                     
         BNE   JCLOUT68                                                         
         MVI   JCLIF,C'N'                                                       
         B     IF                                                               
*                                                                               
JCLOUT68 CLC   JCL(3),=C'OR '      CHECK FOR OR CONDITIONAL                     
         BE    IF                                                               
         CLC   JCL(4),=C'AND '     CHECK FOR AND CONDITION                      
         BNE   JCLOUT70                                                         
         CLI   JCLIF,C'N'          PREVIOUS IF CONDITION MUST                   
         BE    JCLOUT30            HAVE BEEN SATISFIED                          
         MVC   JCL(79),JCL+1       MAKE IT NOW ND CONDITION                     
         MVI   JCLIF,C'N'                                                       
         B     IF                                                               
*                                                                               
JCLOUT70 CLC   JCL(08),=C'LOGDATA='                                             
         BNE   JCLOUT72                                                         
         MVC   JCL(1),JCL+8        GENERATE DATA CARD FOR S/A LOGO              
         EDIT  (2,QDEST),(5,JCL+1),FILL=0                                       
         MVC   JCL+6(8),QJOBJES                                                 
*                                                                               
JCLOUT72 CLC   JCL(8),=C'ORIGIN= '                                              
         BNE   JCLOUT76                                                         
         MVC   JCL+7(33),QJOBONAM                                               
         MVC   JCL+40(33),QJOBOADD                                              
*                                                                               
JCLOUT76 CLC   JCL(16),=CL16'CHECKPOINT'                                        
         BNE   JCLOUT80                                                         
         MVC   JCL+15(22),=C'PAGES=500,TIME=30,KEY='                            
         MVC   JCL+37(8),QJOBJES                                                
*                                                                               
JCLOUT80 CLI   JCLIF,C'Y'          IS SELECTIVE JCL IN OPERATION                
         BNE   JCLOUT30                                                         
         BAS   RE,JCLCHK           CHECK MVS JCL                                
         BNE   JCLOUT30            AND BYPASS IF DELETED                        
         BAS   RE,PUTJCL                                                        
*&&US*&& CLC   JCL(7),=C'//SYSIN'  CHECK IF JUST OUTPUT //SYSIN CARD            
*&&US*&& BNE   JCLOUT30                                                         
*&&US*&& BAS   RE,SYSICARD         TEST TO OUTPUT DDSIO/DSPACE CARDS            
         B     JCLOUT30                                                         
         EJECT                                                                  
***********************************************************************         
*EXAMINE EACH JCL STATEMENT                                           *         
***********************************************************************         
JCLCHK   NTR1                                                                   
         MVI   DELJCL,0            SET DO NOT DELETE FLAG                       
         MVI   THSJCL,0                                                         
*                                                                               
JCLCHKA  CLI   OUTSIN,C'Y'         EXIT IF SPECIAL                              
         BE    JCLCHKX                                                          
         CLC   JCL(2),=C'//'       TEST IF JCL STATEMENT                        
         BNE   JCLCHKX                                                          
         CLI   JCL+2,C'*'                                                       
         BE    JCLCHKX                                                          
         CLC   JCL+2(40),SPACES                                                 
         BE    JCLCHKX                                                          
*                                                                               
         LA    RE,JCL+2            TEST IF EXEC CARD                            
         LA    R0,16                                                            
JCLCHKAZ CLC   0(6,RE),=C' EXEC '                                               
         BE    JCLCHKA1                                                         
         CLC   0(5,RE),=C' NJB '                                                
         BE    JCLCHKA0                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,JCLCHKAZ                                                      
         B     JCLCHKC                                                          
*                                                                               
JCLCHKA0 CLC   JCL+2(5),=C'XXXXX'  TEST IF I CREATE NJB NAME                    
         BNE   JCLCHKC                                                          
         MVC   JCL+2(8),QJOBJES    SET JOB NAME                                 
         CLI   JCL+9,C' '                                                       
         BE    JCLCHKC                                                          
         CLI   JCL+10,C' '                                                      
         BE    JCLCHKC                                                          
         MVC   WORK(70),JCL+10     JOB NAME TOO LONG                            
         MVC   JCL+10(70),SPACES                                                
         MVC   JCL+11(69),WORK                                                  
         B     JCLCHKC                                                          
*                                                                               
JCLCHKA1 CLI   ACCEMU,C'E'         CHANGE MONACC TO ACCEMU                      
         BNE   JCLCHKA2                                                         
         ST    RE,SAVERE           SAVE CURRENT LOCATION                        
         BRAS  RE,CHMONACC                                                      
         L     RE,SAVERE                                                        
*                                                                               
JCLCHKA2 CLI   SPONVER,C' '        CHANGE SPONSOR VERSION                       
         BNH   JCLCHKA5                                                         
         CLC   SPONAGY,QUSER       FOR USER ONLY                                
         BNE   JCLCHKA5                                                         
         ST    RE,SAVERE           SAVE CURRENT LOCATION                        
         BRAS  RE,CHSPON                                                        
         L     RE,SAVERE                                                        
*                                                                               
JCLCHKA5 MVC   STEPNAMP,STEPNAME   SAVE STEPNAME PREVIOUS                       
         MVC   STEPNAME,JCL+2      SAVE STEPNAME                                
         AP    FSTEXEC,=P'1'       BUMP EXEC CARD WITHIN JOB                    
         AP    JOBEXEC,=P'1'       BUMP EXEC CARD WITHIN MAXSTEPS               
         AP    TOTEXEC,=P'1'       BUMP EXEC CARD WITHIN WRAP                   
         MVI   OUTSYP,C'N'                                                      
         CP    FSTEXEC,=P'1'                                                    
         BNE   *+10                                                             
         MVC   STEPNAMF,STEPNAME   SAVE STEPNAME FIRST                          
         CLC   JCL+2(5),=C'XXXXX'  TEST IF I CREATE STEP NAME                   
         BNE   JCLCHKX                                                          
*                                                                               
         MVC   JCL+2(4),QJOBJES    SET STEP NAME AND NUMBER                     
         UNPK  JCL+6(3),FSTEXEC                                                 
         OI    JCL+8,C'0'                                                       
         MVC   STEPNAME,JCL+2                                                   
         CLI   SINGLE,0            TEST IF SINGLE JOB                           
         BE    JCLCHKX             NO                                           
*                                                                               
         MVC   JCL+2(8),QJOBJES    SET FIRST STEP NAME TO JOB NAME              
         CLI   JCL+9,C' '                                                       
         BE    JCLCHKA8                                                         
         CLI   JCL+10,C' '                                                      
         BE    JCLCHKA8                                                         
         MVC   WORK(70),JCL+10     STEP NAME TOO LONG                           
         MVC   JCL+10(70),SPACES                                                
         MVC   JCL+11(69),WORK                                                  
*                                                                               
JCLCHKA8 CP    FSTEXEC,=P'1'                                                    
         BE    JCLCHKA9                                                         
         UNPK  JCL+6(3),TOTEXEC    SET OTHER STEP NAMES TO ABS NUM              
         OI    JCL+8,C'0'                                                       
         MVI   JCL+9,C' '                                                       
*                                                                               
JCLCHKA9 MVC   STEPNAME,JCL+2      SAVE STEPNAME                                
         CP    FSTEXEC,=P'1'                                                    
         BNE   *+10                                                             
         MVC   STEPNAMF,STEPNAME   SAVE STEPNAME OF FIRST MULTI STEP            
*                                                                               
JCLCHKB  CLI   SINGLE,0            EXIT IF NOT SINGLE JOB WRAP                  
         BE    JCLCHKX                                                          
         MVC   CONDPARM,CONDPPRO   SET COND/ACCT PARMS FOR PROC=                
         MVC   ACCTPARM,ACCTPPRO                                                
         LA    RE,6(RE)            FIND END OF EXEC CARD DATA                   
         LA    R0,10                                                            
JCLCHKB1 CLI   0(RE),C' '                                                       
         BNE   JCLCHKB2                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,JCLCHKB1                                                      
         B     JCLCHKX                                                          
JCLCHKB2 CLI   0(RE),C' '                                                       
         BE    JCLCHKB3                                                         
         CLC   0(4,RE),=C'PGM='                                                 
         BNE   *+16                                                             
         MVC   CONDPARM,CONDPPGM   SET COND/ACCT PARMS FOR PGM=                 
         MVC   ACCTPARM,ACCTPPGM                                                
         CLC   0(6,RE),CONDPPRO    TEST IF CARD ALREAD HAS COND=                
         BE    JCLCHKB4                                                         
         CLC   0(6,RE),CONDPPGM                                                 
         BE    JCLCHKB4                                                         
         LA    RE,1(RE)                                                         
         B     JCLCHKB2                                                         
JCLCHKB3 LR    RF,RE               TEST IF THIS IS SINGLE EXEC CARD             
         BCTR  RF,0                                                             
         CLI   0(RF),C','                                                       
         BE    JCLCHKX                                                          
         MVC   0(11,RE),CONDPARM                                                
         BAS   RE,PUTJCL                                                        
         L     RE,=A(MVSEOJ)                                                    
         MVC   JCL,0(RE)                                                        
         MVC   JCL+15(05),ACCTPARM                                              
         L     RF,=A(JJ1)                                                       
         MVC   JCL+20(9),15(RF)    EXTRACT ACCT INFO FROM JOB CARD              
         B     JCLCHKX                                                          
JCLCHKB4 B     JCLCHKX                                                          
*                                                                               
JCLCHKC  L     RE,=A(MVSSYP)       TEST IF //SYSPRINT DD CARD                   
         CLC   JCL(11),0(RE)                                                    
         BNE   JCLCHKD                                                          
         LA    RF,JCL+11           SEARCH FOR SYSOUT PARM                       
         LA    R0,10                                                            
         CLC   0(7,RF),=C'SYSOUT=(C,,'                                          
         BE    *+16                                                             
         LA    RF,1(RF)                                                         
         BCT   R0,*-14                                                          
         B     JCLCHKD                                                          
         LA    RF,7(RF)                                                         
         CLI   0(RF),C'('          NON BASICS HAVE SYSOUT=(C,,...               
         BE    JCLCHKCA                                                         
         CLC   1(20,RF),SPACES     TEST FOR EMPTY BASIC CARD                    
         BNE   JCLCHKC2                                                         
*                                                                               
JCLCHKC1 CLI   OUTSYP,C'Y'         EMPTY BASIC SYSPRINT CARD                    
         BE    JCLCHDEL                                                         
         L     RE,=A(JP1)                                                       
         CLI   0(RE),C' '          TEST IF HAVE REAL CARD SAVED                 
         BE    JCLCHKCA            NO                                           
         BAS   RE,PUTJESC          OUTPUT ANY SAVED JES CARDS                   
         BAS   RE,PUTAFPC          OUTPUT ANY SAVED OUTPUT CARDS                
         BAS   RE,PUTSPRC          OUTPUT REAL SYSPRINT CARD                    
         B     JCLCHDEL                                                         
*                                                                               
JCLCHKC2 L     RE,=A(JP1)          NON EMPTY BASIC CONTAINS LASER DATA          
         CLI   0(RE),C' '                                                       
         BE    JCLCHKCA            OUTPUT THIS CARD IF NO REAL SYSPRINT         
         LA    RE,15(RE)                                                        
         CLI   0(RE),C' '          SET CONTINUATION IF NECESSARY                
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         BCTR  RE,0                                                             
         CLI   0(RE),C','                                                       
         BE    *+8                                                              
         MVI   1(RE),C','                                                       
*                                                                               
JCLCHKC3 L     RE,=A(JP2)          INITIALISE SAVE CONTINUATION CARD            
         MVC   0(80,RE),SPACES                                                  
         MVC   0(2,RE),=C'//'                                                   
         LA    R1,JCL+67           SET LEN FOR EXECUTED MOVE                    
         SR    R1,RF                                                            
         BM    JCLCHKC4                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   15(0,RE),2(RF)      COPY LASER PARMS                             
*                                                                               
JCLCHKC4 CLI   OUTSYP,C'Y'         HAS SYSPRINT CARD BEEN OUTPUT                
         BE    JCLCHDEL            YES THEN DELETE THIS BASIC                   
         BAS   RE,PUTJESC          OUTPUT ANY SAVED JES CARDS                   
         BAS   RE,PUTAFPC          OUTPUT ANY SAVED OUTPUT CARDS                
         BAS   RE,PUTSPRC          OUTPUT NEW REAL SYSPRINT CARD                
         B     JCLCHDEL                                                         
*                                                                               
JCLCHKCA CLI   OUTSYP,C'Y'         HAS SYSPRINT CARD BEEN OUTPUT                
         BNE   *+12                NO                                           
         OI    THSJCL,X'11'        YES SET DELETED SYSPRINT CARD                
         B     JCLCHDEL                                                         
         L     RE,=A(JP1)          SAVE NEW //SYSPRINT CARD                     
         MVC   0(80,RE),JCL                                                     
         MVC   80(80,RE),SPACES                                                 
         MVI   OUTSYP,C'Y'                                                      
         OI    THSJCL,X'10'        SET THIS JCL IS MVS //SYSPRINT               
         B     JCLCHKX                                                          
*                                                                               
JCLCHKD  TM    LSTJCL,X'10'        TEST IF LAST CARD WAS //SYSPRINT             
         BZ    JCLCHKE                                                          
         CLI   JCL+2,C' '          IS THIS A CONTINUATION                       
         BNE   JCLCHKE                                                          
         TM    LSTJCL,X'01'        TEST IF WE DELETED LAST CARD                 
         BO    JCLCHDEL                                                         
         L     RE,=A(JP2)          SAVE CONTINUATION                            
         MVC   0(80,RE),JCL                                                     
         B     JCLCHKX                                                          
JCLCHKE  B     JCLCHKX                                                          
*                                                                               
JCLCHDEL MVI   DELJCL,1            DELETE THIS JCL CARD                         
         B     JCLCHKX                                                          
JCLCHKX  MVC   LSTJCL,THSJCL       SAVE JCL FLAG                                
         CLI   DELJCL,0            EXIT WITH CC=EQL IF OK CARD                  
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO SET UP THE SUBSIDIARY SCAN TABLE                          *         
***********************************************************************         
SETSUB   NTR1                                                                   
         L     RF,=A(SOFTABLE)                                                  
         USING SOFTABLE,RF                                                      
*                                                                               
         MVC   SOFTMED(1),QSUBUSER                                              
*        MVC   SOFTCOMP(1),QUSER                                                
         CLI   QSYSTEM,C'A'                                                     
         BNE   *+8                                                              
         BRAS  RE,GETUSER                                                       
         MVC   SOFTAGY(2),QUSER                                                 
*                                                                               
SETSUB3  MVC   SOFTLEDG(1),QSUBUSER                                             
         MVC   SOFTREP(2),QUSER                                                 
         LA    R1,TODAY                                                         
*&&UK*&& MVC   SOFTDAY,0(R1)                                                    
*&&US*&& MVC   SOFTDAY,3(R1)                                                    
         MVC   SOFTIPL(8),0(R1)                                                 
*&&UK                                                                           
         MVC   SOFTIPLM(8),TODAYM1                                              
         MVC   SOFTIPLP(8),TODAYP1                                              
*&&                                                                             
         MVC   SOFTPROG(2),QPROGRAM                                             
         MVC   SOFTSYS(3),SYSTEM                                                
         MVC   SOFTLOG1(7),QJOBLOG1                                             
         MVC   SOFTLOG2(7),QJOBLOG2                                             
         EDIT  (2,QORIGIN),(5,SOFTORIG),FILL=0                                  
         EDIT  (2,QDEST),(5,SOFTDEST),FILL=0                                    
*                                                                               
         LA    RE,SOFTCOMP                                                      
         ST    RE,PARA                                                          
         LA    RE,SOFTHEX                                                       
         ST    RE,PARA+4                                                        
         DROP  RF                                                               
         GOTO1 =V(HEXOUT),PARA,,,1                                              
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO OUTPUT REQUESTS AND JCL                                   *         
***********************************************************************         
PUTJCL   NTR1                      OUTPUT JCL CARD                              
         CLI   JOBIND,C'E'                                                      
         BE    XIT                 EXIT IF EXCLUDING THIS JOB                   
         MVC   LPUTTYP,PUTTYP      SAVE LAST PUTTYP                             
         MVI   PUTTYP,X'01'                                                     
         MVC   P,SPACES                                                         
         MVC   JESIOA,JCL                                                       
*                                                                               
PUTJCL0  CLC   FRSTNAME,SPACES     WAS FIRST JOB NAME INPUT                     
         BE    PUTJCL0B                                                         
         CLI   FRSTNAMF,C'Y'       TEST IF WE ALREADY DID THIS                  
         BNE   PUTJCL0B                                                         
         CLC   JCL(2),=C'//'       TEST IF JCL STATEMENT                        
         BNE   PUTJCL0B                                                         
         LA    RE,JCL+2            TEST IF JOB CARD                             
         LA    R0,16                                                            
         CLC   0(5,RE),=C' JOB '                                                
         BE    PUTJCL0A                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,*-14                                                          
         B     PUTJCL0B                                                         
*                                                                               
PUTJCL0A MVI   FRSTNAMF,C'N'       SET NOT FIRST JOB CARD ANYMORE               
         MVC   JCL+2(8),FRSTNAME   AND SET INPUT FIRST NAME                     
         MVC   JESIOA+2(8),JCL+2                                                
*                                                                               
PUTJCL0B CLI   NETID,C' '          TEST IF JES NETWORK REQUIRED                 
         BE    PUTJCL3                                                          
         TM    NETFRST,X'02'       TEST IF SPECIAL LAST CALL                    
         BO    PUTJCL2                                                          
         CLC   JCL(2),=C'//'       TEST IF JCL STATEMENT                        
         BNE   PUTJCL3                                                          
         CLI   JCL+2,C'*'                                                       
         BE    PUTJCL3                                                          
         CLC   JCL+2(40),SPACES    TEST EOJ STATEMENT                           
         BNE   PUTJCL1                                                          
         OI    NETEOJ,X'01'        SET EOJ CARD HELD FOR NETWORK CARD           
         B     XIT                                                              
*                                                                               
PUTJCL1  LA    RE,JCL+2            TEST IF JOB CARD                             
         LA    R0,16                                                            
         CLC   0(5,RE),=C' JOB '                                                
         BE    PUTJCL2                                                          
         LA    RE,1(RE)                                                         
         BCT   R0,*-14                                                          
         B     PUTJCL3                                                          
*                                                                               
PUTJCL2  MVC   JCLSAVE,JCL         SAVE THIS JOB CARD                           
         TM    NETEOJ,X'01'                                                     
         BZ    PUTJCL3             EXIT IF NO EOJ CARD PENDING                  
         MVC   JCL,SPACES                                                       
         LA    RE,JCL                                                           
         L     RF,=A(JESNET)                                                    
         MVC   0(18,RE),0(RF)                                                   
         MVC   18(8,RE),NETID      SET NETWORK ID                               
         LA    RE,18(RE)                                                        
         CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         MVC   0(4,RE),=C',HC='                                                 
         LA    RE,4(RE)                                                         
         MVC   0(2,RE),=C'01'                                                   
PUTJCL2A TM    NETFRST,X'01'       TEST AND SET FIRST JOB                       
         BO    *+14                                                             
         MVC   0(2,RE),NETHC       HOLD COUNT FOR FIRST JOB                     
         OI    NETFRST,X'01'                                                    
         LA    RE,2(RE)                                                         
         CLI   NETOH,C'Y'          OH=YES REQUIRED                              
         BNE   *+14                                                             
         MVC   0(7,RE),=C',OH=YES'                                              
         LA    RE,7(RE)                                                         
         TM    NETFRST,X'02'       TEST LAST JOB                                
         BO    PUTJCL2B                                                         
         MVC   0(4,RE),=C',RL='                                                 
         MVC   4(8,RE),JCLSAVE+2   SET JOB NAME FROM THIS JOB CARD              
         B     PUTJCL2C                                                         
*                                                                               
PUTJCL2B CLI   NETRL,C' '          TEST IF NETRL INPUT FOR LAST JOB             
         BE    PUTJCL2C                                                         
         MVC   0(4,RE),=C',RL='    RELEASE JOBNAME                              
         CLI   NETRL,C'('                                                       
         BNE   *+10                                                             
         MVC   0(4,RE),=C',NR='    RELEASE (NETNAME,JOBNAME)                    
         MVC   4(19,RE),NETRL      SET JOB NAME FROM NETRL VALUE                
*                                                                               
PUTJCL2C MVC   JESIOA,JCL                                                       
         BAS   RE,PUTALL           OUTPUT //*NET CARD                           
         MVC   JCL,SPACES                                                       
         MVC   JCL(2),=C'//'       OUTPUT // EOJ CARD                           
         MVC   JESIOA,JCL                                                       
         BAS   RE,PUTALL                                                        
         NI    NETEOJ,X'FE'        RESET EOJ CARD SUSPENDED                     
         TM    NETFRST,X'02'                                                    
         BO    XIT                                                              
         MVC   JCL,JCLSAVE         RESTORE // JOB CARD                          
         MVC   JESIOA,JCL                                                       
         B     PUTJCLX                                                          
*                                                                               
PUTJCL3  CLI   SINGLE,0            MVS SINGLE JOB                               
         BE    PUTJCL4                                                          
         LA    RE,JCL                                                           
         L     RF,=A(JESKEEP1)     TEST FOR //*FORMAT... FOR KEEP               
         CLC   0(30,RE),0(RF)                                                   
         BNE   PUTJCL4                                                          
         CLI   STEPNAME,C' '                                                    
         BE    PUTJCL4                                                          
         MVC   WORK(40),22(RE)                                                  
         MVC   22(40,RE),SPACES                                                 
         MVC   22(08,RE),STEPNAME  DDNAME=STEPNAME.SYSPRINT                     
         LA    RE,22(RE)                                                        
         CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
*                                                                               
         MVI   0(RE),C'.'                                                       
         MVC   1(40,RE),WORK                                                    
         MVC   JESIOA,JCL                                                       
         B     PUTJCLX                                                          
*                                                                               
PUTJCL4  DS    0H                  SPOT TAPES TO NET TAPES                      
*&&US                                                                           
         MVC   SPOTSNUM(3),SYSTEM                                               
         BAS   RE,SPOTTY           TEST IF SPOT NETWORK SYSTEM                  
         BNE   PUTJCL4A            YES                                          
*&&                                                                             
         B     PUTJCL5                                                          
*                                                                               
PUTJCL4A LA    RE,JCL+10           SEARCH FOR DSN=SPTTAPE.SP.....               
         LA    RF,50                                                            
PUTJCL4B CLC   0(4,RE),=C'DSN='                                                 
         BNE   PUTJCL4C                                                         
         CLC   4(10,RE),=C'SPTTAPE.SP'                                          
         BE    PUTJCL4D                                                         
PUTJCL4C LA    RE,1(RE)                                                         
         BCT   RF,PUTJCL4B                                                      
         B     PUTJCL5                                                          
PUTJCL4D MVC   12(2,RE),=C'NE'    CHANGE TO DSN=SPTTAPE.NE....                  
         MVC   JESIOA,JCL                                                       
         B     PUTJCLX                                                          
*                                                                               
PUTJCL5  DS    0H                                                               
*                                                                               
PUTJCLX  BAS   RE,PUTALL                                                        
         TM    PUTTYP,X'40'+X'20'  OUTPUT AN EXEC CARD OR EXEC CONT             
         BNM   PUTJCLZ             NO                                           
         L     R1,=A(DDIN)         YES ARE THERE ANY ## DD CARDS INPUT          
         CLI   0(R1),C' '                                                       
         BE    PUTJCLZ                                                          
         CLI   OUTDD,C'Y'          HAVE WE ALREADY OUTPUT THEM                  
         BE    PUTJCLZ                                                          
         BAS   RE,PUTDDC                                                        
PUTJCLZ  B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*PUT REQUEST CARDS                                                    *         
***********************************************************************         
PUTREQ   NTR1                      OUTPUT REQ CARD                              
         CLI   JOBIND,C'E'                                                      
         BE    XIT                 EXIT IF EXCLUDING THIS JOB                   
         MVC   LPUTTYP,PUTTYP      SAVE LAST PUTTYP                             
         MVI   PUTTYP,X'00'                                                     
         SR    R0,R0               R0=ZERO IF FIRST TIME                        
*                                                                               
PUTREQ1  L     RF,REQIOADR         POINT TO A(NEXT CARD)                        
         CLC   0(4,RF),=X'FFFFFFFF'                                             
         BE    XIT                 EXIT IF END OF LIST                          
         ICM   RF,15,0(RF)         GET A(NEXT CARD)                             
         BZ    PUTREQ3                                                          
         L     R2,=A(REQIO)        WHY BOTHER WITH THIS ?                       
         MVC   0(80,R2),0(RF)                                                   
         MVC   P,SPACES                                                         
         MVC   JESIOA,0(R2)                                                     
         L     RE,=A(RFHDR)        TEST IF REQ FILE HEADER CARD                 
         CLC   JESIOA(6),0(RE)                                                  
         BE    PUTREQ2                                                          
         LTR   R0,R0               TEST/SET FIRST TIME                          
         BNZ   PUTREQ2                                                          
         LA    R0,1                                                             
         EDIT  (P4,REQCOUNT),(6,P+80)                                           
         OC    P+83(3),=C'000'                                                  
         CLI   QSYSTEM,C'A'        GIVE BACK COL 80 TO ACCOUNT                  
         BE    PUTREQ2                                                          
         TM    JESIOA+79,X'40'     COL 80 OF 1ST REQ CARD IS DDS FLAG           
         BO    PUTREQ2                                                          
         MVC   P+90(5),=C'(DDS)'                                                
         OI    JESIOA+79,X'40'                                                  
*                                                                               
PUTREQ2  BAS   RE,PUTALL           OUTPUT THIS REQUEST CARD                     
*                                                                               
PUTREQ3  L     RF,REQIOADR         BUMP TO NEXT CARD IN LIST                    
         LA    RF,4(RF)                                                         
         ST    RF,REQIOADR                                                      
         B     PUTREQ1                                                          
         EJECT                                                                  
***********************************************************************         
*PUT CARDS TO JES OR TO JCL CARD PUNCH                                *         
***********************************************************************         
PUTALL   NTR1                                                                   
         NI    PUTTYP,X'01'        SET PUT REC TYPE BITS                        
*&&UK*&& BRAS  RE,CHKDLM           SEE IF WE HAVE A DLM= PROBLEM                
PUTALL0  CLI   SPOOL,C'Y'          JCL DIRECT TO OPERATING SYSTEM               
         BNE   PUTALL2                                                          
         GOTO1 =V(POWWOW),PARA,=C'PUT',=C'POWER',POWKEY,JESIOA-8                
         TM    PARA+8,X'80'        TEST IF RETURN JES JOBID                     
         BZ    PUTALLA                                                          
         MVC   P+83(4),=C'JES='                                                 
         MVC   P+87(8),POWKEY      DISPLAY JOBID ON // EOJ CARD                 
         MVC   LGJESID,POWKEY                                                   
         B     PUTALLA                                                          
*                                                                               
PUTALL2  CLI   SPOOL,C'P'          JCL TO CARD PUNCH                            
         BNE   PUTALL4                                                          
         GOTO1 =V(CARDS),PARA,JESIOA,=C'PE00'                                   
         B     PUTALLA                                                          
*                                                                               
PUTALL4  CLI   SPOOL,C'T'          JCL TO TAPE                                  
         BNE   PUTALL6                                                          
         L     R1,=A(JCLTAPE)                                                   
         PUT   (1),JESIOA                                                       
         B     PUTALLA                                                          
*                                                                               
PUTALL6  DS    0H                                                               
*                                                                               
PUTALLA  MVI   ANYOUT,C'Y'         SET WE HAVE SOME OUTPUT                      
         CLI   TRACE,C'Y'          PRINT CARD IF TRACE=Y                        
         BNE   PUTALLC                                                          
         L     RE,=A(RFHDR)        TEST IF REQ FILE HEADER CARD                 
         CLC   JESIOA(6),0(RE)                                                  
         BNE   *+12                                                             
         CLI   RFHTRACE,C'Y'                                                    
         BNE   PUTALLC                                                          
         MVC   P(80),JESIOA                                                     
         GOTO1 =V(PRINTER)                                                      
         L     RE,=A(MVSEOJ)                                                    
         CLC   JESIOA(40),0(RE)                                                 
         BNE   PUTALLA0                                                         
         MVI   P,C'='                                                           
         MVC   P+1(79),P                                                        
         GOTO1 =V(PRINTER)         PRINT END OF JOB LINES                       
         BASR  RE,RF                                                            
         B     PUTALLC                                                          
*                                                                               
PUTALLA0 EQU   *                                                                
*&&DO                                                                           
         CLI   QSYSTEM,C'A'        TEST IF SYSTEM HAS LOWER CASE                
         BE    PUTALLA1                                                         
         CLI   QSYSTEM,C'M'                                                     
         BE    PUTALLA1                                                         
         B     PUTALLAX                                                         
*                                                                               
PUTALLA1 LA    R2,JESIOA                                                        
         LA    R3,P                                                             
         LA    R4,80                                                            
PUTALLA2 TM    0(R2),X'40'         UNDERLINE LOWER CASE FIELDS                  
         BO    *+8                                                              
         MVI   0(R3),C'-'                                                       
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,PUTALLA2                                                      
         CLC   P,SPACES                                                         
         BE    PUTALLAX                                                         
         GOTO1 =V(PRINTER)                                                      
PUTALLAX DS    0H                                                               
*&&                                                                             
PUTALLC  TM    PUTTYP,X'01'        TEST IF JCL CARD                             
         BZ    PUTALLP                                                          
         CLC   JESIOA(2),=C'//'                                                 
         BE    PUTALLD                                                          
         B     PUTALLP                                                          
*                                                                               
PUTALLD  L     RE,=A(MVSEOJ)       TEST FOR EOJ CARD                            
         CLC   JESIOA(40),0(RE)                                                 
         BNE   *+12                                                             
         OI    PUTTYP,X'02'        SET CARD IS EOJ CARD                         
         B     PUTALLP                                                          
*                                                                               
PUTALLE  LA    RE,JESIOA+2         TEST IF JOB CARD                             
         CLI   0(RE),C'*'                                                       
         BE    PUTALLF                                                          
         LA    R0,16                                                            
         CLC   0(5,RE),=C' JOB '                                                
         BE    PUTALLE1                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,*-14                                                          
         B     PUTALLF                                                          
PUTALLE1 OI    PUTTYP,X'04'        SET JOB CARD FLAG                            
         MVC   LGJOBID,JESIOA+2    EXTRACT MVS JOB NAME                         
         AP    LGJOBNO,=P'1'                                                    
         ZAP   LGEXENOJ,=P'0'                                                   
         B     PUTALLP                                                          
*                                                                               
PUTALLF  L     RE,=A(MVSOUT)       TEST FOR //OUNNN FORMAT CARD                 
         CLC   JESIOA(4),0(RE)                                                  
         BNE   PUTALLG                                                          
         CLC   JESIOA+7(8),7(RE)                                                
         BNE   PUTALLG                                                          
         CLC   JESIOA+15(6),=C'CLASS='                                          
         BNE   PUTALLF2                                                         
         CLC   LGSYSPR(6),=C'CLASS='                                            
         BE    PUTALLF2                                                         
PUTALLF1 LA    RF,JESIOA+23        FIND END OF FORMS= PARAM                     
         CLI   0(RF),C','                                                       
         BE    *+12                                                             
         LA    RF,1(RF)                                                         
         B     *-12                                                             
         LA    R1,JESIOA+15                                                     
         SR    RF,R1                                                            
         CHI   RF,20                                                            
         BNH   *+8                                                              
         LHI   RF,20                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8              MOVE OUT CLASS=X,FORMS=XXXX                  
         B     PUTALLF2                                                         
         MVC   LGSYSPR(0),JESIOA+15                                             
PUTALLF2 LA    RF,JESIOA+15                                                     
         LA    R0,50                                                            
PUTALLF3 CLC   0(9,RF),=C'DEST=KEEP'                                            
         BNE   *+8                                                              
         MVI   LGSYSDI,C'K'        SET KEEP DISPOSITION                         
         LA    RF,1(RF)                                                         
         BCT   R0,PUTALLF3                                                      
PUTALLFX B     PUTALLP                                                          
*                                                                               
PUTALLG  CLI   LGSYSPR,C' '        TEST IF FIRST SYSPRINT CARD                  
         BE    *+14                                                             
         CLC   LGSYSPR(6),=C'CLASS='                                            
         BNE   PUTALLH                                                          
         L     RE,=A(JESKEEP2)     TEST FOR //*FORMAT                           
         CLC   JESIOA(22),0(RE)                                                 
         BNE   *+12                                                             
         MVI   LGSYSDI,C'K'        SET FIRST KEEP DISPOSITION                   
         B     PUTALLP                                                          
         L     RE,=A(MVSSYP)                                                    
         CLC   JESIOA(10),0(RE)                                                 
         BNE   PUTALLH                                                          
         OI    PUTTYP,X'08'        SET FIRST SYSPRINT CARD                      
         LA    RE,JESIOA+10                                                     
         LA    R0,20                                                            
PUTALLG0 CLC   0(9,RE),UNITEQTP                                                 
         BNE   *+12                                                             
         MVI   LGSYSDI,C'T'        SET FIRST TAPE DISPOSITION                   
         B     PUTALLP                                                          
         LA    RE,1(RE)                                                         
         BCT   R0,PUTALLG0                                                      
         CLI   LGSYSDI,C' '                                                     
         BNE   PUTALLG2                                                         
         MVI   LGSYSDI,C'D'        SET FIRST DEFAULT DISP                       
         LA    RE,JESIOA+10                                                     
         LA    R0,60                                                            
PUTALLG1 CLC   0(8,RE),=C'HOLD=YES'                                             
         BNE   *+12                                                             
         MVI   LGSYSDI,C'H'        SET FIRST HOLD DISP                          
         B     PUTALLG2                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,PUTALLG1                                                      
PUTALLG2 LA    RE,JESIOA+10                                                     
         LA    R0,20                                                            
PUTALLG3 CLC   0(7,RE),=C'SYSOUT=' SEARCH FOR SYSOUT= PARM                      
         BE    PUTALLG4                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,PUTALLG3                                                      
         CLI   LGSYSPR,C' '                                                     
         BNE   PUTALLP                                                          
         MVC   LGSYSPR(7),=C'UNKNOWN'                                           
         B     PUTALLP                                                          
PUTALLG4 CLC   7(3,RE),=C'(,)'     TEST FOR NULL SYSOUT                         
         BE    PUTALLP                                                          
         MVI   DUB,C','            SET END OF PARM CHR                          
         CLI   7(RE),C'('                                                       
         BNE   *+8                                                              
         MVI   DUB,C')'                                                         
         LA    R0,L'LGSYSPR                                                     
         LA    RF,LGSYSPR                                                       
PUTALLG5 MVC   0(1,RF),0(RE)       COPY PARM TO LGSYSPR                         
         CLC   0(1,RF),DUB                                                      
         BE    PUTALLG6                                                         
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,PUTALLG5                                                      
PUTALLG6 CLI   0(RF),C','                                                       
         BNE   *+8                                                              
         MVI   0(RF),C' '                                                       
         B     PUTALLP                                                          
*                                                                               
PUTALLH  TM    LPUTTYP,X'08'       TEST IF LAST WAS //SYSPRINT                  
         BZ    PUTALLJ                                                          
         CLI   LGSYSDI,C'T'        TEST IF UNIT=TAPE                            
         BNE   PUTALLJ                                                          
         L     RE,=A(MVSSYPT2)                                                  
         CLC   JESIOA(19),0(RE)    TEST IF // DSN=                              
         BNE   PUTALLJ                                                          
         MVC   LGSYSPR(15),JESIOA+19                                            
         MVC   LGSYSPR+16(4),JESIOA+55                                          
         OI    JOBTAPES,X'80'      SET TAPE OUTPUT SYSPRINT                     
         B     PUTALLP                                                          
*                                                                               
PUTALLJ  CLI   LGOUTTP,C' '        TEST FIRST TAPE ONLY                         
         BNE   PUTALLK                                                          
         L     RE,=A(MVSSYPT1)                                                  
         CLC   JESIOA+12(19),12(RE) DD UNIT=TAPE,DISP=(....                     
         BNE   PUTALLJ2                                                         
         CLC   JESIOA+31(3),=C'NEW'                                             
         BE    PUTALLJ1                                                         
         CLC   JESIOA+31(3),=C'MOD'                                             
         BNE   PUTALLJ2                                                         
PUTALLJ1 OI    PUTTYP,X'10'        SET FIRST STANDARD TAPE                      
         B     PUTALLP                                                          
PUTALLJ2 TM    LPUTTYP,X'10'                                                    
         BZ    PUTALLK                                                          
         L     RE,=A(MVSSYPT2)                                                  
         CLC   JESIOA(19),0(RE)    TEST IF // DSN=                              
         BNE   PUTALLK                                                          
         MVC   LGOUTTP,JESIOA+19                                                
         OI    JOBTAPES,X'40'      SET TAPE OUTPUT STANDARD                     
         B     PUTALLP                                                          
*                                                                               
PUTALLK  LA    RE,JESIOA+2         TEST IF EXEC CARD                            
         CLI   0(RE),C'*'                                                       
         BE    PUTALLL                                                          
         LA    R0,16                                                            
         CLC   0(6,RE),=C' EXEC '                                               
         BE    PUTALLK1                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,*-14                                                          
         TM    LPUTTYP,X'40'+X'20' WAS LAST CARD EXEC CARD WITH CONT            
         BNO   PUTALLL             NO                                           
         OI    PUTTYP,X'20'        SET THIS TO EXEC CONT CARD                   
         B     PUTALLL                                                          
PUTALLK1 LR    R0,RE                                                            
         LA    RE,JESIOA+71        TEST IF CONTINUATION COMING                  
PUTALLK2 CLI   0(RE),C' '                                                       
         BNE   PUTALLK3                                                         
         BCTR  RE,0                                                             
         CR    RE,R0                                                            
         BH    PUTALLK2                                                         
         B     PUTALLK4                                                         
PUTALLK3 CLI   0(RE),C','          TEST IF LAST CHR ON EXEC IS COMMA            
         BNE   PUTALLK4                                                         
         OI    PUTTYP,X'20'        SET EXEC CARD WITH CONTINUATION              
PUTALLK4 OI    PUTTYP,X'40'        SET EXEC CARD FLAG                           
         MVC   LGEXEIDL,LGEXEID    SAVE LAST EXEC EXECUTE NAME                  
         MVC   LGEXEID,JESIOA+2    EXTRACT MVS EXECUTE NAME                     
         AP    LGEXENO,=P'1'                                                    
         AP    LGEXENOJ,=P'1'                                                   
         B     PUTALLP                                                          
*                                                                               
PUTALLL  DS    0H                                                               
*                                                                               
PUTALLP  CLI   WJCLID,C' '         JCL REC TO DISK                              
         BE    PUTALLPX                                                         
         L     R1,=A(WJCL)                                                      
         PUT   (1),JESIOA                                                       
PUTALLPX DS    0H                                                               
*                                                                               
PUTALLQ  CLI   WLOGID,C' '         JCL LOG TO DISK                              
         BE    PUTALLQX                                                         
         TM    PUTTYP,X'40'        TEST IF THIS IS AN EXEC CARD                 
         BZ    PUTALLQ1                                                         
         CP    LGEXENOJ,=P'1'      IS IT THE FIRST OF A JOB                     
         BE    PUTALLQX                                                         
         B     PUTALLQA                                                         
*                                                                               
PUTALLQ1 TM    PUTTYP,X'02'        TEST IF THIS IS EOJ CARD                     
         BZ    PUTALLQX                                                         
*                                                                               
PUTALLQA MVC   LOGSYSID,SYSTEM     SET SYSTEM ID                                
         AP    LGRECNO,=P'1'                                                    
         UNPK  LOGRECNO,LGRECNO    SET RECORD NUMBER (ONE PER EXEC)             
         OI    LOGRECNO+3,X'F0'                                                 
         MVC   LOGJESID,LGJESID    SET JES SPOOL ID                             
         MVC   LOGJOBID,LGJOBID    SET JOB ID                                   
         MVC   LOGEXEID,LGEXEIDL   SET PREVIOUS STEP ID                         
         TM    PUTTYP,X'02'        OR THIS STEP ID IF EOJ                       
         BZ    *+10                                                             
         MVC   LOGEXEID,LGEXEID                                                 
         MVC   LOGSYSDI,LGSYSDI    SET SYSPRINT DISPOSITION                     
         CLI   LOGSYSDI,C' '                                                    
         BNE   *+8                 DEFAULT IS D FOR DELETE                      
         MVI   LOGSYSDI,C'D'                                                    
         MVC   LOGSYSPR,LGSYSPR    SET SYSPRINT SYSOUT= OR TAPE DSN             
         MVC   LOGOUTTP,LGOUTTP    SET FIRST OUTPUT TAPE DSN                    
*&&UK*&& MVC   LOGLOGO2,QJOBLOG2   SET LOGO2 (UK BIN NUMBER)                    
         L     R1,=A(WLOG)                                                      
         PUT   (1),LOGREC                                                       
*                                                                               
PUTALLQB MVC   LOGREC,SPACES       CLEAR FOR NEXT JOB OR STEP                   
         MVI   LGSYSDI,C' '                                                     
         MVC   LGSYSPR,SPACES                                                   
         MVC   LGOUTTP,SPACES                                                   
         TM    PUTTYP,X'02'        TEST FOR EOJ                                 
         BZ    PUTALLQX                                                         
         ZAP   LGEXENOJ,=P'0'                                                   
         MVC   LGJESID,SPACES                                                   
         MVC   LGJOBID,SPACES                                                   
         MVC   LGEXEID,SPACES                                                   
PUTALLQX DS    0H                                                               
*                                                                               
PUTALLX  B     XIT                                                              
*&&UK                                                                           
CHKDLM   CLC   SYSINDLM,SPACES     TEST SYSIN DLM IN EFFECT                     
         BE    CHKDLM04            NO-SKIP                                      
         CLC   SYSINDLM,JESIOA     IS THIS IT?                                  
         BNE   CHKDLM02            NO-SKIP                                      
         MVC   SYSINDLM,SPACES     SET SYSIN DLM NO LONGER IN EFFECT            
         BR    RE                                                               
*                                                                               
CHKDLM02 CLC   JESIOA(2),=C'//'    TEST ANY JCL CARD                            
         BNER  RE                  NO-EXIT                                      
         MVC   P(9),=C'**ERROR**'                                               
         MVC   P+10(38),=C'JCL FOUND BEFORE SYSIN DELIMITER. JOB='              
         MVC   P+48(8),LGJOBID                                                  
         GOTO1 =V(PRINTER)         CAUTION - DESTROYS RE SO NO RETURN           
         ABEND 301                 MUST ABEND                                   
*                                                                               
CHKDLM04 CLC   JESIOA(7),=C'//SYSIN' TEST SYSIN CARD                            
         BNER  RE                                                               
         STM   R0,R1,12(RD)                                                     
         LA    R1,JESIOA+12        ALLOW FOR ' DD *'                            
         LA    R0,50                                                            
*                                                                               
CHKDLM06 CLC   0(4,R1),=C'DLM='                                                 
         BE    CHKDLM08                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,CHKDLM06                                                      
         B     CHKDLM10                                                         
CHKDLM08 CLI   4(R1),C''''         TEST FOR DLM='XX'                            
         BNE   *+8                                                              
         LA    R1,1(R1)                                                         
         MVC   SYSINDLM,4(R1)      SAVE SYSIN DLM NOW IN EFFECT                 
*                                                                               
CHKDLM10 LM    R0,R1,12(RD)                                                     
         BR    RE                                                               
*&&                                                                             
OK       CR    RB,RB               OK EXIT SET CC EQL                           
         B     XIT                                                              
NOTOK    LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
XIT      XIT1                                                                   
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
*SUBROUTINE TO CHECK IF SPOT SYSTEM IS A NETWORK SYSTEM               *         
*CALLS SETS SPOT SYSTEM NUMBER IN SPOTSNUM                            *         
***********************************************************************         
SPOTTY   STM   RE,R1,SPSVRER1                                                   
         MVC   NETSNUM,SPACES                                                   
         MVC   NETSNAME,SPACES                                                  
         MVC   SEQLSNUM+2(3),SPOTSNUM                                           
         GOTO1 =V(DMDDNAME),DMCB,=C'DDNAME',SEQLSNUM                            
         CLI   DMCB+8,0                                                         
         BNE   SPOTTYX             EXIT IF SPOT SYSTEM NOT FOUND                
*                                                                               
SPOTTY1  L     R1,DMCB+8           R1=A(SPOT SYSTEM INFO DATA)                  
         USING DDNAD,R1                                                         
         CLI   DDNASYN3,C'N'       TEST SYNONYM FOR A NET SYSTEM                
         BNE   SPOTTYX                                                          
         MVC   NETSNUM,DDNASYN3                                                 
         MVC   NETSNAME,NETNAME                                                 
*                                                                               
SPOTTYX  LM    RE,R1,SPSVRER1                                                   
         CLI   NETSNUM,C' '                                                     
         BR    RE                  EXIT WITH CC=NEQ IF ITS A NET SYSTEM         
         DROP  R1                                                               
*                                                                               
SPSVRER1 DS    4F                                                               
*                                                                               
SEQLSNUM DC    C'S=XXX'                                                         
NETNAME  DC    C'NET'                                                           
SPOTNETS DS    0CL10                                                            
SPOTSNUM DS    CL3                 SPOT SYSTEM NUM (SET BY CALLER)              
NETSNUM  DS    CL3                 SPOT NET SYSTEM NUM                          
NETSNAME DS    CL3                 SPOT NET SYSTEM NAME                         
         DC    CL1' '              SPARE                                        
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*SUBROUTINE TO CHECK IF IDI OUTPUT TYPE OVERRRIDE APPLIES             *         
*CALLS SETS OUTPUT TYPE/SYSTEM/PROGRAM                                *         
***********************************************************************         
IDIOT    STM   RE,RF,IDIOTREF                                                   
         MVI   IDIOTRCD,C' '                                                    
IDIOT1   CLC   IDIOTOTY(2),=C'S1'  OUTPUT TYPE S1.... (MAX 4 SYS CHRS)          
         BNE   IDIOT2                                                           
         LA    RE,IDIOTOTY+2                                                    
         LA    RF,4                                                             
         B     IDIOT4                                                           
IDIOT2   CLI   IDIOTOTY,C'#'       OUTPUT TYPE #..... (MAX 5 SYS CHRS)          
         BNE   IDIOTX                                                           
         CLC   IDIOTOTY+1(5),SPACES                                             
         BE    IDIOT5                                                           
         LA    RE,IDIOTOTY+1                                                    
         LA    RF,5                                                             
IDIOT4   CLC   0(1,RE),IDIOTSYS    CHECK IF SYSTEM IN OUTPUT TYPE NAME          
         BE    IDIOT5                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,IDIOT4                                                        
         B     IDIOTX                                                           
IDIOT5   LA    RF,IDIOTLST         CHECK FOR SYSTEM/PROGRAM EXCEPTIONS          
IDIOT5A  CLI   0(RF),C' '                                                       
         BE    IDIOT6                                                           
         CLC   0(3,RF),IDIOTSYS    IGNORE IF IN LIST                            
         BE    IDIOTX                                                           
         LA    RF,3(RF)                                                         
         B     IDIOT5A                                                          
*                                                                               
IDIOT6   EQU   *                                                                
*&&UK*&& CLI   QJOBDISP,C'T'       IGNORE IDI ORIDE IF SPOOL TO TAPE            
*&&UK*&& BE    IDIOTX                                                           
*&&US*&& CLI   QJOB@DDS,C'Y'       IGNORE IDI ORIDE IF PRINT@DDS SET            
*&&US*&& BE    IDIOTX                                                           
*                                                                               
IDIOTQ   MVI   IDIOTRCD,C'Q'                                                    
IDIOTX   CLI   IDIOTRCD,C'Q'       EXIT WITH CC EQL IF IDI OVERRIDE             
         L     RE,IDIOTREF                                                      
         BR    RE                                                               
*                                                                               
IDIOTREF DS    2F                                                               
IDIOTRCD DS    C                                                                
IDIOTOTY DS    CL6                                                              
IDIOTSYS DS    CL1                                                              
IDIOTPRG DS    CL2                                                              
*                                                                               
IDIOTLST DS    0C                                                               
*&&UK                                                                           
         DC    C'A07',C'A55',C'A77'                                             
         DC    C'M05',C'M31',C'M74',C'M75'                                      
*&&                                                                             
*&&US                                                                           
         DC    C'A55',C'AT5',C'A56'                                             
*&&                                                                             
IDIOTLSX DC    C' '                                                             
         EJECT                                                                  
***********************************************************************         
*SUBROUTINE TO CHECK IF FREE=CLOSE IS VALID FOR THIS JOB              *         
***********************************************************************         
FREEZE   SR    R0,R0               SET OK TO OUTPUT FREE=NO                     
         CLI   FREE,C'N'           FREE=N PARAMATER CARD INPUT                  
         BE    FREEZENO                                                         
*NOP*    CLI   SINGLE,0            FREE=CLOSE FOR SINGLE JOB                    
*NOP*    BE    FREEZENO                                                         
         LA    RF,FREEBAN          SEARCH TABLE OF BANNED REQUESTS              
*                                                                               
FREEZE1  CLI   0(RF),C' '          END OF TABLE                                 
         BE    FREEZE2                                                          
         CLC   QJOBJES+4(3),0(RF)                                               
         BE    FREEZENO                                                         
         LA    RF,L'FREEBAN(RF)                                                 
         B     FREEZE1                                                          
*                                                                               
FREEZE2  CLI   QPRIPO,C'I'         TEST PRG OUTPUT TYPE FOR NOT OK              
         BE    FREEZENO                                                         
         CLI   QPRIPO,C'R'                                                      
         BE    FREEZENO                                                         
         CLI   QPRIPO,C'Y'                                                      
         BE    FREEZENO                                                         
         CLI   QPRIPO,C'Z'                                                      
         BE    FREEZENO                                                         
         CLI   QPRIPO,C'9'                                                      
         BE    FREEZENO                                                         
         B     FREEZEX                                                          
*                                                                               
FREEZENO LA    R0,1                SET NO FREE=CLOSE ALLOWED                    
FREEZEX  LTR   R0,R0                                                            
         BR    RE                  EXIT WITH CC=EQ IF FREE=CLOSE OK             
*                                                                               
FREEBAN  DS    0CL3                REQS THAT CHANGE BOXWIDTH                    
*&&UK*&& DC    C'ACC'                                                           
*&&UK*&& DC    C'AES'                                                           
*&&UK*&& DC    C'AEY'                                                           
*&&UK*&& DC    C'AE1'                                                           
*&&UK*&& DC    C'AE2'                                                           
*&&UK*&& DC    C'AFI'                                                           
*&&UK*&& DC    C'AIL'                                                           
*&&UK*&& DC    C'AM2'                                                           
*&&UK*&& DC    C'AM3'                                                           
*&&UK*&& DC    C'APH'                                                           
*&&UK*&& DC    C'APL'                                                           
*&&UK*&& DC    C'ARH'                                                           
*&&UK*&& DC    C'ARL'                                                           
*&&UK*&& DC    C'ARU'                                                           
*&&UK*&& DC    C'ARW'                                                           
*&&UK*&& DC    C'ATC'                                                           
*&&UK*&& DC    C'ATE'                                                           
*&&UK*&& DC    C'ATG'                                                           
*&&UK*&& DC    C'AT2'                                                           
*&&UK*&& DC    C'AVH'                                                           
*&&UK*&& DC    C'AVL'                                                           
*&&UK*&& DC    C'AXH'                                                           
*&&UK*&& DC    C'AXL'                                                           
*&&UK*&& DC    C'A1H'                                                           
*&&UK*&& DC    C'A1L'                                                           
*&&UK*&& DC    C'A8A'                                                           
*&&UK*&& DC    C'BWR'                                                           
*&&UK*&& DC    C'LWR'                                                           
*&&UK*&& DC    C'MBA'                                                           
*&&UK*&& DC    C'MDR'                                                           
*&&UK*&& DC    C'MGR'                                                           
*&&UK*&& DC    C'MMT'                                                           
*&&UK*&& DC    C'MRM'                                                           
*&&UK*&& DC    C'MWR'                                                           
FREEBANX DC    C'   '                                                           
         EJECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
FUL      DS    F                                                                
FUL1     DS    F                                                                
HALF     DS    H                                                                
HALF1    DS    H                                                                
SAVERE   DS    F                                                                
SAVERF   DS    F                                                                
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(REQWRAP)                                                       
         DC    A(REQWRAPX)                                                      
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
*                                                                               
ACOMRG   DS    A                                                                
LOADADDR DC    A(LOADAREA)                                                      
AJCLIN   DC    A(JCLIN)                                                         
ADDIN    DC    A(DDIN)                                                          
WORK     DS    CL80                                                             
DMCB     DS    6F                                                               
PARA     DS    6F                                                               
C        DS    CL80                                                             
UPSI     DC    X'00'                                                            
TODAY    DC    CL8' '                                                           
*&&UK                                                                           
TODAYM1  DC    CL8' '              TODAY MINUS ONE DAY                          
TODAYP1  DC    CL8' '              TODAY PLUS ONE DAY                           
*&&                                                                             
TODPAK   DC    PL3'0'                                                           
TODAYJOB DC    C'N'                                                             
*&&UK                                                                           
NPDATE   DC    CL8'29/09/07'       NO PRINTING DATE FOR UK                      
NPDPAK   DC    PL3'0'                                                           
*&&                                                                             
*&&US                                                                           
NPDATE   DC    CL8'12/31/27'       NO PRINTING DATE FOR US                      
NPDPAK   DC    PL3'0'                                                           
*&&                                                                             
JOBIND   DC    C'I'                                                             
PROGTYZ  DC    C'E'                DEFAULT IS EXCLUDE PROGTYPE=Z                
EOFSW    DC    C'N'                                                             
CARDSW   DC    C'N'                                                             
RCMSW    DC    C'N'                                                             
ANYOUT   DC    C'N'                                                             
LASTOUT  DC    C'N'                                                             
CDPAGES  DC    PL4'0'                                                           
TEMPPACK DC    PL4'0'                                                           
MAXSTEPS DC    PL4'255'                                                         
MAXREQS  DC    PL4'9999'                                                        
REQSTEP  DC    PL4'0'                                                           
REQCOUNT DC    PL4'0'                                                           
REQSTART DC    PL4'1'                                                           
REQEND   DC    PL4'999999'                                                      
TRACE    DC    C'Y'                                                             
RFHTRACE DC    C'N'                                                             
TAPEUNIT DC    C' '                                                             
UNITEQTP DC    C'UNIT=TAPE'                                                     
MSGLEV1  DC    C'1'                                                             
MSGLEV2  DC    C'1'                                                             
MSGCLA   DC    C'A'                                                             
FLASH    DC    CL4' '                                                           
FORMS    DC    CL4' '                                                           
CLASS    DC    CL1' '                                                           
*&&UK                                                                           
BETAKEEP DC    CL1' '                                                           
BETAONLY DC    CL1'Y'                                                           
BETAOPTN DC    CL1'Y'                                                           
*&&                                                                             
*&&US                                                                           
BETAKEEP DC    CL1' '                                                           
BETAONLY DC    CL1' '                                                           
BETAOPTN DC    CL1' '                                                           
*&&                                                                             
WHATDEST DC    CL1' '                                                           
FLAGDEST DC    XL1'00'                                                          
PORFDEFS DC    XL1'00'                                                          
JOBSTART DC    CL8' '                                                           
JOBEND   DC    CL8' '                                                           
TYPRUN   DC    CL8' '                                                           
FRSTNAME DC    CL8' '                                                           
FRSTNAMF DC    C'Y'                                                             
ANYLOGO  DC    C'Y'                                                             
ANYSPLTP DC    C'N'                                                             
DIRCLASS DC    C' '                                                             
DIROUT   DC    C' '                                                             
BURSTPQ  DC    C' '                                                             
FLASHCPY DC    C' '                                                             
ARCOUT   DC    C' '                                                             
ARCCLASS DC    C' '                                                             
ARCTYPE  DC    C' '                                                             
SVARCCLS DC    C' '                                                             
SVARCTYP DC    C' '                                                             
ARCFLAG  DC    XL1'00'                                                          
REQWHY   DC    CL22' '                                                          
INDSPACE DC    C' '                                                             
OTDSPACE DC    C' '                                                             
INMEDDSP DC    CL4' '              SYSIN MEDDSPC FOR UK MEDIA REQUEST           
SYSDDSIO DC    CL8' '              SYSIN DDSIO FOR REQUEST                      
DIRDEST  DC    XL2'0000'                                                        
ARCDEST  DC    XL2'0000'                                                        
*                                                                               
ARCXDATA DS    0C                                                               
ARCXCLA  DC    C' '                                                             
ARCXFRM  DC    CL4' '                                                           
ARCXFDE  DC    CL8' '                                                           
ARCXPDE  DC    CL8' '                                                           
ARCXDATL EQU   *-ARCXDATA                                                       
*                                                                               
SYSMAX   DC    H'256'              MAXIMUM NUMBER OF SYSTEMS                    
SYSINP   DC    H'0'                NUMBER OF SYSTEMS INPUT                      
SYSNUM   DC    H'1'                CURRENT SYSTEM NUMBER (PRESET TO 1)          
SYSTEM   DC    CL3'  '             CURRENT SYSTEM                               
JCLASS   DC    C'A'                CLASS FOR SYSTEM                             
DISP     DC    C'D'                DISPOSITION FOR SYSTEM                       
SYSKEY   DC    CL2' '              CURRENT SYSTEM FOR WORKER KEY                
*                                                                               
SYSINDLM DC    CL2' '              ACTIVE SYSIN DLM= CHARS                      
MAXTAPES DC    X'00'                                                            
IGNORE   DC    CL8' '                                                           
SPOOL    DC    C'Y'                                                             
JCLOPT   DC    C' '                                                             
TYPJCL   DC    C'O'                                                             
SYSPMODE DC    C' '                SET TO C'O' IF //  OUTPUT CARDS              
SYSPOUT  DC    C' '                                                             
AFP      DC    CL8' '                                                           
AFPKEEP  DC    C' '                                                             
FREE     DC    C'Y'                                                             
REPSPACE DC    C'Y'                                                             
RJEOPT   DC    CL3' '                                                           
JSEPOPT  DC    C' '                                                             
LOGOADR  DC    C' '                                                             
PUTTYP   DC    X'00'                                                            
LPUTTYP  DC    X'00'                                                            
ACCEMU   DC    X'00'                                                            
SPONVER  DC    C' '                SPONSORX VERSION LETTER                      
SPONAGY  DC    CL2' '              ALPHA AGENCY TO APPLY                        
QCTR     DC    X'00'                                                            
QCARDS   DC    X'00'                                                            
BYTE     DC    X'00'                                                            
ACCSE    DC    X'00'                                                            
ACCPY    DC    X'00'                                                            
ACSTA    DC    X'00'                                                            
ACOPN    DC    C'  '                                                            
BYTE1    DC    X'00'                                                            
ANYDIRRC DC    X'00'                                                            
*        EQU   X'80'               GOING TO BOTH (PRINT AND QUEUE)              
*        EQU   X'01'               DIRECT= CREATED                              
ANYARCRC DC    X'00'                                                            
*        EQU   X'02'               ARC= CREATED                                 
*        EQU   X'40'               ELIGIBLE                                     
*                                                                               
NUMSLETS DC    C'0123456789ABCDEFGHILJKMNOPQRSTUVWXYZ'                          
*                                                                               
ACCOUNT  DC    C'ACCOUNT '                                                      
ACFILEL  DC    C'NACCDIR NACCMST X '                                            
ACCDIR   DC    C'ACCDIR  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
*                                                                               
GETREC   DC    CL8'GETREC'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
DMOPEN   DC    CL8'DMOPEN'                                                      
DMSYSTEM DC    CL8'CONTROL'                                                     
DMFILES  DC    C'N'                                                             
CTFILE   DC    CL7'CTFILE'                                                      
         DC    C'X'                                                             
         DC    F'0'                                                             
*                                                                               
DMWORK   DC    XL96'00'            FOR GETREC/PUTREC                            
*                                                                               
REQIOADR DC    A(0)                A(NEXT CARD)                                 
REQIOMAP DC    XL64'00'            A(EACH CARD IN REQUEST) MAX=1+15             
         DC    XL4'FFFFFFFF'       END OF LIST                                  
*                                                                               
DIR      DS    CL64                                                             
KEY      DS    CL64                                                             
*                                                                               
BLOCK    DS    CL192                                                            
SCANCARD DC    CL80' '                                                          
SINGLE   DC    X'00'                                                            
JCLIF    DC    C'Y'                                                             
JCL      DS    CL80                                                             
JCLSAVE  DS    CL80                                                             
OUTJOB   DC    C' '                                                             
OUTDD    DC    C' '                                                             
OUTJES   DC    C' '                                                             
OUTAFP   DC    C' '                                                             
OUTSYP   DC    C' '                                                             
OUTSIN   DC    C' '                                                             
JOBTAPES DC    X'00'                                                            
TSTTAPES DC    X'00'                                                            
         DC    2X'00'                                                           
*                                                                               
AFRSTO   DS    A                   A(FIRST CARD OUTPUT CARD)                    
NFRSTO   DS    H                   NUMBER OF CARDS IN FIRST SET                 
NSETS    DS    H                   NUMBER OF SETS OUTPUT                        
AFRSTCLA DS    A                                                                
AFRSTFRM DS    A                                                                
ALASTFRM DS    A                                                                
AFRSTCOP DS    A                                                                
AFRSTDES DS    A                                                                
AFRSTFOR DS    A                                                                
AFRSTPAG DS    A                                                                
ANEXTO   DS    A                                                                
ALASTO   DS    A                   A(LAST OUTPUT CARD)                          
*                                                                               
OUTCRDN  DC    PL2'0'                                                           
OUTCRDF  DC    PL2'0'                                                           
OUTCRDL  DC    PL2'0'                                                           
FSTEXEC  DC    PL4'0'                                                           
JOBEXEC  DC    PL4'0'                                                           
TOTEXEC  DC    PL4'0'                                                           
JCLPARM  DS    X                                                                
THSJCL   DS    X                                                                
LSTJCL   DS    X                                                                
DELJCL   DS    X                                                                
XTRAFLAG DS    X                                                                
DATAMGR  DC    V(DATAMGR)                                                       
NXTJCL   DS    A                                                                
BEFJCL   DC    CL10' '                                                          
AFTJCL   DC    CL10' '                                                          
THISJCL  DC    CL10' '                                                          
NOTFJCL  DC    CL10' '                                                          
JCLINJID DC    CL8' '                                                           
JCLINJLN DC    AL1(0)                                                           
JCLINJNO DC    PL4'0'                                                           
SPLCOUNT DC    AL1(1)              COUNT OF SPOOL TAPES IS JOB                  
STEPNAME DC    CL8' '              MVS THIS JOB STEP NAME                       
STEPNAMF DC    CL8' '              MVS FIRST JOB STEP NAME                      
STEPNAMP DC    CL8' '              MVS PREVIOUS JOB STEP NAME                   
WJCLID   DC    CL8' '              WRAP JCL DATA DISK FILE ID                   
WLOGID   DC    CL8' '              WRAP LOG DATA DISK FILE ID                   
NETID    DC    CL8' '              JES NETWORK ID                               
NETRL    DC    CL19' '             JES NETWORK LAST NET CARD RL= OR NR=         
NETHC    DC    C'00'               JES NETWORK FRST NET CARD HC=                
NETOH    DC    C' '                JES NETWORK OH=YES REQUIRED                  
NETEOJ   DC    X'00'                                                            
NETFRST  DC    X'00'                                                            
QDKEY    DC    CL16'REQS SS'                                                    
JDKEY    DC    CL16'JOBS SS'                                                    
QD       DC    CL16' '                                                          
JD       DC    CL16' '                                                          
PANBOOK  DC    CL10' '                                                          
POWKEY   DC    CL10' '                                                          
         EJECT                                                                  
CONDPARM DC    CL11' '                                                          
ACCTPARM DC    CL05' '   '                                                      
CONDPPRO DC    CL11',CONE=EVEN,'                                                
ACCTPPRO DC    CL05'ACCU='                                                      
CONDPPGM DC    CL11',COND=EVEN,'                                                
ACCTPPGM DC    CL05'ACCT='                                                      
*                                                                               
         DC    D'0'                                                             
JESIOA   DS    CL80                                                             
*                                                                               
LGRECNO  DC    PL4'0'                                                           
LGJOBNO  DC    PL4'0'                                                           
LGEXENO  DC    PL4'0'                                                           
LGEXENOJ DC    PL4'0'                                                           
LGJESID  DC    CL8' '                                                           
LGJOBID  DC    CL8' '                                                           
LGEXEID  DC    CL8' '                                                           
LGEXEIDL DC    CL8' '                                                           
LGSYSDI  DC    CL1' '                                                           
LGSYSPR  DC    CL20' '                                                          
LGOUTTP  DC    CL16' '                                                          
*&&UK                                                                           
LGLOGO1  DC    CL7' '                                                           
*&&                                                                             
         DC    D'0'                                                             
LOGREC   DC    CL80' '                                                          
         ORG   LOGREC                                                           
LOGSYSID DS    CL3                 SYSTEM ID                                    
LOGRECNO DS    CL4                 RECORD NUMBER                                
         DS    CL1                                                              
LOGJESID DS    CL8                 JES ID IF SPOOLED TO MVS DIRECT              
         DS    CL1                                                              
LOGJOBID DS    CL8                 NAME ON JOB CARD                             
         DS    CL1                                                              
LOGEXEID DS    CL8                 NAME ON EXECUTE CARD                         
         DS    CL1                                                              
LOGSYSDI DS    CL1                 SYSPRINT DISPOSITION                         
         DS    CL1                                                              
*&&US                                                                           
LOGSYSPR DS    CL20                SYSPRINT SYSOUT= OR TAPE DSN=                
         DS    CL1                                                              
LOGOUTTP DS    CL16                FIRST OUTPUT TAPE DSN=                       
         DS    CL6                 SPARE                                        
*&&                                                                             
*&&UK                                                                           
LOGSYSPR DS    CL20                SYSPRINT SYSOUT= OR TAPE DSN=                
LOGOUTTP DS    CL16                FIRST OUTPUT TAPE DSN=                       
LOGLOGO2 DS    CL7                 LOGO2 (USED IN UK FOR BIN NUMBER)            
*&&                                                                             
LOGRECX  EQU   *                                                                
         ORG                                                                    
         EJECT                                                                  
***********************************************************************         
*MASTER LITERAL POOL                                                  *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*INITIALIZE - READ FIRST CONTROL CARDS FOR SPECIALS                   *         
***********************************************************************         
INIWRP   NTR1  BASE=*                                                           
         ST    R1,ACOMRG           SAVE MVS SUPV INFO                           
         L     R1,0(R1)                                                         
         LH    R2,0(R1)                                                         
         LTR   R2,R2                                                            
         BZ    INIWRP1C                                                         
         CHI   R2,8                                                             
         BNH   *+8                                                              
         LHI   R2,8                                                             
         LA    R1,2(R1)            R1=A(PARM DATA)                              
         LA    RF,UPSITAB                                                       
INIWRP1A CLI   0(R1),C'0'                                                       
         BE    INIWRP1B                                                         
         CLI   0(R1),C'1'                                                       
         BNE   INIWRP1C                                                         
         OC    UPSI,0(RF)                                                       
INIWRP1B LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R2,INIWRP1A                                                      
INIWRP1C B     INIWRP2                                                          
UPSITAB  DC    X'8040201008040201'                                              
*                                                                               
INIWRP2  L     RF,=A(HDRMSG1)      REQUEST WRAP                                 
         MVC   TITLE+6(12),0(RF)                                                
         L     RF,=A(HDRMSG2)      CONTROL STATEMENTS                           
         MVC   SUB1(18),0(RF)                                                   
         L     RF,=A(HDRMSGU)      UNDERLINE                                    
         MVC   SUB2(18),0(RF)                                                   
         MVC   SUB1+083(03),=C'REQ'                                             
         MVC   SUB2+083(03),=C'NUM'                                             
*                                                                               
INIWRP3  TM    UPSI,X'80'          READ FIRST CARD(S) FOR SPECIALS              
         BZ    INIWRP4                                                          
         GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
*                                                                               
INIWRP3A CLC   C(6),=C'DDSIO='     DDSIO=XXXXXXXX TO CHANGE DATA                
         BNE   INIWRP3B                                                         
         L     RE,=V(DDSIO)                                                     
         MVC   0(8,RE),C+6                                                      
         B     INIWRP3P                                                         
*                                                                               
INIWRP3B CLC   C(9),=C'SYSDDSIO='  SYSDDSIO=XXXXXXXX TO CHANGE SYSIN            
         BNE   INIWRP3E                                                         
         MVC   SYSDDSIO,C+9        SAVE INPUT SYSDDSIO VALUE                    
         B     INIWRP3P                                                         
*                                                                               
INIWRP3E CLC   C(7),=C'DSPACE='    DSPACE=X TO CHANGE DATASPACE                 
         BNE   INIWRP3F                                                         
         MVC   INDSPACE,C+7        SAVE INPUT DSPACE CHR                        
         ICM   RE,15,=V(SSB)                                                    
         MVC   SSODSPAC-SSOOFF(1,RE),C+7                                        
         B     INIWRP3P                                                         
*                                                                               
INIWRP3F EQU   *                                                                
*&&UK                                                                           
         CLC   C(8),=C'MEDDSPC='   MEDDSPC=.. TO NAME MEDIA DATASPACE           
         BNE   INIWRP3G                                                         
         MVC   INMEDDSP,C+8        SAVE INPUT MEDDSPC VALUE                     
         B     INIWRP3P                                                         
*&&                                                                             
INIWRP3G CLC   C(9),=C'STXITER=N'  STXITER=N TO NOP STXIT                       
         BE    *+14                                                             
         CLC   C(7),=C'DUMP=OS'    DUMP=OS DOES THE SAME                        
         BNE   INIWRP4                                                          
         L     RF,=V(STXITER)      NOP ROUTINE                                  
         MVC   0(2,RF),=X'07FE'                                                 
         B     INIWRP3P                                                         
*                                                                               
INIWRP3P MVC   P(80),C             PRINT SPECIAL CONTROL CARD                   
         GOTO1 =V(PRINTER)                                                      
         BASR  RE,RF                                                            
         B     INIWRP3                                                          
*                                                                               
INIWRP4  GOTO1 =V(DATCON),PARA,(5,0),(10,TODAY)                                 
         GOTO1 =V(STXITER),PARA,DUMPLIST                                        
         GOTO1 DATAMGR,DMCB,DMOPEN,DMSYSTEM,DMFILES                             
*                                                                               
INIWRPX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*INTERPRET CONTROL CARDS                                              *         
***********************************************************************         
CONTROL  NTR1  BASE=*                                                           
         L     RE,=A(PARMTBL)      SEARCH TABLE OF VALID PARMS                  
         SR    R1,R1                                                            
CON1     CLI   0(RE),C' '          ERROR IF NOT IN TABLE                        
         BE    CONERR                                                           
         IC    R1,16(RE)           GET LEN OF PARM VALUE                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   C(0),0(RE)                                                       
         BE    *+12                                                             
         LA    RE,L'PARMTBL(RE)                                                 
         B     CON1                                                             
         L     RF,20(RE)           GET ADR OF ROUTINE/FIELD                     
*                                                                               
CON2     TM    18(RE),X'01'        MVI TYPE PARM                                
         BZ    CON3                                                             
         MVC   0(1,RF),17(RE)      SET IMMEADIATE VALUE                         
         B     CONOK                                                            
*                                                                               
CON3     TM    18(RE),X'02'        MVC TYPE PARM                                
         BZ    CON4                                                             
         LA    R2,C+1(R1)          POINT TO SOURCE FIELD                        
         IC    R1,17(RE)           GET LEN OF RECEIVING FIELD                   
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R2)       MOVE DATA TO FIELD                           
         B     CONOK                                                            
*                                                                               
CON4     TM    18(RE),X'04'        ROUTINE TYPE PARM                            
         BZ    CON5                                                             
         BR    RF                  GO TO VALIDATION ROUTINE                     
*                                                                               
CON5     B     CONERR                                                           
*                                                                               
CONJCL   L     RF,AJCLIN           ROUTINE FOR $$..... JCL STATEMENTS           
         CLI   0(RF),X'FF'                                                      
         BE    CONERR              NO  MORE ROOM                                
         MVC   0(80,RF),C                                                       
         MVC   0(2,RF),=C'//'                                                   
         LA    RF,80(RF)                                                        
         ST    RF,AJCLIN           BUMP FOR NEXT CARD                           
         B     CONOK                                                            
*                                                                               
CONDD    L     RF,ADDIN            ROUTINE FOR ##..... DD STATEMENTS            
         CLI   0(RF),X'FF'                                                      
         BE    CONERR              NO  MORE ROOM                                
         MVC   0(80,RF),C                                                       
         MVC   0(2,RF),=C'//'                                                   
         LA    RF,80(RF)                                                        
         ST    RF,ADDIN            BUMP FOR NEXT CARD                           
         B     CONOK                                                            
*                                                                               
CONDAT   CLC   C+5(6),=C'CREATE'   ROUTINE FOR DATE=                            
         BNE   CONDAT0                                                          
         MVI   TODAYJOB,C'Y'       SET USE CREATION DATE IN JOB RECORD          
         B     CONOK                                                            
CONDAT0  L     RF,=V(DATVAL)                                                    
         LTR   RF,RF                                                            
         BZ    CONDAT1                                                          
         GOTO1 (RF),PARA,(0,C+5),DUB                                            
         OC    0(4,R1),0(R1)                                                    
         BZ    CONERR                                                           
CONDAT1  MVC   TODAY,C+5           OVERRIDE IPL DATE                            
         B     CONOK                                                            
*                                                                               
CONNPD   L     RF,=V(DATVAL)       ROUTINE FOR NPDATE=                          
         LTR   RF,RF                                                            
         BZ    CONNPD1                                                          
         GOTO1 (RF),PARA,(0,C+7),DUB                                            
         OC    0(4,R1),0(R1)                                                    
         BZ    CONERR                                                           
CONNPD1  MVC   NPDATE,C+7          OVERRIDE NO PRINT DATE                       
         B     CONOK                                                            
*                                                                               
CONSCN   BAS   RE,SETSCAN          ROUTINE FOR SCAN=                            
         BNE   CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONSYS   L     R2,=A(SYSTAB)       ROUTINE FOR SYSTEM=                          
         MVC   SYSTEM,C+7          SAVE SYSTEM/SUBSYSTEM                        
*&&US                                                                           
         CLI   SYSTEM,C'N'         TEST NET SYSTEM                              
         BNE   CONSYS1                                                          
         MVC   DUB(2),=C'S='       SET S=NXX FOR NETPAK SYSTEM                  
         MVC   DUB+2(3),SYSTEM                                                  
         GOTO1 =V(DMDDNAME),DMCB,=C'DDNAME',DUB                                 
         CLI   DMCB+8,0                                                         
         BNE   CONSYS1                                                          
         L     R1,DMCB+8           R1=A(NET SYSTEM INFO DATA)                   
         USING DDNAD,R1                                                         
         CLI   DDNASEN3,C'S'       TEST IF SPOT SYSTEM                          
         BNE   CONSYS1                                                          
         MVC   SYSTEM,DDNASEN3     CONVERT NET TO SPOT                          
         DROP  R1                                                               
*&&                                                                             
CONSYS1  CLC   SYSTEM,0(R2)                                                     
         BE    CONSYS2             MATCH ON 2/3 CHR SYSTEM (EG A1)              
         CLC   SYSTEM(1),0(R2)                                                  
         BE    CONSYS2             MATCH ON ONE CHR SYSTEM (EG A)               
         CLI   0(R2),C' '                                                       
         BE    CONSYS2             ELSE USE END OF TABLE VALUES                 
         LA    R2,5(R2)                                                         
         B     CONSYS1                                                          
CONSYS2  MVC   JCLASS,3(R2)                                                     
         MVC   DISP,4(R2)          SAVE DEFAULT DISP FOR SYSTEM                 
         CLI   JCLASS,C' '                                                      
         BNE   *+8                                                              
         MVI   JCLASS,C'A'         SET DEFAULT CLASS FOR SYSTEM                 
         CLI   DISP,C' '                                                        
         BNE   *+8                                                              
         MVI   DISP,C'D'           SET DEFAULT DISP FOR SYSTEM                  
CONSYS3  L     R2,=A(SYSLST)                                                    
         LH    R0,SYSMAX                                                        
CONSYS4  CLC   0(3,R2),SYSTEM      CHECK FOR DUPLICATE SYSTEM                   
         BE    CONERR                                                           
         CLC   0(3,R2),SPACES      CHECK FOR FREE SPACE                         
         BE    CONSYS5                                                          
         LA    R2,5(R2)                                                         
         BCT   R0,CONSYS4                                                       
         B     CONERR              TABLE IS FULL                                
CONSYS5  MVC   0(5,R2),SYSTEM      MOVE SYSTEM/JCLASS/DISP TO TABLE             
         LH    R2,SYSINP                                                        
         LA    R2,1(R2)                                                         
         STH   R2,SYSINP           BUMP NUMBER OF SYSTEMS INPUT                 
         B     CONOK                                                            
*                                                                               
CONSRE   LA    R2,C+14             ROUTINE FOR START REQUEST=                   
         LA    R3,REQSTART                                                      
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONERE   LA    R2,C+12             ROUTINE FOR END REQUEST=                     
         LA    R3,REQEND                                                        
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONNHC   LA    R2,C+6              ROUTINE FOR NETHC=                           
         LA    R3,DUB                                                           
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         CP    DUB(4),=P'99'                                                    
         BH    CONERR                                                           
         UNPK  NETHC,DUB(4)                                                     
         OI    NETHC+1,X'F0'                                                    
         B     CONOK                                                            
*                                                                               
CONSTP   LA    R2,C+9              ROUTINE FOR MAXSTEPS=                        
         LA    R3,MAXSTEPS                                                      
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         CP    MAXSTEPS,=P'1'                                                   
         BL    CONERR                                                           
         CP    MAXSTEPS,=P'255'                                                 
         BH    CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONTAP   CLI   C+09,C' '           ROUTINE FOR MAXTAPES=                        
         BE    CONTAP1                                                          
         CLI   C+09,C'S'           S FOR SPOOL TAPE                             
         BNE   *+12                                                             
         OI    MAXTAPES,X'80'      SET SPOOL TAPE FLAG                          
         B     CONTAP1                                                          
         CLI   C+09,C'D'           D FOR DATA TAPE                              
         BNE   *+12                                                             
         OI    MAXTAPES,X'40'      SET DATA TAPE FLAG                           
         B     CONTAP1                                                          
         B     CONERR                                                           
CONTAP1  CLI   C+10,C' '                                                        
         BE    CONTAP2                                                          
         CLI   C+10,C'S'           S FOR SPOOL TAPE                             
         BNE   *+12                                                             
         OI    MAXTAPES,X'80'      SET SPOOL TAPE FLAG                          
         B     CONTAP2                                                          
         CLI   C+10,C'D'           D FOR DATA TAPE                              
         BNE   *+12                                                             
         OI    MAXTAPES,X'40'      SET DATA TAPE FLAG                           
         B     CONTAP2                                                          
         B     CONERR                                                           
CONTAP2  B     CONOK                                                            
*                                                                               
CONREQ   LA    R2,C+8              ROUTINE FOR MAXREQS=                         
         LA    R3,MAXREQS                                                       
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         CP    MAXREQS,=P'1'                                                    
         BL    CONERR                                                           
         CP    MAXREQS,=P'255'                                                  
         BH    CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONCDP   LA    R2,C+8              ROUTINE FOR CDPAGES=                         
         LA    R3,CDPAGES                                                       
         BAS   RE,NUMBER                                                        
         BNE   CONERR                                                           
         CP    CDPAGES,=P'1000'                                                 
         BL    CONERR                                                           
         CP    CDPAGES,=P'100000'                                               
         BH    CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONLOA   L     R2,=A(LOADAREA)     ROUTINE FOR LOAD=                            
         GOTO1 =V(LOADER),PARA,C+5,(R2),(C'A',(R2))                             
         L     R2,PARA+4                                                        
         LTR   R2,R2                                                            
         BZ    CONERR                                                           
         ST    R2,LOADADDR                                                      
         B     CONOK                                                            
*                                                                               
CONSEL   CLC   C+8(10),=C'PROGTYPE=Z' SPECIAL CODE FOR PROGTYPE                 
         BNE   CONSEL1                                                          
         MVC   PROGTYZ,C                                                        
         B     CONOK                                                            
CONSEL1  L     RF,=A(SELADD)       ROUTINE FOR INCLUDE/EXCLUDE                  
         BASR  RE,RF                                                            
         BNE   CONERR                                                           
         B     CONOK                                                            
*                                                                               
CONMLV   CLI   C+09,C' '           ROUTINE FOR MSGLEVEL=                        
         BE    CONMLV1                                                          
         CLI   C+09,C'*'                                                        
         BE    CONMLV1                                                          
         CLI   C+09,C'0'                                                        
         BL    CONERR                                                           
         CLI   C+09,C'2'                                                        
         BH    CONERR                                                           
         MVC   MSGLEV1,C+09                                                     
CONMLV1  CLI   C+10,C' '                                                        
         BE    CONMLV2                                                          
         CLI   C+10,C'*'                                                        
         BE    CONMLV2                                                          
         CLI   C+10,C'0'                                                        
         BL    CONERR                                                           
         CLI   C+10,C'1'                                                        
         BH    CONERR                                                           
         MVC   MSGLEV2,C+10                                                     
CONMLV2  B     CONOK                                                            
*                                                                               
CONTPU   CLI   C+05,C'*'           ROUTINE FOR TAPE=                            
         BE    CONTPU1                                                          
         CLI   C+05,C'C'           TAPE=CART                                    
         BNE   *+14                                                             
         MVC   UNITEQTP,=C'UNIT=CART'                                           
         B     CONTPU1                                                          
         CLI   C+05,C'T'           TAPE=TAPE                                    
         BNE   *+14                                                             
         MVC   UNITEQTP,=C'UNIT=TAPE'                                           
         B     CONTPU1                                                          
         CLI   C+05,C'V'           TAPE=VTS                                     
         BNE   CONERR                                                           
         MVC   UNITEQTP,=C' UNIT=VTS'                                           
         B     CONTPU1                                                          
CONTPU1  MVC   TAPEUNIT,C+05       SET VALUE FOR TAPE=X PARAM CARD              
         L     RE,=A(MVSSYPT1)                                                  
         MVC   15(9,RE),UNITEQTP   SET VALUE FOR SYSPRINT UNIT=                 
         B     CONOK                                                            
*                                                                               
CONSPO   CLI   C+08,C'A'           ROUTINE FOR SPONSOR=V/AG                     
         BL    CONERR                                                           
         CLI   C+08,C'C'                                                        
         BH    CONERR                                                           
         CLI   C+09,C'/'                                                        
         BNE   CONERR                                                           
         CLI   C+10,C' '                                                        
         BNH   CONERR                                                           
         MVC   SPONVER,C+08        SAVE SPONSOR VERSION                         
         MVC   SPONAGY,C+10        SAVE SPONSOR AGENCY ALPHA                    
         B     CONOK                                                            
*                                                                               
CONOK    CR    RB,RB               OK EXIT SET CC EQL                           
         B     CONXIT                                                           
CONERR   LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
CONXIT   XIT1                                                                   
*                                                                               
SETSCAN  NTR1                      ROUTINE TO SET UP SCAN TABLE                 
         MVC   WORK,SPACES                                                      
         LA    R2,C+6                                                           
         LA    R3,WORK+2                                                        
         SR    R4,R4                                                            
         BAS   RE,SCAN6            VALIDATE OPERAND A                           
         STC   R4,WORK             L'A'                                         
         LA    R2,1(R2)                                                         
         LA    R3,WORK+34                                                       
         SR    R4,R4                                                            
         BAS   RE,SCAN6            AND OPERAND B                                
         STC   R4,WORK+1           L'B'                                         
         L     R2,=A(SUBTABLE)                                                  
         LA    R3,SUBTABN                                                       
         LA    R2,4(R2)                                                         
*                                                                               
SCAN2    CLC   0(SUBTABL,R2),SPACES                                             
         BE    SCAN4                                                            
         LA    R2,SUBTABL(R2)                                                   
         BCT   R3,SCAN2                                                         
         B     SCANERR                                                          
*                                                                               
SCAN4    MVC   0(SUBTABL,R2),WORK                                               
         L     R2,=A(SUBTABLE)                                                  
         L     R3,0(R2)                                                         
         LA    R3,1(R3)                                                         
         ST    R3,0(R2)                                                         
         B     SCANOK                                                           
*                                                                               
SCAN6    CLC   0(1,R2),C+5         LOOK FOR DELIMITER                           
         BER   RE                                                               
         MVC   0(1,R3),0(R2)                                                    
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         CH    R4,=Y((SUBTABL-2)/2)                                             
         BH    SCANERR                                                          
         B     SCAN6                                                            
*                                                                               
SCANOK   CR    RB,RB               OK EXIT SET CC EQL                           
         B     SCANXIT                                                          
SCANERR  LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
SCANXIT  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*VAL/CONVERT NUMBER R2=DATA,R3=DESTINATION OF PL4,RF=BINARY VALUE     *         
***********************************************************************         
NUMBER   NTR1  BASE=*,LABEL=*      ROUTINE TO VALIDATE NUMERIC DATA             
         LA    R4,1(R2)            R2=A(SOURCE DATA)                            
         SR    R1,R1               R3=A(OBJECT PL4 FIELD)                       
*                                                                               
NUM2     CLI   0(R4),C' '          R1=L'FIELD-1                                 
         BE    NUM4                                                             
         LA    R4,1(R4)                                                         
         LA    R1,1(R1)                                                         
         B     NUM2                                                             
*                                                                               
NUM4     MVC   DUB,=8X'F0'                                                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVZ   DUB(0),0(R2)                                                     
         CLC   DUB,=8X'F0'                                                      
         BNE   NUMERR                                                           
         EX    R1,*+8                                                           
         B     NUM6                                                             
         PACK  0(4,R3),0(0,R2)                                                  
NUM6     XC    DUB,DUB                                                          
         MVC   DUB+4(4),0(R3)                                                   
         CVB   RF,DUB                                                           
*                                                                               
NUMOK    CR    RB,RB               OK EXIT SET CC EQL                           
         B     NUMXIT                                                           
NUMERR   LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
NUMXIT   XIT1  REGS=(RF)                                                        
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*TEST IF OUTPUT GOING TO PRINT QUEUE AND IF SO CREATE DIRECT= CARD    *         
*BYTE1=1 TO CREATE THE DIRECT=CARD OR BYTE1=0 TO TEST IF DIRECT       *         
*RETURN X'01' IF GOING DIRECT PLUS X'80' IF BOTH                      *         
*+++++++++1+++++++++2+++++++++3+++++++++4+++++++++5+++++++++6+++++++++*         
*123456789012345678901234567890123456789012345678901234567890123456789*         
*                                                                     *         
*IRECT=SPP,FFFF,UUUUU,P,DITCCAC,C,ID=XXXXXXXXXXXXXXXXXX,DDDDDDDDDDD,XX*         
*                                                                     *         
***********************************************************************         
ANYDIR   NTR1  BASE=*                                                           
         MVI   ANYDIRRC,0          SET RETURN CODE                              
         CLI   BYTE1,0             TEST QUERY CALL                              
         BE    *+12                                                             
         L     RE,=A(LDIRECT)      SET LAST DIRECT=... CARD TO NONE             
         MVI   0(RE),C' '                                                       
         OC    DIRDEST,DIRDEST     IF SET DIRDEST= CARD FOUND                   
         BNZ   ANYDIR10            FORCE DIRECT CARD                            
         CLI   DIROUT,C'N'         EXIT IF DIROUTPUT=N                          
         BE    ANYDIRX                                                          
         CLI   DIROUT,C'Y'         FORCE DIRECT IF DIROUTPUT=Y                  
         BE    ANYDIR10                                                         
*&&UK                                                                           
ANYDIRA  CLI   QSYSTEM,C'A'        SPECIALS FOR ACCOUNT SYSTEM                  
         BNE   ANYDIRAX                                                         
         CLC   QPROGRAM,=C'55'     TEST CHECKS                                  
         BNE   ANYDIRAX                                                         
ANYDIRA2 CLC   QJOBPROC+38(3),=C'LSR'                                           
         BNE   ANYDIRAX                                                         
         MVC   QDEST,=H'4000'      SET DEST TO C'LASER'=H'4000'                 
         B     ANYDIR11                                                         
ANYDIRAX DS    0H                                                               
*&&                                                                             
*&&US                                                                           
ANYDIRA  CLI   QSYSTEM,C'A'        SPECIALS FOR ACCOUNT SYSTEM                  
         BNE   ANYDIRAX                                                         
         CLC   QPROGRAM,=C'55'     TEST CHECKS                                  
         BNE   ANYDIRAX                                                         
ANYDIRA1 CLC   QJOBPROC+38(7),=C'SHUTTLE'                                       
         BNE   ANYDIRA2                                                         
         MVC   QDEST,=H'1200'      SET DESTINATION TO C'CHEK'=H'1200'           
         B     ANYDIR10                                                         
ANYDIRA2 CLC   QJOBPROC+38(3),=C'LSR'                                           
         BNE   ANYDIRAX                                                         
         MVC   QDEST,=H'4000'      SET DESTINATION TO C'LSR'=H'4000'            
         B     ANYDIR11                                                         
ANYDIRAX DS    0H                                                               
*&&                                                                             
ANYDIR2  CLI   QCLASS,C'Q'         ANY OF THESE MEANS GOING DIRECT              
         BE    ANYDIR10                                                         
         CLI   QJOBPTYP,C'Q'                                                    
         BE    ANYDIR10                                                         
         CLI   QJOBPTYP,C'B'       TEST BOTH                                    
         BNE   *+12                                                             
         OI    ANYDIRRC,X'80'      SET GOING BOTH                               
         B     ANYDIR10                                                         
         CLC   QJOBPROC(20),=CL20'PRINTING DIRECT'                              
         BE    ANYDIR10                                                         
         CLC   QOUTTYPE(3),=C'CDQ' CDROM VIA PQ                                 
         BE    ANYDIR10                                                         
         CLC   QOUTTYPE(2),=C'C$'  CDROM VIA PQ                                 
         BE    ANYDIR10                                                         
         CLI   QOUTTYPE,C'@'       SQL FORMULA                                  
         BE    ANYDIR10                                                         
         CLI   QOUTTYPE,C'/'       AFP REMOTE FORM CODE                         
         BE    ANYDIR10                                                         
         CLI   QJOBOSID,C' '       OVERNIGHT SOON REPORT ID PRESENT             
         BH    ANYDIR10                                                         
*                                                                               
ANYDIR3  MVC   IDIOTOTY,QJOBORUT   TEST FOR IDI OUTPUT TYPE OVERRIDE            
         MVC   IDIOTSYS,QSYSTEM                                                 
         MVC   IDIOTPRG,QPROGRAM                                                
         BAS   RE,IDIOT                                                         
         BNE   ANYDIRX             EXIT IF NO IDI OVERRIDE                      
*                                                                               
ANYDIR10 MVC   JCL,SPACES                                                       
         MVC   JCL(21),=C'DIRECT=SPP,FFFF,NNNNN'                                
         MVC   JCL+7(1),QSYSTEM                                                 
         MVC   JCL+8(2),QPROGRAM                                                
         MVC   JCL+11(4),QJOBPOWO                                               
         B     ANYDIR12                                                         
*                                                                               
ANYDIR11 MVC   JCL,SPACES                                                       
         MVC   JCL(21),=C'DIRECT=SPP,COLR,NNNNN'                                
         MVC   JCL+7(3),QJOBPOWO                                                
         MVC   JCL+11(4),QJOBPROC+41                                            
*                                                                               
ANYDIR12 OI    ANYDIRRC,X'01'      SET DIRECT=CARD CREATED                      
         CLI   BYTE1,0                                                          
         BE    ANYDIRX             EXIT IF QUERY CALL                           
*                                                                               
         SR    R1,R1                                                            
         MVC   DUB,QDEST                                                        
         ICM   R1,3,DIRDEST        OVER-RIDE DESTINATION                        
         BNZ   *+8                                                              
         ICM   R1,3,DUB            R1=DEST ID NUMBER                            
*&&US                                                                           
ADFIX1   CHI   R1,9158             TEST GMMRE (GM NEW YORK)                     
         BNE   ADFIX1X                                                          
         CLI   QSYSTEM,C'S'        SPOT/NET                                     
         BNE   ADFIX1X                                                          
         CLC   QPROGRAM,=C'I2'     INVOICE MATCHING                             
         BNE   ADFIX1X                                                          
         CLI   QJOBPTYP,C'B'       OUTPUT TYPE BOTH                             
         BNE   ADFIX1X                                                          
         LHI   R1,9156             SET TO GMMPTR (GM TROY)                      
ADFIX1X  DS    0H                                                               
*&&                                                                             
         CVD   R1,DUB                                                           
         UNPK  JCL+16(5),DUB                                                    
         OI    JCL+20,X'F0'                                                     
*                                                                               
ANYDIR40 MVC   JCL+21(8),=C',Q,    A'                                           
         CLI   QJOBPTYP,C'B'       TEST IF BOTH PRINT AND DIRECT                
         BNE   *+8                                                              
         MVI   JCL+22,C'P'                                                      
         TM    ARCFLAG,X'08'+X'02' TEST IF SYSPRINT ARCHIVE COPY                
         BNO   *+8                                                              
         MVI   JCL+22,C'P'         SET TO BOTH PRINT AND DIRECT                 
*                                                                               
         CLI   QJOBART,C' '        REPORT TYPE                                  
         BNH   ANYDIR42                                                         
         MVC   JCL+26(1),QJOBART                                                
         CLI   SVARCTYP,C' '       SET REPORT TYPE FROM ARC=CARD                
         BNH   ANYDIR42                                                         
         MVC   JCL+26(1),SVARCTYP                                               
*                                                                               
ANYDIR42 MVI   JCL+29,C' '         ARCHIVE STATUS                               
         TM    ANYARCRC,X'02'      TEST IF ARC=CARD GENERATED                   
         BZ    ANYDIR44                                                         
         MVC   JCL+27(1),SVARCCLS  SET ARCHIVE CLASS FROM ARC=CARD              
         CLI   JCL+27,C' '                                                      
         BNH   ANYDIR44                                                         
         TM    ARCFLAG,X'08'       TEST IF SYSPRINT FOR ARCHIVE                 
         BZ    ANYDIR44                                                         
         NI    JCL+27,X'BF'        LOWER CASE MEANS ARCHIVED                    
         MVI   JCL+29,C'D'                                                      
         B     ANYDIR46                                                         
*                                                                               
ANYDIR44 TM    ANYARCRC,X'02'      TEST IF ARC=CARD GENERATED                   
         BZ    ANYDIR48                                                         
         MVC   JCL+27(1),SVARCCLS  UPPER CASE MEANS ARCHIVABLE                  
         MVI   JCL+29,C'A'                                                      
ANYDIR46 TM    ANYARCRC,X'40'      TEST ARCHIVE ELIGIBLE                        
         BZ    ANYDIR48                                                         
         MVC   JCL+27(1),SVARCCLS                                               
         MVI   JCL+29,C'E'         SET ARCHIVE ELIGIBLE                         
*                                                                               
ANYDIR48 MVC   JCL+28(1),QCLASS    REPORT CLASS                                 
         CLI   QJOBPQC,C'A'                                                     
         BL    *+10                                                             
         MVC   JCL+28(1),QJOBPQC   REPORT CLASS OVERRIDE                        
         MVC   JCL+30(3),=C'N,1'                                                
         CLC   QOUTTYPE(4),=C'DOWN'                                             
         BE    *+14                                                             
         CLC   QOUTTYPE(5),=C'DIRDL'                                            
         BNE   ANYDIR56                                                         
         MVI   JCL+24,C'D'         SET DIRECT/DOWNLOADABLE REPORT               
         B     ANYDIR70            AND DONT SENT LOGOS                          
*                                                                               
ANYDIR56 CLC   QOUTTYPE(3),=C'CDQ'                                              
         BE    *+14                                                             
         CLC   QOUTTYPE(2),=C'C$'                                               
         BNE   ANYDIR58                                                         
         MVI   JCL+28,C'$'         SET SPECIAL CLASS OF $ FOR CDROM             
         MVI   JCL+30,C'Y'         SET WANT LOGOS                               
         B     ANYDIR70                                                         
*                                                                               
ANYDIR58 CLC   QOUTTYPE(3),=C'SQL'                                              
         BE    *+14                                                             
         CLC   QOUTTYPE(6),=C'DIRSQL'                                           
         BNE   ANYDIR60                                                         
         MVI   JCL+24,C'S'         SET DIRECT/SQL REPORT                        
         B     ANYDIR70            AND DONT SENT LOGOS                          
*                                                                               
ANYDIR60 CLI   QOUTTYPE,C'@'                                                    
         BNE   ANYDIR62                                                         
         MVI   JCL+24,C'S'         SET DIRECT/SQL REPORT                        
         MVI   JCL+55,C','                                                      
         MVC   JCL+56(1),QSYSTEM   SET C'SPP  @ABCDE' AS REPORT DESC            
         MVC   JCL+57(2),QPROGRAM                                               
         MVC   JCL+61(6),QOUTTYPE                                               
         B     ANYDIR70            NO LOGOS FOR SQL REPORTS                     
*                                                                               
ANYDIR62 CLI   QOUTTYPE,C'/'                                                    
         BNE   ANYDIR63                                                         
         MVI   JCL+55,C','                                                      
         MVC   JCL+56(1),QSYSTEM   SET C'SPP  /SPPXX' AS REPORT DESC            
         MVC   JCL+57(2),QPROGRAM                                               
         MVC   JCL+61(6),QOUTTYPE                                               
         CLC   QOUTTYPE+3(3),SPACES                                             
         BNE   *+22                                                             
         MVC   JCL+62(1),QSYSTEM                                                
         MVC   JCL+63(2),QPROGRAM                                               
         MVC   JCL+65(2),QOUTTYPE+1                                             
         B     ANYDIR70            NO LOGOS FOR AFP REPORTS                     
*                                                                               
ANYDIR63 EQU   *                                                                
*&&US                                                                           
         CLI   QSYSTEM,C'T'        TALENT SYSTEM WANTS LOGOS                    
         BNE   ANYDIR64                                                         
         MVI   JCL+30,C'Y'                                                      
         B     ANYDIR70                                                         
*&&                                                                             
ANYDIR64 CLC   QJOBORUT(2),=C'S1'  TEST IDI OVERRIDE TO FORCE DIRECT            
         BE    *+12                                                             
         CLI   QJOBORUT,C'#'                                                    
         BNE   ANYDIR70                                                         
         L     R1,=A(LOGOTAB)                                                   
ANYDIR66 CLI   0(R1),X'FF'         SEARCH TABLE OF AGENCY ALPHAS                
         BE    ANYDIR70                                                         
         CLC   QUSER,0(R1)                                                      
         BE    ANYDIR67                                                         
         LA    R1,2(R1)            BUMP TO NEXT ENTRY                           
         B     ANYDIR66                                                         
ANYDIR67 MVI   JCL+30,C'Y'         SET WANT LOGOS ON DIRECT REPORTS             
*                                                                               
ANYDIR70 CLI   QJOBOSID,C' '       TEST OVERNIGHT SOON                          
         BNH   ANYDIR72                                                         
         MVI   JCL+33,C','         OUTPUT PQ REPORT INFO                        
         MVC   JCL+34(3),=C'ID='                                                
         GOTO1 =V(HEXOUT),PARA,QDEST,JCL+37,2,=C'TOG'                           
         GOTO1 (RF),(R1),QJOBOSID,JCL+41,5                                      
         MVC   JCL+51(4),=C'0000'  ZERO CI DISK ADDRESS                         
*                                                                               
ANYDIR72 MVC   JCL+32(1),QJOBCOPY                                               
         CLI   JCL+56,C' '         TEST IF DESCRIPTION ALREADY SET              
         BH    ANYDIR74                                                         
         CLI   QJOBPQDS,C' '                                                    
         BNH   ANYDIR74                                                         
         MVI   JCL+55,C','                                                      
         MVC   JCL+56(11),QJOBPQDS                                              
*                                                                               
ANYDIR74 CLI   QJOBPQFM,C' '       SET REMOTE AFP FORM CODE                     
         BNH   *+14                                                             
         MVI   JCL+67,C','                                                      
         MVC   JCL+68(2),QJOBPQFM                                               
         CLI   QOUTTYPE,C'/'                                                    
         BNE   ANYDIR76                                                         
         MVI   JCL+67,C','                                                      
         MVC   JCL+68(2),QOUTTYPE+1                                             
         CLC   QOUTTYPE+3(3),SPACES                                             
         BE    *+10                                                             
         MVC   JCL+68(2),QOUTTYPE+4                                             
*                                                                               
ANYDIR76 CLI   DIRCLASS,C' '       OVERRIDE CLASS WITH DIRCLASS=. CARD          
         BNH   *+10                                                             
         MVC   JCL+28(1),DIRCLASS                                               
*                                                                               
         L     RE,=A(LDIRECT)      SAVE LAST GENERATED DIRECT=... CARD          
         MVC   0(80,RE),JCL                                                     
         BAS   RE,PUTJCL                                                        
*                                                                               
ANYDIR78 MVC   JCL,SPACES                                                       
         MVC   JCL(8),=C'BURSTPQ='                                              
         MVC   JCL+8(1),QJOBPQB    PQ BURST FROM PROFILE                        
         CLI   BURSTPQ,C' '                                                     
         BE    *+10                                                             
         MVC   JCL+8(1),BURSTPQ    OVERRIDE WITH BURSTPQ=. CARD                 
         CLI   JCL+8,C' '                                                       
         BNH   *+12                                                             
         BAS   RE,PUTJCL                                                        
         B     ANYDIR80                                                         
         CLC   QOUTTYPE,=C'DOWNBR' PQ BURST FROM OUTPUT TYPE DOWNBR             
         BNE   *+16                                                             
         MVI   JCL+8,C'R'          BURST AT REQUEST LEVEL                       
         BAS   RE,PUTJCL                                                        
         B     ANYDIR80                                                         
         CLC   QOUTTYPE,=C'DOWNBP' PQ BURST FROM OUTPUT TYPE DOWNBP             
         BNE   ANYDIR80                                                         
         MVI   JCL+8,C'P'          BURST AT CHANGE OF PERSON                    
         BAS   RE,PUTJCL                                                        
*                                                                               
ANYDIR80 DS    0H                                                               
*&&US                                                                           
         CLI   QSYSTEM,C'T'        TALENT SYSTEM                                
         BNE   ANYDIRX                                                          
         MVC   JCL,SPACES                                                       
         MVC   JCL(10),=C'LOGOS=USER'  WILL START LOGOS FROM GENCON             
         BAS   RE,PUTJCL                                                        
*&&                                                                             
ANYDIRX  TM    ANYDIRRC,X'01'      EXIT WITH CC EQL ID DIRECT=CARD              
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*TEST IF ARCHIVING REQUIRED AND IF SO CREATE ARCHIVE=CARD             *         
*BYTE1=1 TO CREATE THE ARC=CARD OR BYTE1=0 TO TEST IF ARCHIVE         *         
*RETURN X'02' IF ARCHIVING PLUS X'40' IF ELIGIBLE                     *         
*                                                                     *         
*+++++++++1+++++++++2+++++++++3+++++++++4+++++++++5+++++++++6++++++++ *         
*12345678901234567890123456789012345678901234567890123456789012345678 *         
*                                                                     *         
*RC=AG,SPP,FFFFF ,UUUUU,UUUUU,T,C                                     *         
***********************************************************************         
ANYARC   NTR1  BASE=*                                                           
         MVI   ANYARCRC,0          SET RETURN CODE                              
         CLI   BYTE1,0                                                          
         BE    ANYARC1                                                          
         L     RE,=A(LARC)         SET LAST ARC=... CARD TO NONE                
         MVI   0(RE),C' '                                                       
         NI    ARCFLAG,255-X'02'   SET NO ARC=CARD THIS TIME                    
         MVI   SVARCTYP,C' '                                                    
         MVI   SVARCCLS,C' '                                                    
*                                                                               
ANYARC1  OC    ARCDEST,ARCDEST     IF SET ARCDEST= CARD FOUND                   
         BNZ   ANYARC8             FORCE ARC= CARD                              
         CLI   ARCOUT,C'N'         EXIT IF ARCOUTPUT=N                          
         BE    ANYARCX                                                          
         CLI   ARCOUT,C'Y'         FORCE ARCHIVE= IF ARCOUTPUT=Y                
         BE    ANYARC8                                                          
*                                                                               
ANYARC2  CLI   QJOBARCS,C'N'       ARCHIVABLE FOR PQ                            
         BE    ANYARCX             NO                                           
         CLI   QJOBARCS,C'A'       ARCHIVABLE FOR PQ                            
         BE    ANYARC8             YES                                          
         CLI   QJOBARCS,C'E'       ELIGIBLE FOR ARCHIVING                       
         BNE   ANYARC3                                                          
         OI    ANYARCRC,X'40'                                                   
         B     ANYARC8                                                          
*                                                                               
ANYARC3  CLI   QJOBARC,C' '        ANY OF THESE MEANS ARCHIVING                 
         BH    ANYARC8                                                          
         B     ANYARCX                                                          
*                                                                               
ANYARC8  OI    ANYARCRC,X'02'      SET ARC=CARD DEFINED                         
         CLI   BYTE1,0                                                          
         BE    ANYARCX             EXIT IF QUERY CALL                           
*                                                                               
ANYARC9  MVC   JCL,SPACES                                                       
         MVC   JCL(15),=C'SEGMENT=9999999'                                      
         BAS   RE,PUTJCL                                                        
*                                                                               
ANYARC10 MVC   JCL,SPACES                                                       
         MVC   JCL(29),=C'ARC=AG,SPP,FFFF  ,NNNNN,NNNNN'                        
         MVC   JCL+4(2),QUSER                                                   
         MVC   JCL+7(1),QSYSTEM                                                 
         MVC   JCL+8(2),QPROGRAM                                                
         MVC   JCL+11(4),QJOBPOWO                                               
         CLI   QJOBFMD,C' '        TEST IF AFP FORM DEFINED                     
         BE    ANYARC12                                                         
         MVC   JCL+11(5),QJOBFMD                                                
*                                                                               
ANYARC12 SR    R1,R1               DEST AND ORIGIN USER ID NUMS                 
         ICM   R1,3,ARCDEST        OVER-RIDE DESTINATION                        
         BNZ   *+8                                                              
         ICM   R1,3,QDEST                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JCL+18(5),DUB                                                    
         SR    R1,R1                                                            
         ICM   R1,3,QORIGIN                                                     
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JCL+24(5),DUB                                                    
*                                                                               
ANYARC14 MVC   JCL+29(4),=C',T,C'  ARCHIVE REPORT TYPE AND CLASS                
         MVC   JCL+30(1),QJOBART                                                
         MVC   JCL+32(1),QJOBARC                                                
         CLI   ARCTYPE,C' '        OVERRIDE TYPE WITH ARCTYPE=. CARD            
         BNH   *+10                                                             
         MVC   JCL+30(1),ARCTYPE                                                
         CLI   ARCCLASS,C' '       OVERRIDE ARCC WITH ARCCLASS=. CARD           
         BNH   *+10                                                             
         MVC   JCL+32(1),ARCCLASS                                               
         MVC   SVARCTYP,JCL+30     SAVE REPORT TYPE                             
         MVC   SVARCCLS,JCL+32     SAVE ARCHIVE CLASS                           
*                                                                               
ANYARC16 B     ANYARC20            *NOP*                                        
         MVC   JCL+33(9),=C',00000000' ARCHIVE DOCUMENT TYPE                    
         ICM   RF,15,QJOBADT                                                    
         BZ    ANYARC20                                                         
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JCL+34(8),DUB                                                    
*                                                                               
ANYARC20 CLI   REQWHY,C' '         TEST IF REASON WHY TEXT DEFINED              
         B     ANYARC30            *NOP* BNH                                    
         MVI   JCL+42,C','                                                      
         MVC   JCL+43(22),REQWHY                                                
*                                                                               
ANYARC30 L     RE,=A(LARC)         SAVE LAST GENERATED ARC=.. CARD              
         MVC   0(80,RE),JCL                                                     
         BAS   RE,PUTJCL                                                        
         OI    ARCFLAG,X'02'       SET ARC=CARD OUTPUT                          
         CLI   QJOBARCS,C'E'                                                    
         BNE   *+8                                                              
         OI    ARCFLAG,X'40'       SET BECAUSE REPORT IS ELIGIBLE               
*                                                                               
ANYARCX  TM    ANYARCRC,X'02'      EXIT WITH CC EQL IF ARC=CARD                 
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO PLAY WITH MONACC AND SPOOF                                *         
***********************************************************************         
CHMONACC NTR1  BASE=*              CHANGE MONACC TO ACCEMU                      
         LA    R1,JCL+2                                                         
         LA    R0,30                                                            
CHMONAC2 CLC   0(6,R1),=C'MONACC'                                               
         BE    CHMONAC4                                                         
         CLC   0(5,R1),=C'SPOOF'                                                
         BE    CHMONAC6                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,CHMONAC2                                                      
         B     CHMONACX                                                         
CHMONAC4 MVC   0(6,R1),=C'ACCEMU'                                               
         B     CHMONACX                                                         
CHMONAC6 MVC   0(5,R1),=C'SFEMU'                                                
CHMONACX XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO PLAY WITH SPONSOR VERSION                                 *         
***********************************************************************         
CHSPON   NTR1  BASE=*              CHANGE SPONSOR VERSION                       
         LA    R1,JCL+2                                                         
         LA    R0,30                                                            
CHSPON1  CLC   0(7,R1),=C'SPONSOR'                                              
         BE    CHSPON2                                                          
         LA    R1,1(R1)                                                         
         BCT   R0,CHSPON1                                                       
         B     CHSPONX                                                          
CHSPON2  LR    RE,R1                                                            
         AHI   RE,-1                                                            
         CLI   0(RE),C' '          TEST IF PROC NAME BY DEFAULT                 
         BE    CHSPON4                                                          
         AHI   RE,-4                                                            
         CLC   0(5,RE),=C'PROC='                                                
         BE    CHSPON4                                                          
         B     CHSPONX                                                          
CHSPON4  LA    RE,7(R1)            RE=A(END OF SPONSOR PROCNAME)                
         LA    RF,JCL+79-14                                                     
CHSPON5  CLI   0(RE),C' '          FIND NEXT SPACE ON EXEC CARD                 
         BE    CHSPON6                                                          
         LA    RE,1(RE)                                                         
         CR    RE,RF                                                            
         BNH   CHSPON5                                                          
         B     CHSPONX                                                          
CHSPON6  MVC   0(14,RE),=C',PROG=SPONSOR '                                      
         MVC   13(1,RE),SPONVER                                                 
CHSPONX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO DIG OUT GENERATION NUMBER FOR SPOOL TAPES                 *         
***********************************************************************         
GETSPLCT NTR1  BASE=*                                                           
         LA    R2,6(R3)            THIS IS THE A(SSSTAPE.SS0PPAA)               
         L     R3,=A(SPLPOOL)                                                   
         L     R0,4(R3)                                                         
         LA    R4,8(R3)                                                         
         LTR   R0,R0                                                            
         BZ    GSCNEW                                                           
*                                                                               
GSC2     CLC   0(15,R2),0(R4)      LOOK FOR MATCH                               
         BE    GSCOLD                                                           
         LA    R4,16(R4)                                                        
         BCT   R0,GSC2                                                          
*                                                                               
GSCNEW   MVC   0(15,R4),0(R2)      NEW ENTRY                                    
         MVI   15(R4),1            GEN 1                                        
         L     R1,4(R3)            UPDATE N'ENTRIES                             
         LA    R1,1(R1)                                                         
         C     R1,0(R3)                                                         
         JH    *+2                                                              
         ST    R1,4(R3)                                                         
         MVI   SPLCOUNT,1                                                       
         B     GSCX                                                             
*                                                                               
GSCOLD   LLC   R1,15(R4)           OLD ENTRY - BUMP                             
         LA    R1,1(R1)                                                         
         STC   R1,15(R4)                                                        
         STC   R1,SPLCOUNT                                                      
*                                                                               
GSCX     XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CLEAR SELECTION POOL                                      *         
***********************************************************************         
SELCLEAR NMOD1 0,SELCLEAR                                                       
         L     R2,=A(SELPOOL)                                                   
         LM    R3,R4,0(R2)                                                      
         LA    R2,8(R2)                                                         
         BCTR  R4,0                                                             
*                                                                               
SELCL2   EX    R4,*+8                                                           
         B     *+10                                                             
         XC    0(0,R2),0(R2)                                                    
         LA    R2,1(R2,R4)                                                      
         BCT   R3,SELCL2                                                        
SELCLX   XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO ADD ENTRY TO SELECTION POOL                               *         
***********************************************************************         
SELADD   NMOD1 0,SELADD                                                         
         L     R2,=A(SELPOOL)                                                   
         LM    R3,R4,0(R2)                                                      
         LA    R2,8(R2)                                                         
         BCTR  R4,0                                                             
*                                                                               
         USING SELD,R2                                                          
SELAD2   EX    R4,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),0(R2)                                                    
         BZ    SELAD4                                                           
         LA    R2,1(R2,R4)                                                      
         BCT   R3,SELAD2                                                        
         L     RF,=A(LOGMSG4)                                                   
         GOTO1 =V(LOGIO),PARA,1,(40,(RF))                                       
         B     SELAOK                                                           
*                                                                               
SELAD4   MVC   SCANCARD(72),C+8                                                 
         GOTO1 =V(SCANNER),PARA,(C'C',SCANCARD),(6,BLOCK)                       
         SR    R0,R0                                                            
         ICM   R0,1,4(R1)                                                       
         BZ    SELANOK                                                          
         LA    R1,BLOCK                                                         
*                                                                               
SELAD6   CLC   12(10,R1),=CL10'DEST'                                            
         BNE   *+14                                                             
         MVC   SELDEST,22(R1)                                                   
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'ORIGIN'                                          
         BNE   *+14                                                             
         MVC   SELORIG,22(R1)                                                   
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'AGENCY'                                          
         BNE   *+14                                                             
         MVC   SELUSER,22(R1)                                                   
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'DISP'                                            
         BNE   *+14                                                             
         MVC   SELDISP,22(R1)                                                   
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'SPECIAL'                                         
         BNE   *+14                                                             
         MVC   SELSPEC,22(R1)                                                   
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'OUTPUT'                                          
         BNE   *+14                                                             
         MVC   SELOUT,22(R1)                                                    
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'JOB'                                             
         BNE   *+14                                                             
         MVC   SELJOB,22(R1)                                                    
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'CLASS'                                           
         BNE   *+14                                                             
         MVC   SELCLASS,22(R1)                                                  
         B     SELAD8                                                           
*                                                                               
         CLC   12(10,R1),=CL10'PROGRAM'                                         
         BNE   *+14                                                             
         MVC   SELPROG,22(R1)                                                   
         B     SELAD8                                                           
*&&US                                                                           
         CLC   12(10,R1),=CL10'COMSCORE'                                        
         BNE   *+14                                                             
         MVC   SELCOMSC,22(R1)                                                  
         B     SELAD8                                                           
*&&                                                                             
SELAD7   CLC   12(10,R1),=CL10'PROGTYPE'                                        
         BNE   SELAD7A                                                          
         MVC   SELPTYP,22(R1)                                                   
         CLI   22(R1),C'*'                                                      
         BNE   SELAD8                                                           
         MVC   SELPTYP,23(R1)                                                   
         NI    SELPTYP,X'03'                                                    
         B     SELAD8                                                           
*                                                                               
SELAD7A  CLC   12(10,R1),=CL10'PROGOUTPUT'                                      
         BNE   SELAD7B                                                          
         MVC   SELPOUT,22(R1)                                                   
         CLI   22(R1),C'*'                                                      
         BNE   SELAD8                                                           
         MVC   SELPOUT,23(R1)                                                   
         NI    SELPOUT,X'03'                                                    
         B     SELAD8                                                           
*                                                                               
SELAD7B  EX    R4,*+8                                                           
         B     *+10                                                             
         XC    0(0,R2),0(R2)                                                    
         B     SELANOK                                                          
*                                                                               
SELAD8   LA    R1,32(R1)                                                        
         BCT   R0,SELAD6                                                        
*                                                                               
         MVC   SELIND,C            (E OR I FROM EXCLUDE/INCLUDE)                
         B     SELAOK                                                           
*                                                                               
SELAOK   CR    RB,RB               OK EXIT SET CC EQL                           
         B     SELAXIT                                                          
SELANOK  LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
SELAXIT  XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO SEE IF A JOB QUALIFIES                                    *         
***********************************************************************         
SELFILT  NMOD1 0,SELFILT                                                        
         L     R2,=A(SELPOOL)                                                   
         LM    R3,R4,0(R2)                                                      
         LA    R2,8(R2)                                                         
         BCTR  R4,0                                                             
         MVI   JOBIND,C'I'                                                      
         CLI   0(R2),0                                                          
         BE    SELFOK              NO ENTRIES = OK                              
         CLI   0(R2),C'E'          IF FIRST ENTRY IS AN EXCLUDE                 
         BE    SELFI1              THEN DEFAULT IS INCLUDE                      
         MVI   JOBIND,C'E'                                                      
*                                                                               
         USING SELD,R2                                                          
SELFI1   EX    R4,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),0(R2)                                                    
         BNZ   SELFI3                                                           
*                                                                               
SELFI2   CLI   JOBIND,C'I'                                                      
         BE    SELFOK                                                           
         B     SELFNOK                                                          
*                                                                               
SELFI3   OC    SELDEST,SELDEST                                                  
         BZ    *+14                                                             
         CLC   SELDEST,QJOBJES                                                  
         BNE   SELFNEXT                                                         
         OC    SELJOB,SELJOB                                                    
         BZ    SELFI4                                                           
         MVC   DUB,QJOBJES                                                      
         CLI   QJOBDUPS,C' '       TEST IF DUPLICATE SYSTEMS LETTER             
         BNH   *+10                                                             
         MVC   DUB+4(1),QJOBDUPS                                                
         CLC   SELJOB,DUB                                                       
         BNE   SELFNEXT                                                         
SELFI4   OC    SELORIG,SELORIG                                                  
         BZ    *+14                                                             
         CLC   SELORIG,QJOBOID                                                  
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELUSER,SELUSER                                                  
         BZ    *+14                                                             
         CLC   SELUSER,QUSER                                                    
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELOUT,SELOUT                                                    
         BZ    *+14                                                             
         CLC   SELOUT,QOUTTYPE                                                  
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELCLASS,SELCLASS                                                
         BZ    *+14                                                             
         CLC   SELCLASS,QCLASS                                                  
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELDISP,SELDISP                                                  
         BZ    *+14                                                             
         CLC   SELDISP,QJOBDISP                                                 
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELSPEC,SELSPEC                                                  
         BZ    *+14                                                             
         CLC   SELSPEC,QOUTSPEC                                                 
         BNE   SELFNEXT                                                         
*                                                                               
         OC    SELPROG,SELPROG                                                  
         BZ    *+14                                                             
         CLC   SELPROG,QPROGRAM                                                 
         BNE   SELFNEXT                                                         
*&&US                                                                           
         CLI   SELCOMSC,0          COMSCORE REQUESTS IN JOB                     
         BE    *+12                                                             
         TM    QJOBCTRL,X'01'                                                   
         BZ    SELFNEXT                                                         
*&&                                                                             
SELFI5   CLI   SELPTYP,0           PROGRAM TYPE FILTER                          
         BE    SELFI6                                                           
         CLI   SELPTYP,C'A'        TEST *1 THRU *4 INPUT                        
         BL    SELFI5A                                                          
         CLC   SELPTYP,QPRIPT                                                   
         BNE   SELFNEXT                                                         
         B     SELFI6                                                           
SELFI5A  CLI   SELPTYP,1           TYPE *1 IS A THRU I                          
         BNE   SELFI5B                                                          
         CLI   QPRIPT,C'A'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPT,C'I'                                                      
         BH    SELFNEXT                                                         
         B     SELFI6                                                           
SELFI5B  CLI   SELPTYP,2           TYPE *2 IS J THRU R                          
         BNE   SELFI5C                                                          
         CLI   QPRIPT,C'J'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPT,C'R'                                                      
         BH    SELFNEXT                                                         
         B     SELFI6                                                           
SELFI5C  CLI   SELPTYP,3           TYPE *3 IS S THRU Y                          
         BNE   SELFI5D                                                          
         CLI   QPRIPT,C'S'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPT,C'Y'                                                      
         BH    SELFNEXT                                                         
         B     SELFI6                                                           
SELFI5D  CLI   SELPTYP,4           TYPE *4 IS 0 THRU 9                          
         BNE   SELFI6                                                           
         CLI   QPRIPT,C'0'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPT,C'9'                                                      
         BH    SELFNEXT                                                         
*                                                                               
SELFI6   CLI   SELPOUT,0           PROGRAM OUTPUT FILTER                        
         BE    SELFI7                                                           
         CLI   SELPOUT,C'A'        TEST *1 THRU *4 INPUT                        
         BL    SELFI6A                                                          
         CLC   SELPOUT,QPRIPO                                                   
         BNE   SELFNEXT                                                         
         B     SELFI7                                                           
SELFI6A  CLI   SELPOUT,1           OUTPUT *1 IS A THRU I                        
         BNE   SELFI6B                                                          
         CLI   QPRIPO,C'A'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPO,C'I'                                                      
         BH    SELFNEXT                                                         
         B     SELFI7                                                           
SELFI6B  CLI   SELPOUT,2           OUTPUT *2 IS J THRU R                        
         BNE   SELFI6C                                                          
         CLI   QPRIPO,C'J'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPO,C'R'                                                      
         BH    SELFNEXT                                                         
         B     SELFI7                                                           
SELFI6C  CLI   SELPOUT,3           OUTPUT *3 IS S THRU Y                        
         BNE   SELFI6D                                                          
         CLI   QPRIPO,C'S'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPO,C'Y'                                                      
         BH    SELFNEXT                                                         
         B     SELFI7                                                           
SELFI6D  CLI   SELPOUT,4           OUTPUT *4 IS 0 THRU 9                        
         BNE   SELFI7                                                           
         CLI   QPRIPO,C'0'                                                      
         BL    SELFNEXT                                                         
         CLI   QPRIPO,C'9'                                                      
         BH    SELFNEXT                                                         
*                                                                               
SELFI7   DS    0H                                                               
*                                                                               
SELFMTCH MVC   JOBIND,SELIND       MATCHED ON ALL ENTRIES                       
*                                                                               
SELFNEXT LA    R2,1(R2,R4)         BUMP TO NEXT SELECTION ENTRY                 
         BCT   R3,SELFI1                                                        
         B     SELFI2                                                           
*                                                                               
SELFOK   CR    RB,RB               OK EXIT SET CC EQL                           
         B     SELFXIT                                                          
SELFNOK  LTR   RB,RB               NOT OK EXIT SET CC NEQ                       
SELFXIT  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*GET AGENCY-ALPHA CODE FOR A USER-ID NUMBER                           *         
*ENTER WITH R1=A(OUTPUT AGENCY ALPHA-ID)                              *         
***********************************************************************         
GETUSER  NTR1  BASE=*,LABEL=*                                                   
         L     R2,=A(IO)                                                        
         XC    0(25,R2),0(R2)                                                   
         MVI   0(R2),C'I'                                                       
         MVC   23(2,R2),QORIGIN    USE ORIGIN ID NUMBER                         
         GOTO1 DATAMGR,PARA,DMREAD,CTFILE,(R2),(R2)                             
         CLI   8(R1),0                                                          
         BNE   XIT                                                              
         LA    R2,28(R2)           POINT TO FIRST ELEMENT                       
         SR    R0,R0               LOCATE AGENCY-ID ELEMENT                     
GETUSER2 CLI   0(R2),0             TEST E-O-R                                   
         BE    GETCPY                                                           
         CLI   0(R2),X'06'         ALPHA-ID ELEMENT                             
         BE    GETUSER5                                                         
         CLI   0(R2),X'21'         SYSTEM ELEMENT                               
         BE    GETUSER4                                                         
GETUSER3 IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GETUSER2                                                         
GETUSER4 CLI   CTSYSNUM-CTSYSD(R2),6             ACCOUNT SYSTEM                 
         BNE   GETUSER3                                                         
         L     RE,=A(SOFTCOMP)                                                  
         MVC   0(L'SOFTCOMP,RE),CTSYSAGB-CTSYSD(R2)    AGENCY CODE              
         MVC   ACCSE,CTSYSSE-CTSYSD(R2)   ACC SE NUMBER                         
         MVC   ACCPY,CTSYSAGB-CTSYSD(R2)  ACC COMPANY CODE                      
         B     GETUSER3                                                         
GETUSER5 MVC   QUSER,2(R2)         MOVE ALPHA-ID FOR PAN REQUESTS               
         B     GETUSER3                                                         
         EJECT                                                                  
***********************************************************************         
*GET THE ACC COMPANY RECORD - SET STATUS BYTE                         *         
***********************************************************************         
GETCPY   DS    0H                                                               
*&&US                                                                           
         MVI   ACSTA,0                                                          
         MVI   ACOPN,C'N'                                                       
         LA    R0,MXACB                                                         
         L     R3,=A(ACBUF)                                                     
         USING ACBD,R3                                                          
GETCPY2  CLI   0(R3),X'FF'         TEST END OF TABLE                            
         BE    GETCPY4                                                          
         CLC   ACCSE,ACBSE         MATCH ON FILE                                
         BNE   GETCPY3                                                          
         MVI   ACOPN,C'Y'          SET FILE ALREADY OPENED                      
         CLC   ACCPY,ACBCPY        MATCH ON COMPANY                             
         BE    GETCPYX                                                          
*                                                                               
GETCPY3  LA    R3,ACBLNQ(R3)                                                    
         BCT   R0,GETCPY2                                                       
         DC    H'0'                TABLE IS FULL                                
*                                                                               
GETCPY4  XC    ACBD(ACBLNQ),ACBD                                                
         MVC   ACBSE,ACCSE         SET NEW TABLE ENTRY                          
         MVC   ACBCPY,ACCPY                                                     
         MVI   ACBLNQ(R3),X'FF'    SET NEW EOT                                  
*                                                                               
GETCPY5  L     RF,=V(UTL)                                                       
         MVC   4(1,RF),ACCSE       SET ACC SE                                   
         CLI   ACOPN,C'Y'          TEST FILE OPENED                             
         BE    GETCPY6                                                          
         GOTO1 DATAMGR,DMCB,DMOPEN,ACCOUNT,ACFILEL OPEN ACCOUNT                 
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
*                                                                               
GETCPY6  LA    R2,KEY                                                           
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,ACCPY                                                    
         GOTO1 DATAMGR,DMCB,DMREAD,ACCDIR,KEY,DIR  GET COMPANY                  
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         LA    RF,DIR+(CPYKDA-CPYKEY)                                           
         L     R2,=A(IO)                                                        
         GOTO1 DATAMGR,DMCB,GETREC,ACCMST,(RF),(R2),DMWORK                      
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         LA    R2,CPYRFST                                                       
         SR    R0,R0                                                            
*                                                                               
GETCPY7  CLI   0(R2),CPYELQ        GET COMPANY ELEMENT                          
         BE    GETCPY8                                                          
         CLI   0(R2),0                                                          
         BE    GETCPYX                                                          
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GETCPY7                                                          
*                                                                               
         USING CPYELD,R2                                                        
GETCPY8  CLI   CPYLN,CPYLN3Q                                                    
         BL    GETCPYX                                                          
         MVC   ACBSTA,CPYSTATA     SAVE COMPANY STATUS BYTE                     
*                                                                               
GETCPYX  MVC   ACSTA,ACBSTA        SET STATUS BYTE                              
         L     RF,=V(UTL)                                                       
         MVI   4(RF),10            RESET CONTROL SE NUMBER                      
         DROP  R2,R3                                                            
*&&                                                                             
         B     XIT                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*BUILD MVS OUTPUT CARDS AND SYSPRINT                                  *         
***********************************************************************         
IMVOC    NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(AFP1)         //OU001 OUTPUT CARD#1                        
         L     RF,=A(MVSOUT)                                                    
         ST    R3,AFRSTO           SAVE A(FIRST OUTPUT CARD)                    
         ST    R3,ALASTO                                                        
         MVC   NFRSTO,=H'1'        SET NUMBER OF CARDS IN SET                   
         MVC   NSETS,=H'1'         SET NUMBER OF SETS                           
         XC    AFRSTCLA,AFRSTCLA                                                
         XC    AFRSTFRM,AFRSTFRM                                                
         XC    ALASTFRM,ALASTFRM                                                
         XC    AFRSTCOP,AFRSTCOP                                                
         XC    AFRSTDES,AFRSTDES                                                
         XC    AFRSTFOR,AFRSTFOR                                                
         XC    AFRSTPAG,AFRSTPAG                                                
         MVC   0(80,R3),0(RF)      //OU001 OUTPUT CLASS=X                       
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDF,OUTCRDN     SAVE NUM OF FIRST OF THIS SET                
         ZAP   OUTCRDL,OUTCRDN                                                  
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
         LA    R2,15(R3)                                                        
         ST    R2,AFRSTCLA                                                      
*                                                                               
IMVOC1A  MVC   6(1,R2),CLASS       SET CLASS                                    
*&&UK*&& CLI   CLASS,C'Q'          IF GOING DIRECT                              
*&&UK*&& BNE   *+8                                                              
*&&UK*&& MVI   6(R2),C'0'          DELETE THIS OUTPUT                           
         LA    R2,7(R2)                                                         
*                                                                               
IMVOC1B  CLC   FORMS,SPACES        SET FORMS                                    
         BE    IMVOC1B2                                                         
         MVC   0(7,R2),=C',FORMS='                                              
         MVC   7(4,R2),FORMS                                                    
         LA    R2,1(R2)                                                         
         ST    R2,AFRSTFRM                                                      
         LA    R2,6(R2)            BUMP TO END OF FORMS                         
         CLI   0(R2),C' '                                                       
         BE    IMVOC1B1                                                         
         LA    R2,1(R2)                                                         
         B     *-12                                                             
IMVOC1B1 ST    R2,ALASTFRM                                                      
IMVOC1B2 DS    0H                                                               
*                                                                               
IMVOC1C  MVC   0(11,R2),=C',COPIES=001'                                         
         CLI   QJOBCOPY,C' '       TEST IF DEFINED AS SPACE CHR                 
         BE    IMVOC1C2                                                         
         CLI   QJOBCOPY,C'0'       TEST IF DEFINED AS A CHARACTER               
         BL    IMVOC1C1                                                         
         MVC   10(1,R2),QJOBCOPY                                                
         B     IMVOC1C2                                                         
IMVOC1C1 LLC   R1,QJOBCOPY         DEFINED AS BINARY VALUE                      
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  8(3,R2),DUB                                                      
IMVOC1C2 LA    R2,1(R2)                                                         
         ST    R2,AFRSTCOP                                                      
         LA    R2,10(R2)                                                        
*                                                                               
IMVOC1D  CLC   QJOBCC,=C'NONE'                                                  
         BE    IMVOC1D2                                                         
         CLC   QJOBCC,SPACES                                                    
         BE    IMVOC1D2                                                         
IMVOC1D1 MVC   0(5,R2),=C',FCB='                                                
         MVC   5(4,R2),QJOBCC                                                   
         LA    R2,5(R2)                                                         
         CLI   0(R2),C' '                                                       
         BE    IMVOC1D2                                                         
         LA    R2,1(R2)                                                         
         B     *-12                                                             
IMVOC1D2 LA    RF,1(R2)                                                         
         ST    RF,AFRSTDES                                                      
*                                                                               
IMVOC1E  CLI   AFP,C' '            APF DESTINATION INPUT                        
         BE    IMVOC1F                                                          
         MVC   0(6,R2),=C',DEST='                                               
         MVC   6(8,R2),AFP                                                      
         LA    R2,6(R2)                                                         
         CLI   0(R2),C' '                                                       
         BE    IMVOC2                                                           
         LA    R2,1(R2)                                                         
         B     *-12                                                             
*                                                                               
IMVOC1F  MVC   WORK,RJEOPT         RJE SUPPORT                                  
         CLC   QJOBORUT(3),=C'RJE'                                              
         BNE   *+10                                                             
         MVC   WORK(3),QJOBORUT+3                                               
         CLC   WORK(3),SPACES                                                   
         BE    IMVOC1F1                                                         
         CLC   QJOBJES+4(3),=C'A55'                                             
         BE    IMVOC1F1                                                         
         MVC   0(6,R2),=C',DEST='                                               
         MVC   6(3,R2),WORK                                                     
         LA    R2,9(R2)                                                         
IMVOC1F1 DS    0H                                                               
*                                                                               
IMVOC2   MVI   0(R2),C','          // OUTPUT CARD#2                             
         LH    R3,NFRSTO                                                        
         LA    R3,1(R3)                                                         
         STH   R3,NFRSTO           BUMP NUMBER OF CARDS IN FIRST SET            
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ALASTO                                                        
         MVC   0(2,R3),=C'//'                                                   
         LA    R2,15(R3)                                                        
*                                                                               
IMVOC2B  MVC   0(6,R2),=C'FLASH='                                               
         MVC   6(4,R2),FLASH                                                    
         CLI   FLASH,C' '                                                       
         BNE   *+10                                                             
         MVC   6(4,R2),=C'NONE'                                                 
         MVI   10(R2),C','                                                      
         LA    R2,11(R2)                                                        
*                                                                               
IMVOC2C  CLI   QJOBCHRS,C' '       SINGLE CHARS=                                
         BE    IMVOC2X                                                          
         CLI   QJOBCHR2,C' '                                                    
         BNE   IMVOC2D                                                          
         MVC   0(6,R2),=C'CHARS='                                               
         MVC   6(4,R2),QJOBCHRS                                                 
         MVI   10(R2),C','                                                      
         LA    R2,11(R2)                                                        
         B     IMVOC2X                                                          
*                                                                               
IMVOC2D  MVC   0(7,R2),=C'CHARS=(' CHARS=(CHR1,CHR2,CHR3,CHR4)                  
         MVC   7(4,R2),QJOBCHRS                                                 
         LA    R2,11(R2)                                                        
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR2                                                 
         LA    R2,5(R2)                                                         
         CLI   QJOBCHR3,C' '                                                    
         BE    IMVOC2E                                                          
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR3                                                 
         LA    R2,5(R2)                                                         
         CLI   QJOBCHR4,C' '                                                    
         BE    IMVOC2E                                                          
         MVI   0(R2),C','                                                       
         MVC   1(4,R2),QJOBCHR4                                                 
         LA    R2,5(R2)                                                         
IMVOC2E  MVI   0(R2),C')'          TERMINATE CHARS=( STRING                     
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
*                                                                               
IMVOC2X  BCTR  R2,0                REMOVE TRAILING COMMA                        
         CLI   0(R2),C','                                                       
         BNE   *+8                                                              
         MVI   0(R2),C' '                                                       
*                                                                               
IMVOC3   CLI   QJOBFMN,0           TEST IF ANY FORMDEF/PAGEDEF INFO             
         BNE   IMVOC3A                                                          
*&&UK*&& B     IMVOC4                                                           
IMVOC3A  MVI   0(R2),C','          SET CONTINUATION ON PREVIOUS CARD            
         LH    R3,NFRSTO                                                        
         LA    R3,1(R3)                                                         
         STH   R3,NFRSTO           BUMP NUMBER OF CARDS IN FIRST SET            
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ALASTO                                                        
         MVC   0(2,R3),=C'//'                                                   
         LA    R2,15(R3)                                                        
*                                                                               
IMVOC3B  CLI   QJOBFMD,C' '                                                     
         BE    IMVOC3C                                                          
         OI    PORFDEFS,X'01'      SET FORMDEF OUTPUT                           
         ST    R2,AFRSTFOR                                                      
         MVC   0(8,R2),=C'FORMDEF='                                             
         MVC   8(5,R2),QJOBFMD                                                  
         MVI   13(R2),C'1'                                                      
         LA    R2,14(R2)                                                        
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
*                                                                               
IMVOC3C  LA    RF,QJOBPGD          OUPUT PAGEDEF AND SAVE LOCATION              
         CLI   QJOBPGD,C' '                                                     
         BNE   IMVOC3C1                                                         
         LA    RF,=CL6'DSPAGE'     USE DUMMY PAGEDEF NAME                       
IMVOC3C1 OI    PORFDEFS,X'02'      SET PAGEDEF OUTPUT                           
         ST    R2,AFRSTPAG                                                      
         MVC   0(8,R2),=C'PAGEDEF='                                             
         MVC   8(5,R2),0(RF)       SET ACTUAL PAGEDEF NAME                      
         MVI   13(R2),C'1'                                                      
         CLC   0(6,RF),=CL6'DSPAGE'                                             
         BNE   *+14                                                             
         MVC   8(6,R2),0(RF)       SET DUMMY PAGEDEF NAME                       
         NI    PORFDEFS,255-X'02'  AND TURN OFF PAGEDEF OUTPUT FLAG             
         LA    R2,14(R2)                                                        
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
*                                                                               
IMVOC3D  DS    0H                                                               
IMVOC3X  BCTR  R2,0                REMOVE TRAILING COMMA                        
         CLI   0(R2),C','                                                       
         BNE   *+8                                                              
         MVI   0(R2),C' '                                                       
*                                                                               
IMVOC4   DS    0H                                                               
*                                                                               
IMVOCA   CLC   NSETS+1(1),QJOBFMN  TEST IF MULTIPLE SETS REQUIRED               
         BNL   IMVOCAX                                                          
         LH    RF,NSETS            BUMP FOR NEXT SETS                           
         LA    RF,1(RF)                                                         
         STH   RF,NSETS                                                         
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ANEXTO                                                        
         L     RE,AFRSTO           GET A(FIRST CARD IN SET)                     
         LH    RF,NFRSTO           GET NUMBER OF CARDS IN SET                   
IMVOCA2  MVC   0(80,R3),0(RE)                                                   
         ST    R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         LA    RE,80(RE)                                                        
         BCT   RF,IMVOCA2                                                       
         L     R3,ANEXTO                                                        
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDL,OUTCRDN     SAVE NUM OF LAST IN THIS SET                 
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
*                                                                               
IMVOCA3  ICM   RF,15,AFRSTFOR      LOCATE FORMDEF=.....N                        
         BZ    IMVOCA4                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         LH    RE,NSETS                                                         
         IC    RE,NUMSLETS(RE)                                                  
         STC   RE,13(RF)           PLUG IN NEXT NUMBER OR LETTER                
*                                                                               
IMVOCA4  ICM   RF,15,AFRSTPAG      LOCATE PAGEDEF=.....N                        
         BZ    IMVOCA5                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         CLC   8(6,RF),=CL6'DSPAGE'                                             
         BE    IMVOCA5                                                          
         LH    RE,NSETS                                                         
         IC    RE,NUMSLETS(RE)                                                  
         STC   RE,13(RF)           PLUG IN NEXT NUMBER OR LETTER                
         CLI   QJOBPGN,1                                                        
         BNE   *+8                                                              
         MVI   13(RF),C'1'         ONLY ONE PAGEDEF                             
*                                                                               
IMVOCA5  B     IMVOCA              BACK FOR NEXT SET                            
IMVOCAX  DS    0H                                                               
*                                                                               
IMVOCC   MVI   WHATDEST,C' '       SET EXTRA KEEP COPIES FLAG                   
         MVI   FLAGDEST,0                                                       
         CLI   QJOBDISP,C'K'       TEST OUTPUT DISP KEEP                        
         BE    IMVOCC0B                                                         
         CLI   QJOBKEEP,C'Y'                                                    
         BE    IMVOCC0B                                                         
*                                                                               
IMVOCC0  CLI   BETAOPTN,C' '       TEST EXTRA COPY TO BETA OPTION               
         BE    IMVOCCX                                                          
         CLI   QJOBPDD,C'Y'        TEST NO PRINTING AT DDS DATE                 
         BNE   IMVOCCX                                                          
         TM    ANYDIRRC,X'01'      TEST IF DIRECT=CARD                          
         BO    IMVOCCX             YES                                          
         CLI   BETAKEEP,C' '                                                    
         BE    IMVOCCX                                                          
         MVI   WHATDEST,C'B'       SET WANT EXTRA DEST=BETA COPY                
         OI    FLAGDEST,X'02'                                                   
         XC    NSETS,NSETS                                                      
         LLC   R0,QJOBFMN          SET ONLY WANT ONE SET OF DEST=BETA           
         AHI   R0,-1                                                            
         BNP   *+8                                                              
         STC   R0,NSETS+1          INIT COUNT TO QJOBFMN-1                      
         B     IMVOCC1                                                          
*                                                                               
IMVOCC0B MVI   WHATDEST,C'K'       SET WANT DEST=KEEP OUTPUT CARD(S)            
         OI    FLAGDEST,X'01'                                                   
*                                                                               
IMVOCC0C CLI   BETAONLY,C'Y'       DO WE WANT TO USE BETAKEEP                   
         BE    *+14                                                             
         XC    NSETS,NSETS                                                      
         B     IMVOCC1                                                          
         CLI   BETAKEEP,C' '       TEST BETAKEEP= INPUT                         
         BE    IMVOCC1                                                          
         MVI   WHATDEST,C'B'       SET ONLY WANT DEST=BETA COPIES               
*                                                                               
IMVOCC1  LH    RF,NSETS            BUMP FOR NEXT SETS                           
         LA    RF,1(RF)                                                         
         STH   RF,NSETS                                                         
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ANEXTO                                                        
         L     RE,AFRSTO           GET A(FIRST CARD IN SET)                     
         LH    RF,NFRSTO           GET NUMBER OF CARDS IN SET                   
IMVOCC2  MVC   0(80,R3),0(RE)                                                   
         ST    R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         LA    RE,80(RE)                                                        
         BCT   RF,IMVOCC2                                                       
         L     R3,ANEXTO                                                        
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDL,OUTCRDN     SAVE NUM OF LAST IN THIS SET                 
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
*                                                                               
IMVOCC3  ICM   RF,15,AFRSTFOR      LOCATE FORMDEF=.....N                        
         BZ    IMVOCC4                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         LH    RE,NSETS                                                         
         IC    RE,NUMSLETS(RE)                                                  
         STC   RE,13(RF)           PLUG IN NEXT NUMBER OR LETTER                
*                                                                               
IMVOCC4  ICM   RF,15,AFRSTPAG      LOCATE PAGEDEF=.....N                        
         BZ    IMVOCC5                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         CLC   8(6,RF),=CL6'DSPAGE'                                             
         BE    IMVOCC5                                                          
         LH    RE,NSETS                                                         
         IC    RE,NUMSLETS(RE)                                                  
         STC   RE,13(RF)           PLUG IN NEXT NUMBER OR LETTER                
         CLI   QJOBPGN,1                                                        
         BNE   IMVOCC5                                                          
         MVI   13(RF),C'1'         ONLY ONE PAGEDEF                             
*                                                                               
IMVOCC5  ICM   RF,15,AFRSTCOP      LOCATE COPIES=NNN                            
         BZ    IMVOCC6                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         B     *+10                LEAVE SAME AS ORIGINAL VALUE                 
         MVC   7(3,RF),=C'001'     SET TO ONE COPY                              
*                                                                               
IMVOCC6  ICM   RF,15,AFRSTDES      LOCATE DEST=XXXXXXXX                         
         BZ    IMVOCC7                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         BCTR  RF,0                                                             
         MVC   0(15,RF),=C',DEST=KEEP,    '                                     
         CLI   WHATDEST,C'K'                                                    
         BE    IMVOCC6A                                                         
         MVC   0(15,RF),=C',DEST=BETA,    '                                     
*                                                                               
IMVOCC6A ICM   RF,15,AFRSTCLA      LOCATE CLASS=                                
         BZ    IMVOCC7                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
IMVOCC6B CLI   WHATDEST,C'K'       TEST DEST=KEEP                               
         BNE   IMVOCC6C                                                         
         CLI   AFPKEEP,C' '        TEST AFPKEEP= ENTERED                        
         BE    IMVOCC7                                                          
         MVC   6(1,RF),AFPKEEP                                                  
         B     IMVOCC7                                                          
IMVOCC6C CLI   WHATDEST,C'B'       TEST DEST=BETA                               
         BNE   IMVOCC7                                                          
         CLI   BETAKEEP,C' '       TEST BETAKEEP= ENTERED                       
         BE    IMVOCC7                                                          
         MVC   6(1,RF),BETAKEEP                                                 
         B     IMVOCC7                                                          
*                                                                               
IMVOCC7  CLC   NSETS+1(1),QJOBFMN  TEST IF MULTIPLE SETS REQUIRED               
         BL    IMVOCC1             YES BACK FOR NEXT SET                        
*                                                                               
IMVOCC8  CLI   BETAKEEP,C' '       TEST BETAKEEP= INPUT                         
         BE    IMVOCCX                                                          
         CLI   WHATDEST,C'B'                                                    
         BE    IMVOCCX                                                          
         MVI   WHATDEST,C'B'       SET WANT DEST=BETA OUTPUT CARDS(S)           
         XC    NSETS,NSETS                                                      
         LLC   R0,QJOBFMN          SET ONLY WANT ONE SET OF DEST=BETA           
         AHI   R0,-1                                                            
         BNP   *+8                                                              
         STC   R0,NSETS+1          INIT COUNT TO QJOBFMN-1                      
         B     IMVOCC1                                                          
IMVOCCX  DS    0H                                                               
*                                                                               
IMVOCE   CLI   QJOBFCH,C' '        TEST FICHE CLASS DEFINED                     
         BE    IMVOCEX                                                          
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ALASTO                                                        
         L     RF,=A(MVSFICHE)                                                  
         MVC   0(80,R3),0(RF)      //OU001 OUTPUT CLASS=S,DEST=FICHE            
         MVC   21(1,R3),QJOBFCH                                                 
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDL,OUTCRDN     SAVE NUM OF LAST IN THIS SET                 
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
IMVOCEX  DS    0H                                                               
*                                                                               
IMVOCF   CLI   QJOBARC,C' '        TEST ARCHIVE CLASS DEFINED                   
         BNH   IMVOCF1                                                          
         MVI   HALF,C'S'           DEFAULT CLASS=S                              
         CLI   QJOBARC,C'B'                                                     
         BNE   *+8                                                              
         MVI   HALF,C'B'           SWITCH TO CLASS=B IF QJOBARC=B               
         B     IMVOCF2                                                          
IMVOCF1  TM    ANYARCRC,X'02'      TEST ARC=CARD REQUIRED                       
         BZ    IMVOCFX                                                          
         MVI   HALF,C'S'           HARD CODE CLASS                              
         CLI   ARCCLASS,C' '                                                    
         BE    *+10                                                             
         MVC   HALF(1),ARCCLASS                                                 
IMVOCF2  TM    ANYARCRC,X'02'      TEST ARC=CARD REQUIRED                       
         BZ    IMVOCFX                                                          
         TM    ANYARCRC,X'40'      TEST IF ONLY ARCHIVE ELIGIBLE                
         BO    IMVOCFX                                                          
*NOP*    CLI   CLASS,C'Q'          NOT IF GOING DIRECT                          
*NOP*    BE    IMVOCFX                                                          
         L     R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         ST    R3,ALASTO                                                        
         L     RF,=A(MVSARC)                                                    
         MVC   0(80,R3),0(RF)      //OU001 OUTPUT CLASS=S,DEST=BARR             
         MVC   21(1,R3),HALF       SET CLASS                                    
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDL,OUTCRDN     SAVE NUM OF LAST IN THIS SET                 
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
         OI    ARCFLAG,X'08'       SET ARCHIVE SYSPRINT OUTPUT                  
IMVOCFX  DS    0H                                                               
*                                                                               
IMVOCJ   DS    0H                  EXTRA CARDS FOR ARCHIVE PDF OR TXT           
*&&UK*&& NOP   *+4                                                              
*&&US*&& B     IMVOCL                                                           
         TM    ANYARCRC,X'02'      TEST IF ARC=CARD                             
         BZ    IMVOCL                                                           
         TM    ANYARCRC,X'40'      TEST IF ONLY ELIGIBLE                        
         BO    IMVOCL                                                           
         MVC   ARCXDATA(ARCXDATL),SPACES                                        
         TM    ANYDIRRC,X'01'      TEST IF DIRECT=CARD                          
         BO    IMVOCJ4             YES                                          
*                                                                               
IMVOCJ1  CLI   PORFDEFS,0          TEST FORMDEF FOR PRINT OUTPUT                
*&&UK*&& BE    IMVOCL              SKIP WHOLE SET RATHER THAN CLASS=0           
*&&US*&& BE    IMVOCJ3                                                          
         CLC   QJOBFMD(5),=C'BASIC'                                             
*&&UK*&& BE    IMVOCL              SKIP WHOLE SET RATHER THAN CLASS=0           
*&&US*&& BE    IMVOCJ3                                                          
IMVOCJ2  MVI   ARCXCLA,C'I'        VALUES FOR AFP PRINT OUTPUT                  
         MVC   ARCXFRM(3),=C'PDF'                                               
         B     IMVOCK                                                           
IMVOCJ3  MVI   ARCXCLA,C'F'        VALUES FOR NON-AFP PRINT OUTPUT              
         MVI   ARCXCLA,C'0'        SET TO CLASS THAT DOESNT PRINT               
         MVC   ARCXFRM(3),=C'TXT'                                               
         MVC   ARCXFDE(6),=C'BASIC1'                                            
         MVC   ARCXPDE(6),=C'PLAIN1'                                            
         B     IMVOCK                                                           
*                                                                               
IMVOCJ4  CLI   PORFDEFS,0          TEST FORMDEF FOR DIRECT OUTPUT               
*&&UK*&& BE    IMVOCL              SKIP WHOLE SET RATHER THAN CLASS=0           
*&&US*&& BE    IMVOCJ6                                                          
         CLC   QJOBFMD(5),=C'BASIC'                                             
*&&UK*&& BE    IMVOCL              SKIP WHOLE SET RATHER THAN CLASS=0           
*&&US*&& BE    IMVOCJ6                                                          
IMVOCJ5  MVI   ARCXCLA,C'I'        VALUES FOR AFP AND DIRECT                    
         MVC   ARCXFRM(3),=C'PDF'                                               
         B     IMVOCK                                                           
IMVOCJ6  MVI   ARCXCLA,C'F'        VALUES FOR NON-AFP AND DIRECT                
         MVI   ARCXCLA,C'0'        SET TO CLASS THAT DOESNT PRINT               
         MVC   ARCXFRM(3),=C'TXT'                                               
         MVC   ARCXFDE(6),=C'BASIC1'                                            
         MVC   ARCXPDE(6),=C'PLAIN1'                                            
IMVOCJX  DS    0H                                                               
*                                                                               
IMVOCK   L     R3,ALASTO           COPY FIRST OUTPUT STATEMENT CARDS            
         LA    R3,80(R3)                                                        
         ST    R3,ANEXTO                                                        
         L     RE,AFRSTO           GET A(FIRST CARD IN SET)                     
         LH    RF,NFRSTO           GET NUMBER OF CARDS IN SET                   
IMVOCK2  MVC   0(80,R3),0(RE)                                                   
         ST    R3,ALASTO                                                        
         LA    R3,80(R3)                                                        
         LA    RE,80(RE)                                                        
         BCT   RF,IMVOCK2                                                       
         L     R3,ANEXTO                                                        
         AP    OUTCRDN,=P'1'                                                    
         ZAP   OUTCRDL,OUTCRDN     SAVE NUM OF LAST IN THIS SET                 
         UNPK  4(3,R3),OUTCRDN                                                  
         OI    6(R3),C'0'                                                       
*                                                                               
IMVOCK3  ICM   RF,15,AFRSTFOR      LOCATE FORMDEF=......                        
         BZ    IMVOCK4                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         CLI   ARCXFDE,C' '        LEAVE ALONE IF NO SPECIAL VALUE              
         BE    IMVOCK4                                                          
         MVC   8(6,RF),ARCXFDE                                                  
*                                                                               
IMVOCK4  ICM   RF,15,AFRSTPAG      LOCATE PAGEDEF=......                        
         BZ    IMVOCK5                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         CLI   ARCXPDE,C' '        LEAVE ALONE IF NO SPECIAL VALUE              
         BE    IMVOCK5                                                          
         MVC   8(6,RF),ARCXPDE                                                  
*                                                                               
IMVOCK5  ICM   RF,15,AFRSTCOP      LOCATE COPIES=NNN                            
         BZ    IMVOCK6                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         MVC   7(3,RF),=C'001'     SET TO ONE COPY                              
*                                                                               
IMVOCK6  ICM   RF,15,AFRSTDES      LOCATE DEST=XXXXXXXX                         
         BZ    IMVOCK7                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
*                                                                               
IMVOCK7  ICM   RF,15,AFRSTCLA      LOCATE CLASS=                                
         BZ    IMVOCK8                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         MVC   6(1,RF),ARCXCLA                                                  
         CLI   ARCXCLA,C'0'        SET COPIES=000 IF CLASS=0                    
         BNE   IMVOCK8                                                          
         ICM   RF,15,AFRSTCOP                                                   
         BZ    IMVOCK8                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         MVC   7(3,RF),=C'000'                                                  
*                                                                               
IMVOCK8  ICM   RF,15,AFRSTFRM      LOCATE FORMS=.....                           
         BZ    IMVOCK9                                                          
         LA    RE,6(RF)                                                         
         ICM   R1,15,ALASTFRM                                                   
         SR    R1,RE               R1=L'FORM NAME                               
         BNP   IMVOCK9                                                          
         S     RF,AFRSTO                                                        
         AR    RF,R3                                                            
         MVC   DUB,=C'11111111'    DUB=ARCXFRM PADDED WITH 1'S                  
         MVC   DUB(3),ARCXFRM                                                   
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   6(0,RF),DUB                                                      
*                                                                               
IMVOCK9  DS    0H                                                               
*                                                                               
IMVOCL   CLI   CLASS,C'Q'          IF GOING DIRECT                              
*&&UK*&& BNE   IMVOCS                                                           
*&&US*&& B     IMVOCS                                                           
         ICM   RF,15,AFRSTCOP      FIRST SET WAS CHANGED TO CLASS=0             
         BZ    IMVOCS                                                           
         MVC   7(3,RF),=C'000'     SO SET COPIES T0 0 ON FIRST SET              
*                                                                               
IMVOCS   L     R3,=A(JP1)          SYSPRINT-1 OUTPUT MODE                       
         L     RF,=A(MVSSYP)                                                    
         MVC   0(15,R3),0(RF)      //SYSPRINT  DD                               
         LA    R2,15(R3)                                                        
         MVC   0(10,R2),=C'SYSOUT=(,)'                                          
         LA    R2,10(R2)                                                        
*                                                                               
IMVOCS1  CLI   CLASS,C'Q'          IS OUTPUT CLASS Q                            
         BNE   IMVOCS1A                                                         
         CLI   QJOBDISP,C'T'       AND DISPOSITION NOT TAPE                     
         BE    IMVOCS1A                                                         
*&&UK*&& B     IMVOCS1N            UK SET HOLD=N                                
*&&US*&& B     IMVOCS1Y            US SET HOLD=Y                                
IMVOCS1A CLI   QJOBDISP,C'H'       TEST OUTPUT DISP HOLD                        
         BNE   IMVOCS2                                                          
IMVOCS1Y MVC   0(7,R2),=C',HOLD=Y'                                              
         B     *+10                                                             
IMVOCS1N MVC   0(7,R2),=C',HOLD=N'                                              
         LA    R2,7(R2)                                                         
*                                                                               
IMVOCS2  BAS   RE,FREEZE           TEST IF OK TO OUTPUT FREE=CLOSE              
         BNE   IMVOCS2A            NO                                           
         MVC   0(11,R2),=C',FREE=CLOSE'                                         
         LA    R2,11(R2)                                                        
IMVOCS2A DS    0H                                                               
*                                                                               
IMVOCS3  MVC   0(9,R2),=C',OUTPUT=('                                            
         LA    R2,9(R2)                                                         
         ZAP   HALF,OUTCRDF        SET HALF TO FIRST CARD NUM                   
*                                                                               
IMVOCS4  CP    HALF,OUTCRDL        TEST IF DONE LAST ONE                        
         BNH   *+14                                                             
         BCTR  R2,0                                                             
         MVI   0(R2),C')'          TERMINATE OUTPUT= PARAM                      
         B     IMVOCX              HOO FUCKING RAH                              
         LA    RF,71(R3)                                                        
         SR    RF,R2                                                            
         CHI   RF,8                TEST IF ENOUGH ROOM                          
         BL    IMVOCS6                                                          
IMVOCS5  MVC   0(8,R2),=C'*.OU000,'                                             
         UNPK  4(3,R2),HALF                                                     
         OI    6(R2),C'0'                                                       
         AP    HALF,=P'1'                                                       
         LA    R2,8(R2)                                                         
         B     IMVOCS4                                                          
*                                                                               
IMVOCS6  LA    R3,80(R3)           CREATE CONTINUATION CARD                     
         MVC   0(2,R3),=C'//'                                                   
         LA    R2,15(R3)                                                        
         B     IMVOCS5                                                          
*                                                                               
IMVOCX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*SUBSIDIARY SCAN TABLE FOR SOFT JCL STATEMENTS                        *         
***********************************************************************         
SOFTABLE DC    A(SOFTABNQ)         NUMBER OF ENTRIES IN TABLE                   
*                                                                               
         DC    AL1(7),AL1(2),CL15'&&AGENCY'                                     
SOFTAGY  DC    CL15' '                                                          
*                                                                               
         DC    AL1(6),AL1(1),CL15'&&MEDIA'                                      
SOFTMED  DC    CL15' '                                                          
*                                                                               
         DC    AL1(8),AL1(1),CL15'&&COMPANY'                                    
SOFTCOMP DC    CL15' '                                                          
*                                                                               
         DC    AL1(7),AL1(1),CL15'&&LEDGER'                                     
SOFTLEDG DC    CL15' '                                                          
*                                                                               
         DC    AL1(4),AL1(2),CL15'&&REP'                                        
SOFTREP  DC    CL15' '                                                          
*&&UK                                                                           
         DC    AL1(6),AL1(8),CL15'&&IPL-1'                                      
SOFTIPLM DC    CL15' '                                                          
*                                                                               
         DC    AL1(6),AL1(8),CL15'&&IPL+1'                                      
SOFTIPLP DC    CL15' '                                                          
*&&                                                                             
         DC    AL1(4),AL1(8),CL15'&&IPL'                                        
SOFTIPL  DC    CL15' '                                                          
*                                                                               
         DC    AL1(8),AL1(2),CL15'&&PROGRAM'                                    
SOFTPROG DC    CL15' '                                                          
*                                                                               
         DC    AL1(7),AL1(2),CL15'&&SYSTEM'                                     
SOFTSYS  DC    CL15' '                                                          
*                                                                               
         DC    AL1(6),AL1(7),CL15'&&LOGO1'                                      
SOFTLOG1 DC    CL15' '                                                          
*                                                                               
         DC    AL1(6),AL1(7),CL15'&&LOGO2'                                      
SOFTLOG2 DC    CL15' '                                                          
*                                                                               
         DC    AL1(7),AL1(5),CL15'&&ORIGIN'                                     
SOFTORIG DC    CL15' '                                                          
*                                                                               
         DC    AL1(5),AL1(5),CL15'&&DEST'                                       
SOFTDEST DC    CL15' '                                                          
*                                                                               
         DC    AL1(4),AL1(2),CL15'&&DAY'                                        
SOFTDAY  DC    CL15' '                                                          
*                                                                               
         DC    AL1(8),AL1(2),CL15'&&HEXCOMP'                                    
SOFTHEX  DC    CL15' '                                                          
*                                                                               
SOFTABNQ EQU   (*-SOFTABLE)/32                                                  
*&&US                                                                           
SPOTNET  DS    0CL5                                                             
       ++INCLUDE DMSPTNET          SPOT SYSTEMS THAT ARE ALSO NET               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*TAPE AND WORKER FILES DTFS/DCBS                                      *         
***********************************************************************         
EODREQS  DMWK  BUFFER=WKRQBUF                                                   
*                                                                               
EODJOBS  DMWK  BUFFER=WKRJBUF                                                   
*                                                                               
JCLTAPE  DCB   DDNAME=JCLTAPE,DSORG=PS,RECFM=FB,                       X        
               LRECL=80,BLKSIZE=80,MACRF=PM                                     
*                                                                               
WJCL     DCB   DDNAME=WJCL,DSORG=PS,RECFM=FB,                          X        
               LRECL=80,BLKSIZE=00,MACRF=PM                                     
*                                                                               
WLOG     DCB   DDNAME=WLOG,DSORG=PS,RECFM=FB,                          X        
               LRECL=80,BLKSIZE=00,MACRF=PM                                     
         EJECT                                                                  
***********************************************************************         
*TABLE OF VALID CONTROL PARAMETER CARDS                               *         
*+00     CL16  VALUE OF PARAMETER ID                                  *         
*+16     XL1   LENGTH OF PARAMETER ID                                 *         
*+17     XL1   MVI CHR OR MVC LENGTH                                  *         
*+18     XL1   01=MVI,02=MVC,04=ROUTINE                               *         
*+19     CL1   N/D                                                    *         
*+20     AL4   A(FIELD) OR A(ROUTINE)                                 *         
***********************************************************************         
         DS    0F                                                               
PARMTBL  DS    0CL24                                                            
         DC    CL16'MVS             ',X'03D601',C' ',A(TYPJCL)                  
         DC    CL16'FREE=Y          ',X'06E801',C' ',A(FREE)                    
         DC    CL16'FREE=N          ',X'06D501',C' ',A(FREE)                    
         DC    CL16'SYSPRINT=OUTPUT ',X'0FD601',C' ',A(SYSPOUT)                 
         DC    CL16'SYSPRINT=DUMMY  ',X'0EC401',C' ',A(SYSPOUT)                 
         DC    CL16'LOGO=N          ',X'06D501',C' ',A(ANYLOGO)                 
         DC    CL16'TRACE=Y         ',X'07E801',C' ',A(TRACE)                   
         DC    CL16'TRACE=N         ',X'07D501',C' ',A(TRACE)                   
         DC    CL16'REPSPACEND=Y    ',X'0CE801',C' ',A(REPSPACE)                
         DC    CL16'REPSPACEND=N    ',X'0CD501',C' ',A(REPSPACE)                
         DC    CL16'RFHTRACE=Y      ',X'0AE801',C' ',A(RFHTRACE)                
         DC    CL16'RFHTRACE=N      ',X'0AD501',C' ',A(RFHTRACE)                
         DC    CL16'SINGLE          ',X'060101',C' ',A(SINGLE)                  
         DC    CL16'ACC=EMU         ',X'07C501',C' ',A(ACCEMU)                  
         DC    CL16'NETOH=Y         ',X'07E801',C' ',A(NETOH)                   
         DC    CL16'DIROUTPUT=Y     ',X'0BE801',C' ',A(DIROUT)                  
         DC    CL16'DIROUTPUT=N     ',X'0BD501',C' ',A(DIROUT)                  
         DC    CL16'ARCOUTPUT=Y     ',X'0BE801',C' ',A(ARCOUT)                  
         DC    CL16'ARCOUTPUT=N     ',X'0BD501',C' ',A(ARCOUT)                  
*                                                                               
         DC    CL16'AFP=            ',X'040802',C' ',A(AFP)                     
         DC    CL16'AFPKEEP=        ',X'080102',C' ',A(AFPKEEP)                 
         DC    CL16'AFTER=          ',X'060A02',C' ',A(AFTJCL)                  
         DC    CL16'BEFORE=         ',X'070A02',C' ',A(BEFJCL)                  
         DC    CL16'CLASS=          ',X'060102',C' ',A(CLASS)                   
         DC    CL16'DISP=           ',X'050102',C' ',A(DISP)                    
         DC    CL16'DISPOSITION=    ',X'0C0102',C' ',A(DISP)                    
         DC    CL16'END JOB=        ',X'080802',C' ',A(JOBEND)                  
         DC    CL16'IGNORE=         ',X'070802',C' ',A(IGNORE)                  
         DC    CL16'JCL=            ',X'040102',C' ',A(JCLOPT)                  
         DC    CL16'JSEP=           ',X'050102',C' ',A(JSEPOPT)                 
         DC    CL16'MSGCLASS=       ',X'090102',C' ',A(MSGCLA)                  
         DC    CL16'RJE=            ',X'040302',C' ',A(RJEOPT)                  
         DC    CL16'SPOOL=          ',X'060102',C' ',A(SPOOL)                   
         DC    CL16'START JOB=      ',X'0A0802',C' ',A(JOBSTART)                
         DC    CL16'TYPRUN=         ',X'070802',C' ',A(TYPRUN)                  
         DC    CL16'NETID=          ',X'060802',C' ',A(NETID)                   
         DC    CL16'NETRL=          ',X'061302',C' ',A(NETRL)                   
         DC    CL16'JCLDISK=        ',X'080802',C' ',A(WJCLID)                  
         DC    CL16'LOGDISK=        ',X'080802',C' ',A(WLOGID)                  
         DC    CL16'FIRST NAME=     ',X'0B0802',C' ',A(FRSTNAME)                
         DC    CL16'LOGOADR=        ',X'080102',C' ',A(LOGOADR)                 
         DC    CL16'BETAKEEP=       ',X'090102',C' ',A(BETAKEEP)                
         DC    CL16'BETAONLY=       ',X'090102',C' ',A(BETAONLY)                
         DC    CL16'BETAOPTN=       ',X'090102',C' ',A(BETAOPTN)                
         DC    CL16'DIRCLASS=       ',X'090102',C' ',A(DIRCLASS)                
         DC    CL16'ARCCLASS=       ',X'090102',C' ',A(ARCCLASS)                
         DC    CL16'ARCTYPE=        ',X'080102',C' ',A(ARCTYPE)                 
         DC    CL16'BURSTPQ=        ',X'080102',C' ',A(BURSTPQ)                 
         DC    CL16'FLASH=          ',X'060102',C' ',A(FLASHCPY)                
*                                                                               
         DC    CL16'$$              ',X'020004',C' ',A(CONJCL)                  
         DC    CL16'##              ',X'020004',C' ',A(CONDD)                   
         DC    CL16'DATE=           ',X'050004',C' ',A(CONDAT)                  
         DC    CL16'NPDATE=         ',X'070004',C' ',A(CONNPD)                  
         DC    CL16'START REQUEST=  ',X'0E0004',C' ',A(CONSRE)                  
         DC    CL16'END REQUEST=    ',X'0C0004',C' ',A(CONERE)                  
         DC    CL16'EXCLUDE         ',X'080004',C' ',A(CONSEL)                  
         DC    CL16'INCLUDE         ',X'080004',C' ',A(CONSEL)                  
         DC    CL16'LOAD=           ',X'050004',C' ',A(CONLOA)                  
         DC    CL16'MSGLEVEL=       ',X'090004',C' ',A(CONMLV)                  
         DC    CL16'SCAN=           ',X'050004',C' ',A(CONSCN)                  
         DC    CL16'SYSTEM=         ',X'070004',C' ',A(CONSYS)                  
         DC    CL16'NETHC=          ',X'060004',C' ',A(CONNHC)                  
         DC    CL16'MAXSTEPS=       ',X'090004',C' ',A(CONSTP)                  
         DC    CL16'MAXTAPES=       ',X'090004',C' ',A(CONTAP)                  
         DC    CL16'MAXREQS=        ',X'080004',C' ',A(CONREQ)                  
         DC    CL16'CDPAGES=        ',X'080004',C' ',A(CONCDP)                  
         DC    CL16'TAPE=           ',X'050004',C' ',A(CONTPU)                  
         DC    CL16'SPONSOR=        ',X'080004',C' ',A(CONSPO)                  
*                                                                               
PARMTBLX DC    CL16'                ',X'000000',C' ',A(0)                       
         EJECT                                                                  
ACCXLST  DC    0CL2' '             ACC JOBS EXCEPTIONS FROM IDI ORID            
         DC    C'A1'                                                            
         DC    C'A2'                                                            
         DC    C'B2'                                                            
         DC    C'B3'                                                            
         DC    C'C2'                                                            
         DC    C'C6'                                                            
         DC    C'FA'                                                            
         DC    C'M2'                                                            
         DC    C'PA'                                                            
         DC    C'PC'                                                            
         DC    C'T4'                                                            
         DC    C'T5'                                                            
         DC    C'21'                                                            
         DC    C'23'                                                            
         DC    C'25'                                                            
         DC    C'55'                                                            
         DC    C'89'                                                            
         DC    C'9A'                                                            
         DC    C'90'                                                            
         DC    C'91'                                                            
         DC    C'92'                                                            
         DC    C'93'                                                            
         DC    C'94'                                                            
         DC    C'95'                                                            
         DC    C'96'                                                            
         DC    C'98'                                                            
         DC    X'FF'                                                            
         EJECT                                                                  
SYSLST   DC    256CL5' '           ROOM FOR 256 SYSTEMS (SEE SYSMAX)            
*                                                                               
SYSTAB   DC    0CL5' '             SYSTEM/SUBSYS(2)/CLASS/DISPOSITION           
*&&US                                                                           
         DC    C'S  AD'            SPOT                                         
         DC    C'P  AD'            PRINT                                        
         DC    C'R  BD'            REP                                          
         DC    C'A  AD'            ACCOUNT                                      
         DC    C'C  AD'            CONTROL                                      
         DC    C'D  AD'            COST PER POINT                               
         DC    C'E  AD'            PERSON                                       
         DC    C'L  AD'            PLANNING                                     
         DC    C'B  AD'            MEDIABASE                                    
         DC    C'T  AD'            TALENT                                       
         DC    C'F  AD'            SPOT TRAFFIC                                 
*&&                                                                             
*&&UK                                                                           
         DC    C'M  AD'            MEDIA                                        
         DC    C'A  AD'            ACCOUNT                                      
         DC    C'C  AD'            CONTROL                                      
         DC    C'E  AD'            PERSON                                       
         DC    C'L  AD'            PLANNING                                     
         DC    C'B  AD'            MEDIABASE                                    
         DC    C'F  AD'            ARTIST FEES                                  
*&&                                                                             
         DC    C'   AD'            END OF TABLE                                 
         EJECT                                                                  
***********************************************************************         
*GENERATED MVS JCL STATEMENTS                                         *         
***********************************************************************         
JJ1      DC    CL80' '             MVS //XXXXXXXX JOB CARDS                     
JJ2      DC    CL80' '                                                          
JJ3      DC    CL80' '                                                          
JJX      DC    CL80' '                                                          
*                                                                               
JES1     DC    CL80' '             MVS //*JES CARDS                             
JES2     DC    CL80' '                                                          
JES3     DC    CL80' '                                                          
JESX     DC    CL80' '                                                          
*                                                                               
AFP1     DC    CL80' '             MVS //OUTPUT CARDS                           
AFP2     DC    CL80' '                                                          
AFP3     DC    CL80' '                                                          
         DC    400CL80' '                                                       
*                                                                               
JP1      DC    CL80' '             MVS //SYSPRINT  DD CARDS                     
JP2      DC    CL80' '                                                          
JP3      DC    CL80' '                                                          
         DC    20CL80' '                                                        
JPX      DC    CL80' '                                                          
*                                                                               
         DC    80X'FF'                                                          
*                                                                               
JCLIN    DC    80CL80' '           JCL CARDS INPUT AS $$.........               
         DC    80X'FF'                                                          
*                                                                               
DDIN     DC    100CL80' '          DD CARDS INPUT AS ##.........                
         DC    80X'FF'                                                          
*                                                                               
LDIRECT  DC    CL80' '             LAST DIRECT=... CARD                         
LORIGIN  DC    CL80' '             LAST ORIGIN=... CARD                         
LLOGO    DC    CL80' '             LAST LOGO=..... CARD                         
LARC     DC    CL80' '             LAST ARC=...... CARD                         
LDDSIO   DC    CL80' '             LAST DDSIO=.... CARD                         
LDSPACE  DC    CL80' '             LAST DSPACE=... CARD                         
*                                                                               
*                   0000000000111111111122222222223333333333444444              
*                   0123456789012345678901234567890123456789012345              
*                                                                               
MVSJMS   DC    CL80'//             MSGLEVEL=(1,1),MSGCLASS=A        '           
MVSSYI   DC    CL80'//SYSIN     DD *                                '           
MVSSYP   DC    CL80'//SYSPRINT  DD SYSOUT=*                         '           
MVSSYPT1 DC    CL80'//SYSPRINT  DD UNIT=TAPE,DISP=(MOD,CATLG,CATLG),'           
MVSSYPT2 DC    CL80'//             DSN=XXXTAPE.                     '           
MVSEOJ   DC    CL80'//'                                                         
MVSSPOOL DC    CL80'SPOOL=XXXTAPE.SS0PPAA'                                      
*                                                                               
MVSOUT   DC    CL80'//OU001 OUTPUT CLASS=X'                                     
MVSFICHE DC    CL80'//OU001 OUTPUT CLASS=S,FORMS=FICHE,COPIES=001,DEST=X        
               FICHE'                                                           
MVSARC   DC    CL80'//OU001 OUTPUT CLASS=S,FORMS=BARR,COPIES=001,DEST=BX        
               ARR'                                                             
*                                                                               
JESKEEP1 DC CL80'//*FORMAT   PR,DDNAME=SYSPRINT,DEST=ANYLOCAL'                  
JESKEEP2 DC CL80'//*FORMAT   PR,DDNAME=SYSPRINT,DEST=KEEP'                      
JESFICHE DC CL80'//*FORMAT   PR,DDNAME=SYSPRINT,DEST=FICHE,FORMS=FICHE'         
JESARC   DC CL80'//*FORMAT   PR,DDNAME=SYSPRINT,DEST=BARR,FORMS=BARR'           
JESNET   DC CL80'//*NET         ID=XXXXXXXX,HC=01,RL=XXXXXXXX'                  
*                                                                               
RFHDR    DC    CL80'RFHDR='                                                     
         EJECT                                                                  
LOGMSG1  DC    CL80'INPUT CONTROL STATEMENTS - END WITH /*  '                   
LOGMSG2  DC    CL80'ABOVE STATEMENT INVALID - REENTER       '                   
LOGMSG3  DC    CL80'SYSTEM=SS STATEMENT MISSING             '                   
LOGMSG4  DC    CL80'SELECTION POOL FILLED                   '                   
LOGMSG5  DC    CL80'INVALID $$ JOB DATA FOR SINGLE JOB      '                   
*                                                                               
HDRMSG1  DC    CL40'REQUEST WRAP                            '                   
HDRMSG2  DC    CL40'CONTROL STATEMENTS                      '                   
HDRMSG3  DC    CL40'JCL STATEMENTS AND REQUESTS             '                   
HDRMSGU  DC    CL40'----------------------------------------'                   
*                                                                               
PGMEQTAB DS    0CL10               PROGRAM/PROGRAM EQUATE                       
         DC    C'RE011RE010'                                                    
         DC    C'RE086RE085'                                                    
         DC    C'PP013PP012'                                                    
         DC    C'PP021PP020'                                                    
         DC    C'PP053PP052'                                                    
         DC    C'PP072PP071'                                                    
         DC    C'PP073PP071'                                                    
         DC    C'PP075PP071'                                                    
         DC    X'FF'                                                            
*                                                                               
SYSEQTAB DS    0CL8                QSYSTEM/2BYTE NAME/3BYTE NAME/INDICS         
*&&US*&& DC    C'SSPSPT',AL1(0,0)                                               
*&&US*&& DC    C'PPPPRT',AL1(0,0)                                               
*&&US*&& DC    C'RREREP',AL1(0,0)                                               
*&&US*&& DC    C'TTATAL',AL1(0,0)                                               
*&&UK*&& DC    C'MMEMED',AL1(0,0)                                               
*&&UK*&& DC    C'FFEFEE',AL1(0,0)                                               
         DC    C'AACACC',AL1(0,0)                                               
         DC    C'BMBMBA',AL1(0,0)                                               
         DC    C'CCTCON',AL1(0,0)                                               
         DC    C'EPEPER',AL1(0,0)                                               
         DC    C'LMPMPL',AL1(0,0)                                               
         DC    X'FF'                                                            
*                                                                               
REQIOH   DC    CL80' '             SET OF CARDS THAT COMPRISE A REQUEST         
REQIO1   DC    CL80' '                                                          
REQIO2   DC    CL80' '                                                          
REQIO3   DC    CL80' '                                                          
REQIO4   DC    CL80' '                                                          
REQIO5   DC    CL80' '                                                          
REQIO6   DC    CL80' '                                                          
REQIO7   DC    CL80' '                                                          
REQIO8   DC    CL80' '                                                          
REQIO9   DC    CL80' '                                                          
REQIOA   DC    CL80' '                                                          
REQIOB   DC    CL80' '                                                          
REQIOC   DC    CL80' '                                                          
REQIOD   DC    CL80' '                                                          
REQIOE   DC    CL80' '                                                          
REQIOF   DC    CL80' '                                                          
*                                                                               
LOGOTAB  DS    0CL2                AGENCY ALPHAS THAT WANT LOGOS ON PQ          
         DC    X'FFFF'                                                          
         EJECT                                                                  
REQLEN   DC    H'0'                                                             
         DC    H'0'                                                             
REQIO    DS    2000C                                                            
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'SSB     '                                                    
SSB      DC    X'0000FF',X'02',4X'00',CL8' ',240X'00'                           
         DC    CL8'UTL     '                                                    
UTL      DC    F'0',X'0A',251X'00'                                              
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'I/O AREA'                                                    
IO       DS    2000C                                                            
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'JOBIO   '                                                    
JOBIO    DS    2000C               ENOUGH FOR HEADER AND 15 REQ CARDS           
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'SELPOOL '                                                    
SELPOOL  DC    F'500'              MAX NUMBER OF ENTRIES                        
         DC    AL4(SELLNQ)         WIDTH OF ENTRY                               
         DS    (500*SELLNQ)C                                                    
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'BOOKBUFF'                                                    
BOOKBUFF DS    1112C                                                            
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'SPLPOOL*'                                                    
SPLPOOL  DC    F'200'                                                           
         DC    F'0'                                                             
         DS    3200C                                                            
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'SUBTABLE'                                                    
SUBTABLE DC    F'0'                                                             
SUBTABL  EQU   66                  L'A L'B FIELDA(32) FIELD(32)                 
SUBTABN  EQU   100                                                              
         DC    (SUBTABN)CL(SUBTABL)' '                                          
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'WKRQBUF '                                                    
WKRQBUF  DS    60000C                                                           
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'WKRJBUF '                                                    
WKRJBUF  DS    60000C                                                           
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'REQWRK  '                                                    
REQWRK   DS    20000C                                                           
*                                                                               
         DC    0D'0'                                                            
         DC    CL8'LOADAREA'                                                    
LOADAREA BR    RE                                                               
         DS    4000C                                                            
*                                                                               
         DC    0D'00'                                                           
         DC    CL8'ACBUF   '                                                    
ACBUF    DC    X'FF'                                                            
         ORG   *-1                                                              
         DS    XL(MXACB*ACBLNQ)                                                 
MXACB    EQU   300                                                              
*                                                                               
REQWRAPX DS    0C                                                               
         EJECT                                                                  
***********************************************************************         
*DSECT TO COVER AN ITEM IN THE SELECTION POOL                         *         
***********************************************************************         
SELD     DSECT                                                                  
SELIND   DS    CL1                                                              
SELDEST  DS    CL4                                                              
SELJOB   DS    CL8                                                              
SELOUT   DS    CL6                                                              
SELCLASS DS    CL1                                                              
SELORIG  DS    CL6                                                              
SELUSER  DS    CL2                 AGENCY ALPHA                                 
SELDISP  DS    CL1                                                              
SELSPEC  DS    CL1                                                              
SELPROG  DS    CL2                                                              
SELPTYP  DS    CL1                                                              
SELPOUT  DS    CL1                                                              
SELCOMSC DS    CL1                                                              
SELLNQ   EQU   *-SELD                                                           
         EJECT                                                                  
***********************************************************************         
*DSECT TO COVER AN ENTRY IN THE ACC COMPANY BUFFER                    *         
***********************************************************************         
ACBD     DSECT                                                                  
ACBSE    DS    XL1                 ACC SE NUMBER                                
ACBCPY   DS    XL1                 COMPANY CODE                                 
ACBSTA   DS    XL1                 STATUS BYTE A                                
ACBLNQ   EQU   *-ACBD                                                           
         EJECT                                                                  
***********************************************************************         
*INCLUDED SYSTEM DSECTS                                               *         
***********************************************************************         
*EDREQD                                                                         
       ++INCLUDE EDREQD                                                         
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
*DMDDNAMED                                                                      
DDNAD    DSECT                                                                  
       ++INCLUDE DMDDNAMED                                                      
         EJECT                                                                  
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
         EJECT                                                                  
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
         EJECT                                                                  
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         EJECT                                                                  
*ACGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007EDREQWRAP 03/21/17'                                      
         END                                                                    
