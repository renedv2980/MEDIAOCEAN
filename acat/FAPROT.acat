*          DATA SET FAPROT     AT LEVEL 001 AS OF 08/01/14                      
         TITLE 'FAPROT - FACPAK MEMORY PROTECTION ROUTINES'                     
*CATALP FAPROT                                                                  
FAPROT   CSECT                                                                  
         PRINT NOGEN                                                            
         REQUS                                                                  
         ENTRY PROTON                                                           
         ENTRY PROTOFF                                                          
                                                                                
***********************************************************************         
* SET PROTECTION ON - CODE FROM MCPROT MACRO - ONLY USES RE AND RF    *         
***********************************************************************         
PROTON   STM   RE,RF,12(RD)                                                     
         LRL   RE,VSSB             RE=A(SSB)                                    
         USING SSBD,RE                                                          
         LTR   RE,RE               EXIT IF NO SSB                               
         JZ    PROTXIT                                                          
         ICM   RF,3,SSBCNTL        EXIT IF OFFLINE                              
         JZ    PROTXIT                                                          
         TM    SSBPROT,SSBPONQ     TEST STORAGE PROTECTION ENABLED              
         JZ    PROTXIT                                                          
*                                                                               
         ICM   RE,15,SSBTKADR      RE=A(TASK) FROM SSB                          
         JZ    PROTXIT                                                          
         USING TCBD,RE                                                          
         TM    TCBPROT,TCBPKAPQ    TEST APPLIC SPKEY SET                        
         JZ    PROTXIT                                                          
         TM    TCBPROT,TCBPKFPQ    TEST FACPAK SPKEY OVERRIDE SET               
         JZ    PROTXIT                                                          
         L     RF,4(RD)                                                         
         TM    4(RF),X'80'         TEST IN KEY 8 PROTECTION MODE                
         JZ    PROTXIT                                                          
         NI    4(RF),X'FF'-X'80'                                                
         NI    TCBPROT,X'FF'-TCBPKFPQ                                           
*                                                                               
         ICM   RE,15,TCBMONWK      TEST INFO SET IN MONITOR WORKD               
         JZ    PROTON1                                                          
         OC    0(4,RE),0(RE)       TEST IF ANY DATA SET BY PROTOFF              
         JZ    PROTON1                                                          
         XC    0(16,RE),0(RE)      CLEAR WHAT WAS SET BY PROTOFF                
*                                                                               
PROTON1  SPKA  144                 SWITCH TO KEY 9                              
         J     PROTXIT                                                          
                                                                                
***********************************************************************         
* SET PROTECTION OFF - CODE FROM MCPROT MACRO - ONLY USES RE AND RF   *         
***********************************************************************         
PROTOFF  STM   RE,RF,12(RD)                                                     
         LRL   RE,VSSB             RE=A(SSB)                                    
         USING SSBD,RE                                                          
         LTR   RE,RE               EXIT IF NO SSB                               
         JZ    PROTXIT                                                          
         ICM   RF,3,SSBCNTL        EXIT IF OFFLINE                              
         JZ    PROTXIT                                                          
         TM    SSBPROT,SSBPONQ     TEST STORAGE PROTECTION ENABLED              
         JZ    PROTXIT                                                          
*                                                                               
         ICM   RE,15,SSBTKADR      RE=A(TASK) FROM SSB                          
         JZ    PROTXIT                                                          
         USING TCBD,RE                                                          
         TM    TCBPROT,TCBPKAPQ    TEST APPLIC SPKEY SET                        
         JZ    PROTXIT                                                          
         L     RF,4(RD)            TOP BIT IN BACK POINTER HAS STATUS           
         NI    4(RF),X'FF'-X'80'                                                
         TM    TCBPROT,TCBPKFPQ    TEST FACPAK SPKEY OVERRIDE SET               
         JO    PROTXIT                                                          
*                                                                               
         SPKA  128                 SWITCH TO KEY 8                              
         OI    4(RF),X'80'         SET IN KEY 8 STORAGE MODE                    
         OI    TCBPROT,TCBPKFPQ    SET FACPAK SPKEY OVERRIDE                    
*                                                                               
         ICM   RE,15,TCBMONWK      TEST IF SAVE STORAGE AVAILABLE               
         JZ    PROTXIT                                                          
         OC    0(4,RE),0(RE)       IF ALREADY SET DON'T OVERRIDE                
         JNZ   PROTXIT                                                          
         ST    RB,0(RE)            SAVE CALLER'S BASE REGISTER                  
         MVC   4(4,RE),12(RD)      SAVE CALLER'S RETURN ADDRESS                 
         LRL   RF,VSSB                                                          
         C     RB,SSBLOADR-SSBD(RF)                                             
         JL    PROTXIT                                                          
         C     RB,SSBHIADR-SSBD(RF)                                             
         JH    PROTXIT                                                          
         MVC   8(8,RE),22(RB)      SAVE CALLER'S PROGRAM LABEL                  
*                                                                               
PROTXIT  LM    RE,RF,12(RD)                                                     
         BR    RE                                                               
*                                                                               
VSSB     DC    V(SSB)                                                           
                                                                                
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAPROT    08/01/14'                                      
         END                                                                    
