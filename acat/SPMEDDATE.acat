*          DATA SET SPMEDDATE  AT LEVEL 051 AS OF 03/05/18                      
*CATALP MEDDATE                                                                 
         TITLE 'BUILDS LIST OF DATES AT FRONT OF BLOCK'                         
VMDDATE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DATEDLQ,**DATE**                                                 
         USING DATED,RC                                                         
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         MVC   SAVQDATE,QSTART                                                  
         L     R8,MEDBUFF                                                       
         USING MEDBLOCK,R8                                                      
         MVC   MEDDFORM,SPOTPROF+2                                              
         TM    MEDDFORM,X'80'     IF CHARACTER INSTEAD OF BINARY                
         BZ    *+8                                                              
         NI    MEDDFORM,X'0F'      STRIP ZONE                                   
         LA    RE,MEDDATES                                                      
         LA    RF,75+14+6+4                                                     
         ST    RF,MEDNUMDE                                                      
         MH    RF,=H'12'                                                        
         XCEF                                                                   
         EJECT                                                                  
*              RESET CALENDAR MONTHS IF WEEKS REQUESTED                         
         XC    MYFLAG,MYFLAG       CLEAR MY FLAG                                
         MVI   MEDNOLNK,C'N'       USE LINKS FOR ADDING                         
         CLI   MEDDFORM,2          CALENDAR DATES REQUESTED                     
         BNE   CDX                                                              
         CLI   ALLOWCWM,C'Y'       ALLOW CALENDAR WEEKS AND MONTHS              
         BNE   CD1                                                              
         MVI   MEDNOLNK,C'Y'       SET TO BYPASS ADD LINKS                      
         B     CDX                                                              
         SPACE 2                                                                
CD1      DS    0H                                                               
         OC    MEDNUMWK,MEDNUMWK   WEEKS REQUIRED                               
         BZ    CDX                  NO                                          
         OC    MEDNUMMO,MEDNUMMO   MONTHS REQUIRED                              
         BZ    CDX                  NO                                          
         MVI   MEDDFORM,0          SET TO BROADCAST WEEK DATES                  
CDX      DS    0H                                                               
         EJECT                                                                  
*              ROUTINE TO FORCE START AND END TO BROADCAST WEEKS                
         SPACE 3                                                                
         CLI   MEDDFORM,2          DONT FORCE FOR CALENDAR MONTHS               
         BE    MD4                                                              
         CLC   RCPROG,=C'D6'       OR FOR DAILY TIMESHEETS                      
         BE    MD4                                                              
         CLI   MEDDAILY,C'Y'       OR FOR ANY DAILY REPORT                      
         BE    MD4                                                              
         CLI   MEDDFORM,19          OR FOR SPECIAL P&G OPTION                   
         BNE   *+24                                                             
         CLC   =X'F0F7F0F1',QSTART+2  IF THE DATE WAS FUDGED                    
         BE    MD2                    LEAVE IT ALONE                            
         CLC   =X'F0F4F0F2',QSTART+2  IF THE DATE WAS FUDGED                    
         BE    MD2                    LEAVE IT ALONE                            
*                                                                               
         GOTO1 GETDAY,DMCB,QSTART,DUB                                           
         SR    R6,R6                                                            
         IC    R6,0(R1)            (DAY NUMBER)                                 
         LA    R7,1                ASSUME MONDAY                                
         L     RF,=V(SYSPROFS)     UNLES A SPECIAL DAY OF WEEK                  
         LTR   RF,RF               IS FOUND IN THE PROFILE                      
         BZ    MD1                                                              
         CLI   8(RF),0                                                          
         BE    MD1                                                              
         ZIC   R7,8(RF)            FOUND ONE - SO USE THAT                      
         SPACE 2                                                                
MD1      CR    R6,R7                                                            
         BE    MD2                                                              
         BH    *+8                                                              
         LA    R6,7(R6)                                                         
         SR    R7,R6                                                            
         MVC   DUB,QSTART                                                       
         GOTO1 ADDAY,DMCB,DUB,QSTART,(R7)                                       
         SPACE 2                                                                
MD2      CLI   MEDDFORM,19          OR FOR SPECIAL P&G OPTION                   
         BNE   *+24                                                             
         CLC   =X'F0F6F3F0',QEND+2  IF THE DATE WAS FUDGED                      
         BE    MD4                    LEAVE IT ALONE                            
         CLC   =X'F0F4F0F1',QEND+2  IF THE DATE WAS FUDGED                      
         BE    MD4                    LEAVE IT ALONE                            
*                                                                               
         GOTO1 GETDAY,DMCB,QEND,DUB                                             
         SR    R6,R6                                                            
         IC    R6,0(R1)                                                         
         LA    R7,7                ASSUME ENDING ON SUNDAY                      
         L     RF,=V(SYSPROFS)                                                  
         LTR   RF,RF                                                            
         BZ    MD3                                                              
         CLI   8(RF),0                                                          
         BE    MD3                                                              
         ZIC   R7,8(RF)            START DAY FOUND IN PROFILE                   
         BCT   R7,MD3              END IS 1 LESS THAN THIS                      
         LA    R7,7                                                             
         SPACE 2                                                                
MD3      CR    R6,R7                                                            
         BE    MD4                                                              
         BL    *+8                                                              
         LA    R7,7(R7)                                                         
         SR    R7,R6                                                            
         MVC   DUB,QEND                                                         
         GOTO1 ADDAY,DMCB,DUB,QEND,(R7)                                         
         SPACE 2                                                                
MD4      GOTO1 DATCON,DMCB,(0,QSTART),(2,BQSTARTP)                              
         GOTO1 DATCON,DMCB,(0,QEND),(2,BQENDP)                                  
         GOTO1 DATCON,DMCB,(0,QSTART),(3,BQSTART)                               
         GOTO1 DATCON,DMCB,(0,QEND),(3,BQEND)                                   
         EJECT                                                                  
*              NOW BUILD THE LISTS                                              
         SPACE 3                                                                
         LA    R2,MEDDATES         WEEKS                                        
         L     R3,MEDNUMWK                                                      
         LTR   R3,R3                                                            
         BZ    MD10                                                             
         LA    R3,75                                                            
         CLC   RCPROG,=C'D6'       BRAND DAILY TIMESHEET                        
         BE    MD4A                                                             
         CLI   MEDDAILY,C'Y'       DAILY REPORTS                                
         BE    MD4A                                                             
         GOTO1 MOBILE,DMCB,((R3),QSTART),(4,(R2))                               
         B     MD6                                                              
         SPACE 2                                                                
MD4A     GOTO1 MOBILE,DMCB,((R3),QSTART),(9,(R2))                               
         SPACE 2                                                                
MD6      CLI   0(R2),X'FF'         FIND END OF LIST                             
         BE    MD8                                                              
         LA    R2,4(R2)                                                         
         B     MD6                                                              
         SPACE 2                                                                
MD8      MVI   0(R2),0                                                          
         SPACE 2                                                                
MD10     L     R3,MEDNUMMO         MONTHS                                       
         LA    R2,MEDMON01                                                      
         LTR   R3,R3                                                            
         BZ    MD16                                                             
**       CLI   MEDDAILY,C'Y'                                                    
**       BE    MD11                                                             
**       ZIC   R0,MEDDFORM         DATE CONTROL                                 
         CLC   QPROG,=C'BU'        CHECK IF CALLED BY BILLING                   
         BE    MD10F               TO USE WEEKS BUFFER, OTHERWISE               
         CLC   QPROG,=C'DU'        25 MONTHS WILL NOT FIT                       
         BE    MD10F                                                            
         CLC   QPROG,=C'B1'                                                     
         BE    MD10F                                                            
         CLC   QPROG,=C'D1'                                                     
         BE    MD10F                                                            
         CLI   MEDDAILY,C'Y'                                                    
         BE    MD11                                                             
         ZIC   R0,MEDDFORM         DATE CONTROL                                 
         B     MD10M                                                            
MD10F    OI    MYFLAG,SPOTBIL      SET FALG TO INDICATE BILLING                 
         CLI   MEDDAILY,C'Y'       BILLING PARTIAL PERIOD                       
         BNE   MD10F5                                                           
         MVC   0(4,R2),BQSTARTP    MOVE HEX START/END OF PERIOD                 
         MVI   4(R2),X'FF'         EOT                                          
         XC    MEDDAILY,MEDDAILY   CLEAR AS THIS WAS A FUDGE                    
         B     MD12                                                             
MD10F5   ZIC   R0,MEDDFORM         DATE CONTROL                                 
         CLC   =C'H9',QAGY         CHECK IF P&G                                 
         BE    *+14                                                             
         CLC   =C'DU',QAGY                                                      
         BNE   MD10M                                                            
*                                  DO SPECIAL CALL TO MOBILE                    
         CLC   =C'BBV',QCLT        TO GET THEIR FISCAL CALENDAR                 
         BE    MD10H               TO GET THEIR FISCAL CALENDAR                 
         CLC   =C'BBI',QCLT                                                     
         BE    MD10H               TO GET THEIR FISCAL CALENDAR                 
         CLC   =C'BBF',QCLT                                                     
         BE    MD10H               TO GET THEIR FISCAL CALENDAR                 
         CLC   =C'BBN',QCLT                                                     
         BE    MD10H               TO GET THEIR FISCAL CALENDAR                 
*                                                                               
         CLC   =C'HPG',QCLT        DO SPECIAL CALL TO MOBILE                    
         BE    MD10H               TO GET THEIR FISCAL CALENDAR                 
         CLC   =C'PG ',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PGB',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PGG',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PGP',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PG1',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PG2',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PG3',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PG4',QCLT                                                     
         BE    MD10H                                                            
         CLC   =C'PG5',QCLT                                                     
         BNE   *+12                                                             
*                                                                               
MD10H    IC    R0,=C'P'                                                         
         B     MD10M                                                            
*                                                                               
         CLC   =C'MMM',QCLT                                                     
         BE    MD10J                                                            
         CLC   =C'MMH',QCLT                                                     
         BE    MD10J                                                            
         CLI   QPROG+1,C'U'       CHECK IF NET BILLING                          
         BNE   MD10M              IF NOT, DONE CHECKING                         
         CLC   =C'MM3',QCLT       IF NET, CHECK MM3                             
         BNE   MD10M                                                            
*                                                                               
MD10J    MVC   MYQSTART,QSTART                                                  
         CLC   MYQSTART(6),=X'FAF3F1F1F3F0'  IF BEFORE NOV30/03 DO NOT          
         BNH   MD10K                         ADD 1 WEEK                         
         CLC   MYQSTART(6),=X'FAF5F0F1F0F1'  OR AFTER JAN1/05 DO NOT            
         BH    MD10K                         ADD 1 WEEK                         
         GOTO1 ADDAY,DMCB,MYQSTART,MYQSTART,7  ADD 1 WEEK BECAUSE...            
*    ... HAVE SUBTRACTED THAT WEEK EARLIER IN MOBILE CALLED BY BILLING          
MD10K    IC    R0,=C'M'                                                         
         GOTO1 MOBILE,DMCB,((R3),MYQSTART),((R0),(R2))                          
         B     MD12                                                             
*                                                                               
MD10M    GOTO1 MOBILE,DMCB,((R3),QSTART),((R0),(R2))                            
         B     MD12                                                             
         SPACE 2                                                                
MD11     LA    R4,MEDDATES         DAILY - MONTHS BECOME WEEKS                  
MD11A    MVC   0(2,R2),0(R4)                                                    
         LA    R0,6                                                             
         CLI   4(R4),0                                                          
         BE    *+12                                                             
         LA    R4,4(R4)                                                         
         BCT   R0,*-12                                                          
         MVC   2(2,R2),0(R4)                                                    
         LA    R2,4(R2)                                                         
         LA    R4,4(R4)                                                         
         CLI   0(R4),0                                                          
         BE    MD14                                                             
         BCT   R3,MD11A                                                         
         B     MD14                                                             
         SPACE 2                                                                
MD12     CLI   0(R2),X'FF'                                                      
         BE    MD14                                                             
         LA    R2,4(R2)                                                         
         B     MD12                                                             
         SPACE 2                                                                
MD14     MVI   0(R2),0                                                          
         BAS   RE,FIXMONTH                                                      
         TM    MYFLAG,SPOTBIL                                                   
         BO    MD20                FOR BILLING SKIP QUARTER AND WEEKS           
         BAS   RE,QUARTER                                                       
         SPACE 2                                                                
MD16     BAS   RE,FIXWEEKS                                                      
         SPACE 2                                                                
MD20     MVC   MEDPERD(4),BQSTARTP                                              
         EJECT                                                                  
*              ROUTINE TO BUILD CHUNK ADDRESSES                                 
         SPACE 3                                                                
         LA    R2,MEDWEEKS         WEEKS                                        
         L     R3,MEDNUMWK                                                      
         LA    R4,MEDDATA                                                       
         BAS   RE,MD22                                                          
         LA    R2,MEDMON01         MONTHS                                       
         L     R3,MEDNUMMO                                                      
         TM    MYFLAG,SPOTBIL      IF BILLING POINT TO WEEKS BUFFER             
         BZ    *+8                                                              
         LA    R2,MEDWEEKS                                                      
         BAS   RE,MD22                                                          
         LA    R2,MEDQRT01         QUARTERS                                     
         L     R3,MEDNUMQT                                                      
         BAS   RE,MD22                                                          
         CLI   MEDNYBEF,C'Y'                                                    
         BNE   *+8                                                              
         A     R4,MEDLCHNK                                                      
         ST    R4,MEDPERD+4                                                     
         B     MD30                                                             
         SPACE 2                                                                
MD22     LTR   R3,R3                                                            
         BCR   8,RE                                                             
         ST    R4,4(R2)  PUT PRESENT BUFFER ADDRESS IN                          
         L     RF,MEDLCHNK                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),0(R4)                                                    
         OC    0(4,R2),0(R2)                                                    
         BNZ   *+10                                                             
         XC    4(4,R2),4(R2)                                                    
MD24     LA    R2,12(R2)                                                        
         A     R4,MEDLCHNK                                                      
         BCT   R3,MD22                                                          
         BR    RE                                                               
         EJECT                                                                  
*              NOW GENERATE THE ADDING ADDRESSES                                
         SPACE 3                                                                
MD30     LA    R2,MEDDATES                                                      
         L     R3,MEDNUMDE                                                      
         SPACE 2                                                                
MD32     OC    0(4,R2),0(R2)       FOR EACH ACTIVE DATE, - A SUBSET -           
         BZ    MD34                                                             
         BAS   RE,MD36             GO AND FIND A SUPERSET                       
         SPACE 2                                                                
MD34     LA    R2,12(R2)                                                        
         BCT   R3,MD32                                                          
         B     MD50                                                             
         SPACE 2                                                                
MD36     NTR1                                                                   
         LA    R4,12(R2)                                                        
         SPACE 2                                                                
MD38     OC    0(4,R4),0(R4)                                                    
         BZ    MD40                                                             
         CLC   0(2,R2),0(R4)       CHECK START/END FALL WITHIN                  
         BL    MD40                SUPERSETS START END                          
         CLC   2(2,R2),2(R4)                                                    
         BH    MD40                                                             
         MVC   8(4,R2),4(R4)       TELL SUBSET TO ADD TO SUPER'S ADDR.          
         B     XIT                                                              
         SPACE 2                                                                
MD40     LA    R4,12(R4)                                                        
         BCT   R3,MD38                                                          
         XC    8(4,R2),8(R2)       THIS MUST BE THE SUPERSET                    
         B     XIT                                                              
         EJECT                                                                  
*              NOW FIND FIRST MEDAFRST AND MEDALAST                             
         SPACE 3                                                                
MD50     LA    R2,MEDWEEKS                                                      
         L     R3,MEDNUMWK                                                      
         BAS   RE,MD52                                                          
         LA    R2,MEDMON01                                                      
         L     R3,MEDNUMMO                                                      
         TM    MYFLAG,SPOTBIL      IF BILLING POINT TO WEEKS BUFFER             
         BZ    *+8                                                              
         LA    R2,MEDWEEKS                                                      
         BAS   RE,MD52                                                          
         LA    R2,MEDQRT01                                                      
         L     R3,MEDNUMQT                                                      
         BAS   RE,MD52                                                          
         LA    R2,MEDPERD                                                       
         LR    R3,R2                                                            
         B     MD56                                                             
         SPACE 2                                                                
MD52     LTR   R3,R3               HAVE WE AN ACTIVE LEVEL                      
         BCR   8,RE                                                             
         BCTR  R3,0                YES - SO FIND LAST POSSIBLE                  
         MH    R3,=H'12'                                                        
         AR    R3,R2                                                            
         SPACE 2                                                                
MD54     OC    0(4,R3),0(R3)       FIND LAST ACTUAL DATE                        
         BNZ   MD56                                                             
         SH    R3,=H'12'                                                        
         B     MD54                                                             
         SPACE 2                                                                
MD56     ST    R2,MEDAFRST                                                      
         ST    R3,MEDALAST                                                      
         EJECT                                                                  
*              CHECK FOR SPECIAL BEFORE AND AFTER ROUTINES                      
         SPACE 3                                                                
MD60     L     R4,MEDPERD+4                                                     
         CLI   MEDNYBEF,C'Y'                                                    
         BNE   MD62                                                             
         S     R4,MEDLCHNK                                                      
         ST    R4,MEDBFORE+4                                                    
         L     R5,=F'-1'                                                        
         GOTO1 ADDAY,DMCB,QSTART,DUB,(R5)                                       
         GOTO1 DATCON,DMCB,(0,DUB),(2,MEDBFORE+2)                               
         SPACE 2                                                                
MD62     CLI   MEDNYAFT,C'Y'                                                    
         BNE   MD64                                                             
         L     R4,MEDPERD+4                                                     
         A     R4,MEDLCHNK                                                      
         ST    R4,MEDAFTER+4                                                    
         GOTO1 ADDAY,DMCB,QEND,DUB,1                                            
         GOTO1 DATCON,DMCB,(0,DUB),(2,MEDAFTER)                                 
         MVC   MEDAFTER+2(2),=X'FFFF'                                           
         SPACE 2                                                                
MD64     CLI   MEDNYTOT,C'Y'                                                    
         BNE   MD66                                                             
         MVC   MEDTOTAL+2(2),=X'FFFF'                                           
         A     R4,MEDLCHNK                                                      
         ST    R4,MEDTOTAL+4                                                    
         ST    R4,MEDPERD+8                                                     
         ST    R4,MEDBFORE+8                                                    
         ST    R4,MEDAFTER+8                                                    
         SPACE 2                                                                
MD66     CLC   QSTAUTO,=C'ES'                                                   
         BNE   XIT                                                              
         MVC   QSTART(12),SAVQDATE                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO DEDUCE QUARTER DATES                                  
         SPACE 3                                                                
QUARTER  NTR1                                                                   
         L     R4,MEDNUMQT                                                      
         LTR   R4,R4                                                            
         BZ    XIT                                                              
         LA    R2,MEDMON01                                                      
         LA    R3,MEDQRT01                                                      
*                                                                               
         CLI   MEDSPQRT,C'Y'       TEST SPECIAL QUARTERS INVOKED                
         BE    SQU                                                              
         SPACE 2                                                                
QU2      BAS   RE,QU4                                                           
         LA    R2,24(R2)                                                        
         CLI   MEDDAILY,C'Y'                                                    
         BE    *+8                                                              
         LA    R2,12(R2)                                                        
         LA    R3,12(R3)                                                        
         BCT   R4,QU2                                                           
         B     XIT                                                              
         SPACE 2                                                                
QU4      NTR1                                                                   
         MVC   0(4,R3),0(R2)       USE START/END FROM FIRST MONTH               
         LA    R2,12(R2)                                                        
         OC    2(2,R2),2(R2)                                                    
         BZ    XIT                                                              
         MVC   2(2,R3),2(R2)       USE END OF MONTH 2 IF PRESENT                
         CLI   MEDDAILY,C'Y'                                                    
         BE    XIT                                                              
         LA    R2,12(R2)                                                        
         OC    2(2,R2),2(R2)                                                    
         BZ    XIT                                                              
         MVC   2(2,R3),2(R2)       OR MONTH 3                                   
         B     XIT                                                              
         EJECT                                                                  
*              WORK OUT QUARTER DATES WITH SPECIAL MONTHS                       
         SPACE 3                                                                
SQU      LA    R5,SPQRTTAB                                                      
         SPACE 1                                                                
SQU2     CLC   0(2,R5),RCAGENCY                                                 
         BE    SQU4                                                             
         LA    R5,L'SPQRTTAB(R5)                                                
         CLI   0(R5),X'FF'                                                      
         BNE   SQU2                                                             
         B     QU2                 NO SPECIAL TABLE FOUND                       
*                                                                               
SQU4     DS    0H                                                               
         GOTO1 DATCON,DMCB,(2,2(R2)),(3,DUB)                                    
         ZIC   RE,DUB+1            GET END DATE MONTH                           
         BCTR  RE,0                                                             
         IC    RE,2(RE,R5)         GET QUARTER NUMBER FOR MONTH                 
         BCTR  RE,0                                                             
         MH    RE,=H'12'                                                        
         LA    RE,MEDQRT01(RE)     POINT TO QUARTER ENTRY                       
*                                                                               
         OC    0(2,RE),0(RE)       TEST QTR START DATE PRESENT                  
         BZ    *+14                NO                                           
         CLC   0(2,RE),0(R2)       CURRENT QTR START TO MONTH START             
         BL    *+10                LOW - KEEP IT                                
         MVC   0(2,RE),0(R2)                                                    
*                                                                               
         CLC   2(2,RE),2(R2)       CURRENT QTR END TO MONTH END                 
         BH    *+10                LATER - KEEP IT                              
         MVC   2(2,RE),2(R2)                                                    
*                                                                               
         LA    R2,12(R2)           NEXT MONTH ENTRY                             
         OC    0(2,R2),0(R2)                                                    
         BNZ   SQU4                                                             
         B     XIT                                                              
         SPACE 2                                                                
SPQRTTAB DS    0XL14     J F M A M J J A S O N D                                
         DC    C'JW',AL1(1,1,2,2,2,3,3,3,4,4,4,4)                               
         DC    X'FF'                                                            
         EJECT                                                                  
*              ROUTINE TO FIX WEEKS INTO APPROPRIATE QUARTERS                   
         SPACE 3                                                                
FIXWEEKS NTR1                                                                   
         MVC   MYWEEKS,MEDWEEKS                                                 
         XC    MEDWEEKS(224),MEDWEEKS                                           
         LA    R2,MYWEEKS                                                       
         LA    R3,MEDWEEKS                                                      
         LA    R4,MEDQRT01                                                      
         LA    R5,5                                                             
         SPACE 2                                                                
FW2      LR    R6,R3                                                            
         LA    R7,14                                                            
         SPACE 2                                                                
FW4      OC    0(4,R2),0(R2)                                                    
         BZ    XIT                                                              
         OC    0(4,R4),0(R4)                                                    
         BZ    FW6                                                              
         CLC   0(2,R2),2(R4)       DOES WEEK START COME IN QUARTER              
         BH    FW8                                                              
         SPACE 2                                                                
FW6      MVC   0(4,R6),0(R2)       YES - SO PUT INTO QUARTER'S WEEKLIST         
         LA    R2,4(R2)                                                         
         LA    R6,12(R6)                                                        
         BCT   R7,FW4                                                           
         SPACE 2                                                                
FW8      LA    R3,180(R3)          NO  - SO BUMP TO NEXT QUARTER                
         LA    R4,12(R4)                                                        
         BCT   R5,FW2                                                           
         B     XIT                                                              
         EJECT                                                                  
*              FIX MONTHS INTO APPROPRIATE SLOTS                                
         SPACE 3                                                                
FIXMONTH NTR1                                                                   
         MVC   MYMONTHS,MEDMON01                                                
         XC    MEDMON01(100),MEDMON01                                           
         LA    R2,MYMONTHS                                                      
         LA    R3,MEDMON01                                                      
         LA    R4,13                                                            
         TM    MYFLAG,SPOTBIL      IF BILLING, DO 25 MONTHS                     
         BZ    FM2                                                              
         LA    R4,25                                                            
         LA    R3,MEDWEEKS                                                      
         SPACE 2                                                                
FM2      MVC   0(4,R3),0(R2)                                                    
         LA    R2,4(R2)                                                         
         LA    R3,12(R3)                                                        
         BCT   R4,FM2                                                           
         B     XIT                                                              
         EJECT                                                                  
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 3                                                                
DATED    DSECT                                                                  
MYWEEKS  DS    CL224                                                            
MYMONTHS DS    CL100                                                            
MYQSTART DS    CL12                                                             
MYFLAG   DS    X                                                                
SPOTBIL  EQU   X'80'               CALLED BY SPOT BILLING, NEED 25 MOS          
SAVQDATE DS    CL12                                                             
DATEDLQ  EQU   *-DATED                                                          
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'051SPMEDDATE 03/05/18'                                      
         END                                                                    
