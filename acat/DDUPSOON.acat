*          DATA SET DDUPSOON   AT LEVEL 003 AS OF 10/28/15                      
*CATALP UPSOON                                                                  
         TITLE 'DDUPSOON - BUFFER CHANGED RECORDS FOR UPDATIVE SOONS'           
*&&      SET   DEB=N                                                            
***********************************************************************         
* UPSOON ROUTINE - THIS ROUTINE IS INTENDED TO INTERCEPT WRITES FROM  *         
* RECOVERY AND PUT THEM TO A FILE (NAME = UPSOON) AND THEN RETURN THIS*         
* CHANGED RECORD TO DADDS RATHER THAN THE UNCHANGED RECORD FROM THE   *         
* FILE. THIS (WE BELIEVE) WILL ALLOW UPDATIVE SOONS TO WORK AS NORMAL *         
***********************************************************************         
         PRINT NOGEN                                                            
UPSOON   CSECT                                                                  
         NMOD1 WORKL,*UPSOON*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         USING BSPARA,BSPLIST                                                   
         USING SVKRECD,SVKAREA                                                  
         USING DTFPHD,UPSFILE                                                   
         ST    RD,SAVERD                                                        
         ST    R1,SAVER1                                                        
         MVC   IPARMS,0(R1)                                                     
*&&DO                                                                           
         CLI   INITSOON,C'X'       FAILED TO INITIALISE BEFORE?                 
         BE    EXITL               YES                                          
*                                                                               
         ICM   RF,15,VSSB          NEED OFFLINE EXTENDED SSB                    
         OC    0(2,RF),0(RF)                                                    
         BNZ   MAIN02                                                           
         CLI   2(RF),X'FF'                                                      
         JNE   *+2                                                              
         TM    SSOFLAG2-SSOOFF(RF),SSO2UPSN                                     
         BO    MAIN04                                                           
*                                                                               
MAIN02   MVI   INITSOON,C'X'                                                    
         B     EXITL                                                            
*&&                                                                             
MAIN04   CLI   INITSOON,C'N'       INITIALISED YET?                             
         BNE   *+8                 YES                                          
         BRAS  RE,INIT                                                          
         CLI   INITSOON,C'X'       USING INITSOON?                              
         BE    EXITL               NO - SET NOT FOUND                           
*&&DEB                                                                          
         MVC   MSG,SPACES                                                       
         MVC   MSG(4),0(R1)                                                     
         MVC   MSG+5(10),=CL10'REQUESTED'                                       
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         L     R1,SAVER1                                                        
         LA    R3,ACTTAB                                                        
         USING ACTTABD,R3                                                       
         LHI   RF,ACTTABL                                                       
MAIN06   CLI   ACTCMD,255                                                       
         JE    *+2                 INVALID COMMAND                              
         CLC   ACTCMD,0(R1)                                                     
         BE    *+8                                                              
         BXH   R3,RF,MAIN06                                                     
*                                                                               
         ICM   RF,15,ACTRTN                                                     
         BR    RF                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET DA FILE RECORD (CALLED FROM DADDS)                   *         
* NTRY: P1     = C'GETR'                                              *         
*       P2     = PARAMETERS AS PASSED TO DADDS                        *         
***********************************************************************         
GETR     L     R9,IP2                                                           
         USING DDPLIST,R9                                                       
         MVC   SVKFILE,DDPFILE     SET FILE NUMBER                              
         L     RF,DDPDA                                                         
         MVC   SVKKEY(4),0(RF)                                                  
         LA    RF,SVKRECD                                                       
         ST    RF,BSPAREC          SET A(RECORD)                                
         MVI   BSPLENR,X'00'       SET DO NOT ADD                               
         SAM31                                                                  
         GOTO1 VBINSRCH,BSPLIST                                                 
         TM    0(R1),X'80'         KEY FOUND?                                   
         BZ    GETR02              YES                                          
*&&DEB                                                                          
         MVC   MSG,SPACES          SET FAIL - SAY WHAT WE WANTED                
         MVC   MSG+00(04),=CL4'FAIL'                                            
         MVC   MSG+06(05),=CL5'FILE='                                           
         MVC   FULL(1),DDPFILE                                                  
         GOTO1 VHEXOUT,PLIST,FULL,MSG+11,1                                      
         L     RE,DDPDA                                                         
         MVC   FULL,0(RE)                                                       
         MVC   MSG+15(04),=CL4'D/A='                                            
         GOTO1 (RF),(R1),FULL,MSG+19,4                                          
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITL                                                            
*                                                                               
GETR02   ICM   RF,15,BSPAREC       GET A(RECORD IN BINSRCH LIST)                
         JZ    *+2                                                              
         MVC   FULL,SVKDA-SVKRECD(RF)                                           
         SAM24                                                                  
*                                                                               
         ICM   R3,15,DDPIOA        READ RECORD INTO I/O AREA                    
         AHI   R3,-(RCVLENQ)       SAVE RCVHDR LENGTH OF W/S AROUND IT          
         MVC   SAVE,0(R3)                                                       
         GOTO1 VDADDS,DMCB,RDID,(R3),0,UPSFILE,FULL,0                           
         OC    DM3(2),DM3                                                       
         JNZ   *+2                                                              
*                                                                               
         MVC   0(RCVLENQ,R3),SAVE                                               
         LH    R0,DM3+2            ADJUST LENGTH TO REMOVE RECOVERY HDR         
         AHI   R0,-(RCVLENQ)                                                    
         STH   R0,DM3+2                                                         
         MVC   DDPERR(4),DM3       SET ERROR FLAGS AND LENGTH                   
*&&DEB                                                                          
         MVC   MSG,SPACES          SET PASS - SAY WHAT WE WANTED                
         MVC   MSG+00(04),=CL4'PASS'                                            
         MVC   MSG+06(05),=CL5'FILE='                                           
         MVC   FULL(1),DDPFILE                                                  
         GOTO1 VHEXOUT,PLIST,FULL,MSG+11,1                                      
         L     RE,DDPDA                                                         
         MVC   FULL,0(RE)                                                       
         MVC   MSG+15(04),=CL4'D/A='                                            
         GOTO1 (RF),(R1),FULL,MSG+19,4                                          
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITOK                                                           
         DROP  R9                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET IS FILE RECORD (CALLED FROM ISDDS)                   *         
* NTRY: 0(R1)  = C'GETI'                                              *         
***********************************************************************         
GETI     DC    H'0'                NEED CODE HERE TO GET I/S BY KEY             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT RECORD TO UPSFILE                                    *         
* NTRY: P1     = C'PUTR'                                              *         
*       P2     = A(RECOVERY FILE HEADER)                              *         
***********************************************************************         
PUTR     ICM   R9,15,IP2                                                        
         USING RCVHDRD,R9                                                       
         CLI   RRECTY,3            DO NOT ACCEPT ADDS                           
         BNE   PR02                                                             
*&&DEB                                                                          
         MVC   MSG,SPACES                                                       
         MVC   MSG+00(30),=CL30'ADD REQUESTED - IGNORED'                        
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITL                                                            
*                                                                               
PR02     XC    SVKRECD(SVRECLQ),SVKRECD                                         
         MVC   SVKFILE,RFILTY      SET FILE TYPE                                
         ICM   R0,15,RVCHR         USE D/A AS KEY FOR THIS RECORD               
         BZ    PUTI                                                             
         STCM  R0,15,SVKKEY                                                     
*                                                                               
         LH    R3,RCVLEN           ADD RECORD TO UPSFILE                        
         XR    R0,R0               R0=L'TRAILER                                 
         TM    RTIME,RTIMEEXT                                                   
         BZ    PR04                RECOVERY RECORD HAS NO TRAILER               
         LA    R1,0(R9,R3)         R1=A(END OF RECORD)                          
         AHI   R1,-1                                                            
         IC    R0,0(R1)            R0=L'TRAILER IN LAST BYTE                    
         SR    R3,R0                                                            
         JNP   *+2                                                              
         STH   R3,0(R9)            SET REC LEN TO EXCLUDE TRAILER               
         NI    RTIME,255-RTIMEEXT  SET NO TRAILER                               
*                                                                               
PR04     GOTO1 VDADDS,DMCB,WTCKD,(R9),(R3),UPSFILE,DM7,0                        
         OC    DM3(2),DM3                                                       
         JNZ   *+2                                                              
*                                                                               
         MVC   SVKDA,DM7           SET D/A FROM UPSFILE                         
         LTR   R0,R0                                                            
         BZ    PR06                                                             
         AR    R3,R0                                                            
         STH   R3,0(R9)            SET REC LEN TO INCLUDE TRAILER               
         OI    RTIME,RTIMEEXT      SET TRAILER PRESENT                          
*&&DEB                                                                          
         MVC   MSG,SPACES          SET UPSFILE ADD - SAY WHAT WE DID            
         MVC   MSG+00(11),=CL11'UPSFILE ADD'                                    
         MVC   MSG+13(05),=CL5'FILE='                                           
         MVC   FULL(1),RFILTY                                                   
         GOTO1 VHEXOUT,PLIST,FULL,MSG+18,1                                      
         MVC   FULL,RVCHR                                                       
         MVC   MSG+21(04),=CL4'D/A='                                            
         GOTO1 (RF),(R1),FULL,MSG+25,4                                          
         MVC   FULL,SVKDA                                                       
         MVC   MSG+35(08),=CL8'UPS D/A='                                        
         GOTO1 (RF),(R1),FULL,MSG+43,4                                          
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
         MVC   MSG,SPACES          NOW DECIPHER WHAT BINSRCH DID                
         MVC   MSG+00(09),=CL9'BSRCH PUT'                                       
*&&                                                                             
PR06     LA    RF,SVKRECD                                                       
         ST    RF,BSPAREC          SET A(RECORD)                                
         MVI   BSPLENR,X'00'       SET DO NOT ADD                               
         SAM31                                                                  
         GOTO1 VBINSRCH,BSPLIST                                                 
         TM    0(R1),X'80'         KEY FOUND?                                   
         BO    PR08                NO                                           
*                                                                               
         SAM31                                                                  
         ICM   RF,15,0(R1)                                                      
BS       USING SVKRECD,RF                                                       
         MVC   DUB+0(4),BS.SVKDA   SAVE OLD D/A FOR DEBUG                       
         MVC   DUB+4(4),SVKDA      SAVE NEW D/A FOR DEBUG                       
         MVC   BS.SVKDA,SVKDA                                                   
         SAM24                                                                  
         DROP  BS                                                               
*&&DEB                                                                          
         MVC   MSG+10(04),=CL04'KEY='                                           
         GOTO1 VHEXOUT,DMCB,SVKRECD,MSG+14,5                                    
         MVC   MSG+25(08),=CL08'OLD D/A='                                       
         GOTO1 (RF),(R1),DUB,MSG+33,4                                           
         MVC   MSG+42(08),=CL08'NEW D/A='                                       
         GOTO1 (RF),(R1),DUB+4,MSG+50,4                                         
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITOK                                                           
*                                                                               
PR08     LA    RF,SVKRECD                                                       
         ST    RF,BSPAREC          SET A(RECORD)                                
         MVI   BSPLENR,X'01'       SET ADD                                      
         SAM31                                                                  
         GOTO1 VBINSRCH,BSPLIST                                                 
         ICM   R0,15,0(R1)                                                      
         JZ    *+2                                                              
*&&DEB                                                                          
         MVC   MSG,SPACES                                                       
         MVC   MSG+00(09),=CL9'BSRCH ADD'                                       
         MVC   MSG+10(04),=CL04'KEY='                                           
         GOTO1 VHEXOUT,DMCB,SVKRECD,MSG+14,5                                    
         MVC   MSG+25(08),=CL08'NEW D/A='                                       
         GOTO1 (RF),(R1),SVKDA,MSG+33,4                                         
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT IS FILE RECORD (CALLED FROM ISDDS)                   *         
* NTRY: 0(R1)  = C'PUTI'                                              *         
***********************************************************************         
PUTI     B     EXITL               NEED CODE HERE TO PUT I/S BY KEY             
         EJECT                                                                  
***********************************************************************         
* PERFORM INITIALISATION                                              *         
***********************************************************************         
INIT     NTR1  ,                                                                
         LA    R2,FULL                                                          
         EXTRACT (2),FIELDS=(TIOT)                                              
         L     R2,FULL                                                          
         AHI   R2,TIOENTRY-TIOT1                                                
         USING TIOELNGH,R2                                                      
*                                                                               
         XR    RF,RF                                                            
INIT02   ICM   RF,1,TIOELNGH       GO DOWN TIOT LOOKING FOR UPSFILE DD          
         BNZ   *+12                                                             
         MVI   INITSOON,C'X'       DO NOT USE UPSFILE                           
         B     EXITL                                                            
*                                                                               
         CLC   CUPSFILE,TIOEDDNM                                                
         BE    *+8                                                              
         BXH   R2,RF,INIT02                                                     
         DROP  R2                                                               
*                                                                               
         LA    R0,UPSFILE          OPEN UPSFILE                                 
         GOTO1 VDADDS,DMCB,DAOPEN,,,(R0)                                        
*                                                                               
         ICM   RF,15,VMASTC                                                     
         BZ    INIT04                                                           
         TM    MCRFLAG1-MASTD(RF),MCRUSDIN                                      
         BO    INIT04                                                           
*                                                                               
         LA    R0,UPSFILE          ERASE UPSFILE                                
         XC    DM6,DM6                                                          
         LA    RF,DM6                                                           
         ST    RF,DM5                                                           
         GOTO1 VDADDS,DMCB,WTERASE,,,(R0),=A(0)                                 
*                                                                               
INIT04   MVC   DNEXT,=XL4'00010000'                                             
*                                                                               
         LHI   R0,MAXKEYS          DO GETMAIN FOR BINSRCH TABLE                 
         MHI   R0,SVRECLQ                                                       
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         ST    R1,AXAAREA                                                       
         ST    R0,LXAAREA                                                       
*                                                                               
         MVC   BSPSTRT,AXAAREA     INITIALISE BINSRCH PARAMETERS                
         LHI   R0,SVRECLQ                                                       
         ST    R0,BSPLENR          SET L'RECORD                                 
         LHI   R0,SVKEYLQ                                                       
         ST    R0,BSPLENK          SET L'KEY                                    
         MVI   BSPKEYD,0           SET DISPLACEMENT TO KEY                      
         LHI   R0,MAXKEYS                                                       
         ST    R0,BSPEND           SET MAXIMUM NUMBER OF RECORDS                
*                                                                               
         MVI   SVKAREA,X'FF'                                                    
         MVC   SVKAREA+1(L'SVKAREA-1),SVKAREA                                   
         LA    RF,SVKRECD                                                       
         ST    RF,BSPAREC          ADD A DUMMY EOF RECORD FOR DEBUG             
         MVI   BSPLENR,X'01'                                                    
         SAM31                                                                  
         GOTO1 VBINSRCH,BSPLIST                                                 
*                                                                               
         ICM   RF,15,VMASTC                                                     
         BZ    INITX                                                            
         TM    MCRFLAG1-MASTD(RF),MCRUSDIN                                      
         BZ    INITX                                                            
         GOTO1 FILLBSP,(RC)        FILL BINSRCH TABLE                           
*                                                                               
INITX    MVI   INITSOON,C'Y'                                                    
*&&DEB                                                                          
         MVC   MSG,SPACES                                                       
         MVC   MSG(40),=CL40'DDUPSOON INITIALISED OK'                           
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
*&&                                                                             
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REPOPULATE BINSRCH LIST                                  *         
***********************************************************************         
FILLBSP  NTR1  ,                                                                
         LR    R9,RD               GET SOME W/S FOR THIS MODULE                 
         AHI   RD,8096             ADJUST RD POINTER                            
         XC    0(12,RD),0(RD)      CLEAR CHAIN AREA                             
         ST    R9,4(RD)            AND SET BACKWARDS POINTER PROPERLY           
         LR    R0,R9                                                            
         LR    R1,RD                                                            
         SR    R1,R9                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR W/S THUS OBTAINED                      
*                                                                               
         MVC   TRACK(4),=X'00010101'                                            
         LHI   R0,1                RESET NEW BLOCK FLAG                         
         B     FBS08                                                            
*                                                                               
FBS02    LTR   R0,R0               JUST READ A NEW BLOCK?                       
         BZ    FBS04               YES - TRY NEXT TRACK THEN                    
*                                                                               
         LLC   RF,BLOCK            NEW BLOCK - RECORD 1                         
         AHI   RF,1                                                             
         STC   RF,BLOCK                                                         
         MVI   BLOCK+1,X'01'                                                    
         XR    R0,R0               SET NEW BLOCK FLAG                           
         B     FBS08                                                            
*                                                                               
FBS04    LH    RF,TRACK            NEXT TRACK                                   
         LA    RF,1(RF)                                                         
         STH   RF,TRACK                                                         
         MVC   BLOCK,=X'0101'      RESET BLOCK/RECORD                           
         LHI   R0,1                RESET NEW BLOCK FLAG                         
         B     FBS08                                                            
*                                                                               
FBS06    LH    RF,BLOCK            NEXT RECORD ON FILE                          
         LA    RF,1(RF)                                                         
         STH   RF,BLOCK                                                         
         CLI   BLOCK+1,0           MAKE SURE RECORD IS OK                       
         BNE   *+8                                                              
         MVI   BLOCK+1,1                                                        
         LHI   R0,1                RESET NEW BLOCK FLAG                         
*                                                                               
FBS08    MVC   DM6(4),TRACK        SET TTTTBBRR                                 
         GOTO1 VDADDS,DMCB,RDID,(R9),0,,DM6                                     
         OC    DM3(2),DM3                                                       
         BZ    FBS10                                                            
         TM    DM3+1,X'08'         RECORD NOT FOUND                             
         BO    FBS02                                                            
         TM    DM3+1,X'04'         EOF                                          
         BO    EXITOK                                                           
         DC    H'0'                UNKNOWN ERROR                                
*                                                                               
FBS10    MVC   SVKRECD(SVKEYLQ),0(R9)                                           
         MVC   SVKDA,DM6           DISK ADDRESS                                 
*                                                                               
         LA    RF,SVKRECD                                                       
         ST    RF,BSPAREC          SET A(RECORD)                                
         MVI   BSPLENR,X'01'       SET ADD IF NOT FOUND                         
         SAM31                                                                  
         GOTO1 VBINSRCH,BSPLIST                                                 
         ICM   R1,15,0(R1)                                                      
         BNZ   FBS06               NEXT RECORD                                  
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* COMMON ROUTINES                                                     *         
***********************************************************************         
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
XMOD     L     RD,SAVERD                                                        
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
MSGH     DC    AL2(60)                                                          
MSG      DC    CL60' '                                                          
*                                                                               
SPACES   DC    60C' '                                                           
*                                                                               
MAXKEYS  EQU   30000               MAXIMUM NUMBER OF KEYS ALLOWED               
MAXKLEN  EQU   48                  MAXIMUM LENGTH OF KEY ALLOWED                
*                                                                               
RDID     EQU   1                                                                
WTCKD    EQU   5                                                                
WTERASE  EQU   8                                                                
DAOPEN   EQU   14                                                               
*                                                                               
         LTORG                                                                  
VBINSRCH DC    V(BINSRCH)                                                       
VDADDS   DC    V(DADDS)                                                         
VMASTC   DC    V(MASTC)                                                         
VSSB     DC    V(SSB)                                                           
VHEXOUT  DC    V(HEXOUT)                                                        
*                                                                               
CUPSFILE DC    CL7'UPSFILE'                                                     
         EJECT                                                                  
***********************************************************************         
* ACTION TABLE FOR RECOGNISED COMMANDS                                *         
***********************************************************************         
ACTTAB   DC    CL4'GETI',AL4(GETI)                                              
         DC    CL4'GETR',AL4(GETR)                                              
         DC    CL4'PUTI',AL4(PUTI)                                              
         DC    CL4'PUTR',AL4(PUTR)                                              
         DC    X'FF'                                                            
*                                                                               
ACTTABD  DSECT                     DSECT TO COVER ACTION TABLE                  
ACTCMD   DS    CL4                 COMMAND                                      
ACTRTN   DS    A                   ROUTINE                                      
ACTTABL  EQU   *-ACTTABD                                                        
*                                                                               
UPSOON   CSECT                                                                  
*                                                                               
         DS    0D                                                               
         DC    CL8'INITSOON'                                                    
         DC    C'='                                                             
INITSOON DC    C'N'                INITIALISATION FLAG                          
         DC    XL6'00'                                                          
*                                                                               
         DC    CL8'AXAAREA='                                                    
AXAAREA  DC    A(0)                A(XA AREA)                                   
         DC    A(0)                                                             
         DC    CL8'LXAAREA='                                                    
LXAAREA  DC    A(0)                L'XA AREA                                    
         DC    A(0)                                                             
*                                                                               
         DC    CL8'BSPLIST='                                                    
BSPLIST  DS    0CL32               BINSRCH PARAMETER LIST                       
BSP1     DC    A(0)                                                             
BSP2     DC    A(0)                                                             
BSP3     DC    A(0)                                                             
BSP4     DC    A(0)                                                             
BSP5     DC    A(0)                                                             
BSP6     DC    A(0)                                                             
BSP7     DC    A(0)                                                             
BSP8     DC    A(0)                                                             
*                                                                               
         DC    CL8'UPSFILE='                                                    
UPSFILE  DMDA  BLKSIZE=8096                                                     
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVERD   DS    A                                                                
SAVER1   DS    A                                                                
*                                                                               
IPARMS   DS    0CL24                                                            
IP1      DS    A                                                                
IP2      DS    A                                                                
IP3      DS    A                                                                
IP4      DS    A                                                                
IP5      DS    A                                                                
IP6      DS    A                                                                
*                                                                               
PLIST    DS    6F                                                               
*                                                                               
DMCB     DS    0CL32                                                            
DM1      DS    F                                                                
DM2      DS    F                                                                
DM3      DS    F                                                                
DM4      DS    F                                                                
DM5      DS    F                                                                
DM6      DS    F                                                                
DM7      DS    F                                                                
DM8      DS    F                                                                
*                                                                               
TRACK    DS    H                                                                
BLOCK    DS    H                                                                
*                                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
*                                                                               
SVKAREA  DS    XL(SVRECLQ)                                                      
SAVE     DS    XL(RCVLENQ)                                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER SAVED KEY IN XA TABLE                                *         
***********************************************************************         
SVKRECD  DSECT                                                                  
SVKFILE  DS    X                                                                
SVKKEY   DS    XL(MAXKLEN)                                                      
SVKEYLQ  EQU   *-SVKRECD                                                        
SVKDA    DS    XL4                                                              
SVRECLQ  EQU   *-SVKRECD                                                        
         EJECT                                                                  
***********************************************************************         
* PARAMETER LIST FOR GETR (AS PASSED TO DADDS)                        *         
***********************************************************************         
DDPLIST  DSECT                                                                  
DDPRTN   DS    A                   DADDS ROUTINE                                
DDPIOA   DS    A                   I/O AREA                                     
DDPERR   DS    H                                                                
DDPLEN   DS    H                   RECORD LENGTH                                
DDPFILE  DS    X                   EXT FILE NUMBER                              
DDPDTF   DS    XL3                 DTF                                          
DDPDA    DS    A                   D/A                                          
DDPXTRA  DS    A                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*DMRCVRHDR                                                                      
RCVHDRD  DSECT                                                                  
RCVLEN   DS    H                                                                
RCVSPR   DS    H                                                                
       ++INCLUDE DMRCVRHDR                                                      
RCVLENQ  EQU   *-RCVHDRD                                                        
*                                                                               
*DDBSPARA                                                                       
       ++INCLUDE DDBSPARA                                                       
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
*DDMASTD                                                                        
       ++INCLUDE DDMASTD                                                        
*IEFTIOT1                                                                       
         IEFTIOT1                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDUPSOON  10/28/15'                                      
         END                                                                    
