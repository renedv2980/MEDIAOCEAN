*          DATA SET CXSUIC     AT LEVEL 005 AS OF 02/03/95                      
*CATALP CXSUIC                                                                  
         TITLE 'CXSUIC - CONTROL SECURITY EXTRACT - USER ID RECORD'             
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSUIC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSUIC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSUID,R2           R2=A(EXTRACT RECORD)                         
         USING CTIREC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSUILEN,CXSUILEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSUILEN(2),=AL2(CXSUIDL)                                        
         MVC   CXSUITYP,CXSUIDQ                                                 
         MVI   CXSUIDX,MXTRTQ                                                   
         MVI   CXSUIDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSUSC   MVI   CXSUICOD-1,MXTRTQ   USER ID CODE                                 
         MVC   CXSUICOD,CTIKID                                                  
*                                                                               
         LA    R4,CTIDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
CXSELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXSNXTX                                                          
         CLI   0(R4),X'02'         GET USER ID NUMBER ELEMENT                   
         BE    CXSNUM                                                           
         CLI   0(R4),X'06'         GET AGENCY ID ELEMENT                        
         BE    CXSAID                                                           
         CLI   0(R4),CTDSTELQ      GET DESTINATION DETAIL ELEMENT               
         BE    CXSDST                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXSELM                                                           
*                                                                               
CXSNUM   MVI   CXSUINUM-1,MXTRTQ   USER ID NUMBER                               
         SR    RF,RF                                                            
         ICM   RF,3,2(R4)                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSUINUM,DUB+5(3)                                                
         B     CXSNXT                                                           
*                                                                               
CXSAID   MVI   CXSUIAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSUIAID,2(R4)                                                   
         B     CXSNXT                                                           
*                                                                               
         USING CTDSTD,R4                                                        
CXSDST   MVI   CXSUINAM-1,MXTRTQ   DESTINATION NAME                             
         MVC   CXSUINAM,CTDSTNAM                                                
         TR    CXSUIADD,MXTRT                                                   
         MVI   CXSUIADD-1,MXTRTQ   DESTINATION ADDRESS LINE 1                   
         MVC   CXSUIADD,CTDSTADD                                                
         TR    CXSUIADD,MXTRT                                                   
         MVI   CXSUIAD2-1,MXTRTQ   DESTINATION ADDRESS LINE 2                   
         MVC   CXSUIAD2,CTDSTAD2                                                
         TR    CXSUIAD2,MXTRT                                                   
         MVI   CXSUIAD3-1,MXTRTQ   DESTINATION ADDRESS LINE 3                   
         MVC   CXSUIAD3,CTDSTAD3                                                
         TR    CXSUIAD3,MXTRT                                                   
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0D                                                               
         DC    C'CXSUICC '                                                      
CXSUICC  DS    0XL4                CLIENT SAVE DATA AREA                        
         DC    1000XL4'00'         A/M,CLI                                      
         DC    8X'FF'                                                           
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSUID                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005CXSUIC    02/03/95'                                      
         END                                                                    
