*          DATA SET DENSIWEEK  AT LEVEL 021 AS OF 05/01/02                      
*CATALP NSIWEEK                                                                 
         TITLE 'NSIWEEK - NSW DATE TO WEEK NO. CONVERSION AND BACK'             
NSIWEEK  CSECT                                                                  
*  P1   CONVERTING DATE TO WEEK#                                                
*        ENTRY: A(6-BYTE DATE)                                                  
*        EXIT : NSW WEEK# IN BYTE 0                                             
*       CONVERTING WEEK# TO DATE                                                
*        ENTRY: BYTE  0  = C'D'                                                 
*               BYTE 1-3 = A(2-BYTE YEAR/MONTH)                                 
*        EXIT : BYTE 1-3 = A(6-BYTE DATE) (CALLER MUST RETRIEVE                 
*                           IMMEDIATELY)                                        
*  P2   A(GETDAY)                                                               
*        ENTRY: START DAY OF WEEK IN BYTE 0                                     
*               IF ZERO, USE DEFAULT (SATURDAY)                                 
*        YEAR NO. RETURNED IN BYTE 0 (DATE TO WEEK# CONV)                       
*                                                                               
*  P3   A(ADDAY)                                                                
*                                                                               
*  P4   A(DATCON)                                                               
         SPACE 2                                                                
         PRINT NOGEN                                                            
         DS    500C                                                             
         ORG   NSIWEEK                                                          
         NMOD1 NSWDX-NSWD,**NSIW**                                              
         USING NSWD,RC                                                          
         MVC   ACTION,0(R1)                                                     
         MVC   STARTDAY,4(R1)      GET START DAY                                
         CLI   STARTDAY,7          IF INVALID,                                  
         BNH   *+8                                                              
         MVI   STARTDAY,0           FORCE TO USE DEFAULT INSTEAD                
                                                                                
         LM    R2,R5,0(R1)                                                      
         LR    R9,R1                                                            
         LA    R3,0(R3)            CLEAR HIGH-ORDER-BYTE                        
         STM   R3,R5,GETDAY                                                     
         LA    R2,0(R2)            CLEAR HIGH-ORDER-BYTE                        
         CLI   ACTION,C'D'                                                      
         BNE   NSW2                                                             
         SPACE 1                                                                
         MVC   BYEAR(2),0(R2)                                                   
         GOTO1 DATCON,PARA,(3,(R2)),DUB                                         
         MVC   DATE(2),DUB         SET EBCDIC YEAR                              
         B     NSW4                                                             
*                                                                               
NSW2     DS    0H                                                               
         GOTO1 ADDAY,PARA,0(R2),DATE,0  FIX Y2K PROBLEM--ADD 0 TO DATE          
*&&DO                                                                           
         MVC   DATE,0(R2)                                                       
*&&                                                                             
         GOTO1 GETDAY,PARA,DATE,DUB     FIND SATURDAY DATE                      
         ZIC   R3,PARA                                                          
         ZICM  R4,STARTDAY,(1)          USER START DAY OVERRIDE                 
         BNZ   *+8                       IF GIVEN                               
         LA    R4,6                                                             
         SR    R4,R3                                                            
         BNP   *+8                                                              
         SH    R4,=H'7'                                                         
         GOTO1 ADDAY,PARA,DATE,DUB,(R4)                                         
         MVC   DATE,DUB                                                         
         MVC   SVDATE,DATE               AND SAVE                               
         GOTO1 ADDAY,PARA,DUB,DATE,6    GET FRIDAY FOR YEAR                     
         GOTO1 DATCON,PARA,DATE,(3,DUB)                                         
         MVC   4(1,R9),DUB         MOVE YEAR                                    
*                                                                               
NSW4     MVC   DATE+2(4),=C'0101'  FIND DAY NUMBER OF JAN01                     
         GOTO1 GETDAY,PARA,DATE,DUB                                             
         ZIC   R3,PARA             DAY 1-7                                      
         ZICM  R4,STARTDAY,(1)     USER START DAY OVERRIDE                      
         BNZ   *+8                  IF GIVEN                                    
         LA    R4,6                                                             
         SR    R4,R3                                                            
         BZ    NSW6                BACK UP TO PREVIOUS SATURDAY                 
         BM    *+8                                                              
         SH    R4,=H'7'            MAKE IT -1 TO -6                             
         BM    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ADDAY,PARA,DATE,DUB,(R4)                                         
         MVC   DATE,DUB                                                         
*                                                                               
NSW6     CLI   ACTION,C'D'         CONVERT FROM WEEK# TO DATE?                  
         BE    NSWDATE              YEP                                         
*                                                                               
*------------------------- CONVERT TO WEEK# --------------------------*         
*                                                                               
         SR    R3,R3                                                            
NSWW2    LA    R3,1(R3)            INCREMENT WEEK#                              
         CLC   DATE,SVDATE         REACHED TARGET WEEK YET?                     
         BNL   NSWW4                YES                                         
         BAS   RE,NEXTWEEK          NO, GET NEXT WEEK                           
         B     NSWW2                                                            
NSWW4    STC   R3,ACTWEEK                                                       
         STC   R3,0(R9)                                                         
         B     NSWX                                                             
*                                                                               
*-------------------------- CONVERT TO DATE --------------------------*         
*                                                                               
NSWDATE  ZIC   R3,BWEEK            COUNTDOWN WITH WEEK NO.                      
NSWD2    BCT   R3,*+8                                                           
         B     NSWD4                                                            
         BAS   RE,NEXTWEEK                                                      
         B     NSWD2                                                            
NSWD4    LA    RE,DATE                                                          
         ST    RE,0(R9)            STORE A(DATE) IN PARAM LIST                  
         B     NSWX                                                             
*                                                                               
NSWX     XIT1                                                                   
         SPACE 2                                                                
***********************************************************************         
*===================== GET NEXT SATURDAY'S DATE ======================*         
*                                                                               
NEXTWEEK ST    RE,SAVERE                                                        
         GOTO1 ADDAY,PARA,DATE,DUB,7                                            
         MVC   DATE,DUB                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
***********************************************************************         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
NSWD     DSECT                                                                  
DUB      DS    D                                                                
PARA     DS    6F                                                               
GETDAY   DS    V                                                                
ADDAY    DS    V                                                                
DATCON   DS    V                                                                
SAVERE   DS    A                   SAVE REG 14                                  
SVDATE   DS    CL6                                                              
DATE     DS    CL6                                                              
ACTION   DS    CL1                                                              
ACTWEEK  DS    CL1                                                              
WEEKTYPE DS    CL1                                                              
BYEAR    DS    XL1                 BINARY YEAR                                  
BWEEK    DS    XL1                 BINARY WEEK                                  
STARTDAY DS    XL1                 USER OVERRIDE START DAY OF WEEK              
NSWDX    EQU   *                                                                
         SPACE 1                                                                
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021DENSIWEEK 05/01/02'                                      
         END                                                                    
