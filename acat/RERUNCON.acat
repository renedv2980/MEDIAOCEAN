*          DATA SET RERUNCON   AT LEVEL 017 AS OF 12/12/05                      
*CATALP RERUNCON                                                                
         TITLE 'RERUNCON - MODULE TO CONTROL REPORTER RUN'                      
*                                                                               
*********************************************************************           
*                                                                   *           
*        REREPRUNM --- CONTROLLER FOR REPORTER RUN                  *           
*                                                                   *           
*  CATALOG DATA SET = RERUNM(A)                                     *           
*                                                                   *           
* ----------------------------------------------------------------- *           
* UPDATE HISTORY:                                                   *           
*                                                                   *           
* -----/-- (---) --- HISTORY LOST                                   *           
*                                                                   *           
* OCT25/89 (MRR) --- SET MASTER'S FILE ID FOR CONVERSION FROM       *           
*                     MASTCON TO MASTER                             *           
*                                                                   *           
* 02/09/90  PJS  --- MULTIPLE CARD REQUESTS -                       *           
*                    - CONTINUATION A '+' IN COLUMN 80              *           
*                                                                   *           
*                    CHANGED MASTER DSECT FROM DDREPMASTD           *           
*                      TO DDMASTD.  DISABLED TIMER CODE.            *           
*                                                                   *           
*                    PASS EXTRA ADDRESS LIST TO REQUESTS            *           
*                                                                   *           
* MAY15/90 (MRR) --- >ADD DOWNLOADING SUPPORT                       *           
*                                                                   *           
* DEC13/90 (MRR) --- >SWITCH WORKAREA FROM 'RESAVE' TO 'REGSAVE'    *           
*                                                                   *           
* DEC20/90 (MRR) --- >ADD A(SUBSIDIARY REP LIST) TO PASS TO SUB-    *           
*                     MODULES                                       *           
* OCT10/91 (BU ) --- >IMPLEMENT SPACEND VIA DISK FILE - CHANGES     *           
*                     BEGUN WITH LEVEL 27.                          *           
*                                                                   *           
* DEC19/91 (MRR) --- >SET RCSOON FROM DDMAST                        *           
*                    >OPEN AND CLOSE APPLMSG FILE, ADDR IN XADDR    *           
*                    >ADD XADDR ENTRY FOR VXREQTAB                  *           
*                                                                   *           
* MAR03/94 (BU ) --- >SUPPRESS LOGOS FOR 'DOWNLOAD' REQUESTS WHEN   *           
*                     RUNMODE NOT = SINGLE                          *           
*                                                                   *           
* MAR04/95 (MHER)--- >OPEN RECOVERY FILE AS DESCRIBED IN OPEN       *           
*                                                                   *           
* FEB21/02 (BU ) --- >SET 'CLO/ERR' ON ERROR ABORT                  *           
*                                                                   *           
* DEC12/05 (BU ) --- >ADD 'CTFILE' TO 'OPEN' LIST                   *           
*                                                                   *           
*                                                                   *           
*                                                                   *           
*                     ***  END TOMBSTONE  ***                       *           
*********************************************************************           
*                                                                               
RERUNCON CSECT                                                                  
RERUNM   EQU   *                                                                
         ENTRY RERUNM              FOR COMPATIBILITY - OLD CSECT NAME           
         PRINT NOGEN                                                            
         NBASE 0,*RUNCON*,=V(REGSAVE),RR=R2                                     
         L     RA,=V(REWORKC)                                                   
         USING WORKD,RA                                                         
         MVI   RCTIMER,C'N'                                                     
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         ST    R2,RELO                                                          
*                                                                               
         LA    RF,XADDR            PASS EXTRA ADDRESS LIST TO REPORT            
         ST    RF,VXADDR                                                        
*                                                                               
         USING VXADDRD,RF                                                       
         LA    RE,APPLMSG                                                       
         ST    RE,VXDCBAPP                                                      
         DROP  RF                                                               
*                                                                               
         OPEN  (APPLMSG,(OUTPUT))                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         EJECT                                                                  
* INITIAL CONTROL CARDS *                                                       
         SPACE 2                                                                
READCARD MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
* PRESET TO CURRENT DATE *                                                      
         GOTO1 DATCON,DMCB,(5,0),(10,RCDATE)                                    
         MVC   RCUPSI,MCUPSI                                                    
*&&DO*&& MVC   RCJOB,24(R1)                                                     
         SPACE 1                                                                
*&&OS                                                                           
         LA    R2,DUB                                                           
         EXTRACT (R2),FIELDS=TIOT                                               
         MVC   RCJOB,0(R2)                                                      
*&&                                                                             
         SPACE 1                                                                
*                                                                               
*- TELL DDMASTER ABOUT POSSIBLE CONTINUATION CARDS                              
*                                                                               
*  A '+' IN COLUMN 80 = CONTINUATION ON NEXT CARD.                              
*                                                                               
         MVI   MCRQCOL,80                                                       
         MVI   MCRQCHAR,C'+'                                                    
*                                                                               
         GOTO1 =V(RUNSTART)                                                     
*&&OS*&& MVC   SPECS,MCAPHAS1      SET 01 LOAD ADDRESS                          
*&&OS*&& MVC   APPLIC,MCAPHAS2     SET 02 LOAD ADDRESS                          
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCREPFL,MCUSER                                                   
         MVC   RCRQFILT,MCPROG                                                  
         MVC   RCSTNUM,MCSTART                                                  
         MVC   RCENNUM,MCEND                                                    
         MVC   RCSPCEND,MCSPCEND   SPACEND OPTION: U/F                          
         CLC   MCREQCOD,SPACES                                                  
         BE    *+10                                                             
         MVC   RCRQFILT,MCREQCOD                                                
         L     RE,=V(RCDOWNLD)                                                  
         MVC   0(1,RE),MCDOWNLD                                                 
         MVI   RCSOON,0                                                         
         OC    MCAMCORE,MCAMCORE                                                
         BZ    *+8                                                              
         MVI   RCSOON,1                                                         
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),MCIDSENO                                                 
         SPACE 2                                                                
         MVI   RCFLONLY,C'Y'       GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 =V(REREPORT),RR=RELO                                             
         MVI   RCFLONLY,C'N'                                                    
         EJECT                                                                  
*==================================================================*            
* OPEN FILE ROUTINES                                               *            
* MASTER WAS CHANGED 04MAR96 TO SET RECOVERY DEFAULT TO Y FOR REP  *            
* NOTE THAT RECOVERY WILL NOT BE OPENED IF NEITHER THE DIRECTORY   *            
* OR FILE ARE OPENED FOR UPDATE                                    *            
* SSB+3 IS SET TO 0 TO SUPPRESS RECOVERY COPIES                    *            
* PROGRAMS THAT NEED COPIES SHOULD SET SSB+3 TO X'08'  MH 04MAR96  *            
*==================================================================*            
         SPACE 2                                                                
OPEN2    CLI   RCWRITE,C'N'        TEST WRITE=NO                                
         BE    OPEN10              THEN NO UPDATES/NO RECOVERY                  
*                                                                               
         CLI   FCUPDIR,C'Y'                                                     
         BNE   *+8                                                              
         MVI   FREPDIR,C'U'                                                     
*                                                                               
         CLI   FCUPFILE,C'Y'                                                    
         BNE   *+8                                                              
         MVI   FREPFIL,C'U'                                                     
*                                                                               
         CLI   MCRECOVR,C'N'       IS RECOVERY SUPPRESSED                       
         BE    OPEN10              YES -                                        
*                                                                               
         CLI   FREPDIR,C'U'        IS DIR SET FOR UPDATE                        
         BE    OPEN6               YES                                          
         CLI   FREPFIL,C'U'        IS FIL SET FOR UPDATE                        
         BE    OPEN6               YES                                          
         B     OPEN10              SO WHAT'S THE POINT ?                        
*                                                                               
OPEN6    MVI   FREPRCVR,C'U'       SET TO OPEN RECOVERY                         
*                                                                               
OPEN10   L     R2,FILEC                                                         
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'REP  ',FILELIST,(R2)                    
*                                                                               
         L     R2,=V(SSB)                                                       
         CLI   FREPRCVR,C'U'                                                    
         BNE   *+8                                                              
         MVI   3(R2),0             SUPPRESS COPIES                              
*                                                                               
         MVC   P(8),=C'RECOVER='                                                
         MVC   P+8(1),MCRECOVR                                                  
         MVC   P+10(14),=C'FILE OPEN LIST'                                      
         MVC   P+25(FILELISX-FILELIST),FILELIST                                 
         L     R1,=A(APPLMSG)                                                   
         LA    R0,P                                                             
         PUT   (1),(0)                                                          
*                                                                               
         GOTO1 FILCON,DMCB,0       GET REP RECORD INTO CORE                     
         GOTO1 =V(REOPEN)                                                       
*                                                                               
         L     R2,=V(SSB)                                                       
         MVI   2(R2),X'FF'         SET OFFLINE FLAG IN SSB                      
         B     OPENX                                                            
         SPACE 1                                                                
FILELIST DS    0H                                                               
FREPDIR  DC    CL8'NREPDIR'                                                     
FREPFIL  DC    CL8'NREPFILE'                                                    
FREPRCVR DC    CL8'XREPRCVR'       DEFAULT IS TO EXCLUDE FROM LIST              
FCTFILE  DC    CL8'NCTFILE'                                                     
FILELISX DC    C'X '                                                            
         EJECT                                                                  
* CONTROL DIVIDERS AND REQUESTS *                                               
         SPACE 2                                                                
OPENX    MVC   P,SPACES                                                         
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         L     RE,=V(RCDOWNLD)     DOWNLOAD REQUEST?                            
         ZICM  RF,0(RE),1                                                       
         BNZ   GOREQS              YES - NO LOGOS AT ALL                        
         LA    RF,MCREMOTE                                                      
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY   NON-ZERO IF DIRECT REPORT                    
         BNZ   GOREQS              NO LOGOS IF DIRECT REPORT                    
         DROP  RF                                                               
         GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOREQS   BAS   RE,ANYLINUP                                                      
         BAS   RE,ANYTIMER                                                      
         GOTO1 FILCON,DMCB,1       GO AND HANDLE REQUESTS                       
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         CLI   RCTIMER,C'Y'                                                     
         BNE   FINALISE                                                         
         GOTO1 =V(TIMER),DMCB,0                                                 
         SPACE 2                                                                
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         L     RE,=V(RCDOWNLD)     DOWNLOAD REQUEST?                            
         ZICM  RF,0(RE),1                                                       
         BNZ   CLOSE1              YES - NO LOGOS AT ALL                        
         LA    RF,MCREMOTE                                                      
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY   NON-ZERO IF DIRECT REPORT                    
         BNZ   CLOSE1              NO LOGOS IF DIRECT REPORT                    
         DROP  RF                                                               
         GOTO1 LOGO,DMCB,LOGOC                                                  
         EJECT                                                                  
* CLOSE FILES AND END JOB *                                                     
         SPACE 2                                                                
CLOSE1   DS    0H                                                               
*&&DO                                                                           
         L     R1,ADIN                                                          
         CLI   INFILE,C'Y'                                                      
         BNE   CLOSE2                                                           
         CLOSER (R1)                                                            
         SPACE 2                                                                
CLOSE2   L     R1,ADOUT                                                         
         CLI   OUTFILE,C'Y'                                                     
         BNE   CLOSE4                                                           
         L     R3,ADIO             WRITE OUT AN FF RECORD                       
*                                  TO SORT TO BOTTOM FOR EOF                    
         MVC   0(12,R3),=X'000C0000FFFFFFFFFFFFFFFF'                            
         PUT   (R1),(R3)                                                        
         CLOSER (R1)                                                            
         SPACE 2                                                                
CLOSE4   L     R1,ADWORK                                                        
         CLI   WORKFILE,C'Y'                                                    
         BNE   GOEND                                                            
         CLOSER (R1)                                                            
*&&                                                                             
         SPACE 2                                                                
GOEND    DS    0H                                                               
***      GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         GOTO1 ,DMCB,=C'CLOSE'          SET CLOSE/NO ERROR                      
         CP    MCNDUMPS,=P'0'           DUMPS?                                  
         BE    GOEND020                 NO                                      
         GOTO1 ,DMCB,=C'CLO/ERR'        YES - SET CLOSE/ERROR                   
GOEND020 EQU   *                                                                
         GOTO1 PRINT                    ERROR/NO ERROR SET ABOVE                
*                                                                               
         CLOSE (APPLMSG)                                                        
*                                                                               
         CP    MCNDUMPS,=P'0'                                                   
         BE    *+18                                                             
         OC    MCREMPQK,MCREMPQK   IS IT A SOON JOB?                            
         BZ    *+8                 NO                                           
         LA    R2,1                                                             
*                                                                               
         LTR   R2,R2                                                            
         BZ    NORMEND                                                          
*&&DO*&& CANCEL                                                                 
*&&OS*&& ESTAE 0                                                                
*&&OS*&& ABEND 999                                                              
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         XBASE                                                                  
         EJECT                                                                  
*                   LINE-UP PATTERN CONTROL                                     
         SPACE 2                                                                
ANYLINUP NTR1                                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   LINEND                                                           
         MVC   P,SPACES                                                         
         GOTO1 PRINT,DMCB,P,=C'BC01'                                            
         GOTO1 PRINT,DMCB,P,=C'BL09'                                            
         LA    R2,2                                                             
         SPACE 2                                                                
LINLOOP  MVI   P,C'X'              PRINT 109 X'S ON HEADLINES 10 & 11           
         MVC   P+1(108),P                                                       
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,LINLOOP                                                       
         SPACE 2                                                                
LINEND   XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO FIX V-TYPES IF TIMER IS ON                            
         SPACE 2                                                                
ANYTIMER NTR1                                                                   
****     CLI   RCTIMER,C'Y'                                                     
****     BNE   TIMEX                                                            
****     MVC   FILCON(4),VA                                                     
****     MVC   APPLIC(4),VB                                                     
****     MVC   VALUEMON(4),VC                                                   
****     MVC   ROLLER(4),VD                                                     
****     MVC   DATAMGR(4),VE                                                    
****     MVC   ADDAY(4),VF                                                      
****     MVC   GETBROAD(4),VG                                                   
****     MVC   DATCON(4),VH                                                     
****     MVC   REPORT(4),VI                                                     
****     MVC   GETDAY(4),VJ                                                     
         SPACE 2                                                                
TIMEX    XIT1                                                                   
         SPACE 2                                                                
*A       DC    AL1(1),VL3(TIMER)                                                
*B       DC    AL1(2),VL3(TIMER)                                                
*C       DC    AL1(3),VL3(TIMER)                                                
*D       DC    AL1(4),VL3(TIMER)                                                
*E       DC    AL1(5),VL3(TIMER)                                                
*F       DC    AL1(6),VL3(TIMER)                                                
*G       DC    AL1(7),VL3(TIMER)                                                
*H       DC    AL1(8),VL3(TIMER)                                                
*I       DC    AL1(9),VL3(TIMER)                                                
*J       DC    AL1(10),VL3(TIMER)                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        CONSTANTS                                                              
*                                                                               
PHASE1   DC    C'RENN01  '                                                      
PHASE2   DC    C'RENN02  '                                                      
         SPACE 2                                                                
         DS    0F                                                               
APPLMSG  DCB   DDNAME=APPLMSG,DSORG=PS,RECFM=FB,                       X        
               LRECL=80,BLKSIZE=80,MACRF=PM                                     
         SPACE 2                                                                
         DS    0F                                                               
RELO     DS    F                                                                
         EJECT                                                                  
XADDR    DS    0F                  COVER WITH REXADDRD                          
*                                                                               
         DC    V(RQSTNUM)          # REQ CARDS. SEE REREPFILM.                  
         DC    V(RQSTCARD)         MULTI-CARD AREA.  "                          
*                                                                               
         DC    V(RCDOWNLD)         DOWNLOADING FLAG                             
         DC    V(DOWNDONE)         DOWNLOAD DATA PRODUCED                       
*                                   ZERO IS NO                                  
*                                   ONE IS YES                                  
         DC    V(DOWNDEF)          DOWNLOADING CONTROL LIST                     
*                                                                               
         DC    V(REPSREP)          SUBSIDIARY REP LIST                          
*                                                                               
         DC    F'0'                A(APPLMSG DCB)                               
*                                                                               
         DC    F'0'                A(REQUEST TABLE TO USE IN RERQRP AND         
*                                                            REREPORT)          
*                                                                               
         EJECT                                                                  
       ++INCLUDE DDLOGOD                                                        
         EJECT                                                                  
       ++INCLUDE DDMASTD                                                        
         EJECT                                                                  
       ++INCLUDE REREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE REXADDRD                                                       
         EJECT                                                                  
       ++INCLUDE DDREMOTED                                                      
         EJECT                                                                  
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017RERUNCON  12/12/05'                                      
         END                                                                    
