*          DATA SET ACWRIDATE  AT LEVEL 003 AS OF 05/01/02                      
*CATALP WRIDATE                                                                 
         TITLE 'WRIDATE - BUILD MONTH TABLES'                                   
WRIDATE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRIDEND-WRID,*WRIDATE,CLEAR=YES,RR=RE                            
         USING WRID,RC                                                          
*                                  PARAMETER 1 BYTE 1 FISCAL                    
*                                              BYTES 2-4 A(START-END)           
*                                  PARAMETER 2 A(800 BYTE DATE AREA)            
*                                  PARAMETER 3 A(COMFACS)                       
         LR    R9,R1                                                            
         L     R2,8(R9)                                                         
         USING COMFACSD,R2                                                      
         MVC   ADDAY,CADDAY                                                     
         MVC   DATCON,CDATCON                                                   
         L     R2,0(R9)                                                         
         MVC   QSTART(12),0(R2)                                                 
         SPACE 1                                                                
         MVC   WORK(4),QSTART      MAX 12 MONTHS PERIOD                         
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+6,F'1'    PLUS ONE YEAR              
         MVC   WORK(6),WORK+6                                                   
         GOTO1 ADDAY,DMCB,(C'D',WORK),WORK+6,F'-1'   LESS ONE DAY               
         CLC   QEND,WORK+6                                                      
         BL    *+10                                                             
         MVC   QEND,WORK+6                                                      
         SPACE 1                                                                
         MVC   FISCMON,0(R9)                                                    
         BAS   RE,BUILDMON                                                      
         BAS   RE,SETDBASE                                                      
         BAS   RE,GENDATES                                                      
         B     XIT                                                              
         EJECT                                                                  
*              SET UP DATE BASE                                                 
         SPACE 3                                                                
SETDBASE NTR1                                                                   
         MVI   YTDSW,C'N'                                                       
         MVC   WORK(4),QSTART      BUILD PERIOD LIST                            
         BAS   RE,LUKUPMON                                                      
         MVC   PERIOD(1),WORK                                                   
         MVI   PERIOD+1,24                                                      
         LA    R2,PERIOD                                                        
         BAS   RE,SB10                                                          
         SPACE 1                                                                
         LA    R1,SBLIST           DEVELOP YTD EXPRESSIONS                      
         LA    R0,12                                                            
         SPACE 1                                                                
SB2      CLC   0(1,R1),FISCMON     CONVERT 1-9 A-C  TO 01-12                    
         BE    SB4                                                              
         LA    R1,3(R1)                                                         
         BCT   R0,SB2                                                           
         MVC   WORK+2(2),=C'01'                                                 
         B     SB6                                                              
         SPACE 1                                                                
SB4      MVC   WORK+2(2),1(R1)                                                  
         SPACE 1                                                                
SB6      MVC   WORK(2),QSTART      NOW DERIVE START OF FISCAL YEAR              
         CLC   WORK(4),QSTART                                                   
         BNH   SB8                                                              
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+6,F'-1'  BACKUP 1 YEAR               
         MVC   WORK(6),WORK+6                                                   
         SPACE 1                                                                
SB8      BAS   RE,LUKUPMON                                                      
         MVC   YTD(1),WORK                                                      
         MVC   FISCAL(1),WORK                                                   
         MVC   WORK(4),QEND                                                     
         BAS   RE,LUKUPMON                                                      
         MVC   YTD+1(1),WORK                                                    
         MVC   FISCAL+1(1),WORK                                                 
         LA    R2,YTD                                                           
         MVI   YTDSW,C'Y'                                                       
         BAS   RE,SB10                                                          
         MVI   YTDSW,C'N'                                                       
         SPACE 1                                                                
         MVI   ROLLING,13          DEVELOP ROLLING                              
         MVI   ROLLING+1,24                                                     
         LA    R2,ROLLING                                                       
         BAS   RE,SB10                                                          
         SPACE 1                                                                
         ZIC   R1,FISCAL           DEVELOP FISCAL                               
         LA    R1,11(R1)                                                        
         STC   R1,FISCAL+1                                                      
         LA    R2,FISCAL                                                        
         BAS   RE,SB10                                                          
         B     XIT                                                              
         SPACE 1                                                                
SBLIST   DC    C'101202303404505606'                                            
         DC    C'707808909A10B11C12'                                            
         EJECT                                                                  
*              NOW EXPAND START-END FOR MONTH/QUARTER/SIX PHRASES               
         SPACE 3                                                                
SB10     NTR1                                                                   
         BAS   RE,SB12             FIRST HANDLE FOR THIS YEAR                   
         SPACE 1                                                                
         ZIC   R1,0(R2)            THEN LAST YEAR                               
         SH    R1,=H'12'                                                        
         STC   R1,160(R2)                                                       
         ZIC   R1,1(R2)                                                         
         SH    R1,=H'12'                                                        
         STC   R1,161(R2)                                                       
         LA    R2,160(R2)                                                       
         BAS   RE,SB12                                                          
         SPACE 1                                                                
         ZIC   R1,0(R2)            THEN NEXT YEAR (FROM LAST YEAR)              
         AH    R1,=H'24'                                                        
         STC   R1,160(R2)                                                       
         ZIC   R1,1(R2)                                                         
         AH    R1,=H'24'                                                        
         STC   R1,161(R2)                                                       
         LA    R2,160(R2)                                                       
         BAS   RE,SB12                                                          
         B     XIT                                                              
         SPACE 1                                                                
SB12     NTR1                                                                   
         LA    R3,2(R2)                                                         
         LA    R4,SBRELS                                                        
         LA    R5,18                                                            
         XC    0(38,R3),0(R3)                                                   
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         SPACE 1                                                                
SB14     IC    R1,0(R2)            START MON NO                                 
         IC    R0,0(R4)                  +START RELATIVE                        
         CLI   YTDSW,C'Y'          THE START FOR YTD EXPRESSIONS                
         BNE   *+6                       IS ALWAYS THE SAME                     
         SR    R0,R0                                                            
         AR    R1,R0                                                            
         STC   R1,0(R3)                                                         
         SPACE 1                                                                
         IC    R1,0(R2)            START MON NO                                 
         IC    R0,1(R4)                  +END RELATIVE                          
         AR    R1,R0                                                            
         STC   R1,1(R3)                                                         
         SPACE 1                                                                
SB16     LA    R3,2(R3)                                                         
         LA    R4,2(R4)                                                         
         BCT   R5,SB14                                                          
         B     XIT                                                              
         SPACE 1                                                                
SBRELS   DC    AL1(0,0,1,1,2,2,3,3,4,4,5,5)            M1-M6                    
         DC    AL1(6,6,7,7,8,8,9,9,10,10,11,11)        M7-M12                   
         DC    AL1(0,2,3,5,6,8,9,11)                   Q1,Q4                    
         DC    AL1(0,5,6,11)                           61,62                    
         EJECT                                                                  
*              ROUTINE TO BUILD LIST OF MONTHS                                  
         SPACE 3                                                                
BUILDMON NTR1                                                                   
         MVC   WORK(4),QEND        GET START MONTH                              
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 ADDAY,DMCB,(C'M',WORK),WORK+6,F'-23'                             
         LA    R3,48               NUMBER OF MONTHS                             
         LA    R2,MONLIST          POSITION TO START OF MONTH LIST              
         SPACE 1                                                                
*                                                                               
BM3      GOTO1 DATCON,DMCB,(0,WORK+6),(0,0(R2))    EBCDIC YYMM                  
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,WORK+20)  PACKED YYMM                  
         MVC   4(2,R2),WORK+20                                                  
         MVC   WORK(6),WORK+6                                                   
         GOTO1 ADDAY,DMCB,(C'M',WORK),WORK+6,F'1'   NEXT MONTH                  
         LA    R2,6(R2)                                                         
         BCT   R3,BM3                                                           
         B     XIT                                                              
         SPACE 1                                                                
LUKUPMON NTR1                                                                   
         LA    R2,MONLIST                                                       
         LA    R3,1                                                             
         LA    R4,48                                                            
         SPACE 1                                                                
LM2      CLC   0(4,R2),WORK        WORK CONTAINS YYMM                           
         BE    LM4                                                              
         CLC   4(2,R2),WORK                   OR YM (PACKED)                    
         BE    LM4                                                              
         LA    R2,6(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,LM2                                                           
         MVI   WORK,0                                                           
         LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     XIT                                                              
         SPACE 1                                                                
LM4      STC   R3,WORK                                                          
         B     XIT                                                              
         EJECT                                                                  
*              GENERATE DATES FOR USER                                          
         SPACE 3                                                                
GENDATES NTR1                                                                   
         LA    R2,PERIOD                                                        
         L     R3,4(R9)                                                         
         LA    R4,480              480 DATE NUMBERS TO CONVERT                  
         SPACE 1                                                                
GEND2    XC    0(2,R3),0(R3)                                                    
         ZIC   R1,0(R2)            PICK UP DATE NUMBER                          
         LTR   R1,R1                                                            
         BZ    GEND4                                                            
         BCTR  R1,0                                                             
         MH    R1,=H'6'                                                         
         LA    R1,MONLIST(R1)      DISPLACE INTO MONLIST                        
         MVC   0(2,R3),4(R1)       AND PICK UP THE S/E PWOS                     
         SPACE 1                                                                
GEND4    LA    R2,1(R2)                                                         
         LA    R3,2(R3)                                                         
         BCT   R4,GEND2                                                         
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              MODULE DSECTS                                                    
         SPACE 3                                                                
WRID     DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
DMCB     DS    6F                                                               
FULL     DS    F                                                                
ADDAY    DS    V                                                                
DATCON   DS    V                                                                
QSTART   DS    CL6                                                              
QEND     DS    CL6                                                              
FISCMON  DS    XL1                                                              
YTDSW    DS    CL1                                                              
         DS    0D                                                               
MONLIST  DS    CL288               48 MONTHS YYMM (EBCDIC) YM(PWOS)             
PERIOD   DS    CL40                20 START/END MONTH NUMBER PAIRS              
YTD      DS    CL40                                                             
ROLLING  DS    CL40                                                             
FISCAL   DS    CL40                                                             
LASTPER  DS    CL40                                                             
LASTYTD  DS    CL40                                                             
LASTROLL DS    CL40                                                             
LASTFISC DS    CL40                                                             
NEXTPER  DS    CL40                                                             
NEXTYTD  DS    CL40                                                             
NEXTROLL DS    CL40                                                             
NEXTFISC DS    CL40                                                             
WRIDEND  DS    0D                                                               
         SPACE 1                                                                
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACWRIDATE 05/01/02'                                      
         END                                                                    
