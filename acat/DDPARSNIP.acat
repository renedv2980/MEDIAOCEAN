*          DATA SET DDPARSNIP  AT LEVEL 073 AS OF 05/01/02                      
*CATALP PARSNIP                                                                 
         TITLE 'PARSNIP - GENERALIZED EXPRESSION PARSER'                        
         SPACE 3                                                                
*        PARAMETER LIST                                                         
         SPACE 1                                                                
*        PARAM1  BYTE  0      L'INPUT, 0=SRCEEN HEADER                          
*                BYTES 1-3    A(INPUT)                                          
         SPACE 1                                                                
*        PARAM2  BYTE  0      ON INPUT, MAX COMPONENTS  (0=NO LIMIT)            
*                             ON OUTPUT, ACTUAL NUMBER OF COMPONENTS            
*                BYTES 1-3    A(OUTPUT TABLE)                                   
         SPACE 1                                                                
*        PARAM3  BYTE  0      ON INPUT, CONTROL BYTE  (SEE DDPARSNIPD)          
*                             ON OUTPUT, ERROR CODE OR NULL                     
*                BYTES 1-3    A(SEPARATOR OVERRIDES) OR NULL                    
         SPACE 1                                                                
PARSNIP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**PARS**                                             
         USING WORKD,RC            RC=A(W/S)                                    
         MVC   PARMTAB,0(R1)                                                    
         ST    R1,SAVEDR1                                                       
         ST    RD,SAVEDRD                                                       
         EJECT                                                                  
START    XC    TEXTPTR,TEXTPTR     INITIALIZE POINTER TO NEXT TOKEN             
         OC    TEXTLEN,TEXTLEN                                                  
         BNZ   BLDTRT                                                           
         SR    R2,R2               IF TEXTLEN = 0 GET LENGTH FROM               
         ICM   R2,7,ATEXT              SCREEN HEADER                            
         MVC   TEXTLEN,5(R2)                                                    
         LA    R2,8(R2)            BUMP A(TEXT) BY 8                            
         STCM  R2,7,ATEXT                                                       
*                                                                               
BLDTRT   LA    R2,DEFSEPS          USE EITHER THE DEFAULT SEPARATORS            
         OC    SEPLIST,SEPLIST                                                  
         BZ    *+10                                                             
         SR    R2,R2               OR THE USER DEFINED SEPARATORS               
         ICM   R2,7,SEPLIST                                                     
*                                                                               
         XC    TRTAB,TRTAB         TO SET UP TRANSLATION TABLE                  
         TM    CONTROL,PSNNPARQ    TEST TO TREAT () AS                          
         BNZ   *+12                NORMAL CHARACTERS                            
         MVI   TRTAB+77,OPENP      NO, SET IN TR TABLE                          
         MVI   TRTAB+93,CLOSEP                                                  
*                                                                               
         LA    R1,DIVIDER          ORDER OF SEPARATOR DEFINITIONS IS            
         LA    R5,4                    DIV,FSEP,VSEP,ASEP                       
*                                                                               
BLDT10   STC   R1,MASK                                                          
         ZIC   R3,0(R2)            EXTRACT NUMBER OF SEPARATORS FOR             
         LA    R2,1(R2)                THIS TYPE                                
         LTR   R3,R3                                                            
         BZ    BLDT30                                                           
*                                                                               
BLDT20   ZIC   R4,0(R2)            SET APPROPRIATE BIT IN APPROPRIATE           
         LA    R2,1(R2)                BYTE IN TRTAB                            
*                                                                               
         CLI   MASK,DIVIDER        ERROR IF C' ' FOR DIVIDER                    
         BNE   BLDT25                                                           
         C     R4,=X'00000040'     C' ' = X'40'                                 
         BE    ERROR                                                            
*                                                                               
BLDT25   LA    RF,TRTAB(R4)                                                     
         OC    0(1,RF),MASK                                                     
         BCT   R3,BLDT20                                                        
*                                                                               
BLDT30   SRL   R1,1                SHIFT BIT FLAG TO NEXT SEPARATOR             
         BCT   R5,BLDT10                                                        
         B     MAIN                                                             
*                                                                               
DEFSEPS  DC    X'017E016B016B016B' DEFAULT SEPARATORS ARE =,,,                  
         EJECT                                                                  
*  THIS CHUNK OF CODE WILL RUN A TRACE OF THE LEXICAL ANALYZER                  
*  EACH TABLE ENTRY WILL CONTAIN A TOKEN INSTEAD OF A COMPNENT                  
*                                                                               
MAIN     CLC   TRACEOPT,=C'TRAC'   TRACE OPTION FROM FOURTH PARM                
         BNE   BEGIN                                                            
         SR    R4,R4                                                            
         SR    R6,R6               POINT TO BEGINNING OF OUTPUT AREA            
         ICM   R6,7,AOUTPUT                                                     
         BAS   RE,LEXAN                                                         
*                                                                               
TRACE    OC    ID,ID                                                            
         BZ    TRACEX                                                           
         MVC   0(4,R6),ATOKEN      A(TOKEN)                                     
         MVC   4(1,R6),ID          ID                                           
         MVC   5(1,R6),TOKENLEN    TOKENLEN                                     
         L     R5,ATOKEN                                                        
         ZIC   RF,TOKENLEN                                                      
         CLI   ID,COMP                                                          
         BE    *+8                                                              
         LA    RF,1                                                             
         C     RF,=F'24'                                                        
         BNH   *+8                                                              
         LA    RF,24                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R6),0(R5)       TOKEN TEXT                                   
         LA    R6,32(R6)                                                        
         LA    R4,1(R4)                                                         
         BAS   RE,LEXAN                                                         
         B     TRACE                                                            
*                                                                               
TRACEX   STC   R4,NUMENTS                                                       
         B     EXIT                                                             
         EJECT                                                                  
BEGIN    DS    0H                                                               
         SR    R6,R6               POINT TO BEGINNING OF OUTPUT AREA            
         ICM   R6,7,AOUTPUT                                                     
         USING PSND,R6                                                          
*                                                                               
         ST    R6,LASTENT                                                       
         XC    0(PSNL,R6),0(R6)                                                 
         MVI   NUMENTS,1                                                        
*                                                                               
         BAS   RE,LEXAN            GET FIRST TOKEN                              
         BAS   RE,DOEXPR           AND PARSE...                                 
         CLI   ID,0                                                             
         BNE   ERROR                                                            
         B     EXIT                                                             
*                                                                               
DOEXPR   NTR1                      EXPR "EXPRESSION" ->                         
         BAS   RE,DOFEXPR              FEXPR | MFLDS                            
         BAS   RE,DOMFLDS                                                       
         B     XIT                                                              
*                                                                               
DOFEXPR  NTR1                      FEXPR "FIELD EXPRESSION" ->                  
         CLI   ID,0                    COMP | ALIST | RSIDE                     
         BE    ERROR               OR  NULL                                     
         MVI   PSNTAG,PSNFLDQ                                                   
         CLI   ID,COMP                                                          
         BE    DOFE10                                                           
         TM    CONTROL,PSNNONLQ                                                 
         BO    ERROR               ERROR IF NO NULL FIELDS ALLOWED              
         B     XIT                                                              
*                                                                               
DOFE10   MVI   MATCHARG,COMP                                                    
         BAS   RE,MATCH                                                         
         BAS   RE,DOALIST                                                       
         BAS   RE,DORSIDE                                                       
         B     XIT                                                              
*                                                                               
DOMFLDS  NTR1                      MFLDS "MORE FIELDS" ->                       
         TM    ID,FLDSEP               FLDSEP | FEXPR | MFLDS                   
         BZ    XIT                 OR  NULL                                     
         MVC   PSNFSEP,TOKEN                                                    
         MVI   MATCHARG,FLDSEP                                                  
         BAS   RE,MATCH                                                         
         LA    RE,PSNFLD                                                        
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOFEXPR                                                       
         BAS   RE,DOMFLDS                                                       
         B     XIT                                                              
         EJECT                                                                  
DORSIDE  NTR1                      RSIDE "RIGHT-HAND SIDE" ->                   
         TM    ID,DIVIDER              DIVIDER | VLIST                          
         BZ    XIT                 OR  NULL                                     
         TM    CONTROL,PSNVNOKQ                                                 
         BO    ERROR               ERROR IF VALUES NOT ALLOWED                  
         MVC   PSNVSEP,TOKEN                                                    
         MVI   MATCHARG,DIVIDER                                                 
         BAS   RE,MATCH                                                         
         BAS   RE,DOVLIST                                                       
         B     XIT                                                              
*                                                                               
DOVLIST  NTR1                      VLIST "VALUE LIST" ->                        
         CLI   ID,OPENP                '(' | VAL | MVALS                        
         BNE   DOVL10              OR  VAL | SMVALS                             
         MVI   MATCHARG,OPENP                                                   
         BAS   RE,MATCH                                                         
         LA    RE,PSNVAL                                                        
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOVAL                                                         
         BAS   RE,DOMVALS                                                       
         B     XIT                                                              
*                                                                               
DOVL10   LA    RE,PSNVAL                                                        
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOVAL                                                         
         BAS   RE,DOSMVALS                                                      
         B     XIT                                                              
*                                                                               
DOMVALS  NTR1                      MVALS "MORE VALUES" ->                       
         CLI   ID,CLOSEP               VALSEP | VAL | MVALS                     
         BNE   DOMV10              OR  ')'                                      
         MVI   MATCHARG,CLOSEP                                                  
         BAS   RE,MATCH                                                         
         B     XIT                                                              
*                                                                               
DOMV10   MVC   PSNVSEP,TOKEN                                                    
         TM    CONTROL,PSNMVOKQ                                                 
         BZ    ERROR               ERROR IF MULTIBLE VALUES NOT ALLOWED         
         MVI   MATCHARG,VALSEP                                                  
         BAS   RE,MATCH                                                         
         LA    RE,PSNVAL                                                        
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOVAL                                                         
         BAS   RE,DOMVALS                                                       
         B     XIT                                                              
*                                                                               
DOSMVALS NTR1                      SMVALS "SPECIAL MORE VALUES" ->              
         TM    ID,VALSEP              VALSEP(NOT FLDSEP) | VAL | SMVALS         
         BZ    XIT                 OR NULL                                      
         TM    ID,FLDSEP                                                        
         BO    XIT                                                              
         MVC   PSNVSEP,TOKEN                                                    
         TM    CONTROL,PSNMVOKQ                                                 
         BZ    ERROR               ERROR IF MULTIBLE VALUES NOT ALLOWED         
         MVI   MATCHARG,VALSEP                                                  
         BAS   RE,MATCH                                                         
         LA    RE,PSNVAL                                                        
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOVAL                                                         
         BAS   RE,DOSMVALS                                                      
         B     XIT                                                              
         EJECT                                                                  
DOVAL    NTR1                      VAL "VALUE" ->                               
         CLI   ID,0                    COMP                                     
         BE    ERROR               OR  NULL                                     
         MVI   PSNTAG,PSNVALQ                                                   
         CLI   ID,COMP                                                          
         BE    DOV10                                                            
         TM    CONTROL,PSNNONLQ                                                 
         BO    ERROR               ERROR IF NULL VALUES NOT ALLOWED             
         B     XIT                                                              
*                                                                               
DOV10    MVI   MATCHARG,COMP                                                    
         BAS   RE,MATCH                                                         
         BAS   RE,DOALIST                                                       
         B     XIT                                                              
*                                                                               
DOALIST  NTR1                      ALIST "ATTRIBUTE LIST" ->                    
         CLI   ID,OPENP                '(' | ATTR | MATTR                       
         BNE   XIT                 OR  NULL                                     
         TM    CONTROL,PSNAOKQ                                                  
         BZ    ERROR               ERROR IF ATTRIBUTES NOT ALLOWED              
         MVI   MATCHARG,OPENP                                                   
         BAS   RE,MATCH                                                         
         LA    RE,PSNATTR                                                       
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOATTR                                                        
         BAS   RE,DOMATTR                                                       
         B     XIT                                                              
*                                                                               
DOATTR   NTR1                      ATTR "ATTRIBUTE" ->                          
         CLI   ID,0                    COMP                                     
         BE    ERROR               OR  NULL                                     
         MVI   PSNTAG,PSNATTRQ                                                  
         CLI   ID,COMP                                                          
         BE    DOA10                                                            
         TM    CONTROL,PSNNONLQ                                                 
         BO    ERROR               ERROR IF NULL ATTRIBUTES NOT ALLOWED         
         B     XIT                                                              
*                                                                               
DOA10    MVI   MATCHARG,COMP                                                    
         BAS   RE,MATCH                                                         
         B     XIT                                                              
*                                                                               
DOMATTR  NTR1                      MATTR "MORE ATTRIBUTES" ->                   
         CLI   ID,CLOSEP               ATTRSEP | ATTR | MATTR                   
         BNE   DOMA10              OR  ')'                                      
         MVI   MATCHARG,CLOSEP                                                  
         BAS   RE,MATCH                                                         
         B     XIT                                                              
*                                                                               
DOMA10   MVC   PSNASEP,TOKEN                                                    
         MVI   MATCHARG,ATTRSEP                                                 
         BAS   RE,MATCH                                                         
         LA    RE,PSNATTR                                                       
         ST    RE,LINKFLD                                                       
         BAS   RE,NEXTENT                                                       
         BAS   RE,DOATTR                                                        
         BAS   RE,DOMATTR                                                       
         B     XIT                                                              
         EJECT                                                                  
* MATCH CURRENT ID AGAINST ARGUMENT PASSED HERE AND GET NEXT ID                 
*                                                                               
MATCH    NTR1                                                                   
         CLI   MATCHARG,CUTOFF     IF ARGUMENT BELOW CUTOFF POINT               
         BNL   MATCH10                                                          
         CLC   MATCHARG,ID         AND ARGUMENT UNEQUAL TO ID                   
         BNE   ERROR               THEN SYNTAX ERROR                            
         B     MATCH20                                                          
*                                                                               
MATCH10  NC    MATCHARG,ID         ELSE IF ARGUMENT BIT NOT SET IN ID           
         BZ    ERROR               THEN SYNTAX ERROR                            
*                                                                               
MATCH20  CLI   MATCHARG,COMP       IF ID IS COMP                                
         BNE   MATCH90                                                          
*                                                                               
         MVC   PSNLEN,TOKENLEN     PUT COMPONENT ATTRIBUTES INTO PSND           
         MVC   PSNCOMP,ATOKEN                                                   
         MVI   STAT,X'FF'          INITIALIZE ALL STATUS BITS TO BE SET         
         ZIC   R2,TOKENLEN                                                      
         L     R3,ATOKEN           LOOP THROUGH COMPONENT'S CHARS               
*                                                                               
MATCH25  MVC   BYTE,0(R3)          EXTRACT EACH CHAR AND RAISE CASE             
         OI    BYTE,X'40'                                                       
*                                                                               
MATCH30  CLI   BYTE,C'0'           IF CHAR IS A DIGIT                           
         BL    MATCH40                                                          
         CLI   BYTE,C'9'                                                        
         BH    MATCH40                                                          
         NI    STAT,X'FF'-PSNALFQ  THEN CLEAR ALPHA BIT                         
         B     MATCH70                                                          
*                                                                               
MATCH40  CLI   BYTE,C'A'           IF CHAR IS BETWEEN C'A' AND C'F'             
         BL    MATCH50                                                          
         CLI   BYTE,C'F'                                                        
         BH    MATCH50                                                          
         NI    STAT,X'FF'-PSNNUMQ  THEN CLEAR NUMERIC BIT                       
         B     MATCH70                                                          
*                                                                               
MATCH50  CLI   BYTE,C'G'           IF CHAR IS BETWEEN C'G' AND C'Z'             
         BL    MATCH60                                                          
         CLI   BYTE,C'Z'                                                        
         BH    MATCH60             THEN CLEAR NUMERIC AND HEX BITS              
         NI    STAT,X'FF'-PSNNUMQ-PSNHEXQ                                       
         B     MATCH70                                                          
*                                                                               
MATCH60  MVI   STAT,0              ELSE CLEAR ALL BITS                          
         B     MATCH80                                                          
*                                                                               
MATCH70  LA    R3,1(R3)            NEXT CHAR                                    
         BCT   R2,MATCH25                                                       
*                                                                               
MATCH80  MVC   PSNSTAT,STAT        SAVE STATUS BYTE IN PSND                     
*                                                                               
         TM    PSNSTAT,PSNNUMQ     IF COMP IS NUMERIC                           
         BZ    MATCH90                                                          
*                                                                               
         CLI   TOKENLEN,15         THEN IF LONGER THAN 15 BYTES THAN            
         BH    MATCH85                 SET BIG INDICATOR                        
*                                                                               
         L     RE,ATOKEN           PACK NUMBER INTO DUB                         
         ZIC   RF,TOKENLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,RE)                                                      
*                                                                               
         CP    DUB,=P'2147000000'  IF LARGER THEN 2.1 BILLION THEN SET          
         BH    MATCH85                 BIG INDICATOR                            
*                                                                               
         CVB   R1,DUB              CONVERT TO BINARY                            
         ST    R1,PSNNUM           AND STORE IN PSND                            
         B     MATCH90                                                          
*                                  TURN OFF NUMERIC BIT                         
MATCH85  NI    PSNSTAT,X'FF'-PSNNUMQ                                            
         OI    PSNSTAT,PSNBIGQ     AND TURN ON BIG INDICATOR                    
*                                                                               
MATCH90  BAS   RE,LEXAN            GET NEXT ID                                  
         B     XIT                                                              
         EJECT                                                                  
LEXAN    NTR1                                                                   
         XC    ID,ID               RETURN ID = 0 IF END OF EXPRESSION           
         CLC   TEXTPTR,TEXTLEN                                                  
         BNL   XIT                                                              
*                                                                               
         ZIC   R4,TEXTPTR          R4 = TEXTPTR                                 
         ZIC   R5,TEXTLEN          R5 = TEXTLEN                                 
*                                                                               
         SR    R3,R3               R3 = A(TOKEN)                                
         ICM   R3,7,ATEXT                                                       
         AR    R3,R4                                                            
*                                                                               
         CLI   0(R3),C' '          IF FIRST CHAR IS A SPACE                     
         BNE   LEXAN10                                                          
         OC    TRTAB+C' '(1),TRTAB+C' '    AND SPACE IS A SEPARATOR             
         BZ    LEXAN10                                                          
         MVC   ID,TRTAB+C' '       THEN SET ID TO SEPARATOR                     
         ST    R3,ATOKEN                                                        
         MVC   TOKEN,0(R3)                                                      
         BAS   RE,EATSPACE         AND SKIP TRAILING SPACES                     
         CR    R4,R5               IF SPACE IS LAST LEXAN                       
         BL    LEXAN100                                                         
         XC    ID,ID               THEN SET ID BACK TO 0                        
         B     LEXAN100                                                         
*                                                                               
LEXAN10  BAS   RE,EATSPACE         ELSE EAT LEADING SPACES                      
         CR    R4,R5                                                            
         BNL   XIT                 XIT IF END FOUND                             
         ST    R3,ATOKEN                                                        
*                                                                               
         ZIC   RE,0(R3)            IF FIRST CHAR IS A SEPARATOR                 
         LA    RE,TRTAB(RE)                                                     
         OC    0(1,RE),0(RE)                                                    
         BZ    LEXAN20                                                          
         MVC   ID,0(RE)            THEN SET ID TO SEPARATOR                     
         MVC   TOKEN,0(R3)                                                      
         LA    R4,1(R4)                                                         
         B     LEXAN100                                                         
*                                                                               
LEXAN20  MVI   ID,COMP             ELSE SET ID TO COMP                          
*                                                                               
         LR    R2,R3               R2 = A(LAST NON-SPACE CHAR IN COMP)          
*                                                                               
LEXAN30  CR    R4,R5               IF END ENCOUNTERED                           
         BNL   LEXAN50                                                          
         ZIC   RE,0(R3)            OR SEPARATOR ENCOUNTERED                     
         LA    RE,TRTAB(RE)                                                     
         OC    0(1,RE),0(RE)                                                    
         BNZ   LEXAN50             THEN COMPONENT ENDS                          
*                                                                               
         CLI   0(R3),C' '          ELSE IF SPACE ENCOUNTERED                    
         BNE   LEXAN40                                                          
         BAS   RE,EATSPACE         THEN SKIP SPACES                             
         B     LEXAN30                                                          
*                                                                               
LEXAN40  LR    R2,R3               ELSE SET A(LAST NON-SPACE)                   
         LA    R3,1(R3)            AND BUMP TO NEXT CHAR                        
         LA    R4,1(R4)                                                         
         B     LEXAN30                                                          
*                                                                               
LEXAN50  L     R3,ATOKEN           COMPONENT LENGTH = A(LAST NON-SPACE)         
         SR    R2,R3                         - A(FIRST NON-SPACE) + 1           
         LA    R2,1(R2)                                                         
         STC   R2,TOKENLEN                                                      
*                                                                               
LEXAN100 STC   R4,TEXTPTR          SAVE POINTER TO NEXT COMPONENT               
         B     XIT                                                              
         EJECT                                                                  
EATSPACE CR    R4,R5               RETURN IF END OF COMPONENT REACHED           
         BNLR  RE                                                               
         CLI   0(R3),C' '          OR IF NON-SPACE REACHED                      
         BHR   RE                                                               
         LA    R3,1(R3)            ELSE BUMP TO NEXT CHAR                       
         LA    R4,1(R4)                                                         
         B     EATSPACE                                                         
*                                                                               
NEXTENT  L     R6,LASTENT          SET R6 TO NEXT ENTRY                         
         LA    R6,PSNL(R6)                                                      
         ST    R6,LASTENT                                                       
         L     RF,LINKFLD          STORE NEW R6 INTO LINK FIELD                 
         ST    R6,0(RF)                                                         
         XC    0(PSNL,R6),0(R6)                                                 
         ZIC   RF,NUMENTS          BUMP COUNTER FOR NUMBER OF ENTRIES           
         LA    RF,1(RF)                                                         
         STC   RF,NUMENTS                                                       
         OC    MAXENTS,MAXENTS     IF MAXIMUM NUMBER OF ENTRIES SET             
         BZR   RE                                                               
         CLC   NUMENTS,MAXENTS     AND CURRENT NUMBER EXCEED MAXIMUM            
         BH    ERROR               THEN ERROR                                   
         BR    RE                                                               
         EJECT                                                                  
ERROR    L     R1,SAVEDR1                                                       
         MVC   4(1,R1),NUMENTS     NUMBER OF ENTRIES SO FAR                     
         MVI   8(R1),X'80'                                                      
         MVC   9(3,R1),ATOKEN+1                                                 
         B     RESTORE                                                          
*                                                                               
EXIT     L     R1,SAVEDR1                                                       
         MVC   4(1,R1),NUMENTS                                                  
         MVI   8(R1),0                                                          
*                                                                               
RESTORE  L     RD,SAVEDRD                                                       
*                                                                               
XIT      XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* WORKING STORAGE AREA                                                          
*                                                                               
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
*                                                                               
PARMTAB  DS    0CL16               PARAMETER TABLE                              
TEXTLEN  DS    X                   LENGTH OF TEXT OR ZERO(SCREEN FIELD)         
ATEXT    DS    AL3                 A(TEXT)                                      
MAXENTS  DS    X                   MAXIMUM NUMBER OF COMPONENTS                 
AOUTPUT  DS    AL3                 A(OUTPUT BLOCK)                              
CONTROL  DS    X                   CONTOL BYTE                                  
SEPLIST  DS    AL3                 A(SEPARATOR OVERRIDE LIST)                   
TRACEOPT DS    CL4                 C'TRAC' IF LEXAN TRACE DESIRED               
*                                                                               
SAVEDR1  DS    F                   SAVED R1 UPON BEGIN OF ROUTINE               
SAVEDRD  DS    F                   SAVED RD UPON BEGIN OF ROUTINE               
*                                                                               
TRTAB    DS    XL256               TRANSLATION TABLE FOR LEX ANALYSIS           
MASK     DS    C                                                                
NUMENTS  DS    X                   NUMBER OF ENTRIES IN OUTPUT AREA             
LASTENT  DS    A                   A(CURRENT LAST ENTRY)                        
*                                                                               
MATCHARG DS    C                   ARGUMENT TO MATCH FUNCTION                   
BYTE     DS    C                   USED IN MATCH TO DETERMINE STATUS            
STAT     DS    X                   BYTE WHERE STATUS IS FORMED                  
*                                                                               
TOKEN    DS    C                   SEPARATOR FROM LEX. ANALYSIS                 
TEXTPTR  DS    X                   POINTS WITHIN TEXT TO NEXT TOKEN             
ATOKEN   DS    A                   ADDRESS OF CURRENT TOKEN                     
TOKENLEN DS    X                   TOKEN LENGTH                                 
ID       DS    X                   LEXICAL ID CODE FOR TOKEN                    
*                                                                               
LINKFLD  DS    A                   ARGUMENT TO NEXTENT FUNCTION                 
*                                                                               
WORKX    EQU   *                                                                
*                                  EQUATES FOR LEXICAL ANALYSIS                 
DIVIDER  EQU   X'80'               DIVIDER                                      
FLDSEP   EQU   X'40'               FIELD SEPARATOR                              
VALSEP   EQU   X'20'               VALUE SEPARATOR                              
ATTRSEP  EQU   X'10'               ATTRIBUTE SEPARATOR                          
CUTOFF   EQU   X'0F'               CUTOFF BETWEEN SEPARATORS AND OTHERS         
CLOSEP   EQU   X'03'               CLOSE PARENTHESIS                            
OPENP    EQU   X'02'               OPEN PARENTHESIS                             
COMP     EQU   X'01'               COMPONENT                                    
         EJECT                                                                  
       ++INCLUDE DDPARSNIPD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'073DDPARSNIP 05/01/02'                                      
         END                                                                    
