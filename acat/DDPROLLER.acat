*          DATA SET DDPROLLER  AT LEVEL 011 AS OF 05/01/02                      
*CATALP PROLLER                                                                 
         TITLE 'PACKED DECIMAL GENERALISED ACCUMULATORS'                        
PROLLER  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LWSEND-LWSD,PROLLER                                              
         LR    R8,RC                                                            
         USING LWSD,R8                                                          
         LM    R2,R5,4(R1)                                                      
         EJECT                                                                  
*                  DEFINE AND CLEAR TABLE                                       
         SPACE 3                                                                
         CLI   3(R1),0                                                          
         BNE   LINE                                                             
         STM   R3,R4,0(R2)         LINES COLUMNS                                
         MVC   PFLD,=H'6'          DEFAULT IS 6 PACKED                          
         MVI   ZAPEX,X'50'                                                      
         MVI   APEX,X'55'                                                       
         CLI   4(R1),8                                                          
         BNE   *+22                                                             
         MVC   PFLD,=H'8'          OPTIONALLY 8 PACKED                          
         MVI   ZAPEX,X'70'                                                      
         MVI   APEX,X'77'                                                       
         MVI   0(R2),8             SAVE LENGTH THE 8 BYTE HEADER                
         LR    R5,R3                                                            
         MR    R4,R4                                                            
         LA    R2,8(R2)                                                         
         SPACE 2                                                                
CLEAR    ZIC   RF,ZAPEX                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         ZAP   0(0,R2),=P'0'                                                    
         AH    R2,PFLD                                                          
         BCT   R5,CLEAR                                                         
         B     ROLLEX                                                           
         EJECT                                                                  
*                  CLEAR A LINE OR POINT TO A LINE                              
         SPACE 3                                                                
LINE     MVC   PFLD,=H'6'          DEFAULT IS 6 PACKED                          
         MVI   ZAPEX,X'50'                                                      
         MVI   APEX,X'55'                                                       
         CLI   0(R2),8                                                          
         BNE   *+18                                                             
         MVC   PFLD,=H'8'          OPTIONALLY 8 PACKED                          
         MVI   ZAPEX,X'70'                                                      
         MVI   APEX,X'77'                                                       
         CLI   3(R1),2                                                          
         BH    POST                                                             
         L     R5,4(R2)                                                         
         MH    R5,PFLD             WIDTH OF LINE IN R7                          
         BCTR  R3,R0                                                            
         LR    R7,R3                                                            
         MR    R6,R5                                                            
         L     R5,4(R2)                                                         
         LA    R2,8(R7,R2)                                                      
         CLI   3(R1),2                                                          
         BE    CLEAR                                                            
         ST    R2,0(R1)                                                         
         B     ROLLEX                                                           
         EJECT                                                                  
*                   ROUTINE TO ADD A VALUE INTO TABLE                           
         SPACE 3                                                                
POST     CLI   3(R1),3                                                          
         BNE   LINADD                                                           
*                                  PARA1 (R2) TABLE                             
*                                      2 (R3) VALUE                             
*                                      3 (R4) LINE NUMBER                       
*                                      4 (R5) COLUMN NUMBER                     
         SPACE 2                                                                
         L     R7,4(R2)            COMPUTE TABLE WIDTH                          
         MH    R7,PFLD                                                          
         BCTR  R4,R0                                                            
         MR    R6,R4               NOW R7 HAS DISPLACEMENT                      
         BCTR  R5,R0                                                            
         MH    R5,PFLD                                                          
         AR    R7,R5               ADD IN COLUMNAR DISPLACEMENT                 
         LA    R7,8(R2,R7)                                                      
         ZIC   RF,APEX                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         AP    0(0,R7),0(0,R3)                                                  
         B     ROLLEX                                                           
         EJECT                                                                  
*                   FACILITY TO ADD LINES AND CROSS CAST                        
         SPACE 3                                                                
LINADD   CLI   3(R1),4                                                          
         BNE   CROSS                                                            
         BAS   RE,ADDLINE                                                       
         B     ROLLEX                                                           
         SPACE 2                                                                
CROSS    CLI   3(R1),5                                                          
         BNE   TOTALS                                                           
         BAS   RE,CROSSCST                                                      
         B     ROLLEX                                                           
         EJECT                                                                  
*                   CONTROL OF ADDING LINE1 TO THE OTHER LINES                  
         SPACE 3                                                                
TOTALS   CLI   3(R1),6                                                          
         BNE   GRAND                                                            
         XR    R6,R6                                                            
         ICM   R6,7,1(R2)          R6 = NUMBER OF LINES                         
         LA    R3,1                                                             
         BAS   RE,CROSSCST         CROSS CAST LINE 1                            
         LA    R4,2                                                             
         SPACE 2                                                                
TOTLOOP  BAS   RE,ADDLINE          NOW ADD IT INTO THE OTHER LINES              
         CR    R4,R6                                                            
         BE    ROLLEX                                                           
         LA    R4,1(R4)                                                         
         B     TOTLOOP                                                          
         EJECT                                                                  
*                   CONTROL OF ADDING ALL LINES TO THE LAST                     
         SPACE 3                                                                
GRAND    CLI   3(R1),7                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R3,1                                                             
         XR    R4,R4                                                            
         ICM   R4,7,1(R2)          R4 = NUMBER OF LINES                         
         SPACE 2                                                                
GRANDLUP BAS   RE,CROSSCST         CROSS CAST LINE                              
         BAS   RE,ADDLINE          AND ADD INTO GRAND TOTALS                    
         LA    R3,1(R3)                                                         
         CR    R3,R4                                                            
         BNE   GRANDLUP                                                         
         SPACE 2                                                                
ROLLEX   XMOD1 1                                                                
         EJECT                                                                  
*                   ROUTINE TO CROSS CAST                                       
         SPACE 2                                                                
CROSSCST NTR1                                                                   
         SPACE 2                                                                
         L     R4,4(R2)            R3 = LINE TO CROSS CAST                      
         MH    R4,PFLD             R4 = WIDTH OF TABLE                          
         BCTR  R3,R0                                                            
         LR    R7,R3                                                            
         MR    R6,R4               DISLACEMENT IN R7                            
         LA    R7,8(R7,R2)         R7 NOW POINTS TO START OF LINE               
         AR    R4,R7                                                            
         SH    R4,PFLD             R4 TO THE LAST COLUMN                        
         ZIC   RF,ZAPEX                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         ZAP   0(0,R4),=P'0'                                                    
         SR    R6,R6                                                            
         SPACE 2                                                                
CROSSLUP CR    R7,R4                                                            
         BE    CROSSEND                                                         
         ZIC   RF,APEX                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         AP    0(0,R4),0(0,R7)                                                  
         AH    R7,PFLD                                                          
         B     CROSSLUP                                                         
         SPACE 2                                                                
CROSSEND XIT1                                                                   
         EJECT                                                                  
*                   ROUTINE TO ADD TWO LINES TOGETHER                           
         SPACE 3                                                                
ADDLINE  NTR1                                                                   
         L     R5,4(R2)            R3 LINE 1                                    
*                                  R4 LINE 2                                    
         MH    R5,PFLD             R5 = WIDTH OF LINE                           
         SPACE 2                                                                
         LR    R7,R3                                                            
         BCTR  R7,R0                                                            
         MR    R6,R5                                                            
         LA    R3,8(R7,R2)         R3 TO START OF LINE 1                        
         SPACE 2                                                                
         LR    R7,R4                                                            
         BCTR  R7,R0                                                            
         MR    R6,R5                                                            
         LA    R4,8(R7,R2)         R4 TO START OF LINE 2                        
         L     R5,4(R2)                                                         
         SPACE 2                                                                
ADDLOOP  ZIC   RF,APEX                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         AP    0(0,R4),0(0,R3)     NOW ADD THE LINES TOGETHER                   
         AH    R3,PFLD                                                          
         AH    R4,PFLD                                                          
         BCT   R5,ADDLOOP                                                       
         XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR WORKING STORAGE                                        
         SPACE 1                                                                
LWSD     DSECT                                                                  
PFLD     DS    H                   6 OR 8                                       
ZAPEX    DS    CL1                 LENGTHS FOR ZAP 50 OR 70                     
APEX     DS    CL1                 LENGTHS FOR AP 55 OR 77                      
LWSEND   EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011DDPROLLER 05/01/02'                                      
         END                                                                    
