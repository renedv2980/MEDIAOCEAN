*          DATA SET FATOSCRP   AT LEVEL 003 AS OF 05/01/02                      
*CATALP FATOSCRP                                                                
         SPACE 1                                                                
***********************************************************************         
* THIS MODULE IS CALLED TO PROCESS THE TWA AFTER A SCRIPT TRANSACTION *         
* COMPLETES. ALL TWA FLAGS ETC ARE SET IN THE NORMAL WAY BUT NO ACTUAL*         
* OUTPUT IS GENERATED.                                                *         
* XL1 0=XMIT CHANGES,1=XMIT ALL. RETURN 0=WRITE,1=ERASE/WRITE         *         
* AL3 A(TWA)                                                          *         
* AL4 A(OUTPUT BUFFER)                                                *         
* AL4 A(UTL ENTRY)                                                    *         
* AL4 A(TRANSLATOR I/O BLOCK)                                         *         
***********************************************************************         
         SPACE 1                                                                
         TITLE 'TOSCRP - OUTPUT TRANSLATOR FOR SCRIPTS'                         
         PRINT NOGEN                                                            
TOSCRP   CSECT                                                                  
         NMOD1 WORKX-TIOBD,**TOSCRP                                             
         USING TIOBD,RC                                                         
         LR    R4,R1               R4=A(PARAM LIST)                             
         L     RE,12(R4)                                                        
         MVC   TIOBD(TIOBL),0(RE)  MOVE TIOB TO MY W/S                          
         SPACE 1                                                                
INIT1    L     RE,8(R4)            RE=A(UTL ENTRY)                              
         USING UTLD,RE                                                          
         MVC   CTRY,TCTRY          SAVE TERMINALS CTRY/LANG/SYSTEM              
         MVC   LANG,TLANG                                                       
         MVC   SYS,TOVSYS                                                       
         OC    TSVCREQ,TSVCREQ                                                  
         BZ    *+8                                                              
         MVI   SYS,1                                                            
INIT4    SR    R0,R0               POINT TO TRANSLATE TABLE FOR CTRY            
         IC    R0,CTRY                                                          
         CLI   CTRY,15                                                          
         BNH   *+6                                                              
         SR    R0,R0                                                            
         SLL   R0,2                                                             
         L     RF,=V(CTRYXLOT)                                                  
         AR    RF,R0                                                            
         MVC   OUTXLAT,0(RF)       SAVE A(OUTPUT TRANSLATE TABLE)               
*                                                                               
HLP      TM    TFLAG,TFLAGHLP      TEST IF HELP PANEL ON SCREEN                 
         BZ    HLPX                                                             
         CLC   TSVCREQ+1(1),$PFK+1 IGNORE IF $PFK S/R                           
         BE    HLPX                                                             
         CLC   TSVCREQ+1(1),$HLP+1 IGNORE IF $HELP S/R                          
         BE    HLPX                                                             
         MVI   0(R4),1             SET TO XMIT ALL FIELDS                       
         NI    TFLAG,255-TFLAGHLP  AND TURN OFF HELP PANEL FLAG                 
HLPX     EQU   *                                                                
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* LOCATE BEFORE AND AFTER INDS AT END OF TWA. SET TO TRANSMIT ALL TWA *         
* FIELDS IF EITHER IND IS NON ZERO.                                   *         
***********************************************************************         
*&&US                                                                           
XMT      CLI   0(R4),1             XMT ALL IF BEFORE=AFTER=YES                  
         BE    XMTX                                                             
         L     R3,0(R4)                                                         
         LA    R3,64(R3)                                                        
         SR    R0,R0                                                            
XMT1     ICM   R0,1,0(R3)          SEARCH FOR END OF TWA                        
         BZ    XMT3                                                             
         CLI   1(R3),NOPFLD        IGNORE NOP'D FIELDS                          
         BE    XMT2                                                             
         TM    1(R3),FATBXHDR      TEST EXTENDED FLDHDR                         
         BZ    XMT1A                                                            
         CH    R0,=H'17'                                                        
         BNL   XMT2                                                             
         DC    H'0'                                                             
XMT1A    CH    R0,=H'9'                                                         
         BNL   XMT2                                                             
         DC    H'0'                DIE IF INVALID FIELD IN TWA                  
*                                                                               
XMT2     AR    R3,R0               BUMP TO NEXT FIELD                           
         B     XMT1                                                             
*                                                                               
XMT3     CLI   1(R3),0             TEST BEFORE                                  
         BE    XMTX                                                             
         CLI   2(R3),0             TEST AFTER                                   
         BE    XMTX                                                             
         MVI   0(R4),1             SET TO XMT ALL FIELDS                        
XMTX     EQU   *                                                                
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* LOOP THROUGH EACH FIELD IN TWA AND TEST IF IT IS TO BE TRANSMITTED  *         
* AND SAVE CURSOR POSITIONING INFO                                    *         
***********************************************************************         
         SPACE 1                                                                
FSTFLD   L     R3,0(R4)            TWA ADR                                      
         LA    R3,0(R3)                                                         
         ST    R3,ATWA                                                          
         XC    CURINFO,CURINFO     CLEAR CURSOR POSITION INFO                   
         LA    R3,64(R3)           R3=A(NEXT TWA FIELD)                         
         USING FLDHDRD,R3                                                       
         SR    R6,R6                                                            
         SPACE 2                                                                
NXTFLD   IC    R6,FLDLEN           R6=LEN OF TWA FLD                            
         CH    R6,=H'9'            TEST FOR VALID TWA FIELD                     
         BNL   NXTFLD1                                                          
         LTR   R6,R6               ZERO FOR END OF TWA                          
         BZ    SETCURSE                                                         
         DC    H'0'                                                             
NXTFLD1  LA    RF,0(R3,R6)         SET R9=A(NEXT ACTIVE FIELD)                  
         SR    R9,R9                                                            
         SR    R0,R0                                                            
NXTFLD2  CLI   0(RF),0                                                          
         BE    NXTFLD3                                                          
         TM    1(RF),NOPFLD                                                     
         BNO   *+14                                                             
         IC    R0,0(RF)                                                         
         AR    RF,R0                                                            
         B     NXTFLD2                                                          
         LR    R9,RF                                                            
*                                                                               
NXTFLD3  TM    FLDATB,NOPFLD       BYPASS NOP FIELDS                            
         BO    TRANSX                                                           
         CLI   FLDOLEN,X'FF'                                                    
         BNE   *+8                                                              
         OI    CURSES,X'80'        SET ALL FIELDS ARE NOW NEW                   
*                                                                               
NXTFLD4  TM    FLDATB,FATBPROT     TEST IF FIELD IS UNPROTECTED                 
         BO    NXTFLD5                                                          
         OC    CUFUADR,CUFUADR                                                  
         BNZ   NXTFLD5                                                          
         ST    R3,CUFUADR          SAVE A(FIRST UNPROT FIELD HDR)               
*                                                                               
NXTFLD5  TM    FLDOIND,FOUTCUR     TEST SET CURSOR IN FLD HDR                   
         BZ    *+8                                                              
         ST    R3,CUFHADR                                                       
         TM    TIOBINDS,TIOBSETC   TEST SPECIAL CURSOR SETTING                  
         BZ    NXTFLD8                                                          
         OC    CUTIADR,CUTIADR     TEST CURSOR FIELD FOUND                      
         BNZ   NXTFLD8                                                          
*                                                                               
NXTFLD6  OC    TIOBCURD,TIOBCURD   TEST IF A TWA DISPLACEMENT                   
         BZ    NXTFLD7                                                          
         LR    R0,R3                                                            
         S     R0,ATWA                                                          
         CLM   R0,3,TIOBCURD       TEST THIS IS THE CORRECT FIELD               
         BNE   NXTFLD8                                                          
         ST    R3,CUTIADR          SET CURSOR FIELD FOUND                       
         MVC   CUTINDX,TIOBCURI    SET INDEX INTO FIELD                         
         B     NXTFLD8                                                          
*                                                                               
NXTFLD7  OC    TIOBCURS,TIOBCURS   TEST ABS SCR ADR OF CURSOR                   
         BZ    NXTFLD8                                                          
         LH    RF,TIOBCURS                                                      
         SH    RF,FLDADR                                                        
         BM    NXTFLD8             CURSOR IS BEFORE THIS FLD STARTS             
         LR    R0,R6                                                            
         SH    R0,=H'8'                                                         
         TM    FLDATB,FATBXHDR                                                  
         BZ    *+8                                                              
         SH    R0,=H'8'                                                         
         CR    RF,R0               TEST IF CURSOR IN THIS FIELD                 
         BNL   NXTFLD8                                                          
         ST    R3,CUTIADR          SET CURSOR FIELD FOUND                       
         STC   RF,CUTINDX          SET INDEX INTO FIELD                         
*                                                                               
NXTFLD8  CLI   0(R4),1             TEST IF TRANSMITTING ALL FIELDS              
         BE    TRANS1              YES                                          
         TM    CURSES,X'80'        TEST IF NEW FIELD                            
         BZ    *+8                                                              
         OI    FLDOLEN,X'80'                                                    
         TM    FLDOIND,FOUTTRN                                                  
         BO    TRANSMIT            TRANSMIT THIS FIELD                          
         TM    FLDOLEN,X'80'                                                    
         BO    TRANSMIT            TRANSMIT THIS NEW FIELD                      
         LTR   R9,R9                                                            
         BZ    *+12                                                             
         TM    7(R9),X'80'                                                      
         BO    TRANSMIT            TRANSMIT THIS BECAUSE NEXT IS NEW            
         B     TRANSX                                                           
*                                                                               
NXTFLDX  B     TRANSX                                                           
         EJECT                                                                  
***********************************************************************         
* THIS TWA FIELD IS TO BE TRANSMITTED. ANY UNRESOLVED DATA DICTIONARY *         
* REFERENCES WILL BE RESOLVED AND ANY RESIDUAL INVALID CHRS WILL BE   *         
* SET TO SPACES. SIGNIFICANT FIELD LENTH SET IN FIELD HDR+7.          *         
***********************************************************************         
         SPACE 1                                                                
TRANSMIT OI    FLDOIND,FOUTTRN     SET ON TRANSMIT BIT                          
*                                                                               
TRANS1   LR    R7,R6               SET UP FLD DATA AT FDATA                     
         SH    R7,=H'9'            R7=MAX FLD LEN - 1                           
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
         EX    R7,MOVEIT                                                        
         EX    R7,TESTDDIT         TEST IF DD REQD (DESTROYS R1 AND R2)         
         BNZ   TRANS2              YES                                          
TRANS1A  LA    R8,FDATA(R7)        POINT TO LAST CHR OF DATA                    
         LA    R7,1(R7)                                                         
         B     TRANS3                                                           
*                                                                               
TRANS2   MVI   P1+0,C'T'           SET TRANSLATE WHOLE FIELD                    
         MVI   P1+1,C'U'           SET CASE TO UPPER/LOWER                      
         TM    FLDATB,FATBLC                                                    
         BZ    *+8                                                              
         MVI   P1+1,C'L'                                                        
         MVC   P1+2(1),SYS         SET SYSTEM                                   
         MVC   P1+3(1),LANG        SET LANGUAGE                                 
         LA    RF,FDATA            SET ADR AND LEN OF DATA                      
         ST    RF,P2                                                            
         LA    RF,1(R7)                                                         
         STC   RF,P2                                                            
         ICM   RF,15,=V(DICTATE)   GO TO V(DICTATE) FOR TRANSLATION             
         BZ    TRANS2A                                                          
         LA    R1,P1                                                            
         BASR  RE,RF                                                            
TRANS2A  EX    R7,RESTORIT         MOVE TRANSLATED FIELD BACK TO TWA            
         B     TRANS1A                                                          
*                                                                               
TRANS3   TM    FLDATB,FATBDEL      DELETE TRAILING BLANKS                       
         BO    *+16                NO                                           
         CLI   0(R8),C' '                                                       
         BNE   *+8                                                              
         MVI   0(R8),0                                                          
         CLI   0(R8),0                                                          
         BNE   *+14                                                             
         BCTR  R8,0                                                             
         BCT   R7,TRANS3                                                        
         B     *+12                                                             
         LR    R7,R8                                                            
         LA    R8,FDATA-1                                                       
         SR    R7,R8                                                            
         NI    LTFLAG,X'FF'-FOUTMOD  SET FIELD NOT NEW                          
         TM    FLDOLEN,X'80'                                                    
         BZ    *+8                                                              
         OI    LTFLAG,FOUTMOD      SET NEW FIELD FLAG                           
         STC   R7,FLDOLEN          SET SIGNIFICANT FIELD LEN                    
         LR    R7,R6                                                            
         SH    R7,=H'9'                                                         
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
         L     RF,OUTXLAT          POINT TO TRANLATE TABLE FOR CTRY             
         EX    R7,CLEANIT          CLEAN OUTPUT CHRS FOR TRANSMISSION           
         EX    R7,RESTORIT         MOVE BACK TO TWA                             
*                                                                               
TRANS9   OI    LTFLAG,FOUTTRN      SET TRANSMIT FLAG                            
         BZ    *+8                                                              
         OI    LTFLAG,X'02'        TURN ON PREVNEW FLAG                         
         OI    LTFLAG,FOUTTRN      SET TRANS FLAG                               
         NI    LTFLAG,FOUTTRN+FOUTMOD  TURN OFF CURSOR/PREVNEW FLAGS            
         TM    LTFLAG,FOUTMOD                                                   
         BZ    *+8                                                              
         OI    LTFLAG,X'02'        TURN ON PREVNEW FLAG                         
*                                                                               
TRANSX   AR    R3,R6               BUMP TO NEXT TWA FLD                         
         B     NXTFLD                                                           
*                                                                               
NULLIT   XC    FDATA(0),FDATA                                                   
MOVEIT   MVC   FDATA(0),FLDDATA                                                 
TESTDDIT TRT   FDATA(0),TRTDD                                                   
CLEANIT  TR    FDATA(0),0(RF)                                                   
RESTORIT MVC   FLDDATA(0),FDATA                                                 
         EJECT                                                                  
***********************************************************************         
* RETURN REQUESTED POSITION OF CURSOR IN TBUFF                        *         
***********************************************************************         
         SPACE 1                                                                
SETCURSE L     R2,4(R4)            R2=A(TBUFF)                                  
         USING TBHDATA,R2                                                       
         XC    TBHCURS(5),TBHCURS  CLEAR TBHCURS/TBHCURD/TBHCURI                
*                                                                               
SETCUR1  ICM   RE,15,CUTIADR       TEST IF CURSOR FIELD MATCH TO FATIOB         
         BZ    SETCUR2                                                          
         LR    R1,RE                                                            
         S     R1,ATWA                                                          
         STH   R1,TBHCURD          SET DISPLACEMENT TO FIELD HEADER             
         SR    R0,R0                                                            
         IC    R0,CUTINDX                                                       
         STC   R0,TBHCURI          SET INDEX TO CURSOR IN FIELD                 
         AH    R0,2(RE)                                                         
         STH   R0,TBHCURS          SET ABSOLUTE SCREEN LOCATION                 
         B     SETCURX                                                          
*                                                                               
SETCUR2  TM    TIOBINDS,TIOBSETC   TEST SET TO ABSOLUTE LOCATION                
         BZ    SETCUR3                                                          
         OC    TIOBCURD,TIOBCURD                                                
         BNZ   SETCUR3                                                          
         MVC   TBHCURS,TIOBCURS                                                 
         B     SETCURX                                                          
*                                                                               
SETCUR3  ICM   RE,15,CUFHADR       TEST IF CURSOR SET VIA FLD HDR               
         BZ    SETCUR4                                                          
         LR    R1,RE                                                            
         S     R1,ATWA                                                          
         STH   R1,TBHCURD          SET DISPLACEMENT TO FIELD HEADER             
         MVC   TBHCURS,2(RE)       SET ABSOLUTE SCREEN LOCATION                 
         B     SETCURX                                                          
*                                                                               
SETCUR4  ICM   RE,15,CUFUADR       DEFAULT IF TO FIRST UNPROT FLD               
         BZ    SETCURX                                                          
         LR    R1,RE                                                            
         S     R1,ATWA                                                          
         STH   R1,TBHCURD          SET DISPLACEMENT TO FIELD HEADER             
         MVC   TBHCURS,2(RE)       SET ABSOLUTE SCREEN LOCATION                 
*                                                                               
SETCURX  EQU   *                                                                
         SPACE 1                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
NOPFLD   EQU   X'FF'                                                            
$PFK     DC    X'010B'                                                          
$HLP     DC    X'0109'                                                          
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
TRTDD    DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'03030404040401040303040404040000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00000000000000000000000000000000'  40-4F                    
         DC    XL16'00000000000000000000000000000000'  50-5F                    
         DC    XL16'00000000000000000000000000000000'  60-6F                    
         DC    XL16'00000000000000000000000000000000'  70-7F                    
         DC    XL16'00000000000000000000000000000000'  80-8F                    
         DC    XL16'00000000000000000000000000000000'  90-9F                    
         DC    XL16'00000000000000000000000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'00000000000000000000000000000000'  C0-CF                    
         DC    XL16'00000000000000000000000000000000'  D0-DF                    
         DC    XL16'00000000000000000000000000000000'  E0-EF                    
         DC    XL16'00000000000000000000000000000000'  F0-FF                    
         SPACE 2                                                                
*DDDDEQUS                                                                       
       ++INCLUDE DDDDEQUS                                                       
         EJECT                                                                  
* WORKING STORAGE DSECT                                                         
*                                                                               
       ++INCLUDE FATIOB                                                         
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
ATWA     DS    A                                                                
OUTXLAT  DS    A                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
*                                                                               
CURINFO  DS    0XL14               CURSOR INFORMATION                           
CUFUADR  DS    A                   A(FIRST UNP FLD HDR)                         
CUFHADR  DS    A                   A(TWA FLD HDR WITH SET CURS IN HDR)          
CUTIADR  DS    A                   A(TWA FLD HDR WITH CURS IN TIOB)             
CUTINDX  DS    X                   INDEX INTO FIELD CONTAINING CURSOR           
CURSES   DS    X                   CURSOR FLAGS                                 
*                                                                               
HALF     DS    H                                                                
LTFLAG   DS    C                                                                
CTRY     DS    X                                                                
LANG     DS    X                                                                
SYS      DS    X                                                                
XTND     DS    X                                                                
NONO     DS    X                                                                
FDATA    DS    CL255                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
*DDFLDHDR                                                                       
       ++INCLUDE DDFLDHDR                                                       
         EJECT                                                                  
*FATBHD                                                                         
       ++INCLUDE FATBHD                                                         
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FATOSCRP  05/01/02'                                      
         END                                                                    
