*          DATA SET SPGETOMCOM AT LEVEL 024 AS OF 03/17/03                      
*CATALP SPOMCOM                                                                 
         TITLE 'SPOMCOM - RETRIEVE ORDER MANAGER COMMENTS'                      
SPOMCOM  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*SPOMCOM,CLEAR=Y                                     
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1                                                            
         USING OMCOMD,RA                                                        
*                                                                               
         MVI   OMCERR,0                                                         
         MVC   NEXTBUFF,OMCABUFF                                                
         XC    OMCCOUNT,OMCCOUNT                                                
*                                                                               
         CLI   OMCACT,OMCACT_GET                                                
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R0,OMCABUFF         CLEAR OUTPUT  BUFFER                         
         LH    R1,OMCLBUFF                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,OMCABUFF                                                      
         AH    R0,OMCLBUFF                                                      
         BCTR  R0,0                                                             
         ST    R0,LASTBUFF         SAVE BUFFER END ADDRESS                      
*                                                                               
K        USING COMKEY,KEY                                                       
         XC    KEY,KEY                                                          
         MVC   K.COMKTYPE,=X'0D0C'                                              
         MVC   K.COMKAGY,OMCAGMD                                                
         MVI   K.COMCTYPE,C'O'     TYPE 'O' COMMENTS                            
*                                                                               
         MVC   COMTYPE,=CL8'MEDIA' MEDIA  COMMENTS                              
         BAS   RE,GETCOM                                                        
*                                                                               
         MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVC   KEY+4(2),OMCCLT                                                  
         MVC   COMTYPE,=CL8'CLT'                                                
         BAS   RE,GETCOM                                                        
*                                                                               
         MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVC   KEY+8(1),OMCPRD                                                  
         MVC   COMTYPE,=CL8'PRD'                                                
         BAS   RE,GETCOM                                                        
*                                                                               
         MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVC   KEY+9(1),OMCEST                                                  
         MVC   COMTYPE,=CL8'EST'                                                
         CLI   OMCFLT,0                                                         
         BE    MAIN10                                                           
         MVC   KEY+10(1),OMCFLT                                                 
         MVC   COMTYPE,=CL8'FLT'                                                
MAIN10   BAS   RE,GETCOM                                                        
*                                                                               
         CLC   KEY(L'COMKEY),KEYSAVE                                            
         BE    EXIT                IF WE HAD IT, GETCOM PRINTED IT THEN         
*                                                                               
         CLI   OMCFLT,0            TEST FLIGHT PRESENT                          
         BE    MAIN20              NO DONE                                      
         MVC   KEY,KEYSAVE         YES, THEN NONE FOR FLIGHT SPECFIC            
         MVI   KEY+10,0                 LET'S SEE IF ESTIMATE LEVEL             
         MVC   COMTYPE,=CL8'EST'                                                
         BAS   RE,GETCOM                                                        
*                                                                               
         CLC   KEY(L'COMKEY),KEYSAVE                                            
         BE    EXIT                IF WE HAD IT, GETCOM PRINTED IT THEN         
*                                                                               
MAIN20   XC    KEY,KEY                                                          
         MVC   KEY(COMKPRD-COMKEY),KEYSAVE                                      
         MVC   KEY+6(3),=C'ALL'                                                 
         MVC   KEY+9(1),OMCEST                                                  
         MVC   COMTYPE,=CL8'EST'                                                
         CLI   OMCFLT,0                                                         
         BE    MAIN30                                                           
         MVC   KEY+10(1),OMCFLT                                                 
         MVC   COMTYPE,=CL8'FLT'                                                
MAIN30   BAS   RE,GETCOM                                                        
*                                                                               
         CLC   KEY(L'COMKEY),KEYSAVE                                            
         BE    EXIT                IF WE HAD IT, GETCOM PRINTED IT THEN         
*                                                                               
         CLI   OMCFLT,0            TEST FLIGHT PRESENT                          
         BE    EXIT                NO DONE                                      
         MVC   KEY,KEYSAVE         YES, THEN NONE FOR FLIGHT SPECFIC            
         MVI   KEY+10,0                 LET'S SEE IF ESTIMATE LEVEL             
         MVC   COMTYPE,=CL8'EST'                                                
         BAS   RE,GETCOM                                                        
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
GETCOM   NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         L     RF,OMCACOMF                                                      
         L     RF,CDATAMGR-COMFACSD(RF)                                         
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                      
*                                                                               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   EXIT                                                             
*                                                                               
         GOTO1 (RF),(R1),=C'GETREC',=C'SPTFILE',KEY+14,IO,DMWORK                
*                                                                               
         LA    R6,IO                                                            
         LA    R6,24(R6)                                                        
*                                                                               
GETCOM2  CLI   0(R6),0                                                          
         BE    EXIT                                                             
         CLI   0(R6),5                                                          
         BE    *+12                                                             
         CLI   0(R6),X'15'                                                      
         BNE   GETCOM4                                                          
*                                                                               
         L     RE,NEXTBUFF                                                      
         SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AHI   RF,-3               SET FOR EX MOVE                              
*                                                                               
         LA    R0,2(RE,RF)         POINT TO END OF MOVE                         
         C     R0,LASTBUFF         TEST PAST END OF BUFFER                      
         BH    GETCOVFL            BUFFER OVERFLOW                              
*                                                                               
         EX    RF,*+8              MOVE COMMENT                                 
         B     *+10                                                             
         MVC   8(0,RE),2(R6)       MOVE COMMENT DATA                            
*                                                                               
         MVC   0(8,RE),COMTYPE     MOVE COMMENT TYPE                            
*                                                                               
         LA    RE,80(RE)                                                        
         ST    RE,NEXTBUFF                                                      
*                                                                               
         LH    R0,OMCCOUNT                                                      
         AHI   R0,1                                                             
         STH   R0,OMCCOUNT                                                      
*                                                                               
GETCOM4  SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     GETCOM2                                                          
*                                                                               
GETCOVFL MVI   OMCERR,OMCERR_OVFL                                               
         B     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6A                                                               
KEY      DS    CL24                                                             
KEYSAVE  DS    CL24                                                             
DMWORK   DS    12D                                                              
COMTYPE  DS    CL8                                                              
NEXTBUFF DS    A                                                                
LASTBUFF DS    A                                                                
         DS    0D                                                               
IO       DS    2000C                                                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
       ++INCLUDE SPOMCOMD                                                       
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPGENCOM                                                       
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024SPGETOMCOM03/17/03'                                      
         END                                                                    
