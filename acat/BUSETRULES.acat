*          DATA SET BUSETRULES AT LEVEL 014 AS OF 05/01/02                      
*CATALP BUSETRLS                                                                
         SPACE 2                                                                
**********************************************************************          
* SUBROUTINE TO DECODE RULES ENTRIES FROM OUTLINE RECORDS            *          
*                                                                    *          
* P1  (1) IS RECORD LEVEL                                            *          
* P1+1(3) IS RECORD ADDRESS                                          *          
* P2  (1) IS LEVEL CHANGE SWITCH (Y/N)                               *          
* P2+1(3) IS RULES TABLE ADDRESS                                     *          
*                                                                    *          
* P1(1)=0 IS INITIALIZATION CALL                                     *          
*                                                                    *          
* TABLE FORMAT IS AS FOLLOWS                                         *          
* +0  16 4 BYTE ENTRIES WITH A(LAST RULE) FOR THIS LEVEL             *          
* +64    2 BYTE RULE LENGTH FOLLOWED BY FIXED RULE AREA              *          
*        VARIABLE RULE LISTS FOLLOW, DISPLACEMENTS TO WHICH          *          
*        ARE GIVEN IN THE FIXED RULE AREA. PRECEDING EACH            *          
*        RULE LIST IS A ONE BYTE COUNT OF THE NUMBER OF ENTRIES.     *          
*                                                                    *          
*  RULES MAY NOT CONFLICT. IF A RULE IS SPECIFIED AT A HIGHER        *          
*  LEVEL, IT MAY NOT BE OVERRIDDEN (OR RESPECIFIED) AT A LOWER LEVEL.*          
*                                                                    *          
*  AFTER MODULE                                                      *          
* P2(1)=0 IF OK                                                      *          
*      =X'FF' IF ERROR                                               *          
*   (2-4)=A(RULE IN ERROR)                                                      
*                                                                    *          
**********************************************************************          
         TITLE 'BUSETRULES - BUILD RULE PROCESSING TABLE'                       
SETRULES CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,SETRULES,CLEAR=YES,RR=R8                             
         USING WORKD,RC                                                         
         ST    R8,RELO                                                          
*                                                                               
         ST    R1,APARMS           SAVE PARM ADDRESS                            
         MVC   AREC(8),0(R1)       SAVE A(REC)/A(TABLE)                         
         MVI   4(R1),0             CLEAR ERROR BYTE                             
         L     R8,ATAB                                                          
         USING QRHDRD,R8                                                        
*                                                                               
         CLI   AREC,0              TEST INITIALIZATION CALL                     
         BNE   SET2                                                             
         XC    0(256,R8),0(R8)     CLEAR TOP OF TABLE                           
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
RELO     DC    A(0)                                                             
         SPACE 1                                                                
* TEST ANY RULES IN THIS OUTLINE *                                              
         SPACE 1                                                                
SET2     L     R6,AREC                                                          
         USING BURECD,R6                                                        
         LA    R6,BUFRSTEL                                                      
         SR    R0,R0               CLEAR REGISTER FOR ELEMENT LENGTH            
*                                                                               
SET2A    CLI   0(R6),0             TEST FOR EOR                                 
         BE    SET2B                                                            
         CLI   0(R6),BURULELQ      TEST FOR A RULE ELEMENT                      
         BE    SET3                                                             
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         B     SET2A                                                            
*                                                                               
SET2B    ZIC   R1,LEVEL            GET OUTLINE'S LEVEL                          
         SLL   R1,2                INDEX TO LAST ENTRY AT ITS LEVEL             
         A     R1,ATAB             NOTHING NOW TO COPY FROM IT                  
         XC    0(4,R1),0(R1)       CLEAR ENTRY FOR LEVEL                        
         B     EXIT                                                             
         SPACE 1                                                                
* FIND END OF CURRENT DATA *                                                    
         SPACE 1                                                                
SET3     L     R8,ATAB                                                          
         USING QRHDRD,R8                                                        
         LA    R8,QRDATA                                                        
         SR    R0,R0                                                            
SET4     ICM   R0,3,0(R8)                                                       
         BZ    SET6                                                             
         AR    R8,R0                                                            
         B     SET4                                                             
SET6     DS    0H                                                               
         ST    R8,FULL             SAVE NEW ENTRY ADDRESS                       
         XC    0(QRDX-QRD+2,R8),0(R8) CLEAR L'MIN ENTRY + 2 FOR EOT             
         LA    R0,QRDX-QRD                                                      
         STCM  R0,3,0(R8)          SET MINIMUM ENTRY LENGTH                     
         SPACE 1                                                                
* FIND ENTRY AT PREVIOUS LEVEL *                                                
         SPACE 1                                                                
         CLI   LEVEL,4             TEST LEVEL 4 (= OUTLINE LEVEL)               
         BNE   SET7                                                             
         L     R8,ATAB                                                          
         XC    QRLEVTAB(64),QRLEVTAB  RESET TABLE ENTRIES                       
         B     SET8                                                             
*                                                                               
SET7     ZIC   R0,LEVEL            GET LEVEL NUMBER                             
*                                                                               
SET7A    LR    R1,R0                                                            
         BCTR  R1,0                                                             
         LTR   R1,R1                                                            
         BNP   SET8                                                             
         SLL   R1,2                X 4                                          
         A     R1,ATAB             POINT TO  ADDRESS                            
         ICM   RE,15,0(R1)         POINT TO ENTRY                               
         BNZ   SET7B                                                            
         BCT   R0,SET7A            IF NO ENTRY, TRY PREVIOUS LEVEL              
         B     SET8                                                             
*                                                                               
         SPACE 1                                                                
* COPY ENTRY FROM PREVIOUS LEVEL TO CURRENT *                                   
         SPACE 1                                                                
SET7B    LR    R0,RE               SAVE 'FROM' REG                              
         LH    R9,0(RE)            SET 'TO' LENGTH                              
         LR    RF,R9               SET 'FROM' LENGTH                            
         LA    R9,16(R9)           SET TO CLEAR 16 BYTES AT END                 
         MVCL  R8,RE               COPY ENTRY                                   
*                                                                               
         LR    RE,R0                RESTORE REG                                 
         MVI   QRFLAG-QRD(RE),X'FF' FLAG 'FROM' RULE NOT EXTRACTABLE            
*                                                                               
SET8     ZIC   RE,AREC             GET LEVEL NUMBER                             
         SLL   RE,2                X 4                                          
         A     RE,ATAB                                                          
         L     R8,FULL                                                          
         ST    R8,0(RE)            SET NEW RULE ADDRESS IN LEVEL TAB            
*                                                                               
         USING QRD,R8                                                           
         MVC   QRLVL,LEVEL         SET LEVEL NUMBER IN RULE                     
         MVI   QRFLAG,0               AND SET 'TO' RULE EXTRACTABLE             
         CLI   LEVCHSW,C'Y'        TEST FOR LEVEL CHANGE OUTLINE                
         BNE   *+8                                                              
         OI    QRFLAG,X'40'        SET LEVEL CHANGE INDICATOR                   
*                                                                               
         L     R6,AREC                                                          
         USING BURECD,R6                                                        
         MVC   QRNODE(12),BUKNODE  MOVE NODE/CODE TO RULE                       
         EJECT                                                                  
* TEST FOR CONFLICTING RULES FROM PREVIOUS LEVELS *                             
         SPACE 1                                                                
         L     R6,AREC                                                          
         USING BURECD,R6                                                        
         LA    R6,BUFRSTEL                                                      
         B     SET12                                                            
*                                                                               
SET10    SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    SET20                                                            
SET12    CLI   0(R6),BURULELQ      TEST RULE ELEMENT                            
         BNE   SET10                                                            
*                                                                               
         USING BURULD,R6                                                        
*                                                                               
         CLI   BURULTYP,RUDUMMY    TEST DUMMY (MUST BE ONLY RULE)               
         BE    EXIT                YES - DONE                                   
         SPACE 1                                                                
* FIND THE RULE TYPE IN THE TABLE *                                             
         SPACE 1                                                                
         LA    R0,(SETTABX-SETTAB)/L'SETTAB                                     
         LA    R1,SETTAB                                                        
         USING SETTABD,R1                                                       
*                                                                               
SET14    CLC   BURULTYP,STTBRULE                                                
         BE    SET16                                                            
         LA    R1,STTBNEXT                                                      
         BCT   R0,SET14                                                         
         DC    H'0'                                                             
*                                                                               
SET16    SR    RE,RE                                                            
         ICM   RE,3,STTBDSPL       GET DSPL TO FIELD                            
         AR    RE,R8                                                            
         TM    STTBIND,STTBDATA    TEST IF DISPLACEMENT TO DATA                 
         BO    SET18               YES                                          
         OC    0(2,RE),0(RE)       TEST HAVE DATA ALREADY                       
         BNZ   SETERR              YES - CONFLICT ERROR                         
         B     SET10                                                            
*                                                                               
SET18    CLI   0(RE),0             TEST DATA PRESENT FOR RULE                   
         BE    SET10               NO-NEXT RULE ELEMENT                         
*                                                                               
SETERR   MVC   ELEM(30),=CL30'XXX SPECIFIED AT HIGHER LEVEL'                    
         MVC   ELEM(3),STTBNAME    INSERT RULE TYPE NAME                        
         LA    R1,ELEM                                                          
         B     BADEXIT                                                          
         DROP  R6                                                               
         EJECT                                                                  
* NOW BUILD NEW RULES ELEMENTS *                                                
         SPACE 1                                                                
SET20    L     R6,AREC                                                          
         USING BURECD,R6                                                        
         LA    R6,BUFRSTEL                                                      
         B     SET24                                                            
*                                                                               
SET22    SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         CLI   0(R6),0             TEST FOR EOR                                 
         BE    SET30               YES                                          
*                                                                               
SET24    CLI   0(R6),BURULELQ      TEST RULE ELEMENT                            
         BNE   SET22                                                            
*                                                                               
         USING BURULD,R6                                                        
SET26    LA    R0,(SETTABX-SETTAB)/L'SETTAB                                     
         LA    R1,SETTAB                                                        
         USING SETTABD,R1                                                       
*                                                                               
         CLC   BURULTYP,STTBRULE                                                
         BE    SET28                                                            
         LA    R1,STTBNEXT                                                      
         BCT   R0,*-14                                                          
         DC    H'0'                                                             
*                                                                               
SET28    XC    ELEM,ELEM                                                        
         MVC   ELEM(1),BURULTYP                                                 
         MVI   ELEM+1,3                                                         
         MVC   ELEM+2(1),STTBLEN   SET LEN OF EACH DATA ITEM                    
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,7,STTBRTN                                                     
         A     RF,RELO                                                          
         BASR  RE,RF               DO BALR FOR HELP IN DUMPS                    
         DC    H'0'                NO RETURN HERE VIA RE                        
         SPACE 1                                                                
SET30    CLI   QROVSYS,0           TEST SYSTEM SPECIFIED                        
         BE    NOSYS               NO                                           
         MVC   HALF,QRLEN                                                       
         AH    R8,HALF             POINT TO END OF ENTRY                        
         XC    0(2,R8),0(R8)       CLEAR EOT MARKER                             
         B     EXIT                                                             
         EJECT                                                                  
         USING QRD,R8                                                           
*                                                                               
* SYSTEM/MEDIA, AGENCY, CLIENT, PRODUCT/PRODUCT GROUP, AND                      
* ESTIMATE FILTER CAN ONLY BE SPECIFIED ONCE FOR EACH OUTLINE                   
* (I.E. ONLY 1 RULE ELEM.)                                                      
*                                                                               
SETMED   OC    QRMED,QRMED         TEST PREVIOUSLY SPECIFIED                    
         BNZ   BADMED                                                           
         MVC   QROVSYS,BURULE      SET CALLOV SYSNUM                            
         MVC   QRSYSTYP,BURULE+1   SET SYSTEM TYPE                              
         MVC   QRMED,BURULE+2      SET MEDIA CODE                               
         B     SET22                                                            
*                                                                               
SETAGY   OC    QRAGYC,QRAGYC       TEST AGENCY PREVIOUSLY SPECIFIED             
         BNZ   BADAGY              YES                                          
         MVC   QRAGYC,BURULE                                                    
         B     SET22                                                            
*                                                                               
SETCLT   OC    QRCLT,QRCLT         TEST PREVIOUSLY SPECIFIED                    
         BNZ   BADCLT                                                           
         MVC   QRCLT,BURULE                                                     
         B     SET22                                                            
*                                                                               
SETPRD   OC    QRPRD,QRPRD         TEST PREVIOUSLY SPECIFIED                    
         BNZ   BADPRD                                                           
         MVC   QRPRD,BURULE                                                     
         B     SET22                                                            
*                                                                               
SETPGR   OC    QRPGR(4),QRPGR      TEST PRD OR PRDGRP PREV SPECIFIED            
         BNZ   BADPRD                                                           
         MVC   QRPGR(4),BURULE                                                  
         B     SET22                                                            
*                                                                               
SETFLT   OC    QRFLT,QRFLT         TEST PREVIOUSLY SPECIFIED                    
         BNZ   BADFLT                                                           
         MVC   QRFLT,BURULE                                                     
         B     SET22                                                            
         EJECT                                                                  
SETLST   SR    RE,RE                                                            
         ICM   RE,3,STTBDSPL       POINT TO DSPL                                
         AR    RE,R8                                                            
         OC    0(2,RE),0(RE)       TEST CODE PROCESSED ALREADY                  
         BNZ   SET22               YES - IGNORE                                 
         MVC   0(2,RE),0(R8)       SET DSPL TO RULE LIST DATA                   
*                                                                               
SETLST2  ZIC   RE,BURULEN                                                       
         SH    RE,=H'5'            SET FOR EX                                   
         LA    R1,ELEM                                                          
         ZIC   RF,ELEM+1                                                        
         AR    R1,RF               POINT TO END OF ELEM                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),BURULE *EXECUTED*                                        
         LA    RF,1(RE,RF)                                                      
         STC   RF,ELEM+1           SET NEW ELEM LEN                             
*                                                                               
         LR    R7,R6               SAVE ELEMENT ADDRESS                         
         SPACE 1                                                                
* SEARCH FOR MORE SIMILAR ELEMENTS                                              
         SPACE 1                                                                
SETLST4  SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    SETLSTX                                                          
         CLC   ELEM(1),BURULTYP                                                 
         BE    SETLST2                                                          
         B     SETLST4                                                          
*                                                                               
SETLSTX  DS    0H                                                               
         SR    R0,R0               CALCULATE NUMBER OF ENTRIES IN LIST          
         ZIC   R1,ELEM+1           GET LENGTH OF DATA IN ELEMENT                
         SH    R1,=H'3'            ADJUST FOR OVERHEAD                          
         ZIC   RF,ELEM+2           GET LENGTH OF EACH ENTRY                     
         DR    R0,RF                                                            
         LTR   R0,R0               SHOULD BE NO REMAINDER                       
         BZ    *+6                                                              
         DC    H'0'                                                             
* NOW MOVE DATA TO RULES AREA                                                   
         LH    RF,0(R8)                                                         
         AR    RF,R8               POINT TO RULE LIST AREA                      
         STC   R1,0(RF)            SET NUMBER OF ENTRIES                        
*                                                                               
         ZIC   RE,ELEM+1           GET DATA LENGTH                              
         SH    RE,=H'4'            SET FOR EX                                   
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    RE,*+8              MOVE DATA                                    
         B     *+10                                                             
         MVC   1(0,RF),ELEM+3      *EXECUTED*                                   
*                                                                               
         LA    RE,2(RE)            SET DATA LENGTH MOVED + NUM ENTRIES          
         AH    RE,0(R8)            ADD TO PREVIOUS RULE LENGTH                  
         STH   RE,0(R8)            AND SET NEW RULE LENGTH                      
*                                                                               
         LR    R6,R7               RESTORE ELEMENT ADDRESS                      
         B     SET22                                                            
         EJECT                                                                  
* ERROR EXITS                                                                   
*                                                                               
NOSYS    LA    R1,=CL30'SYSTEM/MEDIA NOT SPECIFIED'                             
         B     BADEXIT                                                          
*                                                                               
BADMED   LA    R1,=CL30'CONFLICTING MEDIA'                                      
         B     BADEXIT                                                          
*                                                                               
BADAGY   LA    R1,=CL30'CONFLICTING AGENCIES'                                   
         B     BADEXIT                                                          
*                                                                               
BADCLT   LA    R1,=CL30'CONFLICTING CLIENTS'                                    
         B     BADEXIT                                                          
*                                                                               
*&&US                                                                           
BADPRD   LA    R1,=CL30'CONFLICTING PRDS OR PRDGRPS'                            
         B     BADEXIT                                                          
*                                                                               
*&&                                                                             
*&&UK                                                                           
BADPRD   LA    R1,=CL30'CONFLICTING PRODUCTS'                                   
         B     BADEXIT                                                          
*                                                                               
*&&                                                                             
BADFLT   LA    R1,=CL30'CONFLICTING ESTIMATE FILTERS'                           
*                                                                               
BADEXIT  L     RE,ATAB                                                          
         MVC   0(9,RE),=C'*** ERROR'                                            
         MVC   10(30,RE),0(R1)     MOVE ERROR MESSAGE                           
         L     R1,APARMS           GET PARM LIST ADDRESS                        
         ST    R8,4(R1)            RETURN ERROR RULE ADDRESS                    
         MVI   4(R1),X'FF'         SET ERROR FLAG                               
         B     EXIT                                                             
         EJECT                                                                  
* RULE TYPE EDIT TABLE (DSECT FOLLOWS)                                          
         SPACE 1                                                                
SETTAB   DS    0XL(SETTABL)                                                     
*                                                                               
         DC    AL1(RUSYS),AL3(SETMED),AL1(STTBDATA),AL2(QRSYSTYP-QRD)           
         DC    AL1(0),CL3'SYS'                                                  
*                                                                               
         DC    AL1(RUAGY),AL3(SETAGY),AL1(STTBDATA),AL2(QRAGYC-QRD)             
         DC    AL1(0),CL3'AGY'                                                  
*                                                                               
         DC    AL1(RUCLT),AL3(SETCLT),AL1(STTBDATA),AL2(QRCLT-QRD)              
         DC    AL1(0),CL3'CLT'                                                  
*                                                                               
         DC    AL1(RUPRD),AL3(SETPRD),AL1(STTBDATA),AL2(QRPRD-QRD)              
         DC    AL1(0),CL3'PRD'                                                  
*                                                                               
       ++INCLUDE BUSETRULTB                                                     
*                                                                               
SETTABX  EQU   *                                                                
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* DSECT TO COVER MODULE WORKING STORAGE                                         
*                                                                               
WORKD   DSECT                                                                   
APARMS   DS    A                                                                
PARM1    DS    0A                                                               
LEVEL    DS    0X                  OUTLINE LEVEL NUMBER                         
AREC     DS    A                                                                
PARM2    DS    0A                                                               
LEVCHSW  DS    0C                  LEVEL CHANGE SWITCH (Y/N)                    
ATAB     DS    A                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    C                                                                
BYTE2    DS    C                                                                
ELEM     DS    XL256                                                            
WORKX    EQU   *                                                                
         SPACE 2                                                                
* DSECT TO COVER SETTAB ENTRIES                                                 
*                                                                               
SETTABD  DSECT                     ** DSECT FOR SETTAB ENTRIES **               
*                                                                               
STTBRULE DS    CL1                 RULE CODE                                    
STTBRTN  DS    AL3                 ROUTINE ADDRESS                              
STTBIND  DS    X                   INDICATORS                                   
STTBDATA EQU   X'80'               STTBDSPL HOLDS DSPL TO DATA ITSELF           
STTBDSPL DS    AL2                 DSPL IN RULE TABLE TO DATA DSPL              
STTBLEN  DS    AL1                 LIST ENTRY DATA LENGTH                       
STTBNAME DS    CL3                 RULE TYPE NAME                               
STTBNEXT EQU   *                                                                
SETTABL  EQU   *-SETTABD           TABLE ENTRY LENGTH                           
         EJECT                                                                  
       ++INCLUDE BUEXTRULED                                                     
         EJECT                                                                  
       ++INCLUDE BUGENEQUS                                                      
         EJECT                                                                  
       ++INCLUDE BUGENFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014BUSETRULES05/01/02'                                      
         END                                                                    
