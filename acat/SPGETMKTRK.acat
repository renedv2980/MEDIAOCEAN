*          DATA SET SPGETMKTRK AT LEVEL 027 AS OF 05/01/02                      
*CATALP GETMKTRK                                                                
         TITLE 'SPOTPAK GET MARKET RANKS PROGRAM'                               
GETMKTRK CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**MKRK**                                                       
*                                                                               
         ST    R1,PARM                                                          
         L     R9,0(R1)                                                         
         USING SPWORKD,R9                                                       
         LA    RA,2048(R9)                                                      
         LA    RA,2048(RA)                                                      
         USING SPWORKD+4096,RA                                                  
*                                                                               
         STM   R9,RC,SPGMR9                                                     
         B     GM010                                                            
*                                                                               
EQXIT    CR    RB,RB               SET CC =                                     
         B     EXIT                                                             
*                                                                               
NEQXIT   LTR   RB,RB               SET CC NOT =                                 
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
GM010    MVI   FIRST,C'Y'                                                       
         LA    R0,2304                                                          
         AR    R0,R0               4608 = 2304 * 2                              
         L     R1,MKTLIST          NEW LIST                                     
         L     R2,ADBUY            OLD LIST                                     
         SR    R3,R3               MKT NUM                                      
         SR    R4,R4                                                            
*                                                                               
GM020    IC    R4,0(R2)                                                         
         SRDL  R4,4                                                             
         SRL   R5,28                                                            
         LA    R8,2                                                             
*                                                                               
GM025    LTR   R4,R4                                                            
         BZ    GM070                                                            
         BAS   RE,GETSMKT          GET THE SERVICE MARKET                       
         LH    R6,=H'3000'         DEFAULT RANK TO LAST                         
         OC    SVCMKT,SVCMKT       TEST FOR ZERO MARKET                         
         BZ    GM045                                                            
         CLI   FIRST,C'Y'                                                       
         BNE   GM030                                                            
         MVI   FIRST,C'N'                                                       
         BAS   RE,GETRKTAB         GET THE RANK TABLE                           
         BE    GM030                                                            
         DC    H'0'                                                             
*                                                                               
GM030    L     R7,THISYSDL                                                      
*                                                                               
GM035    OC    0(6,R7),0(R7)       FIND RANK NUMBER FOR MARKET                  
         BZ    GM045                                                            
         CLC   SVCMKT,4(R7)                                                     
         BE    GM040                                                            
         LA    R7,6(R7)                                                         
         B     GM035                                                            
*                                                                               
GM040    SR    R6,R6                                                            
         ICM   R6,15,0(R7)                                                      
*                                                                               
GM045    IC    RF,QMGR             # TO RANK                                    
         SLL   RF,28                                                            
         SRL   RF,28               MAKE BINARY                                  
         CH    RF,=H'6'                                                         
         BH    GM060               7 = ALL MARKETS                              
         BE    *+12                                                             
         MH    RF,=H'10'                                                        
         B     GM050                                                            
         LA    RF,100              6 = 100                                      
*                                                                               
GM050    CR    RF,R6                                                            
         BL    GM070                                                            
*                                                                               
GM060    STH   R6,0(R1)            STORE IN MGRP AREA OF MKT LIST               
         STH   R3,2(R1)            MKT NUM                                      
         STC   R4,4(R1)            IND                                          
         LA    R1,5(R1)            NEXT NEW ENTRY                               
         C     R1,MKTLISTX         CHECK LIST FULL                              
         BL    GM070                                                            
         DC    H'0'                                                             
*                                                                               
GM070    LA    R3,1(R3)            BUMP MKT NUM                                 
         LR    R4,R5                                                            
         BCT   R8,GM025                                                         
*                                                                               
GM080    LA    R2,1(R2)            NEXT OLD ENTRY                               
         BCT   R0,GM020                                                         
*                                                                               
         LA    R2,0               MKTLIST NOW COMPLETE                          
         L     RE,MKTLIST         COUNT # OF MKTS IN LIST                       
*                                                                               
GM090    OC    0(5,RE),0(RE)                                                    
         BZ    GM100                                                            
         LA    RE,5(RE)            NEXT MKT                                     
         BCT   R2,GM090                                                         
*                                                                               
GM100    LPR   R2,R2               COUNT                                        
         BZ    EQXIT               NONE FOUND                                   
*                                                                               
         GOTO1 XSORT,DMCB,MKTLIST,(R2),5,5,0    SORT IN MKT RANK ORDER          
*                                                                               
         L     R2,MKTLIST          SET RANKING GROUPS 10-50,100,101             
         SR    RE,RE                                                            
*                                                                               
GM110    LA    RE,10(RE)           NEXT # TO SAVE                               
         LR    R5,RE               END OF THIS GROUP (EX. 1-10 = 10)            
         LA    RF,10               MAX # FOR 1ST 5 GRPS                         
         CH    RE,=H'60'                                                        
         BL    GM120                                                            
         LA    R5,100              END LIMIT (51-100)                           
         LA    RE,60               DISP INTO MKT RANK LITERAL TABLE             
         LA    RF,50               MAX # 51-100                                 
         BE    GM120               = TO 60 TOP 100                              
         LA    RE,70               TOP 101                                      
         LA    R5,4000             MAX # 101+                                   
         LR    RF,R5               END LIMIT (101+)                             
*                                                                               
GM120    OC    0(5,R2),0(R2)       END OF LIST                                  
         BZ    GM900                                                            
         LH    R0,0(R2)            MKT RANK                                     
         CR    R0,R5                                                            
         BH    GM110               MKT IN NEXT GRP                              
         STH   RE,0(R2)            STORE GROUP #                                
         LA    R2,5(R2)            NEXT MKT                                     
         BCT   RF,GM120                                                         
         B     GM110                                                            
*                                                                               
GM900    B     EQXIT                                                            
         EJECT                                                                  
*                                                                               
* ROUTINE TO GET THE SERVICE MARKET NUMBER FOR AN AGENCY MARKET                 
* MARKET TABLE ENTRY IS AGYMKT(2)/ARBMKT(2)/NSIMKT(2)                           
* INPUT :  R3 = AGENCY MARKET NUM                                               
* OUTPUT:  SVCMKT = SERVICE MARKET NUM                                          
*                                                                               
         SPACE                                                                  
GETSMKT  NTR1                                                                   
         LA    R6,MKTAB            SEARCH FOR MARKET IN MARKET TABLE            
*                                                                               
GS030    OC    0(6,R6),0(R6)       TEST FOR END OF TABLE                        
         BZ    GS050               YES - CREATE NEW ENTRY                       
         CLM   R3,3,0(R6)          COMPARE MARKETS                              
         BE    GS060               FOUND                                        
         LA    R6,6(R6)            NEXT MARKET IN TABLE                         
         C     R6,AMKTABX          TEST FOR ROOM IN TABLE                       
         BL    GS030                                                            
         DC    H'0'                                                             
*                                                                               
GS050    L     R8,ADMARKET         READ MARKET RECORD                           
         USING MKTRECD,R8                                                       
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'M'                                                         
         MVC   KEY+1(1),MED                                                     
         CVD   R3,DUB                                                           
         UNPK  KEY+2(4),DUB                                                     
         OI    KEY+5,X'F0'                                                      
         MVC   KEY+6(2),AGY                                                     
         GOTO1 HIGHMKT                                                          
         CLC   KEY(8),0(R8)                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
         STCM  R3,3,0(R6)          STORE AGENCY MARKET IN TABLE                 
         CLI   MKTRS1,C'1'                                                      
         BNE   *+14                                                             
         MVC   2(2,R6),MKTRSM1     ARB MARKET                                   
         B     *+18                                                             
         CLI   MKTRS1,C'0'                                                      
         BNE   *+10                                                             
         MVC   4(2,R6),MKTRSM1     NSI MARKET                                   
         CLI   MKTRS2,C'1'                                                      
         BNE   *+14                                                             
         MVC   2(2,R6),MKTRSM2     ARB MARKET                                   
         B     *+18                                                             
         CLI   MKTRS2,C'0'                                                      
         BNE   *+10                                                             
         MVC   4(2,R6),MKTRSM2     NSI MARKET                                   
*                                                                               
GS060    L     RE,ADCLT            SERVICE IN CLIENT HEADER                     
         MVC   SVCMKT,4(R6)        NSI                                          
         CLI   CPROF+3-CLTHDR(RE),C'0'                                          
         B     *+10                FORCE TO NSI ALWAYS                          
         MVC   SVCMKT,2(R6)        ARB                                          
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
* ROUTINE TO GET THE RANK TABLE                                                 
* OUTPUT:   THISYSDL = A(RANK TABLE)                                            
* COMMENTS: THISYSE = A(YEAR/SERVICE ENTRY TABLE)                               
*                     ENTRY = YEAR(1)/SERVICE(1)/AL4(MARKET LIST)               
*           THISYSL = A(YEAR/SERVICE MARKET LIST)                               
*                     ENTRY = SVCMKT(2)                                         
*                                                                               
         SPACE                                                                  
GETRKTAB NTR1                                                                   
*        PACK  DUB,QEND(2)         BOOK YEAR = REQ END YEAR - 1                 
*        CVB   R6,DUB                                                           
*        BCTR  R6,0                                                             
*  Y2K STUFF                                                                    
         GOTO1 DATCON,DMCB,(0,QEND),(3,DUB)                                     
         ZIC   R6,DUB                                                           
         BCTR  R6,0                                                             
*                                                                               
         STC   R6,WORK                                                          
         GOTO1 DATCON,DMCB,(5,0),(3,FULL)  GET TODAY'S DATE                     
         STC   R6,FULL+2                                                        
         MVI   FULL+3,11                                                        
         CLC   FULL(2),FULL+2      COMPARE TODAY'S Y/M TO NOV OF                
         BH    *+10                BOOK YEAR                                    
         BCTR  R6,0                NOT LATER - BACK UP ONE YEAR                 
         STC   R6,WORK                                                          
         L     R1,ADCLT            SERVICE IN CLIENT HEADER                     
         MVI   WORK+1,C'N'                                                      
         CLI   CPROF+3-CLTHDR(R1),C'0'                                          
         BE    *+8                 ALWAYS NSI                                   
         MVI   WORK+1,C'A'                                                      
         LA    R6,YSMKTAB         LOOK FOR YEAR/SERVICE IN YR/SVC TABLE         
         LA    R0,6                                                             
*                                                                               
GR070    CLI   0(R6),0             TEST FOR EMPTY SLOT                          
         BE    GR090                                                            
         CLC   0(2,R6),WORK        TEST FOR MATCH YR/SVC                        
         BNE   GR080                                                            
         ST    R6,THISYSE          THIS YR/SVC ENTRY                            
         ICM   R7,15,2(R6)                                                      
         ST    R7,THISYSL          THIS YR/SVC LIST                             
         B     GR100                                                            
*                                                                               
GR080    LA    R6,6(R6)                                                         
         BCT   R0,GR070                                                         
         L     R6,ANXTYS           TABLE FULL - GET A(NEXT ENTRY)               
         EJECT                                                                  
*                                                                               
* BUILD LIST OF MARKETS FOR REQUEST YEAR / SERVICE                              
*                                                                               
         SPACE                                                                  
GR090    MVC   0(2,R6),WORK        BUILD NEW YR/SVC ENTRY                       
         ICM   R7,15,2(R6)                                                      
         XCEF  (R7),1000                                                        
         ST    R6,THISYSE          THIS YR/SVC ENTRY                            
         ST    R7,THISYSL          THIS YR/SVC LIST                             
         LA    RE,6(R6)            SET UP ADDRESS OF NEXT ENTRY                 
         C     RE,=A(YSMKTABX)                                                  
         BL    *+8                                                              
         LA    RE,YSMKTAB                                                       
         ST    RE,ANXTYS                                                        
*                                                                               
         L     R8,ADBLOCK          BUILD MARKET LIST FOR THIS YR/SVC            
         USING DBLOCK,R8                                                        
         XC    0(256,R8),0(R8)                                                  
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBGETMKB                                                 
         MVI   DBSELMED,C'T'                                                    
         MVC   DBSELSRC(1),1(R6)                                                
         MVI   DBSELSRC,C'N'       FORCE NSI                                    
         MVC   DBSELBK(1),0(R6)                                                 
         MVI   DBSELBK+1,11        NOVEMBER BOOK                                
         MVC   DBCOMFCS,ACOMFACS                                                
         MVC   DBAREC,ADEMREC                                                   
         MVC   THISBOOK,DBSELBK    SAVE FOR NEXT LOOKUP                         
         XC    THISCNT,THISCNT                                                  
         GOTO1 DEMAND,DMCB,ADBLOCK,SVMRKT                                       
         CLC   THISCNT,=H'200'     IF MOST OF NOV SWEEP DONE                    
         BH    GR100               USE IT ELSE                                  
*----->  CLI   DBERROR,X'10'       IF NO NOV                                    
*----->  BNE   GR100                                                            
         ZIC   RE,DBSELBK          JUST DO THE PREV YEAR                        
         BCTR  RE,0                                                             
         STC   RE,DBSELBK                                                       
         MVC   THISBOOK,DBSELBK                                                 
         GOTO1 DEMAND,DMCB,ADBLOCK,SVMRKT                                       
         EJECT                                                                  
*                                                                               
* BUILD LIST OF MARKET UNIVERSES FOR YEAR/SERVICE/DEMO AND SORT                 
* ENTRY IS YEAR(1)/SERVICE(1)/DEMO(3)/AL3(UNIVERSE LIST)                        
* UNIVERSE LIST ENTRY IS UNIVERSE(4)/SVCMKT(2)                                  
*                                                                               
         SPACE                                                                  
GR100    MVC   WORK(2),0(R6)       YR/SVC                                       
         L     R1,ADEST            DEMO FROM ESTIMATE HEADER                    
         MVC   WORK+2(3),EDEMLST-ESTHDR(R1)                                     
         CLI   QDEMOVRD,C'Y'       CHECK FOR DEMO MENU OVERRIDE                 
         BNE   GR130                                                            
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING DMNRECD,R8                                                       
         MVC   DMNKTYP,=X'0D26'                                                 
         MVC   DMNKAGMD,BAGYMD                                                  
         MVC   DMNKCODE,QDEMNOS    MENU CODE                                    
         GOTO1 READ                                                             
         L     R2,AREC                                                          
         MVC   AREC,AMENREC                                                     
         GOTO1 GET                                                              
         ST    R2,AREC                                                          
         L     R8,AMENREC                                                       
         LA    R8,24(R8)                                                        
         SR    R0,R0                                                            
*                                                                               
GR110    CLI   0(R8),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R8),5                                                          
         BE    GR120                                                            
         IC    R0,1(R8)                                                         
         AR    R8,R0                                                            
         B     GR110                                                            
*                                                                               
         USING DMNEL05,R8                                                       
GR120    MVC   WORK+2(3),DMNRTN    DEMO FOUND                                   
         DROP  R8                                                               
*                                                                               
GR130    MVC   DEMO,WORK+2         SAVE THE DEMO CODE                           
         MVI   DEMO+1,C'U'         UNIVERSE                                     
         LA    R6,DEMKTAB          LOOK FOR YR/SVC/DEMO IN TABLE                
         LA    R0,6                                                             
*                                                                               
GR140    CLI   0(R6),0             TEST FOR EMPTY SLOT                          
         BE    GR160                                                            
         CLC   0(5,R6),WORK        TEST FOR MATCH YR/SVC/DEMO                   
         BNE   GR150                                                            
         SR    R7,R7                RANK LIST FOUND - EXIT                      
         ICM   R7,7,5(R6)                                                       
         B     GR300                                                            
*                                                                               
GR150    LA    R6,8(R6)                                                         
         BCT   R0,GR140                                                         
         L     R6,ANXTDEM          TABLE FULL - GET A(NEXT ENTRY)               
*                                                                               
GR160    MVC   0(5,R6),WORK        BUILD NEW YR/SVC ENTRY                       
         SR    R7,R7                                                            
         ICM   R7,7,5(R6)                                                       
         XCEF  (R7),2000           CLEAR THE MRKT LIST                          
         ST    R7,THISYSDL         THIS YR/SVC/DEMO LIST                        
         LA    RE,8(R6)            SET UP ADDRESS OF NEXT ENTRY                 
         C     RE,=A(DEMKTABX)                                                  
         BL    *+8                                                              
         LA    RE,DEMKTAB                                                       
         ST    RE,ANXTDEM                                                       
*                                                                               
GR170    L     R8,THISYSL          READ THE UNIVERSES                           
*                                                                               
GR180    OC    0(2,R8),0(R8)       TEST FOR END OF MARKET LIST                  
         BZ    GR200                                                            
         L     R1,ADBLOCK                                                       
         USING DBLOCK,R1                                                        
         XC    0(256,R1),0(R1)                                                  
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBGETTOT                                                 
         MVI   DBSELMED,C'T'                                                    
         MVC   DBSELSRC(1),1(R6)                                                
         MVC   DBSELBK(1),0(R6)                                                 
         MVI   DBSELBK+1,11        NOVEMBER BOOK                                
         MVC   DBSELBK,THISBOOK    OVERRIDE IF REQUIRED                         
         MVC   DBCOMFCS,ACOMFACS                                                
         MVC   DBAREC,ADEMREC                                                   
         MVC   DBSELRMK,0(R8)                                                   
         MVI   DBSELDAY,X'40'      SET TO READ MON 5-515P                       
         MVC   DBSELTIM(2),=H'1700'                                             
         MVC   DBSELTIM+2(2),=H'1715'                                           
         GOTO1 DEMAND,DMCB,ADBLOCK,SVUNIV                                       
         OC    0(4,R7),0(R7)       TEST FOR ZERO UNIVERSE                       
         BNZ   *+10                                                             
         MVC   0(4,R7),=F'-1'      YES - FORCE TO 1,000                         
         MVC   4(2,R7),0(R8)       MOVE MARKET TO TABLE                         
         LA    R7,6(R7)                                                         
         ST    R7,THISYSDL                                                      
*                                                                               
GR190    LA    R8,2(R8)            NEXT MARKET                                  
         B     GR180                                                            
         EJECT                                                                  
*                                                                               
* COUNT THE MARKETS IN UNIVERSE LIST, SORT AND INSERT RANK NUMBERS              
* TO PRODUCE THE RANK LIST                                                      
*                                                                               
         SPACE                                                                  
GR200    SR    R7,R7               COUNT THE MARKETS                            
         ICM   R7,7,5(R6)                                                       
         LR    RE,R7                                                            
         SR    R8,R8                                                            
*                                                                               
GR210    OC    0(6,RE),0(RE)                                                    
         BZ    GR220                                                            
         LA    RE,6(RE)                                                         
         BCT   R8,GR210                                                         
*                                                                               
GR220    LPR   R8,R8                                                            
         BZ    NEQXIT                                                           
         GOTO1 XSORT,DMCB,(R7),(R8),6,6,0    SORT IN REVERSE UNIVERSE           
         LA    R1,1                                                             
         LR    RE,R7               REPLACE UNIVERSES WITH RANK NUMBERS          
         LA    R8,500                                                           
*                                                                               
GR230    OC    0(6,RE),0(RE)                                                    
         BZ    GR300                                                            
         STCM  R1,15,0(RE)                                                      
         LA    R1,1(R1)                                                         
         LA    RE,6(RE)                                                         
         BCT   R8,GR230                                                         
*                                                                               
GR300    ST    R7,THISYSDL                                                      
         B     EQXIT                                                            
         EJECT                                                                  
*                                                                               
* HOOK TO SAVE MARKET NUMBER FROM RATING SERVICE MARKET RECORD                  
*                                                                               
         DS    0D                                                               
         USING *,RF                                                             
SVMRKT   NTR1  BASE=SPGMRB                                                      
         LM    R9,RC,SPGMR9                                                     
         DROP  RF                                                               
         L     R8,ADBLOCK                                                       
         USING DBLOCK,R8                                                        
         L     R6,DBAREC                                                        
         USING DMKEY,R6                                                         
         OC    DMRMKT,DMRMKT                                                    
         BZ    EXIT                                                             
         L     R7,THISYSL          A(THIS YR/SVC MARKET LIST)                   
         LA    R0,250                                                           
*                                                                               
SM010    OC    0(2,R7),0(R7)                                                    
         BZ    SM020                                                            
         CLC   DMRMKT,0(R7)        CHECK FOR DUPLICATE                          
         BE    EXIT                                                             
         LA    R7,2(R7)                                                         
         BCT   R0,SM010                                                         
         DC    H'0'                                                             
*                                                                               
SM020    MVC   0(2,R7),DMRMKT      SAVE SERVICE MARKET NUM                      
         LH    RE,THISCNT          COUNT THE MARKETS                            
         LA    RE,1(RE)                                                         
         STH   RE,THISCNT                                                       
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
* HOOK TO SAVE MARKET UNIVERSE IN MARKET TABLE                                  
*                                                                               
         DS    0D                                                               
         USING *,RF                                                             
SVUNIV   NTR1  BASE=SPGMRB                                                      
         LM    R9,RC,SPGMR9                                                     
         DROP  RF                                                               
         L     R8,ACOMFACS                                                      
         USING COMFACSD,R8                                                      
         GOTO1 CDEMOUT,DMCB,(C'D',DEMO),ADBLOCK,FULL                            
         DROP  R8                                                               
         L     RE,THISYSDL                                                      
         L     R1,FULL                                                          
         LNR   R1,R1                                                            
         STCM  R1,15,0(RE)         SAVE UNIVERSE IN MARKET TABLE                
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
PARM     DS    F                                                                
SPGMR9   DS    F                                                                
SPGMRA   DS    F                                                                
SPGMRB   DS    F                                                                
SPGMRC   DS    F                                                                
AMKTABX  DC    A(MKTABX)                                                        
ADEMREC  DC    A(DEMREC)                                                        
AMENREC  DC    A(MENREC)                                                        
ANXTYS   DC    A(YSMKTAB)                                                       
ANXTDEM  DC    A(DEMKTAB)                                                       
THISYSE  DS    A                                                                
THISYSL  DS    A                                                                
THISYSDL DS    A                                                                
SVCMKT   DS    XL2                                                              
DEMO     DS    XL3                                                              
FIRST    DS    CL1                                                              
THISBOOK DS    CL2                                                              
THISCNT  DS    H                                                                
XFF      DC    16X'FF'                                                          
*                                                                               
         DS    0D                                                               
         DC    CL8'*YSMKTAB'                                                    
YSMKTAB  DC    XL2'0000',AL4(YSMKTS)                                            
         DC    XL2'0000',AL4(YSMKTS+1000)                                       
         DC    XL2'0000',AL4(YSMKTS+2000)                                       
         DC    XL2'0000',AL4(YSMKTS+3000)                                       
         DC    XL2'0000',AL4(YSMKTS+4000)                                       
         DC    XL2'0000',AL4(YSMKTS+5000)                                       
YSMKTABX EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*DEMKTAB'                                                    
DEMKTAB  DC    XL5'00',AL3(DEMKTS)                                              
         DC    XL5'00',AL3(DEMKTS+3000)                                         
         DC    XL5'00',AL3(DEMKTS+6000)                                         
         DC    XL5'00',AL3(DEMKTS+9000)                                         
         DC    XL5'00',AL3(DEMKTS+12000)                                        
         DC    XL5'00',AL3(DEMKTS+15000)                                        
DEMKTABX EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'**MKTAB*'                                                    
MKTAB    DC    500XL6'00'                                                       
MKTABX   EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*YSMKTS*'                                                    
YSMKTS   DS    6XL1000'00'                                                      
*                                                                               
         DS    0D                                                               
         DC    CL8'*DEMKTS*'                                                    
DEMKTS   DS    6XL3000'00'                                                      
*                                                                               
         DS    0D                                                               
         DC    CL8'*DEMREC*'                                                    
DEMREC   DS    1500X                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*MENREC*'                                                    
MENREC   DS    300X                                                             
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
DBDUMMY  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DEDEMFILE                                                      
       ++INCLUDE SPGENDMN                                                       
MKTRECD  DSECT                                                                  
       ++INCLUDE SPGENMKT                                                       
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027SPGETMKTRK05/01/02'                                      
         END                                                                    
