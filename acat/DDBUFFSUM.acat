*          DATA SET DDBUFFSUM  AT LEVEL 005 AS OF 05/01/02                      
*CATALP BUFFSUM                                                                 
         TITLE 'BUFFSUM - SUMMARISE BUFFALO RECORDS'                            
BUFFSUM  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 BUFFSUMX-BUFFSUMD,**BUFF**,CLEAR=YES                             
         USING BUFFSUMD,RC         RC=A(LOCAL W/S)                              
         LR    R2,R1               R2=A(USER PLIST)                             
         L     RA,0(R2)            P1=A(BUFFALOC)                               
         USING BUFFALOD,RA                                                      
         MVC   USERRD,4(RD)        SAVE REGISTER LINK-BACK                      
         MVC   BREAKPOS,4(R2)      SAVE BREAK POSITION & LENGTH                 
         MVC   BREAKLEN,7(R2)                                                   
         MVC   AFILTS,8(R2)        SAVE A(USER HOOK) AND A(FILTERS)             
         MVC   AHOOK,12(R2)                                                     
         SR    R0,R0                                                            
         ICM   R0,1,0(R2)                                                       
         BNZ   *+8                                                              
         LA    R0,1                DEFAULT ROW NUMBER IS 1                      
         GOTO1 ,BUFFPARM,=C'HIGH',BUFFALOD,RECORD1,(R0)                         
         MVI   EOF,C'N'                                                         
*                                  BUILD FILTER CONTROL BLOCK                   
         ICM   RF,15,AFILTS                                                     
         BZ    BUFFSX                                                           
         SR    R1,R1                                                            
BUFFS2   CLI   0(RF),X'FF'         TEST E-O-T                                   
         BE    BUFFSX                                                           
         IC    R1,0(RF)                                                         
         LA    RE,POSFLT1(R1)                                                   
         MVI   0(RE),X'FF'         SET X'FF' FOR KEY POSITION USED              
         IC    R1,1(RF)                                                         
         LA    RF,2(RF,R1)         BUMP TO NEXT FILTER TABLE ENTRY              
         B     BUFFS2                                                           
*                                                                               
BUFFSX   B     BUFF2                                                            
         EJECT                                                                  
* READ BUFFALO RECORDS AND SUMMARISE                                            
*                                                                               
BUFF0    LA    R0,=C'SEQ'          SET SEQUENTIAL MODE                          
         ST    R0,BUFFPARM                                                      
BUFF2    GOTO1 =V(BUFFALO),BUFFPARM                                             
         CLI   8(R1),0             TEST ANY ERRORS                              
         BE    BUFF4               NO                                           
         CLI   8(R1),X'80'         TEST E-O-F PASSED                            
         BNE   *+12                                                             
         MVI   EOF,C'Y'                                                         
         B     BUFF8                                                            
         DC    H'0'                FUNNY BUFFALO ERROR RETURNED                 
*                                  APPLY FILTERS TO RECORD                      
BUFF4    ICM   RF,15,AFILTS                                                     
         BZ    BUFF8                                                            
         MVC   POSFLT2,POSFLT1     COPY FILTER CONTROL BLOCK                    
BUFF5    CLI   0(RF),X'FF'         TEST E-O-T                                   
         BNE   BUFF6                                                            
         OC    POSFLT2,POSFLT2     TEST ALL FILTERS PASSED                      
         BNZ   BUFF0               NO - GET ANOTHER RECORD                      
         B     BUFF8                                                            
BUFF6    ZIC   R1,0(RF)            R1=POSITION IN BUFFALO KEY                   
         ZIC   RE,1(RF)                                                         
         BCTR  RE,0                RE=LENGTH OF FILTER-1                        
         LA    R3,POSFLT2(R1)      POINT TO FILTER POSITION                     
         CLI   0(R3),0             TEST FILTER PASSED                           
         BE    BUFF7                                                            
         LA    R1,RECORD1(R1)      R1=A(KEY DATA)                               
         EX    RE,*+8              COMPARE KEY FILTER WITH ACTUAL KEY           
         B     *+10                                                             
         CLC   0(0,R1),2(RF)                                                    
         BNE   BUFF7                                                            
         MVI   0(R3),0             SET FILTER IS VALID                          
BUFF7    LA    RF,3(RE,RF)                                                      
         B     BUFF5                                                            
*                                  ADD RECORDS OR PASS RECORD2 TO USER          
BUFF8    CLI   EOF,C'Y'                                                         
         BE    BUFF12                                                           
         OC    RECORD2(256),RECORD2 TEST FIRST TIME                             
         BZ    BUFF14                                                           
         CLI   BREAKLEN,0          ZERO CONTROL BREAK LENGTH                    
         BE    BUFF12              MEANS DON'T SUMMARISE                        
         ZIC   RE,BREAKPOS         SET STARTING POSITION                        
         LA    R4,RECORD1                                                       
         AR    R4,RE                                                            
         LA    RE,RECORD2(RE)                                                   
         SR    R1,R1                                                            
         ICM   R1,1,BREAKLEN       R1=CONTROL BREAK POSITION IN KEY             
         BZ    BUFF16                                                           
         BCTR  R1,0                                                             
         EX    R1,*+8              MATCH LAST KEY WITH CURRENT                  
         B     *+10                                                             
         CLC   0(0,R4),0(RE)                                                    
         BE    BUFF16                                                           
BUFF12   OC    RECORD2(256),RECORD2                                             
         BZ    BUFF14                                                           
         LA    RE,BUFF14                                                        
         NTR1  ,                   CALL USER HOOK PASSING A(REC) IN R1          
         L     RF,AHOOK                                                         
         L     RE,USERRD                                                        
         LR    R0,RE               PASS USER'S RD VALUE IN R0                   
         LA    R1,RECORD2                                                       
         LM    R2,RC,28(RE)        RESTORE CALLERS R2 THRU RC                   
         BASR  RE,RF                                                            
         XIT1  ,                                                                
BUFF14   CLI   EOF,C'Y'                                                         
         BE    BUFFX                                                            
         MOVE  (RECORD2,1000),RECORD1                                           
         B     BUFF0               GET ANOTHER RECORD                           
         EJECT                                                                  
* ADD RECORD1 VALUES TO RECORD2                                                 
*                                                                               
BUFF16   L     R1,BUFFLCOM         SET COMMENT LENGTH                           
         A     R1,BUFFLKEY         SET KEY LENGTH                               
         LA    RE,RECORD1(R1)      DETAIL COUNTERS                              
         LA    RF,RECORD2(R1)      SUMMARY COUNTERS                             
         L     R0,BUFFCOLS         NUMBER OF COUNTERS                           
         CLI   BUFFFLVR,C'D'       DATA ONLY                                    
         BE    BUFF0                                                            
         CLI   BUFFFLVR,C'P'       PACKED ACCUMULATORS                          
         BE    BUFF20                                                           
*                                  ADD BINARY VALUES                            
BUFF18   ICM   R1,15,0(RE)                                                      
         ICM   R3,15,0(RF)                                                      
         AR    R1,R3                                                            
         STCM  R1,15,0(RF)         SAVE SUMMARY VALUES                          
         LA    RE,4(RE)            SETUP NEXT COUNTER                           
         LA    RF,4(RF)                                                         
         BCT   R0,BUFF18           DO AGAIN                                     
         B     BUFF0               GET ANOTHER RECORD                           
*                                  ADD PACKED VALUES                            
BUFF20   AP    0(8,RF),0(8,RE)                                                  
         LA    RE,8(RE)            SETUP NEXT COUNTER                           
         LA    RF,8(RF)                                                         
         BCT   R0,BUFF20           DO AGAIN                                     
         B     BUFF0               GET ANOTHER RECORD                           
*                                                                               
BUFFX    XIT1                                                                   
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         SPACE 1                                                                
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
BUFFSUMD DSECT                                                                  
DUB      DS    D                                                                
USERRD   DS    A                   USER'S REGISTER SAVE AREA                    
AFILTS   DS    A                   A(BUFFKEY FILTERS TABLE OR ZERO)             
AHOOK    DS    A                   A(USER'S HOOK ROUTINE)                       
BUFFPARM DS    6F                  PARM LIST TO CALL BUFFALO                    
BREAKPOS DS    X                   CONTROL BREAK POSITION IN BUFFKEY            
BREAKLEN DS    X                   CONTROL BREAK LENGTH IN BUFFKEY              
EOF      DS    C                   C'Y'=END OF BUFFALO FILE                     
POSFLT1  DS    XL256               POSITION FILTERS (INITIAL)                   
POSFLT2  DS    XL256               POSITION FILTERS (WORK AREA)                 
RECORD1  DS    1000C               BUFFALO I/O AREA                             
RECORD2  DS    1000C               BUFFALO SUMMARIZED RECORD                    
BUFFSUMX EQU   *                                                                
         SPACE 1                                                                
* DDBUFFALOD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDBUFFSUM 05/01/02'                                      
         END                                                                    
