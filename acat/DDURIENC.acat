*          DATA SET DDURIENC   AT LEVEL 001 AS OF 11/01/18                      
*CATALP URIENC                                                                  
                                                                                
* P1 B0     LENGTH OF STRING TO CONVERT                                         
* P1 B1-3   A(STRING TO CONVERT MAX 20 CHARS)                                   
* P2 B0     PARAMETER 1                                                         
*    X'80'  IGNORE TRAILING SPACES                                              
* P2 B1-3   A(OUTPUT FIELD)                                                     
* P3 B0     NEW LENGTH                                                          
*                                                                               
* RETURNS NEW LENGTH IN P2 B2-3                                                 
*                                                                               
*MPEN 001 30OCT18 <DSRD-15233> INITIAL VERSION                                  
*                                                                               
                                                                                
         TITLE 'URI ENCODE A STRING'                                            
URIENC   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**URIE**                                                       
         LR    RA,R1               RA=A(PARAM LIST)                             
         USING PARMD,RA                                                         
         LLC   R4,PARLEN           L'STRING                                     
         CHI   R4,MAXLEN                                                        
         JH    URIENCX                                                          
         XR    R3,R3                                                            
         ICM   R3,7,PARINP         R3=A(STRING)                                 
         SHI   R4,1                                                             
         OC    0(0,R3),0(R3)       Any string to convert?                       
         EX    R4,*-6                                                           
         BZ    URIENCX                                                          
         XR    R2,R2                                                            
         ICM   R2,7,PAROUT         Move input field to output area              
         MVC   0(0,R2),0(R3)                                                    
         EX    R4,*-6                                                           
         TM    PARIND,PARIGS       Ignoring trailing spaces?                    
         JZ    URIENC04                                                         
         AHI   R4,1                If yes then find true length of              
         LR    RF,R2               string                                       
         AR    RF,R4                                                            
*                                                                               
URIENC02 CLI   0(RF),C' '          Find last significant character              
         JH    URIENC04                                                         
         BCTR  R4,0                                                             
         JCT   RF,URIENC02                                                      
*                                                                               
URIENC04 AHI   R4,1                R2=START OF STRING                           
         LR    R3,R2               R3=CURRENT POS IN STRING                     
         LR    R7,R4               R4=LENGTH OF STRING                          
*                                  R7=STRING LENGTH COUNTER                     
         USING CNVTABD,R5                                                       
URIENC06 LA    R5,CNVTAB           R5=TABLE FOR CONVERSION                      
*                                                                               
URIENC08 CLI   0(R5),X'FF'         CHECK FOR MATCH ON CHARACTER IN              
         BE    URIENC16            CONVERT TABLE                                
         CLC   0(1,R3),CNVTFRM                                                  
         BE    URIENC10                                                         
         LA    R5,CNVTABL(R5)                                                   
         B     URIENC08                                                         
*                                                                               
URIENC10 LA    RF,0(R2,R4)         POINT RF TO END OF STRING                    
         BCTR  RF,0                AND SHUNT EVERYTHING UP 3                    
*                                  CHARACTERS TO CONVERT                        
URIENC12 CR    RF,R3                                                            
         JNH   URIENC14                                                         
         LA    R1,L'CNVTTOC-1(RF)                                               
         MVC   0(1,R1),0(RF)                                                    
         BCT   RF,URIENC12                                                      
*                                                                               
URIENC14 MVC   0(L'CNVTTOC,R3),CNVTTOC                                          
         AHI   R3,L'CNVTTOC-1                                                   
         AHI   R4,L'CNVTTOC-1                                                   
*                                                                               
URIENC16 LA    R3,1(R3)                                                         
         BCT   R7,URIENC06                                                      
*                                                                               
URIENCX  STC   R4,PARNLN                                                        
URIENCXX XMOD1 1                                                                
         SPACE 1                                                                
***********************************************************************         
* CONVERT TABLE                                                       *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
MAXLEN   EQU   20                                                               
*                                                                               
CNVTAB   DC    X'40',C'%20'                        Space                        
CNVTABL  EQU   *-CNVTAB                                                         
         DC    X'5A',C'%21'                        !                            
         DC    X'7F',C'%22'                        "                            
         DC    X'7B',C'%23'                        #                            
         DC    X'4A',C'%24'                        Â›                            
         DC    X'6C',C'%25'                        %                            
         DC    X'50',C'%26'                        &                            
         DC    X'7D',C'%27'                        '                            
         DC    X'4D',C'%28'                        (                            
         DC    X'5D',C'%29'                        )                            
         DC    X'5C',C'%2A'                        *                            
         DC    X'4E',C'%2B'                        +                            
         DC    X'6B',C'%2C'                        ,                            
         DC    X'60',C'%2D'                        -                            
         DC    X'4B',C'%2E'                        .                            
         DC    X'61',C'%2F'                        /                            
         DC    X'E0',C'%5C'                        \                            
         DC    X'6D',C'%5F'                        _                            
         DC    X'C0',C'%7B'                        {                            
         DC    X'4F',C'%7C'                        |                            
         DC    X'D0',C'%7D'                        }                            
         DC    X'A1',C'%7E'                        ~                            
CNVTABX  DC    X'FF'                                                            
         SPACE 1                                                                
***********************************************************************         
* Parameter DSECT                                                     *         
***********************************************************************         
         SPACE 1                                                                
PARMD    DSECT                                                                  
PARMS    DS    0X                                                               
PARLEN   DS    XL1                 OLD LENGTH                                   
PARINP   DS    AL3                 A(INPUT)                                     
PARIND   DS    XL1                 INDICATOR #1                                 
PARIGS   EQU   X'80'               IGNORE TRAILING SPACES                       
PAROUT   DS    AL3                 A(OUTPUT)                                    
PARNLN   DS    XL1                 NEW LENGTH                                   
PARMSLEN EQU   *-PARMS                                                          
***********************************************************************         
* Convert table DSECT                                                 *         
***********************************************************************         
         SPACE 1                                                                
CNVTABD  DSECT                                                                  
CNVTFRM  DS    XL1                 FROM CHARACTER                               
CNVTTOC  DS    CL3                 TO CHARACTER                                 
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDURIENC  11/01/18'                                      
         END                                                                    
