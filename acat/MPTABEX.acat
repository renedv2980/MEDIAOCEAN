*          DATA SET MPTABEX    AT LEVEL 035 AS OF 05/01/02                      
*CATALP TABEX                                                                   
         TITLE 'TABEX- CROSS-TAB EXTRACT MODULE'                                
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*   NOTE- IF WE ALLOW WEIGHTED TARGETS AS BASES (AND MAYBE EVEN                 
*         IF NOT) THERE MAY BE A PROBLEM WITH SOME OF THE TARGET                
*         DESCRIPTOR FIELDS IN MPQBLK IF WEIGHTED TARGS ARE USED                
*         FOR ROWS/COLUMNS.                                                     
*         ALSO- THE XMULT LOGIC IN MPQFAC DOESN'T AUTOMATICALLY                 
*         APPLY THE TARGET WEIGHTS                                              
*                                                                               
*   NOTE- THE WAY OF DEALING WITH NUMERIC QSPEC QUESTIONS IS                    
*         A BIT FLAKY                                                           
*                                                                               
***********************************************************************         
*                                                                               
*        PARAMETER LIST                                                         
*        --------------                                                         
*                                                                               
* PARAM1     A(GEND)                                                            
* PARAM2     A(CONTROL BLOCK) -MPTXCTLD                                         
*                                                                               
*                                                                               
         PRINT NOGEN                                                            
TABEX    CSECT                                                                  
         NMOD1 WORKL,**TABX**,RR=R2                                             
         SPACE 2                                                                
         LR    R8,RC               A(LOCAL W/S)                                 
         USING WORKD,R8                                                         
         L     RC,0(R1)            A(GEND)                                      
         USING GEND,RC                                                          
         L     RA,4(R1)            A(CONTROL DSECT)                             
         USING TXCTLD,RA                                                        
         ST    RC,TXAGEND          SAVE A(GEND)                                 
         ST    R2,TXRELO                                                        
         L     R7,=V(BINSRCH)                                                   
         AR    R7,R2                                                            
         ST    R7,TXBINSCH                                                      
         L     R7,=V(COVAIL)                                                    
         AR    R7,R2                                                            
         ST    R7,TXCOVAIL                                                      
         L     R7,=A(GENSUBS)      GENERAL SUBROUTINES                          
         A     R7,TXRELO                                                        
         USING GENSUBS,R7          ARE ADDRESSED VIA R7                         
         LA    R9,IO               POINT R9 TO SYSTEM WORK AREA                 
         AH    R9,=Y(IOLEN)                                                     
         USING SYSD,R9                                                          
*                                                                               
         OC    TXCORE,TXCORE          TEST NEED TO GET CORE                     
         BNZ   RR16                                                             
*                                                                               
         L     RE,=A(500*1024)           500K SHOULD DO IT                      
         ST    RE,DMCB+4                 MINIMUM                                
         ST    RE,DMCB+8                 MAXIMUM                                
         GOTO1 TXCOVAIL,DMCB,C'GET'                                             
         L     R1,DMCB+4                                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   0(8,R1),=C'*TXCORE*'                                             
         LA    R1,8(R1)                                                         
         ST    R1,TXCORE               SET START OF TABEX CORE                  
         L     RF,DMCB+8               AMOUNT OBTAINED                          
         SH    RF,=H'16'                                                        
         ST    RF,TXCORLEN                                                      
         AR    RF,R1                                                            
         ST    RF,TXCOREX              SET END                                  
         MVC   0(8,RF),=C'*TXCORX*'                                             
*                                                                               
*                                  SET DUMP END TO INCLUDE NEW CORE             
         CLI   TXDMPOPT,C'F'       IF FULL DUMP REQ'D                           
         BNE   RR16                                                             
         L     R2,ATWA                                                          
         L     R2,TWAMASTC-T510FFD(R2)   A(MASTC)                               
         USING MASTD,R2                                                         
         L     RF,TXCOREX                                                       
         LA    RF,8(RF)                                                         
         ST    RF,MCDMPEND         SET NEW DUMP END                             
         DROP  R2                                                               
*                                                                               
*                                  FOR EACH REQUEST                             
*                                  ----------------                             
RR16     DS    0H                                                               
         GOTO1 =A(CORMAP),RR=TXRELO  SET CORE MAP                               
         GOTO1 =A(SETROWS),RR=TXRELO SET ROW TABLE                              
         GOTO1 =A(SETCOLS),RR=TXRELO SET COLUMN TABLE                           
*                                  CLEAR MATRIX AREA                            
         L     R1,TXCOLS           NO OF COLS                                   
         M     R0,TXROWS           X NO OR ROWS                                 
         SLL   R1,4                X 16 = LENGTH TO CLEAR                       
         L     R0,TXMTRX                                                        
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R4,WVPARS+4           CLEAR WAVE STATUS                          
         ICM   R5,15,WVPARS+8        FOR BCT                                    
         BZ    RR19                                                             
         USING WVTABD,R4                                                        
*                                                                               
RR18     DS    0H                                                               
         NI    WVSTAT,X'3F'        CLEAR THIS TIME ACTIVE AND DONE              
         A     R4,WVPARS+12        ENTRY LENGTH                                 
         BCT   R5,RR18                                                          
         DROP  R4                                                               
*                                                                               
RR19     DS    0H                                                               
         XC    TXBASE,TXBASE                                                    
         L     R2,TXBASR           BASE RECORD                                  
         USING QTGREC,R2                                                        
         LTR   R2,R2                                                            
         BZ    *+10                                                             
         MVC   TXBASE,QTGKTRG      SET THIS BASE CODE                           
         DROP  R2                                                               
*                                                                               
         L     R6,VMPQBLK          SET FILTER LIST                              
         USING MPQBLKD,R6          NOTE- NONE SET ACTIVE                        
         LA    RF,FILTLST                                                       
         ST    RF,MPQBFLST                                                      
         XC    FILTLST,FILTLST                                                  
         MVC   FILTLST(4),BASBV    BASE VECTOR                                  
         LA    R0,MAXFILTS                                                      
         L     R1,TXFLTVS          FILTER VECTORS                               
*                                                                               
RR20     DS    0H                                                               
         LA    RF,4(RF)                                                         
         ST    R1,0(RF)            SET ADDRESS                                  
         A     R1,BVLEN                                                         
         BCT   R0,RR20                                                          
*                                                                               
         OI    0(RF),X'80'         SET EOL                                      
*                                                                               
         XC    NWAVES,NWAVES                                                    
         XC    BWVPOP,BWVPOP                                                    
         XC    BWVSAM,BWVSAM                                                    
*                                                                               
         CLI   SURWMRGE,C'Y'       FOR MERGED WAVE SURVEYS                      
         BNE   RR22                                                             
         MVC   THISWAV,=2X'FF'     SET NO WAVE                                  
         BAS   RE,PROCWAV          AND PROCESS                                  
         BNE   RR80                ERROR                                        
         B     RR30                DONE                                         
*                                                                               
RR22     DS    0H                                                               
         L     R4,TXWAVLST         ELSE DO EACH WAVE                            
*                                                                               
RR22D    DS    0H                                                               
         CLI   0(R4),0             EOL                                          
         BE    RR22F                                                            
         MVC   THISWAV,0(R4)                                                    
         BAS   RE,PROCWAV                                                       
         BNE   RR80                ERROR                                        
         LA    R4,2(R4)                                                         
         B     RR22D                                                            
*                                                                               
RR22F    DS    0H                                                               
RR30     DS    0H                    AFTER ALL WAVES PROCESSED                  
         GOTO1 =A(ARSET),RR=TXRELO   SET ARRAY DATA                             
*                                                                               
RR80     DS    0H                                                               
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         CLI   TXERROR,0                                                        
         BE    RR84                                                             
*                                                                               
         CLC   TXERROR,MPQBERR     MESSAGE WAS SET BY MPQFAC                    
         BE    RR84                                                             
*                                  SET ERROR MESSAGE                            
         L     R2,=A(ERRMSGS)                                                   
         A     R2,TXRELO                                                        
*                                                                               
RR82     DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   *+6                                                              
         DC    H'0'                BAD ERROR CODE                               
         CLC   TXERROR,0(R2)                                                    
         BE    RR82D                                                            
         LA    R2,L'TXERRMSG+1(R2)                                              
         B     RR82                                                             
*                                                                               
RR82D    DS    0H                                                               
         MVC   TXERRMSG,1(R2)                                                   
*                                                                               
RR84     DS    0H                                                               
         CLI   TXTRCOPT,C'N'       TEST TO PRINT TRACE                          
         BE    RR86                                                             
         CLI   TXTRCOPT,C' '                                                    
         BNH   RR86                                                             
         GOTO1 =A(TXTRCE),RR=TXRELO                                             
*                                                                               
RR86     DS    0H                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*        PROCWAV- PROCESS A WAVE                                                
         SPACE 2                                                                
PROCWAV  NTR1                                                                   
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
         XC    MPQBWVDT,MPQBWVDT   SET WAVE DATE                                
         CLI   THISWAV,X'FF'       NONE IF MERGED WAVE                          
         BE    PW3M                                                             
*                                                                               
         LA    R4,SURWAVS          LOOK FOR WAVE INFO                           
         USING SURWVD,R4                                                        
*                                                                               
PW3      DS    0H                                                               
         CLI   0(R4),X'FF'                                                      
         BNE   *+12                                                             
         MVI   TXERROR,TXWAVERR    WAVE NOT DEFINED                             
         B     PWX                                                              
*                                                                               
         CLC   THISWAV,SURWVDT                                                  
         BE    PW3D                                                             
         LA    R4,SURWVDL(R4)      NEXT ENTRY                                   
         B     PW3                                                              
*                                                                               
PW3D     DS    0H                                                               
         MVC   MPQBWVDT,THISWAV    SET WAVE INFO                                
         MVC   MPQBQVD,SURWVDN     DIR NAME                                     
         MVC   MPQBQVF,SURWVFN     FILE NAME                                    
         CLI   SURWVSOV,C' '       TEST SURVEY CODE OVERRIDE                    
         BNH   *+10                                                             
         MVC   MPQBFCD,SURWVSOV                                                 
         MVC   MPQBNRES,SURWVRC    RESP COUNT                                   
         MVC   POPDFW,SURWVPOP     SET POP FOR DEFAULT WEIGHTS                  
*                                                                               
PW3M     DS    0H                  FIND OR ADD WAVE TAB ENTRY                   
         XC    X,X                                                              
         LA    R4,X                                                             
         USING WVTABD,R4                                                        
         MVC   WVDATE,THISWAV                                                   
         GOTO1 BINSRCH,WVPARS,(1,WVDATE)                                        
         OC    1(3,R1),1(R1)       TEST FULL                                    
         BNZ   *+6                                                              
         DC    H'0'                WAVE TABLE FULL                              
         L     R4,0(R1)            A(ENTRY)                                     
*                                                                               
         TM    WVSTAT,X'40'        TEST DONE THIS TIME ALREADY                  
         BNZ   PWX                                                              
         OI    WVSTAT,X'C0'        SET ACTIVE AND DONE                          
*                                                                               
         MVI   FILTLST,X'80'       DE-ACTIVATE AND SET EOL                      
*                                                                               
         CLI   MPQBNOWT,C'Y'       TEST NEED WEIGHTS                            
         BE    PW4                                                              
         MVC   MPQBWSAV,WSVAREA                                                 
         GOTO1 MPQFAC,DMCB,=C'SAVW',MPQBLKD                                     
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
PW4      DS    0H                                                               
         OC    WVSAM,WVSAM         TEST HAVE WAVE COUNTS                        
         BNZ   PW4D                                                             
*                                                                               
         CLC   WGTCOD,SURDFWGT     IF HAVE DEFAULT WEIGHT                       
         BNE   PW4C                                                             
         MVC   WVSAM,MPQBNRES      SET WAVE COUNTS DIRECTLY                     
         MVC   WVPOP,POPDFW                                                     
*                                                                               
         CLI   MPQBPPRE,0          BUT MAY HAVE TO ADJUST PRECISION             
         BE    PW4D                (POPDFW IS IN UNITS)                         
         MVC   BYTE,MPQBPPRE                                                    
         NI    BYTE,X'7F'          STRIP HOB                                    
         ZIC   RF,BYTE                                                          
         MVI   BYTE,C'D'           SET TO DIVIDE                                
         TM    MPQBPPRE,X'80'      TEST 'NEGATIVE' PREC                         
         BZ    *+8                                                              
         MVI   BYTE,C'M'           YES- MULTIPLY                                
*                                                                               
         SLL   RF,2                                                             
         L     RF,FCTRTAB(RF)      SET DIV/MULT FACTOR                          
         L     R1,POPDFW                                                        
         CLI   BYTE,C'M'                                                        
         BNE   *+10                                                             
         MR    R0,RF                                                            
         B     PW4B4                                                            
*                                                                               
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
*                                                                               
PW4B4    DS    0H                                                               
         STCM  R1,15,WVPOP                                                      
         B     PW4D                                                             
*                                                                               
PW4C     DS    0H                  ELSE MUST COUNT POP                          
         MVI   MPQBNOFL,C'Y'       SUPPRESS FILTERS                             
         MVI   MPQBNOTW,C'Y'       AND COMPONENT WGTING                         
         MVC   FULL(3),=C'ALL'     COUNT ALL RESPONDENTS                        
         MVI   FULL+3,X'FF'                                                     
         GOTO1 MPQFAC,DMCB,(X'90',=C'EXEC'),MPQBLKD,(4,FULL)                    
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
         MVI   MPQBNOTW,C'N'       RESET FOR COMP WGT                           
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERS                          
         MVC   WVSAM(8),MPQBSAM    SET SAMPLE AND POP FOR WAVE                  
*                                                                               
PW4D     DS    0H                                                               
         MVC   TWVPOP,WVPOP        SAVE THIS WAVE POP                           
         OC    BWVPOP,BWVPOP       SAVE POP OF BASE WAVE                        
         BNZ   *+10                                                             
         MVC   BWVPOP,WVPOP                                                     
         MVC   TWVSAM,WVSAM        SAVE THIS WAVE SAMPLE                        
         OC    BWVSAM,BWVSAM       SAVE SAMPLE OF BASE WAVE                     
         BNZ   *+10                                                             
         MVC   BWVSAM,WVSAM                                                     
         L     RF,NWAVES           BUMP WAVE COUNT                              
         LA    RF,1(RF)                                                         
         CH    RF,=Y(WVBN*8)       MAX WAVES PER CALL                           
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    RF,NWAVES                                                        
*                                                                               
         MVC   WVBEQU,=X'8000'     SET 'FIRST WAVE'                             
         CLI   THISWAV,X'FF'       IF MERGED WAVE SURVEY                        
         BE    PW5                                                              
*                                                                               
         L     RF,TXWAVLST         ELSE- FIND THIS WAVE IN WAVE LIST            
         LR    R0,RF                                                            
         CLC   THISWAV,0(RF)       AND USE POSITION IN LIST                     
         BE    *+12                TO SET BIT MASK                              
         LA    RF,2(RF)            (NB- WAVES MAY BE PROCESSED                  
         B     *-14                     OUT OF ORDER)                           
         SR    RF,R0                                                            
         LA    RF,WVBITLST(RF)     BIT MASK                                     
         MVC   WVBEQU,0(RF)                                                     
*                                                                               
PW5      DS    0H                                                               
         ICM   R1,15,WVSAM         ADD TO TOTAL SURVEY SAMPLE                   
         BAS   RE,SEQUIV           EQUIVALENCE TO BASE WAVE                     
         A     R1,TXSURSAM                                                      
         ST    R1,TXSURSAM                                                      
         ICM   R1,15,WVPOP         AND POP                                      
         BAS   RE,PEQUIV           EQUIVALENCE TO BASE WAVE                     
         A     R1,TXSURPOP                                                      
         ST    R1,TXSURPOP                                                      
*                                                                               
PW6      DS    0H                   PROCESS BASE RECORD                         
         OC    TXBASR,TXBASR        TEST HAVE BASE                              
         BNZ   PW7                                                              
         MVC   MPQBSAM(8),WVSAM     NO- USE SURVEY SAM AND POP                  
         B     PW7D                                                             
*                                                                               
PW7      DS    0H                                                               
         MVC   MPQBTGRC,TXBASR     A(BASE RECORD)                               
         MVC   MPQBTGBV,BASBV      FOR BASE BIT VECTOR                          
         MVC   MPQBTGWV,BASWV      FOR BASE COMP WGT VECTOR                     
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'TARGV',MPQBLKD                                    
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
         MVI   FILTLST,X'40'       ACTIVATE AS FILTER AND CLEAR EOL             
*                                                                               
PW7D     DS    0H                                                               
         MVC   WVBASSAM(8),MPQBSAM    SAVE COUNTS                               
         L     R1,MPQBSAM          ADD TO BASE SAMPLE                           
         BAS   RE,SEQUIV           EQUIV TO BASE WAVE                           
         A     R1,TXBSAM                                                        
         ST    R1,TXBSAM                                                        
         L     R1,MPQBPOP          AND POP                                      
         BAS   RE,PEQUIV           EQUIV TO BASE WAVE                           
         A     R1,TXBPOP                                                        
         ST    R1,TXBPOP                                                        
*                                                                               
PW9      DS    0H                                                               
         GOTO1 =A(GETCOLS),RR=TXRELO     GET COLUMN VECTORS AND SAVE            
         BNE   PWQERR                                                           
         GOTO1 =A(GETROWS),RR=TXRELO     GET ROW VECTORS AND XMULT              
         BNE   PWQERR                                                           
*                                                                               
PWX      DS    0H                                                               
         CLI   TXERROR,0           SET CC                                       
         XIT1                                                                   
         SPACE 1                                                                
PWQERR   MVC   TXERROR,MPQBERR     MPQFAC ERROR                                 
         MVC   TXERRMSG,MPQBEMSG                                                
         B     PWX                                                              
         SPACE 3                                                                
WVBITLST DC    X'80004000200010000800040002000100'   WAVES 1-8                  
         DC    X'00800040002000100008000400020001'   WAVES 9-16                 
*                                                                               
         DS    0F                                                               
FCTRTAB  DC    AL4(1,10,100,1000,10000,100000)                                  
         EJECT                                                                  
*        ARSET - SET DATA IN ARRAYS AND DIVIDE BY NO. OF WAVES                  
         SPACE 2                                                                
         DS    0F                                                               
ARSET    NMOD1 0,ARSET                                                          
         L     RC,TXAGEND          RESTORE A(GEND)                              
*                                                                               
         CLC   NWAVES,=F'1'        IF ONLY ONE WAVE                             
         BNH   ARSX                DONE                                         
         L     RF,NWAVES           ELSE, DIVIDE POPS BY WAVE COUNT              
         L     R1,TXSURPOP         SURVEY TOTAL                                 
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,TXSURPOP                                                      
         L     R1,TXBPOP           BASE POP                                     
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,TXBPOP                                                        
*                                  NOW DO MATRIX (COLS WITHIN ROWS)             
         L     R2,TXROWTB          ROW TABLE                                    
         USING TXROWD,R2                                                        
         L     R4,TXMTRX                                                        
*                                                                               
ARS4     DS    0H                                                               
         OC    0(4,R2),0(R2)       TEST END OF ROW TABLE                        
         BZ    ARS30                                                            
*                                                                               
ARS5     DS    0H                                                               
         L     R3,TXCOLTB          COL TABLE                                    
         USING TXCOLD,R3                                                        
*                                                                               
ARS10    DS    0H                                                               
         OC    0(4,R3),0(R3)       TEST END OF COL TABLE                        
         BZ    ARS20                                                            
*                                                                               
         MVC   FULL(L'WVBEQU),TXCWAVS     COUNT COMMON WAVES                    
         NC    FULL(L'WVBEQU),TXRWAVS                                           
*                                                                               
         SR    RF,RF               CLEAR COUNTER                                
         LA    R5,FULL                                                          
         LA    R6,WVBN             NUMBER OF BYTES                              
         MVC   BYTE,0(R5)                                                       
         BAS   RE,BITCOUNT                                                      
         LA    R5,1(R5)                                                         
         BCT   R6,*-14                                                          
*                                                                               
         CH    RF,=H'1'            IF NOT MORE THAN ONE                         
         BNH   ARS12               COMMON WAVE, SKIP DIVIDE                     
*                                                                               
         L     R1,0(R4)            FIRST WORD OF MATRIX CELL                    
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,0(R4)                                                         
         L     R1,4(R4)            2ND WORD OF MATRIX CELL                      
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,4(R4)                                                         
*                                                                               
ARS12    DS    0H                                                               
         LA    R4,CELLW(R4)        NEXT CELL                                    
         LA    R3,TXCOLDL(R3)      NEXT COLUMN                                  
         B     ARS10                                                            
*                                                                               
ARS20    DS    0H                                                               
         LA    R2,TXROWDL(R2)      NEXT ROW                                     
         B     ARS4                                                             
*                                                                               
ARS30    DS    0H                                                               
*                                                                               
ARSX     DS    0H                                                               
         CLI   TXERROR,0           SET CC                                       
         XIT1                                                                   
         EJECT                                                                  
*        SETROWS - SET ROW TABLE                                                
*                                                                               
*        NOTE- EACH LINE MUST HAVE A SPEC ELEM, ANY ANNOTATION                  
*              ELEMS FOR LINES WITH NO SPEC ELEMS ARE IGNORED                   
*                                                                               
         SPACE 2                                                                
         DS    0F                                                               
SETROWS  NMOD1 0,SETROW                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
*                                                                               
         L     R3,TXROWTB          ROW DATA TABLE                               
         USING TXROWD,R3                                                        
         XC    TXROWS,TXROWS       ROW COUNT                                    
         L     R2,TXROWSR          A(ROWSET RECORD)                             
         LA    R2,QTSELS-QTSKEY(R2)   FIRST ELEM                                
         MVI   FELSW,C'Y'                                                       
         MVI   SVLIN,0                                                          
*                                                                               
SR4      DS    0H                                                               
         MVI   ELCODE,X'51'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   SR50                                                             
*                                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BE    SR4                 YES, IGNORE                                  
*                                                                               
         MVC   SVLIN,2(R2)         START OF NEW LINE                            
         L     RF,TXROWS           BUMP ROW COUNT                               
         LA    RF,1(RF)                                                         
         ST    RF,TXROWS                                                        
         XC    TXROWD(TXROWDL),TXROWD                                           
         MVI   TXROWLS,C' '                                                     
         MVC   TXROWLS+1(TXROWLN*L'TXROWLS-1),TXROWLS                           
         ST    R2,TXRSPEC          SET A(FIRST SPEC ELEM)                       
         MVC   TXRLIN+1(1),SVLIN   LINE NUMBER                                  
*                                                                               
         MVI   ELCODE,QTSRAELC     LOOK FOR ROW ANNOTATION ELEMENT              
         MVI   FREEKLN,3                                                        
         XC    FREEKEY,FREEKEY                                                  
         MVC   FREEKEY+(QTSDSSEQ-QTSDSFR)(L'QTSDSSEQ),SVLIN                     
         MVI   FREECTL,X'20'       OUTPUT IN WORK AREA                          
         LA    RF,TXROWLS                                                       
         STCM  RF,15,FREEBLK                                                    
         MVI   FREEBLKL,L'TXROWLS                                               
         MVI   FREEMAX,TXROWLN                                                  
         MVC   AIO,TXROWSR                                                      
         GOTO1 DISPFREE                                                         
         MVC   AIO,AIO1                                                         
         CLI   ACTUAL,0            TEST IF ANY FOUND                            
         BNE   SR40                                                             
*                                                                               
*                                                                               
*              ***LOOK FOR QSPEC, TARGET ANNO***'                               
*                                                                               
         MVI   OPTION2,C'R'        TELL VAL ROUTINES TO RETURN ON ERROR         
         LA    R2,FLDHDR           A(DUMMY FIELD HEADER)                        
         XC    FLDHDR,FLDHDR                                                    
         L     R4,TXRSPEC          A(SPEC ELEMENT)                              
         USING QTSSPELD,R4                                                      
         CLI   QTSSPNQL,C'Q'       ONLY LOOKUP IF SINGLE QS/TG                  
         BNE   SR30                                                             
         ZIC   R1,QTSSPLEN         SET QSPEC/TGT CODE IN DUMMY TWAFLD           
         SH    R1,=Y(QTSSPFXL)                                                  
         BNP   SR30                                                             
         STC   R1,FLDHDR+5                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FLD(0),QTSSPCOD                                                  
         CLI   QTSSPTYP,QTSSPQS    TEST IF QSPEC                                
         BNE   SR20                                                             
         GOTO1 ANY                 GET IT IN WORK                               
         GOTO1 VALQSP              GET THE RECORD                               
         B     SR25                                                             
*                                                                               
SR20     CLI   QTSSPTYP,QTSSPTRG   TEST IF TARGET                               
         BNE   SR30                                                             
         DROP  R4                                                               
         GOTO1 VALTARG                                                          
*                                                                               
SR25     CLI   ERROR,0             TEST RECORD FOUND                            
         BNE   SR30                                                             
         GOTO1 GETREC                                                           
         L     R2,AIO                                                           
         LA    R2,QELSTRT(R2)      POINT TO FIRST ELEMENT                       
         MVI   FELSW,C'Y'                                                       
         MVI   ELCODE,X'C4'        ROW ANNOTATION ELEMENTS                      
         MVI   BYTE,0              USE TO FLAG IF ANY FOUND                     
*                                                                               
SR26     BAS   RE,NEXTEL                                                        
         BNE   SR28                IGNORE IF NONE (OR ALL DONE)                 
         SR    R4,R4                                                            
         ICM   R4,1,2(R2)          GET SUBLINE NUMBER                           
         BZ    SR26                IGNORE IF NO SUBLINE NUMBER                  
         CLI   2(R2),TXROWLN       IGNORE IF TOO HIGH                           
         BH    SR26                                                             
         BCTR  R4,0                                                             
         MH    R4,=Y(L'TXROWLS)    OFFSET FOR SUBLINE WITHIN ENTRY              
         LA    R4,TXROWLS(R4)      R4=A(ROW DESC SUBLINE)                       
         ZIC   R1,1(R2)                                                         
         SH    R1,=Y(3+1)                                                       
         BM    SR26                IGNORE IF NO DATA                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),3(R2)                                                    
         MVI   BYTE,C'Y'           FLAG THAT I'VE DONE SOMETHING                
         B     SR26                                                             
*                                                                               
SR28     CLI   BYTE,C'Y'                                                        
         BE    SR40                                                             
         SPACE 2                                                                
         L     R2,AIO              GET A TITLE ELEMENT IF ANY                   
         LA    R2,QELSTRT(R2)      POINT TO FIRST ELEMENT                       
         MVI   FELSW,C'Y'                                                       
         MVI   ELCODE,X'C1'        TARGET/QSPEC TITLE ELEMENT                   
         BAS   RE,NEXTEL                                                        
         BNE   SR30                NO TITLE                                     
         ZIC   R1,1(R2)            L' TITLE ELEMENT                             
         SH    R1,=H'4'            L'ELEM PRE-AMBLE                             
         BM    SR30                IGNORE IF STUPID LENGTH                      
         CLM   R1,1,=AL1(L'TXROWLS-1)                                           
         BNH   *+8                                                              
         LA    R1,L'TXROWLS-1                                                   
         EX    R1,*+8                                                           
         B     SR40                                                             
         MVC   TXROWLS(0),3(R2)                                                 
         SPACE 2                                                                
SR30     L     R2,TXRSPEC          NO ANNOTATION SO JUST USE SPEC               
         ZIC   R1,1(R2)                                                         
         SH    R1,=Y(9)            EX LEN OF ELEM                               
         CLM   R1,1,=AL1(L'TXROWLS-1)                                           
         BNH   *+8                                                              
         LA    R1,L'TXROWLS-1                                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TXROWLS(0),8(R2)                                                 
*                                                                               
SR40     DS    0H                                                               
         L     R2,TXRSPEC          RESTORE R2 TO SPEC ELEM                      
         LA    R3,TXROWDL(R3)      NEXT TABLE ENTRY                             
         B     SR4                                                              
*                                                                               
SR50     DS    0H                                                               
         XC    0(4,R3),0(R3)       CLEAR END OF TABLE                           
         OC    TXROWS,TXROWS                                                    
         BNZ   SETROWX                                                          
         MVI   TXERROR,TXNRWERR    N0 ROWS                                      
*                                                                               
SETROWX  DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        SETCOLS - SET COLUMN TABLE                                             
         SPACE 2                                                                
         DS    0F                                                               
SETCOLS  NMOD1 0,SETCOL                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
*                                                                               
         L     R3,TXCOLTB          COLUMN DATA TABLE                            
         USING TXCOLD,R3                                                        
         XC    TXCOLS,TXCOLS       COL COUNT                                    
         L     R2,TXCOLSR             A(COLSET RECORD)                          
         LA    R2,QTSELS-QTSKEY(R2)   FIRST ELEM                                
         MVI   FELSW,C'Y'                                                       
         MVI   SVLIN,0                                                          
*                                                                               
SC4      DS    0H                                                               
         MVI   ELCODE,X'51'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   SC50                                                             
*                                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BE    SC4                 YES, IGNORE                                  
*                                                                               
         MVC   SVLIN,2(R2)         START OF NEW LINE                            
         L     RF,TXCOLS           BUMP COL COUNT                               
         LA    RF,1(RF)                                                         
         ST    RF,TXCOLS                                                        
         XC    TXCOLD(TXCOLDL),TXCOLD                                           
         MVI   TXCCHDS,C' '                                                     
         MVC   TXCCHDS+1(TXCCHDN*L'TXCCHDS-1),TXCCHDS                           
         MVC   TXCLIN+1(1),SVLIN   LINE NUMBER                                  
         ST    R2,TXCSPEC          SET A(FIRST SPEC ELEM)                       
*                                                                               
         MVI   ELCODE,QTSCHELC     LOOK FOR COL HEADINGS                        
         MVI   FREEKLN,3                                                        
         XC    FREEKEY,FREEKEY                                                  
         MVC   FREEKEY+(QTSDSSEQ-QTSDSFR)(L'QTSDSSEQ),SVLIN                     
         MVI   FREECTL,X'20'       OUTPUT IN WORK AREA                          
         LA    RF,TXCCHDS                                                       
         STCM  RF,15,FREEBLK                                                    
         MVI   FREEBLKL,L'TXCCHDS                                               
         MVI   FREEMAX,TXCCHDN                                                  
         MVC   AIO,TXCOLSR                                                      
         GOTO1 DISPFREE                                                         
         MVC   AIO,AIO1                                                         
         CLI   ACTUAL,0            TEST IF ANY FOUND                            
         BNE   SC40                                                             
*                                                                               
*              ***LOOK FOR QSPEC, TARGET ANNO***'                               
*                                                                               
         MVI   OPTION2,C'R'        TELL VAL ROUTINES TO RETURN ON ERROR         
         LA    R2,FLDHDR           A(DUMMY FIELD HEADER)                        
         XC    FLDHDR,FLDHDR                                                    
         L     R4,TXCSPEC          A(SPEC ELEMENT)                              
         USING QTSSPELD,R4                                                      
         CLI   QTSSPNQL,C'Q'       ONLY LOOKUP IF SINGLE QS/TG                  
         BNE   SC30                                                             
         ZIC   R1,QTSSPLEN         SET QSPEC/TGT CODE IN DUMMY TWAFLD           
         SH    R1,=Y(QTSSPFXL)                                                  
         BNP   SC30                                                             
         STC   R1,FLDHDR+5                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FLD(0),QTSSPCOD                                                  
         CLI   QTSSPTYP,QTSSPQS    TEST IF QSPEC                                
         BNE   SC20                                                             
         GOTO1 ANY                 GET IT IN WORK                               
         GOTO1 VALQSP              GET THE RECORD                               
         B     SC25                                                             
*                                                                               
SC20     CLI   QTSSPTYP,QTSSPTRG   TEST IF TARGET                               
         BNE   SC30                                                             
         DROP  R4                                                               
         GOTO1 VALTARG                                                          
*                                                                               
SC25     CLI   ERROR,0             TEST RECORD FOUND                            
         BNE   SC30                                                             
         GOTO1 GETREC                                                           
         L     R2,AIO                                                           
         LA    R2,QELSTRT(R2)      POINT TO FIRST ELEMENT                       
         MVI   FELSW,C'Y'                                                       
         MVI   ELCODE,X'C2'        COLUMN HEADING ELEMENTS                      
         MVI   BYTE,0              USE TO FLAG IF ANY FOUND                     
*                                                                               
SC26     BAS   RE,NEXTEL                                                        
         BNE   SC28                IGNORE IF NONE (OR ALL DONE)                 
         SR    R4,R4                                                            
         ICM   R4,1,2(R2)          GET SUBLINE NUMBER                           
         BZ    SC26                IGNORE IF NO SUBLINE NUMBER                  
         CLI   2(R2),TXCCHDN       IGNORE IF TOO HIGH                           
         BH    SC26                                                             
         BCTR  R4,0                                                             
         MH    R4,=Y(L'TXCCHDS)    OFFSET FOR SUBLINE WITHIN ENTRY              
         LA    R4,TXCCHDS(R4)      R4=A(COL DESC SUBLINE)                       
         ZIC   R1,1(R2)                                                         
         SH    R1,=Y(3+1)                                                       
         BM    SC26                IGNORE IF NO DATA                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),3(R2)                                                    
         MVI   BYTE,C'Y'           FLAG THAT I'VE DONE SOMETHING                
         B     SC26                                                             
*                                                                               
SC28     CLI   BYTE,C'Y'           TEST IF ANNOTATION EXISTS                    
         BE    SC40                                                             
         SPACE 2                                                                
         L     R2,AIO              GET A TITLE ELEMENT IF ANY                   
         LA    R2,QELSTRT(R2)      POINT TO FIRST ELEMENT                       
         MVI   FELSW,C'Y'                                                       
         MVI   ELCODE,X'C1'        TARGET/QSPEC TITLE ELEMENT                   
         BAS   RE,NEXTEL                                                        
         BNE   SC30                NO TITLE                                     
         ZIC   R1,1(R2)            L' TITLE ELEMENT                             
         SH    R1,=H'4'            L'ELEM PRE-AMBLE                             
         BM    SC30                IGNORE IF STUPID LENGTH                      
         CLM   R1,1,=AL1(L'TXCCHDS-1)                                           
         BNH   *+8                                                              
         LA    R1,L'TXCCHDS-1                                                   
         EX    R1,*+8                                                           
         B     SC40                                                             
         MVC   TXCCHDS(0),3(R2)                                                 
         SPACE 1                                                                
SC30     L     R2,TXCSPEC          NO ANNOTATION SO JUST USE SPEC               
         ZIC   R1,1(R2)                                                         
         SH    R1,=Y(9)            EX LEN OF ELEM                               
         CLM   R1,1,=AL1(L'TXCCHDS-1)                                           
         BNH   *+8                                                              
         LA    R1,L'TXCCHDS-1                                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TXCCHDS(0),8(R2)                                                 
*                                                                               
SC40     DS    0H                                                               
         L     R2,TXCSPEC          RESTORE R2 TO SPEC ELEM                      
         LA    R3,TXCOLDL(R3)      NEXT TABLE ENTRY                             
         B     SC4                                                              
*                                                                               
SC50     DS    0H                                                               
         XC    0(4,R3),0(R3)       CLEAR END OF TABLE                           
         OC    TXCOLS,TXCOLS                                                    
         BNZ   SETCOLX                                                          
         MVI   TXERROR,TXNCLERR    N0 COLUMNS                                   
*                                                                               
SETCOLX  DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        GETCOLS - GET COLUMN VECTORS AND SAVE                                  
         SPACE 2                                                                
         DS    0F                                                               
GETCOLS  NMOD1 0,GETCOL                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         L     R2,TXCOLTB          COLUMN TABLE                                 
         USING TXCOLD,R2                                                        
         L     R3,TXCOLS           NO OF COLUMNS                                
         L     R4,TXCOLVS          SAVE AREA FOR COLUMN VECTORS                 
*                                  PASS MPQFAC A(FIRST ELEM) FOR LINE           
GC6      DS    0H                                                               
         XC    TXCVECT,TXCVECT                                                  
         XC    TXCVARD,TXCVARD     VARIABLE DESCRIPTOR                          
         LA    RF,TXCVARD                                                       
         ST    RF,AVARD                                                         
         MVC   AXSPEC,TXCSPEC      SET A(SPEC ELEMS)                            
         ST    R4,AVECT            A(VECTOR)                                    
         GOTO1 =A(GETVECT),RR=TXRELO                                            
         CLI   GVRTRN,C'E'         ERROR                                        
         BE    GCERR                                                            
         CLI   GVRTRN,C'N'         NOT DEFIEND FOR WAVE                         
         BE    GC12                NEXT COL                                     
*                                                                               
         MVC   TXCDLEN,GVDLEN      DATE ITEM LENGTH FOR VECTOR                  
         MVC   TXCSTYP,GVTYPE                                                   
         OC    TXCWAVS,WVBEQU      SET ACTIVE FOR WAVE                          
         ST    R4,TXCVECT          SAVE A(VECTOR)                               
         A     R4,GVLEN            BUMP BY VECTOR LENGTH                        
*                                                                               
GC12     DS    0H                  NEXT COL                                     
         LA    R2,TXCOLDL(R2)                                                   
         BCT   R3,GC6                                                           
*                                                                               
         CR    RD,RD               SET CC =                                     
         B     GETCOLX                                                          
*                                                                               
GCERR    DS    0H                                                               
         ST    R2,TXCERR           SET ADDRESS OF COL IN ERROR                  
         LTR   RD,RD               SET CC NOT =                                 
*                                                                               
GETCOLX  DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*        GETROWS - GET ROW VECTORS AND XMULT                                    
         SPACE 2                                                                
         DS    0F                                                               
GETROWS  NMOD1 0,GETROW                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         L     R2,TXROWTB          ROW TABLE                                    
         USING TXROWD,R2                                                        
         L     R3,TXROWS           NO OF ROWS                                   
         L     R5,TXMTRX           START OF MATRIX                              
*                                  PASS MPQFAC A(FIRST ELEM) FOR LINE           
GR6      DS    0H                                                               
         MVC   AXSPEC,TXRSPEC                                                   
         MVC   AVECT,AVW1          GET VECTOR INTO AVW1 (MAY BE ARITH)          
         LA    RF,VARDSC1          VARIABLE DESCRIPTOR                          
         ST    RF,AVARD                                                         
         XC    VARDSC1,VARDSC1                                                  
         GOTO1 =A(GETVECT),RR=TXRELO                                            
         CLI   GVRTRN,C'E'         ERROR                                        
         BE    GRERR                                                            
         CLI   GVRTRN,C'N'         NOT DEFIEND FOR WAVE                         
         BNE   GR7                 NEXT COL                                     
*                                  GET NEXT ROW                                 
         LA    RF,CELLW            REPOSITION IN MATRIX                         
         M     RE,TXCOLS                                                        
         AR    R5,RF                                                            
         B     GR12                                                             
*                                                                               
GR7      DS    0H                                                               
         MVC   TXRDLEN,GVDLEN      DATE ITEM LENGTH FOR VECTOR                  
         MVC   TXRSTYP,GVTYPE                                                   
         OC    TXRWAVS,WVBEQU      SET ACTIVE FOR WAVE                          
         XC    DMCB(24),DMCB                                                    
*                                  SET PARM FOR ROW FOR XMULT                   
         CLI   GVDLEN,0            IF ROW IS A BIT VECTOR                       
         BNE   GR7D                                                             
         MVC   DMCB+8(4),AVW1      BIT VECTOR ADDRESS                           
         MVI   DMCB+8,C'B'                                                      
         B     GR7P                                                             
*                                                                               
GR7D     DS    0H                  NOT A BIT VECTOR                             
         LA    RF,VARDSC1          VARIABLE DESCRIPTOR FOR ROW                  
         ST    RF,DMCB+8                                                        
         MVI   DMCB+8,C'A'                                                      
*                                                                               
GR7P     DS    0H                  NOW XMULT WITH EACH COL                      
         L     R4,TXCOLTB          FIRST COL TABLE ENTRY                        
         USING TXCOLD,R4                                                        
*                                                                               
GR8      DS    0H                                                               
         OC    TXCSPEC,TXCSPEC     END OF COLUMN LIST                           
         BZ    GR12                                                             
         MVC   FULL(L'WVBEQU),TXCWAVS                                           
         NC    FULL(L'WVBEQU),WVBEQU    IS COL ACTIVE FOR THIS WAVE             
         BZ    GR10                     NO, SKIP XMULT                          
*                                  SET PARM FOR COL FOR XMULT                   
         CLI   TXCDLEN,0           IF COL IS A BIT VECTOR                       
         BNE   GR8D                                                             
         MVC   DMCB+12(4),TXCVECT   BIT VECTOR ADDRESS                          
         MVI   DMCB+12,C'B'                                                     
         B     GR8P                                                             
*                                                                               
GR8D     DS    0H                  NOT A BIT VECTOR                             
         LA    RF,TXCVARD          VARIABLE DESCRIPTOR FOR COL                  
         ST    RF,DMCB+12                                                       
         MVI   DMCB+12,C'A'                                                     
*                                                                               
GR8P     DS    0H                                                               
         CLI   DMCB+8,C'A'         IF BOTH ARE ARITHMETIC                       
         BNE   GR8U                                                             
         CLI   DMCB+12,C'A'                                                     
         BNE   GR8U                                                             
*                                  THEN NEED A WORK VECTOR AREA                 
         LA    RF,VARDSC3                                                       
         ST    RF,DMCB+16          PARM 5                                       
         USING MPQVARD,RF                                                       
         MVC   MPQVSTR,AVW2                                                     
         MVI   MPQVPREC,0          ***TRY THIS***                               
         MVI   MPQVDLEN,2          ***AND THIS***                               
         DROP  RF                                                               
*                                                                               
GR8U     DS    0H                                                               
         GOTO1 MPQFAC,DMCB,=C'XMULT',MPQBLKD                                    
         CLI   MPQBERR,0                                                        
         BNE   GRERR                                                            
*                                  ADD INTO MATRIX                              
         L     R1,MPQBSAM          SAMPLE                                       
         BAS   RE,SEQUIV           EQUIVALENCE                                  
         L     RF,0(R5)                                                         
         AR    RF,R1                                                            
         ST    RF,0(R5)                                                         
         L     R1,MPQBPOP          POP                                          
         BAS   RE,PEQUIV           EQUIVALENCE                                  
         L     RF,4(R5)                                                         
         AR    RF,R1                                                            
         ST    RF,4(R5)                                                         
*                                                                               
GR10     DS    0H                                                               
         LA    R5,CELLW(R5)                                                     
         LA    R4,TXCOLDL(R4)      NEXT COLUMN                                  
         B     GR8                                                              
*                                                                               
GR12     DS    0H                                                               
         LA    R2,TXROWDL(R2)      NEXT ROW                                     
         BCT   R3,GR6                                                           
*                                                                               
         CR    RD,RD               SET CC =                                     
         B     GETROWX                                                          
*                                                                               
GRERR    DS    0H                                                               
         ST    R2,TXRERR           SET ADDRESS OF ROW IN ERROR                  
         LTR   RD,RD               SET CC NOT =                                 
*                                                                               
GETROWX  DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*        GETVECT - GET VECTOR                                                   
*                                                                               
*        AXSPEC-  A(EXTERNAL SPEC ELEMS)                                        
*        ISPEC-   INTERNAL SPEC STRING                                          
*        AVECT -  A(VECTOR)                                                     
*        AVARD -  A(VARIABLE BLOCK)                                             
*        GVTYPE - N,Q,L,W                                                       
*        GVDLEN - DATE LENGTH   0,1-4                                           
*        GVRTRN - E=ERROR,N=NOT DEFINED FOR WAVE,0=OK                           
         SPACE 2                                                                
         DS    0F                                                               
GETVECT  NMOD1 0,GETVECT                                                        
         L     RC,TXAGEND          RESTORE A(GEND)                              
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
         MVI   GVRTRN,0                                                         
         MVI   GVTYPE,0                                                         
*                                  PASS MPQFAC A(FIRST ELEM) FOR LINE           
*                                  FIRST JUST TRANSLATE SPEC                    
*                                  TO SEE WHAT KIND WE HAVE                     
         GOTO1 MPQFAC,DMCB,(X'C0',=C'EXEC'),MPQBLKD,AXSPEC                      
         CLI   MPQBERR,0           ANY ERROR IS BAD                             
         BNE   GV062                                                            
*                                                                               
         L     RF,DMCB+12                                                       
         MVC   ISPEC,0(RF)         INTERNAL SPEC STRING                         
         MVC   ISPECL,DMCB+12      LENGTH                                       
         MVC   GVTYPE,DMCB+8       TYPE OF SPEC (N,Q,L)                         
*                                                                               
         CLI   GVTYPE,C'L'         FOR COMPOSITE LOGICAL SPEC                   
         BNE   GV010                                                            
*                                  JUST EXEC                                    
         GOTO1 MPQFAC,DMCB,(X'80',=C'EXEC'),MPQBLKD,AXSPEC                      
         CLI   MPQBERR,0                                                        
         BNE   GV008                                                            
*                                                                               
GV007    DS    0H                  BIT VECTORS                                  
         MVC   GVLEN,BVLEN         VECTOR LENGTH                                
         MVI   GVDLEN,0            DATE ITEM LENGTH = 0 (BITS)                  
*                                  MOVE TO VECTOR AREA                          
         ICM   R0,15,AVECT         TO ADDRESS                                   
         BZ    GVX                 IF GIVEN                                     
         L     R1,BVLEN            TO LEGNTH                                    
         L     RE,MPQBSTK          FROM ADDRESS                                 
         LR    RF,R1               FROM LENGTH = TO LENGTH                      
         MVCL  R0,RE                                                            
         BE    GVX                                                              
*                                                                               
GV008    DS    0H                                                               
         CLI   MPQBERR,MPQERNF     REC NOT FOUND MEANS NOT DEF FOR WAVE         
         BE    GV060                                                            
         B     GV062               OTHER ERRORS ARE REAL                        
*                                                                               
GV010    DS    0H                                                               
         CLI   GVTYPE,C'Q'         FOR SINGLE QSPEC OR TARGET                   
         BNE   GV020                                                            
*                                  TRY TO JUST EXECUTE                          
         GOTO1 MPQFAC,DMCB,(X'80',=C'EXEC'),MPQBLKD,AXSPEC                      
         CLI   MPQBERR,0                                                        
         BE    GV007               IF NO ERROR, WILL HAVE BIT VECTOR            
*                                                                               
         CLI   MPQBERR,MPQERNF     REC NOT FOUND MEANS NOT DEF FOR WAVE         
         BE    GV060                                                            
         CLI   MPQBERR,MPQESYNT    A SYNTAX ERROR MIGHT MEAN ITS A              
         BNE   *+12                                                             
         MVI   GVTYPE,C'N'         NUMERIC QSPEC QUESTION                       
         B     GV020                                                            
         CLI   MPQBERR,MPQEWSPC    WAS IT A WEIGHTED QSPEC/TARGET?              
         BNE   GV062               NO, TRUE ERROR                               
*                                  YES, USE TARGVAL COMMAND                     
         L     RE,MPQBIOA          REC WILL BE HERE IF TARGET                   
         CLI   QTGKRCD-QTGKEY(RE),X'25'                                         
         BE    GV012                                                            
         L     RE,MPQBNDBL         ELSE IT WILL BE IN NODIO AREA                
         L     RE,NDIOA-NODBLKD(RE)                                             
*                                                                               
GV012    DS    0H                  MOVE RECORD TO NEW AREA                      
         L     R0,WRECA            TO ADDRESS                                   
         LA    R1,TXMAXRCL         TO LENGTH                                    
         LR    RF,R1               FROM LENGTH                                  
         MVCL  R0,RE                                                            
*                                                                               
         MVC   SVTARGS,MPQBTGRC    SAVE TARGET ADDRESS                          
         MVC   MPQBTGRC,WRECA      SET NEW REC ADDRESS                          
         MVC   MPQBTGBV,BVW1       SET NEW BIT VECTOR                           
         MVC   MPQBTGWV,AVW1       SET NEW WEIGHTED VECTOR AREA                 
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'TARGVAL',MPQBLKD                                  
         CLI   MPQBCWTP,C' '       SHOULD BE A WEIGHTED TARG                    
         BH    *+6                                                              
         DC    H'0'                NO, MUST BE SOME PROBLEM                     
         MVC   MPQBTGRC(12),SVTARGS RESTORE TARGET ADDRESSES                    
         MVI   GVTYPE,C'W'         TYPE IS COMPONENT WEIGHTED                   
*                                                                               
         CLI   MPQBERR,MPQERNF     REC NOT FOUND MEANS NOT DEF FOR WAVE         
         BE    GV060                                                            
         CLI   MPQBERR,0                                                        
         BNE   GV062               REAL ERROR                                   
*                                                                               
         MVC   GVDLEN,MPQBCWDL     DATE ITEM LENGTH                             
         ZIC   RF,GVDLEN                                                        
         M     RE,NRES                                                          
         ST    RF,GVLEN            VECTOR LENGTH                                
*                                  MOVE TO VECTOR AREA                          
         ICM   R0,15,AVECT         TO ADDRESS                                   
         BZ    GV014               IF GIVEN                                     
         L     R1,GVLEN            TO LEGNTH                                    
         L     RE,AVW1             FROM ADDRESS                                 
         LR    RF,R1               FROM LENGTH = TO LENGTH                      
         MVCL  R0,RE                                                            
*                                                                               
GV014    DS    0H                  SET VARIABLE DESCRIPTOR                      
         L     R5,AVARD                                                         
         USING MPQVARD,R5                                                       
         MVI   MPQVNO,1            SET AS VARIABLE ONE                          
         MVI   MPQVCNTL,0          CONTROL BYTE                                 
         MVC   MPQVDLEN,GVDLEN     DATA LENGTH                                  
         MVC   MPQVPREC,MPQBCWPR   WEIGHT PRECISION                             
         MVC   MPQVSTR,AVECT       A(VECTOR)                                    
         DROP  R5                                                               
         BE    GVX                                                              
*                                                                               
GV020    DS    0H                                                               
         CLI   GVTYPE,C'N'         NUMERIC SPEC                                 
         BNE   GV040                                                            
*                                ADJUST SPEC STRING SO MPQFAC WILL SET          
         ZIC   RF,ISPECL         ARITHMETIC VARIABLE                            
         LA    RF,ISPEC(RF)                                                     
         MVC   0(4,RF),=X'8B000000' MPQFAC SET VARIABLE 0 COMMAND               
         LA    RE,ISPEC                                                         
         ST    RE,DMCB+8           SET A(STRING) FOR MPQFAC                     
         LA    RF,3(RF)                                                         
         STC   RF,DMCB+8           SET L(STRING) FOR MPQFAC                     
*                                                                               
         LA    R5,VARDSC2          VARIABLE DESCRIPTOR AREA                     
         ST    R5,MPQBVLST                                                      
         XC    VARDSC2,VARDSC2                                                  
         USING MPQVARD,R5                                                       
         MVC   MPQVSTR,AVW1                                                     
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'00',=C'EXEC'),MPQBLKD                             
         CLI   MPQBERR,0                                                        
         BE    GV022               IF NO ERROR, WILL HAVE ARITH VECT            
         CLI   MPQBERR,MPQERNF     REC NOT FOUND MEANS NOT DEF FOR WAVE         
         BE    GV060                                                            
         B     GV062               REAL ERROR                                   
*                                                                               
GV022    DS    0H                                                               
         MVC   GVDLEN,MPQVDLEN     DATE ITEM LENGTH                             
         ZIC   RF,GVDLEN                                                        
         M     RE,NRES                                                          
         ST    RF,GVLEN            VECTOR LENGTH                                
*                                  MOVE TO VECTOR AREA                          
         ICM   R0,15,AVECT         TO ADDRESS                                   
         BZ    GV014               IF GIVEN                                     
         L     R1,GVLEN            TO LEGNTH                                    
         L     RE,AVW1             FROM ADDRESS                                 
         LR    RF,R1               FROM LENGTH = TO LENGTH                      
         MVCL  R0,RE                                                            
*                                  RETURN VARIABLE DESCRIPTOR                   
         L     RF,AVARD                                                         
         MVC   0(MPQVARL,RF),MPQVARD                                            
         B     GVX                                                              
*                                                                               
GV040    DS    0H                                                               
         DC    H'0'                                                             
*                                                                               
GV060    DS    0H                                                               
         MVI   GVRTRN,C'N'         SET NOT DEFINED FOR WAVE                     
         B     GVX                                                              
*                                                                               
GV062    DS    0H                  REAL ERROR                                   
         MVI   GVRTRN,C'E'         SET ERROR RETURN                             
         LTR   RD,RD               SET CC NOT =                                 
*                                                                               
GVX      DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         DROP  R5                                                               
         LTORG                                                                  
         EJECT                                                                  
*        CORMAP - SET CORE MAP                                                  
         SPACE 2                                                                
         DS    0F                                                               
CORMAP   NMOD1 0,CORMAP                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         XC    ADDRS(ADDRSL),ADDRS                                              
*                                                                               
*              RESP COUNT AND BIT VECTOR LENGTH                                 
*              --------------------------------                                 
*                                                                               
         MVC   NRES,MPQBNRES       SET RESP COUNT DIRECTLY                      
         CLI   SURWMRGE,C'Y'       IF WAVES MERGED                              
         BE    CORM5                                                            
*                                  ELSE SET FROM LARGEST WAVE                   
         LA    R5,SURWAVS                                                       
         USING SURWVD,R5                                                        
*                                                                               
CORM3    DS    0H                                                               
         CLI   0(R5),X'FF'         END OF SURVEY WAVE LIST                      
         BE    CORM5                                                            
*                                                                               
         CLC   NRES,SURWVRC        TEST ALREADY HAVE LARGER                     
         BNL   *+10                                                             
         MVC   NRES,SURWVRC                                                     
*                                                                               
         LA    R5,SURWVDL(R5)                                                   
         B     CORM3                                                            
*                                                                               
CORM5    DS    0H                                                               
         L     RF,NRES             SET BIT VECTOR LENGTH                        
         LA    RF,7(RF)                                                         
         SRL   RF,3                                                             
         ST    RF,BVLEN                                                         
*                                                                               
         L     RF,NRES             SET MAX ARITH VECTOR LENGTH                  
         SLL   RF,2                = 4 X NRES                                   
         ST    RF,AVLEN                                                         
*                                                                               
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
*        SET VARIOUS ADDRESSES                                                  
*        ---------------------                                                  
*                                                                               
         L     R3,TXCORE           R3 FOR CORE POINTER                          
*                                                                               
         ST    R3,WVTAB            WAVE TABLE                                   
         LA    R3,WVTABLN(R3)                                                   
*                                                                               
         LA    R3,7(R3)            ALIGN FOR WORK REC AREA                      
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,WRECA            WGT SAVE AREA                                
         AH    R3,=Y(TXMAXRCL)     MAX RECORD LENGTH                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR WGT AREA                           
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,WSVAREA          WGT SAVE AREA                                
         ZIC   R1,MPQBWDLN         WGT ITEM LENGTH                              
         M     R0,NRES             X NRES                                       
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR BASE WGT AREA                      
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,BASWV            TARG COMPONENT WGT AREA                      
         LA    R1,4                VECTOR ITEM LENGTH (ALLOW 4)                 
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN                                        
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,AVW1             ARITH VECTOR WORK AREA 1                     
         LA    R1,4                VECTOR ITEM LENGTH (ALLOW 4)                 
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN                                        
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,AVW2             ARITH VECTOR WORK AREA 2                     
         LA    R1,4                VECTOR ITEM LENGTH (ALLOW 4)                 
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         ST    R3,BASBV            BASE BIT VECTOR                              
         A     R3,BVLEN                                                         
*                                                                               
         ST    R3,BVW1             TWO WORK BIT VECTORS                         
         A     R3,BVLEN                                                         
         ST    R3,BVW2                                                          
         A     R3,BVLEN                                                         
*                                                                               
         LA    R3,7(R3)            ALIGN                                        
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,TXROWTB          ROW TABLE                                    
         A     R3,=A(MAXROWS*TXROWDL)                                           
*                                                                               
         LA    R3,7(R3)            ALIGN                                        
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,TXCOLTB          COLUMN TABLE                                 
         A     R3,=A(MAXCOLS*TXCOLDL)                                           
*                                                                               
         ST    R3,TXCOLVS          COLUMN VECTORS                               
         LA    R1,MAXCOLS*2        ALLOW FOR 2-BYTE VECTORS                     
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         ST    R3,TXFLTVS          FILTER VECTORS                               
         LA    R1,MAXFILTS                                                      
         M     R0,BVLEN                                                         
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR MATRIX                             
         SRL   R3,R3                                                            
         SLL   R3,R3                                                            
         MVC   0(8,R3),=C'*MATRX*'                                              
         LA    R3,8(R3)                                                         
         ST    R3,TXMTRX           MATRIX                                       
*                                                                               
*                                  SET WVTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,WVTAB                                                         
         SR    R2,R2                                                            
         LA    R3,WVTABDL                                                       
         LA    R4,2                KEY LENGTH                                   
         LA    R5,MAXWAVS                                                       
         STM   R0,R5,WVPARS                                                     
*                                                                               
CORMX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*        TXTRCE- 'TRACE' - PRINT TABLES AND ARRAYS                              
         SPACE 2                                                                
         DS    0F                                                               
TXTRCE   NMOD1 0,TXTRCE                                                         
         L     RC,TXAGEND          RESTORE A(GEND)                              
         L     R6,ASPOOLD                                                       
         USING SPOOLD,R6                                                        
*                                                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         L     R4,=A(TOTWDS)                                                    
         A     R4,TXRELO                                                        
         LA    R5,TXSURSAM                                                      
         LA    R3,4                                                             
*                                                                               
TXTR08   DS    0H                                                               
         MVC   P(20),0(R4)                                                      
         EDIT  (B4,0(R5)),(10,P+21),COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R4,20(R4)                                                        
         LA    R5,4(R5)                                                         
         BCT   R3,TXTR08                                                        
*                                                                               
         L     R4,WVPARS+4         WAVE TAB                                     
         USING WVTABD,R4                                                        
         ICM   R5,15,WVPARS+8                                                   
         BZ    TXTR2G                                                           
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(5),=C'WVTAB'                                                   
         MVC   P2+10(47),=C'STAT    SAMPLE        POP   BASE SAM   BASEX        
                POP'                                                            
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
TXTR2    DS    0H                                                               
         MVC   P(3),=C'ALL'                                                     
         CLI   WVDATE,X'FF'                                                     
         BE    TXTR2D                                                           
         GOTO1 DATCON,DMCB,(2,WVDATE),(8,P)                                     
TXTR2D   DS    0H                                                               
         GOTO1 HEXOUT,DMCB,WVSTAT,P+10,1,=C'N'                                  
         LA    R3,P+14                                                          
         LA    RE,WVSAM                                                         
         LA    R2,4                                                             
*                                                                               
TXTR2E   DS    0H                                                               
         EDIT  (B4,0(RE)),(10,0(R3)),ZERO=NOBLANK                               
         LA    R3,11(R3)                                                        
         LA    RE,4(RE)                                                         
         BCT   R2,TXTR2E                                                        
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,WVPARS+12        NEXT ENTRY                                   
         BCT   R5,TXTR2                                                         
*                                                                               
TXTR2G   DS    0H                                                               
TR10     DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         ICM   R3,15,TXROWS                                                     
         BNP   TR20                                                             
         MVC   P(19),=C'**ROW DEFINITIONS**'                                    
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         XC    FULL,FULL                                                        
         L     R5,TXROWTB                                                       
         USING TXROWD,R5                                                        
*                                                                               
TR12     DS    0H                                                               
         L     RF,FULL                                                          
         LA    RF,1(RF)                                                         
         ST    RF,FULL                                                          
         EDIT  (B4,FULL),(3,P+0)                                                
         EDIT  (B2,TXRLIN),(3,P+4)  LINE NUMBER                                 
         GOTO1 HEXOUT,DMCB,TXRWAVS,P+8,2,=C'N'                                  
*                                                                               
         ICM   R2,15,TXRSPEC       SPECS                                        
         BP    *+6                                                              
         DC    H'0'                                                             
         MVC   SVLIN,2(R2)         SAVE LINE                                    
         LA    R4,P+13                                                          
         LA    R3,4                ONLY 4 LINES                                 
*                                                                               
TR14     DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    TR16                                                             
         CLI   0(R2),X'51'                                                      
         BE    TR15                                                             
*                                                                               
TR14D    DS    0H                                                               
         ZIC   R0,1(R2)            NEXT ELEM                                    
         AR    R2,R0                                                            
         B     TR14                                                             
*                                                                               
TR15     DS    0H                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BNE   TR14D                                                            
*                                                                               
         GOTO1 HEXOUT,DMCB,(R2),(R4),6,=C'N'                                    
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'7'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   13(0,R4),6(R2)      SPEC                                         
         LA    R4,132(R4)          NEXT LINE                                    
         BCT   R3,TR14D            NEXT ELEM                                    
*                                                                               
TR16     DS    0H                                                               
         MVC   P1+77(30),TXROWLS+000                                            
         MVC   P2+77(30),TXROWLS+030                                            
         MVC   P3+77(30),TXROWLS+060                                            
         MVC   P4+77(30),TXROWLS+090                                            
*                                                                               
TR18     DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R5,TXROWDL(R5)      NEXT ROW                                     
         CLC   FULL,TXROWS         TEST AT END                                  
         BL    TR12                                                             
*                                                                               
TR20     DS    0H                  COLUMNS                                      
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         ICM   R3,15,TXCOLS                                                     
         BNP   TR30                                                             
         MVC   P(22),=C'**COLUMN DEFINITIONS**'                                 
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         XC    FULL,FULL                                                        
         L     R5,TXCOLTB                                                       
         USING TXCOLD,R5                                                        
*                                                                               
TR22     DS    0H                                                               
         L     RF,FULL                                                          
         LA    RF,1(RF)                                                         
         ST    RF,FULL                                                          
         EDIT  (B4,FULL),(3,P+0)                                                
         EDIT  (B2,TXCLIN),(3,P+4)  LINE NUMBER                                 
         GOTO1 HEXOUT,DMCB,TXCWAVS,P+8,2,=C'N'                                  
*                                                                               
         ICM   R2,15,TXCSPEC       SPECS                                        
         BP    *+6                                                              
         DC    H'0'                                                             
         MVC   SVLIN,2(R2)         SAVE LINE                                    
         LA    R4,P+13                                                          
         LA    R3,4                ONLY 4 LINES                                 
*                                                                               
TR24     DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    TR26                                                             
         CLI   0(R2),X'51'                                                      
         BE    TR25                                                             
*                                                                               
TR24D    DS    0H                                                               
         ZIC   R0,1(R2)            NEXT ELEM                                    
         AR    R2,R0                                                            
         B     TR24                                                             
*                                                                               
TR25     DS    0H                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BNE   TR24D                                                            
*                                                                               
         GOTO1 HEXOUT,DMCB,(R2),(R4),6,=C'N'                                    
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'7'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   13(0,R4),6(R2)      SPEC                                         
         LA    R4,132(R4)          NEXT LINE                                    
         BCT   R3,TR24D            NEXT ELEM                                    
*                                                                               
TR26     DS    0H                                                               
         MVC   P1+077(10),TXCCHDS+000                                           
         MVC   P1+088(10),TXCCHDS+010                                           
         MVC   P1+099(10),TXCCHDS+020                                           
         MVC   P1+110(10),TXCCHDS+030                                           
         MVC   P1+122(10),TXCCHDS+040                                           
         MVC   P2+077(10),TXCCHDS+050                                           
         MVC   P2+088(10),TXCCHDS+060                                           
         MVC   P2+099(10),TXCCHDS+070                                           
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R5,TXCOLDL(R5)      NEXT COL                                     
         CLC   FULL,TXCOLS         TEST AT END                                  
         BL    TR22                                                             
*                                                                               
TR30     DS    0H                  MATRIX                                       
         OC    TXROWS,TXROWS                                                    
         BZ    TR50                                                             
         OC    TXCOLS,TXCOLS                                                    
         BZ    TR50                                                             
*                                                                               
         MVI   BYTE,C'S'           SAMPLES                                      
         LA    R2,1                STARTING COLUMN                              
*                                                                               
TR32     DS    0H                                                               
         BAS   RE,MTXPRT                                                        
         LA    R2,11(R2)           NEXT SET                                     
         C     R2,TXCOLS           TEST DONE                                    
         BNH   TR32                                                             
*                                                                               
         MVI   BYTE,C'P'           POP                                          
         LA    R2,1                STARTING COLUMN                              
*                                                                               
TR33     DS    0H                                                               
         BAS   RE,MTXPRT                                                        
         LA    R2,11(R2)           NEXT SET                                     
         C     R2,TXCOLS           TEST DONE                                    
         BNH   TR33                                                             
*                                                                               
TR50     DS    0H                                                               
TR70     DS    0H                                                               
         CLI   TXERROR,0                                                        
         BE    TXTR83                                                           
         MVC   P(8),=C'**ERROR='                                                
         EDIT  (B1,TXERROR),(3,P+10)                                            
         MVC   P+15(40),TXERRMSG                                                
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
TXTR83   DS    0H                                                               
*                                                                               
TXTRCX   DS    0H                                                               
         MVI   LINE,99                                                          
         XIT1                                                                   
         SPACE 3                                                                
*        MTXPRT - PRINT PART OF XTAB MATRIX                                     
*                                                                               
MTXPRT   NTR1                                                                   
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         ST    R2,FIRSTCOL         STARTING COLUMN NUMBER                       
*                                                                               
         MVC   P(19),=C'** SAMPLE COUNTS **'                                    
         CLI   BYTE,C'S'                                                        
         BE    *+10                                                             
         MVC   P(23),=C'** POPULATION COUNTS **'                                
         MVC   P+30(17),=C'COLUMN XX THRU XX'                                   
         EDIT  (R2),(2,P+37),FILL=0                                             
         LA    RF,10(R2)                                                        
         C     RF,TXCOLS                                                        
         BNH   *+8                                                              
         L     RF,TXCOLS                                                        
         ST    RF,LASTCOL          LAST COLUMN NUMBER                           
         EDIT  (RF),(2,P+45),FILL=0                                             
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         L     RF,TXCOLS                                                        
         SLL   RF,4                X 16                                         
         ST    RF,ROWLEN           = ROW LENGTH                                 
         L     R3,TXMTRX           START OF FIRST ROW                           
         CLI   BYTE,C'P'           POPS                                         
         BNE   *+8                                                              
         LA    R3,4(R3)            ARE IN SECOND NUMBER                         
         LA    R5,1                ROW NUMBER                                   
*                                                                               
MTX6     DS    0H                                                               
         LR    R4,R2                                                            
         BCTR  R4,R0                                                            
         SLL   R4,4                                                             
         AR    R4,R3               FIRST COL TO SHOW                            
         MVI   P,C'R'                                                           
         EDIT  (R5),(3,P+1),FILL=0                                              
         LA    RE,P+6                                                           
*                                                                               
MTX7     DS    0H                                                               
         EDIT  (B4,0(R4)),(10,0(RE)),ZERO=NOBLANK                               
         LA    RE,11(RE)                                                        
         LA    R4,16(R4)                                                        
         LA    R2,1(R2)                                                         
         C     R2,LASTCOL                                                       
         BNH   MTX7                                                             
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         C     R5,TXROWS                                                        
         BNL   MTXPRTX                                                          
         LA    R5,1(R5)                                                         
         A     R3,ROWLEN           NEXT ROW                                     
         L     R2,FIRSTCOL                                                      
         B     MTX6                                                             
*                                                                               
MTXPRTX  DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
         SPACE 3                                                                
         DS    0D                                                               
TOTWDS   DS    0X                                                               
         DC    CL20'SURVEY SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         DC    CL20'BASE   SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         SPACE 3                                                                
ERRMSGS  DS    0D                                                               
         DC    AL1(TXWAVERR)                                                    
         DC    CL40'WAVE NOT FOUND'                                             
         DC    AL1(TXSPCERR)                                                    
         DC    CL40'INVALID SPECIFICATION'                                      
         DC    AL1(TXNRWERR)                                                    
         DC    CL40'NO ROWS DEFINED'                                            
         DC    AL1(TXNCLERR)                                                    
         DC    CL40'NO COLUMNS DEFINED'                                         
         DC    X'00'                                                            
         EJECT                                                                  
*                                                                               
GENSUBS  DS    0D                  **START OF GENERAL SUBROUTINES**             
*                                  --------------------------------             
         SPACE 2                                                                
*        COUNT BITS IN BYTE                                                     
*        AND CUME COUNT IN RF                                                   
         SPACE 2                                                                
BITCOUNT DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,1,BYTE                                                        
         BZR   RE                                                               
         SR    R1,R1                                                            
*                                                                               
BITC4    DS    0H                                                               
         SRDL  R0,1                                                             
         LTR   R1,R1               TEST HOB ON                                  
         BNM   *+8                                                              
         LA    RF,1(RF)            YES- BUMP COUNT                              
         LTR   R0,R0               TEST ANY BITS LEFT ON                        
         BZR   RE                                                               
         B     BITC4                                                            
*                                                                               
         DROP  R5                                                               
         SPACE 2                                                                
         SPACE 3                                                                
BINSDEL  NTR1                      REMOVE ITEM FROM BINSRCH TABLE               
         L     R2,0(R1)            A(ITEM)                                      
         L     RF,8(R1)            LENGTH                                       
         L     R4,12(R1)           CURRENT COUNT                                
         LR    RE,R4                                                            
         BCTR  RE,R0                                                            
         ST    RE,12(R1)           SET NEW COUNT                                
*                                                                               
         MH    R4,10(R1)           COUNT X LENGTH                               
         A     R4,4(R1)            PLUS START = END OF TABLE                    
         BCTR  RF,R0                                                            
*                                                                               
BINSD4   DS    0H                                                               
         LA    RE,1(R2,RF)                                                      
         EX    RF,BDMVC                                                         
         LR    R2,RE                                                            
         CR    R2,R4                                                            
         BL    BINSD4                                                           
*                                                                               
         XIT1                                                                   
*                                                                               
BDMVC    MVC   0(0,R2),0(RE)                                                    
         SPACE 3                                                                
*                                  NB- DIV RETURNS VIA RE                       
DIV      DIV   (R0),(RF)                                                        
         SPACE 3                                                                
PEQUIV   DS    0H                  EQUIVALENCE POPS TO BASE WAVE                
         CLC   BWVPOP,TWVPOP                                                    
         BER   RE                                                               
         M     R0,BWVPOP                                                        
         L     RF,TWVPOP                                                        
         B     DIV                                                              
         SPACE 3                                                                
SEQUIV   DS    0H                  EQUIVALENCE SAMPLE TO BASE WAVE              
         CLC   BWVSAM,TWVSAM                                                    
         BER   RE                                                               
         M     R0,BWVSAM                                                        
         L     RF,TWVSAM                                                        
         B     DIV                                                              
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         CLI   FELSW,C'Y'          IF FIRST TIME                                
         MVI   FELSW,C'N'          SET OFF SW                                   
         BE    NEXTEL2             AND USE FIRST ELEM                           
NEXTEL1  ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
NEXTEL2  DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   NEXTEL4                                                          
         LTR   RE,RE                                                            
         BR    RE                                                               
NEXTEL4  DS    0H                                                               
         CLC   0(1,R2),ELCODE                                                   
         BNE   NEXTEL1                                                          
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*        DSECT TO COVER WAVE TABLE                                              
         SPACE 2                                                                
WVTABD   DSECT                                                                  
WVDATE   DS    XL2                 WAVE DATE                                    
WVSTAT   DS    XL1                 STATUS  - X'80'= ACTIVE THIS REQ             
*                                            X'40'= ALREADY DONE                
         DS    XL1                 SPARE                                        
WVSAM    DS    XL4                 SURVEY SAMPLE                                
WVPOP    DS    XL4                 POPULATION                                   
WVBASSAM DS    XL4                 BASE SAMPLE                                  
WVBASPOP DS    XL4                 POPULATION                                   
WVTABDL  EQU   *-WVTABD                                                         
         SPACE 3                                                                
WVTABLN  EQU   (WVTABDL*MAXWAVS)+1 TOTAL LENGTH OF WAVE TABLE                   
*                                  (FOR REACH REPORT)                           
         SPACE 3                                                                
WORKD    DSECT                     TABEX WORK AREA DSECT                        
X        DS    XL20                                                             
X2       DS    XL20                                                             
WKSAM    DS    F                                                                
WKPOP    DS    F                                                                
WKSAM2   DS    F                                                                
WKPOP2   DS    F                                                                
ROWLEN   DS    F                                                                
LASTCOL  DS    F                                                                
FIRSTCOL DS    F                                                                
THISWAV  DS    XL2                 WAVE DATE                                    
FILTLST  DS    XL48                                                             
FELSW    DS    C                                                                
AVECT    DS    A                                                                
WVECT1   DS    XL4                                                              
WVECT2   DS    XL4                                                              
WVECT3   DS    XL4                                                              
WVECT4   DS    XL4                                                              
POPDFW   DS    F                                                                
TWVPOP   DS    F                                                                
BWVPOP   DS    F                                                                
TWVSAM   DS    F                                                                
BWVSAM   DS    F                                                                
NWAVES   DS    F                                                                
WVBEQU   DS    XL2                                                              
WVBN     EQU   L'WVBEQU            NO. OF BYTES                                 
SVLIN    DS    XL2                                                              
PCOUNT   DS    PL3                                                              
FLDHDR   DS    CL8                 DUMMY TWA FIELD HEADER (FOR VALRTNS)         
FLD      DS    CL40                DUMMY TWA FIELD                              
*                                                                               
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*                                                                               
       ++INCLUDE MPTXCTLD                                                       
         SPACE 2                                                                
BINSRCH  EQU   TXBINSCH                                                         
         SPACE 2                                                                
TXCTLD   DSECT                                                                  
         ORG   TXWORK              REDEFINE TXWORK                              
TXRELO   DS    A                                                                
         DS    A                   SPACE (ALIGNS TXCORE WITH RXCORE)            
TXCORE   DS    A                   A(START OF TABLE AREA)                       
TXCOREX  DS    A                   A(END)                                       
TXCORLEN DS    F                   CORE AREA LENGTH                             
TXAGEND  DS    A                   A(GEND) - GENCON GLOBAL AREAS                
TXCOLVS  DS    A                   A(FIRST COLUMN VECTOR)                       
TXFLTVS  DS    A                   A(FIRST FILTER VECTOR)                       
*                                                                               
TXBASE   DS    CL8                 THIS BASE                                    
*                                                                               
         DS    0F                                                               
WVPARS   DS    XL24                BINSRCH PARS FOR WAVE TAB                    
NRES     DS    F                   RESPONDENT COUNT                             
BVLEN    DS    F                   LENGTH OF BIT VECTOR                         
AVLEN    DS    F                   MAX LEN OF ARITH VECTOR                      
AXSPEC   DS    A                                                                
AVARD    DS    A                                                                
GVLEN    DS    F                                                                
GVTYPE   DS    CL1                                                              
GVRTRN   DS    CL1                                                              
GVDLEN   DS    XL1                                                              
CALLNO   DS    XL1                 NUMBER OF READREX CALL                       
SVTARGS  DS    0XL12               SAVE SOME TARGET ADDRESSES                   
SVTGRC   DS    A                   A(RECORD)                                    
SVTGBV   DS    A                   A(BIT VECTOR)                                
SVTGWV   DS    A                   A(WEIGTED VECTOR)                            
*                                                                               
VARDSC1  DS    XL(MPQVARL)                                                      
VARDSC2  DS    XL(MPQVARL)                                                      
VARDSC3  DS    XL(MPQVARL)                                                      
*                                                                               
ISPEC    DS    XL256               AREA FOR INTERNAL SPEC STRING                
ISPECL   DS    XL1                                                              
*                                                                               
*                                  VARIOUS ADDRESSES                            
*                                  -----------------                            
ADDRS    DS    0F                                                               
WVTAB    DS    A                   WAVE TABLE AREA                              
WSVAREA  DS    A                   SAVE AREA FOR SURVEY WGTS                    
BASWV    DS    A                   BASE COMPONENET WEIGHT VECTOR                
BASBV    DS    A                   BASE BIT VECTOR                              
BVW1     DS    A                   WORK BIT VECTOR 1                            
BVW2     DS    A                   WORK BIT VECTOR 2                            
AVW1     DS    A                   ARITHMETIC WORK VECTOR AREA 1                
AVW2     DS    A                   ARITHMETIC WORK VECTOR AREA 2                
WRECA    DS    A                   WORK RECORD AREA                             
*                                                                               
ADDRSL   EQU   *-ADDRS                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPQBLKD                                                        
       ++INCLUDE MPQVARD                                                        
       ++INCLUDE MPQERRQS                                                       
       ++INCLUDE DDNODBLKD                                                      
         PRINT ON                                                               
*                                                                               
       ++INCLUDE MPGENQS                                                        
       ++INCLUDE MPRDRWORKD                                                     
       ++INCLUDE MPEQUATES                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035MPTABEX   05/01/02'                                      
         END                                                                    
