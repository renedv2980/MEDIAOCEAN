*          DATA SET DRIVADD    AT LEVEL 013 AS OF 06/28/12                      
*CATALP DRIVADD                                                                 
         TITLE 'DRIVADD - ADD TWO DRIVER RECORDS TOGETHER'                      
DRIVADD  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DADDLNQ,**DRAD**                                                 
         USING DADDD,RC                                                         
         MVC   SIGN,0(R1)          BYTE 1: -=SUBTRACT                           
         L     RA,0(R1)            PARAMETER 1 A(GLOBAL)                        
         USING GLOBALD,RA                                                       
         LM    R3,R4,4(R1)                                                      
         ST    R1,UPARM            SAVE A(USER'S PARAMETERS)                    
         MVI   0(R1),0             AFTER DRIVADD, BYTE 1 WILL BE SET            
*                                  0=NO ADDITIVE FIELDS                         
*                                  1=ALL ADDITIVE FIELDS ZERO                   
*                                  2=SOME ADDITIVE FIELDS NON-ZERO              
*                                  PARAMETER 2 A(TO RECORD)                     
*                                  PARAMETER 3 A(FROM RECORD)                   
*              ROUTINE TO ADD 2 RECORDS TOGETHER                                
*                                                                               
         MVI   FIXR5R6,C'N'                                                     
*                                                                               
         SPACE 1                                                                
         LLC   R2,0(R4)            GET GLINTD FOR THIS RECORD                   
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         L     R2,GLAFIN           A(FIRST INPUT FIELD)                         
         SPACE 1                                                                
ADDEM2   CLI   0(R2),X'20'         LOOK FOR IN FIELDS                           
         JNE   ADDEMEND                                                         
         USING DRIND,R2                                                         
         CLI   DRINTYPE+1,C'+'     ONLY INTERESTED IN ADDITIVE FIELDS           
         JNE   ADDEMEND                                                         
         LH    R5,DRINDISP                                                      
         LR    R6,R5                                                            
         AR    R5,R3                                                            
         AR    R6,R4                                                            
*                                                                               
         LLC   R1,DRINFLEN         INDIVIDUAL FIELD LENGTH                      
         LLC   R0,DRINREP          REPETITION FACTOR                            
         SPACE 1                                                                
ADDEM4   BRAS  RE,DOADD            GO AND ADD SINGLE FIELD                      
         AR    R5,R1                                                            
         AR    R6,R1                                                            
         JCT   R0,ADDEM4                                                        
         SPACE 1                                                                
ADDEMEND LLC   R1,1(R2)                                                         
         AR    R2,R1                                                            
         CLI   0(R2),X'00'                                                      
         JE    ADDEMX                                                           
         CLI   0(R2),X'10'                                                      
         JE    ADDEMX                                                           
         J     ADDEM2                                                           
*                                                                               
ADDEMX   TM    GLINDS,GLPALDET     OPTION TO GET ALL DETAILS                    
         JNO   XIT                                                              
         L     R1,UPARM                                                         
         MVI   0(R1),2             MAKE ALL RECORDS ACTIVE                      
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO ADD 1 FIELD TO ANOTHER                               
         SPACE 3                                                                
*                                  R5=A(TO FIELD)                               
*                                  R6=A(FROM FIELD)                             
*                                  R1=FIELD LENGTH                              
         SPACE 1                                                                
DOADD    ST    RE,SAVERE                                                        
         ST    R1,SAVER1                                                        
*                                                                               
         CLI   DRINTYPE,C'B'       TEST TYPE                                    
         JE    BINARY                                                           
         CLI   DRINTYPE,C'P'                                                    
         JE    PACKED                                                           
         CLI   DRINTYPE,C'E'                                                    
         JE    EBCDIC                                                           
         CLI   DRINTYPE,C'S'                                                    
         JE    SCALED                                                           
         CLI   DRINTYPE,C'M'                                                    
         JE    MASKED                                                           
         CLI   DRINTYPE,C'C'                                                    
         JE    MERGE                                                            
         CLI   DRINTYPE,C'X'                                                    
         JE    MERGE                                                            
         CLI   DRINTYPE,C'W'                                                    
         JE    MERGE                                                            
         DC    H'0'                                                             
         EJECT                                                                  
*              SCALED, MASKED ADDITION                                          
         SPACE 3                                                                
MASKED   OC    0(1,R5),0(R6)       MASKED - OR EDIT BITS TOGETHER               
         J     SCALEND                                                          
         SPACE 1                                                                
SCALED   IC    RE,0(R5)            RE=TO SCALE                                  
         SLL   RE,25                                                            
         SRL   RE,25                                                            
         TM    0(R5),X'80'                                                      
         JNO   *+6                                                              
         LCR   RE,RE                                                            
         SPACE 1                                                                
         IC    RF,0(R6)            RF=FROM SCALE                                
         SLL   RF,25                                                            
         SRL   RF,25                                                            
         TM    0(R6),X'80'                                                      
         JNO   *+6                                                              
         LCR   RF,RF                                                            
         SPACE 1                                                                
         SR    RE,RF               COMPARE SCALES                               
         JE    SCALEND                                                          
         JM    SCALED2                                                          
         SPACE 1                                                                
*                                  TO SCALE MORE THAN FROM SCALE                
         LR    RF,R1                                                            
         AHI   RF,-2                                                            
         EXRL  RF,SRPTO            SO SHIFT TO FIELD                            
         MVC   0(1,R5),0(R6)       AND USE SCALE FROM FROM FIELD                
         J     SCALEND                                                          
*                                                                               
SCALED2  LCR   RE,RE               FROM SCALE MORE THAN TO SCALE                
         LR    RF,R1                                                            
         AHI   RF,-2                                                            
         EXRL  RF,SRPFROM          SO SHIFT FROM FIELD                          
         MVC   0(1,R6),0(R5)       AND USE SCALE FROM TO FIELD                  
*                                                                               
SCALEND  LA    R5,1(R5)            REST OF FIELD BEHAVES LIKE PACKED            
         LA    R6,1(R6)                                                         
         BCTR  R1,0                                                             
         MVI   FIXR5R6,C'Y'                                                     
         J     PACKED                                                           
*                                                                               
SRPTO    SRP   1(0,R5),0(RE),0                                                  
SRPFROM  SRP   1(0,R6),0(RE),0                                                  
         EJECT                                                                  
*              PACKED ADDITION                                                  
         SPACE 3                                                                
PACKED   BCTR  R1,0                                                             
         LA    RF,0(R1,R5)        PROTECT AGAINST BINARY ZERO FIELDS            
         CLI   0(RF),0                                                          
         JNE   *+8                                                              
         MVI   0(RF),X'0C'                                                      
         LA    RF,0(R1,R6)                                                      
         CLI   0(RF),0                                                          
         JNE   *+8                                                              
         MVI   0(RF),X'0C'                                                      
*                                                                               
         LR    RF,R1                                                            
         SLL   RF,4                                                             
         AR    R1,RF               NEED SAME LENGTH IN BOTH HALVES              
         CLI   SIGN,C'-'                                                        
         JE    PACKED2                                                          
         EXRL  R1,EXAP                                                          
         J     TESTACT                                                          
         SPACE 1                                                                
PACKED2  EXRL  R1,EXSP                                                          
         J     TESTACT                                                          
*                                                                               
EXAP     AP    0(0,R5),0(0,R6)                                                  
EXSP     SP    0(0,R5),0(0,R6)                                                  
         EJECT                                                                  
*              EBCDIC ADDING ROUTINE                                            
         SPACE 3                                                                
EBCDIC   BCTR  R1,0                                                             
         EXRL  R1,PAK1                                                          
         EXRL  R1,PAK2                                                          
         AP    DUB,DUB2                                                         
         SLL   R1,4                                                             
         EXRL  R1,UNPAK                                                         
         CLI   SIGN,C'-'                                                        
         JE    EBCDIC2                                                          
         AP    DUB,DUB2                                                         
         J     TESTACT                                                          
         SPACE 1                                                                
EBCDIC2  SP    DUB,DUB2                                                         
         J     TESTACT                                                          
PAK1     PACK  DUB,0(0,R5)                                                      
PAK2     PACK  DUB2,0(0,R6)                                                     
UNPAK    UNPK  0(0,R5),DUB                                                      
         SPACE 1                                                                
         EJECT                                                                  
*              BINARY ADDING ROUTINES                                           
         SPACE 3                                                                
BINARY   LA    RE,B'1'             GET ICM MASK FOR INPUT LENGTH                
         CLI   DRINFLEN,2                                                       
         JL    BINARY10                                                         
         JNE   *+12                                                             
         LA    RE,B'11'                                                         
         J     BINARY10                                                         
         LA    RE,B'111'                                                        
         CLI   DRINFLEN,4                                                       
         JL    BINARY10                                                         
         LA    RE,B'1111'                                                       
*                                                                               
BINARY10 XR    R1,R1               GET VALUES IN R1/RF                          
         XR    RF,RF                                                            
         EXRL  RE,EXICMR1                                                       
         EXRL  RE,EXICMRF                                                       
         TM    GLINDS3,GLNOOVER    ARE OVERFLOWS DISLLAOWED?                    
         JZ    *+12                                                             
         CLM   R1,15,=X'80000000'  YES-IF ALREADY OVERFLOWED,                   
         JE    BINARY20                LEAVE AS MAX NEGATIVE NUMBER             
         CLI   SIGN,C'-'                                                        
         JE    BINARYM                                                          
         AR    R1,RF                                                            
         JNO   BINARY20                                                         
         TM    GLINDS3,GLNOOVER    OVERLFOW-IS IT DISALLOWED?                   
         JZ    BINARY20                                                         
         ICM   R1,15,=X'80000000'  YES-SET TO MAX NEGATIVE NUMBER               
         J     BINARY20                                                         
BINARYM  SR    R1,RF                                                            
BINARY20 EX    RE,EXSTCM                                                        
         J     TESTACT                                                          
*                                                                               
EXICMR1  ICM   R1,0,0(R5)                                                       
EXICMRF  ICM   RF,0,0(R6)                                                       
EXSTCM   STCM  R1,0,0(R5)                                                       
*                                                                               
         SPACE 1                                                                
TESTACT  L     R1,UPARM            *** CC NOT AFFECTED! ***                     
         JZ    NO                  CONDITION CODE SET BY ADDS ABOVE             
         MVI   0(R1),2             NON-ZERO RESULT                              
         J     DOADDX                                                           
         SPACE 1                                                                
NO       CLI   0(R1),0             ZERO RESULT                                  
         JNE   DOADDX                                                           
         MVI   0(R1),1                                                          
         J     DOADDX                                                           
         EJECT                                                                  
*              MERGE SIGNIFICANT DATA INTO EMPTY COLUMNS                        
         SPACE 3                                                                
MERGE    BCTR  R1,0                                                             
         CLI   DRINTYPE,C'C'       IF CHAR. FIELD, TEST AGAINST SPACES          
         JNE   MERGE2                                                           
         EXRL  R1,CLCFROM          IF THERE'S SIGNIF. DATA IN FROM FLD          
         JNH   DOADDX                                                           
         EXRL  R1,CLCTO            AND NOTHING IN TO FIELD                      
         JH    DOADDX                                                           
         J     MERGEX              MERGE THE DATA                               
         SPACE 1                                                                
MERGE2   EXRL  R1,OCFROM           IF THERE'S SIGNIF. DATA IN FROM FLD          
         JZ    DOADDX                                                           
         EXRL  R1,OCTO             AND NOTHING IN TO FIELD                      
         JNZ   DOADDX                                                           
         SPACE 1                                                                
MERGEX   EXRL  R1,MVCFRTO          MERGE THE DATA                               
         J     DOADDX                                                           
*                                                                               
CLCFROM  CLC   0(0,R6),=CL256' '                                                
CLCTO    CLC   0(0,R5),=CL256' '                                                
OCFROM   OC    0(0,R6),0(R6)                                                    
OCTO     OC    0(0,R5),0(R5)                                                    
MVCFRTO  MVC   0(0,R5),0(R6)                                                    
         SPACE 1                                                                
DOADDX   CLI   FIXR5R6,C'Y'                                                     
         JNE   DOADDXX                                                          
         AHI   R5,-1                                                            
         AHI   R6,-1                                                            
         MVI   FIXR5R6,C'N'                                                     
*                                                                               
DOADDXX  L     RE,SAVERE                                                        
         L     R1,SAVER1                                                        
         BR    RE                                                               
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG AND ODDMENTS                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
         SPACE 1                                                                
*              DSECT FOR ADD MODULE                                             
         SPACE 3                                                                
DADDD    DSECT                                                                  
DUB2     DS    D                                                                
SIGN     DS    CL1                                                              
FIXR5R6  DS    C                                                                
UPARM    DS    A                                                                
SAVERE   DS    A                                                                
SAVER1   DS    A                                                                
DADDLNQ  EQU   *-DADDD                                                          
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013DRIVADD   06/28/12'                                      
         END                                                                    
