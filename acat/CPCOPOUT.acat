*          DATA SET CPCOPOUT   AT LEVEL 057 AS OF 05/01/02                      
*CATALP COPOUT                                                                  
         TITLE 'DUMP/MERGE/LOAD FOR CPP FILE '                                  
COPOUT   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,**CPLD**,=A(COPSAVE),R9,R8                                     
         XC    PREVKEY,PREVKEY     SET UP CONSTANTS                             
         MVI   BYPASS,0                                                         
         MVI   REQNO,0                                                          
         MVI   CURREQ,0                                                         
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE(34),=C'CPFILE DUMP/MERGE/LOAD DIAGNOSTICS'                 
         MVC   SUB1(13),=C'CONTROL CARDS'                                       
         MVC   SUB2(13),=13C'-'                                                 
         MVI   SPACING+3,C'2'                                                   
         EJECT                                                                  
*              INITIALIZATION                                                   
         SPACE 3                                                                
         GOTO1 =V(SORTER),PARA,SORTCARD,RECCARD                                 
         GOTO1 =V(STXITER),PARA,DUMPLIST                                        
         SPACE 2                                                                
CARD     GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
         MVC   P(80),C                                                          
         CLC   C(2),=C'/*'                                                      
         BE    INTIN                                                            
         GOTO1 =V(PRINTER)                                                      
         CLC   C(5),=C'DUMP='                                                   
         BNE   *+14                                                             
         MVC   DUMPOPT,C+5                                                      
         B     CARD                                                             
         SPACE 2                                                                
         CLC   C(5),=C'LOAD='                                                   
         BNE   *+14                                                             
         MVC   LOADOPT,C+5                                                      
         B     CARD                                                             
         SPACE 2                                                                
         CLC   C(6),=C'TRACE='                                                  
         BNE   *+14                                                             
         PACK  TRACEOPT,C+6(3)                                                  
         B     CARD                                                             
         SPACE 2                                                                
         CLC   C(5),=C'TAPE='                                                   
         BNE   *+14                                                             
         MVC   TAPEOPT,C+5                                                      
         B     CARD                                                             
         SPACE 2                                                                
         CLC   C(6),=C'MEDIA='                                                  
         BNE   *+14                                                             
         MVC   MEDIAOPT,C+6                                                     
         B     CARD                                                             
         EJECT                                                                  
         CLC   C(6),=C'PHASE='                                                  
         BNE   CARD2                                                            
         L     R2,=A(PHASE)                                                     
         GOTO1 =V(LOADER),PARA,C+6,(R2),(C'M',(R2))                             
         L     R2,PARA+4                                                        
         LTR   R2,R2                                                            
         BNZ   CARD                                                             
         MVC   P(15),=C'PHASE NOT FOUND'                                        
         GOTO1 =V(PRINTER)                                                      
         DC    H'0'                DUMP IF BAD CONTROL CARD                     
         B     CARD                                                             
         EJECT                                                                  
*              INITIALIZATION - BANK CONTROL                                    
         SPACE 3                                                                
CARD2    CLC   C(5),=C'BANK='                                                   
         BNE   CARD7                                                            
         L     R2,=V(BANKPOOL)                                                  
         SPACE 1                                                                
CARD4    OC    0(2,R2),0(R2)       LOOK FOR A SLOT                              
         BZ    CARD6                                                            
         LA    R2,2(R2)                                                         
         B     CARD4                                                            
         SPACE 1                                                                
CARD6    MVI   BANKOPT,C'Y'                                                     
         MVC   0(2,R2),C+5                                                      
         B     CARD                                                             
         SPACE 1                                                                
CARD7    CLC   C(8),=C'REQUEST='                                                
         BNE   CARD8                                                            
         MVC   REQNO(1),C+8                                                     
         NI    REQNO,X'0F'                                                      
         B     CARD                                                             
         SPACE 1                                                                
CARD8    CLC   C(6),=C'PURGE='     AAYYMMDDYYMMDD                               
         BNE   CARD9               AA= 2 CHAR. AGENCY CODE                      
         L     R2,=A(REQPOOL)                                                   
         L     R3,0(R2)                                                         
         LA    R3,1(R3)                                                         
         C     R3,4(R2)                                                         
         BNH   *+6                                                              
         DC    H'0'                POOL FILLED - EXTEND TABLE                   
         ST    R3,0(R2)                                                         
         BCTR  R3,0                                                             
         MH    R3,=H'40'                                                        
         LA    R2,8(R2,R3)                                                      
         MVC   0(16,R2),C+6                                                     
         B     CARD                                                             
         SPACE 1                                                                
CARD9    CLC   C(6),=C'INPUT='                                                  
         BNE   CARDEND                                                          
         MVC   INPUTOPT,C+6                                                     
         MVI   INTAPES,0                                                        
         CLI   INPUTOPT,C'N'       NO INTERFACE TAPES                           
         BE    CARD                                                             
         MVI   INTAPES,1           FORCE TO 1 INTERFACE TAPE PASS               
         B     CARD                                                             
         SPACE 2                                                                
CARDEND  MVC   P(27),=C'CONTROL CARD NOT RECOGNIZED'                            
         GOTO1 =V(PRINTER)                                                      
         B     CARD                                                             
         EJECT                                                                  
*              CONTROL READING OF INTERFACE TAPES                               
         SPACE 3                                                                
INTIN    ZAP   LINE,=P'75'                                                      
         LA    R5,ELEMENT                                                       
         L     R6,=A(INDATA)                                                    
         USING CPINTERD,R6                                                      
         L     R7,=A(OUTDATA)                                                   
         USING CPKEYD,R7                                                        
         LA    R7,4(R7)                                                         
         SPACE 2                                                                
         CLI   BANKOPT,C'Y'                                                     
         BE    MASTIN                                                           
         MVC   SUB1(13),=C'FILE DETAILS '                                       
         MVC   SUB2(13),=C'------------ '                                       
         MVC   SUB1+40(09),=C'NUMBER OF'                                        
         MVC   SUB2+40(09),=C' RECORDS '                                        
         SPACE 2                                                                
INTIN2   MVC   C(2),=C'NO'         CHECK FOR INTERFACE TAPES                    
         ZIC   RE,INTAPES          GET NUMBER OF INTERFACE TAPES                
         LTR   RE,RE               ANY LEFT                                     
         BZ    INTIN3              NO - DO REST OF JOB                          
         BCTR  RE,0                YES - DECREMENT COUNT                        
         STC   RE,INTAPES          AND SAVE                                     
         MVC   C(3),=C'YES'        SET TO READ INTERFACE TAPES                  
         SPACE 3                                                                
INTIN3   CLC   C(1),=C'NO'                                                      
         BE    MASTIN                                                           
         CLC   C(1),=C'YES'                                                     
         BNE   INTIN2                                                           
         ZAP   INTRECS,=P'0'                                                    
         OPEN  (INTER,(INPUT))                                                  
         SPACE 2                                                                
INTIN4   XCEF  (R7),1000                                                        
         GET   INTER,(R6)                                                       
         CLI   CPIATYPE,C'A'       CHECK FOR START OF REQUEST                   
         BNE   INTIN5                                                           
         CLI   REQNO,0             FILTERING REQUESTS                           
         BE    INTIN5              NO                                           
         CLI   CPIAWICH,C'1'                                                    
         BNE   INTIN5                                                           
         MVI   BYPASS,0                                                         
         ZIC   RE,CURREQ           COUNT THE REQUESTS                           
         LA    RE,1(RE)                                                         
         STC   RE,CURREQ                                                        
         CLC   REQNO,CURREQ        DO WE WANT THIS REQUEST                      
         BE    *+8                                                              
         MVI   BYPASS,1            NO                                           
INTIN5   CLI   BYPASS,0            BYPASS A REQUEST                             
         BNE   INTIN4                                                           
         SPACE 2                                                                
         AP    INTRECS,=P'1'                                                    
         IF    CPIATYPE,EQ,C'A',INTIN6                                          
         IF    CPIATYPE,EQ,C'B',INTIN8                                          
         IF    CPIATYPE,EQ,C'C',INTIN10                                         
         IF    CPIATYPE,EQ,C'D',INTIN12                                         
         DC    H'0'                                                             
         SPACE 2                                                                
INTIN6   CLI   CPIAWICH,C'2'       RECORD 1 HAS NAME                            
         BE    INTIN7                                                           
         MVC   SAVAGYNM,CPIANAME                                                
         CP    INTRECS,=P'1'       SUPPORT MULTIPLE AGENCIES ON TAPE            
         BE    INTIN4                                                           
         SP    INTRECS,=P'1'                                                    
         EDIT  (P4,INTRECS),(7,P+41)                                            
         GOTO1 =V(PRINTER)                                                      
         ZAP   INTRECS,=P'1'                                                    
         B     INTIN4                                                           
         SPACE 2                                                                
INTIN7   MVI   CPKTYPE,X'14'       A RECORD                                     
         MVC   CPKAGY,CPIACODE                                                  
         MVC   CPKMED,MEDIAOPT                                                  
         MVC   SAVAGY,CPIACODE                                                  
         USING CPNAMED,R5                                                       
         MVC   CPNAME(33),SAVAGYNM                                              
         MVC   CPNAME+33(33),CPIANAME     2 HAS ADDRESS                         
         BAS   RE,ELTRIM                                                        
         BAS   RE,ELADD                                                         
         BAS   RE,PUTSORT                                                       
         B     INTIN4                                                           
         SPACE 2                                                                
SAVAGYNM DS    CL33                                                             
         SPACE 2                                                                
INTIN8   CLC   CPIBSTRT,=C'920914' FIX FOR SEPT OVERLAP                         
         BNE   *+10                                                             
         MVC   CPIBSTRT,=C'920928'                                              
         CLC   CPIBSTRT,=C'940912' FIX FOR SEPT OVERLAP                         
         BNE   *+10                                                             
         MVC   CPIBSTRT,=C'940926'                                              
         CLC   CPIBSTRT,=C'950911' FIX FOR SEPT OVERLAP                         
         BNE   *+10                                                             
         MVC   CPIBSTRT,=C'950925'                                              
         CLC   CPIBSTRT,=C'960916' FIX FOR SEPT OVERLAP                         
         BNE   *+10                                                             
         MVC   CPIBSTRT,=C'960930'                                              
         CLC   CPIBSTRT,=C'970915' FIX FOR SEPT OVERLAP                         
         BNE   *+10                                                             
         MVC   CPIBSTRT,=C'970929'                                              
         L     R2,=A(REQPOOL)      B RECORD                                     
         L     R3,0(R2)                                                         
         LA    R3,1(R3)                                                         
         C     R3,4(R2)                                                         
         BNH   *+6                                                              
         DC    H'0'                POOL FILLED - EXTEND TABLE                   
         ST    R3,0(R2)                                                         
         BCTR  R3,0                                                             
         MH    R3,=H'40'                                                        
         LA    R2,8(R2,R3)                                                      
         MVC   0(16,R2),CPIBAGY                                                 
         MVC   P(33),=C'AGENCY=XX START=SSSSSS END=EEEEEE'                      
         MVC   P+7(2),CPIBAGY                                                   
         GOTO1 =V(DATCON),DMCB,(0,CPIBSTRT),(X'20',P+16)                        
         GOTO1 =V(DATCON),DMCB,(0,CPIBEND),(X'20',P+27)                         
         B     INTIN4                                                           
         SPACE 2                                                                
INTIN10  MVI   CPKTYPE,X'12'       C RECORD - CLIENT NAME                       
         MVC   CPKAGY,SAVAGY                                                    
         MVC   CPKMED,MEDIAOPT                                                  
         MVC   CPKCLT,CPICCODE                                                  
         USING CPNAMED,R5                                                       
         MVC   CPNAME(66),SPACES                                                
         MVC   CPNAME(20),CPICNAME                                              
         BAS   RE,ELTRIM                                                        
         BAS   RE,ELADD                                                         
         USING CPCLTD,R5                                                        
         MVI   CPCEL,X'06'                                                      
         MVI   CPCLEN,14                                                        
         MVC   CPCOFF(12),CPICOFF                                               
         BAS   RE,ELADD                                                         
         BAS   RE,PUTSORT                                                       
         B     INTIN4                                                           
         SPACE 2                                                                
INTIN12  BAS   RE,DATACONV         D RECORD                                     
         BAS   RE,PUTSORT          GO CREATE A CLIENT DATA RECORD               
         MVI   CPKTYPE,X'04'                                                    
         XC    CPKCLT,CPKCLT                                                    
         BAS   RE,PUTSORT          THEN ADD AN AGENCY RECORD                    
         B     INTIN4                                                           
         SPACE 2                                                                
INTEOF   EDIT  (P4,INTRECS),(7,P+41)                                            
         ZAP   INTRECS,=P'0'                                                    
         GOTO1 =V(PRINTER)                                                      
         CLOSE (INTER,)                                                         
         B     INTIN2                                                           
         EJECT                                                                  
*              SUBSIDIARY ROUTINES FOR INTERFACE HANDLING                       
         SPACE 3                                                                
         USING CPNAMED,R5                                                       
ELTRIM   NTR1                                                                   
         MVI   CPNEL,X'04'                                                      
         LA    R2,CPNAME+65                                                     
         LA    R3,68                                                            
         LA    R4,65                                                            
         SPACE 2                                                                
ELTRIM2  CLI   0(R2),C' '                                                       
         BNE   ELTRIM4                                                          
         BCTR  R2,0                                                             
         BCTR  R3,0                                                             
         BCT   R4,ELTRIM2                                                       
         SPACE 2                                                                
ELTRIM4  STC   R3,CPNLEN                                                        
         B     XIT                                                              
         SPACE 2                                                                
ELADD    NTR1                                                                   
         GOTO1 =V(HELLO),PARA,(C'P',=CL8'CPFILE'),(R7),(R5)                     
         CLI   PARA+8,0            SET CC                                       
         B     XIT                                                              
         SPACE 2                                                                
PUTSORT  NTR1                                                                   
         L     RF,=A(PHASE)                                                     
         GOTO1 (RF),PARA,(R7)                                                   
         LH    R2,CPLENGTH                                                      
         LA    R2,4(R2)                                                         
         SH    R7,=H'4'                                                         
         STH   R2,0(R7)                                                         
         GOTO1 =V(SORTER),PARA,=C'PUT',(R7)                                     
         MVI   ANYSORT,C'Y'                                                     
         B     XIT                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO CONVERT A CLIENT DATA RECORD                          
         SPACE 3                                                                
DATACONV NTR1                                                                   
         MVI   CPKTYPE,X'02'       FILL KEY                                     
         MVC   CPKAGY,CPIDAGY                                                   
         MVC   CPKMED,CPIDMED                                                   
         MVC   CPKMKT,CPIDMKT                                                   
         MVC   CPKCLT,CPIDCLT                                                   
         MVC   CPKTARGT,CPIDTARG                                                
         MVC   CPKDEMO,CPIDDEMO                                                 
         MVC   CPKSERV,CPIDSERV                                                 
         MVC   CPKDAYPT,CPIDDP                                                  
         MVC   CPKSPTLN,CPIDSL                                                  
         MVC   CPKPROG,CPIDPROG                                                 
         MVC   CPKAFFIL,CPIDAFFL                                                
         SPACE 2                                                                
         USING CPDATAD,R5          DATA ELEMENT                                 
         MVI   CPDEL,X'02'                                                      
         MVI   CPDLEN,6                                                         
         MVC   CPDEQUIV,CPIDEQIV                                                
         MVC   CPDMKTWT,CPIDMKWT                                                
         BAS   RE,ELADD                                                         
         SPACE 2                                                                
         USING CPPERFD,R5          PERFORMANCE ELEMENT                          
         MVC   CPPYEAR,CPIDYEAR                                                 
         MVI   CPPLEN,11                                                        
         MVC   CPPMONTH,CPIDMON                                                 
         LA    R2,CPIDSPOT                                                      
         LA    R3,CPPSPOTS                                                      
         LA    R4,4                                                             
         SPACE 2                                                                
DATACNV2 L     R1,0(R2)                                                         
         XPACK                                                                  
         STH   R1,0(R3)                                                         
         LA    R2,4(R2)                                                         
         LA    R3,2(R3)                                                         
         BCT   R4,DATACNV2                                                      
         BAS   RE,ELADD                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO READ INPUT MASTER FILES                              
*                                                                               
MASTIN   L     R7,=A(OUTDATA)                                                   
         LA    R7,4(R7)                                                         
         USING CPKEYD,R7                                                        
         OPEN  (CPTIN,(INPUT))                                                  
         L     R2,=A(REQPOOL)      SEED TABLE WITH BINARY Y/11/M                
         L     R3,0(R2)                                                         
         LA    R2,8(R2)                                                         
         USING REQPOOLD,R2                                                      
         LTR   R3,R3                                                            
         BZ    MASTIN4                                                          
*                                                                               
MASTIN2  GOTO1 =V(DATCON),PARA,(0,RQPSTART),(3,WORK)                            
         MVC   RQPSTB(1),WORK      SAVE THE YEAR                                
         MVC   RQPSTB+2(1),WORK+1  SAVE THE MONTH                               
*                                                                               
         CLC   RQPSTART+4(2),=C'20'                                             
         BL    MIN2A               IF DAY > OR = TO 20                          
*                                                                               
         SR    R1,R1               UP THE BROADCAST MONTH                       
         IC    R1,RQPSTB+2                                                      
         LA    R1,1(R1)                                                         
         C     R1,=F'12'           IF NOT > THAN 12                             
         BH    *+12                SAVE NEW MONTH                               
         STC   R1,RQPSTB+2                                                      
         B     MIN2A                                                            
*                                                                               
         MVI   RQPSTB+2,1          ELSE SET MONTH TO 1                          
*                                                                               
         SR    R1,R1                                                            
         IC    R1,RQPSTB           AND INCREASE THE YEAR                        
         LA    R1,1(R1)                                                         
         STC   R1,RQPSTB                                                        
*                                                                               
MIN2A    DS    0H                                                               
         MVI   RQPSTB+1,11         THIS IS ALWAYS 11                            
*                                                                               
         GOTO1 =V(DATCON),PARA,(0,RQPEND),(3,WORK)                              
         MVC   RQPNDB(1),WORK      SAVE THE YEAR                                
         MVC   RQPNDB+2(1),WORK+1  SAVE THE MONTH                               
*                                                                               
         MVI   RQPNDB+1,11                                                      
*                                                                               
         LA    R2,40(R2)                                                        
         BCT   R3,MASTIN2                                                       
*                                                                               
MASTIN4  GOTO1 =V(DATCON),PARA,(5,0),(3,DUB)                                    
         GOTO1 =V(DATCON),PARA,(3,DUB),(0,WORK)                                 
         GOTO1 =V(ADDAY),PARA,(C'Y',WORK),WORK+6,-4                             
         GOTO1 =V(DATCON),PARA,(0,WORK+6),(3,LESS4)                             
         ZIC   R2,DUB                                                           
         SH    R2,=H'5'            WORKING OUT HOW LONG TO KEEP DATA            
         STC   R2,OLDELS           SET UP OLDELS TO TODAY -5 YEARS              
         MVC   OLDELS+2,DUB+1                                                   
         EJECT                                                                  
*              READ MASTER FILE, MERGE IN SORT, WRITE NEW MASTER                
         SPACE 3                                                                
MAST     CLI   TAPEOPT,C'Y'        OPEN TAPE IF NEEDED                          
         BNE   MAST2                                                            
         OPEN  (CPTOUT,(OUTPUT))                                                
         SPACE 1                                                                
MAST2    CLI   LOADOPT,C'Y'        AND DISK                                     
         BNE   MAST4                                                            
         GOTO1 =V(ISDDS),PARA,V(ISOPEN),,,CPFILE,0,0                            
         SPACE 2                                                                
MAST4    L     R6,=A(INDATA)                                                    
         L     R7,=A(OUTDATA)                                                   
         BAS   RE,GETMAST                                                       
         BAS   RE,GETSORT                                                       
         CLI   ANYSORT,C'Y'                                                     
         BNE   MAST8                                                            
         SPACE 1                                                                
MAST6    CLC   4(16,R7),4(R6)      MASTER V SORT                                
         BH    MAST10                                                           
         BE    MAST12                                                           
         BAS   RE,PUTMAST          SORT IS HIGHER SO PUT OUT MASTER             
         BAS   RE,GETMAST                                                       
         B     MAST6                                                            
         SPACE 1                                                                
MAST8    BAS   RE,PUTMAST          NO SORT YET - PUTTING OUT MASTER             
         BAS   RE,GETMAST                                                       
         CLI   4(R7),X'FF'         EOF                                          
         BE    MASTEOF                                                          
         CLI   BANKSW,2            AS A RESULT OF GETTING MASTER FILE           
         BNE   MAST8               WE NOW HAVE SOME SORT RECORDS                
         L     R1,=A(SORTDATA)                                                  
         XC    4(16,R1),4(R1)                                                   
         L     R1,=A(INDATA)                                                    
         XC    4(16,R1),4(R1)                                                   
         BAS   RE,GETSORT                                                       
         B     MAST6                                                            
         SPACE 1                                                                
MAST10   LR    R0,R7               MASTER IS HIGHER                             
         LR    R7,R6                                                            
         BAS   RE,PUTMAST          SO WRITE NEW RECORD FROM SORT AREA           
         LR    R7,R0                                                            
         BAS   RE,GETSORT                                                       
         B     MAST6                                                            
         SPACE 1                                                                
MAST12   CLI   4(R7),X'FF'         RECORDS ARE EQUAL - CHECK EOF                
         BE    MASTEOF                                                          
         BAS   RE,MERGE            OTHERWISE MERGE RECORDS                      
         BAS   RE,PUTMAST          AND OUTPUT RESULTS                           
         BAS   RE,GETMAST                                                       
         BAS   RE,GETSORT                                                       
         B     MAST6                                                            
         SPACE 1                                                                
MASTEOF  MVC   P(17),=C'INPUT MASTER FILE'                                      
         MVC   0(32,R7),EOFREC                                                  
         BAS   RE,PUTMAST                                                       
         EDIT  (P4,INTRECS),(7,P+41)                                            
         GOTO1 =V(PRINTER)                                                      
         MVC   P(18),=C'OUTPUT MASTER FILE'                                     
         EDIT  (P4,OUTRECS),(7,P+41)                                            
         GOTO1 =V(PRINTER)                                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   MASTEOF2                                                         
         CLOSE (CPTOUT,)                                                        
         SPACE 1                                                                
MASTEOF2 GOTO1 =V(SORTER),DMCB,=C'END'                                          
         XBASE                                                                  
         EJECT                                                                  
*              ROUTINES TO GET RECORDS FROM MASTER FILE                         
         SPACE 3                                                                
GETMAST  NTR1                                                                   
         SPACE 1                                                                
GETMASTB L     R2,=A(OUTDATA)                                                   
         GET   CPTIN,(R2)                                                       
         AP    INTRECS,=P'1'                                                    
         LA    R7,4(R2)                                                         
         CLI   CPKTYPE,X'06'       BANK RECORDS REQUIRE SPECIAL                 
         BE    BANK8                PROCESSING                                  
         CLC   CPKMED,MEDIAOPT     PASS OTHER MEDIAS TO OUTPUT                  
         BNE   XIT                                                              
         CLI   CPKTYPE,X'02'                                                    
         BE    GETMAST2                                                         
         CLI   CPKTYPE,X'04'                                                    
         BE    GETMAST4                                                         
         B     XIT                                                              
         SPACE 1                                                                
GETMAST2 BAS   RE,RECTRIM          TRIM CLIENT RECORDS                          
         B     XIT                                                              
*                                                                               
GETMAST4 CLI   BANKOPT,C'Y'                                                     
         BE    BANK2                                                            
         BAS   RE,RECTRIM          TRIM AGENCY RECORDS                          
         B     XIT                                                              
*                                                                               
BANK2    LR    R2,R7               ALWAYS TRIM THE BANK DATA                    
         AH    R2,DATADISP                                                      
*                                                                               
BNKTRIM  CLI   0(R2),0                                                          
         BE    BANK3                                                            
         CLI   0(R2),70            LOWER THAN THIS ARE ALWAYS OK                
         BL    BNKTRIM2                                                         
         CLC   0(1,R2),LESS4       MUST BE HIGHER THAN CURRENT-4                
         BH    BNKTRIM2                                                         
         MVI   0(R2),X'FF'                                                      
*                                                                               
BNKTRIM2 ZIC   RE,1(R2)                                                         
         AR    R2,RE                                                            
         B     BNKTRIM                                                          
*                                                                               
BANK3    GOTO1 =V(HELLO),PARA,(C'D',=C'CPFILE'),(X'FF',(R7))                    
*                                                                               
         L     R2,=A(BANKPOOL)     BANK OPTION - IS AGENCY IN POOL              
         SPACE 1                                                                
BANK4    OC    0(2,R2),0(R2)                                                    
         BZ    XIT                 NO                                           
         CLC   0(2,R2),CPKAGY                                                   
         BE    BANK6                                                            
         LA    R2,2(R2)                                                         
         B     BANK4                                                            
         SPACE 1                                                                
BANK6    XC    CPKAGY,CPKAGY       YES - SO ADD A BANK RECORD TO SORT           
         MVI   CPKTYPE,X'06'                                                    
         BAS   RE,PUTSORT                                                       
         MVC   CPKAGY,0(R2)        RESTORE AGENCY CODE                          
         MVI   CPKTYPE,X'04'                                                    
         MVI   BANKSW,1                                                         
         B     XIT                                                              
         SPACE 1                                                                
BANK8    CLI   BANKOPT,C'Y'        BANK RECORDS                                 
         BNE   XIT                 ARE REQUIRED FROM MASTER                     
*                                                                               
         CLC   CPKMED,MEDIAOPT     SAME MEDIA                                   
         BE    *+8                 BYPASS BANK RECORDS                          
         BAS   RE,PUTSORT          SEND BANK RECS. TO SORT IF NOT               
*                                                                               
         CLI   BANKSW,1            SET SWITCH TO GET NEWLY FORMED               
         BNE   GETMASTB            BANK RECORDS FROM SORT                       
         MVI   BANKSW,2                                                         
         B     GETMASTB                                                         
         SPACE 1                                                                
CPTEOF   MVI   4(R7),X'FF'                                                      
         MVC   5(15,R7),4(R7)                                                   
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO GET SORT RECORDS MEGEING TOGETHER                    
*              RECORDS WITH IDENTICAL KEYS (RESULT IN INDATA)                   
         SPACE 3                                                                
GETSORT  NTR1                                                                   
         L     R6,=A(SORTDATA)                                                  
         L     R7,=A(INDATA)                                                    
         CLI   ANYSORT,C'Y'                                                     
         BE    GS2                                                              
         MVI   4(R7),X'FF'                                                      
         MVC   5(15,R7),4(R7)                                                   
         B     GS5                                                              
         SPACE 1                                                                
GS2      MOVE ((R7),1000),(R6)                                                  
         CLI   4(R7),X'FF'                                                      
         BE    XIT                                                              
         SPACE 1                                                                
GS4      GOTO1 =V(SORTER),PARA,=C'GET'                                          
         L     R2,PARA+4                                                        
         LTR   R2,R2                                                            
         BNZ   GS6                                                              
         SPACE 1                                                                
GS5      MVI   4(R6),X'FF'                                                      
         MVC   5(15,R6),4(R6)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GS6      MOVE  ((R6),1000),(R2)                                                 
         OC    4(16,R7),4(R7)                                                   
         BZ    GS2                                                              
         CLC   4(16,R6),4(R7)                                                   
         BNE   XIT                                                              
         BAS   RE,MERGE            SORT RECORDS EQUAL - ADD TOGETHER            
         B     GS4                                                              
         EJECT                                                                  
*              ROUTINE TO TRIM DOWN DATA RECORDS                                
         SPACE 3                                                                
RECTRIM  NTR1                                                                   
         LR    R5,R7                                                            
         AH    R5,DATADISP                                                      
         L     R2,=A(REQPOOL)                                                   
         L     R3,0(R2)                                                         
         LA    R2,8(R2)                                                         
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         USING REQPOOLD,R2                                                      
         SPACE 2                                                                
RECTRIM2 CLC   RQPAGY,CPKAGY       LOCATE AGENCY DETAILS IN POOL                
         BE    RECTRIM4                                                         
         LA    R2,40(R2)                                                        
         BCT   R3,RECTRIM2                                                      
         B     XIT                                                              
         SPACE 2                                                                
RECTRIM4 CLI   0(R5),0                                                          
         BE    RECTRIM8                                                         
         CLI   0(R5),70                                                         
         BL    RECTRIM6                                                         
         SPACE 1                                                                
         CLC   0(3,R5),OLDELS      GET RID OF OLD ELEMENTS                      
         BNL   *+8                                                              
         MVI   0(R5),X'FF'                                                      
         SPACE 1                                                                
         CLC   0(3,R5),RQPSTB      GET RID OF ANY ELEMENTS THAT ARE             
         BL    RECTRIM6            BEING REGENERATED BY THIS RUN                
         CLC   0(3,R5),RQPNDB                                                   
         BH    RECTRIM6                                                         
         MVI   0(R5),X'FF'                                                      
         SPACE 2                                                                
RECTRIM6 ZIC   R1,1(R5)                                                         
         AR    R5,R1                                                            
         B     RECTRIM4                                                         
         SPACE 2                                                                
RECTRIM8 LR    R5,R7                                                            
         AH    R5,DATADISP                                                      
         LA    R2,40(R2)           ANY OTHER AGENCY REQUESTS                    
         BCT   R3,*+8                                                           
         B     TRIMEND                                                          
         CLC   RQPAGY,CPKAGY                                                    
         BE    RECTRIM4                                                         
         B     RECTRIM8                                                         
         SPACE 1                                                                
TRIMEND  GOTO1 =V(HELLO),PARA,(C'D',=C'CPFILE'),(X'FF',(R7)),0                  
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO MERGE TWO RECORDS WITH SAME KEYS TOGETHER            
         SPACE 3                                                                
MERGE    NTR1                                                                   
         CLI   4(R7),X'12'         FOR AGENCY AND CLIENT NAMES                  
         BL    MERGE2                  USE LATEST VERSION                       
         MOVE  ((R7),1000),(R6)                                                 
         B     XIT                                                              
         SPACE 2                                                                
MERGE2   LA    R2,4(R7)                                                         
         GOTO1 =V(HELLO),PARA,(C'D',=C'CPFILE'),(X'02',(R2)),0                  
         LA    R4,4(R6)                                                         
         AH    R4,DATADISP                                                      
         SR    R3,R3                                                            
         SPACE 2                                                                
MERGE4   CLI   0(R4),0                                                          
         BE    XIT                                                              
         LR    R5,R2               LOOK TO SEE IF AN ELEMENT FOR                
         AH    R5,DATADISP         THIS MONTH EXISTS ALREADY                    
         SR    R6,R6                                                            
         SPACE 2                                                                
MERGE6   CLI   0(R5),0                                                          
         BE    MERGE12                                                          
*                                                                               
         CLC   0(3,R5),0(R4)                                                    
         BE    MERGE8                                                           
         IC    R6,1(R5)                                                         
         AR    R5,R6                                                            
         B     MERGE6                                                           
         SPACE 2                                                                
MERGE8   LA    R5,3(R5)            YES SO ADD IN THIS ONE                       
         LA    R6,3(R4)                                                         
         LA    R3,4                                                             
         SPACE 2                                                                
MERGE10  SR    R1,R1                                                            
         ICM   R1,3,0(R5)                                                       
         XUNPK                                                                  
         ST    R1,PARA                                                          
         SR    R1,R1                                                            
         ICM   R1,3,0(R6)                                                       
         XUNPK                                                                  
         A     R1,PARA                                                          
         XPACK                                                                  
         STH   R1,0(R5)                                                         
         LA    R5,2(R5)                                                         
         LA    R6,2(R6)                                                         
         BCT   R3,MERGE10                                                       
         B     MERGE14                                                          
         SPACE 2                                                                
MERGE12  GOTO1 =V(HELLO),PARA,(C'P',=C'CPFILE'),(R2),(R4)                       
         SPACE 2                                                                
MERGE14  SR    R3,R3                                                            
         IC    R3,1(R4)                                                         
         AR    R4,R3                                                            
         B     MERGE4                                                           
         EJECT                                                                  
*              ROUTINES TO WRITE RECORDS                                        
         SPACE 3                                                                
PUTMAST  NTR1                                                                   
         OC    0(20,R7),0(R7)                                                   
         BZ    XIT                                                              
         LH    R2,20(R7)                                                        
         LA    R2,4(R2)                                                         
         STH   R2,0(R7)                                                         
         AP    OUTRECS,=P'1'                                                    
         CP    TRACEOPT,=P'0'                                                   
         BE    PUTMAST2                                                         
         SP    TRACEOPT,=P'1'                                                   
         GOTO1 =V(PRNTBL),PARA,0,(R7),C'DUMP',(R2),=C'2D'                       
         SPACE 2                                                                
PUTMAST2 CLI   TAPEOPT,C'Y'                                                     
         BNE   PUTMAST4                                                         
         CLC   4(16,R7),PREVKEY    INSURE FILE IS IN SEQUENCE                   
         BH    *+6                                                              
         DC    H'0'                                                             
         MVC   PREVKEY,4(R7)                                                    
         PUT   CPTOUT,(R7)                                                      
         SPACE 2                                                                
PUTMAST4 CLI   LOADOPT,C'Y'        LOADING                                      
         BNE   XIT                                                              
         LA    R2,4(R7)            A(REC)                                       
         LH    R3,0(R7)                                                         
         SH    R3,=H'4'            LENGTH                                       
         LA    R4,CPFILE           A(DTF)                                       
         XC    DMCB(24),DMCB                                                    
         STM   R2,R4,DMCB+4                                                     
         GOTO1 =V(IVLDDS),DMCB                                                  
         B     XIT                                                              
         EJECT                                                                  
*              DTFS FOR PROGRAM                                                 
         SPACE 3                                                                
INTER    DCB   DDNAME=INTER,           DOS SYS005                      X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=00040,                                            X        
               BLKSIZE=00000,          DOS BLKSIZE=04000               X        
               MACRF=GM,                                               X        
               EODAD=INTEOF                                                     
         SPACE 2                                                                
CPTIN    DCB   DDNAME=CPTIN,           DOS SYS006                      X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08196,                                            X        
               BLKSIZE=08200,          DOS BLKSIZE=08200               X        
               MACRF=GM,                                               X        
               EODAD=CPTEOF                                                     
         SPACE 2                                                                
CPTOUT   DCB   DDNAME=CPTOUT,          DOS SYS007                      X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08196,                                            X        
               BLKSIZE=08200,          DOS BLKSIZE=08200               X        
               MACRF=PM                                                         
         EJECT                                                                  
*              WORK SPACE AND CONSTANTS                                         
         SPACE 3                                                                
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARA     DS    6F                                                               
DMCB     DS    6F                                                               
C        DS    CL80                                                             
PREVKEY  DS    CL16                                                             
BYPASS   DS    C                                                                
CURREQ   DS    C                                                                
ELEMENT  DS    CL255                                                            
INPUTOPT DC    C'Y'                                                             
TRACEOPT DC    PL4'0'                                                           
LOADOPT  DC    C'Y'                                                             
BANKOPT  DC    C'N'                                                             
BANKSW   DC    AL1(0)                                                           
REQNO    DC    AL1(0)                                                           
INTAPES  DC    AL1(0)                                                           
ANYSORT  DC    C'N'                                                             
DUMPOPT  DC    C'N'                                                             
TAPEOPT  DC    C'Y'                                                             
MEDIAOPT DC    C'T'                                                             
MESSA    DC    C'ANY INTERFACE TAPES? ENTER YES OR NO'                          
MESSB    DC    C'ANY MORE INTERFACE TAPES? ENTER YES OR NO'                     
SORTCARD DC    CL80'SORT FIELDS=(5,16,A),FORMAT=BI,WORK=1 '                     
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=(1000,,,24,100)  '                     
LESS4    DS    CL3                                                              
DUMPLIST DS    0F                                                               
         DC    A(COPOUT)                                                        
         DC    V(ADDAY)                                                         
         ORG   *-4                                                              
         DC    X'80'                                                            
         DS    0F                                                               
INTRECS  DC    PL4'0'                                                           
OUTRECS  DC    PL4'0'                                                           
SAVAGY   DC    CL2'  '                                                          
OLDELS   DC    X'000B00'                                                        
DATADISP DC    H'19'                                                            
EOFREC   DC    H'24'                                                            
         DC    H'0'                                                             
         DC    16X'FF'                                                          
         DC    H'20'                                                            
         DC    X'0000'                                                          
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
CPFILE   DMIS  KEYLEN=16,RECSIZE=V,BLKSIZE=10796,                      X        
               SPARE=0,INDSIZE=500,DSKXTNT=5,IXPDOV=Y                           
         EJECT                                                                  
*              STORAGE AREA FOR PROGRAM                                         
         SPACE 1                                                                
         ENTRY COPSAVE                                                          
         ENTRY REQPOOL                                                          
         ENTRY INDATA                                                           
         ENTRY SORTDATA                                                         
         ENTRY BANKPOOL                                                         
         ENTRY OUTDATA                                                          
         ENTRY PHASE                                                            
         SPACE 1                                                                
         DS    0D                                                               
         DC    C'***SAVE STORAGE*'                                              
COPSAVE  DC    5000X'00'                                                        
         SPACE 1                                                                
         DC    C'**REQUEST POOL**'                                              
REQPOOL  DC    F'0'                                                             
         DC    F'200'                                                           
         DC    4000X'00'                                                        
         SPACE 1                                                                
         DC    C'* INPUT DATA ***'                                              
INDATA   DC    1008X'00'                                                        
         SPACE 1                                                                
         DC    C'** SORT DATA ***'                                              
SORTDATA DC    1008X'00'                                                        
         SPACE 1                                                                
         DC    C'*** BANK POOL **'                                              
BANKPOOL DC    256X'00'                                                         
         DC    C'* OUTPUT DATA **'                                              
OUTDATA  DC    1040X'00'                                                        
         SPACE 2                                                                
         DC    C'**** PHASE *****'                                              
PHASE    BR    14                                                               
         DS    2000C                                                            
         EJECT                                                                  
*              DSECT TO COVER REQUEST POOL ITEM                                 
         SPACE 3                                                                
REQPOOLD DSECT                                                                  
RQPAGY   DS    CL2                                                              
RQPSTART DS    CL6                                                              
RQPEND   DS    CL6                                                              
RQPSTB   DS    CL3                                                              
RQPNDB   DS    CL3                                                              
         SPACE 3                                                                
       ++INCLUDE DMDTFIS                                                        
         SPACE 3                                                                
       ++INCLUDE CPGENINTER                                                     
         EJECT                                                                  
       ++INCLUDE CPGENFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'057CPCOPOUT  05/01/02'                                      
         END                                                                    
