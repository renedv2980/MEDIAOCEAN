*          DATA SET PPGETADRR  AT LEVEL 023 AS OF 03/09/00                      
*CATALP PPGETADR                                                                
         TITLE 'PPGETADR - MODULE TO HANDLE PUB ADDRESS READING'                
*                                                                               
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*  SMYE 03/05/99  ADDRESS DATA OUTPUT SIZE INCREASED FROM 200 TO 300            
*                 BYTES FOR ADDED E-MAIL ADDRESS (AND SOME SPARE)               
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*        INPUT                                                                  
*                                                                               
*   PARAM1   BYTE  0    KIND OF REC  - C'C' = CONTRACT ADDRESS                  
*                                      C'P' = PAYING   ADDRESS                  
*                                      C'S' = SHIPPING ADDRESS                  
*                                      C'T' = TRAFFIC  ADDRESS                  
*                                                                               
*            BYTES 1-3  A(PCLTREC DATA - 7 BYTES AS FOLLOWS)                    
*                                      CLIENT AGENCY - 2                        
*                                      CLIENT MEDIA  - 1                        
*                                      CLIENT CODE   - 3                        
*                                      CLIENT OFFICE - 1                        
*                                                                               
*   PARAM2              A(PUBREC)                                               
*                                                                               
*   PARAM3              A(DATAMGR)                                              
*                                                                               
*        OUTPUT                                                                 
*                                                                               
*   PARAM1   BYTE  0    X'FF' IF ERROR                                          
*                       X'00' IF NO ADDRESS FOUND                               
*                       X'NN' RECORD CODE OF ADDRESS FOUND                      
*                                                                               
*            BYTES 1-3  3X'FF' = AGENCY                                         
*                       X'FF' + X + BLANK = OFFICE X                            
*                       ELSE CLIENT                                             
*                                                                               
*   PARAM2              A(300-BYTES OF ADDRESS DATA (IF FOUND))                 
*                                                                               
*   PARAM3              DISK ADDRESS OF ADDRESS RECORD (IF FOUND)               
*                                                                               
*   PARAM4              DISPLACEMENT INTO RECORD OF ADDRESS ELEMENT             
*                                                                               
PPGETADR CSECT                         **** NOTE: S/B PPGETADR                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPGETADR,CLEAR=YES                                   
         USING WORKD,RC                                                         
         OC    1(3,R1),1(R1)       PCLTREC DATA ADDRESS GIVEN ?                 
         BZ    PGAERR              NO - ERROR                                   
         OC    4(4,R1),4(R1)       PUBREC ADDRESS GIVEN ?                       
         BZ    PGAERR              NO - ERROR                                   
         OC    8(4,R1),8(R1)       DATAMGR ADDRESS GIVEN ?                      
         BZ    PGAERR              NO - ERROR                                   
*                                                                               
         LR    R8,R1               R8=A(PARAMETER LIST)                         
         L     R3,0(R8)            R3=A(PCLTREC DATA)                           
         L     R9,4(R8)            R9=A(PUBREC)                                 
*                                                                               
         MVC   DATAMGR,8(R8)       V(DATAMGR)                                   
         MVC   ADDRTYP(1),0(R8)    ADDRESS TYPE - C, P, S, OR T                 
*                                                                               
         XC    0(16,R8),0(R8)      AREA FOR RETURN INFO                         
         LA    R7,ADRAREA          R7=A(300-BYTE OUTPUT AREA)                   
         LA    RF,300                                                           
         XCEF  (R7),(RF)                                                        
*                                                                               
         USING PGETADRD,R7                                                      
         USING PUBRECD,R9                                                       
         USING PCLTRECD,R3                                                      
*                                                                               
*                                                                               
         XC    WORK,WORK                                                        
         MVI   WORK,X'08'        PAYING                                         
         CLI   ADDRTYP,C'P'                                                     
         BE    RREPADR           LOOK FOR PAYING ADDRESSES (PUBAREC)            
         MVI   WORK,X'09'        TRAFFIC                                        
         CLI   ADDRTYP,C'T'                                                     
         BE    RREPADR           LOOK FOR TRAFFIC ADDRESSES (PUBAREC)           
         MVI   WORK,X'0B'        SHIPPING ADDRESS                               
         CLI   ADDRTYP,C'S'                                                     
         BE    RREPADR           LOOK FOR SHIPPING ADDRESSES (PUBAREC)          
         MVI   WORK,X'0A'        CONTRACT                                       
         CLI   ADDRTYP,C'C'                                                     
         BE    RREPADR           LOOK FOR CONTRACT ADDRESSES (PUBAREC)          
         B     PGAERR            ERROR - INVALID OR NO REC TYPE                 
         SPACE 2                                                                
*                                                                               
RREPADR  DS    0H         **** LOOK FIRST FOR PUB ADDRESS RECORDS ****          
*                                                                               
         LA    R4,KEY                                                           
         USING PUBARECD,R4                                                      
         XC    KEY,KEY                                                          
         MVC   PUBAKEY(9),PUBKEY   MEDIA THRU AGENCY CODE                       
         MVI   PUBAKCOD,X'82'      RECORD CODE                                  
         MVC   PUBAKTYP(1),WORK    ADDRESS TYPE                                 
         MVC   PUBAKCLT,PCLTKCLT   CLIENT CODE                                  
*                                                                               
RADREAD  MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PUBDIR,KEY,KEY                           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PUBAKEY(10),KEYSAVE   ANY ADDRESS RECORDS ?                      
         BNE   RREPPUB             NO - GO TRY FOR ADDRESS ELEMENTS             
         CLC   PUBAKTYP(1),WORK    SAME ADDRESS TYPE ?                          
         BE    RADR2               YES                                          
         CLI   WORK,X'0B'          LOOKING FOR SHIPPING ?                       
         BNE   RREPPUB             NO - GO TRY FOR ADDRESS ELEMENTS             
         MVI   WORK,X'09'          YES AND NOT FOUND - GO TRY FOR               
         B     RREPADR             TRAFFIC                                      
*                                                                               
RADR2    DS    0H                  FOUND RECORD                                 
         CLC   PUBAKCLT,=3X'FF'    IS IT A DEFAULT ?                            
         BE    RADR6               YES - GET THE RECORD                         
         CLC   PUBAKCLT,PCLTKCLT   CLIENT SAME AS IN CLIENT HEADER ?            
         BE    RADR6               YES - GET THE RECORD                         
         CLI   PUBAKCLT,X'FF'      OFFICE OVERRIDE ?                            
         BNE   RADR4               NO - TRY FOR OFFICE                          
         CLC   PUBAKCLT+1(1),PCLTOFF   OFFICE MATCH CLIENT HEADER ?             
         BE    RADR6               YES - GET THE RECORD                         
         CLI   KEYSAVE+11,X'FF'    WAS I LOOKING FOR AN OFFICE ?                
         BNE   RADR4               NO - TRY FOR ONE                             
         MVC   PUBAKCLT,=3X'FF'    YES - AND NOT FOUND SO                       
         B     RADREAD             TRY FOR DEFAULT                              
*                                                                               
RADR4    DS    0H                       TRY FOR AN OFFICE                       
         MVI   PUBAKOFF,X'FF'                                                   
         MVC   PUBAKOFF+1(1),PCLTOFF                                            
         MVI   PUBAKOFF+2,X'40'                                                 
         B     RADREAD                                                          
*                                                                               
RADR6    DS    0H                  GET THE ADDRESS RECORD                       
         GOTO1 DATAMGR,DMCB,(0,GETREC),PUBFIL,KEY+27,IO,DMWORK                  
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   8(4,R8),KEY+27      SAVE DISK ADDRESS FOR WRITER                 
         MVI   15(R8),PGADELQ      SAVE DISPLACEMENT FOR WRITER                 
         MVC   WORK(1),PUBAKTYP    SAVE ADDRESS TYPE                            
         MVC   WORK+1(3),PUBAKOFF  SAVE OFFICE/CLIENT/DEFAULT                   
         LA    R5,IO+PGADELQ                                                    
         ST    R5,FULL             SAVE A(ADDRESS OVERRIDE ELEM)                
*                                                                               
         B     RREP20              SET UP RETURN ADDRESS AND DATA               
*                                                                               
         DROP  R4                                                               
*                                                                               
         EJECT                                                                  
RREPPUB  DS    0H             ** LOOK FOR ADDRESS ELEMENTS IN PUBREC **         
*                                  IF PUB ADDRESS RECORDS NOT FOUND             
         SPACE 2                                                                
         CLI   ADDRTYP,C'S'        LOOKING FOR SHIPPING ?                       
         BNE   RREP0               NO - WORK IS OK                              
         MVI   WORK,X'0B'          YES - MAKE SURE I TRY FOR SHIP CODE          
*                                        ADDRESS FIRST                          
RREP0    SR    R0,R0                                                            
         LA    R2,PUBREC+33                                                     
RREP1    DS    0H                                                               
         CLI   0(R2),0             END OF RECORD ?                              
         BE    RREP6               YES                                          
         CLC   0(1,R2),WORK        CHECK FOR ADDRESS OVERRIDE ELEMENT           
         BE    RREP3                                                            
RREP2    DS    0H                  BUMP TO NEXT ELEMENT                         
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RREP1                                                            
RREP3    DS    0H                  FOUND ELEMENT                                
         USING PUBAOVEL,R2         USE ADDRESS ELEMENT DSECT                    
         CLC   PUBAOFF,=3X'FF'     IS IT A DEFAULT ?                            
         BE    RREP4               YES                                          
         CLC   PUBAOFF,PCLTKCLT    CLIENT SAME AS IN CLIENT HEADER ?            
         BE    RREP4               YES                                          
         CLI   PUBAOFF,X'FF'       OFFICE OVERRIDE ?                            
         BNE   RREP2               NO - GET ANOTHER ELEMENT                     
         CLC   PUBAOFF+1(1),PCLTOFF    OFFICE MATCH CLIENT HEADER ?             
         BNE   RREP2               NO - GET ANOTHER ELEMENT                     
RREP4    DS    0H                                                               
         MVC   WORK+1(3),PUBAOFF   SAVE OFFICE/CLIENT/DEFAULT                   
         ST    R2,FULL             SAVE A(ELEM)                                 
         LA    R4,PUBREC           POINT TO RECORD                              
         SR    R2,R4               GET DISPLACEMENT INTO RECORD                 
         ST    R2,12(R8)           SAVE DISPLACEMENT (FOR WRITER)               
*                                                                               
         B     RREP20              SET UP RETURN ADDRESS AND DATA               
*                                                                               
RREP6    DS    0H                                                               
         CLI   WORK,X'0B'          SEE IF I WAS TRYING FOR SHIPPING             
         BNE   PGAXIT       NO ADDRESS FOUND - EXIT WITH BYTE 0=X'00'           
         MVI   WORK,X'09'          YES AND NOT FOUND - THEN TRY FOR             
         B     RREP0               TRAFFIC                                      
*                                                                               
         DROP  R2                                                               
*                                                                               
         SPACE 3                                                                
RREP20   DS    0H                  ********  OUTPUT  ********                   
         MVI   0(R8),0                                                          
         CLI   WORK+1,0                                                         
         BE    PGAXIT       NO ADDRESS FOUND - EXIT WITH BYTE 0=X'00'           
         L     R2,FULL             RESTORE POINTER TO ADDRESS                   
         USING PUBAOVEL,R2         ADDRESS FOUND                                
         MVC   0(1,R8),PUBAOVEL    FOUND ADDRESS CODE                           
         MVC   1(3,R8),PUBAOFF                                                  
*                                                                               
         MVC   PGADELEM(PGADLEN),PUBAOVEL      MOVE ENTIRE ELEMENT              
*                                                                               
*NOP*    MVC   PGADELEM(2),PUBAOVEL                                             
*NOP*    MVC   PGADNAME(122),PUBAONAM   (NAME,LIN1+2,ATTN,TELE)                 
*                                                                               
*NOP*    CLI   PUBAOVEL+1,165      SEE IF "SHORT" ELEMENT                       
*NOP*    BL    RREP30                                                           
*                                                                               
*NOP*    MVC   PGADLIN3,PUBAOLN3                                                
*NOP*    MVC   PGADFAX,PUBAOFAX                                                 
*                                                                               
*NOP*    CLI   PUBAOVEL+1,225      SEE IF E-MAIL ADDRESS IN ELEMENT             
*NOP*    BL    RREP30                                                           
*                                                                               
*NOP*    MVC   PGADEADD,PUBAOEAD                                                
*                                                                               
RREP30   MVC   PGADKEY(3),PCLTKAGY        AGY/MED                               
         MVC   PGADKRCD(1),PUBAOVEL       RECORD CODE                           
         MVC   PGADKPUB(6),PUBKPUB        PUB CODE                              
*                                                                               
         B     PGAXIT              DONE                                         
*                                                                               
         DROP  R2                                                               
*                                                                               
PGAERR   DS    0H                                                               
         MVI   0(R8),X'FF'         ERROR IN CALL                                
         B     PGAXITX                                                          
*                                                                               
PGAXIT   DS    0H                                                               
         ST    R7,FULL                                                          
         MVC   4(4,R8),FULL        ADDRESS OF ADDRESS DATA                      
PGAXITX  XIT1                                                                   
         LTORG                                                                  
         SPACE 1                                                                
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
PRTDIR   DC    C'PRTDIR  '                                                      
PRTFIL   DC    C'PRTFILE '                                                      
PUBDIR   DC    C'PUBDIR  '                                                      
PUBFIL   DC    C'PUBFILE '                                                      
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE **                        
WORK     DS    CL64                                                             
SPACES   DS    CL20                                                             
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
ELCODE   DS    CL1                                                              
ADDRTYP  DS    CL1                                                              
FULL     DS    F                                                                
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
ADRAREA  DS    300C                CREATE ADDRESS DATA HERE                     
IO       DS    300C                PUBAREC LESS THAN 300 BYTES                  
WORKX    EQU   *                                                                
         EJECT                                                                  
PGETADRD DSECT                                                                  
       ++INCLUDE PPGETADRD                                                      
         EJECT                                                                  
PCLTRECD DSECT                                                                  
PCLTKAGY DS    CL2                                                              
PCLTKMED DS    CL1                                                              
PCLTKCLT DS    CL3                                                              
PCLTOFF  DS    CL3                                                              
         EJECT                                                                  
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBREPEL                                                       
         EJECT                                                                  
         ORG   PUBREC                                                           
PUBARECD DSECT                                                                  
       ++INCLUDE PPUBAREC                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023PPGETADRR 03/09/00'                                      
         END                                                                    
