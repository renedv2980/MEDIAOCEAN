*          DATA SET SPRUNCONS  AT LEVEL 046 AS OF 05/01/02                      
*CATALP SPRUNCON                                                                
         SPACE 1                                                                
********************************************************************            
*                                                                  *            
* THIS IS A NEW OS COMPATIBLE VERSION. LIVE DOS IS SPRUNMST        *            
*                                                                  *            
********************************************************************            
         TITLE 'MODULE TO CONTROL SPONSOR RUN'                                  
SPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,SPRUNCON,=V(SPSAVE)                                            
         LA    R7,2048(RB)                                                      
         LA    R7,2048(R7)                                                      
         USING SPRUNCON+4096,R7                                                 
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD+4096,R9                                                  
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
*                                                                               
         OC    PRINT,PRINT         TEST VPRINT UNRESOLVED                       
         BNZ   *+12                                                             
         L     R0,=V(PRINTS)       POINT TO NEW ENTRY POINT                     
         ST    R0,PRINT                                                         
*                                                                               
         MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
*                                                                               
         LA    R2,DUB                                                           
         EXTRACT (R2),FIELDS=TIOT                                               
         MVC   RCJOB,0(R2)                                                      
*                                                                               
         GOTO1 =V(RUNSTART)                                                     
         MVC   RCUPSI,MCUPSI                                                    
*                                                                               
         MVI   BYTE,QMSPACK        LOAD MSPACK                                  
         BAS   RE,SPLOAD                                                        
         MVC   MSPACK,4(R1)                                                     
*                                                                               
         MVI   BYTE,QMSUNPK        LOAD MSUNPK                                  
         BAS   RE,SPLOAD                                                        
         MVC   MSUNPK,4(R1)                                                     
*                                                                               
         MVI   BYTE,QGETRATE       LOAD GETRATE                                 
         BAS   RE,SPLOAD                                                        
         MVC   GETRATE,4(R1)                                                    
*                                                                               
         MVI   BYTE,QTSAROFF       LOAD TSAROFF                                 
         BAS   RE,SPLOAD                                                        
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         MVC   TSAROFF,4(R1)                                                    
         DROP  RE                                                               
*                                                                               
         MVI   BYTE,QBLDMGA        LOAD SPBLDMGA                                
         BAS   RE,SPLOAD                                                        
         MVC   VBLDMGA,4(R1)                                                    
*                                                                               
         MVI   BYTE,QGETDEM1       LOAD SPGETDEME (T00A20)                      
         BAS   RE,SPLOAD                                                        
         MVC   GETDEMO,4(R1)                                                    
         B     READCARD                                                         
*                                                                               
SPLOAD   LR    R0,RE                                                            
         MVC   DUB,SPACES                                                       
         MVC   DUB(4),=C'T00A'                                                  
         GOTO1 HEXOUT,DMCB,BYTE,DUB+4,1,0                                       
         SPACE 1                                                                
*=========================================================*                     
* SEE IF WE NEED TO LOAD  TEST VERSION OF THIS PHASE      *                     
*=========================================================*                     
         SPACE 1                                                                
         L     R1,MCTSTPL          GET ADDRESS OF CESSPOOL                      
*                                                                               
SPLOAD2  CLI   0(R1),0                                                          
         BE    SPLOAD10                                                         
         CLC   DUB(6),5(R1)        MATCH TEST=T000.. FOR 6                      
         BE    SPLOAD4             NOTE PHASE NAME IS AFTER TEST= !             
         LA    R1,L'MCREQREC(R1)                                                
         B     SPLOAD2                                                          
*                                                                               
SPLOAD4  MVC   DUB(7),5(R1)        ADD VERSION TO PHASE NAME                    
*                                                                               
SPLOAD10 GOTO1 LOADER,DMCB,DUB,0                                                
         BAS   RE,LOADCHEK                                                      
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
* INITIAL CONTROL CARDS *                                                       
READCARD DS    0H                                                               
*                                                                               
* NOTE DID NOT CALL SPLOAD SO COULD OVERRIDE VERSION                            
         MVC   DUB(8),=CL8'T00A7A' LOAD STAPACK                                 
         MVC   DUB+6(1),MCSTAVER   MOVE OVERRIDE VERSION                        
         GOTO1 LOADER,DMCB,DUB,0                                                
         BAS   RE,LOADCHEK                                                      
         MVC   VSTAPACK,4(R1)                                                   
*                                                                               
*&&OS*&& MVC   SPECS,MCAPHAS1      SET LOAD ADDRESS                             
         MVC   APPLIC,MCAPHAS2                                                  
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TODAY)                                 
         GOTO1 (RF),(R1),,(3,TODAYB)                                            
         GOTO1 (RF),(R1),,(2,TODAYP)                                            
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCAGYCRD,MCUSER                                                  
         MVC   RCAGENCY,MCUSER                                                  
         MVC   RCLANG,MCLANG                                                    
         MVC   RCCTRY,MCCTRY                                                    
         MVC   AGYNM,MCORIGIN                                                   
         MVC   AGYADR,MCORIGAD                                                  
         MVC   RCORIGID,MCORIGID                                                
         MVC   RECOVERY,MCRECOVR                                                
         MVC   OVERLOAD,MCLOAD                                                  
         L     R3,MCAPHAS3         FOR MVS THIS ADDRESS WILL BE SET             
         ST    R3,SUBCON            PROPERLY AFTER LOAD IS DONE                 
         SPACE 2                                                                
         MVI   RCFLONLY,C'Y'       GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
*                                  NOW LOAD IN SPECIAL FILE HANDLER             
         MVC   DUB,FCPHASE                                                      
         CLI   DUB+6,C' '                                                       
         BNE   *+10                                                             
         MVC   DUB+6(1),MCTEST3                                                 
         GOTO1 LOADER,DMCB,DUB,(R3)                                             
         BAS   RE,LOADCHEK                                                      
*&&OS*&& MVC SUBCON,4(R1)           SET SUBCON ADDRESS AFTER LOAD               
         EJECT                                                                  
*&&DO                                                                           
         A     R3,DMCB                                                          
         LA    R3,8(R3)                                                         
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,ASORT                                                         
*&&                                                                             
         SPACE 1                                                                
********************************************************************            
* THE FOLLOWING CODE ALWAYS ATTEMPTS TO LOAD AN 04 PHASE. PREVIOUS *            
* TEST FOR V(MEDTABLE) PRESENT WAS INEFFECTIVE. WHEN ERROR OCCURS  *            
* IT IS IGNORED. NEED A NEW SPEC FOR MEDIA SUMMARY MODULES TO      *            
* EXPLICITLY STATE THAT AN 04 PHASE IS PRESENT.                    *            
********************************************************************            
         SPACE 1                                                                
         MVC   DUB,=C'SPNN04  '    LOAD IN A SPNN04 PHASE                       
         MVC   DUB+2(2),RCPROG                                                  
         MVC   DUB+6(1),MCTEST4                                                 
         CLC   OVERLOAD,SPACES                                                  
         BE    *+10                                                             
         MVC   DUB(4),OVERLOAD                                                  
         L     R3,MEDTABLE                                                      
         GOTO1 LOADER,DMCB,DUB,(R3)                                             
*&&OS*&& MVC   MEDTABLE,4(R1)         SET LOAD ADDRESS                          
         B     OPEN2                                                            
         SPACE 2                                                                
LOADCHEK OC    DMCB+4(4),DMCB+4                                                 
         BCR   7,RE                                                             
         MVC   WORK(16),=C'CANT FIND PHASE '                                    
         MVC   WORK+16(8),DUB                                                   
         GOTO1 LOGIO,DMCB,1,(24,WORK)                                           
         GOTO1 (RF),(R1),,=C'SPONSOR IS GIVING UP'                              
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 999                                                              
         EJECT                                                                  
* OPEN FILE ROUTINES *                                                          
         SPACE 1                                                                
OPEN2    CLI   RCWRITE,C'N'        TEST WRITE = NO                              
         BE    OPEN3                                                            
*                                                                               
         CLC   =C'NNN',FCUPDIR     CHECK NO UPDATES                             
         BE    OPEN3                                                            
         CLI   FCUPFILE,C'Y'                                                    
         BNE   *+8                                                              
         MVI   FSPTFILE,C'U'                                                    
         CLI   FCUPDIR,C'Y'                                                     
         BNE   *+8                                                              
         MVI   FSPTDIR,C'U'                                                     
         CLI   FCUPSTAT,C'Y'                                                    
         BNE   *+8                                                              
         MVI   FSTAFILE,C'U'                                                    
*                                                                               
         CLI   RECOVERY,C'N'                                                    
         BE    OPEN3                                                            
         MVC   FRECV(10),=C'URECV   X '                                         
         L     RF,=V(SSB)                                                       
         XC    0(4,RF),0(RF)                                                    
         MVI   2(RF),X'FF'                                                      
         SPACE                                                                  
*                                                                               
         CLI   MCRECOVR,C'W'       TEST FACWK RECOVERY                          
         BNE   OPEN3                                                            
         GOTO1 =V(DATCON),DMCB,(4,MCDATE),(1,FULL)                              
         L     RF,=V(SSB)                                                       
         USING SSOOFF,RF                                                        
         MVI   SSOXTND,X'FF'   +02  X'FF' TO SHOW OFFLINE EXTENSION             
         NI    SSOSTAT2,255-SSOSNRCV                                            
         OI    SSOSTAT2,SSOSRWRK    SET FACWK RECOVERY BIT IN SSB               
         L     R0,SSOFWBUF                                                      
         L     RF,SSOFWNDX                                                      
         USING UKRECD,RF            SET FACWRK KEY VALUES                       
         MVC   UKUSRID,MCORIGID     USERID                                      
         MVC   UKSYSPRG(1),MCSYSTEM SYSTEM                                      
         MVC   UKSYSPRG+1(2),MCPROG PROGRAM                                     
         CLI   MCFACPAK,C' '                                                    
         BNH   *+10                                                             
         MVC   UKSUBPRG,MCFACPAK    FACPAK SYSTEM ID                            
         MVC   UKDAY,FULL+2         DAY NUMBER                                  
         MVI   UKCLASS,C'R'         CLASS=RECOVERY                              
         MVI   UKFLAG,X'01'         FLAG DUPLICATES ALLOWED                     
         MVC   UKFILNO,=X'FFFF'     LET WRKR KNOW UKFLAG IS VALID               
         DROP  RF                                                               
         SPACE 2                                                                
OPEN3    L     R2,ADAGY                                                         
         MVC   WORK(2),RCAGYCRD                                                 
         BAS   RE,GETAGINF         GO AND SET FILE NO. IN UTL                   
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'SYSFLES',0                            
         ICM   RE,15,DMCB+12       A(FILE LIST FOR THIS SPOT SYSTEM)            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SR    R1,R1                                                            
         ICM   R1,3,2(RE)          NUMBER OF FILES IN SYSTEM                    
         LA    RE,4(RE)            BUMP TO FIRST FILE IN LIST                   
*                                                                               
OPEN3A   CLI   3(RE),X'33'         STRFDR?                                      
         BE    OPEN3B              YES -- SEE IF IT'S NOP                       
         LA    RE,8(RE)            NO, TRY NEXT FILE                            
         BCT   R1,OPEN3A                                                        
         B     OPEN3C              NO TRAFFIC FILES -- REMOVE FROM LIST         
*                                                                               
OPEN3B   TM    0(RE),X'80'         ARE TRAFFIC FILES NOP                        
         BZ    OPEN3D              NO                                           
*                                                                               
OPEN3C   MVC   FSTRFFL(10),FRECV   YES -- TAKE THEM OUT OF THE LIST             
         MVI   FCUPTRF,C'N'        AND FORGET ABOUT UPDATING TRAFFIC            
*                                                                               
OPEN3D   DS    0H                                                               
         TM    MCOPT1,MCQ1XSP      TEST TO OPEN EXTENDED SPTFILES               
         BZ    OPEN3E              NO                                           
         MVI   FXSPDIR,C' '        SET TO OPEN                                  
         MVI   FXSPFILE,C' '                                                    
         CLI   FCUPFILE,C'Y'                                                    
         BNE   OPEN3E                                                           
         MVI   FXSPDIR,C'U'        SET TO OPEN FOR UPDATE                       
         MVI   FXSPFILE,C'U'                                                    
OPEN3E   DS    0H                                                               
         L     R2,ADBUY            PROVIDE I/O AREA FOR OPEN                    
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'SPOT',SPTFLIST,(R2)                     
         CLI   FCOPNDEM,C'Y'       OPTION TO OPEN DEM FILES                     
         BNE   OPEN4                                                            
         GOTO1 (RF),(R1),,,DEMFLIST                                             
         EJECT                                                                  
*=========================================================*                     
* TEST TO OPEN UPDATIVE TRAFFIC FILES                     *                     
* IF YES, NEED TO GET SE NUMBER FROM SYSLIST/SYSLIST REC  *                     
*=========================================================*                     
         SPACE 1                                                                
OPEN4    CLI   FCUPTRF,C'Y'        TRAFFIC UPDATIVE                             
         BNE   OPEN10                                                           
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,C'W'                                                         
         MVI   KEY+17,C'S'                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',KEY,ADBUY                     
         L     R6,ADBUY                                                         
         CLC   KEY(25),0(R6)                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   ELCODE,X'A4'                                                     
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
OPEN4A   BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   3(4,R6),=C'SPOT'    TEST SPOT ELEM                               
         BNE   OPEN4A                                                           
         CLC   11(1,R6),SPOTSE     TEST RIGHT SE                                
         BNE   OPEN4A                                                           
* NOW WE KNOW THE SPOT SYSTEM NUMBER FROM SPOTX                                 
         MVC   DUB(3),=C'STR'      BUILD THE TRAFFIC SYSTEM NAME STRX           
         MVC   DUB+3(1),7(R6)      MOVE SYSTEM NUMBER                           
         SPACE                                                                  
         CLI   7(R6),C'Q'          IF SPOT Q                                    
         BNE   *+12                                                             
         MVI   RCUTLTRF,X'56'      SAVE TRAFFIC SYSTEM NUMBER                   
         B     OPEN4D                                                           
         CLI   7(R6),C'S'          IF SPOT S                                    
         BNE   *+12                                                             
         MVI   RCUTLTRF,X'57'      SAVE TRAFFIC SYSTEM NUMBER                   
         B     OPEN4D                                                           
         CLI   7(R6),C'U'          IF SPOT U                                    
         BNE   *+12                                                             
         MVI   RCUTLTRF,X'59'      SAVE TRAFFIC SYSTEM NUMBER                   
         B     OPEN4D                                                           
         CLI   7(R6),C'V'          IF SPOT V                                    
         BNE   *+12                                                             
         MVI   RCUTLTRF,X'5A'      SAVE TRAFFIC SYSTEM NUMBER                   
         B     OPEN4D                                                           
         CLI   7(R6),C'Y'          IF SPOT Y                                    
         BNE   *+12                                                             
         MVI   RCUTLTRF,X'5B'      SAVE TRAFFIC SYSTEM NUMBER                   
         B     OPEN4D                                                           
         SPACE                                                                  
* NOW LOOK FOR A TRAFFIC ELEMENT BY THIS NAME                                   
         SPACE                                                                  
         L     R6,ADBUY                                                         
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
OPEN4B   BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   3(4,R6),DUB         MATCH NAME                                   
         BNE   OPEN4B                                                           
         MVC   RCUTLTRF,11(R6)     SAVE TRAFFIC SYSTEM NUMBER                   
*                                                                               
OPEN4D   CLI   RECOVERY,C'N'                                                    
         BE    *+10                                                             
         MVC   STRFRCV(10),=C'UTRFRCV X '                                       
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),RCUTLTRF    SET SE NUMBER FOR OPEN                       
         L     R2,ADBUY                                                         
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'STRF',STRFLIST,(R2)                     
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),SPOTSE      RESTORE SPOT SE NUMBER                       
         B     OPEN10                                                           
         EJECT                                                                  
         GETEL R6,28,ELCODE                                                     
ELCODE   DS    X                                                                
*                                                                               
STRFLIST DS    0D                                                               
         DC    CL8'UTRFFILE'                                                    
         DC    CL8'UTRFDIR '                                                    
STRFRCV  DC    CL8'X       '                                                    
         DC    C'  '                                                            
*                                                                               
SPTFLIST DS    0C                                                               
FSPTFILE DC    CL8'NSPTFILE'                                                    
FXSPFILE DC    CL8'ZXSPFILE'       DEFAULT TO DO NOT OPEN                       
FSPTDIR  DC    CL8'NSPTDIR'                                                     
FXSPDIR  DC    CL8'ZXSPDIR'        DEFAULT TO DO NOT OPEN                       
FSTAFILE DC    CL8'NSTAFILE'                                                    
         DC    CL8'NCTFILE'                                                     
FSTRFFL  DC    CL8'NSTRFFL'                                                     
         DC    CL8'NSTRFDR'                                                     
FRECV    DC    C'X '                                                            
         DC    CL8' '              SPACE TO ADD ON RECOVERY                     
         SPACE 2                                                                
DEMFLIST DS    0C                                                               
         DC    CL8'NDEMDIRN'                                                    
         DC    CL8'NDEMDIRO'                                                    
         DC    CL8'NDEMFILN'                                                    
         DC    CL8'NDEMFILO'                                                    
         DC    CL8'NDEMFILP'                                                    
         DC    CL8'NDEMFILQ'                                                    
         DC    CL8'NDEMDIRA'                                                    
         DC    CL8'NDEMFILA'                                                    
         DC    CL8'NDEMDIRR'                                                    
         DC    CL8'NDEMFILR'                                                    
         DC    CL8'NNTIDIR '                                                    
         DC    CL8'NNTIFIL '                                                    
         DC    CL8'NHUTFL  '                                                    
         DC    C'X '                                                            
         SPACE 2                                                                
OPEN10   DS    0H                                                               
         SR    R0,R0                                                            
         MVC   DUB,=CL8'T00ADD'     LOAD LIVE VERSION                           
         GOTO1 LOADER,DMCB,DUB,(R0)                                             
         L     RF,ACOMFACS                                                      
         MVC   CDEMAND-COMFACSD(4,RF),DMCB+4                                    
         MVC   DEMAND,DMCB+4                                                    
*                                                                               
         CLC   =C'**',RCAGYCRD     IF AGY = **, FUGGEDABOUDIT                   
         BE    OPEN12                                                           
*                                                                               
         XC    KEY,KEY             GET AGENCY RECORD INTO CORE                  
         MVI   KEY,X'06'                                                        
         MVC   KEY+1(2),RCAGYCRD                                                
         GOTO1 READ                                                             
         GOTO1 GETAGY                                                           
*                                                                               
OPEN12   CLC   LOGONAME,SPACES                                                  
         BNE   *+10                                                             
         MVC   LOGONAME,AGYNM                                                   
         CLC   LOGOADD,SPACES                                                   
         BNE   *+10                                                             
         MVC   LOGOADD,AGYADR                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   *+10                                                             
         ZAP   LOGOLINE,=P'66'                                                  
         EJECT                                                                  
* CONTROL DIVIDERS AND REQUESTS *                                               
         SPACE 1                                                                
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOREQS   BAS   RE,ANYLINUP                                                      
         GOTO1 FILCON                                                           
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         SPACE 2                                                                
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC     PRINT END LOGOS                              
         EJECT                                                                  
* CLOSE FILES AND END JOB *                                                     
         SPACE 1                                                                
CLOSE1   DS    0H                                                               
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
         SPACE 1                                                                
         L     RF,=V(SSB)                                                       
         USING SSOOFF,RF                                                        
         TM    SSOSTAT2,SSOSRWRK   TEST FACWK RECOVERY                          
         BZ    CLOSE2                                                           
*                                                                               
         ICM   R7,15,SSOFWNDX      CLOSE FACWK FILE                             
         USING UKRECD,R7                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R0,=C'CLOSE'                                                     
         CP    MCNDUMPS,=P'0'                                                   
         BE    *+8                                                              
         LA    R0,=C'CLO/PUR'                                                   
         GOTO1 =V(DATAMGR),DMCB,(X'00',(R0)),=C'FACWRK',(R7),MCIO,     C        
               SSOFWBUF                                                         
         DROP  RF                                                               
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON FACWRK FILE                         
         SPACE 1                                                                
CLOSE2   DS    0H                                                               
         L     R3,ADAGY                                                         
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'SPOT',,(R3)                           
*                                                                               
         GOTO1 ,DMCB,=C'CLOSE'                                                  
         CP    MCNDUMPS,=P'0'                                                   
         BE    GOPRT                                                            
         GOTO1 ,DMCB,=C'CLO/ERR'                                                
*                                                                               
GOPRT    GOTO1 =V(PRINT)                                                        
         CLI   MCREPTS,2                                                        
         BNE   GOEND                                                            
         GOTO1 =V(PRINTS2)                                                      
         SPACE 2                                                                
GOEND    DS    0H                                                               
         LTR   R2,R2                                                            
         BZ    NORMEND                                                          
         ABEND 999                                                              
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         XBASE                                                                  
         EJECT                                                                  
* ROUTINE TO EXTRACT FILE NUMBER & AGENCY CODE                                  
* FROM 2-BYTE AGENCY CODE                                                       
         SPACE 1                                                                
GETAGINF NTR1                                                                   
         L     R3,ADBUY             PROVIDE AN I/O AREA                         
         USING CND,R3                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,WORK                                                       
         CLC   RCORIGID,=X'002B'     IF TCH1 (OR NO ORIGIN GIVEN)               
         BE    *+10                                                             
         MVC   CNID,RCORIGID                                                    
         GOTO1 =V(CONFID),DMCB,(1,CND),FULL                                     
         OC    FULL,FULL                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,=V(UTL)                                                       
         MVC   4(1,R5),CNSSE                                                    
         ZIC   R0,CNSCD                                                         
         SRL   R0,4                                                             
         STC   R0,BAGY                                                          
         SPACE 1                                                                
* SPECIAL CODE FOR NETPAK *                                                     
         SPACE 1                                                                
         CLI   MCNETPAK,C'Y'                                                    
         BNE   GETAG4                                                           
         MVC   4(1,R5),CNNSE                                                    
         ZIC   R0,CNNCD                                                         
         SRL   R0,4                                                             
         STC   R0,BAGY                                                          
*                                                                               
GETAG4   MVC   SPOTSE,4(R5)        SET FROM UTL ENTRY                           
         MVC   PRNTSE,CNPSE                                                     
         MVC   ACCTSE,CNASE                                                     
         MVC   ACCTCD,CNACD                                                     
*                                                                               
         LA    RF,SPOTSES                                                       
GETAG5   DS    0H                                                               
         CLC   1(1,RF),SPOTSE                                                   
         BE    GETAG6                                                           
         LA    RF,2(RF)                                                         
         B     GETAG5                                                           
*                                                                               
GETAG6   DS    0H                                                               
         MVC   FILENUM,0(RF)                                                    
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
       ++INCLUDE SPOTSES                                                        
         EJECT                                                                  
* LINE-UP PATTERN CONTROL *                                                     
         SPACE 1                                                                
ANYLINUP NTR1                                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   LINEND                                                           
*                                                                               
         L     RF,=V(REMOTEC)                                                   
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY   NO LINEUP IF REMOTE                          
         BNZ   LINEND                                                           
         DROP  RF                                                               
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
         OC    MCREMPQK,MCREMPQK   NO LINEUP IF SOON                            
         BNZ   LINEND                                                           
         DROP  R6                                                               
         MVC   P,SPACES                                                         
         GOTO1 PRINT,DMCB,P,=C'BC01'                                            
         GOTO1 PRINT,DMCB,P,=C'BL09'                                            
         SPACE 2                                                                
USLINUP  DS    0H                                                               
         LA    R4,3                DO IT THREE TIMES                            
USLINUP2 LA    R2,3                                                             
         GOTO1 PRINT,DMCB,P,=C'BL04'                                            
         SPACE 2                                                                
USLINUP4 DS    0H                  85 X'S ON HEADS 14,15,16                     
         MVI   P,C'X'                                                           
         MVC   P+1(84),P                                                        
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,USLINUP4                                                      
         MVC   P,SPACES                                                         
         GOTO1 (RF),(R1),,=C'BC01'                                              
         GOTO1 (RF),(R1),,=C'BL09'                                              
         BCT   R4,USLINUP2                                                      
         MVC   P,SPACES                                                         
         SPACE 2                                                                
LINEND   XIT1                                                                   
         SPACE 2                                                                
OVERLOAD DC    CL4' '                                                           
RECOVERY DC    C'Y'                                                             
         EJECT                                                                  
       ++INCLUDE DDREMOTED                                                      
*                                                                               
       ++INCLUDE DDCNTRL                                                        
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE DMWRKRK                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'046SPRUNCONS 05/01/02'                                      
         END                                                                    
