*          DATA SET CTREPFILM  AT LEVEL 007 AS OF 05/01/02                      
*CATALP CTFILMST                                                                
         TITLE 'MAIN FILE CONTROLLER'                                           
CTFILCON CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 40,CTFILCON                                                      
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         LA    R8,2048(R9)                                                      
         LA    R8,2048(R8)                                                      
         USING CTFILCON+4096,R9,R8                                              
         L     RA,=V(CTWORKC)                                                   
         USING CTWORKD,RA                                                       
         USING MONARCHD,RC                                                      
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         LA    RE,ENDREQ                                                        
         STM   R0,RF,MCREQLST                                                   
         LA    RE,FINAL                                                         
         STM   R0,RF,MCRUNLST                                                   
         DROP  R1                                                               
         EJECT                                                                  
*                   INITIALISATION, REQUEST READ AND FINALISATION               
         SPACE 3                                                                
INITIAL  MVI   MODE,RUNFRST        FIRST TIME HOOK                              
         BAS   RE,GO                                                            
         SPACE 2                                                                
READREQ  CP    RCRQTOT,RCENNUM                                                  
         BNL   EOF                                                              
         SPACE 2                                                                
READCARD GOTO1 =V(REQSTART)                                                     
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   QPROG(80),MCREQREC                                               
         CLC   QPROG(2),=C'/*'                                                  
         BE    FINAL                                                            
         CLC   QPROG(2),=C'/&&'                                                 
         BE    FINAL                                                            
         B     REQCON                                                           
         SPACE 3                                                                
EOF      DS    0H                                                               
FINAL    MVI   MODE,RUNLAST        LAST TIME HOOK                               
         BAS   RE,GO                                                            
         SPACE 2                                                                
FCEXT    XMOD1 1                                                                
         EJECT                                                                  
*                   FILTERING AND EDITING REQUESTS                              
         SPACE 3                                                                
REQCON   DS    0H                                                               
         SPACE 2                                                                
RC2      AP    RCRQTOT,=P'1'                                                    
         CP    RCRQTOT,RCSTNUM     CHECK WE HAVE REACHED START OPTION           
         BL    READREQ                                                          
         GOTO1 REQREP,DMCB                                                      
         OC    DMCB(4),DMCB                                                     
         BZ    RC4                                                              
         AP    RCRQERR,=P'1'                                                    
         B     READREQ                                                          
         SPACE 2                                                                
RC4      AP    RCRQVAL,=P'1'                                                    
         MVI   MODE,REQFRST                                                     
         L     RF,=V(BOXAREA)                                                   
         LTR   RF,RF                                                            
         BZ    RC6                                                              
         CLI   0(RF),C'N'                                                       
         BNE   *+8                                                              
         MVI   0(RF),C'Y'                                                       
         SPACE 2                                                                
RC6      BAS   RE,GO                                                            
         EJECT                                                                  
*              ROUTINES TO HANDLE SORTED RECORD READING                         
         SPACE 3                                                                
         CLI   INFILE,C'Y'                                                      
         BNE   COMMENTS                                                         
         L     R2,ADIN                                                          
         L     R3,ADIO                                                          
         CLI   FIRSTREC,C'Y'                                                    
         BNE   SORTLOOP                                                         
         MVI   FIRSTREC,C'N'                                                    
         B     *+6                                                              
         SPACE 2                                                                
FIRSTREC DC    C'Y'                                                             
         DS    CL1                                                              
         GET   (R2),(R3)                                                        
         SPACE 2                                                                
SORTLOOP CLC   4(3,R3),RCRQTOT     CHECK REQUEST NO IN RECORD                   
         BH    ENDREQ              AGAINST PRESENT REQUEST NO                   
         BL    SORTGET                                                          
         CLC   4(8,R3),=8X'FF'                                                  
         BE    ENDREQ                                                           
         MVI   MODE,PROCSORT                                                    
         BAS   RE,GO                                                            
         SPACE 2                                                                
SORTGET  GET   (R2),(R3)                                                        
         B     SORTLOOP                                                         
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF COMMENT RECORDS                   
         SPACE 3                                                                
COMMENTS CLI   FCRDCOM,C'Y'                                                     
         BNE   ERRORS                                                           
         LA    R4,KEY                                                           
         USING CTCKEY,R4                                                        
         MVC   CTCKEY,SPACES                                                    
         MVI   CTCKTYP,C'C'                                                     
         MVC   CTCKUSER,QAGENCY                                                 
         MVC   CTCKSYS,QMEDIA                                                   
         MVC   CTCKID,QID                                                       
         MVI   CTCKSUB,0                                                        
         BAS   RE,HIGH                                                          
         B     COM4                                                             
         SPACE 2                                                                
COM2     BAS   RE,SEQ                                                           
         SPACE 2                                                                
COM4     CLC   KEY(24),KEYSAVE                                                  
         BE    COM6                                                             
         CLC   QID,SPACES                                                       
         BNE   COM20                                                            
         SPACE 2                                                                
COM6     CLC   KEY(14),KEYSAVE                                                  
         BE    COM8                                                             
         CLC   QMEDIA,SPACES                                                    
         BNE   COM20                                                            
         SPACE 2                                                                
COM8     CLC   KEY(13),KEYSAVE                                                  
         BE    COM10                                                            
         CLC   QAGENCY,SPACES                                                   
         BNE   COM20                                                            
         SPACE 2                                                                
COM10    CLC   KEY(1),KEYSAVE                                                   
         BNE   COM20                                                            
         BAS   RE,FILTER                                                        
         BNE   COM2                                                             
         MVI   MODE,PROCCOM                                                     
         BAS   RE,GO                                                            
         B     COM2                                                             
         SPACE 2                                                                
COM20    DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF ERROR RECORDS                     
         SPACE 3                                                                
ERRORS   CLI   FCRDERR,C'Y'                                                     
         BNE   ID                                                               
         LA    R4,KEY                                                           
         USING CTEKEY,R4                                                        
         XC    CTEKEY,CTEKEY                                                    
         MVI   CTEKTYP,C'E'                                                     
         MVI   CTEKSYS,0                                                        
         CLC   QSYSTEM,SPACES                                                   
         BE    ER2                                                              
         PACK  DUB,QSYSTEM                                                      
         CVB   R0,DUB                                                           
         STC   R0,CTEKSYS                                                       
         SPACE 2                                                                
ER2      BAS   RE,HIGH                                                          
         B     ER6                                                              
         SPACE 2                                                                
ER4      BAS   RE,SEQ                                                           
         SPACE 2                                                                
ER6      CLC   KEY(24),KEYSAVE                                                  
         BE    ER8                                                              
         CLC   QSYSTEM,SPACES                                                   
         BNE   ER10                                                             
         SPACE 2                                                                
ER8      CLC   KEY(23),KEYSAVE                                                  
         BNE   ER10                                                             
         BAS   RE,FILTER                                                        
         BNE   ER4                                                              
         MVI   MODE,PROCERR                                                     
         BAS   RE,GO                                                            
         B     ER4                                                              
         SPACE 2                                                                
ER10     DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF ID RECORDS                        
         SPACE 3                                                                
ID       CLI   FCRDID,C'Y'                                                      
         BNE   JCL                                                              
         LA    R4,KEY                                                           
         USING CTIREC,R4                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         BAS   RE,HIGH                                                          
         B     ID4                                                              
         SPACE 2                                                                
ID2      BAS   RE,SEQ                                                           
         SPACE 2                                                                
ID4      CLI   CTIKTYP,C'I'                                                     
         BNE   ID6                                                              
         BAS   RE,FILTER                                                        
         BNE   ID2                                                              
         MVI   MODE,PROCID                                                      
         BAS   RE,GO                                                            
         B     ID2                                                              
         SPACE 2                                                                
ID6      DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF JCL RECORDS                       
         SPACE 3                                                                
JCL      CLI   FCRDJCL,C'Y'                                                     
         BNE   LIB                                                              
         LA    R4,KEY                                                           
         USING CTJKEY,R4                                                        
         XC    CTJKEY,CTJKEY                                                    
         MVI   CTJKTYP,C'J'                                                     
         MVC   CTJKID,QID                                                       
         MVI   CTJKSUB,0                                                        
         BAS   RE,HIGH                                                          
         B     JCL4                                                             
         SPACE 2                                                                
JCL2     BAS   RE,SEQ                                                           
         SPACE 2                                                                
JCL4     CLC   KEY(24),KEYSAVE                                                  
         BE    JCL6                                                             
         CLC   QID,SPACES                                                       
         BNE   JCL8                                                             
         SPACE 2                                                                
JCL6     CLI   CTJKTYP,C'J'                                                     
         BNE   JCL8                                                             
         BAS   RE,FILTER                                                        
         BNE   JCL2                                                             
         MVI   MODE,PROCJCL                                                     
         BAS   RE,GO                                                            
         B     JCL2                                                             
         SPACE 2                                                                
JCL8     DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF LIBRARY RECORDS                   
         SPACE 3                                                                
LIB      CLI   FCRDLIB,C'Y'                                                     
         BNE   OUTPUT                                                           
         LA    R4,KEY                                                           
         USING CTLKEY,R4                                                        
         XC    CTLKEY,CTLKEY                                                    
         MVI   CTLKTYP,C'L'                                                     
         MVC   CTLKNAME,QID                                                     
         MVI   CTLKSUB,0                                                        
         BAS   RE,HIGH                                                          
         B     LIB4                                                             
         SPACE 2                                                                
LIB2     BAS   RE,SEQ                                                           
         SPACE 2                                                                
LIB4     CLC   KEY(24),KEYSAVE                                                  
         BE    LIB6                                                             
         CLC   QID,SPACES                                                       
         BNE   LIB8                                                             
         SPACE 2                                                                
LIB6     CLI   CTLKTYP,C'L'                                                     
         BNE   LIB8                                                             
         BAS   RE,FILTER                                                        
         BNE   LIB2                                                             
         MVI   MODE,PROCLIB                                                     
         BAS   RE,GO                                                            
         B     LIB2                                                             
         SPACE 2                                                                
LIB8     DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF OUTPUT RECORDS                    
         SPACE 3                                                                
OUTPUT   CLI   FCRDOUT,C'Y'                                                     
         BNE   PROFILE                                                          
         LA    R4,KEY                                                           
         USING CTOREC,R4                                                        
         XC    CTOKEY,CTOKEY                                                    
         MVI   CTOKEY,C'O'                                                      
         BAS   RE,HIGH                                                          
         B     OUT4                                                             
         SPACE 2                                                                
OUT2     BAS   RE,SEQ                                                           
         SPACE 2                                                                
OUT4     CLI   CTOKTYP,C'O'                                                     
         BNE   OUT6                                                             
         BAS   RE,FILTER                                                        
         BNE   OUT2                                                             
         MVI   MODE,PROCOUT                                                     
         BAS   RE,GO                                                            
         B     OUT2                                                             
         SPACE 2                                                                
OUT6     DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL READING PROFILE RECORDS                      
         SPACE 3                                                                
PROFILE  CLI   FCRDPROF,C'Y'                                                    
         BNE   PROTO                                                            
         LA    R4,KEY                                                           
         USING CTPREC,R4                                                        
         XC    CTPKEY,CTPKEY                                                    
         MVI   CTPKEY,C'P'                                                      
         BAS   RE,HIGH                                                          
         B     PROF4                                                            
         SPACE 2                                                                
PROF2    BAS   RE,SEQ                                                           
         SPACE 2                                                                
PROF4    CLI   CTPKTYP,C'P'                                                     
         BNE   PROF6                                                            
         BAS   RE,FILTER                                                        
         BNE   PROF2                                                            
         MVI   MODE,PROCPROF                                                    
         BAS   RE,GO                                                            
         B     PROF2                                                            
         SPACE 2                                                                
PROF6    DS    0H                                                               
         EJECT                                                                  
*              ROUTINES TO CONTROL REQUEST PROTOTYPE RECORDS                    
         SPACE 3                                                                
PROTO    CLI   FCRDREQ,C'Y'                                                     
         BNE   TERM                                                             
         LA    R4,KEY                                                           
         USING CTRREC,R4                                                        
         XC    CTRKEY,CTRKEY                                                    
         MVI   CTRKEY,C'R'                                                      
         BAS   RE,HIGH                                                          
         B     PROTO4                                                           
         SPACE 2                                                                
PROTO2   BAS   RE,SEQ                                                           
         SPACE 2                                                                
PROTO4   CLI   CTRKTYP,C'R'                                                     
         BNE   PROTO6                                                           
         BAS   RE,FILTER                                                        
         BNE   PROTO2                                                           
         MVI   MODE,PROCREQ                                                     
         BAS   RE,GO                                                            
         B     PROTO2                                                           
         SPACE 2                                                                
PROTO6   DS    0H                                                               
         EJECT                                                                  
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF TERMINAL RECORDS                  
         SPACE 3                                                                
TERM     CLI   FCRDTERM,C'Y'                                                    
         BNE   USER                                                             
         LA    R4,KEY                                                           
         USING CTTKEY,R4                                                        
         XC    CTTKEY,CTTKEY                                                    
         MVC   CTTKLINE,QLINE                                                   
         MVC   CTTKADDR,QLINE+4                                                 
         MVI   CTTKTYP,C'T'                                                     
         BAS   RE,HIGH                                                          
         B     TRM4                                                             
         SPACE 2                                                                
TRM2     BAS   RE,SEQ                                                           
         SPACE 2                                                                
TRM4     CLC   KEY(15),KEYSAVE                                                  
         BE    TRM6                                                             
         CLC   QLINE+4(4),SPACES                                                
         BNE   TRM10                                                            
         SPACE 2                                                                
TRM6     CLC   KEY(11),KEYSAVE                                                  
         BE    TRM8                                                             
         CLC   QLINE,SPACES                                                     
         BNE   TRM10                                                            
         SPACE 2                                                                
TRM8     CLI   CTTKTYP,C'T'                                                     
         BNE   TRM10                                                            
         BAS   RE,FILTER                                                        
         BNE   TRM2                                                             
         MVI   MODE,PROCTERM                                                    
         BAS   RE,GO                                                            
         B     TRM2                                                             
         SPACE 2                                                                
TRM10    B     ENDREQ                                                           
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF USER RECORDS                      
         SPACE 3                                                                
USER     CLI   FCRDUSER,C'Y'                                                    
         BNE   XTRACT                                                           
         LA    R4,KEY                                                           
         USING CTUREC,R4                                                        
         XC    CTUKEY,CTUKEY                                                    
         MVI   CTUKEY,C'U'                                                      
         MVC   FIELDKEY,KEY                                                     
         SPACE 1                                                                
USER2    MVC   KEY,FIELDKEY                                                     
         AI    CTUKPAGE,1          GET TO NEXT FIELD RECORD                     
         BAS   RE,HIGH                                                          
         CLI   CTUKTYP,C'U'                                                     
         BNE   ENDREQ                                                           
         MVC   FIELDKEY,KEY                                                     
         BAS   RE,FILTER                                                        
         BNE   USER2                                                            
         MVI   MODE,PROCUSER                                                    
         BAS   RE,GO                                                            
         MVI   CTUKAGY+1,X'01'     FORCE DATAMGR TO READ CTUSER                 
         BAS   RE,HIGH             NOW GO AND READ USER RECORDS                 
         B     USER6                                                            
         SPACE 1                                                                
USER4    BAS   RE,SEQ                                                           
         SPACE 1                                                                
USER6    CLC   KEY(16),KEYSAVE     ARE WE DONE WITH THIS SET OF                 
         BNE   USER2               USER RECORDS YET?                            
         BAS   RE,FILTER                                                        
         BNE   USER4                                                            
         MVI   MODE,PROCUSER                                                    
         BAS   RE,GO                                                            
         B     USER4                                                            
         SPACE 1                                                                
FIELDKEY DS    CL32                                                             
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF XTRACT RECORDS                    
         SPACE 3                                                                
XTRACT   CLI   FCRDXT,C'Y'                                                      
         BNE   FORM                                                             
         LA    R4,KEY                                                           
         USING CTXREC,R4                                                        
         XC    CTXKEY,CTXKEY                                                    
         MVI   CTXKTYP,C'X'                                                     
         MVC   CTXKAGY,QAGENCY                                                  
         BAS   RE,HIGH                                                          
         B     XT4                                                              
         SPACE 2                                                                
XT2      BAS   RE,SEQ                                                           
         SPACE 2                                                                
XT4      CLI   CTXKTYP,C'X'                                                     
         BNE   FORM                                                             
         CLC   QAGENCY,SPACES                                                   
         BE    XT6                                                              
         CLC   CTXKAGY,QAGENCY                                                  
         BNE   FORM                                                             
         SPACE 2                                                                
XT6      BAS   RE,FILTER                                                        
         BNE   XT2                                                              
         MVI   MODE,PROCXT                                                      
         BAS   RE,GO                                                            
         B     XT2                                                              
         EJECT                                                                  
*              ROUTINES TO CONTROL READING OF CPP PROJECTION FORMULAS           
         SPACE 3                                                                
FORM     CLI   FCRDFORM,C'Y'                                                    
         BNE   ENDREQ                                                           
         LA    R4,KEY                                                           
         USING CTYREC,R4                                                        
         XC    CTYKEY,CTYKEY                                                    
         MVI   CTYKTYP,C'Y'                                                     
         MVC   CTYKAGY,QAGENCY                                                  
         BAS   RE,HIGH                                                          
         B     FORM4                                                            
         SPACE 2                                                                
FORM2    BAS   RE,SEQ                                                           
         SPACE 2                                                                
FORM4    CLI   CTYKTYP,C'Y'                                                     
         BNE   ENDREQ                                                           
         CLC   QAGENCY,SPACES                                                   
         BE    FORM6                                                            
         CLC   CTYKAGY,QAGENCY                                                  
         BNE   ENDREQ                                                           
         SPACE 2                                                                
FORM6    BAS   RE,FILTER                                                        
         BNE   FORM2                                                            
         MVI   MODE,PROCFORM                                                    
         BAS   RE,GO                                                            
         B     FORM2                                                            
         EJECT                                                                  
*              ROUTINE TO FILTER                                                
         SPACE 2                                                                
FILTER   NTR1                                                                   
         SPACE 2                                                                
* GET QSTART AND QEND IN Y2K COMPAT FORMAT IF POSSIBLE                          
         LA    R1,QSTART                                                        
         BAS   RE,QY2K                                                          
         LA    R1,QEND                                                          
         BAS   RE,QY2K                                                          
         SPACE 2                                                                
         MVC   WORK,SPACES                                                      
         L     R4,ADACTIV                                                       
         LTR   R4,R4                                                            
         BZ    FILTER2                                                          
         USING CTACTD,R4                                                        
         GOTO1 DATCON,DMCB,(3,CTACTDT),(0,WORK)                                 
         SPACE 2                                                                
FILTER2  CLC   QSTART,SPACES                                                    
         BE    FILTER4                                                          
         CLC   WORK(6),SPACES                                                   
         BE    NOTOK                                                            
         CLC   WORK(6),QSTART                                                   
         BL    NOTOK                                                            
         SPACE 2                                                                
FILTER4  CLC   QEND,SPACES                                                      
         BE    OK                                                               
         CLC   WORK(6),SPACES                                                   
         BE    NOTOK                                                            
         CLC   WORK(6),QEND                                                     
         BH    NOTOK                                                            
         SPACE 2                                                                
OK       SR    R1,R1                                                            
         B     FILTEX                                                           
         SPACE 2                                                                
NOTOK    LA    R1,1                                                             
         SPACE 2                                                                
FILTEX   LTR   R1,R1                                                            
         XIT1                                                                   
         EJECT                                                                  
QY2K     NTR1                      IF YEAR IS NUMERIC, --> Y2K FMT              
         LHI   R0,2                                                             
         LR    R5,R1                                                            
*                                                                               
QY2KA    CLI   0(R5),C'0'                                                       
         BL    QY2KX                                                            
         CLI   0(R5),C'9'                                                       
         BH    QY2KX                                                            
         LA    R5,1(R5)                                                         
         BCT   R0,QY2KA                                                         
*                                                                               
         LR    R5,R1                                                            
         MVC   WORK(2),0(R5)                                                    
         MVC   WORK+2(2),=C'01'                                                 
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,WORK,WORK+6                                          
         MVC   0(2,R5),WORK+6      CHANGE YEAR BYTES                            
*                                                                               
QY2KX    XIT1                                                                   
         EJECT                                                                  
ENDREQ   MVI   MODE,REQLAST                                                     
         SPACE 2                                                                
LASTGO   BAS   RE,GO                                                            
         L     RF,=V(BOXAREA)                                                   
         LTR   RF,RF                                                            
         BZ    READREQ                                                          
         CLI   0(RF),C'Y'                                                       
         BNE   READREQ                                                          
         MVI   0(RF),C'N'                                                       
         SPACE 2                                                                
DUMPCHCK B     READREQ                                                          
         EJECT                                                                  
*                   ROUTINE TO GO TO APPLICATION PROGRAMS                       
         SPACE 2                                                                
GO       NTR                                                                    
         CLI   RCTRACE,C'Y'                                                     
         BNE   GO2                                                              
         MVC   P,SPACES                                                         
         MVC   P(10),=C'MODE=X''  '''                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,DMCB,MODE,P+7,1,=C'N'                                     
         BAS   RE,GETMODE                                                       
         GOTO1 REPORT                                                           
         SPACE 2                                                                
GO2      EQU   *                                                                
         GOTO1 APPLIC,DMCB,WORKC                                                
         SPACE 2                                                                
         CLI   RCWRITE,C'Y'        CHECK IF WRITE = NO OPTION IS ON             
         BNE   GOEND                                                            
         L     R2,LASTFILE                                                      
         CLI   MODE,WRITREC        OPTION TO WRITE BACK LAST RECORD             
         BNE   GOEND                                                            
         BAS   RE,WRITE                                                         
GOEND    XIT                                                                    
         SPACE 2                                                                
GETMODE  SR    R2,R2               TRANSLATE MODE FROM                          
         IC    R2,MODE             TABLE ON NEXT PAGE                           
         BCTR  R2,R0                                                            
         SLL   R2,3                                                             
         LA    R2,MODETAB(R2)                                                   
         MVC   P+20(8),0(R2)                                                    
         BR    RE                                                               
         EJECT                                                                  
*              TABLE OF MODE VALUES                                             
         SPACE 3                                                                
MODETAB  DC    CL8'RUNFRST'                                                     
         DC    CL8'REQFRST'                                                     
         DC    CL8'USERFRST'                                                    
         DC    CL8'MEDFRST'                                                     
         DC    CL8'CLTFRST'                                                     
         DC    CL8'IDFRST'                                                      
         DC    CL8'SYSFRST'                                                     
         DC    CL8'LINEFRST'                                                    
         DC    CL8'TERMFRST'                                                    
         DC    CL8'SPARE'                                                       
         SPACE 1                                                                
         DC    CL8'RUNLAST'                                                     
         DC    CL8'REQLAST'                                                     
         DC    CL8'USERLAST'                                                    
         DC    CL8'MEDLAST'                                                     
         DC    CL8'CLTLAST'                                                     
         DC    CL8'IDLAST'                                                      
         DC    CL8'SYSLAST'                                                     
         DC    CL8'LINELAST'                                                    
         DC    CL8'TERMLAST'                                                    
         DC    CL8'SPARE'                                                       
         SPACE 2                                                                
         DC    CL8'PROCCOM'                                                     
         DC    CL8'PROCDEST'                                                    
         DC    CL8'PROCERR'                                                     
         DC    CL8'PROCID'                                                      
         DC    CL8'PROCJCL'                                                     
         DC    CL8'PROCLIB'                                                     
         DC    CL8'PROCSTRM'                                                    
         DC    CL8'PROCTERM'                                                    
         DC    CL8'PROCSORT'                                                    
         DC    CL8'SPARE'                                                       
         DC    CL8'WRITREC'                                                     
         DC    CL8'PROCOUT'                                                     
         DC    CL8'PROCPROF'                                                    
         DC    CL8'PROCREQ'                                                     
         DC    CL8'PROCUSER'                                                    
         DC    CL8'PROCXT'                                                      
         DC    CL8'PROCFORM'                                                    
         EJECT                                                                  
         EJECT                                                                  
*              DATA MANAGER INTERFACE                                           
         SPACE 3                                                                
READ     LA    RF,DMREAD                                                        
         B     LINKDIR                                                          
         SPACE 2                                                                
HIGH     LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     LINKDIR                                                          
         SPACE 2                                                                
SEQ      LA    RF,DMRSEQ                                                        
         MVC   KEYSAVE,KEY                                                      
         B     LINKDIR                                                          
         SPACE 2                                                                
WRITE    LA    RF,DMWRT                                                         
         L     R2,ADRECORD                                                      
         CLC   25(2,R2),LASTLEN                                                 
         BH    DMERR2                                                           
         B     LINKDIR                                                          
         SPACE 2                                                                
LINKDIR  NTR                                                                    
         SPACE 2                                                                
LD2      ST    RF,DMCB                                                          
         MVC   TRACEKEY,KEY                                                     
         MVC   DMCB(1),DMINBTS                                                  
         L     R2,FILEC                                                         
         GOTO1 DATAMGR,DMCB,,CTFILE,KEY,(R2),(0,DMWORK)                         
         MVC   KEY,0(R2)                                                        
         BAS   RE,POSTEL                                                        
         MVC   LASTLEN,25(R2)                                                   
         B     DMCHECK                                                          
         EJECT                                                                  
*                   DATA MANAGER INTERFACE (CHECK ERRORS)                       
         SPACE 3                                                                
DMCHECK  CLI   DMCB+8,0                                                         
         BE    ANYTRACE                                                         
         MVC   TRACEWRK,WORK                                                    
         MVC   WORK(27),=C'*** DATA MANAGER ERROR  ***'                         
         B     DMERR4                                                           
         SPACE 2                                                                
DMERR2   MVC   WORK(27),=C'*** ILLEGAL FILE UPDATE ***'                         
         SPACE 2                                                                
DMERR4   GOTO1 LOGIO,WORK+32,1,(27,WORK)                                        
         MVC   WORK(27),=C'*** REQUEST NUMBER NNNN ***'                         
         UNPK  WORK+19(4),RCRQTOT                                               
         OI    WORK+22,X'F0'                                                    
         BASR  RE,RF                                                            
         MVC   WORK(27),SPACES                                                  
         BASR  RE,RF                                                            
         MVC   WORK,TRACEWRK                                                    
         BAS   RE,DMTRACE                                                       
         MVI   MODE,DISKERR        TELL APPLICATION PROGRAM THE NEWS            
         BAS   RE,GO                                                            
         CLI   MODE,GOTONXTR       USER CAN OPT TO GO ONTO NEXT REQUEST         
         BNE   DMEND               THE DEFAULT VALUE IS TO IGNORE THEM          
         SPACE 2                                                                
         L     RD,4(RD)            UNWIND WITHOUT XIT                           
         LM    RE,RC,12(RD)        SORRY ABOUT THIS MEL                         
         B     ENDREQ                                                           
         SPACE 2                                                                
TRACEWRK DS    CL64                                                             
THISSEQ  DC    X'00'                                                            
         DC    X'00'                                                            
         EJECT                                                                  
*                   MONITOR TRACING OF DATA MANAGER CALLS                       
         SPACE 3                                                                
ANYTRACE CLI   RCTRACE,C'Y'        TRACE OPTION                                 
         BNE   DMEND                                                            
         L     R2,DMCB+12                                                       
         SPACE 2                                                                
TRACE4   BAS   RE,DMTRACE                                                       
         SPACE 2                                                                
DMEND    XIT                                                                    
         EJECT                                                                  
*                   ROUTINE TO TRACE DATA MANAGER CALLS                         
         SPACE 3                                                                
DMTRACE  NTR                                                                    
         MVC   P,SPACES                                                         
         LM    R2,R5,DMCB                                                       
         MVC   TRACEDM8,DMCB+8                                                  
         MVC   P(8),0(R2)          COMMAND                                      
         MVC   P+10(8),0(R3)       FILE                                         
         LA    R4,TRACEKEY                                                      
         LA    R6,25                                                            
         SPACE 2                                                                
DMTRACE2 GOTO1 HEXOUT,DMCB,(R4),P+20,(R6),=C'N'                                 
         CLC   3(4,R3),=C'FILE'                                                 
         BNE   DMTRACE4                                                         
         LA    R5,33(R5)                                                        
         SPACE 2                                                                
DMTRACE4 EQU   *                                                                
         GOTO1 HEXOUT,DMCB,(R5),P+75,25                                         
         GOTO1 HEXOUT,DMCB,TRACEDM8,P+130,1                                     
         SPACE 2                                                                
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         XIT                                                                    
         EJECT                                                                  
*              ROUTINE TO SET UP ELEMENT ADDRESSES                              
         SPACE 3                                                                
POSTEL   NTR1                                                                   
         ST    R2,ADRECORD                                                      
         AH    R2,DATADISP                                                      
         ST    R2,ADDATA                                                        
         SR    R3,R3                                                            
         XC    ADACTIV(8),ADACTIV                                               
         SPACE 2                                                                
POST2    CLI   0(R2),0                                                          
         BE    POSTEX                                                           
         CLI   0(R2),X'01'                                                      
         BNE   *+8                                                              
         ST    R2,ADACTIV                                                       
         CLI   0(R2),X'02'                                                      
         BNE   *+8                                                              
         ST    R2,ADDESC                                                        
         IC    R3,1(R2)                                                         
         AR    R2,R3                                                            
         B     POST2                                                            
         SPACE 2                                                                
POSTEX   XIT1                                                                   
         EJECT                                                                  
*                   LITERAL POOL FOR PROGRAM                                    
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MONACC                                                 
         SPACE 3                                                                
MONARCHD DSECT                                                                  
TRACEKEY DS    CL32                                                             
THISKEY  DS    CL32                                                             
TRACEDM8 DS    CL1                                                              
FT1      DS    CL1                                                              
FT2      DS    CL1                                                              
FT3      DS    CL1                                                              
DEPTH    DS    CL1                                                              
SAVEMODE DS    CL1                                                              
BOOKIO   DS    CL80                                                             
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDREPMASTD                                                     
         EJECT                                                                  
       ++INCLUDE CTREPWORKD                                                     
         SPACE 2                                                                
       ++INCLUDE CTREPMODES                                                     
         SPACE 3                                                                
         PRINT ON                                                               
BOOKBUFF CSECT                                                                  
         DS    1112C                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007CTREPFILM 05/01/02'                                      
         END                                                                    
