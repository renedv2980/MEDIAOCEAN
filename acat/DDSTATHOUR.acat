*          DATA SET DDSTATHOUR AT LEVEL 008 AS OF 06/03/14                      
*CATALP STATHOUR                                                                
         TITLE 'STATHOUR - ADPAK HOURLY STATISTICS'                             
         PRINT NOGEN                                                            
STATHOUR CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE WRKX-WRKD,STATHOUR,WORK=A(STATWORK)                              
         USING WRKD,RC                                                          
*                                                                               
GETSVP   ST    R1,ACOMRG           SAVE MVS PARM ADDR                           
         MVI   UPSI,0                                                           
         L     R1,0(R1)                                                         
         LH    R2,0(R1)            R2=L'PARM DATA                               
         LTR   R2,R2                                                            
         BZ    GETSVPX                                                          
         CHI   R2,8                                                             
         BNH   *+8                                                              
         LA    R2,8                                                             
         LA    R1,2(R1)            R1=A(PARM DATA)                              
         LA    RF,GETSVPT                                                       
GETSVP1  CLI   0(R1),C','                                                       
         BE    GETSVPX                                                          
         CLI   0(R1),C'0'                                                       
         BE    GETSVP2                                                          
         CLI   0(R1),C'1'                                                       
         BNE   GETSVP2                                                          
         OC    UPSI,0(RF)                                                       
GETSVP2  LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R2,GETSVP1                                                       
         B     GETSVPX                                                          
GETSVPT  DC    X'8040201008040201'                                              
GETSVPX  EQU   *                                                                
*                                                                               
INIT0    TM    UPSI,X'01'          SET TEST SYSTEM DDSIO AND DSPACE             
         BZ    INIT0X                                                           
         LT    RF,=V(DDSIO)                                                     
         BZ    *+10                                                             
*&&UK*&& MVC   0(8,RF),=CL8'DDSIO'                                              
*&&US*&& MVC   0(8,RF),=CL8'DDSION'                                             
         L     RF,=V(SSB)                                                       
         MVI   SSODSPAC-SSOOFF(RF),C'T'                                         
INIT0X   EQU   *                                                                
*                                                                               
*                                  OPEN STATS FILE AND BUILD INDEX              
OPEN     LA    R2,DMCB                                                          
         GOTO1 =V(DMOD000),DMCB,V(OPEN),0,0,V(STATS)                            
*                                                                               
OPEN2    XC    DSKADR,DSKADR       READ PROGRAM INDEX                           
         L     R6,=A(PRGNDX)                                                    
         ST    R6,APRGNDX                                                       
         SH    R6,=H'16'                                                        
         LR    R0,R6                                                            
         BAS   R9,SSTATS                                                        
         LR    R1,R6                                                            
         AH    R1,=H'2416'                                                      
         MVC   0(12,R1),=12X'FF'                                                
         LH    R7,2(R6)                                                         
         BCTR  R7,0                R7=NUM OF TRM INDEX RECS                     
         L     R6,=A(TRMNDX)                                                    
         ST    R6,ATRMNDX                                                       
*                                                                               
OPEN4    SH    R6,=H'16'           READ TERMINAL INDEX                          
         MVC   IOBUFF(16),0(R6)                                                 
         LR    R0,R6                                                            
         BAS   R9,SSTATS                                                        
         MVC   0(16,R6),IOBUFF                                                  
         AH    R6,=H'2416'                                                      
         BCT   R7,OPEN4                                                         
         MVC   0(12,R6),=12X'FF'                                                
*                                                                               
OPEN6    LA    R2,IOBUFF           READ GRAND TOTAL REC & SAVE DATE             
         USING STATRECD,R2                                                      
         LR    R0,R2                                                            
         BAS   R9,SSTATS                                                        
         MVC   DATE,SDDATE                                                      
         MVC   COUNT,STCOUNT                                                    
         EJECT                                                                  
NEXT     L     RA,=V(CPRINT)       INITIALIZE FOR START OF NEW REPORT           
         USING DPRINT,RA                                                        
         MVC   TITLE,=CL60'ADPAK HOURLY STATISTICS - '                          
         L     RF,APRGNDX                                                       
         SH    RF,=H'4'                                                         
         CLI   0(RF),C'A'          TEST IF FACPAK NAME IN INDEX HDR             
         BL    NEXT1                                                            
         CLI   3(RF),C' '                                                       
         BNE   NEXT0                                                            
         MVI   TITLE,C' '          3 CHR FACPAK NAME                            
         MVI   TITLE+1,C' '                                                     
         MVC   TITLE+2(3),0(RF)                                                 
         B     NEXT1                                                            
NEXT0    MVI   TITLE,C' '          4 CHR FACPAK NAME                            
         MVC   TITLE+1(4),0(RF)                                                 
NEXT1    MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         ZAP   LINE,=P'75'                                                      
         ZAP   PAGE,=P'1'                                                       
*                                                                               
NEXT2    LA    R3,IOBUFF           READ CARD FOR PARAMETERS                     
         GOTO1 =V(CARDS),DMCB,(R3),=C'RE00'                                     
         CLC   0(2,R3),=C'/*'                                                   
         BNE   PARAM1                                                           
         XBASE                                                                  
*                                                                               
PARAM1   CLC   0(3,R3),=C'ALL'     P1 - 0=ALL 1=TRMS 2=PRGS                     
         BNE   PARAM1A                                                          
         MVI   P1,0                                                             
         MVC   TITLE+26(17),=C'ALL SYSTEM VALUES'                               
         MVC   MID1+1(10),=C'FIELD NAME'                                        
         MVC   MID2+1(17),=17C'-'                                               
         B     PARAM2                                                           
PARAM1A  CLC   0(3,R3),=C'TRM'                                                  
         BNE   PARAM1B                                                          
         MVI   P1,1                                                             
         MVC   MID1+1(15),=C'LINE-ID ADDRESS'                                   
         MVC   MID2+1(15),=C'------- -------'                                   
         B     PARAM2                                                           
PARAM1B  CLC   0(3,R3),=C'PRG'                                                  
         BNE   PARAM1C                                                          
         MVI   P1,2                                                             
         MVC   MID1+1(15),=C'SYSTEM  PROGRAM'                                   
         MVC   MID2+1(15),=C'------- -------'                                   
         B     PARAM2                                                           
PARAM1C  B     ERROR1                                                           
*                                                                               
PARAM2   CLC   4(3,R3),=C'DAY'     P2 - 0=DAILY 1=CUMULATIVE                    
         BNE   PARAM2A                                                          
         MVI   P2,0                                                             
         MVC   TITLE+43(15),=C' - DAILY TOTALS'                                 
         LA    R4,SDPRD0-SHDRFLDS                                               
         ST    R4,P2INDEX                                                       
         B     PARAM3                                                           
PARAM2A  CLC   4(3,R3),=C'CUM'                                                  
         BNE   PARAM2B                                                          
         MVC   TITLE+43(17),=C' - ONE DAYS TOTAL'                               
         LH    R0,COUNT                                                         
         CVD   R0,DUB                                                           
         UNPK  TITLE+46(3),DUB                                                  
         OI    TITLE+48,X'F0'                                                   
         LA    R4,STPRD0-SHDRFLDS                                               
         ST    R4,P2INDEX                                                       
         B     PARAM3                                                           
PARAM2B  B     ERROR2                                                           
*                                                                               
PARAM3   XC    P3,P3               P3 - A (FLD ENTRY)                           
         CLI   P1,0                                                             
         BE    PARAM4                                                           
         LA    R5,FLDTBL                                                        
PARAM3A  CLI   0(R5),X'FF'                                                      
         BE    ERROR3                                                           
         CLC   0(2,R5),8(R3)                                                    
         BE    *+12                                                             
         LA    R5,24(R5)                                                        
         B     PARAM3A                                                          
         ST    R5,P3                                                            
         MVC   TITLE+26(17),2(R5)                                               
         B     PARAM4                                                           
*                                                                               
PARAM4   EQU   *                                                                
*                                                                               
PARAMX   MVC   MID1+19(90),MID1FLD                                              
         MVC   MID2+19(90),MID2FLD                                              
         EJECT                                                                  
ALL      CLI   P1,0                ALL VALUES PRINTING                          
         BNE   SPEC                                                             
         L     R3,ATRMNDX          READ FIRST TERMINAL RECORD                   
         MVC   DSKADR,8(R3)                                                     
         LA    R0,IOBUFF                                                        
         BAS   R9,RSTATS                                                        
*                                                                               
         LA    R3,FLDTBL           R3=A(NEXT ENTRY IN FLDTBL)                   
ALL2     CLI   0(R3),X'FF'                                                      
         BE    ALLX                                                             
         ST    R3,P3                                                            
         MVC   P+1(17),2(R3)       SET FIELD NAME                               
         MVC   COLNUM,20(R3)       SET COLUMN NUMBER                            
         LA    RE,ROW1                                                          
         ST    RE,ROWADR                                                        
         OC    COLNUM(3),COLNUM    SPECIAL ROUTINE REQUIRED                     
         BNZ   ALL4                YES                                          
         BAS   RE,INVCOL           NO INVERT COLUMN INTO ROW#1                  
         B     ALL6                                                             
*                                                                               
ALL4     L     RF,COLNUM           GO TO SPECIAL ROUTINE                        
         BASR  RE,RF                                                            
ALL6     BAS   RE,EDTROW           EDIT ROW#1                                   
         MVC   SPACING,=C'BL02'                                                 
         GOTO1 =V(PRINTER)         PRINT                                        
         LA    R3,24(R3)                                                        
         B     ALL2                BUMP TO NEXT ENTRY                           
ALLX     B     NEXT                                                             
         EJECT                                                                  
SPEC     LA    R2,IOBUFF           TRM & PRG DISPLAY FOR SPECIFIC FIELD         
         XC    LASTHDR,LASTHDR                                                  
         CLI   P1,1                                                             
         BNE   SPEC2                                                            
         L     R3,ATRMNDX          R3&R4 USED FOR TERMINAL INDEX                
         LA    R4,12                                                            
         B     SPEC6                                                            
SPEC2    L     R3,APRGNDX          R3&R4 USED FOR PROGRAM INDEX                 
         LA    R4,21                                                            
         SR    R3,R4                                                            
         B     SPEC6                                                            
*                                                                               
SPEC6    AR    R3,R4               BUMP TO NEXT INDEX ENTRY                     
         LA    R5,0(R3,R4)                                                      
         SH    R5,=H'4'                                                         
         MVC   DSKADR,0(R5)        EXTRACT DISK ADDRESS                         
         CLI   0(R3),X'FF'                                                      
         BE    SPEC20                                                           
         LR    R0,R2                                                            
         BAS   R9,RSTATS           READ RECORD                                  
*                                                                               
SPEC8    CLI   P2,0                CHECK DAYLY FOR DATE                         
         BNE   SPEC10                                                           
         CLC   SDDATE,DATE                                                      
         BNE   SPEC6                                                            
*                                                                               
SPEC10   CLC   SHNAME1,LASTHDR+2   SAME MAJOR ID                                
         BE    SPEC12              YES                                          
         CLI   LASTHDR,0           FIRST TIME                                   
         BE    SPEC26              YES                                          
         B     SPEC20                                                           
*                                                                               
SPEC12   MVC   P+9(7),SHNAME2                                                   
         L     R5,P3               GET ADDRESS OF FIELD ENTRY                   
         MVC   COLNUM,20(R5)                                                    
         LA    RE,ROW1                                                          
         ST    RE,ROWADR                                                        
         OC    COLNUM(3),COLNUM                                                 
         BNZ   SPEC14                                                           
         BAS   RE,INVCOL           INVERT COLUMN INTO ROW#1                     
         B     SPEC16                                                           
SPEC14   L     RF,COLNUM           GO TO SPECIAL ROUTINE                        
         BASR  RE,RF                                                            
*                                                                               
SPEC16   BAS   RE,INVTRCNT         INVERT TRANS COUNT INTO ROW#4                
         BAS   RE,ADDROW           ADD ROW#1 TO ROW#0                           
         BAS   RE,ADDTRCNT         ADD ROW#4 TO ROW#3 (BUMP TRANS CNT)          
         BAS   RE,EDTROW           EDIT ROW#1                                   
         MVC   SPACING,=C'BL01'                                                 
         GOTO1 =V(PRINTER)                                                      
         MVC   LASTHDR,SHDRFLDS                                                 
         B     SPEC6                                                            
*                                                                               
SPEC20   L     R5,P3               CONTROL BREAK                                
         MVC   P+1(15),=C'        -------'                                      
         MVC   P+19(90),PFLD                                                    
         GOTO1 =V(PRINTER)                                                      
         TM    19(R5),X'80'                                                     
         BZ    SPEC22                                                           
         MVC   P+9(7),=C'TOTAL  '  FIELD NEEDS TOTAL                            
         B     SPEC24                                                           
SPEC22   BAS   RE,AVGROW           AVERAGE ROW#0                                
         MVC   P+9(7),=C'AVERAGE'  FIELD NEEDS AVERAGE                          
*                                                                               
SPEC24   LA    RE,ROW0             PRINT TOTALS/AVERAGES                        
         ST    RE,ROWADR                                                        
         BAS   RE,EDTROW                                                        
         MVC   SPACING,=C'BL02'                                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
SPEC26   MVC   P+1(7),SHNAME1                                                   
         XC    ADDCNT,ADDCNT       CLEAR ROW#0                                  
         XC    ROW0(60),ROW0       CLEAR ROW ACCUMULATORS                       
         XC    ROW1(60),ROW1                                                    
         XC    ROW2(60),ROW2                                                    
         XC    ROW3(60),ROW3                                                    
         XC    ROW4(60),ROW4                                                    
         CLI   0(R3),X'FF'                                                      
         BE    SPECX               CONTROL BREAK CAUSED BY END OF INDEX         
         MVC   LASTHDR,SHDRFLDS                                                 
         B     SPEC10                                                           
SPECX    B     NEXT                                                             
         EJECT                                                                  
ERROR1   MVC   P+1(15),=C'PARAM#1 INVALID'                                      
         B     ERRORX                                                           
ERROR2   MVC   P+1(15),=C'PARAM#2 INVALID'                                      
         B     ERRORX                                                           
ERROR3   MVC   P+1(15),=C'PARAM#3 INVALID'                                      
         B     ERRORX                                                           
*                                                                               
ERRORX   MVC   P+17(80),IOBUFF                                                  
         GOTO1 =V(PRINTER)                                                      
         B     NEXT                                                             
         EJECT                                                                  
FLD07    NTR1                      COMPUTE MEAN I/O TIME                        
         MVC   COLNUM,=F'1'        ROW#1 = TRANSACTION TIME                     
         LA    RE,ROW1                                                          
         ST    RE,ROWADR                                                        
         BAS   RE,INVCOL                                                        
         MVC   COLNUM,=F'2'        ROW#2 - CPU TIME                             
         LA    RE,ROW2                                                          
         ST    RE,ROWADR                                                        
         BAS   RE,INVCOL                                                        
*                                                                               
         LA    RE,ROW1             ROW#1 = ROW#1 - ROW#2                        
         LA    RF,ROW2                                                          
         LA    R5,15                                                            
FLD07A   L     R0,0(RE)                                                         
         S     R0,0(RF)                                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R5,FLD07A                                                        
*                                                                               
         LA    RE,ROW1                                                          
         ST    RE,ROWADR                                                        
FLD07X   XIT1                                                                   
         EJECT                                                                  
RSTATS   LA    R8,DMRDIR           STATS FILE I/O                               
         B     IOSTATS                                                          
SSTATS   LA    R8,DMRSEQ                                                        
*                                                                               
IOSTATS  GOTO1 =V(DATAMGR),DMCB,(R8),STATS,DSKADR,(R0)                          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DIE IF DISK ERROR                            
         BR    R9                                                               
         EJECT                                                                  
INVCOL   NTR1                      INVERT COL# COLNUM TO ROW @ ROWADR           
         LA    R3,IOBUFF                                                        
         A     R3,P2INDEX                                                       
         L     R4,COLNUM                                                        
         SLL   R4,2                                                             
         AR    R3,R4                                                            
         L     R5,ROWADR                                                        
         LA    R0,15                                                            
INVCOL1  MVC   0(4,R5),0(R3)                                                    
         LA    R3,40(R3)                                                        
         LA    R5,4(R5)                                                         
         BCT   R0,INVCOL1                                                       
INVCOLX  XIT1                                                                   
*                                                                               
INVTRCNT NTR1                      INVERT TRANS COUNT INTO ROW#4                
         LA    R5,ROW4             FILL WITH ONES IF NOT AVG TYPE               
         LA    R0,15                                                            
         LA    R1,1                                                             
         ST    R1,0(R5)                                                         
         LA    R5,4(R5)                                                         
         BCT   R0,*-8                                                           
         L     R5,P3                                                            
         TM    19(R5),X'80'                                                     
         BO    INVTRCX                                                          
         LA    R3,IOBUFF           FILL WITH TRANS COUNT IF AVG TYPE            
         A     R3,P2INDEX                                                       
         LA    R5,ROW4                                                          
         LA    R0,15                                                            
INVTRC1  MVC   0(4,R5),0(R3)                                                    
         LA    R3,40(R3)                                                        
         LA    R5,4(R5)                                                         
         BCT   R0,INVTRC1                                                       
INVTRCX  XIT1                                                                   
*                                                                               
EDTROW   NTR1                      EDIT ROW @ ROWADR INTO PRINT LINE            
         LA    R3,P+19                                                          
         L     R4,ROWADR                                                        
         LA    R5,15                                                            
EDTROW1  L     R6,0(R4)                                                         
         EDIT  (R6),(6,(R3))                                                    
         L     R1,P3               GET NUM OF DEC PLACES FROM TABLE             
         MVC   DUB,19(R1)                                                       
         NI    DUB,X'7F'                                                        
         SR    R1,R1                                                            
         IC    R1,DUB                                                           
         LTR   R1,R1               TEST NUMBER OF DEC PLACES                    
         BZ    EDTROW2                                                          
         CLC   0(6,R3),SPACES                                                   
         BE    EDTROW2                                                          
         MVC   DUB,0(R3)           MOVE IN DEC POINT                            
         LA    RE,5                                                             
         SR    RE,R1                                                            
         LA    RF,0(R3,RE)                                                      
         MVI   0(RF),C'.'                                                       
         OI    1(RF),X'F0'                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),DUB+1                                                    
EDTROW2  LA    R3,6(R3)                                                         
         LA    R4,4(R4)                                                         
         BCT   R5,EDTROW1                                                       
EDTROWX  XIT1                                                                   
         EJECT                                                                  
ADDROW   NTR1                      ADD ROW#1 TO ROW#0                           
         L     RE,ADDCNT                                                        
         LA    RE,1(RE)                                                         
         ST    RE,ADDCNT                                                        
         LA    R2,ROW4                                                          
         LA    RE,ROW0                                                          
         LA    RF,ROW1                                                          
         LA    R3,15                                                            
ADDROW1  L     R1,0(RF)            WEIGHT BY TRANS COUNT IN ROW#4               
         L     R0,0(R2)                                                         
         MR    R0,R0                                                            
         A     R1,0(RE)                                                         
         ST    R1,0(RE)                                                         
         LA    R2,4(R2)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R3,ADDROW1                                                       
ADDROWX  XIT1                                                                   
*                                                                               
ADDTRCNT NTR1                      ADD ROW#4 TO ROW#3                           
         LA    RE,ROW3                                                          
         LA    RF,ROW4                                                          
         LA    R3,15                                                            
ADDTRC1  L     R0,0(RE)                                                         
         A     R0,0(RF)                                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R3,ADDTRC1                                                       
ADDTRCX  XIT1                                                                   
*                                                                               
AVGROW   NTR1                      AVERAGE ROW#0                                
         LA    R4,ROW0             R4=A(SUM OF WEIGHTED VALUES)                 
         LA    R5,ROW3             R5=A(SUM OF WEIGHTS)                         
         LA    R3,15                                                            
AVGROW1  SR    RE,RE                                                            
         L     RF,0(R4)                                                         
         L     R0,0(R5)                                                         
         LTR   R0,R0                                                            
         BZ    *+18                                                             
         DR    RE,R0                                                            
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,0(R4)                                                         
         LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         BCT   R3,AVGROW1                                                       
AVGROWX  XIT1                                                                   
         EJECT                                                                  
FLDTBL   DS    0F                                                               
         DC    CL19'01TRANSACTION COUNT',X'80',A(0)                             
         DC    CL19'02MEAN INPUT LENGTH',X'00',A(6)                             
         DC    CL19'03MEAN OTPUT LENGTH',X'00',A(7)                             
         DC    CL19'04MEAN I/O COUNT   ',X'01',A(8)                             
         DC    CL19'05MEAN OVRLAY COUNT',X'01',A(9)                             
         DC    CL19'06MEAN CPU TIME    ',X'00',A(2)                             
         DC    CL19'07MEAN I/O TIME    ',X'00',A(FLD07)                         
         DC    CL19'08MEAN TOTAL TIME  ',X'00',A(1)                             
         DC    CL19'09MEAN INPUT Q TIME',X'00',A(3)                             
         DC    CL19'10INPUT Q FREQUENCY',X'02',A(4)                             
         DC    CL19'11OTPUT Q FREQUENCY',X'02',A(5)                             
FLDTBLX  DC    X'FF'                                                            
*                                                                               
MID1FLD  DC    CL48' TOTAL BEFOR 08-09 09-10 10-11 11-12 12-01 01-02'           
         DC    CL42' 02-03 03-04 04-05 05-06 06-07 07-08 AFTER'                 
MID2FLD  DC    CL48' ----- 08-AM    AM    AM    AM    AM    PM    PM'           
         DC    CL42'    PM    PM    PM    PM    PM    PM 08-PM'                 
PFLD     DC    CL48' ----- ----- ----- ----- ----- ----- ----- -----'           
         DC    CL42' ----- ----- ----- ----- ----- ----- -----'                 
*                                                                               
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMRDIR   DC    CL8'DMRDIR'                                                      
STATS    DC    CL8'STATS'                                                       
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    4F                                                               
PRGNDX   DS    110XL21             MAX 110 PROGRAMS                             
         DS    XL90                SPARE AT END OF PROGRAM INDEX                
         DS    XL12                                                             
*                                                                               
         DS    4F                                                               
TRMNDX   DS    12000XL12           MAX 12000 TERMINALS                          
         DS    XL12                                                             
*                                                                               
STATWORK DS    0D                                                               
         DS    1024D                                                            
*                                                                               
SSB      DS    0D                                                               
         DC    X'00000002',XL28'00'                                             
*                                                                               
UTL      DS    0D                                                               
         DC    F'0',X'01',XL27'00'                                              
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
DSKADR   DS    F                                                                
ACOMRG   DS    A                                                                
UPSI     DS    X                                                                
         DS    XL3                                                              
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P2INDEX  DS    F                                                                
APRGNDX  DS    A                                                                
ATRMNDX  DS    A                                                                
COLNUM   DS    F                                                                
ROWADR   DS    A                                                                
ADDCNT   DS    F                                                                
DATE     DS    CL6                                                              
COUNT    DS    H                                                                
LASTHDR  DS    CL16                                                             
WORK     DS    CL20                                                             
*                                                                               
ROW0     DS    15F                                                              
ROW1     DS    15F                                                              
ROW2     DS    15F                                                              
ROW3     DS    15F                                                              
ROW4     DS    15F                                                              
*                                                                               
IOBUFF   DS    2416C                                                            
*                                                                               
WRKX     DS    0C                                                               
         EJECT                                                                  
       ++INCLUDE DDSTATREC                                                      
                                                                                
       ++INCLUDE DDDPRINT                                                       
                                                                                
* FASSBOFF                                                                      
SSOOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDSTATHOUR06/03/14'                                      
         END                                                                    
