*          DATA SET NERWIN     AT LEVEL 018 AS OF 05/01/02                      
*************************************                                           
*  NERWIN - INPUT MODULE FOR NETPAK OVERNIGHT REPORT WRITER                     
*  CONFIGURES MEMORY AND READS UNITS                                            
*                                                                               
*  INPUTS FROM EDIT : GEND, NERWBLOCK                                           
*                                                                               
*CATALP NERWIN                                                                  
         TITLE 'NERWIN - NETWORK OVERNIGHT REPORT WRITER INPUT PHASE'           
         PRINT NOGEN                                                            
NERWIN   CSECT                                                                  
         NMOD1 0,**RWIN**,RR=R2                                                 
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,4(R1)                                                         
         USING NERWBLOK,R8                                                      
         LA    RA,2048(RB)                                                      
         LA    RA,2048(RA)                                                      
         USING NERWIN+4096,RA                                                   
         L     R9,ASYSD                                                         
         USING NETSYSD,R9                                                       
         L     R7,AWS                                                           
         USING WORKD,R7                                                         
*                                                                               
         MVC   CALLRD,4(RD)        SAVE W/S BACK LINK                           
*                                                                               
*                                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
         MVI   PRDFLAG,1           SET PRODUCT FLAG                             
         CLC   NBSELPRD,=C'ALL'                                                 
         BE    ALLPRODS                                                         
         CLC   NBSELPRD,=C'POL'                                                 
         BE    ALLPRODS                                                         
         OC    NBSELPRD,NBSELPRD                                                
         BNZ   ONEPROD                                                          
         OC    NBSELPNM,NBSELPNM                                                
         BNZ   ONEPROD                                                          
ALLPRODS MVI   PRDFLAG,0           SET TO 0 IF ALL PRODS CHOSEN                 
ONEPROD  EQU   *                                                                
*                                                                               
         SPACE 2                                                                
*                                                                               
*                                                                               
         BAS   RE,MEMINIT          INITIALIZE MEMORY                            
*                                                                               
*                                                                               
         MVI   NBDATA,C'U'         SELECT UNIT RECORDS                          
         MVI   NBUSER+13,C'N'      DONT FILTER PRE-EMPTS                        
*                                                                               
GETFIRST NETGO NSNETIO,DMCB,NETBLOCK   GET FIRST UNIT                           
         CLI   NBERROR,0                                                        
         BNE   PROCERR                                                          
         CLI   NBMODE,NBPROCUN     IF  UNIT RECORD                              
         BNE   GF2                                                              
         MVI   NACINIT,0           A NEW UNIT SO INIT ACCTG BLOCK               
         BAS   RE,FILLAC           FILL FIRST AC BLOCK                          
         CLI   NACMODE,NACREJ      IF DATA DOESN'T MEET FILTER                  
         BE    GETFIRST                                                         
         BAS   RE,FILLMORE          FILL IN ADDITIONAL INFO                     
         B     CKOVER                                                           
GF2      CLI   NBMODE,NBREQLST     IF NO UNITS                                  
         BE    TXMOD                THEN DONE                                   
         B     GETFIRST            OTHER MODES ARE IGNORED                      
*                                                                               
GETNEXT  NETGO NSNETIO,DMCB,NETBLOCK                                            
         CLI   NBERROR,0                                                        
         BNE   PROCERR                                                          
         CLI   NBMODE,NBPROCUN     IF  UNIT RECORD                              
         BNE   GN2                                                              
         MVI   NACINIT,0           A NEW UNIT SO INIT ACCTG BLOCK               
         BAS   RE,FILLAC           FILL FIRST AC BLOCK                          
         CLI   NACMODE,NACREJ      IF DATA DOESN'T MEET FILTER                  
         BE    GETNEXT                                                          
         BAS   RE,FILLMORE                                                      
         B     CKOVER                                                           
GN2      CLI   NBMODE,NBREQLST     IF NO UNITS                                  
         BE    TXMOD                                                            
         B     GETNEXT             OTHER MODES ARE IGNORED                      
*                                                                               
CKOVER   BAS   RE,UNIT             FILL IN UNIT LINE                            
         BAS   RE,HOOK                                                          
         CLI   NACMODE,NACBP       CK IF MORE TO FILTER                         
         BNE   GETNEXT                                                          
         GOTO1 INTINIT,DMCB,AINTERN    CLEAR INTERNAL RECS                      
ACCTLOOP BAS   RE,FILLAC           FILL NEXT AC BLOCK                           
         CLI   NACMODE,NACBP      CK IF LAST ACCT BLOCK                         
         BNE   GETNEXT                                                          
         BAS   RE,PRACC                                                         
         BAS   RE,ACCHOOK                                                       
         B     ACCTLOOP                                                         
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
TXMOD    XMOD1                                                                  
*                                                                               
PROCERR  DC    F'0'                                                             
*                                                                               
*                                                                               
*****************************************************************               
         EJECT                                                                  
*****************************************************************               
* HOOK - CALLS USER HOOK                                                        
HOOK     NTR1                                                                   
         B     HK2                                                              
ACCHOOK  NTR1                                                                   
HK2      L     RF,AINHOOK                                                       
         L     RE,CALLRD         RESTORE CALLING ROUTINE REGISTERS              
         LR    R0,RE                                                            
         LM    R1,RC,24(RE)                                                     
         BASR  RE,RF                                                            
         XIT1                                                                   
*****************************************************************               
         EJECT                                                                  
*****************************************************************               
* MEMINIT - INITIALIZE MEMORY                                                   
*       CALLS TO: INTINIT - INITIALIZE INTERNAL RECORD.                         
*       OUTPUTS: ADDRESSES IN NERWBLOCK                                         
*       INTERNALS: R5 -  DRIVE TABLE                                            
*                  R6 - CURRENT LEN OF INTERNAL RECORD                          
*                  R4 - CURRENT LENGTH OF PRINT LINE                            
*                                                                               
MEMINIT  NTR1                                                                   
         L     R5,ADRIVE1                                                       
         USING RWINBLOK,R5           GET LENGTH OF INTERNAL RECORD              
         SR    R6,R6                                                            
         SR    R4,R4                                                            
MILOOP   CLI   RWINTYPE,0                                                       
         BE    MIEND                                                            
         STH   R6,RWOFFS           SET OFFSET INTO INTERNAL REC                 
         ZIC   R1,RWOUTLEN                                                      
         LA    R6,0(R6,R1)         SUM INTERNAL RECORD LENGTH                   
         ZIC   R1,RWPRLEN                                                       
         LTR   R1,R1                                                            
         BZ    MI2                 DON'T ADD THIS IF NOT PRINTABLE              
         LA    R4,1(R4,R1)         SUM PRINT LINE + 1 COL FOR SPACE             
*                                                                               
MI2      LA    R5,RWBLKLEN(R5)      NEXT ENTRY IN DRIVE TABLE                   
         B     MILOOP                                                           
MIEND    ST    R6,IRECLEN                                                       
         LA    R1,132                                                           
         CR    R4,R1               CK IF EXCEED PRINT LINE                      
         BL    MI4                                                              
         DC    H'0'                                                             
MI4      L     R2,ABEGSTR                                                       
         ST    R2,AINTERN          SET ADRESSES                                 
         LA    R2,0(R2,R6)                                                      
         ST    R2,APREVREC                                                      
         LA    R2,0(R2,R6)                                                      
         ST    R2,ABRK1IR                                                       
         LA    R2,0(R2,R6)                                                      
         ST    R2,ABRK2IR                                                       
         LA    R2,0(R2,R6)                                                      
         ST    R2,ABRK3IR                                                       
         LA    R2,0(R2,R6)                                                      
         ST    R2,ABRK4IR                                                       
         LA    R2,0(R2,R6)                                                      
         ST    R2,ABRK5IR                                                       
         LA    R2,0(R2,R6)         END OF INTERNAL REC                          
         ST    R2,ABRK6IR                                                       
         LA    R2,0(R2,R6)                                                      
         L     R1,AENDSTR                                                       
         CR    R1,R2               CK IF ENOUGH STORAGE                         
         BNL   MI5                                                              
         DC    H'0'                                                             
MI5      GOTO1 INTINIT,DMCB,AINTERN    INITIALIZE INTERNAL RECS                 
         GOTO1 INTINIT,DMCB,APREVREC                                            
         GOTO1 INTINIT,DMCB,ABRK1IR                                             
         GOTO1 INTINIT,DMCB,ABRK2IR                                             
         GOTO1 INTINIT,DMCB,ABRK3IR                                             
         GOTO1 INTINIT,DMCB,ABRK4IR                                             
         GOTO1 INTINIT,DMCB,ABRK5IR                                             
         B     XIT                                                              
*****************************************************************               
         DROP  R5                                                               
         EJECT                                                                  
*****************************************************************               
* INTINIT - INITIALIZE THE INTERNAL RECORD POINTED TO BY ARG1                   
*           MOVE ZEROES FOR NUMERICS AND ALPHAS                                 
*           PACKED ZEROES FOR COST                                              
*       INPUTS: ARG1 (R6) - A(INTERNAL RECORD TO INITIALIZE)                    
INTINIT  NTR1                                                                   
         L     R6,0(R1)                                                         
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         USING RWINBLOK,R5                                                      
ININ2    CLI   RWINTYPE,0                                                       
         BE    ININXIT                                                          
         ZIC   RF,RWOUTLEN          PUT LENGTH IN RF                            
         CLI   RWINTYPE,NRICOST      CK FOR COST                                
         BE    ININCOST                                                         
         XCEF  (R6),(RF)            CLEAR IT                                    
         B     ININNEXT                                                         
*                                                                               
ININCOST MVI   0(R6),0              FOR COST 1ST BYTE IS ZERO                   
         ZAP   1(8,R6),ZERO         AND ZAP NEXT 8                              
*                                                                               
ININNEXT ZIC   R1,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R1,R6)                                                      
         LA    R5,RWBLKLEN(R5)      BUMP TO NEXT DRIVE                          
         B     ININ2                                                            
*                                                                               
ININXIT  B     XIT                                                              
************************************************************                    
         DROP  R5                                                               
         EJECT                                                                  
************************************************************                    
*              ROUTINE TO BUILD INTERNAL RECORD                                 
*  LOCALS - R5 -A(CURRENT PIECE OF DRIVE TABLE)                                 
*           R6 -A(CURRENT INTERNAL RECORD)                                      
*   OUTPUT: FILLS PREVIOUS RECORD                                               
*           FILLS INTERNAL RECORD                                               
*                                                                               
UNIT     NTR1                                                                   
         L     R5,AINTERN                                                       
         L     R4,APREVREC                                                      
         L     R3,IRECLEN                                                       
         BCTR  R3,0                                                             
         EXMVC R3,0(R4),0(R5)      MOVE CURRENT RECORD TO PREV RECORD           
*                                                                               
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         L     R6,AINTERN          GET A(INTERNAL RECORD)                       
UN2      CLI   RWINTYPE,0                                                       
         BE    UNXIT                                                            
         ST    R6,RWAOUT                                                        
         MVI   RWFUNC,RWMOVE                                                    
         GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
*                                                                               
UN4      ZIC   R2,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R2,R6)                                                      
         LA    R5,RWBLKLEN(R5)     BUMP TO NEXT DRIVE                           
         B     UN2                                                              
UNXIT    B     XIT                                                              
         DROP  R5                                                               
**********************************************************                      
         EJECT                                                                  
************************************************************                    
* PRACC        ROUTINE TO PROCESS ACCCTG SUB-RECORD                             
*  LOCALS - R5 -A(CURRENT PIECE OF DRIVE TABLE)                                 
*           R6 -A(CURRENT INTERNAL RECORD)                                      
*                                                                               
PRACC    NTR1                                                                   
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         L     R6,AINTERN          GET A(INTERNAL RECORD)                       
PAC2     CLI   RWINTYPE,0                                                       
         BE    PACXIT                                                           
         ST    R6,RWAOUT                                                        
         MVI   RWFUNC,RWMOVE                                                    
         GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
*                                                                               
PAC4     ZIC   R2,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R2,R6)                                                      
         LA    R5,RWBLKLEN(R5)     BUMP TO NEXT DRIVE                           
         B     PAC2                                                             
PACXIT   B     XIT                                                              
         DROP  R5                                                               
**********************************************************                      
         EJECT                                                                  
**********************************************************                      
* GETPER - SET SAVEPER TO THE CORRECT PERIOD                                    
*       INPUTS: APERLST1 - PERIOD LIST                                          
*               NBACTDAT - CURRENT DATE                                         
*       OUTPUT: SAVEPER - THE PERIOD THAT NBACTDAT BELONGS TO                   
**********************************************************                      
GETPER   NTR1                                                                   
         L     R6,APERLST1                                                      
GPLOOP   CLC   NBACTDAT,2(R6)                                                   
         BNH   GOTPER                                                           
         LA    R6,4(R6)            NEXT DATE SET                                
         B     GPLOOP                                                           
GOTPER   MVC   SAVEPER,0(R6)                                                    
         XIT1                                                                   
         EJECT                                                                  
**********************************************************                      
PRLUP    NTR1                                                                   
         MVC   WORK,SPACE                                                       
         LA    R4,NBPRD                                                         
         LA    R5,WORK                                                          
         BAS   RE,PRLUP2                                                        
         LA    R4,NBPRD2                                                        
         LA    R5,WORK+3                                                        
         BAS   RE,PRLUP2                                                        
         B     XIT                                                              
         SPACE 2                                                                
PRLUP2   L     R2,ANETWS1          A(CLIENT RECORD)                             
         USING CLTHDR,R2                                                        
         LA    R2,CLIST            PRODUCT LIST                                 
         DROP  R2                                                               
         LA    R3,220                                                           
         CLI   0(R4),0                                                          
         BER   RE                                                               
         CLI   0(R4),X'FF'                                                      
         BER   RE                                                               
         SPACE 2                                                                
PRLUP4   CLC   0(1,R4),3(R2)                                                    
         BE    PRLUP6                                                           
         LA    R2,4(R2)                                                         
         BCT   R3,PRLUP4                                                        
         BR    RE                                                               
         SPACE 2                                                                
PRLUP6   MVC   0(3,R5),0(R2)                                                    
         BR    RE                                                               
         EJECT                                                                  
********************************************************                        
* FILLMORE - ROUTINE TO FILL SUPPLEMENTARY BLOCK                                
*          LOCALS:  R5 -      BLOCK                                             
********************************************************                        
FILLMORE NTR1                                                                   
         BAS   RE,GETPER           GET PERIOD                                   
         B     XIT                                                              
         EJECT                                                                  
********************************************************                        
* FILLAC - ROUTINE TO FILL ACCOUNTING BLOCK                                     
*          SUPPORTS AUDIT TRAIL AND PAID/CLEARED FILTER DATES                   
*          LOCALS:  R5 - NACCTBLOCK                                             
*                   R6 - USED FOR NEXTEL                                        
********************************************************                        
FILLAC   NTR1                                                                   
         CLI   NACINIT,1                                                        
         BNE   FAC2                                                             
         XC    NBACTUAL,NBACTUAL   IF SECOND PASS, SET COSTS TO ZERO.           
         XC    NBASSIGN,NBASSIGN   THEY HAVE ALREADY BEEN PRINTED,ADDED         
         XC    NBINTEG,NBINTEG                                                  
         XC    NBCALCOS,NBCALCOS                                                
         MVI   NBUNITST,0          SET UNIT STATUS TO 0 FOR THESE LINES         
         B     FAC6                                                             
*                                                                               
FAC2     L     R3,NBAIO            INITIALIZE                                   
         USING NURECD,R3                                                        
         LA    R4,NUMAINEL                                                      
         DROP  R3                                                               
         ST    R4,NACABILE         SET CURRENT ELEMENTS TO FIRST EL             
         ST    R4,NACAPAYE                                                      
         MVI   NACMODE,0           INITIALIZE                                   
         MVI   NACINIT,1                                                        
*                                                                               
         TM    NBUNITST,X'42'      IF PRE-EMPT OR MISSED                        
         BZ    PU1                                                              
         XC    NBACTUAL,NBACTUAL   SET COSTS TO 0 BUT                           
         XC    NBASSIGN,NBASSIGN   LEAVE BILLING AND PAYING                     
         XC    NBINTEG,NBINTEG                                                  
         XC    NBCALCOS,NBCALCOS                                                
*                                                                               
*  CONDITINAL ASSIGNED COST                                                     
PU1      CLI   NBUSER+8,C'Y'       IF FLAG SET TO USE ASSIGNED                  
         BNE   PU3                                                              
         OC    NBASSIGN,NBASSIGN   AND IF ASSIGNED COST NON-ZERO                
         BNZ   PU2                                                              
         TM    NBUNITST,X'08'      OR ASSIGNED COST TRULY ZERO                  
         BZ    PU3                                                              
PU2      MVC   NBCALCOS,NBASSIGN     THEN USE ASSIGNED COST                     
         B     PU4                                                              
PU3      MVC   NBCALCOS,NBACTUAL   ELSE USE ACTUAL COST                         
*                                                                               
PU4      CLI   NBSELFLT,0          IF A FILTER                                  
         BE    FILTDON                                                          
         CLI   NBSELFLT,4          UNBILLED                                     
         BNE   FLT2                                                             
         L     R1,NBCALCOS                                                      
         A     R1,NBINTEG                                                       
         S     R1,NBBILTGR         FOR FILTER = UNCLEARED                       
         S     R1,NBBILIGR                                                      
         LTR   R1,R1               IF ZERO THEN REJECT                          
         BZ    FACREJ                                                           
         B     FILTDON                                                          
FLT2     CLI   NBSELFLT,2                                                       
         BNE   FILTDON             OTHER FILTS TAKEN CARE OF BY NETIO           
         L     R1,NBACTUAL                                                      
         A     R1,NBINTEG                                                       
         S     R1,NBPAYTGR         FOR FILTER = UNCLEARED                       
         S     R1,NBPAYIGR                                                      
         LTR   R1,R1               IF ZERO THEN REJECT                          
         BZ    FACREJ                                                           
         B     FILTDON                                                          
*                                                                               
FILTDON  CLI   PRDFLAG,1           IF ONE PRODUCT                               
         BNE   PU5                                                              
         CLI   NBPRD2,0                                                         
         BE    PU5                                                              
*                                                                               
PRDPCT   SR    R6,R6                                                            
         ICM   R6,3,NBP1SHR        PCTG FOR FIRST PRODUCT                       
         CLC   NBEFFPNM,NBPRD      IF THIS FIRST PRODUCT THEN GO ON             
         BE    APRD2               IF NOT ITS PROD2                             
         CLC   NBEFFPNM,NBPRD2                                                  
         BE    APRD1                                                            
         DC    H'0'                                                             
APRD1    L     R5,=F'10000'        PUT 100.00 IN R5                             
         SR    R5,R6               THIS IS PCTG FOR PROD 2                      
         LR    R6,R5                                                            
APRD2    DS    0H                                                               
*                                                                               
         L     R5,NBACTUAL         ADJUST ACTUAL                                
         MR    R4,R6                                                            
         SLDA  R4,1                DOUBLE FOR ROUNDING                          
         D     R4,=F'10000'                                                     
         LTR   R5,R5                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         ST    R5,NBACTUAL                                                      
*                                                                               
         L     R5,NBASSIGN         ADJUST ASSIGNED COST                         
         MR    R4,R6                                                            
         SLDA  R4,1                DOUBLE FOR ROUNDING                          
         D     R4,=F'10000'                                                     
         LTR   R5,R5                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         ST    R5,NBASSIGN                                                      
*                                                                               
         L     R5,NBCALCOS         ADJUST CALCULATED ASSIGNED COST              
         MR    R4,R6                                                            
         SLDA  R4,1                DOUBLE FOR ROUNDING                          
         D     R4,=F'10000'                                                     
         LTR   R5,R5                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         ST    R5,NBCALCOS                                                      
*                                                                               
         L     R5,NBINTEG          ADJUST INTEGRATION COST                      
         MR    R4,R6                                                            
         SLDA  R4,1                DOUBLE FOR ROUNDING                          
         D     R4,=F'10000'                                                     
         LTR   R5,R5                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         ST    R5,NBINTEG                                                       
*                                                                               
*                                                                               
*                                                                               
PU5      EQU   *                                                                
FAC6     XC    NACDTFLD(NACDTLN),NACDTFLD   CLEAR DATA FIELDS                   
*                                                                               
         L     R6,NACABILE         GET NEXT BILL ELEMENT                        
         MVI   SRCHEL,X'10'                                                     
NEXTBEL  BAS   RE,NEXTEL                                                        
         BNE   DOPAY               LAST ELEMENT                                 
         USING NUBILD,R6                                                        
         ST    R6,NACABILE         NEW CURRENT ELEMENT                          
         TM    NUBILST,X'20'       CHECK IF UNBILLED                            
         BO    NEXTBEL                                                          
         CLI   PRDFLAG,0           EXTRA FILTERING IF 1 PROD                    
         BE    DOBILL1                                                          
         CLC   NBEFFPNM,NUBILPRD                                                
         BNE   NEXTBEL                                                          
*                                                                               
DOBILL1  CLI   NACSBTY,0                CK IF BILL TYPE FILTER                  
         BE    DOBILL4                                                          
         CLC   NACSBTY,NUBILBTY                                                 
         BNE   NEXTBEL                                                          
*                                                                               
DOBILL4  OC    NACSBDT(2),NACSBDT                                               
         BZ    DOBILL6               DATE FILTER                                
         CLC   NACSBDT(2),NUBILDAT                                              
         BH    NEXTBEL                                                          
DOBILL6  OC    NACSBDT+2(2),NACSBDT+2                                           
         BZ    DOBILL8                                                          
         CLC   NACSBDT+2(2),NUBILDAT                                            
         BL    NEXTBEL                                                          
DOBILL8  CLI   NACSMOD,C'A'        FOR AUDIT TRAIL                              
         BNE   DOBILL16                                                         
         CLI   NUBILTYP,C'T'        TIME                                        
         BNE   DOBILL10                                                         
         MVI   NACBTFLG,1          ELEMENT EXISTS                               
         MVC   NACBTDAT(21),NUBILDAT                                            
         BAS   RE,NEXTEL           IF CORRESPONDING INT, DO IT TOO              
         BNE   DOBILL11                                                         
         CLI   NUBILTYP,C'I'                                                    
         BNE   DOBILL11                                                         
         CLC   NACBTDAT,NUBILDAT                                                
         BNE   DOBILL11                                                         
         CLC   NACBTPRD,NUBILPRD                                                
         BNE   DOBILL11                                                         
         ST    R6,NACABILE         GOT ONE                                      
         MVC   NACBIDAT(21),NUBILDAT                                            
         MVI   NACBIFLG,1                                                       
         B     DOBILL11                                                         
DOBILL10 MVC   NACBIDAT(21),NUBILDAT                                            
         MVI   NACBIFLG,1          ELEMENT EXISTS                               
         BAS   RE,NEXTEL           IF CORRESPONDING TIME, DO IT TOO             
         BNE   DOBILL11                                                         
         CLI   NUBILTYP,C'T'                                                    
         BNE   DOBILL11                                                         
         CLC   NACBIDAT,NUBILDAT                                                
         BNE   DOBILL11                                                         
         CLC   NACBIPRD,NUBILPRD                                                
         BNE   DOBILL11                                                         
         ST    R6,NACABILE         GOT ONE                                      
         MVC   NACBTDAT(21),NUBILDAT                                            
         MVI   NACBTFLG,1                                                       
         B     DOBILL11                                                         
DOBILL11 B     DOPAY                                                            
*                                                                               
DOBILL16 CLI   NUBILTYP,C'T'       FOR NON-AUDIT TRAIL, DO SUMS                 
         BNE   DOBILL18                                                         
         MVI   NACBTFLG,1          ELEMENT EXISTS                               
         L     R2,NUBILGRS                                                      
         A     R2,NACBTGRS                                                      
         ST    R2,NACBTGRS                                                      
         L     R2,NUBILNET                                                      
         A     R2,NACBTNET                                                      
         ST    R2,NACBTNET                                                      
         B     NEXTBEL                                                          
DOBILL18 MVI   NACBIFLG,1          ELEMENT EXISTS                               
         L     R2,NUBILGRS         INTEGRATION                                  
         A     R2,NACBIGRS                                                      
         ST    R2,NACBIGRS                                                      
         L     R2,NUBILNET                                                      
         A     R2,NACBINET                                                      
         ST    R2,NACBINET                                                      
         B     NEXTBEL                                                          
*                                                                               
DOPAY    L     R6,NACAPAYE         GET NEXT PAY ELEMENT                         
         MVI   SRCHEL,X'12'                                                     
NEXTPEL  BAS   RE,NEXTEL                                                        
         BNE   FACXIT              LAST ELEMENT                                 
         USING NUPAYD,R6                                                        
         ST    R6,NACAPAYE         NEW CURRENT ELEMENT                          
*                                                                               
         OC    NACSPDT(2),NACSPDT                                               
         BZ    PAY6                DATE FILTER                                  
         CLC   NACSPDT(2),NUPAYDAT                                              
         BH    NEXTPEL                                                          
PAY6     OC    NACSPDT+2(2),NACSPDT+2                                           
         BZ    PAY8                                                             
         CLC   NACSPDT+2(2),NUPAYDAT                                            
         BL    NEXTPEL                                                          
PAY8     CLI   NACSMOD,C'A'        FOR AUDIT TRAIL                              
         BNE   PAY16                                                            
         CLI   NUPAYTYP,C'T'        TIME                                        
         BNE   PAY10                                                            
         MVC   NACPTDAT(21),NUPAYDAT                                            
         MVI   NACPTFLG,1          ELEMENT EXISTS                               
         BAS   RE,NEXTEL           IF CORRESPONDING INT, DO IT TOO              
         BNE   PAY11                                                            
         CLI   NUPAYTYP,C'I'                                                    
         BNE   PAY11                                                            
         CLC   NACPTDAT,NUPAYDAT                                                
         BNE   PAY11                                                            
         ST    R6,NACAPAYE         GOT ONE                                      
         MVC   NACPIDAT(21),NUPAYDAT                                            
         MVI   NACPIFLG,1                                                       
         B     PAY11                                                            
PAY10    MVC   NACPIDAT(21),NUPAYDAT                                            
         MVI   NACPIFLG,1          ELEMENT EXISTS                               
         BAS   RE,NEXTEL           IF CORRESPONDING TIME, DO IT TOO             
         BNE   PAY11                                                            
         CLI   NUPAYTYP,C'T'                                                    
         BNE   PAY11                                                            
         CLC   NACPIDAT,NUPAYDAT                                                
         BNE   PAY11                                                            
         ST    R6,NACAPAYE         GOT ONE                                      
         MVC   NACPTDAT(21),NUPAYDAT                                            
         MVI   NACPTFLG,1                                                       
         B     PAY11                                                            
PAY11    B     FACXIT                                                           
*                                                                               
PAY16    CLI   NUPAYTYP,C'T'       FOR NON-AUDIT TRAIL, DO SUMS                 
         BNE   PAY18                                                            
         MVI   NACPTFLG,1          ELEMENT EXISTS                               
         L     R2,NUPAYGRS                                                      
         A     R2,NACPTGRS                                                      
         ST    R2,NACPTGRS                                                      
         L     R2,NUPAYNET                                                      
         A     R2,NACPTNET                                                      
         ST    R2,NACPTNET                                                      
         B     NEXTPEL                                                          
PAY18    MVI   NACPIFLG,1          ELEMENT EXISTS                               
         L     R2,NUPAYGRS         INTEGRATION                                  
         A     R2,NACPIGRS                                                      
         ST    R2,NACPIGRS                                                      
         L     R2,NUPAYNET                                                      
         A     R2,NACPINET                                                      
         ST    R2,NACPINET                                                      
         B     NEXTPEL                                                          
         DROP  R6                                                               
*                                                                               
FACXIT   CLI   NACPTFLG,0          IF ANY FLAGS SET, DONT REJECT                
         BNZ   FAC10                                                            
         CLI   NACPIFLG,0                                                       
         BNZ   FAC10                                                            
         CLI   NACBTFLG,0                                                       
         BNZ   FAC10                                                            
         CLI   NACBIFLG,0                                                       
         BNZ   FAC10                                                            
*                                  NOW CK FILTERS                               
         OC    NACSPDT,NACSPDT     IF ANY FILTER, THEN REJECT                   
         BNZ   FAC8                                                             
         OC    NACSBDT,NACSBDT                                                  
         BNZ   FAC8                                                             
         OC    NACSBTY,NACSBTY                                                  
         BNZ   FAC8                                                             
         B     FAC12                                                            
FACREJ   EQU   *                   REJECT                                       
FAC8     MVI   NACMODE,NACREJ                                                   
         B     XIT                                                              
*                                                                               
FAC10    MVI   NACMODE,NACBP       NACBP IF AUDIT TRAIL, ELSE NACEND            
         CLI   NACSMOD,C'A'                                                     
         BNE   FAC12                                                            
         B     XIT                                                              
FAC12    MVI   NACMODE,NACEND      ALWAYS END UNLESS AUDIT TRAIL                
         B     XIT                                                              
*                                                                               
********************************************************                        
         EJECT                                                                  
********************************************************                        
* ADDINTS      ROUTINE TO ADD INTERNAL RECORDS                                  
*      ADDS INTERNAL RECORD IN ARG2 TO INTERNAL RECORD IN ARG1                  
*        INPUTS :ARG1 (R4) - A(INTERNAL RECORD TO ADD INTO)                     
*                ARG2 (R5) - A(INTERNAL RECORD TO ADD FROM)                     
*                                                                               
ADDINTS  NTR1                                                                   
         L     R4,0(R1)                                                         
         L     R5,4(R1)                                                         
*                                                                               
         USING RWINBLOK,R6                                                      
         L     R6,ADRIVE1          GET A(DRIVE TABLE)                           
ADDI2    CLI   RWINTYPE,0                                                       
         BE    ADDIXIT                                                          
*                                                                               
         ST    R5,RWAIN                                                         
         ST    R4,RWAOUT                                                        
         MVI   RWFUNC,RWADD                                                     
         GOTO1 VNEWRW,DMCB,NETSYSD,(R6)                                         
*                                                                               
ADDI4    ZIC   R2,RWOUTLEN                                                      
         LA    R5,0(R5,R2)                                                      
         LA    R4,0(R4,R2)                                                      
         LA    R6,RWBLKLEN(R6)      BUMP TO NEXT DRIVE                          
         B     ADDI2                                                            
ADDIXIT  B     XIT                                                              
         DROP  R6                                                               
*********************************************************                       
         EJECT                                                                  
XIT      XIT1                                                                   
*                                                                               
         GETEL R6,NBDTADSP,SRCHEL                                               
*              LTORG ETC                                                        
         SPACE 3                                                                
*                                                                               
ZERO     DC    XL1'0F'             PACKED ZERO                                  
SPACE    DC    132CL1' '                                                        
         SPACE 2                                                                
         EJECT                                                                  
HEDLEN   EQU   10                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR W/S                                                    
         SPACE 3                                                                
*                                                                               
*                                                                               
WORKD    DSECT                                                                  
       ++INCLUDE NEACCTBLK                                                      
*                                                                               
PRDFLAG  DS    CL1                                                              
SRCHEL   DS    CL1                                                              
SAVEPER  DS    CL4                                                              
CALLRD   DS    F                                                                
WORKX    EQU   *                                                                
*                                                                               
         EJECT                                                                  
       ++INCLUDE NERWBLOCK                                                      
         EJECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
       ++INCLUDE NERWINCLS                                                      
       ++INCLUDE NERWEQUS                                                       
         EJECT                                                                  
       ++INCLUDE NEGENUNIT                                                      
         EJECT                                                                  
*                                                                               
       ++INCLUDE NETINCLS                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018NERWIN    05/01/02'                                      
         END                                                                    
