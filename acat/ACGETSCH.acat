*          DATA SET ACGETSCH   AT LEVEL 003 AS OF 05/01/02                      
*CATALP ACGETSCH                                                                
         SPACE 2                                                                
************************************************************                    
*                                                          *                    
* ACGETSCH - MAINTAIN A SCHEME BUFFER CONSISTING OF THE    *                    
*            SCHEME RECORD FOLLOWED BY THE CATEGORY RECS   *                    
*            IN REPORTING SEQUENCE.  EACH REC WILL BE      *                    
*            FOLLOWED BY ONE BYTE BINARY ZERO              *                    
*                                                          *                    
* ON ENTRY, P1  BYTES 1-3 = A(SCHEME BLOCK)                *                    
*                                                          *                    
************************************************************                    
         TITLE 'ACGETSCH - MAINTAIN A SCHEME BUFFER'                            
ACGETSCH CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETS**,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         MVC   PARMS(PARML),0(R1)                                               
         ST    RE,RELO                                                          
*                                                                               
SCHEME1  L     R8,ASCBLOCK                                                      
         USING SCBLOCKD,R8                                                      
         L     R9,SCACOM                                                        
         USING COMFACSD,R9                                                      
*                                                                               
SCHEME2  CLC   SCSCHEME,SCLSTSCH   TEST FOR CHANGE IN SCHEME                    
         BE    SCHEMEX             NO-EXIT                                      
*                                                                               
         LA    RE,SCHREC                                                        
         ST    RE,ASCHEME          SET RECORD AREA ADCONS                       
         LA    RE,CATREC                                                        
         ST    RE,ACAT                                                          
         XC    SCADCONS,SCADCONS   CLEAR ADCONS                                 
         MVC   SCLSTSCH,SCSCHEME   UPDATE SCHEME CODE                           
         BAS   RE,GETSCH           GET SCHEME HEADER                            
         BNE   SCHEMEX             DID NOT FIND IT                              
         BAS   RE,GETCAT           GET THE CATEGORIES                           
*                                                                               
SCHEMEX  XMOD1 1                                                                
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO GET THE SCHEME RECORD                           *              
* ON EXIT, CC=EQ IF SCHEME FOUND, CC=NEQ IF NOT FOUND            *              
* ROUTINE SETS ADCONS POINTING TO SCHEME RECORD AND CATEGORY     *              
* SEQUENCE ELEMENT IN BUFFER                                     *              
******************************************************************              
         SPACE 1                                                                
GETSCH   NTR1  ,                                                                
         LA    R4,KEY                                                           
         USING ACSHKEY,R4                                                       
         XC    ACSHKEY,ACSHKEY                                                  
         MVI   ACSHRTYP,ACSHEQU                                                 
         MVI   ACSHSREC,ACSHSEQU                                                
         MVC   ACSHCUL,SCCUL                                                    
         MVC   ACSHCODE,SCSCHEME                                                
         MVC   AIO,ASCHEME         SET IO ADDRESS                               
         BAS   RE,HIGH                                                          
         CLC   ACSHKEY,KEYSAVE                                                  
         BNE   NOTOK                                                            
*                                                                               
GETSCH2  L     R4,AIO              R4=A(SCHEME RECORD)                          
         L     R2,SCABUFF          R2=A(BUFFER START)                           
         LR    RE,R2               MOVE THE RECORD INTO BUFFER                  
         LR    R0,R4               SOURCE ADDRESS                               
         LH    R1,ACLENGTH                                                      
         LA    RF,1(R1)            MOVE RECORD + 1 BYTE OF ZERO                 
         MVCL  RE,R0                                                            
         ST    RE,ANEXTREC         SET BUFFER POINTER                           
*                                                                               
GETSCH4  ST    R2,SCASCH           SET SCHEME RECORD POINTER                    
         LR    R6,R2                                                            
         MVI   ELCODE,ACSCELQ      LOOK FOR SEQUENCE ELEMENT                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         ST    R6,SCASEQEL                                                      
*                                                                               
GETSCHX  B     OK                  EXIT WITH CC=EQ                              
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO GET THE CATEGORY RECORDS IN SCHEME SEQUENCE     *              
* ROUTINE SETS ADCON POINTING TO FIRST CATEGORY RECORD           *              
******************************************************************              
         SPACE 1                                                                
GETCAT   NTR1  ,                                                                
         L     R6,SCASEQEL         R6=A(SEQUENCE ELEMENT)                       
         USING ACSCD,R6                                                         
         ZIC   R5,ACSCLEN                                                       
         SH    R5,=Y(ACSCAT-ACSCD) SUBTRACT ELEMENT OVERHEAD                    
         SR    R4,R4                                                            
         LA    RF,L'ACSCAT                                                      
         DR    R4,RF               COMPUTE N'CATEGORIES                         
         LA    R6,ACSCAT-ACSCD(R6) R6=A(CATEGORY CODE)                          
*                                                                               
GETCAT2  LA    R4,KEY                                                           
         USING ACCTKEY,R4                                                       
         XC    ACCTKEY,ACCTKEY     BUILD CATEGORY'S KEY                         
         MVI   ACCTRTYP,ACCTEQU                                                 
         MVI   ACCTSREC,ACCTSEQU                                                
         MVC   ACCTCUL,SCCUL                                                    
         MVC   ACCTSCH,SCSCHEME                                                 
         MVC   ACCTCODE,0(R6)                                                   
         MVC   AIO,ACAT                                                         
         BAS   RE,HIGH                                                          
         CLC   ACCTKEY,KEYSAVE     TEST IF CATEGORY FOUND                       
         BNE   GETCAT6                                                          
*                                                                               
GETCAT4  L     RE,ANEXTREC         RE=A(NEXT RECORD POSITION)                   
         OC    SCAFSTCT,SCAFSTCT   TEST IF FIRST CATEGORY                       
         BNZ   *+8                 NO                                           
         ST    RE,SCAFSTCT                                                      
         L     R4,AIO                                                           
         LR    R0,R4               SOURCE                                       
         LH    R1,ACLENGTH                                                      
         LA    RF,1(R1)            MOVE RECORD + 1 BYTE ZERO                    
         MVCL  RE,R0                                                            
         ST    RE,ANEXTREC         SET NEW BUFFER POINTER                       
*                                                                               
GETCAT6  LA    R6,L'ACSCAT(R6)                                                  
         BCT   R5,GETCAT2                                                       
         MVI   0(RE),X'00'                                                      
*                                                                               
GETCATX  B     EXIT                                                             
         EJECT                                                                  
OK       CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
NOTOK    LTR   RB,RB                                                            
         B     EXIT                                                             
         SPACE 2                                                                
         GETEL (R6),49,ELCODE                                                   
         SPACE 2                                                                
* DATA MANAGER INTERFACE                                                        
*                                                                               
READ     NTR1  ,                                                                
         MVC   COMMAND,DMREAD                                                   
         B     ACCFILE                                                          
         SPACE 1                                                                
SEQ      NTR1  ,                                                                
SEQSKIP  MVC   COMMAND,DMRSEQ                                                   
         OI    DMINBTS,X'08'       PASS DELETES                                 
         B     ACCFILE                                                          
         SPACE 1                                                                
HIGH     NTR1  ,                                                                
         MVC   COMMAND,DMRDHI                                                   
         SPACE 1                                                                
ACCFILE  MVC   KEYSAVE,KEY                                                      
         GOTO1 CDATAMGR,DMCB,(DMINBTS,COMMAND),ACCOUNT,KEY,AIO,0                
         L     RE,AIO              EXTRACT KEY FROM RECORD                      
         MVC   KEY,0(RE)                                                        
         CLC   COMMAND,DMREAD      TEST FOR READ                                
         BE    ACCFILE2            YES                                          
         TM    ACSTATUS-ACKEYD(RE),X'80' TEST FOR DELETED RECORD                
         BO    SEQSKIP             SKIP DELETED RECORD                          
         SPACE 1                                                                
ACCFILE2 NI    DMINBTS,X'FF'-X'08' TURN OFF PASS DELETES                        
         TM    DMCB+8,X'12'                                                     
         BO    NOTOK               RECORD NOT FOUND/DELETED                     
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     OK                                                               
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
ACCOUNT  DC    C'ACCOUNT'                                                       
DMRDHI   DC    C'DMRDHI'                                                        
DMREAD   DC    C'DMREAD'                                                        
DMRSEQ   DC    C'DMRSEQ'                                                        
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* DSECT TO COVER ACGETTXT WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
APARM    DS    A                                                                
PARMS    DS    0A                                                               
ASCBLOCK DS    A                                                                
PARML    EQU   *-PARMS                                                          
SAVERE   DS    A                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
ELCODE   DS    X                                                                
DMINBTS  DS    X                                                                
COMMAND  DS    CL6                                                              
*                                                                               
KEY      DS    CL(ACSTATUS-ACKEYD)                                              
KEYSAVE  DS    CL(L'KEY)                                                        
*                                                                               
AIO      DS    A                                                                
ASCHEME  DS    A                                                                
ACAT     DS    A                                                                
ANEXTREC DS    A                                                                
*                                                                               
SCHREC   DS    CL2000                                                           
CATREC   DS    CL2000                                                           
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* EQUATES                                                                       
*                                                                               
         SPACE 2                                                                
SCBLOCKD DSECT                                                                  
       ++INCLUDE ACSCBLOCK                                                      
         SPACE 2                                                                
* ACGENBOTH                                                                     
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACGETSCH  05/01/02'                                      
         END                                                                    
