*          DATA SET RETIMVALS  AT LEVEL 009 AS OF 05/01/02                      
*CATALP RETIMVAL                                                                
         TITLE 'RETIMVAL - REP MODULE TO VALIDATE TIME INPUT'                   
******************************************************************              
*                                                                *              
*       RETIMVAL --- REP VALIDATE TIME INPUT (BROADCAST DAY      *              
*                     IS 500AM TO 459AM                          *              
*                                                                *              
* -------------------------------------------------------------- *              
* UPDATE HISTORY:                                                *              
*                                                                *              
* FEB08/91 (MRR) --- INITIAL RELEASE (TAKEN FROM DDTIMVAL)       *              
*                                                                *              
* OCT21/93 (SKU) --- DISALLOW 12A AND 12P TIME ENTRIES           *              
*                                                                *              
* MAY24/95 (SKU) --- FORCE PM IF MIDNIGHT IS CROSSED AND         *              
*                    FIRST TIME FIELD DOES NOT SPECIFY AM OR PM  *              
*                                                                *              
******************************************************************              
         PRINT NOGEN                                                            
RETIMVAL CSECT                                                                  
         NMOD1 TIMDLEN,*RETMVL*                                                 
         USING TIMD,RC                                                          
         LM    R2,R3,0(R1)         PARAMETER 1 BYTE 0   L'INPUT                 
         SR    R4,R4                                1-3 A(INPUT)                
         IC    R4,0(R1)                      2          A(OUTPUT)               
         SLL   R2,8                                                             
         SRL   R2,8                                                             
         SPACE 2                                                                
*                                  AFTER TIMVAL P1 BYTE 0 X'FF' INVALID         
         EJECT                                                                  
*              ESTABLISH LENGTH OF FIELDS                                       
         SPACE 3                                                                
         CLC   0(4,R2),=C'NONE'    SPECIAL NONE CONDITION                       
         BNE   TM20                                                             
         MVC   0(4,R3),=C'NONE'                                                 
TM10     MVI   0(R1),0                                                          
         B     TMEXT                                                            
         SPACE 2                                                                
TM20     CLC   0(3,R2),=C'VAR'                                                  
         BNE   TM30                                                             
         MVC   0(4,R3),=C'VARY'                                                 
         B     TM10                                                             
         SPACE 2                                                                
TM30     LR    R5,R2                                                            
         LR    R6,R4                                                            
         SPACE 2                                                                
TM40     CLI   0(R5),C'-'          LOOK FOR A DASH                              
         BE    TM60                                                             
         LA    R5,1(R5)                                                         
         BCT   R6,TM40                                                          
         SR    R5,R5               NOT THERE - SO ONLY ONE TIME                 
         B     TM80                                                             
         SPACE 2                                                                
TM60     SR    R5,R2                                                            
         SR    R4,R5                                                            
         BCTR  R4,R0                                                            
         XR    R4,R5                                                            
         XR    R5,R4                                                            
         XR    R4,R5                                                            
         LTR   R5,R5                                                            
         BNP   TMERR                                                            
         SPACE 2                                                                
TM80     CH    R4,=H'1'                                                         
         BL    TMERR                                                            
         CH    R4,=H'5'                                                         
         BH    TMERR                                                            
         CH    R5,=H'5'                                                         
         BH    TMERR                                                            
         EJECT                                                                  
*              NOW ESTABLISH FIXED LENGTH FIELDS                                
         SPACE 3                                                                
         MVC   TIME1(10),=10C'0'                                                
         LA    R6,TIME1+5          FIRST FIELD                                  
         SR    R6,R4                                                            
         BCTR  R4,0                                                             
         EX    R4,TM100                                                         
*                                                                               
         BAS   RE,NORM             NOON OR MID                                  
         BNE   *+10                                                             
         MVC   TIME1(5),DUB                                                     
*                                                                               
         CLI   LET1,C'Z'                                                        
         BL    *+10                                                             
         MVC   TIME1,TIME1+1                                                    
         CLC   TIME1(2),=C'00'                                                  
         BNE   TM110                                                            
         MVC   TIME1(2),TIME1+2                                                 
         MVC   TIME1+2(2),=C'00'                                                
         B     TM110                                                            
         SPACE 2                                                                
TM100    MVC   0(0,R6),0(R2)                                                    
         SPACE 1                                                                
TM110    LTR   R5,R5                                                            
         BZ    TM120                                                            
         LA    R2,2(R2,R4)         SECOND FIELD                                 
         LA    R6,TIME2+5                                                       
         SR    R6,R5                                                            
         BCTR  R5,0                                                             
         EX    R5,TM100                                                         
*                                                                               
         BAS   RE,NORM             NOON OR MID                                  
         BNE   *+10                                                             
         MVC   TIME2(5),DUB                                                     
*                                                                               
         CLC   TIME2+3(2),=C'CC'                                                
         BE    TM120                                                            
         CLC   TIME2(2),=C'00'                                                  
         BNE   TM120                                                            
         MVC   TIME2(2),TIME2+2                                                 
         MVC   TIME2+2(2),=C'00'                                                
         SPACE 2                                                                
TM120    CLI   LET1,C'Z'           IS THE LETTER OMITTED                        
         BL    TM140               FROM THE FIRST FIELD                         
         MVC   LET1,LET2           ASSUME SECOND VALUE                          
         CLI   LET1,C'N'                                                        
         BNE   *+8                                                              
         MVI   LET1,C'A'           NOON BECOMES AM                              
         CLI   LET1,C'M'                                                        
         BNE   *+8                                                              
         MVI   LET1,C'P'           MIDNIGHT BECOMES PM                          
         SPACE 1                                                                
         CLI   LET2,C'A'           IF SECOND AM                                 
         BNE   TM135                                                            
         CLC   =C'12',TIME2        IF WE JUST CROSSED MIDNIGHT                  
         BNE   TM130                                                            
         CLC   =C'12',TIME1        AND FIRST TIME IS BEFORE MIDNIGHT            
         BE    TM140                                                            
         MVI   LET1,C'P'           FORCE FIRST TIME FIELD TO PM                 
         B     TM140                                                            
                                                                                
TM130    DS    0H                                                               
         CLC   TIME1,TIME2                                                      
         BNH   TM140               AND FIRST IS HIGH                            
         MVI   LET1,C'P'           MAKE IT PM                                   
         CLC   TIME1,=C'1200'                                                   
         BL    TM140                                                            
         MVI   LET1,C'M'                                                        
         CLC   TIME1,=C'1200'                                                   
         BE    TM140                                                            
         MVI   LET1,C'A'                                                        
         B     TM140                                                            
         EJECT                                                                  
TM135    DS    0H                                                               
         CLI   LET2,C'P'           IF SECOND PM                                 
         BNE   TM140                                                            
         CLC   =C'12',TIME2        IF WE JUST CROSSED NOON                      
         BNE   TM138                                                            
         CLC   =C'12',TIME1        AND FIRST TIME IS BEFORE NOON                
         BE    TM140                                                            
         MVI   LET1,C'A'           FORCE FIRST TIME FIELD TO AM                 
         B     TM140                                                            
                                                                                
TM138    DS    0H                                                               
         CLC   TIME1,TIME2                                                      
         BNH   TM140               AND FIRST IS HIGH                            
         MVI   LET1,C'A'           MAKE IT AM                                   
         CLC   TIME1,=C'1200'                                                   
         BL    TM140                                                            
         MVI   LET1,C'N'                                                        
         CLC   TIME1,=C'1200'                                                   
         BE    TM140                                                            
         MVI   LET1,C'P'                                                        
*              BIT OF LOGIC ON THE FIELDS                                       
         SPACE 3                                                                
TM140    LA    R5,TIME1            CHECK FIRST TIME                             
         LA    R6,MIL1                                                          
         BAS   RE,VALTIM                                                        
         XC    MIL2,MIL2                                                        
         CLC   TIME2,=10C'0'       IF SECOND IS MISSING WERE DONE               
         BE    TMOK                                                             
         CLC   TIME2+3(2),=C'CC'                                                
         BNE   *+14                                                             
         MVC   MIL2,=C'CC'                                                      
         B     TMOK                                                             
         LA    R5,TIME2            CHECK SECOND TIME                            
         LA    R6,MIL2                                                          
         BAS   RE,VALTIM                                                        
         CLC   MIL2,MIL1           IF SECOND TIME IS LESS THAN FIRST            
         BH    TMOK                                                             
         CLC   MIL2,=H'500'        THEN IF MUST BE BEFORE 5AM                   
         BL    TMOK                                                             
         B     TMERR                                                            
         EJECT                                                                  
NORM     LA    RF,1                CHECK FOR NOON OR MID INPUT                  
         CLC   0(3,R2),=C'NOO'                                                  
         BE    *+10                                                             
         CLC   0(3,R2),=C'MID'                                                  
         BNE   NORM2                                                            
*                                                                               
         SR    RF,RF               FOR NOON OR MID                              
         MVC   DUB(4),=C'1200'     SET TIME IN DUB                              
         MVC   DUB+4(1),0(R2)                                                   
*                                                                               
NORM2    LTR   RF,RF                                                            
         BR    RE                                                               
         EJECT                                                                  
*              CHECK TIMES AND CONVERT TO MILITARY                              
         SPACE 3                                                                
VALTIM   CLC   0(2,R5),=C'00'      9 BECOMES 900, 10 BECOMES 1000               
         BNE   TM200                                                            
         MVC   0(2,R5),2(R5)                                                    
         MVC   2(2,R5),=C'00'                                                   
         SPACE 2                                                                
TM200    MVC   0(2,R6),=H'1200'    NOON AND MIDNIGHT CHECK                      
         CLC   0(5,R5),=C'1200N'                                                
         BER   RE                                                               
         CLC   0(5,R5),=C'1200A'   FORBID THIS ENTRY                            
         BE    TMERR                                                            
         MVC   0(2,R6),=H'2400'                                                 
         CLC   0(5,R5),=C'1200M'                                                
         BER   RE                                                               
         CLC   0(5,R5),=C'1200P'   FORBID THIS ENTRY                            
         BE    TMERR                                                            
         SPACE 2                                                                
         MVC   DUB(4),=10C'0'      NUMERIC CHECK                                
         MVZ   DUB(4),0(R5)                                                     
         CLC   DUB(4),=10C'0'                                                   
         BNE   TMERR                                                            
         SPACE 2                                                                
         CLC   0(2,R5),=C'01'      HOURS TESTS                                  
         BL    TMERR                                                            
         CLC   0(2,R5),=C'12'                                                   
         BH    TMERR                                                            
         SPACE 2                                                                
         CLC   2(2,R5),=C'59'      MINUTES TEST                                 
         BH    TMERR                                                            
         SPACE 2                                                                
         PACK  DUB,0(4,R5)         CONVERT TO CALENDAR                          
         CVB   R0,DUB                                                           
         STH   R0,0(R6)                                                         
         CLC   0(2,R5),=C'12'                                                   
         BE    TM220                                                            
         CLI   4(R5),C'A'          MORNING NOW OK                               
         BCR   8,RE                                                             
         AH    R0,=H'1200'                                                      
         STH   R0,0(R6)                                                         
         CLI   4(R5),C'P'          ADD 1200 FOR PM TIMES                        
         BCR   8,RE                                                             
         B     TMERR                                                            
         SPACE 2                                                                
TM220    CLI   4(R5),C'P'          BUT FOR TIMES THAT START WITH                
         BCR   8,RE                12, THINGS ARE A LITTLE DIFFERENT            
         SH    R0,=H'1200'                                                      
         STH   R0,0(R6)                                                         
         CLI   4(R5),C'A'                                                       
         BCR   8,RE                                                             
         SPACE 2                                                                
TMERR    XC    0(4,R3),0(R3)                                                    
         MVI   0(R1),X'FF'                                                      
         B     TMEXT                                                            
         SPACE 2                                                                
TMOK     MVC   0(4,R3),MIL1                                                     
         MVI   0(R1),0                                                          
         SPACE 2                                                                
TMEXT    XMOD1 1                                                                
         EJECT                                                                  
*              DSECT FOR TIMVAL                                                 
         SPACE 3                                                                
TIMD     DSECT                                                                  
TIMDST   EQU   *                                                                
DUB      DS    D                                                                
TIME1    DS    CL4                                                              
LET1     DS    CL1                                                              
TIME2    DS    CL4                                                              
LET2     DS    CL1                                                              
MIL1     DS    CL2                                                              
MIL2     DS    CL2                                                              
TIMDEND  EQU   *                                                                
TIMDLEN  EQU   TIMDEND-TIMDST                                                   
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009RETIMVALS 05/01/02'                                      
         END                                                                    
