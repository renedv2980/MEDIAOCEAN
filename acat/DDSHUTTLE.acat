*          DATA SET DDSHUTTLE  AT LEVEL 005 AS OF 05/01/02                      
*          DATA SET DRIVOUT    AT LEVEL 020 AS OF 07/11/85                      
*CATALP DRIVOUT                                                                 
         TITLE 'DRIVOUT - OUTPUT HANDLER FOR DRIVER'                            
DRIVOUT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 200,**DROUT*,R9,R7                                               
         USING DOUTD,RC                                                         
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         CLI   GLANYERR,C'Y'       DON'T HANDLE IF ANY ERRORS EARLIER           
         BE    XIT                                                              
         BAS   RE,OUTINIT                                                       
         EJECT                                                                  
*              MAIN I/O AND PROGRAM FLOW                                        
         SPACE 3                                                                
         L     R5,GLAIO            R5=A(THIS RECORD)                            
         MVI   LASTTYPE,0                                                       
         MVI   CBLEV,0                                                          
         SPACE 1                                                                
OUT      GOTO1 =V(DRIVSORT),DMCB,(RA)                                           
         CLI   8(R1),X'80'         EOF HANDLING                                 
         BNE   OUT2                                                             
         MVI   CBLEV,0                                                          
         BAS   RE,MULTLT                                                        
         B     XIT                                                              
         SPACE 1                                                                
OUT2     BAS   RE,TRASOME                                                       
         CLI   LASTTYPE,2          LAST RECORD WAS A DETAIL                     
         BL    OUT4                                                             
         BAS   RE,SETCB            SET CONTROL BREAK LEVEL                      
         BAS   RE,MULTLT           DEAL WITH LAST TIMES                         
         SPACE 1                                                                
OUT4     MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         ZIC   R2,GLRECNO          FROM RECORD NUMBER                           
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         LH    R1,GLRECLEN                                                      
         ZIC   R6,GLLEVEL          AND LEVEL NUMBER                             
         SLL   R6,2                                                             
         LA    R6,GLARECL0(R6)                                                  
         L     R6,0(R6)            FIGURE OUT WHERE TO POST                     
         ST    R6,AACTREC                                                       
         MOVE  ((R6),(R1)),(R5)    AND DO IT                                    
         MVI   LASTTYPE,1                                                       
         CLC   GLLEVEL,GLDETLEV    IS THIS A DETAIL                             
         BNE   OUT                                                              
         MVI   LASTTYPE,2          YES                                          
         MVC   LASTDET,0(R5)       SAVE PRESENT KEY ETC                         
         BAS   RE,MULTFT           HANDLE ANY FIRST TIME ROUTINES               
         BAS   RE,DETAIL           AND THEN DO THE DETAILS                      
         B     OUT                                                              
         EJECT                                                                  
*              INITIALIZATION FOR OUT ROUTINES                                  
         SPACE 3                                                                
OUTINIT  NTR1                                                                   
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(197),SPACES                                             
         MVC   LASTDET,SPACES                                                   
         SPACE 1                                                                
         L     R2,GLAHEDHK         INTERCEPT SO WE GET                          
         LA    R1,HOOK             THE HEAD HOOK                                
         ST    R1,0(R2)                                                         
         B     XIT                                                              
         EJECT                                                                  
*              SET CONTROL BREAK                                                
         SPACE 3                                                                
*                                  R5=A(RECORD COMING FROM SORT)                
*                                  LASTDET=KEY OF PREVIOUS DETAIL               
         SPACE 1                                                                
SETCB    NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         CLI   GLRECLEV,0          UNLESS RECORD IS IMBEDDED                    
         BNE   SCB2                                                             
         MVI   CBLEV,0                                                          
         CLC   0(1,R5),LASTDET     TEST CHANGE OF RECORD                        
         BNE   XIT                                                              
         SPACE 1                                                                
SCB2     LA    R3,GLLCBS           NOW SET UP TO TEST OTHER LEVELS              
         LA    R4,1                R4=LEVEL NUMBER                              
         AH    R5,GLCONLEN                                                      
         LA    R6,LASTDET                                                       
         AH    R6,GLCONLEN                                                      
         SPACE 1                                                                
SCB4     STC   R4,CBLEV                                                         
         ZIC   R1,0(R3)            PICK UP CUMULATIVE LENGTH                    
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),0(R6)       LOOK FOR A CHANGE AT THIS LEVEL              
         BNE   XIT                 FOUND                                        
         LA    R3,1(R3)            GO AND TRY NEXT LOWER LEVEL                  
         LA    R4,1(R4)                                                         
         B     SCB4                                                             
         EJECT                                                                  
*              CONTROL MULTIPLE FIRST TIME ROUTINES                             
         SPACE 3                                                                
MULTFT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         MVC   GLLEVEL,CBLEV       START FROM LOWEST UP TO DETAIL               
         SPACE 1                                                                
MULTFT2  CLC   GLLEVEL,GLDETLEV    DON'T NEED FT AT DETAIL LEVEL                
         BE    XIT                                                              
         BAS   RE,SINGFT           HANDLE FT ROUTINES AT THIS LEVEL             
         AI    GLLEVEL,1                                                        
         B     MULTFT2                                                          
         EJECT                                                                  
*              CONTROL MULTIPLE LAST TIME ROUTINES                              
         SPACE 3                                                                
MULTLT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         CLC   CBLEV,GLDETLEV      DON'T NEED LT ON DETAIL                      
         BE    XIT                                                              
         ZIC   R1,GLDETLEV         SO PROCESS FROM DETAIL-1 LEVEL               
         BCTR  R1,0                TO CBLEV SETTING BACKWARDS                   
         SPACE 1                                                                
MULTLT2  STC   R1,GLLEVEL                                                       
         BAS   RE,SINGLT           HANDLE LT ROUTINES AT THIS LEVEL             
         CLC   GLLEVEL,CBLEV                                                    
         BE    XIT                                                              
         BCTR  R1,0                                                             
         B     MULTLT2                                                          
         EJECT                                                                  
*              CONTROL FIRST, LAST AND TOTAL ROUTINES                           
         SPACE 3                                                                
SINGFT   NTR1                                                                   
         MVI   BREAK,C'F'                                                       
         MVI   GLHOOK,GLFIRST                                                   
         B     SING2                                                            
         SPACE 1                                                                
SINGLT   NTR1                                                                   
         MVI   BREAK,C'L'                                                       
         MVI   GLHOOK,GLLAST                                                    
         SPACE 1                                                                
SING2    XC    GLARGS,GLARGS                                                    
         MVC   GLARGS(1),GLLEVEL                                                
         BAS   RE,SETAOUT          PICK UP A(THIS OUT) IF ANY                   
         MVC   GLADTENT,ALEVOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           HOOK TO APPLICATION FOR FIRST/LAST           
         OC    ALEVOUT,ALEVOUT                                                  
         BZ    XIT                 (NONE AT THIS LEVEL)                         
         USING DROD,R3                                                          
         L     R3,ALEVOUT                                                       
         CLI   DROFLFOL,0          ANY FIRST/LAST SPECIFIED?                    
         BE    SINGTOT                                                          
         MVI   ELCODE,X'44'        YES SO GO AND LOOK FOR THEM                  
         ZIC   R0,DROFLFOL                                                      
         BAS   RE,EXFLT                                                         
         SPACE 1                                                                
SINGTOT  CLI   BREAK,C'L'          IF LAST TIME ROUTINE                         
         BNE   XIT                                                              
         CLI   DROTLFOL,0          ARE THERE ANY TOTALS SPECIFIED?              
         BE    XIT                                                              
         BAS   RE,TOTALS           YES SO DO THAT                               
         MVI   ELCODE,X'48'                                                     
         ZIC   R0,DROTLFOL                                                      
         BAS   RE,EXFLT            THEN EXECUTE ANY CONTROLS                    
         CLI   PRINTED,C'Y'        WERE TOTALS PRINTED?                         
         BE    XIT                                                              
         MVC   NLINES,GLSPACE      NO SO DO IT NOW                              
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              EXECUTE FIRST, LAST OR TOTAL ELEMENT                             
         SPACE 3                                                                
EXFLT    NTR1                                                                   
*                                  R0=NUMBER OF ELEMENTS TO COME                
*                                  ELCODE = X'44' OR X'48'                      
*                                  BREAK = C'F' OR 'L'                          
*                                  R3=A(OUT)                                    
         MVI   PRINTED,C'N'                                                     
         SPACE 1                                                                
EXFLT2   ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLC   0(1,R3),ELCODE      LOOK FOR MATCH ON ELEMENT CODE               
         BNE   EXFLT2                                                           
         USING DRFLD,R3                                                         
         CLI   ELCODE,X'48'                                                     
         BE    EXFLT4                                                           
         CLC   DRFLTYPE,BREAK      AND FIRST/LAST                               
         BNE   EXFLTEND                                                         
         SPACE 1                                                                
EXFLT4   DS    0H                  CHECK ANY CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRFLIFS                                   
         BNE   EXFLTEND                                                         
         CLI   DRFLLTYP,C'M'       IF MID LINES ACTIVE                          
         BNE   *+12                FORCE THEM TO PRINT                          
         L     R1,GLAFMID                                                       
         MVI   0(R1),C'Y'                                                       
         SPACE 1                                                                
         OC    DRFLRADD,DRFLRADD   ANY ROUTINE TO HANDLE?                       
         BZ    EXFLT6                                                           
         MVC   GLAROUT,DRFLRADD                                                 
         MVC   GLLABEL,DRFLROUT                                                 
         MVC   GLARGS,DRFLARGS                                                  
         XC    GLAIFLD,GLAIFLD                                                  
         MVC   GLAOFLD,DRFLAPOS                                                 
         ST    R3,GLADTENT                                                      
         MVI   GLHOOK,GLROUT                                                    
         BAS   RE,GOHOOK                                                        
         SPACE 1                                                                
EXFLT6   CLI   DRFLELEN,60         ANY LITERALS FOR US TO HANDLE                
         BE    EXFLT8                                                           
         ZIC   R1,DRFLELEN         YES                                          
         SH    R1,=H'60'                                                        
         STC   R1,LITLEN           SET LENGTH                                   
         LA    R1,DRFLLIT                                                       
         ST    R1,ALITIN           ADDRESS                                      
         MVC   ALITOUT,DRFLAPOS    AND WHERE IT SHOULD GO                       
         BAS   RE,DOLIT                                                         
         SPACE 1                                                                
EXFLT8   CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT10                                                          
         CLI   BREAK,C'L'          ONLY LAST GETS HANDLED HERE                  
         BNE   EXFLT10                                                          
         MVI   PRINTED,C'Y'        YES - PASS BACK INFO                         
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
         SPACE 1                                                                
EXFLT10  TM    DRFLOPT,X'80'       OPTION TO SKIP NEXT TIME                     
         BNO   EXFLT12                                                          
         L     R1,GLAFHEAD         YES SO SET FORCEHED                          
         LTR   R1,R1                                                            
         BZ    EXFLT12                                                          
         MVI   0(R1),C'Y'                                                       
         SPACE 1                                                                
EXFLT12  TM    DRFLOPT,X'40'       OPTION TO RESET PAGE NUMBER                  
         BNO   EXFLT14                                                          
         L     R1,GLAPAGE          YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT14                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT14  TM    DRFLOPT,X'20'       OPTION TO RESET SUBPAGE                      
         BNO   EXFLT16                                                          
         L     R1,GLASPAGE         YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT16                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT16  CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT18                                                          
         CLI   BREAK,C'L'          DEALING WITH LAST HERE                       
         BE    EXFLT18                                                          
         MVI   PRINTED,C'Y'        YES - PASS BACK INFO                         
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
         SPACE 1                                                                
EXFLT18  DS    0H                                                               
         SPACE 1                                                                
EXFLTEND BCT   R0,EXFLT2                                                        
         B     XIT                                                              
         EJECT                                                                  
*              SET ADDRESS OF OUT AT SPECIFIED LEVEL                            
         SPACE 3                                                                
SETAOUT  NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT                                                       
         USING DROD,R3                                                          
         SPACE 1                                                                
SAO2     CLI   0(R3),0             NO GOOD IF 0 OR X'10'                        
         BE    SAOMISS                                                          
         CLI   0(R3),X'10'                                                      
         BE    SAOMISS                                                          
         CLI   0(R3),X'30'         WE NEED AN X'30' AT REQUIRED LEVEL           
         BNE   SAONEXT                                                          
         CLC   DROLEV,GLLEVEL                                                   
         BNE   SAONEXT                                                          
         ST    R3,ALEVOUT          FOUND                                        
         B     YES                                                              
         SPACE 1                                                                
SAONEXT  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     SAO2                                                             
         SPACE 1                                                                
SAOMISS  XC    ALEVOUT,ALEVOUT                                                  
         B     NO                                                               
         EJECT                                                                  
*              OVERALL CONTROL OF DETAIL AND TOTALS                             
         SPACE 3                                                                
DETAIL   NTR1                                                                   
         MVI   LINETYPE,C'D'                                                    
         B     DET2                                                             
         SPACE 1                                                                
TOTALS   NTR1                                                                   
         MVI   LINETYPE,C'T'                                                    
         SPACE 1                                                                
DET2     L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          PICK UP ADDRESS OF FIRST OUT                 
         USING DROD,R3                                                          
         ZIC   R1,GLLEVEL                                                       
         CLI   LINETYPE,C'D'                                                    
         BNE   *+8                                                              
         IC    R1,GLDETLEV                                                      
         SLL   R1,2                                                             
         LA    R1,GLARECL0(R1)                                                  
         L     R1,0(R1)            AND A(RECORD AT THIS LEVEL)                  
         ST    R1,ATHISREC                                                      
         DROP  R2                                                               
         SPACE 1                                                                
*                                  CHECK THIS OUT FOR CONDITIONALS              
DET4     GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DETEND                                                           
         CLI   DROLTYP,C'H'        DON'T BOTHER WITH HEADLINES HERE             
         BE    XIT                                                              
         ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,120         DO WE NEED TO HANDLE A LITERAL               
         BE    DET6                                                             
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'120'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
DET6     ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    DET8                                                             
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,ATHISREC                                                      
         ST    R1,GLAIFLD                                                       
         SPACE 1                                                                
DET8     OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    DET10                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLEDIT       DID USE WANT US TO EDIT                      
         BNE   DETEND                                                           
         SPACE 1                                                                
DET10    BAS   RE,FORM             OTHERWISE DRIVER WILL DEAL WITH IT           
         SPACE 1                                                                
DETEND   ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'30'                                                      
         BE    DET4                                                             
         CLI   0(R3),0                                                          
         BE    DETEND2                                                          
         CLI   0(R3),X'10'                                                      
         BNE   DETEND                                                           
         SPACE 1                                                                
DETEND2  CLI   LINETYPE,C'D'       DETAILS WILL NOW PRINT                       
         BNE   XIT                                                              
         BAS   RE,SUPPRESS         CHECK FOR SUPPRESSION FROM LEFT              
         MVC   NLINES,GLSPACE                                                   
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK AGAINST PREVIOUS DETAIL LINE                    
*              UNNECESSARY DETAILS IN KEY WILL BE SUPPRESSED                    
         SPACE 3                                                                
*                                  R2=A(GLINTD)                                 
SUPPRESS NTR1                                                                   
         L     R3,AMYP1            R3=A(MY FIRST DETAIL LINE)                   
         LA    R4,GLPDCBS          R4=A(LIST OF PRINT DETAIL WIDTHS)            
         MVC   SAVEDET,0(R3)       TUCK AWAY PRESENT DETAIL LINE                
         LA    R0,12                                                            
         SPACE 1                                                                
SUP2     CLI   0(R4),0             ANY DETAIL AT THIS LEVEL                     
         BE    SUP4                                                             
         ZIC   R1,0(R4)            YES SO PICK UP PRINT WIDTH                   
         BCTR  R1,0                -1 FOR EX                                    
         AH    R1,GLPDISP          ADD IN DISPLACEMENT FROM LEFT SIDE           
         EX    R1,SUPCOMP          CHECK IF IT'S ALL THE SAME AS BEFORE         
         BNE   SUPEND                                                           
         EX    R1,SUPCLEAR         YES SO CLEAR IN MY PRINT LINE                
         SPACE 1                                                                
SUP4     LA    R4,1(R4)                                                         
         BCT   R0,SUP2                                                          
         SPACE 1                                                                
SUPEND   MVC   LASTDET,SAVEDET                                                  
         B     XIT                                                              
         SPACE 1                                                                
SUPCOMP  CLC   SAVEDET(0),LASTDET                                               
SUPCLEAR MVC   0(0,R3),SPACES                                                   
         EJECT                                                                  
*              CONTROL DRIVER FORMATTING                                        
         SPACE 3                                                                
*                                  R2=A(ASSOCIATED IN)                          
*                                  R3=A(OUT)                                    
*                                  R4=A(INPUT FIELD)                            
*                                  R5=A(PRINT POSITION)                         
FORM     NTR1                                                                   
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    FORM2                                                            
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         B     FORMNUM2                                                         
         SPACE 1                                                                
FORM2    L     R2,DROIADD                                                       
         USING DRIND,R2                                                         
         LTR   R2,R2                                                            
         BZ    XIT                 (NO INPUT FIELD TO HANDLE)                   
         L     R4,GLAIFLD                                                       
         ZIC   R1,DRINLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R4),0(R4)       ANY SIGNIFICANT DATA?                        
         BZ    XIT                                                              
         CLI   DRINTYPE,C'C'       PICK INPUT TYPE                              
         BE    CHAR                                                             
         CLI   DRINTYPE,C'D'                                                    
         BE    DATE                                                             
         CLI   DRINTYPE,C'X'                                                    
         BE    HEX                                                              
         B     FORMNUM                                                          
         EJECT                                                                  
*              CHARACTER ROUTINES                                               
         SPACE 3                                                                
CHAR     CLI   DROTYPE,C'C'        PICK OUTPUT TYPE                             
         BE    CTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    CTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  CHARACTER TO CHARACTER ROUTINES              
CTOC     CLC   DRINLEN,DROLEN      INPUT LENGTH NOT GREATER THAN                
         BH    CTOC2               OUTPUT LENGTH                                
         EX    R1,*+8                                                           
         B     ALIGN                                                            
         MVC   0(0,R5),0(R4)       SO MOVE WHAT WE HAVE                         
         SPACE 1                                                                
*                                  INPUT IS GREATER THAN OUTPUT                 
CTOC2    TM    DROFORM,X'10'       IS CHOPPING ALLOWED                          
         BE    CTOC4                                                            
         ZIC   R1,DROLEN           NO SO USE OUTPUT LENGTH                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   0(0,R5),0(R4)       AND TRUNCATE                                 
         SPACE 1                                                                
CTOC4    ST    R4,DMCB             CHOPPING HERE                                
         MVC   DMCB(1),DRINLEN                                                  
         ST    R5,DMCB+4                                                        
         MVC   DMCB+4(1),DROLEN                                                 
         MVC   DMCB+8(4),=F'20'                                                 
         MVI   DMCB+8,198                                                       
         GOTO1 CHOPPER,DMCB                                                     
         B     XIT                                                              
         SPACE 1                                                                
CTOX     ZIC   R1,DROLEN           CHARACTER TO HEX                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,R5),0(R5)       CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SLL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN TWICE THE OUTPUT LENGTH            
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXIN,DMCB                                                       
         B     XIT                                                              
         EJECT                                                                  
*              DATE AND HEX ROUTINES                                            
         SPACE 3                                                                
DATE     STM   R4,R5,DMCB                                                       
         MVC   DMCB(1),DRINTYPE+1                                               
         MVC   DMCB+4(1),DROTYPE+1                                              
         GOTO1 DATCON,DMCB                                                      
         B     XIT                                                              
         SPACE 1                                                                
HEX      CLI   DROTYPE,C'C'                                                     
         BE    XTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    XTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  HEX TO HEX ROUTINES                          
XTOX     CLC   DRINLEN,DROLEN      CHECK INPUT NOT TOO LONG                     
         BH    XTOX2                                                            
         ZIC   R1,DROLEN                                                        
         BCTR  R1,0                                                             
         SPACE 1                                                                
XTOX2    EX    R1,*+8                                                           
         B     ALIGN                                                            
         MVC   0(0,R5),0(R4)       SO MOVE WHAT WE HAVE                         
         SPACE 1                                                                
XTOC     ZIC   R1,DROLEN           HEX TO CHARACTER                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPACES      CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SRL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN HALF THE OUTPUT LENGTH             
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXIN,DMCB,,,,=C'TOG'                                            
         B     XIT                                                              
         EJECT                                                                  
*              ALIGNMENT AND UNDERLINE ROUTINES                                 
         SPACE 3                                                                
ALIGN    CLI   DROALIGN,C'R'       CHECK SPECIAL ALIGNMENT                      
         BE    RALIGN                                                           
         CLI   DROALIGN,C'C'                                                    
         BE    CALIGN                                                           
         B     ANYUNDER                                                         
         SPACE 1                                                                
RALIGN   L     RF,RIGHT                                                         
         B     ALIGNALL                                                         
         SPACE 1                                                                
CALIGN   L     RF,CENTER                                                        
         SPACE 1                                                                
ALIGNALL ST    R5,DMCB             A(OUTPUT)                                    
         ZIC   R1,DROLEN           L'OUTPUT                                     
         ST    R1,DMCB+4                                                        
         GOTO1 (RF),DMCB           GO TO RIGHT OR CENTER                        
         SPACE 1                                                                
ANYUNDER CLI   DROUNDER,0          ANY UNDERLINING REQUIRED?                    
         BE    XIT                                                              
         ST    R5,DMCB             A('INPUT' FIELD)                             
         MVC   DMCB(1),DROLEN      L'INPUT                                      
         LA    R1,198(R1)                                                       
         ST    R1,DMCB+4           A(LINE BELOW)                                
         MVC   DMCB+4(1),DROUNDER  THIS IS THE UNDERLINE CHARACTER              
         GOTO1 UNDERLIN,DMCB                                                    
         B     XIT                                                              
         EJECT                                                                  
*              OUTPUT NUMERIC                                                   
         SPACE 3                                                                
FORMNUM  XC    EBLOCK,EBLOCK       GENERAL NUMERIC                              
         ST    R4,EBAIN            R4=A(INPUT)                                  
         MVC   EBTIN,DRINTYPE      PICK UP INPUT TYPE                           
         MVC   EBLIN,DRINLEN       AND LENGTH                                   
         B     FORMNUM4                                                         
         SPACE 1                                                                
*                                  NUMERIC VALUE IS PACKED IN DUB               
FORMNUM2 XC    EBLOCK,EBLOCK                                                    
         LA    R1,DUB                                                           
         ST    R1,EBAIN                                                         
         MVI   EBTIN,C'P'                                                       
         MVI   EBLIN,8                                                          
         SPACE 1                                                                
FORMNUM4 ST    R5,EBAOUT           R5=A(OUTPUT)                                 
         MVC   EBLOUT,DROLEN       PASS OTHER OUTPUT VALUES                     
         MVC   EBDECS,DRODEC                                                    
         MVC   EBFILL,DROFILL                                                   
         MVC   EBFLOAT,DROFLOAT                                                 
         MVC   EBROUND,DRODIV                                                   
         MVC   EBOPT,DROEDIT                                                    
         MVC   EBTRIM,DROFORM                                                   
         MVC   EBALIGN,DROALIGN                                                 
         MVI   EBPWIDTH,198                                                     
         GOTO1 EDITOR,DMCB,EBLOCK                                               
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO HANDLE A LITERAL                                      
         SPACE 3                                                                
DOLIT    NTR1                                                                   
         ZIC   R0,LITLEN           LITLEN=LENGTH OF THIS ONE                    
         L     R2,ALITIN           ALITIN=ITS ADDRESS                           
         L     R3,ALITOUT          ALITOUT=WHERE IT GOES                        
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         SPACE 1                                                                
DOLIT2   CLI   0(R2),C'&&'         CHECK FOR START OF SOFT EXPRESSION           
         BE    DOLIT4                                                           
         MVC   0(1,R3),0(R2)       OTHERWISE MOVE OUT THE DATA                  
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DOLIT2                                                        
         ST    R3,ALITOUT          PASS BACK NEXT OUTPUT POSITION               
         B     XIT                                                              
         SPACE 1                                                                
DOLIT4   LR    RF,R0               LOOK FOR END OF SOFT EXPRESSION              
         LA    RE,1(R2)                                                         
         SPACE 1                                                                
DOLIT6   CLI   0(RE),C'&&'                                                      
         BE    DOLIT8                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,DOLIT6                                                        
         B     DOLITEND            NOT FOUND SO TREAT FIRST AS DATA             
         SPACE 1                                                                
DOLIT8   LR    R1,RE               FIGURE OUT L'SOFT TO R1                      
         SR    R1,R2                                                            
         BCT   R1,*+8                                                           
         B     DOLITEND                                                         
         MVC   WORK,SPACES                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),1(R2)       PICK OFF SOFT                                
         LA    RF,WORK                                                          
         ST    RF,GLAIFLD          PASS ITS ADDRESS                             
         ST    R3,GLAOFLD          PASS ADDRESS OF OUTPUT                       
         LA    R1,1(R1)                                                         
         XC    GLARGS,GLARGS                                                    
         STC   R1,GLARGS           PASS INPUT LENGTH IN ARG(1)                  
         MVI   GLHOOK,GLRESLIT     ASK FOR LITERAL RESOLVE                      
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           FIRST FROM APPLICATION                       
         CLI   GLARGS+1,0                                                       
         BNE   DOLIT10                                                          
         MVI   GLAROUT,2           THEN FROM SYSDRIVER                          
         BAS   RE,GOHOOK                                                        
         CLI   GLARGS+1,0                                                       
         BE    DOLITEND            NO HELP SO TREAT BOTH AS DATA                
         SPACE 1                                                                
DOLIT10  LA    R1,2(R1)            SKIP PAST WORD AND DELIMITERS                
         AR    R2,R1                                                            
         SR    R0,R1               REDUCE REMAINING LENGTH                      
         BZ    XIT                 (NONE LEFT)                                  
         ZIC   R1,1(RF)            L'RETURNED OUTPUT                            
         AR    R3,R1                                                            
         SPACE 1                                                                
DOLITEND MVC   0(1,R3),0(R2)       MOVE OUT THE REST OF THE DATA                
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DOLITEND                                                      
         ST    R3,ALITOUT          PASS BACK NEXT OUTPUT POSITION               
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CONTROL THE PRINTING OF DETAIL LINES                  
         SPACE 3                                                                
SPLAT    NTR1                                                                   
         L     R2,AMYP1                                                         
         SR    R1,R1               FIRST COUNT ACTIVE LINES IN R1               
         LA    R0,20                                                            
         SPACE 1                                                                
SPLAT2   CLC   0(198,R2),SPACES                                                 
         BE    *+8                                                              
         LA    R1,1(R1)                                                         
         LA    R2,198(R2)                                                       
         BCT   R0,SPLAT2                                                        
         SPACE 1                                                                
         CLI   NLINES,0                                                         
         BNE   *+8                                                              
         MVI   NLINES,1                                                         
         IC    R0,NLINES                                                        
         AR    R1,R0               THEN ADD IN SPACING LINES                    
         L     R2,GLALINE                                                       
         IC    R0,0(R2)            AND ADD IN PRESENT LINE NUMBER               
         AR    R1,R0                                                            
         L     R2,GLALMAX                                                       
         IC    R0,0(R2)            R0=MAXLINES                                  
         CR    R1,R0               WILL THEY FIT?                               
         BNH   SPLAT4                                                           
         L     R2,GLAFHEAD         NO SO FORCE HEADLINES ON FIRST PRINT         
         MVI   0(R2),C'Y'                                                       
         SPACE 1                                                                
SPLAT4   L     R2,AMYP1                                                         
         LA    R0,20                                                            
         L     RF,REPORT                                                        
         L     R3,GLASPACE                                                      
         L     R4,GLAWORKD                                                      
         L     R4,X'230'(R4)       (A(SPOOLD))                                  
         L     R5,GLAP1                                                         
         SPACE 1                                                                
SPLAT6   CLC   0(198,R2),SPACES    NOW RELEASE THE PRINT LINES                  
         BE    SPLAT8                                                           
         MVC   0(198,R5),0(R2)     (SUPPORT WIDE AS WELL)                       
         MVC   0(198,R2),SPACES    CLEAR MY BUFFER                              
         MVI   0(R3),1             SINGLE SPACE                                 
         GOTO1 (RF),DMCB,(R4)                                                   
         SPACE 1                                                                
SPLAT8   LA    R2,198(R2)                                                       
         BCT   R0,SPLAT6                                                        
         MVC   0(198,R5),SPACES    CLEAR USERS PRINT LINE                       
         CLI   NLINES,1            ANY EXTRA LINES NEEDED?                      
         BE    XIT                                                              
         IC    R0,NLINES                                                        
         BCTR  R0,0                                                             
         STC   R0,0(R3)                                                         
         GOTO1 (RF),DMCB,(R4)      THEN PRINT THESE                             
         B     XIT                                                              
         EJECT                                                                  
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         BE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         BE    GOH2                                                             
         BH    GOH3                                                             
         L     RF,GLAHOOK          PICK UP APPLICATION HOOK                     
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
GOH2     L     RF,GLASYSDR                                                      
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         GOTO1 (RF),DMCB,(RA)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              TRACE AND PRINTING FACILITIES                                    
         SPACE 3                                                                
TRASOME  NTR1                                                                   
         CLI   TRACOPT,C'Y'        SELECTIVE - R2=A(ELEMENT)                    
         BNE   XIT                             R3=LENGTH                        
         CLI   TRACOUNT,25                                                      
         BNL   XIT                                                              
         AI    TRACOUNT,1                                                       
         L     R2,GLAIO                                                         
         LH    R3,BGRECLEN                                                      
         MVC   P,SPACES                                                         
         MVC   P2,SPACES                                                        
         MVC   P3,SPACES                                                        
         BCTR  R3,0                -1                                           
         EX    R3,TRAM1                                                         
         BAS   RE,PRINTOUT                                                      
         GOTO1 HEXOUT,DMCB,(R2),P2,132,=C'SEP'                                  
         EX    R3,TRAM2                                                         
         BAS   RE,PRINTOUT                                                      
         EX    R3,TRAM3                                                         
         BAS   RE,PRINTOUT                                                      
         BAS   RE,PRINTOUT                                                      
         B     XIT                                                              
         SPACE 1                                                                
TRAM1    MVC   P(0),0(R2)                                                       
TRAM2    MVC   P(0),P2                                                          
TRAM3    MVC   P(0),P3                                                          
TRACOUNT DC    X'00'                                                            
         SPACE 1                                                                
PRINTOUT NTR1                                                                   
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         MVC   P,SPACES                                                         
         B     XIT                                                              
         EJECT                                                                  
*              START HEAD HOOK ROUTINES                                         
         SPACE 3                                                                
*                                  R6=POINTER TO BOXCOLS                        
         SPACE 1                                                                
HOOK     NTR1                                                                   
         CLI   LINETYPE,C'D'       IF A DETAIL LINE IS BEING PRINTED            
         BNE   HOOK2                                                            
         L     R2,AMYP1                                                         
         MVC   0(198,R2),LASTDET   RESTORE THE FULL VERSION                     
         SPACE 1                                                                
HOOK2    L     R2,AACTREC          PICK UP GLINTD FOR PRESENT RECORD            
         ZIC   R1,0(R2)                                                         
         BCTR  R1,0                                                             
         SLL   R1,2                                                             
         LA    R1,GLAINTD(R1)                                                   
         L     R2,0(R1)                                                         
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          SO WE CAN GET TO THE OUTS                    
         SPACE 1                                                                
         L     R6,ABOX             INITIALIZE BOXES                             
         USING BOXD,R6                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXWT,1                                                          
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXOFF,0                                                         
         ZIC   R1,GLFHEADL         FIRST HEADLINE                               
         SH    R1,=H'2'                                                         
         BM    HBOX2                                                            
         LA    R1,BOXROWS(R1)                                                   
         MVI   0(R1),C'T'                                                       
         SPACE 1                                                                
HBOX2    ZIC   R1,GLLHEADL         LAST HEADLINE                                
         LA    R1,BOXROWS(R1)                                                   
         MVC   BOXROWS,SPACES                                                   
         MVI   0(R1),C'M'                                                       
         MVI   BOXROWS+58,C'B'                                                  
         SPACE 1                                                                
         MVC   BOXCOLS,SPACES      START OFF COLUMNS                            
         MVC   BOXCOLSR,SPACES                                                  
         LA    R6,BOXCOLS          USE R6 FOR BOXCOLS POINTER                   
         DROP  R6                                                               
         AH    R6,GLPDISP                                                       
         MVI   0(R6),C'L'          FIRST WILL BE AN L                           
         XC    CHUNKADD,CHUNKADD   CLEAR SWITCHES                               
         MVC   LASTHEAD,SPACES                                                  
         EJECT                                                                  
*              OUT RELATED BOX ROUTINES                                         
         SPACE 3                                                                
HOUT30   CLI   0(R3),X'30'                                                      
         BNE   HEAD                                                             
         USING DROD,R3                                                          
         MVC   SAVOLEN,DROLEN      SAVE ODDMENTS FOR HEAD AND CHUNK             
         ST    R6,ALASTBXC                                                      
         CLI   0(R6),C'L'          (FIRST COLUMN)                               
         BE    HOUT32                                                           
         TM    DROFORM,X'20'       OPTION TO SUPPRESS BOX TO LEFT               
         BO    HOUT32                                                           
         OC    CHUNKADD,CHUNKADD   (SEE IF CHUNK IS IN OPERATION)               
         BNZ   HOUT32                                                           
         MVI   0(R6),C'C'                                                       
         SPACE 1                                                                
HOUT32   ZIC   R1,DROLEN           ADVANCE TO NEXT BOXCOLS                      
         AR    R6,R1                                                            
         ZIC   R1,GLGAP                                                         
         AR    R6,R1                                                            
         C     R3,CHUNKADD         HAVE WE REACHED END OF CHUNK?                
         BNE   *+10                                                             
         XC    CHUNKADD,CHUNKADD   YES SO CLEAR IT                              
         MVI   OUTYN,C'N'          CHECK FOR CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   HOOKEND                                                          
         MVI   OUTYN,C'Y'                                                       
         CLI   DROLTYP,C'H'        ONLY INTERESTED IN HEADS & MIDS              
         BE    HOUT34                                                           
         CLI   DROLTYP,C'M'                                                     
         BNE   HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH OUT RELATED TEXTS                                      
         SPACE 3                                                                
HOUT34   ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,120         DO WE NEED TO HANDLE A LITERAL               
         BE    HOUT36                                                           
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'120'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
HOUT36   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    HOUT38                                                           
         USING DRIND,R1                                                         
         LH    R1,DRINDISP(R1)                                                  
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD                                                       
         SPACE 1                                                                
HOUT38   OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    HOUT40                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         BAS   RE,GOHOOK                                                        
         B     HOOKEND                                                          
         SPACE 1                                                                
HOUT40   BAS   RE,FORM                                                          
         B     HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH HEAD ELEMENTS                                          
         SPACE 3                                                                
HEAD     CLI   0(R3),X'40'                                                      
         BNE   CHUNK                                                            
         USING DRHDD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRHDIFS                                   
         BNE   HOOKEND                                                          
         TM    DRHDSTAT,X'80'      IGNORE IF MARKED DELETED                     
         BO    HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRHDAPOS         R5=A(OUTPUT)                                 
         CLI   DRHDELEN,56         DO WE NEED TO HANDLE A LITERAL               
         BE    HEAD2                                                            
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'56'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRHDLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
HEAD2    ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         OC    DRHDRADD,DRHDRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    HEAD4                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRHDROUT                                                 
         MVC   GLAROUT,DRHDRADD                                                 
         MVC   GLARGS,DRHDARGS                                                  
         BAS   RE,GOHOOK                                                        
         SPACE 1                                                                
HEAD4    CLI   DRHDLINE,1          IF THIS IS THE FIRST HEADING LINE            
         BNE   HOOKEND                                                          
         L     R5,DRHDAPOS                                                      
         ZIC   R1,SAVOLEN                                                       
         BCTR  R1,0                                                             
         CLC   LASTHEAD,SPACES                                                  
         BE    HEAD6                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),LASTHEAD    CHECK IF AUTO CHUNK IS ON                    
         BNE   HEAD6                                                            
         L     RE,ALASTBXC         YES - SO PICK UP ADDRESS LAST 'C'            
         MVI   0(RE),C' '                AND CREAM IT                           
         L     R4,ALASTH1          R4=A(BEGINNING OF CHUNK)                     
         SR    R5,R4                                                            
         LA    R5,1(R1,R5)         R5=LENGTH                                    
*                                  CENTER THIS INTO COMMUNAL SPACE              
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         B     HOOKEND                                                          
         SPACE 1                                                                
HEAD6    MVC   LASTHEAD,SPACES                                                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   LASTHEAD(0),0(R5)   SAVE THIS HEADING FOR FUTURE                 
*                                  TEST FOR AUTO CHUNK                          
         ST    R5,ALASTH1          ALSO SAVE ITS ADDRESS                        
         B     HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH CHUNK ELEMENTS                                         
         SPACE 3                                                                
CHUNK    CLI   0(R3),X'42'                                                      
         BNE   HOOKEND                                                          
         USING DRCHD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRCHIFS                                   
         BNE   HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRCHAO1          R5=A(OUTPUT)                                 
         CLI   DRCHELEN,72         DO WE NEED TO HANDLE A LITERAL               
         BE    CHUNK2                                                           
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'72'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRCHLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
CHUNK2   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         OC    DRCHRADD,DRCHRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    CHUNK4                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRCHROUT                                                 
         MVC   GLAROUT,DRCHRADD                                                 
         MVC   GLARGS,DRCHARGS                                                  
         BAS   RE,GOHOOK                                                        
         SPACE 1                                                                
CHUNK4   L     R4,DRCHAO1          CENTER EXPRESSION                            
         LH    R5,DRCHLEN                                                       
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         EJECT                                                                  
*              FINAL ODDMENTS AND MERGEING HEADLINES TOGETHER                   
         SPACE 3                                                                
HOOKEND  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'00'                                                      
         BE    HOOKEND2                                                         
         CLI   0(R3),X'10'                                                      
         BNE   HOOK2                                                            
         SPACE 1                                                                
HOOKEND2 MVI   0(R6),C'R'          PUT IN RIGHT HAND SIDE OF BOX                
         L     R2,AMYH1            OVERLAYING MY HEADLINES (R2)                 
         L     R3,GLAH1            WITH THE USERS (R3)                          
         ZIC   R0,GLANHEAD         NUMBER OF USERS HEADLINES (R0)               
         L     R1,WIDTH            WIDTH OF USERS LINES                         
         BCTR  R1,0                -1                                           
         SPACE 1                                                                
HOOKEND4 EX    R1,HMERGE                                                        
         LA    R2,198(R2)                                                       
         A     R3,WIDTH                                                         
         BCT   R0,HOOKEND4                                                      
         SPACE 1                                                                
         L     R2,AMYM1            NOW THE SAME FOR THE MIDLINES                
         L     R3,GLAM1                                                         
         ZIC   R0,GLANMID          NUMBER OF USERS MIDLINES (R0)                
         SPACE 1                                                                
HOOKEND6 EX    R1,HMERGE                                                        
         LA    R2,198(R2)                                                       
         A     R3,WIDTH                                                         
         BCT   R0,HOOKEND6                                                      
         SPACE 1                                                                
         MVI   GLHOOK,GLHEAD                                                    
         MVC   GLADTENT,GLAFOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,HOOK             FINALLY HOOK TO APPLICATION                  
         MVI   GLAROUT,2                                                        
         BAS   RE,GOHOOK           AND SYSTEM CONTROLLER                        
         B     XIT                                                              
         SPACE 1                                                                
HMERGE   OC    0(0,R3),0(R2)                                                    
         EJECT                                                                  
*              DSECT FOR OUT STORAGE                                            
         SPACE 3                                                                
DOUTD    DSECT                                                                  
CDUB     DS    PL8                                                              
PFILL    DS    CL1                                                              
P        DS    CL132                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL198                                                            
SAVEDET  DS    CL198                                                            
LASTDET  DS    CL198                                                            
LASTTYPE DS    XL1                                                              
CBLEV    DS    XL1                                                              
LASTDET  DS    CL256                                                            
ALEVOUT  DS    A                                                                
ATHISREC DS    A                                                                
BREAK    DS    CL1                                                              
ELCODE   DS    CL1                                                              
LITLEN   DS    CL1                 PARAMETERS FOR DOLIT                         
ALITIN   DS    A                                                                
ALITOUT  DS    A                                                                
PRINTED  DS    CL1                                                              
NLINES   DS    XL1                                                              
LINETYPE DS    CL1                                                              
LASTHEAD DS    CL24                                                             
CHUNKADD DS    A                                                                
OUTYN    DS    CL1                                                              
ALASTBXC DS    A                                                                
ALASTH1  DS    A                                                                
         EJECT                                                                  
       ++INCLUDE DDEBLOCK                                                       
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD                                                      
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DRIVETABLE                                                     
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDSHUTTLE 05/01/02'                                      
         END                                                                    
