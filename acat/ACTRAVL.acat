*          DATA SET ACTRAVL    AT LEVEL 013 AS OF 01/22/08                      
*CATALP ACTRAVL                                                                 
                                                                                
********************************************************************            
* ACTRAVL  - COMPUTE UNUSED TRACK SPACE FOR ACCDIR/ACCMST          *            
*                                                                  *            
* INPUT    - P1 BYTE 0   =  01 = INITIALIZE                        *            
*                           02 = TEST CURRENT SPACE                *            
*                           03 = LAST TIME                         *            
*               BYTE 1-3 =   A(COMFACS)                            *            
*            P2 BYTE 1-3 =   A(LOGIO)                              *            
********************************************************************            
                                                                                
         TITLE 'ACTRAVL- COMPUTE UNUSED TRACK SPACE'                            
ACTRAVL  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,*ACTRAV*                                                       
         MVC   MODE,0(R1)                                                       
         L     R7,0(R1)                                                         
         USING COMFACSD,R7                                                      
         MVC   LOGIO,4(R1)                                                      
*                                                                               
         CLI   MODE,MINIT               INITIALIZE                              
         BE    INIT                                                             
         CLI   MODE,MTEST               TEST CURRENT SPACE                      
         BE    TEST                                                             
         CLI   MODE,MLAST               LAST                                    
         BE    LAST                                                             
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* INITIALIZE -  GET STARTING SPACE                                    *         
***********************************************************************         
                                                                                
INIT     GOTO1 CDATAMGR,DMCB,DTFADD,ACCDIR                                      
         MVC   ADIRDCB,12(R1)           SAVE ADDRESS OF DIR DCB                 
         BAS   RE,DIR                   GET DIRECTORY SPACE                     
         MVC   DIRSTR,DUB                                                       
         SRL   R1,1                     GET HALF                                
         STCM  R1,3,DIRHLF              SAVE HALF NUMBER OF TRACKS              
*                                                                               
         GOTO1 CDATAMGR,DMCB,DTFADD,ACCMST                                      
         MVC   AMSTDCB,12(R1)           SAVE ADDRESS OF MST DCB                 
         GOTOR CDATAMGR,DMCB,DMKEY,ACCMST,(3,DADATA),0,0                        
         L     RF,DATTRKS          Total tracks                                 
         L     RE,DAUTRKS          Total used                                   
         LR    R1,RE                                                            
         SR    RF,RE                                                            
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(5),DUB+5(3)                                                  
         MVC   MSTSTR,DUB                                                       
         SRL   R1,1                     GET HALF                                
         STCM  R1,3,MSTHLF              SAVE HALF NUMBER OF TRACKS              
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* TEST CURRENT SPACE                                                  *         
***********************************************************************         
                                                                                
TEST     MVI   STOP,0                                                           
         BAS   RE,DIR                   GET CURRENT NUMBER                      
         MVC   DIREND,DUB                                                       
         STCM  R1,3,DIRCUR                                                      
*                                                                               
         GOTOR CDATAMGR,DMCB,DMKEY,ACCMST,(3,DADATA),0,0                        
         L     RF,DATTRKS          Total tracks                                 
         L     RE,DAUTRKS          Total used                                   
         LR    R1,RE                                                            
         SR    RF,RE                                                            
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(5),DUB+5(3)                                                  
         MVC   MSTEND,DUB                                                       
         STCM  R1,3,MSTCUR                                                      
*                                                                               
         CLC   DIRCUR,DIRHLF            TEST MORE THAN HALF REMAINS             
         BH    *+8                                                              
         OI    STOP,1                   SET TOO MUCH DIRECTORY USED             
         CLC   MSTCUR,MSTHLF                                                    
         BH    *+8                                                              
         OI    STOP,2                   SET TOO MUCH MASTER FILE USED           
         CLI   STOP,0                   ANY PROBLEMS                            
         BE    XIT                      NO, OK TO CONTINUE                      
*                                                                               
         LA    R0,MSGLQ2                                                        
         GOTO1 LOGIO,DMCB,(1,1),((R0),MSG)                                      
         MVC   REPLY,SPACES                                                     
         GOTO1 (RF),(R1),0,(10,REPLY)                                           
         CLC   =C'STOP',REPLY                                                   
         BNE   XIT                                                              
         DC    H'0'                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
* LAST TIME                                                           *         
***********************************************************************         
                                                                                
LAST     BAS   RE,DIR                   GET CURRENT NUMBER                      
         MVC   DIREND,DUB                                                       
         STCM  R1,3,DIRCUR                                                      
*                                                                               
         GOTOR CDATAMGR,DMCB,DMKEY,ACCMST,(3,DADATA),0,0                        
         L     RF,DATTRKS          Total tracks                                 
         L     RE,DAUTRKS          Total used                                   
         LR    R1,RE                                                            
         SR    RF,RE                                                            
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(5),DUB+5(3)                                                  
         MVC   MSTEND,DUB                                                       
         STCM  R1,3,MSTCUR                                                      
*                                                                               
         LA    R0,MSGLQ                                                         
         GOTO1 LOGIO,DMCB,(X'FF',1),((R0),MSG)                                  
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* COMPUTE AVAILABLE TRACKS -  DIRECTORY                               *         
***********************************************************************         
                                                                                
         USING ISDTF,RF                                                         
DIR      ST    RE,SAVRE                 COMPUTE DIRECTORY TRACKS                
         L     RF,ADIRDCB                                                       
         MVC   ACCN(1),ISFFID+6         ACCOUNT FILE NUMBER                     
                                                                                
         USING EXTENTD,RE                                                       
         LA    RE,ISXTNT                                                        
         SR    R0,R0                                                            
         SR    R1,R1                                                            
DIR2     CLI   0(RE),X'FF'                                                      
         BE    DIR3                                                             
         ICM   R1,3,EXT#TRKS            # OF TRACKS                             
         TM    ISFTYPE,ISFTBIGF         20 bit?                                 
         BO    *+6                      Yes                                     
         SR    R0,R0                    No, then last extent has total          
         AR    R0,R1                                                            
         LA    RE,EXTLNQ(,RE)           (EXTENT DATA IS CUMULATIVE)             
         B     DIR2                                                             
         DROP  RE                                                               
                                                                                
DIR3     SR    R1,R1                                                            
         L     R1,ISOVLAST                                                      
         SRL   R1,12               20 Bit TTTTTBRR ->> 000TTTTT                 
         TM    ISFTYPE,ISFTBIGF                                                 
         BO    *+8                                                              
         SRL   R1,4                16 Bit 000TTTTB ->> 0000TTTT                 
         SR    R0,R1               Total tracks - tracks used                   
         CVD   R0,DUB              R0 = # of tracks available                   
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(5),DUB+5(3)                                                  
         LR    R1,R0               Return R1 with tracks available              
         L     RE,SAVRE                                                         
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS - STORAGE                                                 *         
***********************************************************************         
                                                                                
LOGIO    DS    A                                                                
*                                                                               
MSG      DS    0C                                                               
         DC    C'* TRACK STATUS * '                                             
         DC    C'ACC '                                                          
ACCN     DC    C'? '                                                            
         DC    C'DIR:START='                                                    
DIRSTR   DC    CL5' '                                                           
         DC    C'/END='                                                         
DIREND   DC    CL5' '                                                           
         DC    C'  '                                                            
         DC    C'MST:START='                                                    
MSTSTR   DC    CL5' '                                                           
         DC    C'/END='                                                         
MSTEND   DC    CL5' '                                                           
         DC    C'  '                                                            
MSGLQ    EQU   *-MSG                                                            
         DC    C'REPLY='                                                        
MSGOK    DC    C'GO/STOP'                                                       
MSGLQ2   EQU   *-MSG                                                            
*                                                                               
DTFADD   DC    C'DTFADD '                                                       
DMKEY    DC    C'DMKEY  '                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
*                                                                               
SPACES   DC    CL20' '                                                          
*                                                                               
REPLY    DS    CL10                                                             
*                                                                               
MODE     DS    XL1                                                              
MINIT    EQU   1                                                                
MTEST    EQU   2                                                                
MLAST    EQU   3                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
SAVRE    DS    F                                                                
*                                                                               
ADIRDCB  DS    A                        A(DCB FOR DIRECTORY)                    
AMSTDCB  DS    A                        A(DCB FOR MASTER)                       
*                                                                               
DADATA   DS    0F                                                               
DADNEXT  DS    F                   DNEXT                                        
DAUTRKS  DS    F                   Used tracks                                  
DATTRKS  DS    F                   Total tracks                                 
*                                                                               
DIRCUR   DS    H                        CURRENT REMAINING - DIRECTORY           
MSTCUR   DS    H                                          - MASTER              
DIRHLF   DS    H                        HALF REMAINING - DIRECTORY              
MSTHLF   DS    H                                       - MASTER                 
*                                                                               
STOP     DS    XL1                                                              
*                                                                               
DMTRX    DC    64XL14'00'                                                       
         DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
* DMXTNTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013ACTRAVL   01/22/08'                                      
         END                                                                    
