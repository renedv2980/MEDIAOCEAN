*          DATA SET DEGETPA2   AT LEVEL 047 AS OF 11/19/15                      
*PROCESS USING(WARN(15))           ENABLE ALL USING STATEMENT WARNINGS          
*CATALP DEGETPA2                                                                
         TITLE 'DEGETPAV - PROGRAM AVERAGES READ CONTROLLER'                    
***************************CHANGE LOG*******************************            
*      DATE       LVL    REASON                                                 
*                                                                               
*      OCT10/00    12    VAR CORRECTIONS - IF X'80' IS REQUESTED                
*                                                                               
*      APR02/90     3    TAKE OUT SPECIAL ARB WEEK CODE WHICH DIED              
*                         IN MAY/86 BUT WAS MISSED HERE                         
*                                                                               
********************************************************************            
GETPA2   RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PAVWORKL,**GETPA2,RA                                             
         LR    R9,RC                                                            
         USING PAVWORKD,R9                                                      
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING COMFACSD,R8         R8=A(COMFACS)                                
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
*                                                                               
         MVI   STALFLAG,C'N'       INITIALIZE STATION LINK FLAG                 
*                                                                               
         CLI   0(R1),0             TEST FIRST/NEXT TIME                         
         BNE   GETPAV34                                                         
                                                                                
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    DYTMNO                                                           
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     *-20                                                             
         MVC   DBSELDAY(5),DBXTLIST  SET FIRST DAY/TM                           
         MVI   DBXTLIDX,0                                                       
         DROP  RE                                                               
DYTMNO   DS    0H                                                               
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    PURENO                                                           
         USING DBXPUID,RE                                                       
         CLC   DBXPUID,=C'PURE'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXPUNXT                                                      
         B     *-20                                                             
         MVC   DBSELPUR(2),DBXPULST  SET FIRST PURE NUMBER                      
         MVI   DBXPUIDX,0                                                       
         DROP  RE                                                               
PURENO   DS    0H                                                               
         MVI   DEMOFLAG,0                                                       
         GOTO1 AFIXSTA             DEAL WITH REUSED CALL LETTERS                
         CLI   DBSELDAY,0          TEST IF DAY & TIME SET                       
         BNE   GETPAV1                                                          
         OC    DBSELTIM,DBSELTIM                                                
         BNZ   GETPAV1                                                          
         MVI   DBFUNCT,DBGETPUR    NO - SET FUNCTION TO GET PURE DEMO           
GETPAV1  OC    DBSELBK,DBSELBK     ENSURE SELECTED BOOK SET                     
         BZ    GETPAV30                                                         
GETPAV2  GOTO1 AGETBOOK            GET INITIAL BOOK VALUE                       
         BNE   GETX                EXIT ON ERROR                                
         LA    R1,DBACTBK                                                       
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR     TEST IF A(CONTROL TABLE) SET                 
         BNZ   *+6                                                              
         BASR  RE,RF               NO - GET AGENCY CONTROLS                     
         OC    ACONHDR+1(3),ACONHDR+1                                           
         BZ    GETPAV14                                                         
         MVC   DBACTRMK,DBSELRMK                                                
         OC    DBACTRMK,DBACTRMK                                                
         BNZ   GETPAV3                                                          
         GOTO1 AGETMRKT                                                         
         BNE   GETX                                                             
         XC    DBACTSTA,DBACTSTA   LET CODE BELOW SET IT                        
*                                  GET AGENCY MARKET CONTROLS                   
*                                                                               
GETPAV3  OC    ALET,ALET           SSBTBLET RESOLVED?                           
         BNZ   GETPAV3C            YES                                          
         XC    DUB,DUB             SPECIAL CALL FOR SYSFACS                     
         MVC   DUB(4),=XL4'FEFFFFFF'                                            
         OC    CSWITCH,CSWITCH                                                  
         BNZ   GETPAV3B                                                         
         ICM   RF,15,CMASTC                                                     
         ICM   RF,15,MCSSB-MASTD(RF)       RF=ASSB                              
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
         B     GETPAV3C                                                         
*                                                                               
GETPAV3B L     RF,CSWITCH                                                       
         GOTO1 (RF),DUB                                                         
         L     RF,0(R1)            RF=V(SYSFACS)                                
         L     RF,VSSB-SYSFACD(RF) RF=V(SSB)                                    
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
*                                                                               
GETPAV3C L     R2,ACONHDR          R2=A(CONTROL TABLE)                          
         USING CONHDRD,R2                                                       
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALET                                                     
         LA    R3,CONHDRD+CONHDRLN                                              
         USING CONDTAD,R3                                                       
         CPYA  AR3,AR2                                                          
         SAC   512                                                              
         LA    R4,3                                                             
         SR    R5,R5                                                            
         ICM   R5,7,CONAET                                                      
         CR    R3,R5                                                            
         BNE   GETPAV4                                                          
         SAC   0                                                                
         LAM   AR2,AR3,=2F'0'                                                   
         B     GETPAV14                                                         
*                                  PROCESS VALID MARKET LIST                    
GETPAV4  CLI   CONINDIC,0                                                       
         BNE   GETPAV6                                                          
         OI    DEMOFLAG,X'80'                                                   
         CLC   DBACTRMK,CONMRKT                                                 
         BNE   *+8                                                              
         OI    DEMOFLAG,X'40'                                                   
         B     GETPAV10                                                         
*                                  PROCESS INVALID MARKET LIST                  
GETPAV6  CLI   CONINDIC,X'FF'                                                   
         BNE   GETPAV8                                                          
         OI    DEMOFLAG,X'20'                                                   
         CLC   DBACTRMK,CONMRKT                                                 
         BNE   *+8                                                              
         OI    DEMOFLAG,X'10'                                                   
         B     GETPAV10                                                         
*                                  PROCESS SOURCE OVERRIDE LIST                 
GETPAV8  CLC   DBSELUMK,CONMRKT                                                 
         BNE   GETPAV10                                                         
         OI    DEMOFLAG,X'08'                                                   
         MVC   DUB(1),CONINDIC                                                  
*                                                                               
GETPAV10 BXLE  R3,R4,GETPAV4                                                    
         SAC   0                                                                
         LAM   AR2,AR3,=2F'0'                                                   
*                                  TEST MARKET NUMBERS VALID                    
         TM    DEMOFLAG,X'C0'                                                   
         BM    GETPAV12                                                         
         TM    DEMOFLAG,X'30'                                                   
         BO    GETPAV12                                                         
*                                                                               
         TM    DEMOFLAG,X'08'      TEST SOURCE OVERRIDE PRESENT                 
         BZ    GETPAV14                                                         
         TM    DEMOFLAG,X'01'      YES - HAVE I BEEN HERE BEFORE                
         BNZ   GETPAV12                                                         
         MVI   DEMOFLAG,X'01'      NO - SET NEW SOURCE CODE & RETRY             
         XC    ACONHDR,ACONHDR                                                  
         XC    DBACTUAL,DBACTUAL                                                
         MVC   DBACTSRC,DUB                                                     
         MVC   DBACTMED,DBINTMED                                                
         B     GETPAV2                                                          
*                                  ERROR EXIT                                   
GETPAV12 MVI   DBERROR,INVMRKT                                                  
         B     GETX                                                             
*                                                                               
GETPAV14 DS    0H                                                               
         MVI   PVSELNDY,0          ASSUME "AVG-N" NOT REQUESTED                 
         CLI   DBFUNCT,DBGETPUR    TEST PURE DEMO REQUIRED                      
         BE    GETPAV16                                                         
         OC    DBSELPUR,DBSELPUR   TEST PURE NUMBER PASSED                      
         BNZ   GETPAV16                                                         
         LA    R1,X'80'                                                         
         CLI   DBSELDAY,X'FF'      TEST FOR ALL DAYS REQUIRED                   
         BE    GETPAV15                                                         
         CLI   DBSELDAY,X'7C'      TEST FOR ROTATORS (M-F, M-S)                 
         BE    GETPAV15                                                         
         CLI   DBSELDAY,X'7F'                                                   
         BE    GETPAV15                                                         
         LHI   R1,X'02'                                                         
         TM    DBSELDAY,X'90'      TEST FOR VAR OR AVG-N                        
         BO    GETPAV15                                                         
         SR    R1,R1                                                            
         CLI   DBSELDAY,X'03'      TEST SAT-SUN ROTATOR                         
         BNE   GETPAV15                                                         
         CLI   DBDAYOPT,C'Y'       TEST SPECIAL ROTATOR REQUESTED               
         BNE   GETPAV15                                                         
         TM    FMSIND1,FMS1SASU    TEST SPECIAL ROTATOR SUPPORTED               
         BZ    GETPAV15                                                         
         LA    R1,X'81'            SET FOR SPECIAL DQTAB ENTRY                  
*                                                                               
GETPAV15 DS    0C                  BUILD LIST OF DAYS/QTR HOURS                 
         XC    TPTIM2,TPTIM2                                                    
         CLI   DBSELMED,C'O'       OVERNIGHTS                                   
         BE    *+8                                                              
         CLI   DBSELMED,C'T'       FOR USTV                                     
         JNE   DYTMNOP                                                          
         CLI   DBSELSRC,C'N'       AND NSI                                      
         JNE   DYTMNOP                                                          
* SPECIAL CASE OF 600-545 MEANS ALL DAY                                         
         CLC   DBSELTIM(2),=AL2(600)                                            
         BNE   *+14                                                             
         CLC   DBSELTIM+2(2),=AL2(545)                                          
         BE    DYTMNOP                                                          
ALDAY5AX DS    0C                                                               
*                                                                               
         CLC   DBSELTIM+2(2),=X'0000'                                           
         JE    DYTMNOP                                                          
         CLC   DBSELTIM(2),DBSELTIM+2                                           
         JE    DYTMNOP                                                          
*                                                                               
*   TEST FOR NOW EFFECTIVE NOV_13 FOR TESTING PUPROSES                          
         CLI   DBSELMED,C'T'       FOR USTV                                     
         BNE   CHK5A00B                                                         
***      CLC   DBSELBK,=AL2(NOV_13)                                             
         CLC   DBSELBK,=AL2(DEC_15)                                             
         BL    CHK5AM02                                                         
         B     DYTMNOP                                                          
CHK5A00B CLI   DBSELMED,C'O'       OVERIGHTS                                    
***      BNE   *+14                                                             
         BNE   CHK5AM02                                                         
***      CLC   DBSELBK,=X'712C'    DEC0113 WEEK FOR OVERNIGHTS                  
         CLC   DBSELBK,=X'7331'    NOV3015 WEEK FOR OVERNIGHTS                  
         BL    CHK5AM02                                                         
* EFFECTIVE 3A START TIME (DEC15 BOOK)                                          
         CLC   DBSELTIM(2),=AL2(300)    ALLOW DAY SPLIT FROM 3AM                
         BNL   CHK5AM10                 END OF DAY IS 259                       
         MVC   DBSELTIM+2(2),=AL2(259)                                          
         B     DYTMNOP                                                          
* OLD 5A START TIME CODE                                                        
CHK5AM02 DS    0H                                                               
*                                                                               
         CLC   DBSELTIM(2),=AL2(500)                                            
         BL    DYTMNOP                                                          
CHK5AM10 CLC   DBSELTIM(2),=AL2(559) START AFTER 6 IS OK                        
         BH    DYTMNOP                                                          
         CLC   DBSELTIM+2(2),=AL2(601) END BEFORE 6 IS OK                       
         BL    DYTMNOP                                                          
         MVC   TPTIM2(4),DBSELTIM  SET UP SPLIT TIMES                           
         MVC   DBSELTIM+2(2),=AL2(559)  END OF TIME1                            
         MVC   TPTIM2(2),=AL2(600)      START OF TIME2                          
*                                                                               
DYTMNOP  DS    0C                                                               
         CLI   DBSELMED,C'O'       OVERNIGHT                                    
         BNE   *+8                                                              
         LA    R1,0                                                             
         GOTO1 AGETDQ              BUILD LIST OF DAYS/QTR HOURS                 
*                                                                               
         DS    0H                  GET REQUESTED # OF DAYS                      
         ZIC   RE,DBSELDAY                                                      
         SRDL  RE,4                                                             
         CHI   RE,9                 WAS "VAR" OR "AVG-N" REQUESTED?             
         BNE   *+12                  NOPE                                       
         SRL   RF,28                 YEP, SET REGISTER TO # DAYS REQ            
         STC   RF,PVSELNDY                                                      
*                                  GET STATION KEY VALUE (DBACTSTA)             
GETPAV16 OC    DBACTKMK,DBACTKMK                                                
         BNZ   *+10                                                             
         MVC   DBACTKMK,DBSELMK                                                 
         BAS   RE,GETCALL                                                       
         STC   R1,CALLEN           SAVE L'STATION CALL LETTERS                  
         OC    DBACTSTA,DBACTSTA   TEST ACTUAL SET                              
         BNZ   *+10                                                             
         MVC   DBACTSTA,DBSELSTA   NO - SET FROM SELECTED                       
         LA    RE,DBSELSTA(R1)                                                  
         LA    R1,DBACTSTA(R1)                                                  
         CLI   0(RE),C'1'                                                       
         BNE   *+16                                                             
         MVI   0(RE),C'T'                                                       
         MVI   0(R1),C'T'                                                       
         B     GETPAV17                                                         
         CLI   0(RE),C'T'                                                       
         BNE   GETPAV17                                                         
         LA    R4,PONLYTAB                                                      
GETPV16S CLI   0(R4),X'FF'                                                      
         BE    GETPAV17                                                         
         CLC   DBACTSTA(4),0(R4)                                                
         BE    GETPV16X                                                         
         LA    R4,L'PONLYTAB(R4)                                                
         B     GETPV16S                                                         
*                                                                               
GETPV16X CLI   DBBTYPE,0                                                        
         BNE   *+8                                                              
         MVI   DBBTYPE,C'A'                                                     
         CLI   DBBTYPE,C'L'                                                     
         BNE   *+8                                                              
         MVI   DBBTYPE,C'Q'                                                     
         CLI   DBBTYPE,C'W'                                                     
         BNE   *+8                                                              
         MVI   DBBTYPE,C'R'                                                     
         CLI   DBBTYPE,C'U'                                                     
         BNE   *+8                                                              
         MVI   DBBTYPE,C'S'                                                     
GETPAV17 CLI   0(RE),C'T'          TEST P+S1/S2 REQUEST                         
         BNL   *+8                                                              
         MVI   0(R1),C'T'          NO - SET PARENT ONLY                         
         MVC   SAVEBOOK,DBACTBK    SET LOOK-UP BOOK VALUE                       
*                                                                               
         CLI   STALFLAG,C'Y'                                                    
         BE    *+8                                                              
         BRAS  RE,STALINK                                                       
*                                                                               
GETPAV20 LA    R2,DBKEY            BUILD KEY OF DEMO RECORD                     
         USING DRKEY,R2                                                         
         XC    DRKMAJOR,DRKMAJOR                                                
         MVI   DRCODE,DRCODEQU                                                  
         MVC   DRMEDIA,DBINTMED                                                 
         MVC   DRSRC,DBACTSRC                                                   
         MVC   DRSTAT,DBACTSTA                                                  
         MVC   DRKMKT,DBACTKMK                                                  
         MVC   DRBOOK,SAVEBOOK                                                  
         XC    DRBOOK,=X'FFFF'                                                  
         MVC   DRSTYP,DBSTYPE                                                   
         MVC   DRBTYP,DBBTYPE                                                   
*                                                                               
GETPAV21 MVI   IOFLAG,DIR+HIGH+DEM READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
*                                  CHECK STATION FOUND                          
         CLC   DRKEY(DRBOOK-DRKEY),KEYSAVE                                      
         BE    GETPAV26                                                         
*                                                                               
*                                  DEAL WITH EQUIVALENCING                      
GETPAV24 TM    DEMOFLAG,X'04'      TEST ALREADY EQUIVALENCED                    
         BO    GETPAV30                                                         
*                                                                               
         CLI   DBACTSTA+4,C'1'     PRE-EQUATE S1 TO PARENT                      
         BNE   *+12                                                             
         MVI   DBACTSTA+4,C'T'                                                  
         B     GETPAV16                                                         
*                                                                               
* HAVE TO ADD STATION LINK CODE                                                 
         CLI   STALFLAG,C'Y'                                                    
         BNE   GETPV24F                                                         
         GOTOR =A(STALINK),DMCB,(X'01',SAVEBOOK),DBACTSTA,RR=RELO               
         CLC   =X'FFFF',DMCB+8     NO MORE STATIONS IN TABLE                    
         BE    GETPAV30            ALSO DONT DO GETEQIV CALL                    
         MVC   DBSELSTA,DBACTSTA   SINCE WE ARE DEALING W LINK STATION          
         B     GETPAV16                                                         
*                                                                               
GETPV24F MVI   DUB+5,3             NO - GET EQUIVALENT                          
         GOTO1 AGETEQIV                                                         
         BNE   GETPAV30                                                         
         MVC   DUB+4(1),DBACTSTA+4                                              
         MVC   DBACTSTA,DUB        SET NEW STATION CALL LETTERS                 
         CLI   DBACTSTA+4,C' '     SET DEFAULT SUFFIX                           
         BNE   *+8                                                              
         MVI   DBACTSTA+4,C'T'                                                  
GETPAV25 OI    DEMOFLAG,X'04'                                                   
         B     GETPAV16            TRY AGAIN                                    
*                                                                               
GETPAV26 DS    0H                                                               
         CLC   DRBTYP,DBBTYPE      BOOKTYPE MISMATCH                            
         BE    GETPAV27                                                         
         CLI   DRMEDIA,C'O'        DAILIES?                                     
         BNE   GETPAV27                                                         
         CLI   DRSRC,C'N'                                                       
         BNE   GETPAV27                                                         
*                                                                               
         CLI   DBBTYPE,C'C'                                                     
         BNE   *+12                                                             
         MVI   DBBTYPE,C'U'                                                     
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,C'W'                                                     
         BNE   *+12                                                             
         MVI   DBBTYPE,C'Z'                                                     
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,C'H'                                                     
         BNE   *+12                                                             
         MVI   DBBTYPE,C'J'                                                     
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,C'A'                                                     
         BNE   *+12                                                             
         MVI   DBBTYPE,C'Q'                                                     
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,C'R'                                                     
         BNE   *+12                                                             
         MVI   DBBTYPE,C'S'                                                     
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,0           TRIED L ALREADY?                             
         BNE   GETPAV27                                                         
         MVI   DBBTYPE,C'L'        IF NOT, TRY L                                
         B     GETPAV20                                                         
*                                                                               
GETPAV27 OC    SAVEBOOK,SAVEBOOK   TEST IF FAST SERVICE BOOK                    
         BNZ   GETPAV28                                                         
         MVC   SAVEBOOK,DRBOOK     YES - DID I FIND CORRECT RECORD              
         XC    DRBOOK,DRBOOK                                                    
         CLC   DRKEY(L'DRKMAJOR),KEYSAVE                                        
         BNE   GETPAV20                                                         
         MVC   DRBOOK,SAVEBOOK     NO - GO READ FOR BOOK I FOUND                
         B     GETPAV32                                                         
*                                  DEAL WITH OTHER KEY FIELDS                   
GETPAV28 CLC   DRKEY(L'DRKMAJOR),KEYSAVE                                        
         BE    GETPAV32                                                         
         B     GETPAV24                                                         
*                                  SET NOT FOUND                                
GETPAV30 MVI   DBERROR,NOTFOUND                                                 
         B     GETX                                                             
*                                  DIRECTORY ITEM FOUND                         
GETPAV32 MVC   DBACTBK,DRBOOK                                                   
*                                                                               
************************************************************                    
GETPAV34 XC    DBFACTOR,DBFACTOR                                                
         ZIC   R1,DBDQPTR                                                       
         CLI   DBLSTNXT,0          GET NEXT DQTAB ENTRY                         
         BNE   *+8                                                              
         LA    R1,1(R1)            YES - BUMP POINTER NUMBER                    
         STC   R1,DBDQPTR                                                       
         LA    R0,DBDQLEN                                                       
         MR    R0,R0                                                            
         LA    R3,DBDQTAB-DBDQLEN(R1)                                           
         ST    R3,ADQNTRY          SET A(DQTAB ENTRY)                           
DBDQ     USING DBDQD,R3                                                         
*                                  GET FIRST RECORD FOR QTR HOUR/DAY            
         CLI   DBLSTNXT,0                                                       
         BNE   GETPAV40                                                         
         CLI   0(R3),EOT           TEST END OF DQTAB                            
         BE    GETTPXLA                                                         
****************************************************************                
* HERE WE NEED TO CHANGE THE DQTAB TO THE START QH OF THE FIRST*                
* PROGRAM AND END QH OF THE LAST PROGRAM                       *                
****************************************************************                
*                                                                               
GETPV34A LA    R2,DBKEY                                                         
         USING PRKEY,R2                                                         
         L     R3,ADQNTRY                                                       
         MVC   DBMINKEY(1),DBDQ.DBDQSQH        TIME                             
         ZIC   RE,DBDQ.DBDQKDAY                                                 
         SRL   RE,4                                                             
         STC   RE,DBMINKEY+1                                                    
                                                                                
*                                                                               
         XC    PRKMAJOR,PRKMAJOR                                                
         MVI   PRCODE,PRCODEQU                                                  
         MVC   PRMEDIA,DBINTMED                                                 
         MVC   PRSRC,DBSELSRC                                                   
         MVC   PRSTAT,DBACTSTA                                                  
         MVC   PRKMKT,DBSELMK                                                   
         MVC   PRBOOK,DBSELBK                                                   
         MVC   PRSTYP,DBSTYPE                                                   
*        MVC   PRBTYP,DBBTYPE      PROGRAM NAME HAS NO BTYP                     
         MVI   PRPNMFLG,C'P'                                                    
         XC    PRPNMFLG,=X'40'                                                  
         L     RE,ADQNTRY                                                       
         MVC   PRDAYWK,0(RE)                                                    
         ZIC   RE,PRDAYWK                                                       
         SRL   RE,4                                                             
         STC   RE,PRDAYWK                                                       
         STC   RE,TEMPDAY                                                       
*                                                                               
         MVI   IOFLAG,DIR+HIGH+PAV READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
*                                                                               
         CLC   PRKEY(L'PRKMAJOR),KEYSAVE                                        
         BE    GETPV34B                                                         
         CLI   STALFLAG,C'Y'                                                    
         BNE   GETPAV30                                                         
         GOTOR =A(STALINK),DMCB,(X'01',SAVEBOOK),DBACTSTA,RR=RELO               
         CLC   =X'FFFF',DMCB+8     NO MORE STATIONS IN TABLE                    
         BE    GETPAV30            DONT READ                                    
         MVC   DBSELSTA,DBACTSTA                                                
         B     GETPV34A                                                         
*                                                                               
*                                                                               
*                                                                               
GETPV34B MVI   IOFLAG,FILE+HIGH+PAV                                             
         GOTO1 AIO,IOFLAG                                                       
         TM    DBERROR,EOF                                                      
         BNO   *+12                                                             
         MVI   DBLSTNXT,0                                                       
         B     GETPAV34                                                         
*                                                                               
         L     R2,DBAREC                                                        
         ZIC   R1,PRSTIM           FOR PGM LINEUPS, THIS IS EQH                 
         LA    R4,PRFRSTEL                                                      
         USING PHELEM,R4                                                        
GETPV34C CLI   0(R4),PHCODEQ                                                    
         BE    GETPV34D                                                         
         ZIC   RE,PHELN                                                         
         AR    R4,RE                                                            
         B     GETPV34C                                                         
GETPV34D ZIC   RE,PHDUR            BREAK BOUNDRY PROGRAMS                       
         CR    R1,RE                                                            
         BNL   *+8                                                              
         AHI   R1,96                                                            
         SR    R1,RE                                                            
         STC   R1,DURTAB           QH                                           
         AHI   RE,1                                                             
         STC   RE,DURTAB+1         DURATION                                     
*                                                                               
         CLC   DURTAB(1),DBDQ.DBDQEQH                                           
         BNH   *+12                                                             
         MVI   DBLSTNXT,0                                                       
         B     GETPAV34                                                         
*                                                                               
         CLC   DURTAB(1),DBDQ.DBDQSQH   IF LOWER OR EQUAL TO SQH,               
         BH    GETPV34E                                                         
         MVC   DBDQ.DBDQSQH,DURTAB STORE AS SQH                                 
*        B     GETPV34X                                                         
*                                                                               
GETPV34E CLC   PRSTIM,DBDQ.DBDQEQH IF HIGHER OR EQUAL TO EQH                    
         BL    GETPV34X                                                         
         ZIC   R1,DURTAB                                                        
         STC   R1,DBDQ.DBDQSQH     STORE AS EQH                                 
         ZIC   R1,PRSTIM                                                        
         STC   R1,DBDQ.DBDQEQH                                                  
GETPV34X BAS   RE,SAVEPN           SAVE A COPY OF THIS RECORD                   
         DROP  R2                                                               
         DROP  R4                                                               
*                                                                               
         B     DYTMLX                                                           
************************************************************                    
GETTPXLA CLC   TPTIM2(2),=X'0000'  CHECK FOR SPLIT DAY/TIME                     
         BE    GETTPXLB                                                         
         MVC   DBSELTIM(4),TPTIM2  AND SET IF THER                              
         XC    TPTIM2,TPTIM2                                                    
         MVI   DBLSTNXT,0                                                       
         MVI   DBDQPTR,0                                                        
         XC    DBDQ.DBDQTAB,DBDQ.DBDQTAB                                        
* NEW CODE                                                                      
         XC    SAVEBOOK,=X'FFFF'                                                
         XC    DBACTBK,=X'FFFF'                                                 
         B     GETPAV14                                                         
GETTPXLB CLC   TPSVTIM(2),=X'0000' OTHERWISE RESTORE ORIG IF THERE              
         BE    DYTMC0                                                           
         MVC   DBSELTIM(4),TPSVTIM                                              
*                                                                               
DYTMC0   L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    DYTMEOF                                                          
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     *-20                                                             
         ZIC   RF,DBXTLIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXTLIDX                                                      
         MH    RF,=H'5'                                                         
         LA    RF,DBXTLIST(RF)                                                  
         CLI   0(RF),0                                                          
         BE    DYTMEOF             END OF DAY TIME LIST                         
         MVC   DBSELDAY(5),0(RF)                                                
         MVI   DBLSTNXT,0                                                       
         MVI   DBDQPTR,0                                                        
         XC    DBDQ.DBDQTAB,DBDQ.DBDQTAB                                        
         B     GETPAV14                                                         
DYTMEOF  DS    0H                                                               
         MVI   DBERROR,EOF         YES - SET E-O-F & EXIT                       
         B     GETX                                                             
************************************************************                    
DYTMLX   DS    0H                                                               
         LA    RE,WORKREC                                                       
         LA    RF,2000                                                          
         XCEF                                                                   
*                                                                               
         LA    R2,DBKEY            BUILD KEY OF DEMO RECORD                     
         USING DRKEY,R2                                                         
         XC    DRKMAJOR,DRKMAJOR                                                
         MVI   DRCODE,DRCODEQU                                                  
         MVC   DRMEDIA,DBINTMED                                                 
         MVC   DRSRC,DBACTSRC                                                   
         MVC   DRSTAT,DBACTSTA                                                  
         MVC   DRKMKT,DBACTKMK                                                  
         MVC   DRBOOK,SAVEBOOK                                                  
         XC    DRBOOK,=X'FFFF'                                                  
         MVC   DRSTYP,DBSTYPE                                                   
         MVC   DRBTYP,DBBTYPE                                                   
*                                                                               
         MVI   IOFLAG,DIR+HIGH+DEM READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         CLC   DRKEY(DRBOOK-DRKEY),KEYSAVE                                      
         BNE   GETX                                                             
         MVC   DBMINKEY(1),DBDQ.DBDQKDAY                                        
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BNE   *+8                                                              
         MVI   DBMINKEY,0                                                       
         MVC   DBMINKEY+1(1),DURTAB PROGRAM STARTING QH                         
*                                                                               
GETPAV36 DS    0H                                                               
         ZIC   R0,DURTAB+1                                                      
         MVI   DEMOFLAG,0          SET NO E-O-F HIT                             
*                                                                               
GETPAV37 MVI   IOFLAG,FILE+HIGH+DEM                                             
         B     *+8                                                              
GETPV37A MVI   IOFLAG,FILE+SEQ+DEM                                              
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         L     RE,DBAREC                                                        
         CLC   DRHIGHD-DRKEY(2,RE),DBMINKEY                                     
         BNE   GETPV37X            NOT FOUND?!                                  
*                                                                               
         XC    WORK,WORK           MAD RECORD INTO ASVIREC                      
         LA    RE,DBLOCK                                                        
         ST    RE,WORK                                                          
         MVC   WORK+6(2),DBFACTOR                                               
         MVC   WORK+8(3),DBFILE                                                 
         MVC   WORK+11(3),DBFILE                                                
         MVC   SVINTFIL,DBINTFIL                                                
         MVC   DBINTFIL(2),=C'TO'                                               
         L     RF,DBCOMFCS                                                      
         L     RF,CDEMOMTH-COMFACSD(RF)                                         
         GOTO1 CDEMOMTH,DMCB,=C'ADD',DBAREC,WORKREC,WORK                        
         MVC   DBINTFIL,SVINTFIL                                                
         ZIC   R1,DBMINKEY+1                                                    
         AHI   R1,1                                                             
         STC   R1,DBMINKEY+1                                                    
         CLI   DBMINKEY+1,X'60'                                                 
         BNE   *+12                                                             
         MVI   DBMINKEY+1,0                                                     
         B     GETPV37D                                                         
         CLI   DBMINKEY+1,X'5C'                                                 
         BNE   GETPV37E                                                         
*                                                                               
         ZIC   RE,DBMINKEY                                                      
         SRL   RE,4                                                             
         AHI   RE,1                                                             
         SLL   RE,4                                                             
         STC   RE,DBMINKEY                                                      
         CLI   DBMINKEY,X'80'                                                   
         BNE   GETPV37D                                                         
         MVI   DBMINKEY,X'10'                                                   
         MVC   TEMPBOOK,DRBOOK                                                  
         XC    TEMPBOOK,=X'FFFF'                                                
         L     RE,=V(NSIWEEK)                                                   
         A     RE,RELO                                                          
         ST    RE,VNSIWK                                                        
         GOTO1 VNSIWK,DMCB,(C'D',TEMPBOOK),(1,CGETDAY),CADDAY,CDATCON           
         L     RE,DMCB                                                          
         MVC   DUB(6),0(RE)                                                     
         GOTO1 CADDAY,DMCB,DUB,DUB,7                                            
         GOTO1 VNSIWK,DMCB,DUB,(1,CGETDAY),CADDAY,CDATCON                       
         MVC   TEMPBOOK+1(1),DMCB                                               
         MVC   TEMPBOOK(1),DMCB+4                                               
         XC    TEMPBOOK,=X'FFFF'                                                
         MVC   DRBOOK,TEMPBOOK                                                  
*                                                                               
         MVI   IOFLAG,DIR+HIGH+DEM READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         CLC   DRKEY(DRBOOK-DRKEY),KEYSAVE                                      
         BNE   GETX                                                             
*                                                                               
GETPV37D BCT   R0,GETPAV37                                                      
         B     *+8                                                              
*                                                                               
GETPV37E BCT   R0,GETPV37A                                                      
         ZIC   R0,DURTAB+1                                                      
         ST    R0,WORK+4                                                        
         L     RF,DBCOMFCS                                                      
         L     RF,CDEMOMTH-COMFACSD(RF)                                         
         GOTO1 CDEMOMTH,DMCB,=C'DIV',WORKREC,WORKREC,WORK                       
         BAS   RE,BLDREC                                                        
         B     GETPAV38                                                         
*                                                                               
GETPV37X MVI   DEMOFLAG,1          SET E-O-F HIT                                
         CLI   DBLSTNXT,0          TEST IF FIRST TIME THROUGH                   
         BE    *+8                                                              
         MVI   DBLSTNXT,0          NO -  GET NEXT DQTAB ENTRY                   
         B     GETPAV34                                                         
*                                  CHECK QTR HOUR/DAY                           
GETPAV38 BAS   RE,GETPAVD                                                       
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BE    GETPAV44                                                         
         MVC   WORK(1),DBDQ.DBDQTAB                                             
         NI    WORK,X'F0'                                                       
         CLC   DUB+2(1),WORK       DID I FIND RECORD FOR REQUESTED DAY          
         BNE   GETPV38X             NO                                          
*                                                                               
         CLI   PVNDAYS,0           SEE IF # OF DAYS IS AVAILABLE                
         BE    *+8                  NO, CAN'T MATCH ANYMORE ON DAY CODE         
         CLI   PVSELNDY,0          WAS # OF DAYS REQUESTED?                     
         BE    *+10                 NO                                          
         CLC   PVSELNDY,PVNDAYS     YES, DO # OF DAYS MATCH?                    
         BNE   *+8                   NOPE                                       
         B     GETPAV44                                                         
*                                  DAY CODE MATCHES, NDAYS DOESN'T              
GETPV38X MVI   DEMOFLAG,1          SET E-O-F HIT                                
         B     GETPAV43                                                         
*                                                                               
GETPAV40 DS    0H                                                               
*        CLI   DBBEST,C'A'         TEST IF ALL RECORDS REQUESTED                
*        BE    *+12                                                             
*        CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
*        BNE   GETPAV41                                                         
*        SR    R1,R1                                                            
         B     GETPAV43                                                         
*                                  BUMP TO NEXT QTR HOUR FOR DAY                
GETPAV41 ZIC   R1,DBMINKEY                                                      
         LA    R1,1(R1)            BUMP TO NEXT QTR HOUR                        
         STC   R1,DBMINKEY                                                      
         MVC   DBMINKEY+1(1),DBDQ.DBDQKDAY                                      
         B     GETPAV43                                                         
*                                  SET TO GET NEXT DQTAB ENTRY                  
GETPAV42 MVI   DBLSTNXT,0                                                       
         B     GETPAV34                                                         
*                                                                               
GETPAV43 CLI   DEMOFLAG,0          TEST E-O-F HIT ON PREV READ                  
         BE    DYTMLX              NO - GO READ RECORD                          
         MVI   DBLSTNXT,0          YES - BUMP TO NEXT DQTAB ENTRY               
         B     GETPAV34                                                         
*                                  CHECK QTR HOUR/DAY MATCH                     
GETPAV44 CLC   DUB(1),DBDQ.DBDQEQH                                              
         BH    GETPAV42                                                         
         CLC   DUB+6(1),DBDQ.DBDQSQH                                            
         BL    GETPAV42                                                         
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BE    GETPAV45                                                         
         MVC   WORK(1),DBDQ.DBDQTAB                                             
         NI    WORK,X'F0'                                                       
         CLC   DUB+2(1),WORK       DID I FIND RECORD FOR REQUESTED DAY          
         BNE   GETPAV42                                                         
*                                                                               
         CLI   PVNDAYS,0           SEE IF # OF DAYS IS AVAILABLE                
         BE    GPV44DYX             NO, CAN'T MATCH ANYMORE ON DAY CODE         
         CLI   DUB+2,X'80'         THIS DAY CODE CAN BE 'M-SU' OR 'VAR'         
         BNE   GPV44DYG                                                         
         CLI   DBSELDAY,X'7F'       IF 'M-SU' REQUESTED,                        
         BNE   *+12                                                             
         CLI   PVNDAYS,7             RECD HAD BETTER BE 7 DAYS                  
         BNE   GETPAV40                                                         
         TM    DBSELDAY,X'90'       IF 'VAR' REQUESTED,                         
         BNO   *+12                                                             
         CLI   PVNDAYS,7             RECD HAD BETTER BE LESS THAN 7 DYS         
         BNL   GETPAV40                                                         
GPV44DYG EQU   *                                                                
                                                                                
         CLI   PVSELNDY,0          WAS # OF DAYS REQUESTED?                     
         BE    GPV44DYX             NO                                          
         CLC   PVSELNDY,PVNDAYS     YES, DO # OF DAYS MATCH?                    
         BNE   GETPAV40              NO                                         
GPV44DYX EQU   *                                                                
                                                                                
         DS    0H                                                               
*        CLI   DBBEST,C'A'         TEST IF ALL RECORDS REQUESTED                
*        BNE   GETPAV43                                                         
*                                  CALCULATE NUMBER OF QUARTER HOURS            
GETPAV45 MVI   DBLSTNXT,1                                                       
         CLC   DUB(1),DBDQ.DBDQSQH                                              
         BH    *+10                                                             
         MVC   DUB(1),DBDQ.DBDQSQH                                              
         CLC   DUB+6(1),DBDQ.DBDQEQH                                            
         BNH   GETPAV46                                                         
         MVC   DUB+6(1),DBDQ.DBDQEQH                                            
*        CLI   DBBEST,C'A'         TEST FOR 'ALL' RECORDS                       
*        BE    *+8                 YES-COULD BE MORE RECORDS FOR DAY            
*        MVI   DBLSTNXT,0          NO-FORCE NEXT DQTAB ENTRY                    
*                                                                               
GETPAV46 MVC   DBACTSQC,DUB        RETURN ACTUAL START/END QTR HOUR             
         MVC   DBACTEQC,DUB+6                                                   
*                                  SET FACTOR & RETURN TO CALLER                
GETPAV47 ZIC   R0,DUB                                                           
         ZIC   R1,DUB+6                                                         
         LA    R1,1(R1)                                                         
         SR    R1,R0                                                            
*                                  CALCULATE DAY FACTOR                         
         LA    R0,5                                                             
         CLI   DUB+2,X'00'         TEST M-F                                     
         BE    GETPAV48                                                         
*                                                                               
         CLI   DUB+2,X'90'         TEST VARIOUS                                 
         BE    GETPAV48                                                         
         LA    R0,7                                                             
*                                                                               
         CLI   DUB+2,X'80'         TEST M-S                                     
         BE    GETPAV48                                                         
         LA    R0,1                                                             
*                                                                               
GETPAV48 MR    R0,R0                                                            
         STH   R1,DBFACTOR         SET FACTOR                                   
         LA    R1,1                                                             
         STH   R1,DBDIVSOR         SET TOTAL FACTOR                             
*                                                                               
GETPAV50 MVI   DBRECTYP,DBRECDEM                                                
         MVI   DBMODE,DBMNEXT      NO - SET SEQUENTIAL MODE                     
         MVC   SVINTFIL,DBINTFIL                                                
         BAS   RE,GETX                                                          
         MVC   DBINTFIL(2),SVINTFIL                                             
         ZIC   RE,DUB+6                                                         
         AHI   RE,1                                                             
         STC   RE,DBDQ.DBDQSQH                                                  
*        MVC   DBDQSQH,DUB+6                                                    
         B     GETPV34A                                                         
         DROP  DBDQ                                                             
         EJECT                                                                  
*                                                                               
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
*                                                                               
GETXX    XIT1  ,                   EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
* GET VALUES FROM A PAV DEMO RECORD.                                            
*                                                                               
* ON EXIT DUB+0(1)=START QTR HOUR OF THIS RECORD                                
*         DUB+1(1)=DAY/WEEK OF THIS RECORD                                      
*         DUB+2(1)=DAY OF THIS RECORD                                           
*         DUB+3(1)=DURATION IN QTR HOURS                                        
*         DUB+4(1)=START QTR HOUR OF PREVIOUS RECORD                            
*         DUB+5(1)=DAY/WEEK OF PREVIOUS RECORD                                  
*         DUB+6(1)=END QTR HOUR OF RECORD                                       
*         DUB+7(1)=NUMBER OF WEEKS                                              
*         PVNORMAL='Y' IF NORMAL, 'N' IF FULL CYCLE                             
*                                                                               
GETPAVD  L     RF,DBAREC                                                        
         MVC   DUB(2),PRKMINOR-PRKEY(RF)                                        
         MVC   DUB+2(1),DUB+1                                                   
         NI    DUB+2,X'F0'                                                      
         AH    RF,DBDTADSP                                                      
         USING PHELEM,RF                                                        
         SR    R0,R0                                                            
GETPAVD2 CLI   PHCODE,0            FIND QTR HOUR ELEMENT                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   PHCODE,PHCODEQ                                                   
         BE    *+14                                                             
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETPAVD2                                                         
         ST    RF,DBAQUART                                                      
         MVC   DUB+3(1),PHDUR                                                   
         MVC   DUB+4(1),PHPRVST                                                 
         MVC   DUB+5(1),PHPRVDW                                                 
         MVC   DUB+7(1),PHDWKS                                                  
         NI    DUB+7,X'0F'                                                      
         MVI   PVNORMAL,C'Y'                                                    
         CLI   PHDTYPE,0                                                        
         BE    *+8                                                              
         MVI   PVNORMAL,C'N'                                                    
*                                                                               
         DS    0H                  GET NUMBER OF DAYS                           
         MVI   PVNDAYS,0                                                        
         CLI   PHREVID,X'FF'                                                    
         BNE   *+18                                                             
         CLI   PHREV,2                                                          
         BL    *+10                                                             
         MVC   PVNDAYS,PHNDAYS                                                  
*                                                                               
         DS    0H                                                               
         ZIC   RF,DUB+3                                                         
*                                                                               
         CLI   DBSELMED,C'O'       OVERNIGHT HAS ODD NUMBER OF QTR              
         BNE   *+12                                                             
         CLI   DBSELSRC,C'N'                                                    
         BE    GETPAVD6                                                         
*                                                                               
         SRL   RF,1                FORCE TO EVEN NUMBER OF QTR HRS              
         SLL   RF,1                                                             
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         LA    RF,2                DURATION SHOULD NOT BE LT 2                  
*                                                                               
GETPAVD6 STC   RF,DUB+3                                                         
         ZIC   R0,DUB              GET START QTR HR                             
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         STC   RF,DUB+6            SET END QTR HOUR                             
         ZIC   RF,DUB+7                                                         
         IC    RF,WEEKTAB(RF)                                                   
         STC   RF,DUB+7                                                         
*                                                                               
         DS    0H                                                               
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
* EXTRACT STATION CALL LETTERS FROM DBLOCK INTO DUB                             
*                                                                               
* ON EXIT R1=L'STATION CALL LETTERS (EXCLUDING TYPE)                            
*                                                                               
GETCALL  MVI   DUB,C' '                                                         
         MVC   DUB+1(4),DUB                                                     
         MVC   DUB(4),DBSELSTA                                                  
         LA    R1,4                                                             
         CLI   DBSELMED,C'R'                                                    
         BNER  RE                                                               
         MVC   DUB(5),DBSELSTA     RADIO HAS 5 CHAR CALL LETTERS                
         LA    R1,5                                                             
         BR    RE                                                               
         EJECT                                                                  
*********THIS ROUTINE IS DISABLED PER KATZ 4/23/97*********                     
TIMCHG   CLI   DBTIMCHG,0                                                       
         BR    RE                  DISABLE THIS ROUTINE.                        
         BER   RE                                                               
         CLI   DBFUNCT,DBGETPUR    ALLOW ZERO FOR THIS FUNCTION                 
         BE    *+12                                                             
         OC    DBSELPUR,DBSELPUR   NO PURE - DONT ADJUST                        
         BZR   RE                                                               
         CLI   DBTIMCHG,C'S'       GIVEN SUMMER TIMES                           
         BE    TIMCHG2             ADJUST FALL AND WINTER                       
*                                                                               
* GIVEN A FALL TIME -60 FOR MAY AND JUL                                         
         CLI   DBTIMCHG,C'F'       MAKE SURE FLAG IS VALID                      
         BNER  RE                                                               
         CLI   DBACTBK+1,5         MAY BOOK - NEEDS ADJUSTMENT                  
         BE    *+10                                                             
         CLI   DBACTBK+1,7         JULY BOOK - NEEDS ADJUSTMENT                 
         BNER  RE                   OTHERS DONT                                 
*                                                                               
         CLI   DBSELPUR,4          BOUNDARY CONDITIONS                          
         BH    *+8                                                              
         MVI   DBSELPUR,5                                                       
*                                                                               
         ZIC   R1,DBSELPUR                                                      
         SH    R1,=H'4'                                                         
         STC   R1,DBSELPUR                                                      
         BR    RE                                                               
*                                                                               
* GIVEN A SUMMER TIME +60 MIN FOR ALL EXCEPT MAY/JUL                            
TIMCHG2  CLI   DBACTBK+1,5         MAY BOOK - DONT ADJUST                       
         BER   RE                                                               
         CLI   DBACTBK+1,7         JULY BOOK - DONT ADJUST                      
         BER   RE                                                               
*                                                                               
         CLI   DBSELPUR,93         BOUNDARY CONDITIONS                          
         BL    *+8                                                              
         MVI   DBSELPUR,92                                                      
*                                                                               
         ZIC   R1,DBSELPUR                                                      
         AH    R1,=H'4'                                                         
         STC   R1,DBSELPUR                                                      
         BR    RE                                                               
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
************************************************************                    
* STORE AWAY THE PROGRAM NAME RECORD FOR FUTURE USE                             
************************************************************                    
SAVEPN   NTR1                                                                   
         L     R2,DBAREC                                                        
         USING PRKEY,R2                                                         
         LA    R3,TEMPREC                                                       
         XC    TEMPREC,TEMPREC                                                  
*                                                                               
         LA    RE,PRFRSTEL                                                      
         SR    RE,R2                                                            
SP10     SHI   RE,1                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
         LA    R2,1(RE,R2)                                                      
         CLI   0(R2),0             EOR                                          
         BE    SPX                                                              
         CLI   0(R2),X'21'         OR HIGHER THAN X'21' ELEM                    
         BH    SPX                 WHICH IS IMPOSSIBLE!!!                       
         LA    R3,1(RE,R3)                                                      
         ZIC   RE,1(R2)                                                         
         B     SP10                                                             
SPX      XIT1                                                                   
         DROP  R2                                                               
************************************************************                    
BLDREC   NTR1                                                                   
         LA    R2,TEMPREC                                                       
         USING PRKEY,R2                                                         
         L     R3,DBAREC                                                        
*                                                                               
         L     RE,DBAREC                                                        
         LA    RF,2000                                                          
         XCEF                                                                   
*                                                                               
         MVC   PRBTYP,DBBTYPE       BOOKTYPE                                    
         XC    PRPNMFLG(2),PRPNMFLG TAKE OUT THE LOWER CASE P                   
         ZIC   RE,PRDW                                                          
         SLL   RE,4                                                             
         STC   RE,PRDW                                                          
         MVC   PRSTIM,DURTAB                                                    
*                                                                               
         LA    RE,PRFRSTEL                                                      
         SR    RE,R2                                                            
BR10     SHI   RE,1                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
         LA    R2,1(RE,R2)                                                      
         CLI   0(R2),0             EOR                                          
         BE    BR20                                                             
*                                                                               
         CLI   0(R2),X'20'         PROGRAM NAME DURATION IS "OFF"               
         BNE   BR15                                                             
         USING PHELEM,R2                                                        
         ZIC   R1,PHDUR                                                         
         AHI   R1,1                                                             
         STC   R1,PHDUR                                                         
         DROP  R2                                                               
*                                                                               
BR15     CLI   0(R2),X'21'         OR HIGHER THAN X'21' ELEM                    
         BH    BR20                WHICH IS IMPOSSIBLE!!!                       
         LA    R3,1(RE,R3)                                                      
         ZIC   RE,1(R2)                                                         
         B     BR10                                                             
*                                                                               
BR20     LA    R2,WORKREC                                                       
         USING DRKEY,R2                                                         
         XC    ELEM,ELEM                                                        
         LA    R2,DRFRSTEL                                                      
BR25     CLI   0(R2),X'31'                                                      
         BNL   BR30                                                             
         ZIC   RE,1(R2)                                                         
         AR    R2,RE                                                            
         B     BR25                                                             
*                                                                               
BR30     CLI   0(R2),X'5E'                                                      
         BE    BRX                                                              
         ZIC   RE,1(R2)                                                         
         SHI   RE,1                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         BAS   RE,BRPUTEL                                                       
         ZIC   RE,1(R2)                                                         
         LA    R2,0(RE,R2)                                                      
         B     BR30                                                             
*                                                                               
BRX      DS    0H                                                               
         MVC   ELEM(7),0(R2)                                                    
         BAS   RE,BRPUTEL                                                       
         XIT1                                                                   
*                                                                               
BRPUTEL  NTR1                                                                   
         GOTO1 CHELLO,DMCB,(C'P',=C'DEMFIL'),DBAREC,ELEM                        
         XIT1                                                                   
*                                                                               
         SPACE 1                                                                
WEEKTAB  DC    AL1(0,1,1,2,1,2,2,3,1,2,2,3,2,3,3,4)                             
PONLYTAB DS    0H                                                               
         DC    CL4'KOAT'                                                        
         DC    CL4'KOAT'                                                        
         DC    CL4'KAKW'                                                        
         DC    CL4'WMPB'                                                        
         DC    CL4'WBMA'                                                        
         DC    CL4'WBPX'                                                        
         DC    CL4'KDEN'                                                        
         DC    CL4'WRET'                                                        
         DC    CL4'WEDH'                                                        
         DC    CL4'WTTV'                                                        
         DC    CL4'KAWE'                                                        
         DC    CL4'WFUT'                                                        
         DC    CL4'KETA'                                                        
         DC    CL4'WNJS'                                                        
         DC    CL4'KFPH'                                                        
         DS    CL4'WRPX'                                                        
         DC    CL4'KRJR'                                                        
         DC    CL4'KUTV'                                                        
         DC    CL4'KNIC'                                                        
         DC    CL4'KTNC'                                                        
         DC    CL4'KBTC'                                                        
         DC    CL4'KOED'                                                        
         DC    CL4'WPXW'                                                        
         DC    CL4'KBVO'                                                        
         DC    CL4'KOB '                                                        
         DC    CL4'KRQE'                                                        
         DC    CL4'KTEL'                                                        
         DC    CL4'KWBQ'                                                        
         DC    CL4'WTTO'                                                        
         DC    CL4'KXAN'                                                        
         DC    CL4'KDVR'                                                        
         DC    CL4'WVUA'                                                        
         DC    CL4'WCCO'                                                        
         DC    CL4'WFTC'                                                        
         DC    CL4'KSTP'                                                        
         DC    CL4'WNJN'                                                        
         DC    CL4'KPNX'                                                        
         DC    X'FF'                                                            
EFFS     DC    X'FFFFFFFF'                                                      
ARZERO   DC    16F'0'                                                           
         DROP  RA,RB                                                            
*                                                                               
******************************************************************              
* STATION CALL LETTER LINK CODE                                                 
* CHECK DEMTABS TABLE FOR CALL LETTER LINKS                                     
* ENTRY  - DMCB(1)=00 = FOR OVN/WEEKLY INITIAL CALL.  SET DBACTSTA              
*                       TO CALL LETTERS IN LAST TABLE ENTRY                     
*                                                                               
*          DMCB(1)=01 = FOR OVN/WEEKLY- READ PREVIOUS TABLE ENTRY               
*          DMCB+1(3)=A(BOOK)                                                    
*          DMCB+4=A(STATION OUTPUT)                                             
*          IF NO MORE ENTRIES TO GO BACK DMCB+8(2),=X'FFFF'                     
*                                                                               
* FOR DMCB(1)=00                                                                
* ON EXIT- DBACTSTA IS SET                                                      
* FOR LATEST BOOK LOOKUPS                                                       
*          DBACTSTA IS SET TO LATEST STATION CALL LETTER                        
*          DBSELSTA IS SET TO NEXT TO LATEST STATION CALL LETTER                
*          FOR LATEST BOOK LOOKUP                                               
* OVERNIGHTS/WEEKLY LOOKUPS WILL ALWAYS EXIT WITH ASTALINK POINTING             
* TO THE LAST ENTRY IN THE GROUP                                                
*                                                                               
******************************************************************              
STALINK  NTR1  BASE=*,LABEL=*                                                   
                                                                                
         CLI   DMCB,X'01'                                                       
         BE    STALNKA0                                                         
                                                                                
         MVI   STALFLAG,C'N'                                                    
                                                                                
         L     RF,DBCOMFCS                                                      
         ICM   RF,15,CDEMTABS-COMFACSD(RF)                                      
         GOTO1 (RF),DMCB,CALL_LNK                                               
         ICM   RE,15,0(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     R0,4(R1)            R0 HAS LENGTH OF TABLE ENTRY                 
         USING CALLLNKD,RE                                                      
STALNK30 CLI   0(RE),X'FF'         EOT                                          
         BE    STALINKX                                                         
*                                                                               
STALNK40 CLC   CALLSTAT,DBSELSTA                                                
         BE    STALNK42                                                         
         AR    RE,R0                                                            
         B     STALNK30                                                         
*  A LATEST BOOK LOOKUP?                                                        
STALNK42 MVI   STALFLAG,C'Y'                                                    
         OC    DBSELBK,DBSELBK                                                  
         BZ    STALNK64                                                         
*                                                                               
* LETS ALWAYS GO TO THE LATEST STATION CALL LETTER.  THE CODE WILL              
* GO TO THE PREVIOUS LINK IN TABLE IF STATION IS NOT FOUND                      
* ITS TOO COMPLICATED GIVEN THAT CALL LETTER CHANGES FOR OVERNIGHTS             
* HAPPENS IN THE MIDDLE OF A WEEK AT TIMES- (CROSSING DIRECTORY MAJOR           
* KEYS).                                                                        
*                                                                               
STALNK50 DS    0H                                                               
*                                                                               
*  LOOK FOR LATEST CALL LETTER CHANGE ENTRY                                     
STALNK64 CLI   0(RE),X'FF'        SOMETHING IS REALLY WRONG IF                  
         BNE   *+6                FFFF ENTRY NOT FOUND IN TABLE                 
         DC    H'0'                                                             
         CLC   CALLEBK,=X'FFFF'                                                 
         BE    STALNK80                                                         
         AR    RE,R0                                                            
         B     STALNK64                                                         
STALNK80 MVC   DBACTSTA,CALLSTAT  DBACTSTA=LATEST CALL LETTER                   
         MVI   DBACTSTA+4,C'T'                                                  
         ST    RE,ASTALINK        SAVE ADDRESS OF LAST ENTRY                    
         ST    R0,LSTALINK        L' STALINK TABLE ENTRY                        
         LR    RF,RE              RF= LATEST ENTRY IN TABLE                     
STALNK90 MVC   DBSELSTA,CALLSTAT  DBSELSTA=NEXT  LATEST CALL LETTER             
         MVI   DBSELSTA+4,C'T'                                                  
         B     STALINKX                                                         
                                                                                
*  BUMP TO PREVIOUS TABLE ENTRY                                                 
STALNKA0 L     RE,DMCB                                                          
         MVC   INBOOK(2),0(RE)                                                  
         L     RF,DMCB+4           STATION OUTPUT AREA                          
         L     RE,ASTALINK                                                      
         USING CALLLNKD,RE                                                      
         XC    DMCB+8(2),DMCB+8                                                 
STALNKB0 CLC   CALLSTBK,=AL2(FROM_BEGINNING)                                    
         BE    STALNKF0                                                         
         L     R0,LSTALINK                                                      
         SR    RE,R0                                                            
         ST    RE,ASTALINK                                                      
         MVC   0(4,RF),CALLSTAT                                                 
         MVI   4(RF),C'T'                                                       
         OC    INBOOK,INBOOK       IF LATEST LOOKUP FAILED JUST BUMP            
         BZ    STALINKX            BACK ONE ENTRY AND RETRY                     
                                                                                
         CLC   INBOOK,CALLOSBK     NO NEED TO READ ANY FURTHER BACK             
         BL    STALNKE0                                                         
         CLC   INBOOK,CALLOEBK                                                  
         BH    STALNKE0                                                         
         B     STALINKX                                                         
STALNKE0 B     STALNKB0                                                         
                                                                                
STALNKF0 MVC   DMCB+8(2),=X'FFFF'  INDICATE NO MORE TABLE ENTRIES               
                                                                                
STALINKX XIT1  ,                                                                
*INBOOK   DS    XL(L'DBSELBK)       STORAGE PROTECTION VIOLATION                
         DROP  RE,RB                                                            
         LTORG                                                                  
*********************************************************************           
         DROP  R6,R8,R9,RC                                                      
         EJECT                                                                  
*                                                                               
*                                                                               
       ++INCLUDE DEGETD                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE DEDBEXTRAD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDMONYREQU                                                     
         SPACE 1                                                                
         PRINT ON                                                               
         TITLE 'DEGETPAV - PROGRAM AVERAGES READ CONTROLLER (LOCAL WORK+        
               AREA)'                                                           
***********************************************************************         
*====================== GETPA2'S LOCAL WORK AREA =====================*         
                                                                                
PAVWORKD DSECT                                                                  
                                                                                
PVSELNDY DS    XL1                 REQUESTED NUMBER OF DAYS FOR AVG-N           
PVNDAYS  DS    XL1                 NUMBER OF DAYS FROM RECORD                   
*                                                                               
TPSVTIM  DS    XL4                                                              
TPTIM2   DS    CL4                                                              
*                                                                               
SVINTFIL DS    CL2                                                              
TEMPBOOK DS    XL2                                                              
STALFLAG DS    C                                                                
ASTALINK DS    A                                                                
LSTALINK DS    F                                                                
*                                                                               
MAXPGM   EQU   96                  EVEN AT 1 PGM PER QH, MAX PER DAY            
MAXPNLEN EQU   15                                                               
DURTAB   DS    XL(DURTABL)          SQH,DURATION                                
DURTABL  EQU   2                                                                
PREVPGM  DS    XL(DURTABL)          SQH,DURATION OF PREVIOUS READ PGM           
TEMPDUR  DS    XL1                                                              
TEMPDAY  DS    XL1                                                              
VNSIWK   DS    V                                                                
INBOOK   DS    XL(L'DBSELBK)                                                    
ELEM     DS    0XL255                                                           
TEMPREC  DS    XL255                                                            
         ORG                                                                    
WORKREC  DS    XL2000                                                           
PAVWORKX EQU   *                                                                
PAVWORKL EQU   PAVWORKX-PAVWORKD                                                
*                                                                               
*                                                                               
***********************************************************************         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'047DEGETPA2  11/19/15'                                      
         END                                                                    
