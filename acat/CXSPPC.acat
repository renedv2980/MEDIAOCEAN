*          DATA SET CXSPPC     AT LEVEL 017 AS OF 05/01/02                      
*CATALP CXSPPC                                                                  
         TITLE 'CXSPPC - CONTROL SEC. EXTRACT - PERSON PROGRAM RECORD'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSPPC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSPPC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSPPD,R2           R2=A(EXTRACT RECORD)                         
         USING SA0REC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSPPLEN,CXSPPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSPPLEN(2),=AL2(CXSPPDL)                                        
         MVC   CXSPPTYP,CXSPPDQ                                                 
         MVI   CXSPPDX,MXTRTQ                                                   
         MVI   CXSPPDX+1,MXTRTQ                                                 
*                                                                               
CXSAID   MVI   CXSPPAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSPPAID,SA0KAGY                                                 
*                                                                               
CXSNUM   MVI   CXSPPNUM-1,MXTRTQ   PERSON NUMBER                                
         SR    RF,RF                                                            
         ICM   RF,3,SA0KNUM                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSPPNUM,DUB+5(3)                                                
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   CXSPPACT,C'A'       GOTO ADD PROCESS                             
         B     CXSSYS                                                           
CXSACT1  CLI   CXSPPACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPPACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPPACT,C'A'       IF ADD DO 'K' DELETE ALL                     
         BE    CXSACT2                                                          
         B     CXSSYS              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSPPACT   SAVE THIS ACTION                             
         MVI   CXSPPACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
         MVI   CXSPPSYS-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         MVI   CXSPPPGM-1,MXTRTQ                                                
         MVI   CXSPPLAC-1,MXTRTQ                                                
         MVI   CXSPPAUT-1,MXTRTQ                                                
         CLI   SAVEACTN,C'D'                                                    
         BNE   RETEXIT             IF NOYT DELETE RETURN FOR ADDS               
         B     DELEXIT             ELSE EXIT WITH JUST DELETE                   
*                                                                               
         USING SPATABD,R4                                                       
CXSSYS   MVI   CXSPPSYS-1,MXTRTQ                                                
         MVI   CXSPPPGM-1,MXTRTQ                                                
         MVI   CXSPPLAC-1,MXTRTQ                                                
         MVI   CXSPPAUT-1,MXTRTQ                                                
         ICM   R4,15,ASPATAB       GET LAST ADDRESS SPATAB ENTRY                
         BZ    *+12                                                             
         LA    R4,SPATLEN(R4)      BUMP ADDRESS SPATAB ENTRY                    
         B     CXSS010                                                          
         ICM   R4,15,=V(CXSSPAT)   ELSE INITIALISE TO START                     
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CXSS010  CLI   SPATOVS,0                                                        
         BE    EOFEXIT                                                          
         ST    R4,ASPATAB                                                       
*                                  SYSTEM CODE                                  
         GOTO1 XHEXOUT,DMCB,SPATOVS,CXSPPSYS,1,=C'TOG'                          
*                                  PROGRAM CODE                                 
         GOTO1 XHEXOUT,DMCB,SPATPGM,CXSPPPGM,1,=C'TOG'                          
         MVC   CXSPPLAC,ZEROES                                                  
         MVC   CXSPPAUT,ZEROES                                                  
         LA    R5,SA0DATA          R4=A(FIRST ELEMENT)                          
*                                                                               
         USING CTSYSD,R5                                                        
CXSS020  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXSSNO                                                           
         CLI   0(R5),CTSYSELQ      GET SYSTEM ELEMENT                           
         BNE   CXSS030                                                          
         CLC   CTSYSNUM,SPATOVS                                                 
         BE    CXSS040                                                          
CXSS030  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BH    *+6                                                              
         DC    H'00'                                                            
         AR    R5,RF                                                            
         B     CXSS020                                                          
*                                                                               
CXSS040  GOTO1 XHEXOUT,DMCB,CTSYSLMT,CXSPPLAC,4,=C'TOG'                         
         MVC   CXSPPLAC+8(8),ZEROES                                             
         MVC   PROGRAM,SPATPGM                                                  
         BAS   RE,GETPAVAL                                                      
         OC    PACCVAL,PACCVAL                                                  
         BZ    CXSSNO                                                           
         GOTO1 XHEXOUT,DMCB,PACCVAL,CXSPPAUT,2,=C'TOG'                          
         B     RETEXIT                                                          
         DROP  R5                                                               
*                                                                               
CXSSNO   MVC   CXSPPAUT,ZEROES                                                  
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    ASPATAB,ASPATAB                                                  
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         B     EXIT                                                             
*                                                                               
DELEXIT  XC    ASPATAB,ASPATAB                                                  
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),0             SET EOF AND WRITE 'K' TYPE RECORD            
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
* SUBROUTINE TO GET PROGRAM ACCESS CODE FROM SYSTEM ELEMENT                     
* PROGRAM CODE IN PROGRAM, ACCESS CODE IN PACCVAL                               
* R5=A(SYSTEM ELEMENT)                                                          
*                                                                               
         USING CTSYSD,R5                                                        
GETPAVAL NTR1                                                                   
         MVC   PACCVAL,CTSYSALL                                                 
         LA    R1,CTSYSPGM                                                      
         ZIC   RE,CTSYSLEN                                                      
*                                                                               
GPAV010  CH    RE,=H'16'                                                        
         BNH   GPAVNO                                                           
         CLC   PROGRAM,0(R1)                                                    
         BE    GPAVYES                                                          
         LA    R1,3(R1)            GET NEXT PROGAM CODE                         
         SH    RE,=H'3'                                                         
         B     GPAV010                                                          
*                                                                               
GPAVNO   B     GPAVX                                                            
GPAVYES  MVC   PACCVAL,1(R1)       SAVE ACCESS CODE                             
         ST    R1,PACCADR          SAVE ADDRESS OF ENTRY IN ELEMENT             
         SR    RC,RC                                                            
GPAVX    LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
ASPATAB  DC    A(0)                                                             
XHEXOUT  DC    V(HEXOUT)                                                        
         SPACE 1                                                                
SPACES   DC    80C' '                                                           
ZEROES   DC    80C'0'                                                           
SAVEACTN DC    CL1' '                                                           
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                                                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
PACCADR  DS    A                                                                
PROGRAM  DS    XL1                                                              
PACCVAL  DS    XL2                                                              
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSPPD                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         SPACE 1                                                                
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017CXSPPC    05/01/02'                                      
         END                                                                    
