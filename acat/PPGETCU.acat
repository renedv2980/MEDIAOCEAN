*          DATA SET PPGETCU    AT LEVEL 018 AS OF 08/15/02                      
*CATALP PPGETCU                                                                 
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         TITLE 'PPGETCU-MODULE TO FIND CU VALUES FROM PSCURECS'                 
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
* NOTE THAT THIS MODULE READS PRINT FILE, WILL DESTROY SEQUENCIAL READS         
*                                                                               
* FOR AOR SITUATIONS BE SURE YOU ARE CONNECTED TO THE AOR                       
* IF THE CONTRACT IS AN AOR CONTRACT                                            
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
* PARAMETER LIST                                                                
*                                                                               
* INPUT                                                                         
*                                                                               
* 1       A(PBUYREC)                                                            
*                                                                               
* 2       A(CONREC)                                                             
*                                                                               
* 3       A(DATAMGR)                                                            
*                                                                               
* OUTPUT                                                                        
*                                                                               
* 1       X'FF' IF ERROR                                                        
*                                                                               
* 2       CU VALUE (4 DECIMALS) FROM PBUYREC IF THERE OR FROM PSCUREC           
*                                                                               
* NOTE: PRIMARY LEVEL INDICATOR OF CONTRACT IS USED FOR CU VALUE                
* LOOK-UP. IF IT IS NOT X,P,S,$(N), NO DATA WILL BE RETRIEVED                   
*                                                                               
* IF X'00000001' ZERO WAS ENTERED PBUYREC OR PSCUREC                            
* IF X'00000000' NO CU VALUE WAS FOUND                                          
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
PPGETCU  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPGETCU,CLEAR=YES                                    
         USING WORKD,RC                                                         
         LR    R8,R1               R8=A(PARAMETER LIST)                         
         LM    R2,R3,0(R1)         A(PBUYREC),A(PCONREC)                        
*                                                                               
         MVC   DATAMGR,8(R8)       V(DATAMGR)                                   
         XC    4(4,R1),4(R1)       USED TO RETURN CU VALUE                      
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         USING PBUYRECD,R2                                                      
         USING PCONRECD,R3                                                      
*                                                                               
         OC    PBDCU,PBDCU         SEE IF BUY HAS A CU                          
         BZ    GETCU2                                                           
         MVC   5(3,R8),PBDCU       ALWAYS USE IT                                
         B     GETCUX                                                           
*                                                                               
GETCU2   LA    R6,PCONREC+33                                                    
         MVI   ELCODE,X'20'                                                     
         BRAS  RE,NXTELEM                                                       
         BNE   GETCUX              NO RATE ELEM FOUND - JUST EXIT               
*                                                                               
         MVI   CONTYP,C'X'                                                      
         CLI   PRBLIND,C'X'        LEVEL INDICATOR IS X?                        
         BE    GETCU3                                                           
         MVI   CONTYP,C'P'                                                      
         CLI   PRBLIND,C'P'        LEVEL INDICATOR IS P?                        
         BE    GETCU3                                                           
         MVI   CONTYP,C'S'                                                      
         CLI   PRBLIND,C'S'        LEVEL INDICATOR IS S?                        
         BE    GETCU3                                                           
         MVI   CONTYP,C'$'                                                      
         CLI   PRBLIND,C'$'        LEVEL INDICATOR IS $?                        
         BE    GETCU3                                                           
         CLI   PRBLIND,C'N'        LEVEL INDICATOR IS $ (NET)?                  
         BE    GETCU3                                                           
*                                                                               
         B     GETCUX              DONE, PRIMARY LEVEL IS NOT X,P,S,$           
*                                                                               
GETCU3   DS    0H                                                               
         MVC   WORK(17),SPACES                                                  
*                                                                               
GETCU3C  CLI   PBUYKMED,C'N'       SEE IF NEWSPAPERS                            
         BE    GETN                                                             
         CLI   PBUYKMED,C'O'       SEE IF OUTDOOR                               
         BE    GETO                                                             
*                                                                               
GETM     DS    0H                  MAGAZINES/SUPPLEMENTS/TRADE                  
         CLI   PBDSPACE,C'#'                                                    
         BNE   GETM5                                                            
         MVC   WORK(16),PBDSPACE+1                                              
         B     GETM7                                                            
*                                                                               
GETM5    MVC   WORK(17),PBDSPACE                                                
*                                                                               
GETM7    OC    WORK(17),SPACES                                                  
         B     GETALL                                                           
*                                                                               
GETN     DS    0H                  NEWSPAPERS                                   
         CLI   PBDSPACE,C'#'                                                    
         BNE   GETN5                                                            
         MVC   WORK(7),PBDSPACE+1                                               
         B     GETN7                                                            
*                                                                               
GETN5    MVC   WORK(8),PBDSPACE                                                 
GETN7    OC    WORK(17),SPACES                                                  
         B     GETALL                                                           
*                                                                               
GETO     DS    0H                  OUTDOOR                                      
         MVC   WORK(10),PBDSPACE                                                
         CLI   WORK,X'FF'          SEE IF SRI GIVEN                             
         BE    GETALL                                                           
         LA    R6,PBUYREC+33                                                    
         MVI   ELCODE,X'66'                                                     
         BRAS  RE,NXTELEM                                                       
         BNE   GETCUX              NO SPACE ELEM FOUND                          
         CLI   1(R6),19                                                         
         BH    GETCUX                                                           
*                                                                               
         ZIC   R4,1(R6)                                                         
         AHI   R4,-3                                                            
         BM    GETCUX                                                           
         LA    R7,2(R6)                                                         
         CLI   2(R6),C'#'                                                       
         BNE   GETO5                                                            
         LA    R7,1(R7)                                                         
         AHI   R4,-1                                                            
         BM    GETCUX                                                           
*                                                                               
GETO5    EX    R4,*+8                                                           
         B     *+10                                                             
*                                                                               
         MVC   WORK(0),0(R7)                                                    
         OC    WORK(17),SPACES                                                  
*                                                                               
GETALL   DS    0H                  WORK HAS SPACE DESCRIPTION                   
         LA    R9,KEY                                                           
         USING PSCUREC,R9                                                       
         XC    PSCUKEY,PSCUKEY                                                  
         MVC   PSCUKAGY(3),PCONKAGY                                             
         MVI   PSCUKTYP,X'28'                                                   
         MVC   PSCUKPUB,PCONKPUB                                                
         MVC   PSCUKCLT,PCONKCLT                                                
*                                                                               
GETCU4   MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PRTDIR,KEY,KEY                           
         B     GETCU7                                                           
*                                                                               
GETCU5   GOTO1 DATAMGR,DMCB,(0,DMRSEQ),PRTDIR,KEY,KEY                           
*                                                                               
GETCU7   CLC   KEY(4),KEYSAVE      CK THROUGH RECORD TYPE                       
         BNE   GETCUX                                                           
         CLI   KEY+4,X'FF'         SEE IF "ALL" PUB RECORD FOUND                
         BE    GETCU7H             GO CK CLIENT                                 
         CLC   KEY+4(6),KEYSAVE+4  CK FOR FULL PUB MATCH                        
         BE    GETCU7H                                                          
         CLC   KEY+4(4),KEYSAVE+4  CK FOR BASE PUB NUMBER                       
         BNE   GETCU7C             NO - GO READ FOR "ALL" PUBREC                
         CLC   KEY+8(2),=X'FFFF'   SEE IF ALL/ZONE/EDT RECORD FOUND             
         BE    GETCU7H             YES GO CHK CLIENT                            
*                                                                               
         MVC   KEY+8(2),=2X'FF'    TRY FOR ALL ZONE/EDT REC                     
         MVC   KEY+10(3),PBUYKCLT                                               
         B     GETCU4                                                           
*                                                                               
         B     GETCU5                                                           
*                                                                               
GETCU7C  XC    KEY+4(28),KEY+4     GO TRY FOR "ALL" PUB PSCUREC                 
         MVC   KEY+4(6),=6X'FF'                                                 
         MVC   KEY+10(3),PCONKCLT                                               
         B     GETCU4                                                           
*                                                                               
GETCU7H  DS    0H                                                               
         CLC   KEY+10(3),KEYSAVE+10                                             
         BE    GETCU10             PROCESS CLIENT                               
         CLC   KEY+10(3),=3X'FF'   "ALL" CLT REC FOUND?                         
         BE    GETCU10             YES - PROCESS                                
         CLI   KEYSAVE+10,X'FF'    LOOKING FOR ALL CLT REC?                     
         BE    GETCUX              IF SO STOP LOOKING                           
         MVC   KEY+10(3),=3X'FF'                                                
         B     GETCU4                                                           
         DROP  R9                                                               
*                                                                               
* READ THIS RECORD                                                              
*                                                                               
GETCU10  GOTO1 DATAMGR,DMCB,GETREC,PRTFIL,KEY+27,IO,DMWORK                      
*                                                                               
         LA    R7,IO                                                            
         USING PSCUREC,R7                                                       
         LA    R6,PSCUELEM                                                      
*                                                                               
         CLI   0(R6),0             NO ELEMENTS?                                 
         BE    GETCUX                                                           
*                                                                               
         CLI   CONTYP,C'S'         LEVEL INDICATOR IS S?                        
         BE    *+12                                                             
         CLI   CONTYP,C'$'         LEVEL INDICATOR IS $?                        
         BNE   GETCU14                                                          
*                                                                               
         MVI   ELCODE,X'30'        CONTRACT LEVEL ELEM IN CUREC                 
         CLC   ELCODE,0(R6)        FIRST ELEM IS IT?                            
         BE    *+12                                                             
         BRAS  RE,NXTELEM                                                       
         BNE   GETCUX              NOT FOUND, DONE WITH LEVEL IND S,$           
*                                                                               
         OC    2(2,R6),2(R6)       BOTH LEVEL IND ARE NULL?                     
         BNZ   *+6                                                              
         DC    H'0'                BAD PSCUEL30 ELEM                            
*                                                                               
         USING PSCUEL30,R6                                                      
         CLI   CONTYP,C'S'         LEVEL INDICATOR IS S?                        
         BNE   *+14                                                             
         MVC   CONTYP,PSSLVCON     LEVEL IND TO BE FOR S                        
         B     GETCU12                                                          
         CLI   CONTYP,C'$'         LEVEL INDICATOR IS $?                        
         BNE   *+14                                                             
         MVC   CONTYP,PSDLVCON     LEVEL IND TO BE FOR $                        
         B     GETCU12                                                          
         DROP  R6                                                               
*                                                                               
         DC    H'0'                INVALID CONTYP SWITCH                        
*                                                                               
GETCU12  CLI   CONTYP,0            NO LEVEL INDICATOR TO USE?                   
         BE    GETCUX              DONE                                         
         LA    R6,PSCUELEM         POINT TO BEGINNING AGAIN                     
*                                                                               
GETCU14  MVI   ELCODE,X'01'                                                     
         USING PSCUEL01,R6                                                      
         CLC   ELCODE,0(R6)        FIRST ELEM IS IT?                            
         BE    GETCU20                                                          
*                                                                               
GETCU15  BRAS  RE,NXTELEM                                                       
         BNE   GETCU50                                                          
*                                                                               
GETCU20  CLC   PSCUDESC,WORK       SPACE DESCRIPTION MATCHED?                   
         BE    GETCU30                                                          
         B     GETCU15                                                          
*                                                                               
GETCU30  MVC   5(3,R8),PSCUPCU     USE P (PAGE)                                 
         CLI   CONTYP,C'P'                                                      
         BE    GETCUX                                                           
         MVC   5(3,R8),PSCUXCU     USE X (TIME)                                 
         B     GETCUX                                                           
*                                                                               
GETCU50  DS    0H                  SPACE NOT FOUND IN THE PSCUREC               
         CLC   PSCUKPUB(9),=9X'FF' PROCESSING ALL PUB/ALL CLT RECORD?           
         BE    GETCUX                                                           
         CLC   PSCUKCLT,=3X'FF'    PROCESSING AN "ALL" CLT REC?                 
         BE    GETCU60                                                          
         MVC   KEY+10(3),=3X'FF'   TRY FOR "ALL" CLT RECORD                     
         B     GETCU4              LEAVING PUB ALONE                            
*                                                                               
GETCU60  DS    0H                                                               
         CLC   PSCUKPUB+4(2),=2X'FF'                                            
         BE    GETCU65             PROCESSING ALL Z/E                           
         MVC   KEY+8(2),=2X'FF'    TRY FOR ALL Z/E RECORD                       
         MVC   KEY+10(3),PCONKCLT  FOR MY CLIENT                                
         B     GETCU4                                                           
*                                                                               
GETCU65  MVC   KEY+4(6),=6X'FF'    TRY FOR ALL PUB CLT RECORD                   
         MVC   KEY+10(3),PCONKCLT  MY CLIENT                                    
         B     GETCU4              GO TRY FOR IT                                
*                                                                               
GETCUERR MVI   0(R8),X'FF'         ERROR                                        
         B     GETCUX                                                           
*                                                                               
GETCUX   XIT                                                                    
         EJECT                                                                  
*                                                                               
NXTELEM  ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLC   ELCODE,0(R6)                                                     
         JE    NXTELX              CC IS EQUAL                                  
         CLI   0(R6),0                                                          
         JNE   NXTELEM                                                          
         LTR   R6,R6               CC IS NOT EQUAL                              
NXTELX   BR    RE                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
PRTDIR   DC    C'PRTDIR  '                                                      
PRTFIL   DC    C'PRTFILE '                                                      
*                                                                               
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
WORKD    DSECT                     ** WORKING STORAGE **                        
WORK     DS    CL20                                                             
SPACES   DS    CL20                                                             
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
DMWORK   DS    12D                                                              
ELCODE   DS    CL1                                                              
CONTYP   DS    CL1                                                              
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
IO       DS    4000C                                                            
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
         PRINT OFF                                                              
*                                                                               
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE PBDELEM                                                        
         EJECT                                                                  
*                                                                               
PCONRECD DSECT                                                                  
       ++INCLUDE PCONREC                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE PSCUREC                                                        
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018PPGETCU   08/15/02'                                      
         END                                                                    
