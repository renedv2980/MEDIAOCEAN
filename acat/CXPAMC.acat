*          DATA SET CXPAMC     AT LEVEL 034 AS OF 02/03/95                      
*CATALP CXPAMC                                                                  
         TITLE 'CXPAMC - CONTROL EXTRACT - PERSON APPROVER GROUPS'              
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXPAMC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXPAMC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXPAMD,R2           R2=A(EXTRACT RECORD)                         
         USING SAPEREC,R3          R3=A(CONFILE RECORD)                         
*                                                                               
CXPLEN   XC    CXPAMLEN,CXPAMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXPAMLEN(2),=AL2(CXPAMDL)                                        
         MVC   CXPAMTYP,CXPAMDQ                                                 
         MVI   CXPAMDX,MXTRTQ                                                   
         MVI   CXPAMDX+1,MXTRTQ                                                 
*                                                                               
CXPAGY   MVI   CXPAMAGY-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXPAMAGY,SAPEAGY                                                 
*                                                                               
CXPPID   MVI   CXPAMEFD-1,MXTRTQ   UNIT                                         
         MVC   EFFDATE,SAPEDEF                                                  
         XC    EFFDATE,FFILL                                                    
         GOTO1 VDATCON,DMCB,(2,EFFDATE),(10,CXPAMEFD)                           
*&&US                                                                           
         XC    CXPAMEFD(2),CXPAMEFD+3                                           
         XC    CXPAMEFD+3(2),CXPAMEFD                                           
         XC    CXPAMEFD(2),CXPAMEFD+3                                           
*&&                                                                             
*                                                                               
         MVI   CXPAMPER-1,MXTRTQ                                                
         MVC   CXPAMPER,SPACES                                                  
         MVI   CXPAMEID-1,MXTRTQ                                                
         MVC   CXPAMEID,SPACES                                                  
         MVI   CXPAMAGR-1,MXTRTQ                                                
         MVC   CXPAMAGR,SPACES                                                  
*                                                                               
         LA    R4,SAPEDATA         R4=A(FIRST ELEMENT)                          
*                                                                               
CXPELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXPNXTX                                                          
         CLI   0(R4),SAPWDELQ      GET PERSON PASSWORD ELEMENT                  
         BE    CXPPWD                                                           
         CLI   0(R4),SAPERELQ      GET PERSON DETAILS ELEMENT                   
         BE    CXPPER                                                           
         CLI   0(R4),SAAPCELQ      GET APPROVER GROUP CODE ELEMENT              
         BE    CXPAPC                                                           
CXPNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXPELM                                                           
*                                                                               
         USING SAPWDD,R4                                                        
CXPPWD   SR    RF,RF               PERSON NUMBER                                
         ICM   RF,3,SAPWDNUM                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXPAMPER,DUB+5(3)                                                
         B     CXPNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAPERD,R4                                                        
CXPPER   OC    SAPERPRO,SAPERPRO   PERSON EMAIL OR PROFS ID                     
         BZ    CXPNXT                                                           
         MVC   CXPAMEID,SAPERPRO                                                
         TR    CXPAMEID,MXTRT                                                   
         B     CXPNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAAPCD,R4                                                        
CXPAPC   MVC   CXPAMAGR,SAAPCCOD   PERSON APPROVER GROUP CODE                   
         B     CXPNXT                                                           
         DROP  R4                                                               
*                                                                               
CXPNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    80C' '                                                           
FFILL    DC    80X'FF'                                                          
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXRECID                                                        
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
VDATCON  DC    V(DATCON)                                                        
         DS    0D                                                               
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
EFFDATE  DS    XL2                                                              
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXPAMD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'034CXPAMC    02/03/95'                                      
         END                                                                    
