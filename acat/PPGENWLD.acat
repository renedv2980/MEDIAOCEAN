*          DATA SET PPGENWLD   AT LEVEL 004 AS OF 09/25/01                      
*CATALP PPGENWLD                                                                
***********************************************************************         
*  *******  COPY OF REGENWLD (REPPAK ROUTINE) MADE 09/14/01  *******  *         
***********************************************************************         
         TITLE 'PPGENWLD' - PRTPAK WILDCARD CHARACTER HANDLER ROUTINE           
*              THIS ROUTINE RETURNS A START STRING TO BE USED FOR AN            
*              INITIAL READ HIGH AND FOR COMPARING AS A TEST FOR A SEQ          
*              LOOP EXIT CONDITION. ROUTINE IS THEN USED FOR FILTERING          
*              RECORDS ACCORDING TO THE WILDCARD CRITERIA                       
*                                                                               
*              PARAMETER 1 =  BYTE 0   MAX LENGTH OF WILDCARD STRING            
*                             BYTE 1-3 A(FILTER TEXT STRING)                    
*                                                                               
*              PARAMETER 2 =  IF PARAM 3 BYTE 0 = X'00'                         
*                             BYTE 0   MAX LEN OF STRING TO BE TESTED           
*                             BYTE 0   X'80' ON RETURN IF STRING MATCH          
*                             BYTE 1-3 A(STRING TO BE TESTED)                   
*                                    --- OR ---                                 
*                             IF PARAM 3 BYTE 0 =X'80'                          
*                             BYTE 0   MAX LEN OF START STRING                  
*                             BYTE 0   ON RETURN #OF SIGNFICANT CHARS           
*                             BYTE 1-3 A(AREA TO RETURN START STRING)           
*                                                                               
*              PARAMETER 3 =  BYTE 0   X'80' CREATE SEARCH START STRING         
*                                      X'00' FILTER STRING                      
*                                                                               
**********************************************************************          
*          PPGENWLD -- WILDCARD FILTERING ROUTINE                    *          
**********************************************************************          
*                                                                    *          
* 10OCT96 RHV  VOILA!                                                *          
*                                                                    *          
*                                                                    *          
**********************************************************************          
* PROGRAMMER WARNING: THIS CODE IS BASED ON A VERY COMPLICATED       *          
*                     ALGORITHM & THE CODE IS RATHER DIFFICULT TO    *          
*                     FOLLOW UNLESS YOU ARE FOLLOWING A DIAGRAM OF   *          
*                     THE ALGORITHM USED. KINDLY DO NOT SCREW AROUND *          
*                     WITH THIS UNLESS YOU ARE SURE YOU KNOW WHAT    *          
*                     YOU ARE DOING. THIS ROUTINE IS A REAL PAIN IN  *          
*                     THE ASS TO DEBUG. I HAVE MADE A FLOWCHART OF   *          
*                     THE LOGIC USED SHOULD THE NEED ARISE. - RHV    *          
**********************************************************************          
*                                                                               
*                                                                               
*                                                                               
PPGENWLD CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),**WLD**                                           
         USING WORKD,RC                                                         
*                                                                               
**** MAIN PROGRAM ***                                                           
MAIN     DS    0H                                                               
         ST    R1,ADMCB            SAVE DMCB ADDRESS                            
         MVC   LENWILD,0(R1)       SAVE LEN OF WILDCARD STRING                  
         ZICM  R2,1(R1),3                                                       
         ST    R2,AWILD            SAVE A(WILDCARD STRING)                      
         MVC   LENFIELD,4(R1)      SAVE LEN OUTPUT FIELD                        
         ZICM  R2,5(R1),3                                                       
         ST    R2,AFIELD           SAVE A(OUTPUT FIELD)                         
MAIN010  DS    0H                                                               
         TM    8(R1),X'80'    REQUEST FOR START STRING?                         
         BZ    MAIN020        NO                                                
         BAS   RE,START       YES - DO START STRING ROUTINE                     
         B     XIT                                                              
MAIN020  BAS   RE,FILTER                                                        
XIT      XIT1                                                                   
*                                                                               
START    DS    0H                                                               
         NTR1                                                                   
         L     R2,AWILD            WILDCARD STRING                              
         ZIC   R3,LENWILD          LENGTH OF WILDCARD STRING                    
         L     R4,AFIELD           OUTPUT FIELD                                 
         SR    R5,R5               CLEAR LEN OF OUTPUT FIELD                    
*                                                                               
         LR    R6,R3               OR WILD STRING W/SPACES                      
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),SPACES                                                   
*                                                                               
         LR    R6,R5               INITIALIZE FIELD TO NULLS                    
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),0(R4)                                                    
*                                                                               
         MVI   LENFIELD,0          DEFAULT                                      
         CLI   0(R2),C'?'          WILDCARD CHAR?                               
         BE    START040            YES                                          
         CLI   0(R2),C'*'          WILDCARD CHAR?                               
         BE    START040            YES                                          
         MVC   0(1,R4),0(R2)       NO - COPY THE TEXT CHAR TO FIELD             
         MVI   LENFIELD,1                                                       
START040 DS    0H                                                               
         L     R1,ADMCB                                                         
         MVC   4(1,R1),LENFIELD                                                 
STARTX   DS    0H                                                               
         B     XIT                                                              
*                                                                               
FILTER   DS    0H                                                               
         NTR1                                                                   
         L     R2,AWILD            WILDCARD STRING                              
         ZIC   R3,LENWILD          LENGTH OF WILDCARD STRING                    
         L     R4,AFIELD           TEST FIELD                                   
         ZIC   R5,LENFIELD         LENGTH OF TEST FIELD                         
         LR    R6,R2                                                            
         AR    R6,R3                                                            
         ST    R6,EOFWILD                                                       
         LR    R6,R4                                                            
         AR    R6,R5                                                            
         ST    R6,EOFFIELD                                                      
*                                                                               
         LR    R6,R3               OR WILD STRING W/SPACES                      
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),SPACES                                                   
*                                                                               
         LR    R6,R5                                                            
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         OC    0(0,R4),SPACES      LOGICAL OR TEST FIELD W/ SPACES              
*                                                                               
         MVI   PREV,0                                                           
         B     FLTR030                                                          
*                                                                               
FLTR010  DS    0H                                                               
         MVC   PREV,0(R2)          SAVE PREVIOUS WILD CHARACTER                 
FLTR015  DS    0H                                                               
         LA    R2,1(R2)            NEXT WILD CHARACTER                          
         MVC   BYTE,0(R2)                                                       
         BAS   RE,ISALPHA          SKIP SPACES & SPECIAL CHARACTERS             
         BE    FLTR020                                                          
         CLI   0(R2),C'*'          BUT INCLUDE '*'                              
         BE    FLTR020                                                          
         CLI   0(R2),C'?'          AND '?'                                      
         BE    FLTR020                                                          
         C     R2,EOFWILD          PAST END, STOP SKIPPING                      
         BL    FLTR015                                                          
FLTR020  DS    0H                                                               
         LA    R4,1(R4)            NEXT FIELD CHARACTER                         
         MVC   BYTE,0(R4)                                                       
         BAS   RE,ISALPHA          SKIP SPACES & SPECIAL CHARACTERS             
         BE    FLTR030                                                          
         C     R4,EOFFIELD                                                      
         BNL   *+8                                                              
         B     FLTR020                                                          
FLTR030  DS    0H                                                               
         BAS   RE,CKEND                                                         
         MVC   BYTE,0(R2)                                                       
         BAS   RE,ISALPHA          IS WILD CHAR A LETTER/NUMBER?                
         BNE   FLTR040             NO - SKIP AHEAD                              
FLTR032  DS    0H                                                               
         CLI   PREV,C'*'                                                        
         BE    FLTR034                                                          
         CLC   0(1,R2),0(R4)       DOES IT MATCH THE FIELD CHARACTER?           
         BE    FLTR010             YES - KEEP LOOPING                           
         B     NOMATCH             NO - FIELD FAILS                             
FLTR034  DS    0H                                                               
         LR    R6,R2                                                            
FLTR035  DS    0H                                                               
         C     R6,EOFWILD                                                       
         BNL   FLTR036                                                          
         MVC   BYTE,0(R6)                                                       
         BAS   RE,ISALPHA                                                       
         BNE   FLTR036                                                          
         LA    R6,1(R6)                                                         
         B     FLTR035                                                          
FLTR036  DS    0H                                                               
         SR    R6,R2                                                            
         ST    R2,R2START                                                       
         ST    R4,R4START                                                       
         ST    R6,R6START                                                       
FLTR037  DS    0H                                                               
         C     R4,EOFFIELD                                                      
         BNL   NOMATCH                                                          
         MVC   BYTE,0(R4)                                                       
         BAS   RE,ISALPHA                                                       
         BE    *+12                                                             
         LA    R4,1(R4)                                                         
         B     FLTR037                                                          
         CLC   0(1,R2),0(R4)                                                    
         BE    FLTR038                                                          
         L     R2,R2START                                                       
         L     R4,R4START                                                       
         L     R6,R6START                                                       
         LA    R4,1(R4)                                                         
         ST    R4,R4START                                                       
         B     FLTR037                                                          
FLTR038  DS    0H                                                               
         LA    R4,1(R4)                                                         
         LA    R2,1(R2)                                                         
         BCT   R6,FLTR037                                                       
         BCTR  R2,0                                                             
         BCTR  R4,0                                                             
         B     FLTR010                                                          
FLTR040  DS    0H                                                               
         CLI   0(R2),C'?'          IS WILD CHAR A '?'                           
         BE    FLTR010                                                          
         CLI   0(R2),C'*'          IS WILD CHAR A '*'                           
         BNE   FLTR032             NO - CASE OF SPECIAL CHR IN 1ST POSN         
         MVC   PREV,0(R2)          YES - MAKE * PREVIOUS                        
FLTR050  DS    0H                                                               
         LA    R2,1(R2)            NEXT WILD CHARACTER                          
         MVC   BYTE,0(R2)                                                       
         BAS   RE,ISALPHA          SKIP SPACES & SPECIAL CHARACTERS             
         BE    FLTR055                                                          
         C     R2,EOFWILD                                                       
         BNL   *+8                                                              
         B     FLTR050                                                          
FLTR055  DS    0H                                                               
         B     FLTR030                                                          
*                                                                               
NOMATCH  DS    0H                                                               
         MVI   RESULT,0                                                         
         B     FILTERX                                                          
MATCH    DS    0H                                                               
         MVI   RESULT,X'80'                                                     
FILTERX  DS    0H                                                               
         L     R1,ADMCB                                                         
         MVC   4(1,R1),RESULT                                                   
         B     XIT                                                              
*                                                                               
ISALPHA  DS    0H                                                               
         CLI   BYTE,X'C1'                                                       
         BL    IANO                                                             
         CLI   BYTE,X'C9'                                                       
         BNH   IAYES                                                            
         CLI   BYTE,X'D1'                                                       
         BL    IANO                                                             
         CLI   BYTE,X'D9'                                                       
         BNH   IAYES                                                            
         CLI   BYTE,X'E2'                                                       
         BL    IANO                                                             
         CLI   BYTE,X'E9'                                                       
         BNH   IAYES                                                            
         CLI   BYTE,X'F0'                                                       
         BL    IANO                                                             
         CLI   BYTE,X'F9'                                                       
         BNH   IAYES                                                            
IANO     LTR   RB,RB                                                            
         B     *+6                                                              
IAYES    CR    RB,RB                                                            
         BR    RE                                                               
*                                                                               
CKEND    DS    0H                                                               
         ST    RE,SAVERE                                                        
         C     R2,EOFWILD                                                       
         BL    CKE010                                                           
         CLI   PREV,C'*'                                                        
         BE    MATCH                                                            
         C     R4,EOFFIELD                                                      
         BNL   MATCH                                                            
         B     NOMATCH                                                          
CKE010   DS    0H                                                               
         C     R4,EOFFIELD                                                      
         BNL   CKE020                                                           
         L     RE,SAVERE                                                        
         BR    RE                                                               
CKE020   DS    0H                                                               
         CLI   0(R2),C'*'                                                       
         BNE   NOMATCH                                                          
CKE030   DS    0H                                                               
         LA    R2,1(R2)            NEXT WILD  CHARACTER                         
         MVC   BYTE,0(R2)                                                       
         BAS   RE,ISALPHA          SKIP SPACES & SPECIAL CHARACTERS             
         BE    NOMATCH                                                          
         C     R2,EOFWILD                                                       
         BL    CKE030                                                           
         B     MATCH                                                            
*                                                                               
SPACES   DC    C'                                        '                      
*                                                                               
WORKD    DSECT                                                                  
ADMCB    DS    A                                                                
BYTE     DS    X                                                                
AWILD    DS    A                   A(WILDCARD STRING)                           
AFIELD   DS    A                   A(FIELD TO BE TESTED)                        
LENWILD  DS    X                   LENGTH OF WILDCARD STRING                    
LENFIELD DS    X                   LENGTH OF FIELD TO BE COMPARED               
RESULT   DS    X                                                                
PREV     DS    C                   PREV CHAR IN WILDCARD STRING                 
EOFFIELD DS    A                   END OF FIELD                                 
EOFWILD  DS    A                   END OF WILDCARD STRING                       
R2START  DS    A                                                                
R4START  DS    A                                                                
R6START  DS    A                                                                
SAVERE   DS    A                                                                
WORKDX   EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004PPGENWLD  09/25/01'                                      
         END                                                                    
