*          DATA SET SPACTBKSR  AT LEVEL 249 AS OF 12/01/09                      
*CATALP ACTBKSR                                                                 
                                                                                
*====================================================================           
*        P1    0    SPECIAL LOOKUP OPTION                                       
*              1-3  A(SPWORK)                                                   
*        P2    A(DATES)                                                         
*        P3    A(603 BYTE AREA)    START DATE 2 BYTE                            
*                                  END DATE 2 BYTE                              
*                                  BOOK 2 BYTE                                  
*                                  WEEK 2 BYTE                                  
*                                                                               
*        P4    =C'SPIL'            (KEYWORD FOR EXTENDED LIST)                  
*        P5    F'SPILL MARKET' IF P4 = SPIL                                     
*====================================================================           
         TITLE 'SPACTBKSR - GET ACTUAL BOOK MENU'                               
VMDACTBK CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**ACTBK*,R2                                                    
         L     RA,0(R1)                                                         
         LR    RC,RA                                                            
         AHI   RC,4096                                                          
         USING SPWORKD,RA,RC                                                    
*                                                                               
         CLI   MODE,REQFRST        INIT SAVED VALUES                            
         BE    CLEARIT                                                          
         SPACE 1                                                                
         MVC   SVPARAM(20),0(R1)                                                
         LA    R7,ABKAREA                                                       
         USING ABKBLOCK,R7                                                      
*                                                                               
         LA    R0,ABKAREA                                                       
         LHI   R1,ABKAREAX-ABKAREA                                              
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     RE,ADCLT                                                         
         USING CLTHDR,RE                                                        
         CLI   CEXTRA,C'U'         CANADIAN USING US RATING                     
         BE    GTABK0                                                           
         DROP  RE                                                               
*                                                                               
         MVI   NOHARD,0            NO SOFT DEMOS IN THE US!                     
*                                                                               
         L     RE,ADAGY                                                         
         USING AGYHDR,RE                                                        
         CLI   AGYPROF+7,C'C'      CHECK FOR CANADIAN                           
         BNE   GTABK0                                                           
         MVI   ABKCTRY,C'C'                                                     
         MVI   NOHARD,1            SET TO REMOVE HARD CODE                      
         DROP  RE                                                               
*                                                                               
GTABK0   MVC   ABKOPT1,SVPARAM                                                  
         L     RE,ASVD0            SAVE THE D0 PROFILE                          
         MVC   ABKD0PRF,0(RE)                                                   
*                                                                               
         CLI   Q2LPMWK,C' '        OVERRIDE LPM OPTION ?                        
         BNH   GTABK1                                                           
         MVC   ABKD0PRF+10(1),Q2LPMWK                                           
         MVC   10(1,RE),ABKD0PRF+10  MOVE TO SAVED PROFILE TOO                  
*                                                                               
GTABK1   CLI   Q2OVNITE,C' '         OVERRIDE OVERNITE OPTION                   
         BNH   GTABK2                                                           
         MVC   ABKD0PRF+11(1),Q2OVNITE                                          
         MVC   11(1,RE),ABKD0PRF+11  MOVE TO SAVED PROFILE TOO                  
*                                                                               
GTABK2   MVC   ABKFILE(3),=C'TTN'    SET DEFAULT FILE/MEDIA/SOURCE              
         CLI   QMED,C'R'                                                        
         BNE   *+10                                                             
         MVC   ABKFILE(3),=C'TRA'    RADIO DEFAULT                              
*                                                                               
         XC    CURRCALL,CURRCALL                                                
         MVI   CURRSRC,0                                                        
*                                                                               
         L     RE,ADBUY           CHECK FOR BOOK TYPE ELEMENT                   
         AHI   RE,BDELEM-BUYREC                                                 
*                                                                               
         MVI   ABKBTYP,0          DEFAULT TO NONE                               
         SR    R0,R0                                                            
FBTYEL   CLI   0(RE),0             NOT THERE                                    
         BE    FBTYEL20                                                         
         CLI   0(RE),X'24'         FOUND IT                                     
         BE    FBTYEL2                                                          
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     FBTYEL                                                           
*                                                                               
         USING DLUELEM,RE                                                       
FBTYEL2  CLI   DLUBKTYP,C'O'      NO ACTUAL BOOKTYPE FOR OLYMPICS               
         BE    FBTYEL4                                                          
         MVC   ABKBTYP,2(RE)      SET BTYPE FOR DEMO LOOKUPS                    
*                                                                               
FBTYEL4  CLI   ABKCTRY,C'C'        TEST CANADA                                  
         BNE   FBTYEL20                                                         
*                                                                               
         MVC   ABKALFM,DLUBAMKT    SET ALPHA MARKET                             
*                                                                               
         MVI   CURRSRC,C'N'                                                     
         TM    DLUBFLGS,X'02'      TEST NSI                                     
         BO    *+8                                                              
         MVI   CURRSRC,C'A'        SET FOR BBM                                  
*                                                                               
         CLI   DLUBSTOV,C'A'       TEST STATION OVERRIDE                        
         BL    *+10                                                             
         MVC   CURRCALL,DLUBSTOV                                                
*                                                                               
         MVI   WORK,C'0'                                                        
         CLI   CURRSRC,C'N'                                                     
         BE    *+8                                                              
         MVI   WORK,C'1'                                                        
*                                                                               
         L     RF,ADBLOCK                                                       
         USING DBLOCK,RF                                                        
         OC    DBSELMK,DBSELMK    TEST SPILL                                    
         BZ    FBTYEL20                                                         
                                                                                
* FIND SPILL ELEM FOR THIS MKT                                                  
                                                                                
FBTYEL10 L     RE,ADBUY                                                         
         AHI   RE,BDELEM-BUYREC                                                 
         SR    R0,R0                                                            
*                                                                               
FBTYEL12 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         BE    FBTYEL20                                                         
         CLI   0(RE),3                                                          
         BNE   FBTYEL12                                                         
         USING NDELEM,RE                                                        
         CLC   DBSELMK,NDAGYMKT                                                 
         BNE   FBTYEL12                                                         
*                                                                               
         CLI   NDBKTYPE,C'O'      NO ACTUAL BOOKTYPE FOR OLYMPICS               
         BE    *+10                                                             
         MVC   ABKBTYP,NDBKTYPE                                                 
*                                                                               
         MVC   ABKALFM,NDMKTALF    SET ALPHA MARKET                             
         XC    DBSELMK,DBSELMK                                                  
*                                                                               
         MVI   CURRSRC,C'N'                                                     
         CLI   NDRTGSVC,C'0'       TEST NSI                                     
         BE    *+8                                                              
         MVI   CURRSRC,C'A'        SET FOR BBM                                  
         MVC   WORK,NDRTGSVC       SAVE FOR SWEEP CLASS                         
*                                                                               
         CLI   NDSTA,C'A'          TEST STATION OVERRIDE                        
         BL    *+10                                                             
         MVC   CURRCALL,NDSTA                                                   
         DROP  RE                                                               
*                                                                               
FBTYEL20 L     RE,ADMARKET                                                      
         USING MKTREC,RE                                                        
         ST    RE,ABKAMKTR         PASS A(MKTREC) TO ACTBKG                     
*                                                                               
         CLI   ABKCTRY,C'C'                                                     
         BE    FBTYEL22                                                         
*                                                                               
         L     R1,ADCLT            GET CORRECT SWEEP CLASS                      
         USING CLTHDR,R1                                                        
         MVC   WORK,CPROF+3        MOVE RATING SERVICE                          
         MVI   ABKSRC,C'N'                                                      
         CLI   CPROF+3,C'0'        NSI?                                         
         BE    *+8                                                              
         MVI   ABKSRC,C'A'                                                      
*                                                                               
FBTYEL22 MVC   ABKMCLAS,MKTCLAS1   SET DEFAULT SWEEP CLASS                      
         CLC   WORK(1),MKTRS1      SAME SERVICE                                 
         BE    *+10                                                             
         MVC   ABKMCLAS,MKTCLAS2   NO - OVERRIDE DEFAULT                        
         OI    ABKMCLAS,X'F0'                                                   
         DROP  R1,RE                                                            
*                                                                               
         MVC   CURRFMS,ABKFILE                                                  
*                                                                               
         CLI   CURRSRC,C'A'                                                     
         BL    *+10                                                             
         MVC   ABKSRC,CURRSRC      USE DLUELEM DATA IF IT'S THERE               
*                                                                               
         CLI   CURRCALL,C' '       IF HAVE FROM DLU, LEAVE IT                   
         BH    *+10                                                             
         MVC   CURRCALL,STA        SAVE CURRENT CALL LETTERS                    
*                                                                               
         MVC   CURRMKT,BMKT                                                     
         MVC   CURRCLT,CLT                                                      
         MVC   CURRBOOK,QBOOK1                                                  
*                                                                               
ALFOK    CLI   QMED,C'R'                                                        
         BNE   ALFOK2                                                           
         L     RE,ADSTAT                                                        
         USING STAREC,RE                                                        
         OC    SMKTALPH,SMKTALPH                                                
         BZ    ALFOK1                                                           
         MVC   ABKALFM,SMKTALPH                                                 
         B     ALFOK4                                                           
         DROP  RE                                                               
*                                                                               
ALFOK1   L     RE,ADMARKET                                                      
         USING MKTREC,RE                                                        
         OC    MKTALST(3),MKTALST                                               
         BZ    ALFOK4                                                           
         MVC   ABKALFM,MKTALST                                                  
         B     ALFOK4                                                           
         DROP  RE                                                               
*                                                                               
ALFOK2   CLI   ABKCTRY,C'C'                                                     
         BNE   ALFOK4                                                           
                                                                                
***CANADIAN SETUP                                                               
                                                                                
         CLI   SPOTPROF+15,C'Y'    TEST SOFT DEMO LOOKUPS                       
         BNE   ALFOK4              NO                                           
         MVI   CURRCALL+4,C'T'     FIX UP STATION                               
*                                                                               
         CLI   CURRCALL,C' '                                                    
         BH    *+10                                                             
         MVC   CURRCALL,STA        SAVE CURRENT CALL LETTERS                    
         DROP  RF                                                               
*                                                                               
         L     RF,ADBLOCK                                                       
         USING DBLOCK,RF                                                        
         MVC   DBSELSTA,CURRCALL                                                
         DROP  RF                                                               
*                                                                               
ALFOK4   L     R9,SVPARAM+4                                                     
         MVC   ABKBDAT,0(R9)                                                    
         GOTO1 DATCON,DMCB,(X'02',2(R9)),(0,CURRDATE)                           
         MVC   CURRDATE+6(6),CURRDATE                                           
         GOTO1 DATCON,DMCB,(X'02',0(R9)),(0,CURRDATE)                           
         CLC   CURRDATE(6),QSTART                                               
         BL    *+10                                                             
         MVC   CURRDATE(6),QSTART                                               
         CLC   CURRDATE+6(6),QEND                                               
         BH    *+10                                                             
         MVC   CURRDATE+6(6),QEND                                               
*                                                                               
         MVC   ABKCLT,CURRCLT                                                   
         MVC   ABKMKT,CURRMKT                                                   
         MVC   ABKSTAT,CURRCALL                                                 
         MVC   ABKBOOK,CURRBOOK                                                 
         MVC   ABKSTART(12),CURRDATE                                            
         MVC   ABKRRTYP,QRERATE                                                 
         MVC   ABKAGY,QAGY                                                      
         L     RE,ADBLOCK                                                       
         USING DBLOCK,RE                                                        
         MVC   ABKSPMKT,DBSELMK                                                 
         MVC   ABKSTAT,DBSELSTA                                                 
*                                                                               
         CLI   ABKCTRY,C'C'                                                     
         BNE   CANREG                                                           
         L     R8,ADBUY                                                         
         USING BUYRECD,R8                                                       
         SR    R0,R0                                                            
         IC    R0,BUYREC                                                        
         N     R0,=X'0000000F'                                                  
         CHI   R0,3                TEST MEDIA N                                 
         BNE   CANREG              NO                                           
         CLI   BUYKMSTA+4,X'B0'      POSSIBLE CANADIAN MARKET SUFFIX            
         BL    CANREG                                                           
         XC    DBSELALF,DBSELALF                                                
         L     R9,=A(SUFXTAB)      POINT TO THE CONVERSION TABLE                
         LA    RF,SUFXTABN                                                      
C2CHRMKT CLC   BUYMSTA+4(1),2(R9) MATCH THE SUFFIX                              
         BE    C2CHRMK2                                                         
         LA    R9,L'SUFXTAB(R9)                                                 
         BCT   RF,C2CHRMKT                                                      
         XC    ABKALFM,ABKALFM                                                  
         B     CANREG              GIVE UP ON ALPHA MKT IF NOT THERE            
* SET 3 CHARACTER ALPHA MARKET FOR DEGET                                        
C2CHRMK2 MVC   DBSELALF,3(R9)                                                   
         MVC   ABKALFM,3(R9)                                                    
         CLC   DBSELALF(3),=C'OTT'                                              
         BNE   *+16                                                             
         MVC   ABKALFM(3),=C'OTA'                                               
         MVC   DBSELALF(3),=C'OTA'                                              
CANREG   DS    0C                                                               
         DROP  R8                                                               
* EVERYTHING HAS BEEN DEALT WITH EXCEPT FUSION DO THAT NOW                      
SYSC01   DS    0C                                                               
*          DATA SET SPGETDEME  AT LEVEL 018 AS OF 03/17/05                      
         L     R8,ADBUY                                                         
         USING BUYRECD,R8                                                       
         CLI   BUYREC+6,X'E8'      TEST CABLE LOOKOUP                           
         BL    SYSCX               NO                                           
*                                                                               
         XC    WORK(16),WORK       READ 00A PROFILE                             
         MVC   WORK(4),=C'S00A'                                                 
         NI    WORK,X'BF'          MAKE 'S' LOWERCASE                           
         MVC   WORK+4(2),QAGY                                                   
         L     R0,DATAMGR                                                       
         L     RF,GETPROF                                                       
         GOTO1 (RF),DMCB,WORK,SV00APRF,(R0)                                     
*        MVI   SV00APRF,C'F' <****FOR TESTING                                   
*        MVC   ABKAGY,=C'SJ' <****FOR TESTING                                   
         MVC   ABK0APRF,SV00APRF                                                
         MVI   ABKBTYP,C'X'        FORCE A MISS IF NOT ACTIVE                   
*                                                                               
         CLI   SV00APRF,C'F'       OR FUSION                                    
         BNE   *+12                                                             
         MVI   ABKSRC,C'F'         SET SOURCE                                   
         MVI   ABKBTYP,0           BOOKTYPE                                     
*                                                                               
         CLI   SV00APRF,C'N'       OR NSI                                       
         BNE   *+12                IF NEITHER, SUPPRESS LOOKUP                  
         MVI   ABKSRC,C'N'         SET SOURCE                                   
         MVI   ABKBTYP,C'W'        BOOK TYPE                                    
*                                                                               
         CLI   SV00APRF,C'N'       TEST NSI DEFAULT                             
         BE    *+12                                                             
         CLI   SV00APRF,C'F'       OR FUSION                                    
         BNE   SYSCX               IF NEITHER, SUPPRESS LOOKUP                  
*                                                                               
*          DATA SET SPGETDEME  AT LEVEL 018 AS OF 03/17/05                      
GD11     XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPAGY,QAGY                                                     
         MVI   STAPMED,C'T'                                                     
         CLI   STAPMED,C'C'        FOR CANADA, MEDIA 'T' IS A 'C'               
         BNE   *+8                                                              
         MVI   STAPMED,C'T'                                                     
         MVI   STAPCTRY,C'U'                                                    
         CLI   DBSELMED,C'C'                                                    
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPMKST,BUYREC+4                                                
         GOTO1 VSTAPACK,(R1)                                                    
*        CLI   STAPERR,0                                                        
*        BE    *+6                                                              
*        DC    H'0'                                                             
*                                                                               
         L     RE,ADBLOCK                                                       
         USING DBLOCK,RE                                                        
         MVC   DBSELSTA,STAPQSTA                                                
*                                                                               
GD11A    MVI   STAPACT,C'X'        TRANSLATE 3CHAR NET TO 4CHAR                 
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         L     RE,ADMARKET         CABLE RATING SERVICE OVERRIDE                
         USING MKTREC,RE            ON MARKET RECORD                            
         CLI   MKTCDEM,C'N'        NSI?                                         
         BNE   *+12                                                             
         MVI   ABKBTYP,C'W'        YES, OVERRIDE 00A PROFILE                    
         B     *+16                                                             
         CLI   MKTCDEM,C'F'        FUSION?                                      
         BNE   *+14                NO, 00A IS DEFAULT                           
         MVI   ABKBTYP,0                                                        
         MVC   ABKSRC,MKTCDEM                                                   
         DROP  RE                                                               
*                                                                               
         CLI   QCBLDEM,C'N'        HONOR CABLE RATING SERVICE OVERRIDE          
         BNE   *+12                 AT THE REQUEST LEVEL                        
         MVI   ABKBTYP,C'W'                                                     
         B     *+16                                                             
         CLI   QCBLDEM,C'F'                                                     
         BNE   *+14                                                             
         MVI   ABKBTYP,0                                                        
         MVC   ABKSRC,QCBLDEM                                                   
*                                                                               
         L     RE,ADBLOCK                                                       
         USING DBLOCK,RE                                                        
         MVC   DBSELSTA,STAPQSTA   MOVE 4 CHAR NETWORK CODE                     
         MVI   DBSELSTA+4,C'T'                                                  
         MVC   ABKSTAT,DBSELSTA                                                 
         DROP  RE                                                               
         L     RE,ADSTAT                                                        
         USING STAREC,RE                                                        
         CLI   SMKTALPH,C' '       ALPHA MKT IN IT                              
         BNH   SYSC04              NO                                           
         MVC   ABKALFM(3),SMKTALPH     MOVE CODE                                
         DROP  RE                                                               
         B     SYSCX                                                            
         DROP  R8                                                               
SYSC04   DS    0C                                                               
         L     RE,ADMARKET                                                      
         USING MKTREC,RE                                                        
         CLI   MKTALST,C' '         ANY ALPHA-MKT CODE?                         
         BNH   SYSCX                NO                                          
         MVC   ABKALFM(3),MKTALST                                               
         DROP  RE                                                               
*                                                                               
SYSCX    CLC   SPILCAP(4),=C'SPIL'                                              
         BNE   SYSCX2                                                           
         MVC   ABKSPMKT(2),SPILMKT+2                                            
         CLI   NOHARD,1            TEST NO HARD CODE                            
         BNE   *+10                                                             
         XC    ABKSPMKT,ABKSPMKT   THIS DEFEATS ALPHA MKT SO CLEAR IT           
*                                                                               
         CLI   NOHARD,1            MAINTAIN ALPHA FOR NO HARD                   
         BE    SYSCX2                                                           
         XC    ABKALFM(3),ABKALFM                                               
*                                                                               
SYSCX2   L     RE,=V(SYSPROFS)                                                  
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         LA    RE,SPOTPROF                                                      
         MVC   SVOOWR,8(RE)        SAVE OFF THE FISCAL BASE DAY NUMBER          
         CLI   QRERATE,C'I'        DOING AFFID?                                 
         BNE   *+8                 NO                                           
         MVI   8(RE),0             YES - CLEAR THE FISCAL BASE DAY NUM          
         MVC   ABKS0PRF,0(RE)                                                   
*                                                                               
         L     RE,ASV1W                                                         
         MVC   ABK1WPRF,0(RE)                                                   
         MVC   ABKCMFAC,ACOMFACS                                                
         MVC   ABKDATCN,DATCON                                                  
         MVC   ABKADDAY,ADDAY                                                   
         MVC   ABKMOBIL,MOBILE                                                  
         MVC   ABKDEMND,DEMAND                                                  
         MVC   ABKNSIWK,=V(NSIWEEK)                                             
         MVC   ABKGTDAY,=V(GETDAY)                                              
         GOTO1 =V(ACTBKG),DMCB,ABKAREA                                          
*                                                                               
         L     RE,=V(SYSPROFS)     SYSPROFS RESOLVED?                           
         LTR   RE,RE                                                            
         BNZ   *+8                 YES                                          
         LA    RE,SPOTPROF         NO - USE SPOTPROF                            
         MVC   8(1,RE),SVOOWR      RESTORE THE FISCAL BASE DAY NUMBER           
***********************************************************************         
         EJECT                                                                  
EXIT     LA    R1,SVPARAM                                                       
         MVC   BKLST(250),ABKOLIST                                              
         MVC   BKLST+250(250),ABKOLIST+250                                      
         MVC   BKLST+500(103),ABKOLIST+500                                      
         L     RE,4(R1)                                                         
         MVC   BKLST(2),0(RE)      RESTORE ACTUAL START DATE                    
         L     RE,8(R1)                                                         
         MVC   0(250,RE),BKLST                                                  
         MVC   250(250,RE),BKLST+250                                            
         MVC   500(103,RE),BKLST+500                                            
         MVC   STA,CURRCALL                                                     
EXITX    XIT1                                                                   
         SPACE 1                                                                
CLEARIT  XC    SVAREA,SVAREA      CLEAR SAVED VALUES AT REQ. FIRST              
         B     EXITX                                                            
*                                                                               
         EJECT                                                                  
NOHARD   DS    X                                                                
CURRAREA DS    0CL30                                                            
CURRMKT  DS    CL2                                                              
CURRCALL DS    CL5                                                              
CURRCLT  DS    CL3                                                              
CURRBOOK DS    CL5                                                              
CURRDATE DS    CL12                                                             
CURRFMS  DS    CL3                                                              
CURRSRC  DS    C                                                                
SV00APRF DS    CL50                                                             
SVAREA   DS    CL30                                                             
SVPARAM  DS    5F                                                               
SPILCAP  EQU   SVPARAM+12                                                       
SPILMKT  EQU   SVPARAM+16                                                       
CURRDTES DS    CL4                                                              
ABKAREA  DS    XL1000                                                           
ABKAREAX EQU   *                                                                
BKLST    DS    CL603                                                            
SVOOWR   DS    XL1               SAVED FISCAL BASE DAY NUM FROM 00 PROF         
       ++INCLUDE SPCNCBLTAB                                                     
*******************************************************                         
         EJECT                                                                  
*                                                                               
* STATION CALL LETTER CHANGE LIST-ENTRIES MUST BE IN ALPHA STATION              
*  ORDER. LIST ENTRY=11 BYTES. BYTE 0=(N)NSI,(A)ARB,OR (B)BOTH                  
*                              BYTE 1-2=EFFECTIVE BOOK (YEAR-MONTH)             
*                              BYTE 3-10=FIRST 4(OLD) LAST 4(NEW)               
*                                                                               
         EJECT                                                                  
       ++INCLUDE SPABKBLOCK                                                     
       ++INCLUDE SPSWPTABSD                                                     
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DEDEMEQUS                                                      
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE DEDEMFILE                                                      
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENAGY                                                       
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'249SPACTBKSR 12/01/09'                                      
         END                                                                    
