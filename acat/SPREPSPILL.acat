*          DATA SET SPREPSPILL AT LEVEL 010 AS OF 01/30/95                      
*CATALP REPSPILL                                                                
         TITLE 'MAINTAIN AND PRINT A TABLE OF ORIGINATING MARKETS'              
REPSPILL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,REPSPILL                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         MVC   PARAMS,0(R1)                                                     
         CLI   SPCOMAND,X'01'      SAVE ORIGINATING MKT/STA                     
         BNE   OUTPUT                                                           
         CLI   SVBUYKEY,X'10'      EXIT IF NOT BUY REC                          
         BNH   EXIT                                                             
         L     R3,ADBUY                                                         
         CLC   4(5,R3),SVBUYKEY+4  EXIT IF NOT SPILL                            
         BE    EXIT                                                             
         MVC   SVMKTCOD,SVBUYKEY+4                                              
         SR    R8,R8                                                            
         ICM   R8,7,ADBUFF                                                      
         USING SPBUFF,R8                                                        
         LA    RE,450(R8)                                                       
INPUT1   OC    SPMKTSTA,SPMKTSTA   NOT IN BUFFER ADD IT                         
         BZ    INPUT2                                                           
         CR    R8,RE               TO MANY - JUST GET OUT                       
         BH    EXIT                                                             
         CLC   SPMKTSTA,4(R3)      ALREADY THERE SO BYPASS                      
         BE    EXIT                                                             
         LA    R8,LNSPBUFF(R8)                                                  
         B     INPUT1                                                           
INPUT2   MVC   SPMKTSTA,4(R3)      READ FOR ORIGINATING MARKET                  
         MVC   SPKEY,KEY                                                        
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'M'                                                         
         MVC   KEY+1(1),QMED                                                    
         SR    RF,RF                                                            
         ICM   RF,3,SPMKTSTA                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  KEY+2(4),DUB                                                     
         MVC   KEY+6(2),QAGY                                                    
         GOTO1 HIGHMKT                                                          
         L     RE,ADMARKET                                                      
         USING MKTREC,RE                                                        
         MVC   SPMNAME,MKTNAME     SET UP BUFFER ENTRY                          
         SPACE 2                                                                
         SR    RF,RF               RESTORE MARKET RECORD                        
         ICM   RF,3,SVMKTCOD                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  KEY+2(4),DUB                                                     
         GOTO1 HIGHMKT                                                          
         MVC   KEY,SPKEY                                                        
         B     EXIT                                                             
         EJECT                                                                  
OUTPUT   CLI   SPCOMAND,X'02'      PRINT OUT BUFFER CONTENTS                    
         BNE   EXIT                                                             
         L     R7,APLINE           WHERE TO PUT IT                              
         USING SPPLD,R7                                                         
         SR    R8,R8                                                            
         ICM   R8,7,ADBUFF         WHERE IT IS NOW                              
         USING SPBUFF,R8                                                        
OP1      LA    R6,4                ONLY 4 WILL FIT ON A LINE                    
         LA    R7,12(R7)           BUMP TO START OF PRINT AREA                  
OP2      OC    SPMKTSTA,SPMKTSTA   END OF BUFFER                                
         BZ    EXIT                                                             
         GOTO1 MSUNPK,DMCB,SPMKTSTA,FULL,PLSTA                                  
         MVI   PLBLANK,C' '                                                     
         MVC   PLMNAME,SPMNAME                                                  
         LA    R7,LPLENT(R7)       NEXT PRINT AREA                              
         LA    R8,LNSPBUFF(R8)     NEXT BUFFER ENTRY                            
         BCT   R6,OP2                                                           
         L     R7,APLINE           SET TO NEXT PRINT LINE                       
         LA    R7,132(R7)                                                       
         ST    R7,APLINE                                                        
         B     OP1                                                              
         EJECT                                                                  
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
         DS    0F                                                               
PARAMS   DS    0CL12                                                            
ASPWORK  DS    F                                                                
SPCOMAND DS    C                                                                
ADBUFF   DS    CL3                                                              
APLINE   DS    CL4                                                              
         SPACE 2                                                                
SPKEY    DS    CL13                                                             
SVMKTCOD DS    CL4                                                              
         SPACE 2                                                                
SPBUFF   DS    0CL29                                                            
SPMKTSTA DS    CL5                                                              
SPMNAME  DS    CL24                                                             
SPBUFEN  DS    0C                                                               
LNSPBUFF EQU   SPBUFEN-SPBUFF                                                   
         SPACE 2                                                                
SPPLD    DS    0CL30                                                            
PLSTA    DS    CL4                                                              
PLBLANK  DS    CL1                                                              
PLMNAME  DS    CL24                                                             
         DS    CL1                                                              
SPPLEND  DS    0C                                                               
LPLENT   EQU   SPPLEND-SPPLD                                                    
         PRINT OFF                                                              
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010SPREPSPILL01/30/95'                                      
         END                                                                    
