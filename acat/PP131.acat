*          DATA SET PP131      AT LEVEL 046 AS OF 11/15/11                      
*CATALP PP131                                                                   
         TITLE 'PP131 - PRNT RECOVERY FILE DUMP'                                
********************************************************************            
* THIS MODULE READS THE DISK RECOVERY FILE AND GENERATES TWO TAPES.*            
*                                                                  *            
* ALL PRTDIR RECORDS ARE IGNORED EXCEPT IN TOTAL INPUT COUNT       *            
*                                                                  *            
* TAPES HAVE ALL FILE RECORDS EXCEPT POINTER COPIES AND CHANGES.   *            
*        (THESE RECORDS HAVE X'80' ON IN RRECTYPE)                 *            
********************************************************************            
*                                                                               
*  RCRI  11/11     CHANGE BLOCKSIZE AND IGNORE DELETED RECORDS                  
*                                                                               
*  BOBY  03/09     KEEP DIRECTORY ONLY COPIES AND CHANGES                       
*                                                                               
*  AHYD  05/08     OPEN FILE FOR UPDATE                                         
*                  CHECK CONDITION CODE UPON ERASE                              
*                                                                               
*  BOBY  01/07     2 CH FILE NUMBER                                             
*                  FORCE INPUT OF ERASE=YE OR ERASE=NO                          
*                                                                               
*  BPLA  07/11/01  EXPAND SSB                                                   
*                                                                               
*  BPLA  9/19/96 CHANGE BLKSIZE TO 26364 (FROM 8000)                            
*                                                                               
*  BPLA  3/19/91 PRTDIR LOGIC NO-OPED - PROBLEM WAS IN LOCKER                   
*                                                                               
*  BPLA  11/8/90 CHANGES TO TRY AND FIND PRTFILE BUYREC ADDS                    
*                WITHOUT CORRESPONDING PRTDIR ADDS (20 +21 POINTERS)            
*                DIRECTORY OVERLAYS WILL BE CHECK LIKE ADDS                     
*                                                                               
***********************************************************************         
                                                                                
         PRINT NOGEN                                                            
PP131    CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE 0,*PP131*,=A(WORK)                                               
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
RDCARD   DS    0H                                                               
         XC    DMCB(24),DMCB                                                    
         GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
*                                                                               
         CLC   =C'/*',CARD                                                      
         BE    RC30                                                             
         CLC   =C'PRNT',CARD                                                    
         BNE   RC6                                                              
*                                                                               
         MVC   SENAME,SPACES       INIT SE NAME AREA                            
         MVC   SENAME(3),=C'S=P'   START OF S=PXX                               
         MVC   SENAME+3(2),CARD+4  FILE NUMBER                                  
*                                                                               
         GOTOR =V(DMDDNAME),DMCB,(X'24',=C'DMDDNAME'),SENAME,0                  
         CLI   DMCB+8,0            CHECK FOR ERRORS                             
         BNE   SYSERR                                                           
*                                                                               
         USING DDNAMED,RF          ESTABLISH FILE LIST ENTRY                    
         L     RF,DMCB+8           POINT TO RETURNED SYSTEM FILE LIST           
         MVC   FILNAME,DDNADDN                                                  
         MVC   SENUM,DDNASENO      SAVE SE NUMBER                               
         MVC   FILNUM,DDNASENA+3   FILE ID                                      
         DROP  RF                                                               
*                                                                               
         L     RE,=A(UTL)                                                       
         MVC   4(1,RE),SENUM       SET SENUM IF UTL                             
         B     RDCARD                                                           
*                                                                               
RC6      DS    0H                                                               
         CLC   =C'ERASE=YES',CARD                                               
         BNE   RC7                                                              
         MVI   FLIST,C'U'          UPDATIVE                                     
         MVI   ERASESW,YES                                                      
         B     RDCARD                                                           
*                                                                               
RC7      DS    0H                                                               
         CLC   =C'ERASE=NO ',CARD                                               
         BNE   RC8                                                              
         MVI   FLIST,C'N'          NOT UPDATIVE                                 
         MVI   ERASESW,NO                                                       
         B     RDCARD                                                           
*                                                                               
RC8      DS    0H                                                               
         CLC   =C'DDSIO=',CARD                                                  
         BNE   RC9                                                              
         ICM   RE,15,=V(DDSIO)     DDSIO=                                       
         BNZ   *+6                                                              
         DC    H'00'                                                            
         MVC   0(8,RE),CARD+6                                                   
         B     RDCARD                                                           
*                                                                               
RC9      CLC   =C'DSPACE=',CARD                                                 
         BNE   RCF                                                              
         ICM   RF,15,=A(SSB)       DSPACE=                                      
         USING SSBOFFD,RF                                                       
         MVC   SSODSPAC,CARD+7                                                  
         B     RDCARD                                                           
         DROP  RF                                                               
*                                                                               
RCF      B     CRDERR                                                           
*                                                                               
RC30     DS    0H                  ERASESW MUST BE Y OR N                       
*                                                                               
         CLI   ERASESW,YES         MUST BE Y                                    
         BE    RC12                                                             
         CLI   ERASESW,NO          OR N                                         
         BE    RC12                                                             
         B     ERASER              ELSE ERROR                                   
*                                                                               
RC12     DS    0H                  ERASESW MUST BE Y OR N                       
*                                                                               
         CLC   FILNUM,=C'  '                                                    
         BNH   SYSERR                                                           
*                                                                               
         EJECT                                                                  
***********************************************************************         
* OPEN RECOVERY FILE *                                                          
***********************************************************************         
         SPACE 1                                                                
OPEN     GOTO1 =V(DATAMGR),DMCB,=C'OPEN',=C'PRINT',FLIST,REC                    
*                                                                               
         OPEN  (RCVTAPE,(OUTPUT),RCVCOPY,(OUTPUT))                              
*                                                                               
         MVC   TITLE(31),=CL31'PRNTNN RECOVERY FILE DUMP'                       
         MVC   TITLE+4(2),FILNUM                                                
*                                                                               
GET1     GOTO1 =V(DATAMGR),DMCB,(X'11',=C'DMRSEQ'),=C'PRECOV ',        X        
               DA,REC,A(BUFF)                                                   
         TM    8(R1),X'40'         TEST ERROR                                   
         BNZ   GETX                                                             
         TM    8(R1),X'80'         TEST EOF                                     
         BNZ   END1                                                             
         AP    TRECSIN+14(4),=P'1'                                              
*                                                                               
         TM    RRECTY,X'80'        SKIP POINTER COPIES/CHANGES                  
         BO    GET1                                                             
         CLI   RSIN,X'FF'          SKIP DELETED RECORDS                         
         BE    GET1                                                             
*                                                                               
* NO-OP THIS TEST IF YOU WANT TO DUMP DIRECTORY CHANGES                         
*                                                                               
         CLI   RFILTY,X'40'        TEST PRTDIR REC                              
         BNE   GET3                                                             
*                                                                               
         CLI   RECKEY+26,X'FF'     KEEP DIRECTORY ONLY RECS COPY/CHANGE         
         BE    GET4                                                             
*                                  TEST FOR DIRECTORY OVERLAY                   
         OC    RECKEY+21(3),RECKEY+21   NOT IF PASSIVE                          
         BNZ   GET1                                                             
         CLI   RRECTY,X'01'        SAVE COPY                                    
         BNE   GET2                                                             
         MVC   SAVEREC,RECKEY      SAVE WHOLE RECORD                            
         B     GET1                                                             
*                                                                               
GET2     DS    0H                                                               
         CLI   RRECTY,X'02'        FOR CHANGES                                  
         BNE   GET21                                                            
         CLC   SAVEREC(25),RECKEY  TEST SAME KEY                                
         BNE   GET1                                                             
         CLC   SAVEADDR,RECADDR    TEST SAME DA                                 
         BE    GET1                                                             
         CLI   SAVEREC+25,X'FF'    FF DELETES DO NOT CAUSE                      
         BE    GET1                DIRECTORY OVERLAYS                           
         B     GET2C                                                            
*                                                                               
***                                                                             
***      CODE BELOW WILL DETAIL DICTORY OVERLAYS                                
***                                                                             
***      B     GET2C                 BRANCH AROUND FOR NOW                      
***                                                                             
***      MVC   P+1(24),=C'DIRECTORY OVERLAY - COPY'                             
***      GOTO1 =V(HEXOUT),DMCB,SAVEREC,P+30,27                                  
***      GOTO1 (RF),(R1),SAVEADDR,P+92,4                                        
***      GOTO1 =V(PRINTER)                                                      
***      MVC   P+1(24),=C'DIRECTORY OVERLAY - CHA '                             
***      GOTO1 =V(HEXOUT),DMCB,RECKEY,P+30,27                                   
***      GOTO1 (RF),(R1),RECADDR,P+92,4                                         
***      GOTO1 =V(PRINTER)                                                      
***                                                                             
***      B     GET2C                                                            
***                                                                             
GET21    DS    0H                  DIRECTORY ADDS                               
*                                                                               
         CLI   RRECTY,X'03'        FOR ADDS                                     
         BNE   GET1                                                             
         CLI   RECKEY+26,X'FF'     SKIP NON DIR ONLY RECORDS                    
         BNE   GET1                                                             
         AP    DIRONLY+14(4),=P'1'                                              
         B     GET12             TREAT DIRECTORY ONLY RECS AS ADDS              
*                                                                               
GET2C    DS    0H                                                               
         AP    DIRRECS+14(4),=P'1'                                              
         B     GET12             TREAT DIRECTORY OVERLAYS AS ADDS               
*                                                                               
GET3     DS    0H                                                               
*                                                                               
         CLI   RFILTY,X'41'        TEST PUBDIR REC                              
         BE    GET1                SKIP                                         
*                                                                               
GET4     DS    0H                                                               
GET5     LA    R3,CTRS                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         CLC   RFILTY(2),0(R3)     MATCH FILE/REC                               
         BE    GET8                                                             
         BXLE  R3,R4,*-10                                                       
         B     GET12                                                            
*                                                                               
GET8     AP    14(4,R3),=P'1'                                                   
                                                                                
***********************************************************************         
* ALL RECORDS GO TO TAPE 1 (RCVTAPE) *                                          
***********************************************************************         
GET12    LH    RE,DM5+2            GET REC LEN                                  
         LA    RE,4(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,RECLEN                                                        
         LA    R0,RECLEN                                                        
         L     R1,=A(RCVTAPE)                                                   
         PUT   (1),(0)                                                          
         AP    TRECS1+14(4),=P'1'                                               
*                                                                               
         LA    R0,RECLEN                                                        
         L     R1,=A(RCVCOPY)                                                   
         PUT   (1),(0)                                                          
         AP    TRECS2+14(4),=P'1'                                               
         B     GET1                                                             
                                                                                
GETX     AP    ERRORS,=P'1'                                                     
                                                                                
***********************************************************************         
* BUMP DISK ADDRESS TO START OF NEXT TRACK *                                    
***********************************************************************         
         LH    RE,DA                                                            
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,DA                                                            
         B     GET1                                                             
         EJECT                                                                  
END1     CP    TRECSIN+14(4),=P'1' TEST ANY RECORDS                             
         BL    ENDX                                                             
*                                                                               
         CLOSE (RCVTAPE,,RCVCOPY,)                                              
*                                                                               
         CP    ERRORS,=P'0'        CHECK FOR ERRORS                             
         BE    ENDX                NO - GO ON                                   
*                                                                               
         OI    ERRORS+3,X'0F'                                                   
         UNPK  ERRMSG(4),ERRORS                                                 
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(69,ERRMSG)                             
         B     ENDX                                                             
*                                                                               
ERRMSG   DC    CL30'0000 RECOVERY FILE READ ERRORS'                             
         DC    X'15'                                                            
         DC    CL37'********** FILE NOT ERASED **********'                      
         DC    X'15'                                                            
ERRORS   DC    PL4'0'                                                           
         EJECT                                                                  
ENDX     MVC   P(4),=C'PRNT'                                                    
         MVC   P+4(2),FILNUM                                                    
         MVC   P+7(24),=C'RECOVERY RECORD COUNTERS'                             
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)         SKIP A LINE                                  
         EJECT ,                                                                
***********************************************************************         
* PRINT COUNTERS *                                                              
* 1ST HALVE DIRECTORY OVERLY COUNTER                                            
***********************************************************************         
         ZAP   DUB,DIRRECS+14(4)                                                
         CVB   R1,DUB                                                           
         SRL   R1,1                                                             
         CVD   R1,DUB                                                           
         ZAP   DIRRECS+14(4),DUB                                                
*                                                                               
         SPACE 1                                                                
         LA    R3,CTRS                                                          
         LH    R4,0(R3)                                                         
         LA    R5,TCTRSX-1         SET TO INCLUDE TOTALS                        
         LA    R3,6(R3)                                                         
PRTCNT   MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)         SKIP A LINE                                  
         MVC   P(12),2(R3)                                                      
         OI    17(R3),X'0F'                                                     
         UNPK  P+13(5),14(4,R3)                                                 
         GOTO1 =V(PRINTER)                                                      
         BXLE  R3,R4,PRTCNT                                                     
         EJECT                                                                  
***********************************************************************         
* DO NOT ERASE RECOVERY FILE IF READ ERRORS OCCURED *                           
***********************************************************************         
         SPACE 1                                                                
         CP    ERRORS,=P'0'                                                     
         BNE   EOJ                                                              
         CLI   ERASESW,NO                                                       
         BE    EOJ1                                                             
         CLI   ERASESW,YES         BE SURE THEY WANT TO ERASE                   
         BNE   EOJ1                                                             
***********************************************************************         
* GET RECOVERY FILE DTF ADDRESS *                                               
***********************************************************************         
         GOTO1 =V(DMOD000),ERSPARS,A(DMEXT),,,(X'44',0)                         
         USING DTFPHD,R3                                                        
         L     R3,12(,R1)          GET DTF                                      
         LA    R3,0(,R3)           CLEAR HOB                                    
         CLI   ERASESW,NO                                                       
         BE    EOJ1                DON'T ERASE FILE                             
         TM    DTFOPEN,DTF_RO      READ-ONLY?                                   
         BZ    ERASE                                                            
         WTO   'FILE SET TO READ-ONLY - COMMAND REJECT'                         
         DROP  R3                                                               
                                                                                
***********************************************************************         
* CALL DADDS TO ERASE FILE *                                                    
***********************************************************************         
ERASE    GOTO1 =V(DADDS),ERSPARS,A(WTERASE)                                     
         MVC   HALF,8(R1)          RETURN VALUES                                
         NI    HALF+1,X'FB'                                                     
         OC    HALF,HALF           ANY ERRORS                                   
         BZ    ERASED                                                           
         MVC   P(29),=C'ERASE ERROR ON RECOVERY FILE='                          
         MVC   P+29(8),FILNAME                                                  
         GOTO1 =V(HEXOUT),DMCB,HALF,P+38,2,=C'TOG'                              
         GOTO1 =V(LOGIO),DMCB,1,(42,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVI   ERASERR,YES                                                      
         B     EOJ1                                                             
*                                                                               
ERASED   MVC   P(39),=C'PP131 ** PRNT   RECOVERY FILE ERASED **'                
         MVC   P+13(2),FILNUM                                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(39),=C'PP131 ** PRNT   RECOVERY FILE ERASED **'                
         MVC   P+13(2),FILNUM                                                   
         MVI   P+39,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(40,P)                                  
         B     EOJ                                                              
*                                                                               
EOJ1     DS    0H                                                               
         MVC   P(43),=C'PP131 ** PRNT   RECOVERY FILE NOT ERASED **'            
         MVC   P+13(2),FILNUM                                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(43),=C'PP131 ** PRNT   RECOVERY FILE NOT ERASED **'            
         MVC   P+13(2),FILNUM                                                   
         MVI   P+43,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(44,P)                                  
         CLI   ERASERR,YES                                                      
         BNE   EOJ                                                              
         ABEND 661                                                              
*                                                                               
EOJ      XBASE                                                                  
*                                                                               
SYSMSG   DC    C'** PP131 ** NO VALID SYSTEM - ABORT'                           
SYSERR   DS    0H                                                               
         MVC   P(L'SYSMSG),SYSMSG                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(L'SYSMSG),SYSMSG                                               
         MVI   P+L'SYSMSG,X'15'                                                 
         LA    R0,L'SYSMSG+1                                                    
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),((R0),P)                                
*                                                                               
         MVC   P(80),CARD                                                       
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVI   P+78,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(79,P)                                  
*                                                                               
CANCEL   DS    0H                                                               
         ABEND 999                                                              
*                                                                               
CRDERR   MVC   P(34),=C'PP131 ** INVALID PARAMETER CARD **'                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(34),=C'PP131 ** INVALID PARAMETER CARD **'                     
         MVI   P+35,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(43,P)                                  
*                                                                               
         MVC   P(80),CARD                                                       
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVI   P+78,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(79,P)                                  
         B     CANCEL                                                           
*                                                                               
ERASER   MVC   P(L'ERASEMSG),ERASEMSG                                           
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(L'ERASEMSG),ERASEMSG                                           
         MVI   P+L'ERASEMSG,X'15'                                               
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(65,P)                                  
*                                                                               
         MVC   P(80),CARD                                                       
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVI   P+78,X'15'                                                       
         GOTO1 =V(LOGIO),DMCB,(X'FF',1),(79,P)                                  
*                                                                               
         ABEND 201                                                              
*                                                                               
ERASEMSG DC    C'PP131 ** ERASE= CARD MISSING OR INCORRECT. MUST SPECIFX        
               Y YES OR NO'                                                     
*                                                                               
         CNOP  2,4                                                              
CTRS     DC    H'18'                                                            
         DC    A(CTRSX-1)                                                       
         DC    X'4203',CL12'PRTFILE ADDS',PL4'0'                                
         DC    X'4201',CL12'PRTFILE CPYS',PL4'0'                                
         DC    X'4202',CL12'PRTFILE CHGS',PL4'0'                                
         DC    X'4003',CL12'DIRONLY ADDS',PL4'0'                                
         DC    X'4001',CL12'DIRONLY CPYS',PL4'0'                                
         DC    X'4002',CL12'DIRONLY CHGS',PL4'0'                                
         DC    X'4303',CL12'PUBFILE ADDS',PL4'0'                                
         DC    X'4301',CL12'PUBFILE CPYS',PL4'0'                                
         DC    X'4302',CL12'PUBFILE CHGS',PL4'0'                                
CTRSX    EQU   *                                                                
*                                                                               
DIRONLY  DC    X'0000',CL12'PRTDIR ONLY ',PL4'0'                                
TRECSIN  DC    X'0000',CL12'RECOVERY IN ',PL4'0'                                
TRECS1   DC    X'0000',CL12'OUT1        ',PL4'0'                                
TRECS2   DC    X'0000',CL12'OUT2-BACKUP ',PL4'0'                                
DIRRECS  DC    X'0000',CL12'DIR OVERLAYS',PL4'0'                                
TCTRSX   EQU   *                                                                
         EJECT                                                                  
*                                                                               
FLIST    DC    CL8'NPRECV  '                                                    
         DC    CL8'X       '                                                    
*                                                                               
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
ERASERR  DC    AL1(NO)                                                          
ERASESW  DC    C' '                                                             
SENAME   DC    CL8' '              SE NAME                                      
SENUM    DC    XL1'00'             SE NUMBER                                    
FILNUM   DC    C'  '               FILE NUMBER NOW 2 CHS                        
FILNAME  DC    CL8'FILE-ID'        FILE NAME                                    
*                                                                               
DMCB     DS    6F                                                               
         ORG   *-24                                                             
DM1      DS    F                                                                
DM2      DS    F                                                                
DM3      DS    F                                                                
DM4      DS    F                                                                
DM5      DS    F                                                                
DM6      DS    F                                                                
*                                                                               
DA       DC    F'0'                                                             
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
*                                                                               
BUYERR   DS    CL1                                                              
*                                                                               
CARD     DS    CL80                                                             
SAVEREC  DS    0XL31                                                            
         DS    XL27                                                             
SAVEADDR DS    XL4                                                              
*                                                                               
ERSPARS  DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(ERSPAR6)                                                       
ERSPAR6  DC    A(0)                ** ERASE WHOLE FILE  **                      
*                                                                               
         LTORG                                                                  
*                                                                               
RECLEN   DS    F                                                                
REC      DS    8000C                                                            
         DS    60000C              OVERFLOW BUFFER!                             
         ORG   REC                                                              
       ++INCLUDE DMRCVRHDR                                                      
RECKEY   DS    XL25                                                             
         DS    XL2                                                              
RECADDR  DS    XL4                                                              
         ORG                                                                    
         EJECT                                                                  
RCVTAPE  DCB   DDNAME=OUT1,                                            X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08200,                                            X        
               BLKSIZE=27648,                                          X        
               MACRF=PM                                                         
*                                                                               
RCVCOPY  DCB   DDNAME=OUT2,                                            X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08200,                                            X        
               BLKSIZE=27648,                                          X        
               MACRF=PM                                                         
*                                                                               
BUFF     DS    64000C              BUFFER FOR FULL TRACK READ                   
*                                                                               
         DS    0D                                                               
         DC    CL8'**UTL **'                                                    
UTL      DC    F'0',X'00',XL3'00',XL56'00'                                      
         DS    0D                                                               
         DC    CL8'**SSB **'                                                    
SSB      DC    X'0000FF',X'02',4X'00',CL8' ',32X'00',A(0),204X'00'              
         DS    0D                                                               
         DC    CL8'**WORK**'                                                    
WORK     DS    2000D                                                            
         EJECT                                                                  
       ++INCLUDE DMGREQUS                                                       
*                                                                               
       ++INCLUDE DDDPRINT                                                       
*                                                                               
DDNAMED  DSECT                                                                  
       ++INCLUDE DMDDNAMED                                                      
*                                                                               
DMDTFPH  DSECT                                                                  
       ++INCLUDE DMDTFPH                                                        
*                                                                               
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'046PP131     11/15/11'                                      
         END                                                                    
