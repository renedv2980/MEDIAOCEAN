*          DATA SET REGEPBYBU  AT LEVEL 082 AS OF 05/15/00                      
*CATALP REGENPBY                                                                
         TITLE 'REGENPBY - REPPAK BUY LINE DISPLAY'                             
*              PARAMETER 1 =       A(BUYREC)                                    
*                                       BYTE 0 = X'80' INVOKED BY               
*                                                RECNT63                        
*                        2 =       A(OUTPUT AREA)                               
*                                       BYTE 0 = MAXIMUM LINES                  
*                                       OUTPUT AREA = 81 X MAX LINES            
*                                       BYTE 0 = NUMBER LINES ON RETURN         
*                                       THIS CAN BE 0 (DELETES)                 
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                       WORD 1 = A(UNDAY)                       
*                                            2 = A(UNTIME)                      
*                                            3 = A(DATCON)                      
*                                            4 = A(ADDAY)                       
*                                            IF 4 IS 0 THEN ALTERNATE           
*                                            WEEKS ARE NOT EXPLODED             
*                        4 =       A(CONREC)                                    
*                                                                               
*                        5 =       A(CONTRACT PROGRAM PROFILES)                 
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* 18MAR/92 (SKU) --- FIX MCI BUG                                                
*                                                                               
* 12APR/95 (BU ) --- MULTIPLE MG MISSED DATES                                   
*                                                                               
* 31MAY/95 (SKU) --- MAKEGOOD CONTINUATION TO 2ND COMMENT LINE SUPPORT          
*                                                                               
* 13JUL/95 (SKU) --- FIX CONFIRMATION PRINT BUG                                 
*                                                                               
* 13OCT/95 (BU ) --- ADD KATZ DISPLAY CHANGES:  BAD INFO                        
*                                                                               
* 12FEB/95 (SKU) --- SKIP X'07' CREDIT FROM TOTAL SPOT COUNT                    
*                                                                               
* 02MAR/96 (SKU) --- FIX CONFIRMATION PRINT BUG AGAIN                           
*                                                                               
* 20AUG/98 (SKU) --- DISPLAY DIRECT CREDIT HISTORY                              
*                                                                               
* 22DEC/98 (RHV) --- HANDLE NON-DELETED CANCELLED BUYS                          
*                                                                               
* 20JAN/00 (SKU) --- PROFILE DRIVEN CREDIT HISTORY PRINTING IN                  
*                    WORKSHEET ONLY                                             
* ***  END TOMBSTONE  ***                                                       
***********************************************************************         
REGENPBY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENPBY,RR=R3                                       
         USING WORKD,RC                                                         
         L     RA,0(R1)            DSECT FOR BUYREC                             
         ST    R1,SAVER1                                                        
         USING BUYREC,RA                                                        
         L     R9,4(R1)            OUTPUT AREA                                  
         USING PD,R9               DISPLAY LINE DSECT                           
         USING CONREC,R8                                                        
         L     R8,12(R1)                                                        
*                                                                               
         ST    R3,RELO                                                          
*                                                                               
         MVC   FLAGS,0(R1)         SAVE FLAGS                                   
*                                                                               
         ICM   RE,15,16(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   PROFILES,0(RE)      SAVE CONTRACT PROFILES                       
*                                                                               
         L     R2,8(R1)            ROUTINES                                     
         MVC   VOUTDAY(16),0(R2)                                                
         MVI   MAX,0                                                            
         MVC   MAX+1(1),4(R1)      MAX LINES                                    
         MVI   4(R1),0             NO LINES SO FAR                              
         MVC   DATADISP,=H'34'                                                  
*                                                                               
         TM    RBUYCNTL,X'C0'      VOID POINTER?                                
         BO    EXIT                                                             
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    P4                                                               
         TM    RBUYCNTL,X'80'      DELETED?                                     
         BZ    P5                                                               
         CLI   RBUYCHGI,C'X'       DELETED (NOT CANCELLED?)                     
         BE    EXIT                                                             
* MUST HAVE BEEN CANCELLED                                                      
P4       DS    0H                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P5                  YES                                          
         TM    RCONMODR+1,X'C0'    ACE/GRAPHNET                                 
         BNZ   P5                  DO CANCELLED ACE/GRAPHNET BUYS LATER         
         CLC   RBUYCREA,RBUYCHGD   CREATED SAME DAY AS CANCEL?                  
         BE    EXIT                                                             
         CLC   RBUYKMOD,RCONMOD    BUY K LEVEL V K MOD NUM                      
         BL    EXIT                                                             
*                                                                               
* GET TODAY'S DATE                                                              
P5       GOTO1 VDATCON,DMCB,(5,0),(3,TODAY)                                     
*                                                                               
         LR    RE,R9                                                            
         LH    RF,MAX              MAXIMUM LINES                                
         MH    RF,=Y(L'PRTLN)      LINE COUNT                                   
*                                                                               
*         XCEF                      CLEAR OUTPUT AREA                           
         XCEF                                                                   
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   P6                                                               
* DISPLAY PLAN TOTAL REC LINE                                                   
         L     R1,SAVER1                                                        
         MVI   4(R1),1             1 LINE                                       
         MVC   PRAT-28(27),=C'WEEKLY RATE FOR PLAN XXX IS'                      
         MVC   PRAT-7(3),RBUYKPLN                                               
         MVC   DUB(4),RBUYCOS                                                   
         L     R4,DUB                                                           
         EDIT  (R4),(10,PRAT),2,FLOAT=-,COMMAS=YES,ALIGN=LEFT                   
         EDIT  (2,RBUYTSPT),(4,PTOT-1)                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD PRINT LINE                                                              
* CHECK FOR MODIFICATION CODE PRINT                                             
***********************************************************************         
P6       TM    RCONMODR+1,X'C0'    X'C0'=ACE/GRAPHNET CONTRACT                  
         BZ    P6P                                                              
*                                                                               
         SR    R4,R4                                                            
         LA    R1,RCONELEM                                                      
P6C      ZIC   R4,1(R1)            FIND X'1F' ELEMENT                           
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ELEMENT SHOULD BE THERE                      
         CLI   0(R1),X'1F'                                                      
         BNE   P6C                                                              
*                                                                               
         MVC   SVCONF,6(R1)        SAVE CONFIRMED STATUS                        
         TM    6(R1),X'80'         X'80'=NOT CONFIRMED                          
         BZ    P6F                                                              
*                                                                               
         ZIC   R4,1(R1)                                                         
         AR    R1,R4               GET X'20' (SEND) ELEMENT                     
         CLI   0(R1),X'20'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   5(R1),1             DON'T PRINT CHANGES WHEN                     
         BNE   P6D                 REP VERSION IS 1 AND                         
         CLI   14(R1),0            STATION VERSION IS 0                         
         BNE   P6D                                                              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    P7                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                                                               
*                                                                               
P6D      CLC   5(1,R1),14(R1)      COMPARE REP TO STATION VERSION               
         BH    P6E       THEN COMPARE BUY VERSION TO HIGHER OF THE TWO          
*                        & DON'T PRINT CHANGES FOR OLD VERSIONS                 
         CLC   RBUYVER,14(R1)      STATION VERSION                              
         BE    P6S                                                              
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
         B     EXIT                DON'T PRINT LINE AT ALL                      
*                                                                               
P6E      CLC   RBUYVER,5(R1)       REP VERSION                                  
         BE    P6S                                                              
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
         B     EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
* CONFIRMED -  ONLY PRINT CHANGES FROM LAST MOD NUMBER, AND DON'T               
*              PRINT CHANGE CODE O (ORDER COMMENT)                              
         SPACE 1                                                                
P6F      DS    0H                                                               
*                                                                               
         CLI   RCONMOD,0           DON'T PRINT CHANGES FOR MOD 0                
         BE    P6FA                                                             
*                                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6F10                                                            
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P6F10               CHECK IF CANCELLED AND ADDED IN THE          
         LA    R6,RBUYREC          SAME MOD NUM                                 
         MVI   ELCODE,X'10'        EXTRA DESCRIPTION ELEMENT PRESENT?           
         BAS   RE,GETEL                                                         
         BNE   P6F10                                                            
         USING RBUYXXEL,R6         COMPARE                                      
         CLC   RBUYXXMD,RBUYKMOD   MOD NUMBER WHEN BUY WAS ADDED AND            
         BE    EXIT                MOD NUMBER WHEN IT WAS CANCELLED             
         DROP  R6                  DON'T PRINT LINE IF SAME                     
*                                                                               
P6F10    DS    0H                                                               
         ZIC   R4,RCONMOD                                                       
         BCTR  R4,0                GET LAST MOD NUMBER                          
         ZIC   R1,RBUYKMOD                                                      
         CR    R1,R4                                                            
         BE    P6G                                                              
P6FA     CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
P6G      DS    0H                                                               
         LA    R4,PCHG                                                          
         CLI   RBUYCHGI,C'O'       ORDER COMMENT                                
         BE    P6H                                                              
         MVC   0(1,R4),RBUYCHGI                                                 
         LA    R4,1(R4)                                                         
         MVI   0(R4),C' '          MOVE BLANK TO 2ND CHARACTER                  
         SPACE 1                                                                
P6H      CLI   RBUYCHGI+1,C'O'     ORDER COMMENT                                
         BE    P7                                                               
         CLI   RBUYCHGI+1,C' '     BLANK                                        
         BE    P7                                                               
         MVC   0(1,R4),RBUYCHGI+1                                               
         OC    PCHG,=C'  '         SPACES                                       
         B     P7                                                               
         SPACE 1                                                                
* FOR NON ACE/GRAPHNET CONTRACTS                                                
         SPACE 1                                                                
P6P      DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P6Q                                                              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
P6Q      DS    0H                                                               
         CLI   RCONMOD,0           K MOD NUM                                    
         BE    P7                                                               
         CLI   RCONMOD,X'FF'                                                    
         BE    P7                                                               
         CLI   RBUYKMOD,X'FF'                                                   
         BE    P7                                                               
         CLI   RBUYKMOD,0          BUY K MOD NUM                                
         BE    P7                                                               
         CLC   RBUYKMOD,RCONMOD                                                 
         BL    P7                                                               
*                                                                               
P6S      MVC   PCHG,RBUYCHGI                                                    
*                                                                               
P7       EDIT  (1,RBUYKLIN),(3,PLIN)    LINE NUMBER                             
*                                                                               
         MVC   DMCB(2),RBUYDUR     LENGTH                                       
         NI    DMCB,X'7F'          MINUTE IND                                   
         EDIT  (2,DMCB),(3,PLEN)                                                
         TM    RBUYDUR,X'80'       MINUTES?                                     
         BZ    P9                                                               
         MVC   PLEN-1(3),PLEN                                                   
         MVI   PLEN+2,C'M'                                                      
*                                                                               
P9       MVC   PCLS,RBUYCLS        CLASS                                        
         MVC   PSEC,RBUYSEC        SECTION                                      
         MVC   PTYP,RBUYTYP        SECTION                                      
*                                                                               
* DISPLAY FLIGHT NUMBER IF NO SECTION EXISTS FOR BUY                            
*                                                                               
P12      CLI   RBUYFLT,0           DOES BUY HAVE A FLIGHT                       
         BE    P15                 NO                                           
         OC    VADDAY,VADDAY       ONLY DO IT IF ON-LINE                        
         BNZ   P15                                                              
         CLI   PSEC,0              TEST FOR ABSENCE OF SECTION                  
         BE    *+12                                                             
         CLI   PSEC,C' '                                                        
         BNE   P15                                                              
         MVI   PSEC,C'F'           FLIGHT CODE PREFIX                           
         EDIT  (B1,RBUYFLT),(2,PSEC+1),ALIGN=LEFT                               
*                                                                               
P15      MVC   PPLN,RBUYKPLN       PLAN                                         
         CLC   RBUYKPLN,=3X'FF'                                                 
         BNE   *+10                                                             
         MVC   PPLN,=3C' '                                                      
*                                                                               
*                                                                               
         CLC   RBUYKPLN,=3X'FF'    PLAN                                         
         BE    *+14                                                             
         MVC   PRAT+3(4),=C'PLAN'                                               
         B     P50                                                              
         EDIT  (4,RBUYCOS),(10,PRAT),2,FLOAT=-,COMMAS=YES                       
*                                                                               
P50      MVC   DUB(2),RBUYLEN      REC LENGTH                                   
         LH    R5,DUB                                                           
         LA    R5,RBUYREC(R5)      REC END FOR BXLE                             
*              PREPARE TO GET DAY-TIME ELEMENTS                                 
         BCTR  R5,R0                                                            
         BCTR  R5,R0                                                            
         SR    R4,R4                                                            
         LA    R3,RBUYELEM         FIRST ELEM                                   
* GET DAY SPAN                                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RE,RBUYSTED                                                      
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CR    RE,RF                                                            
         BNH   *+8                                                              
         LA    RF,7(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,DAY1                                                          
*                                                                               
         SH    R9,=Y(L'PRTLN)                                                   
*                                                                               
         SR    R6,R6               LINE CTR FOR DAY-TIME LINES                  
         B     P125                NO DATE ELEM                                 
*              DISPLAY DAY-TIME                                                 
P100     EQU   *                                                                
         CLC   =X'FFFF',2(R3)      CONVERT ERROR DATA?                          
         BNE   P104                NO                                           
         GOTO1 =A(KZDAYERR),RR=RELO YES - RETRIEVE FROM ERROR ELEMENT           
         B     P106                                                             
P104     EQU   *                                                                
         GOTO1 VOUTDAY,DMCB,3(R3),2(R3),PDAY                                    
P106     EQU   *                                                                
*                                                                               
         CLC   =X'FFFF',4(R3)      CONVERT ERROR DATA?                          
         BNE   P108                NO                                           
         GOTO1 =A(KZTIMERR),RR=RELO YES - RETRIEVE FROM ERROR ELEMENT           
         B     P110                                                             
P108     EQU   *                                                                
         GOTO1 VUNTIME,DMCB,4(R3),PTIM                                          
P110     EQU   *                                                                
         B     P125                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
P125     ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),2             DAY TIME ELEM?                               
         BNE   P200                                                             
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R6,1(R6)            DAY-TIME CTR                                 
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    P125                                                             
         B     P100                DISPLAY NEXT DAY-TIME                        
*                                                                               
*              GET DATE ELEMENTS                                                
P200     SR    R7,R7               DATE LINE CTR                                
         L     R1,SAVER1                                                        
         L     R9,4(R1)                                                         
         SH    R9,=Y(L'PRTLN)                                                   
         CLI   0(R3),3             DATE ELEM                                    
         BNE   P260                IF NONE JUST CARRY ON                        
         B     P255                                                             
* PROCESS DATE ELEMENT                                                          
P225     SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RF,9(R3)            NO. PER WEEK                                 
* DISPLAY NPW                                                                   
         EDIT  (RF),(3,PNPW)                                                    
         SR    R2,R2                                                            
         ZIC   R2,10(R3)           NO. OF WEEKS                                 
* DISPLAY NO. OF WEEKS IF NO CLASS                                              
         CLI   PCLS,0                                                           
         BE    *+12                                                             
         CLI   PCLS,C' '                                                        
         BNE   P226                                                             
         EDIT  (R2),(2,PCLS)                                                    
         MVI   PCLS+2,C'W'                                                      
* DISPLAY TOTAL SPOTS MINUS MISSED SPOTS                                        
***>     TM    RBUYCNTL,X'80'                                                   
P226     CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    P229                                                             
         SPACE 1                                                                
         LA    R1,RBUYELEM                                                      
         ZIC   R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BE    P226A                                                            
         CLI   0(R1),4                                                          
         BNE   *-18                                                             
         CLC   2(3,R1),=C'CR='                                                  
         BE    P226B               IGNORE THE MINUS BUY CHECK                   
         SPACE 1                                                                
P226A    TM    RBUYCOS,X'80'                                                    
         BO    P229                                                             
P226B    MR    RE,R2               TOTAL SPOTS IN RF                            
* GET MISSED SPOTS, IF ANY                                                      
         LA    R1,RBUYELEM                                                      
P227     CLI   0(R1),0             LAST?                                        
         BE    P228                                                             
         CLI   0(R1),6             MISSED ELEM?                                 
         BE    P227D                                                            
*        CLI   0(R1),7             MISSED CREDIT ISSUED                         
*        BE    P227D                                                            
P227B    ZIC   R4,1(R1)                                                         
         AR    R1,R4               NEXT ELEM                                    
         B     P227                                                             
* CHECK MISSED DATE AGAINST DATE ELEM                                           
P227D    CLC   2(3,R1),2(R3)        START                                       
         BL    P227B                                                            
         CLC   2(3,R1),5(R3)       END                                          
         BH    P227B                                                            
* MISSED ELEM APPLIES, SUBTRACT MISSED SPOTS                                    
         ZIC   R4,6(R1)            MISSED SPOTS                                 
         SR    RF,R4                                                            
         B     P227B                                                            
* DISPLAY TOTAL SPOTS FOR DATE ELEM                                             
P228     EDIT  (RF),(4,PTOT-1)                                                  
* TEST FOR ALT WEEKS                                                            
P229     TM    8(R3),X'40'         ALT?                                         
         BZ    P235                                                             
         OC    VADDAY,VADDAY                                                    
         BZ    P235                                                             
* MUST BE OFF-LINE CONTRACT - EXPLODE ALTERNATE WEEKS                           
*                                                                               
* GET START-END IN WORK                                                         
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK                                      
*                                                                               
         GOTO1 (RF),(R1),(3,5(R3)),WORK+6                                       
P230     GOTO1 VDATCON,(R1),WORK,(4,PDAT)                                       
         MVI   PDAT+11,C'A'                                                     
         L     R2,DAY1                                                          
         LTR   R2,R2               ROTATOR?                                     
         BZ    P232                                                             
* GET END DATE OF WEEK                                                          
         GOTO1 VADDAY,(R1),WORK,WORK+12,(R2)                                    
*                                                                               
         GOTO1 VDATCON,(R1),WORK+12,(4,PDAT+6)                                  
         MVI   PDAT+5,C'-'                                                      
* GET NEXT ALTERNATE WEEK                                                       
P232     MVC   DUB(6),WORK                                                      
         GOTO1 VADDAY,(R1),DUB,WORK,14                                          
         CLC   WORK(6),WORK+6      PAST ELEM END DATE?                          
         BH    P250                                                             
*                                                                               
* DISPLAY NEXT ALTERNATE WEEK                                                   
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R7,1(R7)            LINE CTR                                     
         CH    R7,MAX                                                           
         BNH   P230                                                             
         LR    R6,R7                                                            
         B     PXIT                                                             
P235     GOTO1 VDATCON,DMCB,(3,2(R3)),(4,PDAT)                                  
         CLC   2(3,R3),5(R3)       SAME START-END?                              
         BE    P240                                                             
*                                                                               
         MVI   PDAT+5,C'-'                                                      
*              CONVERT ELEMENT END DATE TO MMMDD                                
         GOTO1 VDATCON,DMCB,(3,5(R3)),(4,PDAT+6)                                
*                                                                               
P240     TM    8(R3),X'40'         ALTERNATE WEEKS?                             
         BZ    P250                                                             
         MVI   PDAT+11,C'A'                                                     
         LA    R9,L'PRTLN(R9)                                                   
         LA    R7,1(R7)                                                         
         CH    R7,MAX                                                           
         BNH   *+10                                                             
         LR    R6,R7                                                            
         B     PXIT                                                             
         MVC   PDAT(15),=C'ALTERNATE WEEKS'                                     
*              GET NEXT ELEM                                                    
P250     ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     P260                                                             
*                                                                               
         CLI   0(R3),3             ANOTHER DATE ELEM?                           
         BNE   P260                                                             
         TM    8(R3),X'40'         ALT WEEKS                                    
         BZ    P255                                                             
         OC    VADDAY,VADDAY                                                    
         BZ    P255                                                             
         MVI   PDAT+79,C'*'        IND ANOTHER ALT ELEM                         
P255     LA    R7,1(R7)            DATE CTR                                     
         LA    R9,L'PRTLN(R9)                                                   
         CH    R7,MAX                                                           
         BNH   P225                DISPLAY NEXT DATE ELEM                       
         LR    R6,R7                                                            
         B     PXIT                                                             
*                                                                               
P260     DS    0H                                                               
         CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    PXIT                                                             
*                                                                               
         TM    RBUYRTS,X'20'+X'04'                                              
         BNO   P263                                                             
         LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
         MVC   PDAY+6(14),=C'LATE RUN BONUS'                                    
         LA    R6,1(R6)            BUMP LINE COUNT                              
         CH    R6,MAX                                                           
         BH    PXIT                                                             
         B     P280                                                             
*                                                                               
P263     DS    0H                                                               
         LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
*                                                                               
         MVI   MGCNTR#,0           CLEAR COUNTER                                
         LA    R2,PDAT+1           RESET A(PRINTOUT)                            
         LR    R7,R2                                                            
*                                                                               
P265     DS    0H                                                               
         CLI   0(R3),0                                                          
         BE    P280                                                             
         CLI   0(R3),4                                                          
         BE    P267                                                             
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BE    P268                                                             
*                                                                               
P266     DS    0H                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         AR    R3,R4               NEXT ELEM                                    
         B     P265                                                             
*                                                                               
P267     DS    0H                                                               
         CLC   =C'CR=',2(R3)       SKIP IF CREDIT FOR SPOT                      
         BE    P280                                                             
         B     P266                                                             
*                                                                               
P268     DS    0H                                                               
         ZIC   RF,MGCNTR#          INCREMENT COUNTER                            
         AHI   RF,1                                                             
         STC   RF,MGCNTR#                                                       
*                                                                               
         CLI   MGCNTR#,1                                                        
         BE    P270                                                             
         CLI   MGCNTR#,3           MORE THAN 3 MG ELEMENTS?                     
         BNH   P275                NO                                           
*                                                                               
         LA    R9,L'PRTLN(R9)      BUMP TO NEXT PRINT LINE                      
         MVI   MGCNTR#,1           CLEAR COUNTER                                
         LA    R2,PDAT+1           RESET A(PRINTOUT)                            
         LR    R7,R2                                                            
*                                                                               
P270     EQU   *                                                                
         AHI   R6,1                BUMP LINE COUNT                              
         CH    R6,MAX                                                           
         BH    PXIT                                                             
         MVC   PDAY+6(21),=C'MAKE-GOOD FOR LINE(S)'                             
         TM    RBUYRTS,X'20'                                                    
         BO    P275                                                             
         TM    RBUYRTS,X'08'+X'04'                                              
         BZ    P275                                                             
         MVC   PDAY+6(21),=C'LATE RUN FOR LINE(S) '                             
*                                                                               
* DISPLAY MAKE-GOOD REFERENCE                                                   
*                                                                               
P275     EQU   *                                                                
         LR    R2,R7                                                            
         GOTO1 VDATCON,DMCB,(X'83',3(R3)),(4,(R2))    MISSED DATE               
         ZIC   R1,DMCB+4                                                        
         AR    R2,R1                                                            
         MVI   0(R2),C'('                                                       
         AHI   R2,1                                                             
         EDIT  (1,9(R3)),(3,0(R2)),ALIGN=LEFT,ZERO=NOBLANK                      
         AR    R2,R0                                                            
         MVC   0(2,R2),=C')-'                                                   
         AHI   R2,2                                                             
         EDIT  (1,2(R3)),(3,0(R2)),ALIGN=LEFT                                   
         AHI   R7,15                                                            
         B     P266                                                             
*                                                                               
*              PREPARE TO DISPLAY COMMENTS                                      
*                                                                               
P280     DS    0H                                                               
         LM    R3,R5,DMCB+12       BXLE                                         
         CLI   0(R3),4             COMMENT ELEM?                                
         BE    P290                                                             
         CLI   0(R3),0             END OF RECORD?                               
         BE    PXIT                                                             
         B     P400                CHECK FOR ORDER COMMENTS                     
         SPACE 1                                                                
P290     LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)         POSITION TO NEXT AVAILABLE LINE              
*                                                                               
*                                                                               
*                                                                               
*              DISPLAY COMMENT                                                  
P300     CLC   2(3,R3),=C'CR='     CREDIT FOR MISSED                            
         BNE   P305                                                             
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
P301     ZIC   R4,1(R3)                                                         
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     P315                                                             
         CLI   0(R3),0                                                          
         BE    P315                                                             
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   P301                                                             
         SPACE 1                                                                
         MVC   PDAY+6(29),=C'CREDIT FOR SPOT          LINE'                     
         CLI   9(R3),2             NO. OF MISSED                                
         BL    *+10                                                             
         MVC   PDAY+6(29),=C'CREDIT FOR SPOTS         LINE'                     
         SPACE 1                                                                
         EDIT  (1,2(R3)),(2,PDAY+36),ALIGN=LEFT                                 
         B     P315                                                             
         SPACE 1                                                                
P305     DS    0H                                                               
         CLC   2(3,R3),=C'MG='     SKIP OLD STYLE MAKE-GOOD FORMULA             
         BNE   P325                                                             
*                                                                               
P315     LM    R3,R5,DMCB+12       BXLE                                         
* CHECK FOR COMMENT ALONG WITH MG=                                              
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         ZIC   RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
P320     CLI   0(RE),C' '                                                       
         BE    P322                                                             
         CLI   0(RE),C'+'          CHECK FOR CONTINUATION CHAR                  
         BE    P360                                                             
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    P320                                                             
         B     P350                                                             
* MOVE REST OF COMMENT                                                          
P322     DS    0H                                                               
         CLC   =C'CREDIT:',1(RE)   DISPLAY CREDIT HISTORY?                      
         BNE   P323                                                             
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P355                                                             
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P355                                                             
*                                                                               
P323     DS    0H                                                               
         SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    P350                                                             
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)      NEXT LINE                                    
         CH    R6,MAX                                                           
         BH    PXIT                                                             
*                                                                               
         EX    RF,*+8                                                           
         B     P350                                                             
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
P324     DS    0H                                                               
         CLC   =C'CREDIT:',2(R3)   DISPLAY CREDIT HISTORY?                      
         BNE   P325                                                             
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P355                                                             
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P355                                                             
*                                                                               
P325     ZIC   R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
P350     LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
P355     ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   P410                CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BNH   P325                DISPLAY NEXT COMMENT                         
         B     PXIT                OR RETURN                                    
*                                                                               
P360     DS    0H                  CONTINUING MAKEGOOD IN 2ND CMMT LINE         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   P410                CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    PXIT                DISPLAY NEXT COMMENT OR RETURN               
*                                                                               
P365     DS    0H                                                               
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         ZIC   RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
P370     CLI   0(RE),C' '                                                       
         BE    P380                                                             
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    P370                                                             
         B     P390                                                             
* MOVE REST OF COMMENT                                                          
P380     SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    P390                                                             
         CH    R6,MAX                                                           
         BH    PXIT                                                             
*                                                                               
         EX    RF,*+8                                                           
         B     P390                                                             
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
P390     LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
P400     LR    R7,R6               IF NO REGULAR BUY CMTS,                      
         MH    R7,=Y(L'PRTLN)      POSITION TO NEXT AVAILABLE LINE              
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7                                                            
*                                                                               
* DISPLAY CREDIT AUDIT TRAIL                                                    
*                                                                               
P410     DS    0H                                                               
         SR    R7,R7                                                            
*                                                                               
P411     DS    0H                                                               
         CLI   0(R3),X'16'                                                      
         BE    P413                                                             
         BH    P415                                                             
*                                                                               
P412     DS    0H                                                               
         ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PXIT                                                             
         AR    R3,RF                                                            
         B     P411                                                             
*                                                                               
P413     DS    0H                                                               
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P415                                                             
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P415                                                             
*                                                                               
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
*                                                                               
         LTR   R7,R7                                                            
         BZ    P414A                                                            
         MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
         B     P414B                                                            
*                                                                               
P414A    DS    0H                                                               
         LA    R4,PDAY+6                                                        
         MVC   0(7,R4),=C'CREDIT:'                                              
         AHI   R4,7                                                             
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
*                                                                               
P414B    DS    0H                                                               
         USING RBUYCAEL,R3                                                      
         GOTO1 VDATCON,DMCB,(3,RBUYCASD),(4,(R4))                               
         AHI   R4,5                START DATE                                   
         OC    RBUYCAED,RBUYCAED                                                
         BZ    P414C                                                            
         CLC   RBUYCASD,RBUYCAED                                                
         BE    P414C                                                            
         MVI   0(R4),C'-'          END DATE IF ANY                              
         AHI   R4,1                                                             
         GOTO1 VDATCON,DMCB,(3,RBUYCAED),(4,(R4))                               
         AHI   R4,5                                                             
         B     P414D                                                            
*                                                                               
P414C    DS    0H                                                               
         CLI   RBUYCANW,1                                                       
         BNH   P414E                                                            
         MVI   0(R4),C'-'                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCANW,(3,0(R4)),ALIGN=LEFT                                    
         AR    R4,R0                                                            
         MVI   0(R4),C'W'                                                       
         AHI   R4,1                                                             
*                                                                               
P414D    DS    0H                                                               
         TM    RBUYCAFL,X'80'      ALTERNATING WEEKS??                          
         BZ    P414E                                                            
         MVI   0(R4),C'A'                                                       
         AHI   R4,1                                                             
*                                                                               
P414E    DS    0H                  NUMBER OF SPOTS CREDITED                     
         MVI   0(R4),C'('                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCASP,(3,(R4)),ALIGN=LEFT                                     
         AR    R4,R0                                                            
         MVI   0(R4),C')'                                                       
         AHI   R4,1                                                             
         AHI   R7,1                                                             
         CHI   R7,4                                                             
         BL    P412                                                             
         SR    R7,R7                                                            
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R4,PDAY+6                                                        
         B     P412                                                             
         DROP  R3                                                               
*                                                                               
* DISPLAY THE MAKEGOOD LINES WHICH THIS BUY HAS BEEN MADE GOOD BY               
*                                                                               
P415     DS    0H                                                               
         CLI   0(R3),X'56'                                                      
         BE    P420                                                             
         BH    P500                                                             
         ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PXIT                                                             
         AR    R3,RF                                                            
         B     P415                                                             
*                                                                               
P420     DS    0H                                                               
         GOTO1 =A(BLDBLIST),RR=RELO                                             
*                                                                               
         LA    R7,BUYLIST                                                       
         LA    RE,1                                                             
*                                                                               
P430     DS    0H                                                               
         MVC   PDAY+6(20),=C'MADE-GOOD BY LINE(S)'                              
         LA    R2,PDAY+30                                                       
*                                                                               
P440     DS    0H                  IF CELL IS MARKED WITH AN X                  
         CLI   0(R7),C'X'          THERE IS A X'56' ELEMENT FOR THIS            
         BE    P450                BUY                                          
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    P470                                                             
         AHI   R7,1                                                             
         B     P440                                                             
*                                                                               
P450     DS    0H                                                               
         EDIT  (RE),(3,0(R2)),ALIGN=LEFT                                        
*                                                                               
P460     DS    0H                                                               
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    P470                                                             
         AHI   R7,1                                                             
         AR    R2,R0                                                            
         AHI   R2,1                                                             
*                                                                               
         LA    RF,PD+76            CHECK WE DON'T EXCEED DISPLAY LINE           
         CR    R2,RF                                                            
         BNH   P440                                                             
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
         B     P430                                                             
*                                                                               
P470     DS    0H                                                               
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
*                                                                               
P500     CLI   0(R3),X'84'         ORDER COMMENT?                               
         BE    P510                                                             
         ZIC   R4,1(R3)                                                         
         AR    R3,R4                                                            
         CLI   0(R3),0             END OF RECORD??                              
         BE    PXIT                                                             
         B     P500                                                             
         SPACE 1                                                                
P510     CH    R6,MAX              MAXIMUM LINES?                               
         BNL   PXIT                                                             
         SPACE 1                                                                
         TM    RCONMODR+1,X'C0'    IF ACE OR GRAPHNET                           
         BZ    P520                                                             
         TM    SVCONF,X'40'        AND CONFIRMED                                
         BZ    P520                                                             
***>     TM    RBUYCNTL,X'80'      AND CANCELLED                                
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PXIT                DON'T PRINT ORDER COMMENTS                   
         SPACE 1                                                                
P520     MVC   PDAY+6(4),=C'SOC-'  STATION ORDER CMT                            
         TM    2(R3),X'80'                                                      
         BZ    *+8                                                              
         MVI   PDAY+6,C'R'         REP ORDER CMT (ROC)                          
         ZIC   R4,1(R3)                                                         
         SH    R4,=H'4'                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PDAY+10(0),3(R3)                                                 
         CLC   3(3,R3),=C'#DS'     INDICATES DON'T SEND THIS LINE               
         BNE   *+8                                                              
         MVI   NOSEND,C'D'                                                      
         B     P350                LOOP BACK FOR OTHER ORDER CMTS               
*                                                                               
*              RETURN                                                           
PXIT     L     R1,SAVER1                                                        
         CH    R6,MAX                                                           
         BNH   *+8                                                              
         LH    R6,MAX                                                           
         STC   R6,4(R1)            LINE CTR                                     
         MVI   8(R1),0                                                          
         CLI   NOSEND,C'D'                                                      
         BNE   *+8                                                              
         MVI   8(R1),C'D'          INDICATE THIS LINE IS 'DON'T SEND'           
EXIT     XMOD1 1                                                                
*                                                                               
MOVECOM  MVC   PDAY+6(0),2(R3)                                                  
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
**********************************************************************          
* BUILD MADE-GOOD BUY LIST IN SORTED ORDER                                      
* SINCE X'56' IS IN DATE ORDER, WE NEED TO PUT THE MADEGOOD BUY                 
* NUMBERS IN SORTED ORDER FOR DISPLAY                                           
**********************************************************************          
BLDBLIST NTR1  BASE=*,LABEL=*                                                   
         XC    BUYLIST,BUYLIST                                                  
*                                                                               
         LA    R6,RBUYELEM         FOR EACH BUY WE COME ACROSS,                 
BL03     CLI   0(R6),X'56'                                                      
         BH    BLX                                                              
         BE    BL05                                                             
BL04     ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BNE   BL03                                                             
         B     BLX                                                              
*                                                                               
BL05     DS    0H                                                               
         USING RBYMGSEL,R6         IN BUYLIST                                   
         LA    R3,BUYLIST          255 CELLS                                    
*                                                                               
         ZIC   R4,RBYMGSLI                                                      
         AR    R3,R4                                                            
         BCTR  R3,0                                                             
*                                                                               
         MVI   0(R3),C'X'                                                       
*                                                                               
         B     BL04                                                             
*                                                                               
BLX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*   RETRIEVE THE X'22' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        DAY FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,               
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZDAYERR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RBUYELEM                                                      
KZDA0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'22'                 
         CLI   0(R6),X'22'         ERROR ELEMENT?                               
         BNE   KZDA0020            NO  - GO BACK FOR NEXT                       
         MVC   PDAY(10),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'32' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        TIME FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,              
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZTIMERR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RBUYELEM                                                      
KZTM0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'32'                 
         CLI   0(R6),X'32'         ERROR ELEMENT?                               
         BNE   KZTM0020            NO  - GO BACK FOR NEXT                       
         MVC   PTIM(11),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* PRINT LINE DSECT                                                              
       ++INCLUDE REGENPBYD                                                      
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
WORK     DS    CL17                                                             
SVCONF   DS    CL1                 CONFIRMED STATUS FOR ACE/GRAPHNET            
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(UNTIME)                                    
VDATCON  DS    A                   A(DATCON)                                    
VADDAY   DS    A                   A(ADDAY)                                     
DMCB     DS    6F                                                               
DUB      DS    D                                                                
SAVER1   DS    F                                                                
MAX      DS    H                                                                
TODAY    DS    CL3                                                              
DAY1     DS    F                                                                
NOSEND   DS    X                   DON'T SEND STATUS                            
MGCNTR#  DS    X                                                                
MGTARGET DS    CL3                                                              
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
FLAGS    DS    X                   PASSED FROM PARM 1                           
PROFILES DS    XL8                 CONTRACT PROGRAM PROFILES                    
BUYLIST  DS    XL255                                                            
WORKX    EQU   *                                                                
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
       ++INCLUDE RECNTPROF                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'082REGEPBYBU 05/15/00'                                      
         END                                                                    
