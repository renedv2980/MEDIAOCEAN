*          DATA SET ACPAY2JOB  AT LEVEL 003 AS OF 05/14/20                      
*CATALP PAY2JOB                                                                 
                                                                                
         TITLE 'Add Pay Details To Jobs, Job Trxs And Invoices'                 
                                                                                
* Audit                                                                         
*---------------------------------------------------------------------*         
* ABID 003 16MAY20  EXPENSES EU MF A55/5P - SEED CLAIM ON   DSRD-25289          
*                   PAYMENTS                                                    
* NMAL 002 24Aug18  Increased job limit and j2j transfer limit                  
*                   JOBBMAXQ increased 50  to 500                               
*                   J2JBMAXQ increased 100 to 500                               
*---------------------------------------------------------------------*         
*                                                                               
* -----                                                                         
* Input parameters                                                              
* ----------------                                                              
* Parm1 Bytes 1-4 = A(Pay details), see JPYDETD                                 
                                                                                
* Output parameters                                                             
* -----------------                                                             
* Array passed containing individual errors                                     
* Individual array entries carry error codes, any major error will be           
* set into JPYDERR and cc set to not equal (else equal)                         
                                                                                
* CHECKING FINAL COMPILE                                                        
* level 50 check                                                                
* level 51 check                                                                
* Notes                                                                         
* -----                                                                         
* Registers:  RA    = base register (2)                                         
*             RB    = base register (1)                                         
*             RC    = WORKD                                                     
*             R2-R5 = local work registers                                      
*             R6    = JARAYD                                                    
*             R7    = JPYDETD                                                   
*             R8    = (spare)                                                   
*             R9    = COMFACSD                                                  
*                                                                               
                                                                                
PAY2JOBC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**P2JB**,RA,RR=RE,CLEAR=YES                          
                                                                                
         USING WORKD,RC                                                         
         XC    SVERROR,SVERROR                                                  
         ST    RE,MYRELO                                                        
                                                                                
         USING JPYDETD,R7                                                       
         LR    R7,R1                                                            
                                                                                
         USING COMFACSD,R9                                                      
         ICM   R9,B'1111',JPYDCOM                                               
                                                                                
* Main loop through incomming array                                             
* ---------------------------------                                             
                                                                                
         USING JARAYD,R6                                                        
         SAM31 ,                                                                
         ICM   R6,B'1111',JPYDARY  (point to start of array)                    
         MVI   ANYERR,NOQ                                                       
         ZAP   JPYDTRX,PZERO                                                    
         ZAP   JPYDACT,PZERO                                                    
         ZAP   JPYDINV,PZERO                                                    
         ZAP   JPYDDUP,PZERO                                                    
         ZAP   SVEXPNUM,PZERO      INITALIZE EXPENSE RECORD COUNT               
*                                                                               
PAY2J2   MVI   SVREBUSE,NOQ        set to no REB                                
         MVI   SVJOBUSE,NOQ        set to no JOB                                
         MVI   SVEXPUSE,NOQ        SET TO NO EXPENSE UPDATE                     
         XC    ERRCODE,ERRCODE                                                  
         MVC   LOCJARAY,JARAYD                                                  
         ST    R6,SAVER6                                                        
         SAM24 ,                                                                
         LA    R6,LOCJARAY                                                      
         GOTOR GETCRD              establish creditor invoice trx               
         JE    PAY2J4              - processed                                  
         MVC   JARDERR,ERRCODE                                                  
         JL    PAY2J8              - not applicable                             
         MVI   ANYERR,YESQ         - bad data                                   
         MVC   SVERROR,ERRCODE                                                  
         J     PAY2J8              - not applicable                             
*                                                                               
PAY2J4   CLI   SVEXPUSE,YESQ       ANY EXPENSE RECORD UPDATE ?                  
         JNE   PAY2J5              NO, CONTINEU                                 
*                                                                               
         GOTOR PROEXP              GET & UPDATE EXPENCE RECORDS                 
         JE    PAY2J5              - PROCESSED                                  
*                                                                               
         MVC   JARDERR,ERRCODE     SAVE ERROR CODE                              
         JL    PAY2J5              - NOT APPLICABLE                             
*                                                                               
         MVI   ANYERR,YESQ         - BAD DATA                                   
         MVC   SVERROR,ERRCODE     SAVE ERROR CODE DETAILS                      
*                                                                               
PAY2J5   DS    0H                                                               
*&&UK*&& CLI   SVJOBUSE,YESQ       ANY JOB RELATION?                            
*&&UK*&& JNE   PAY2J6                                                           
         TM    JPYDTYP,JPYDTJQ     are jobs asked for?                          
         JZ    PAY2J6                                                           
                                                                                
         GOTOR PROJOB              get & update job & trxs records              
         JE    PAY2J6              - processed                                  
         MVC   JARDERR,ERRCODE                                                  
         JL    PAY2J6              - not applicable                             
         MVI   ANYERR,YESQ         - bad data                                   
         MVC   SVERROR,ERRCODE                                                  
                                                                                
PAY2J6   DS    0H                                                               
*&&UK                                                                           
         CLI   SVREBUSE,INVQ       any Invoice Log REB?                         
         JNE   PAY2J8                                                           
         TM    JPYDTYP,JPYDRIQ     are invoices asked for?                      
         JZ    PAY2J8                                                           
                                                                                
         GOTOR PROREB              get & updated REB                            
         JE    PAY2J8              - processed                                  
         MVC   JARDERR,ERRCODE                                                  
         JL    PAY2J8              - not applicable                             
         MVI   ANYERR,YESQ         - bad data                                   
         MVC   SVERROR,ERRCODE                                                  
*&&                                                                             
                                                                                
PAY2J8   DS    0H                                                               
         SAM31 ,                                                                
         L     R6,SAVER6                                                        
         MVC   JARAYD(JARAYLQ),LOCJARAY                                         
         AHI   R6,JARAYLQ          next entry unless end of array               
         CLI   JARDFIL,JARDEOT                                                  
         JNE   PAY2J2                                                           
         SAM24 ,                                                                
                                                                                
         CLI   ANYERR,YESQ         any serious error?                           
         JNE   PAY2JYES                                                         
*NMAL    MVC   JPYDERR,ERRCODE                                                  
         MVC   JPYDERR,SVERROR                                                  
                                                                                
* Global exit                                                                   
* -----------                                                                   
                                                                                
PAY2JNO  LTR   RB,RB                                                            
         J     PAY2JX                                                           
                                                                                
PAY2JYES CR    RB,RB                                                            
                                                                                
PAY2JX   XIT1                                                                   
                                                                                
* Routines                                                                      
* --------                                                                      
                                                                                
*** Get main creditor invoice posting and establsih properties ***              
                                                                                
         USING TRNRECD,R2                                                       
GETCRD   NTR1                                                                   
                                                                                
         LA    R2,IOAREA1          Retrieve creditor pay record                 
         LA    RF,DMACCMST         ----------------------------                 
         CLI   JARDFIL,JARDFMQ                                                  
         JE    GCRD002                                                          
         LA    RF,DMACCARC                                                      
         CLI   JARDFIL,JARDFAQ                                                  
         JNE   *+2                                                              
                                                                                
GCRD002  GOTO1 CDATAMGR,MYDMCB,DMGETR,(RF),JARDPDA,TRNRECD,MYDMWRK              
         JE    GCRD003                                                          
         LHI   R1,AE$RCNOF                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDH                                                          
                                                                                
         USING TRNELD,R3                                                        
GCRD003  LA    R3,TRNRFST          Retrieve elements                            
         MVI   ISCREDIT,NOQ                                                     
         TM    TRNSTAT,TRNSDR                                                   
         JNZ   GCRD005                                                          
                                                                                
CRED     USING TRNRECD,R4                                                       
         LA    R4,IOKEY                                                         
         MVC   CRED.TRNKEY,TRNKEY  This is already the credit|                  
         ZAP   SVREBAMT,TRNAMNT                                                 
         CLI   JARPREV,YESQ                                                     
         JNE   GCRD004                                                          
         MP    SVREBAMT,PMONE                                                   
                                                                                
GCRD004  MVC   ISPAYREV,JARPREV                                                 
         MVI   ISCREDIT,YESQ                                                    
         CLI   JARPREV,0                                                        
         JNE   GCRD030                                                          
         LHI   R1,AE$NDEBI                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDH                                                          
         DROP  CRED                                                             
                                                                                
GCRD005  XC    SVAFCAMT,SVAFCAMT                                                
         ZAP   SVTRNAMT,TRNAMNT                                                 
         XC    SVOKREF,SVOKREF                                                  
         DROP  R3                                                               
                                                                                
CRED     USING TRNRECD,R4                                                       
         LA    R4,IOKEY            Build credit posting key (from               
         MVC   CRED.TRNKEY,TRNKEY  ACADDTRN.PAYTRNN)                            
         ICM   R1,B'0001',CRED.TRNKSBR                                          
         JZ    GCRD006                                                          
         SHI   R1,1                                                             
         STC   R1,CRED.TRNKSBR                                                  
                                                                                
         USING MPYELD,R3                                                        
GCRD006  LLC   R1,MPYLN                                                         
         AR    R3,R1                                                            
         CLI   MPYEL,0                                                          
         JE    GCRD012                                                          
         CLI   MPYEL,MPYELQ                                                     
         JNE   GCRD008                                                          
         CLI   MPYLN,MPYLN2Q       Test sub reference on element                
         JL    GCRD006                                                          
         MVC   CRED.TRNKSBR,MPYSUB                                              
         J     GCRD006                                                          
                                                                                
         USING FFTELD,R3                                                        
GCRD008  CLI   FFTEL,FFTELQ                                                     
         JNE   GCRD010                                                          
         CLI   FFTTYPE,FFTTKREF                                                 
         JNE   GCRD006                                                          
         CLC   TRNKREF,FFTDATA                                                  
         JE    GCRD006                                                          
         MVC   SVOKREF,FFTDATA                                                  
***      MVC   CRED.TRNKREF,FFTDATA                                             
         J     GCRD006                                                          
                                                                                
         USING AFCELD,R3                                                        
GCRD010  CLI   AFCEL,AFCELQ                                                     
         JNE   GCRD006                                                          
         ZAP   SVAFCAMT,AFCAMNT                                                 
         J     GCRD006                                                          
                                                                                
         USING TRNELD,R3                                                        
GCRD012  LA    R3,TRNRFST          Set payment is reversal status               
         MVI   ISPAYREV,NOQ                                                     
         TM    TRNSTAT,TRNSREV                                                  
         JZ    GCRD020                                                          
         MVI   ISPAYREV,YESQ                                                    
         DROP  R2,R3,CRED                                                       
                                                                                
         USING TRNRECD,R2                                                       
GCRD020  DS    0H                  Get creditor invoice transaction             
         LA    R2,IOKEY            --------------------------------             
         MVC   IOKEYMEM,TRNKEY                                                  
         GOTO1 CDATAMGR,MYDMCB,DMREAD,DMACCDIR,IOKEY,IOKEY                      
         JE    GCRD022                                                          
         OC    SVOKREF,SVOKREF     Try again with real ref if xxx**n            
         JZ    GCRD021             scenario                                     
         MVC   TRNKEY,IOKEYMEM                                                  
         MVC   TRNKREF,SVOKREF                                                  
         XC    SVOKREF,SVOKREF                                                  
         J     GCRD020                                                          
                                                                                
GCRD021  LHI   RF,AE$PNFUS                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     GETCRDH             ignore item                                  
                                                                                
GCRD022  DS    0H                                                               
*&&UK                                                                           
         CLI   TRNKSTYP,TRNTINV    Applicable scenario?                         
         JE    GCRD024                                                          
         CLI   TRNKSTYP,72                                                      
         JE    GCRD024                                                          
         CLI   TRNKSTYP,TRNTNBIN                                                
         JE    GCRD024                                                          
*                                                                               
         CLI   TRNKSTYP,TRNTADPY   (DDB ISSUE)                                  
         JNE   GCRD023                                                          
                                                                                
         LHI   R1,AE$ADMIS                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDL                                                          
*&&                                                                             
*&&US                                                                           
*****  APPLICABLE INVOICE BATCHES 1,10,46,61 FOR US                             
         CLI   TRNKSTYP,TRNTINV    Applicable scenario?                         
         JE    GCRD024                                                          
         CLI   TRNKSTYP,TRNTMEPY                                                
         JE    GCRD024                                                          
         CLI   TRNKSTYP,TRNTMUBL                                                
         JE    GCRD024                                                          
         CLI   TRNKSTYP,61                                                      
         JE    GCRD024                                                          
*&&                                                                             
                                                                                
                                                                                
GCRD023  LHI   R1,AE$ITYAP                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDL                                                          
                                                                                
GCRD024  TM    TRNKSTAT,TRNSDRFT                                                
         JNZ   GCRD023                                                          
                                                                                
         LA    RF,DMACCMST                                                      
*&&UK                                                                           
         TM    TRNKSTAT,TRNSARCH                                                
         JZ    GCRD026                                                          
         LA    RF,DMACCARC                                                      
*&&                                                                             
                                                                                
GCRD026  GOTO1 CDATAMGR,MYDMCB,DMGETR,(RF),TRNKDA,IOAREA1,MYDMWRK               
         JE    GCRD030                                                          
         LHI   R1,AE$RCNOF                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDH                                                          
                                                                                
         USING TRNELD,R3                                                        
GCRD030  LA    R2,IOAREA1          Retrieve elements (R2 on IOAREA1             
         LA    R3,TRNRFST          covered by TRNRECD now)                      
         TM    TRNSTAT,TRNSDR                                                   
         JNZ   GCRD023                                                          
                                                                                
         XC    SVMPYEL,SVMPYEL                                                  
         XC    SVTRSEL,SVTRSEL                                                  
         XC    SVCPJEL,SVCPJEL                                                  
         XC    SVSOREL,SVSOREL                                                  
         XC    SVFFTEL,SVFFTEL                                                  
         XC    SVGINEL,SVGINEL                                                  
         XC    SVAFCAM2,SVAFCAM2                                                
         XC    CLPROJO,CLPROJO                                                  
         MVI   SVNARL,FFQ                                                       
         MVC   CLCLINT,TRNKCACT    NMAL- MOVE CLIENT CODE FROM KEY              
                                                                                
         CLI   TRNEL,TRNELQ       Good record?                                  
         JE    GCRD041                                                          
                                                                                
GCRD040  LHI   R1,AE$INREC                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     GETCRDH                                                          
                                                                                
GCRD041  ZAP   SVEXPAMT,TRNAMNT    SAVE TRANSACTION AMOUNT FOR EXPENSE          
         CLI   TRNLN,TRNLN1Q       SAVE NARRATIVE                               
         JNH   GCRD042                                                          
         CLI   TRNLN,TRNLN1Q+L'SVNARR                                           
         JH    *+2                                                              
         LLC   RF,TRNLN                                                         
         SHI   RF,TRNLN1Q+1                                                     
         STC   RF,SVNARL                                                        
         MVC   SVNARR(0),TRNNARR                                                
         EX    RF,*-6                                                           
                                                                                
         USING TRSELD,R4                                                        
GCRD042  LR    R4,R3                                                            
         MVC   SVRREF,TRNREF                                                    
                                                                                
GCRD044  LLC   R0,TRSLN                                                         
         AR    R4,R0                                                            
         CLI   TRSEL,0                                                          
         JE    GCRD090                                                          
         CLI   TRSEL,TRSELQ                                                     
         JNE   GCRD045                                                          
         LLC   R1,TRSLN                                                         
         SHI   R1,1                                                             
         MVC   SVTRSEL(0),TRSELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
         USING MPYELD,R4                                                        
GCRD045  CLI   MPYEL,MPYELQ                                                     
         JNE   GCRD046                                                          
         LLC   R1,MPYLN                                                         
         SHI   R1,1                                                             
         MVC   SVMPYEL(0),MPYELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
         USING AFCELD,R4                                                        
GCRD046  CLI   AFCEL,AFCELQ                                                     
         JNE   GCRD048                                                          
         ZAP   SVAFCAM2,AFCAMNT                                                 
         J     GCRD044                                                          
                                                                                
         USING CPJELD,R4                                                        
GCRD048  CLI   CPJEL,CPJELQ                                                     
         JNE   GCRD050                                                          
         CLI   CPJTYPE,CPJTJOB                                                  
         JNE   GCRD050                                                          
         LLC   R1,CPJLN                                                         
         SHI   R1,1                                                             
         MVC   SVCPJEL(0),CPJELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
         USING FFTELD,R4                                                        
GCRD050  CLI   FFTEL,FFTELQ                                                     
         JNE   GCRD054                                                          
         CLI   FFTTYPE,FFTTWRKC                                                 
         JNE   GCRD052                                                          
         LLC   R1,FFTLN                                                         
         SHI   R1,1                                                             
         MVC   SVFFTEL(0),FFTELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
GCRD052  CLI   FFTTYPE,FFTTKREF                                                 
*&&UK*&& JNE   GCRD044                                                          
*&&US*&& JNE   GCRD053                                                          
         MVC   SVRREF,FFTDATA                                                   
         J     GCRD044                                                          
                                                                                
*&&US                                                                           
GCRD053  CLI   FFTTYPE,FFTTCLPR                                                 
         JNE   GCRD044                                                          
         MVC   CLCLINT,FFTCLAC         MOVE CLIENT CODE                         
         J     GCRD044                                                          
*&&                                                                             
                                                                                
         USING GINELD,R4                                                        
GCRD054  CLI   GINEL,GINELQ                                                     
         JNE   GCRD055                                                          
         LLC   R1,GINLN                                                         
         SHI   R1,1                                                             
         MVC   SVGINEL(0),GINELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
         USING SORELD,R4                                                        
GCRD055  CLI   SOREL,SORELQ                                                     
         JNE   GCRD056                                                          
         CLI   SORSYS,SORSACC                                                   
         JNE   GCRD056                                                          
         CLC   SORAULA(2),PRODUL                                                
         JNE   GCRD056                                                          
         LLC   R1,SORLN                                                         
         SHI   R1,1                                                             
         MVC   SVSOREL(0),SORELD                                                
         EX    R1,*-6                                                           
         J     GCRD044                                                          
                                                                                
         USING RUIELD,R4                                                        
GCRD056  CLI   RUIEL,RUIELQ                                                     
         JNE   GCRD057                                                          
         MVI   SVREBUSE,REBQ                                                    
         CLI   ISCREDIT,YESQ                                                    
         JE    GCRD044                                                          
         ZAP   SVREBAMT,SVTRNAMT                                                
         J     GCRD044                                                          
                                                                                
         USING OTHELD,R4                                                        
GCRD057  CLI   OTHEL,OTHELQ                                                     
         JNE   GCRD058                                                          
*&&UK*&& MVC   SVREBNO,OTHNUM                                                   
                                                                                
*&&US*&& MVC   CLPROD(12),OTHNUM    MOVE PRODUCT AND JOB DETAIL                 
                                                                                
         J     GCRD044                                                          
                                                                                
         USING GPXELD,R4                                                        
GCRD058  CLI   GPXEL,GPXELQ                                                     
         JNE   GCRD060                                                          
         CLI   GPXTYP,GPXNVLQ                                                   
         JNE   GCRD044                                                          
         MVI   SVREBUSE,INVQ                                                    
         LLC   RE,GPXLN                                                         
         SHI   RE,GPXLN1Q+1                                                     
         BASR  R1,0                                                             
         MVC   SVREBNO(0),GPXDATA                                               
         EX    RE,0(R1)                                                         
         CLI   ISCREDIT,YESQ                                                    
         JE    GCRD044                                                          
         ZAP   SVREBAMT,SVTRNAMT                                                
         J     GCRD044                                                          
*                                                                               
         USING NUMELD,R4                                                        
GCRD060  CLI   NUMEL,NUMELQ        NUMBER ELEMENT ?                             
         JNE   GCRD044             NO, CHECK FOR NEXT ELEMENT                   
*                                                                               
         CLI   NUMTYPE,NUMTYCLM    CLAIM NUMBER AND ITEM DETAILS TYPE?          
         JNE   GCRD044             NO, PROCESS NEXT ELEMENT                     
*                                                                               
         MVI   SVEXPUSE,YESQ       SET EXPENSE TYPE RECORD                      
         MVC   SVCLMNUM,NUMCLM#    SAVE CLAIM NUMBER DETAILS                    
         MVC   SVITMNUM,NUMITMR#   SAVE ITEM NUMBER DETAILS                     
         J     GCRD044                                                          
         DROP  R4                                                               
*                                                                               
GCRD090  CLI   SVTRSEL,TRSELQ      Check required elements on file              
         JNE   GCRD040                                                          
         CLI   SVMPYEL,MPYELQ                                                   
         JNE   GCRD040                                                          
                                                                                
         CLI   ISCREDIT,YESQ                                                    
         JE    GCRD092                                                          
         CP    TRNAMNT,SVTRNAMT    Check amounts for correct credit             
         JE    GCRD092             (what happens on bank/void ???)              
         CLC   SVAFCAMT,SVAFCAM2                                                
         JNE   GCRD023                                                          
                                                                                
GCRD092  CLI   SVGINEL,GINELQ      IO logic via GIN passives                    
         JE    GCRD096                                                          
                                                                                
         CLI   SVFFTEL,FFTELQ      CHECK APPLICABLE SCENARIO                    
         JE    GCRD094                                                          
         CLI   SVSOREL,SORELQ                                                   
         JNE   GETCRDY                                                          
         CLI   SVSOREL+SORLN-SORELD,SORAL2Q                                     
         JL    GETCRDY                                                          
         CLC   SVSOREL+SORAWRK-SORELD(L'SORAWRK),SPACES                         
         JNH   GETCRDY                                                          
                                                                                
GCRD094  CLI   SVCPJEL,CPJELQ                                                   
         JE    GCRD096                                                          
         CLI   SVSOREL,SORELQ                                                   
         JNE   GETCRDY                                                          
                                                                                
GCRD096  CLI   SVCRDTYP,TRNTNBIN                                                
         JE    GETCRDY                                                          
         MVI   SVJOBUSE,YESQ                                                    
         DROP  R2,R3                                                            
                                                                                
GETCRDY  LHI   R1,1                                                             
         J     GETCRDX                                                          
                                                                                
GETCRDH  LHI   R1,2                                                             
         J     GETCRDX                                                          
                                                                                
GETCRDL  LHI   R1,0                                                             
                                                                                
GETCRDX  DS    0H                                                               
         CHI   R1,1                                                             
         XIT1                                                                   
                                                                                
*** Get job transaction and job ***                                             
                                                                                
         USING TRNRECD,R2                                                       
PROJOB   NTR1                                                                   
                                                                                
         LA    R2,IOAREA1          Point to creditor invoice posting            
         USING TRNELD,R3                                                        
         LA    R3,TRNRFST                                                       
                                                                                
         USING JOBBD,R1                                                         
         LAY   R1,JOBBUFF          Initialise job buffer                        
         MVI   JOBBACT,EOTQ                                                     
         DROP  R1                                                               
                                                                                
         MVC   SVCPJF,SPACES       Set SJ account code and w/c                  
         MVC   SVCWCF,SPACES                                                    
                                                                                
*&&US                                                                           
         LLC   R1,JPYDCLL                                                       
         SHI   R1,1                                                             
         MVC   SVCPJF(0),CLCLINT                                                
         EX    R1,*-6                                                           
         AHI   R1,1                                                             
         LA    RF,SVCPJF(R1)                                                    
         LLC   R1,JPYDPRL                                                       
         SHI   R1,1                                                             
         MVC   0(0,RF),CLPROD                                                   
         EX    R1,*-6                                                           
         AHI   R1,1                                                             
         AR    RF,R1                                                            
         LLC   R1,JPYDJOL                                                       
         CHI   R1,L'CLJOB                                                       
         JNH   *+8                                                              
         LHI   R1,L'CLJOB                                                       
         SHI   R1,1                                                             
         MVC   0(0,RF),CLJOB                                                    
         EX    R1,*-6                                                           
         J     PJOB100                                                          
*&&                                                                             
                                                                                
*&&UK                                                                           
         USING CPJELD,R4                                                        
         CLI   SVCPJEL,CPJELQ                                                   
         JNE   PJOB004                                                          
         LA    R4,SVCPJEL                                                       
         LLC   R1,JPYDCLL                                                       
         SHI   R1,1                                                             
         MVC   SVCPJF(0),CPJCLI                                                 
         EX    R1,*-6                                                           
         AHI   R1,1                                                             
         LA    RF,SVCPJF(R1)                                                    
         LLC   R1,JPYDPRL                                                       
         SHI   R1,1                                                             
         MVC   0(0,RF),CPJPRO                                                   
         EX    R1,*-6                                                           
         AHI   R1,1                                                             
         AR    RF,R1                                                            
         LLC   R1,JPYDJOL                                                       
         CHI   R1,L'CPJJOB                                                      
         JNH   PJOB002                                                          
         LHI   R1,L'CPJJOB                                                      
                                                                                
PJOB002  SHI   R1,1                                                             
         MVC   0(0,RF),CPJJOB                                                   
         EX    R1,*-6                                                           
                                                                                
         CLI   CPJLN,CPJLNQ                                                     
         JL    PJOB020                                                          
         MVC   SVCWCF,CPJWRK                                                    
         J     PJOB020                                                          
         DROP  R4                                                               
                                                                                
         USING SORELD,R4                                                        
PJOB004  CLI   SVSOREL,SORELQ                                                   
         JE    PJOB006                                                          
         CLI   SVGINEL,GINELQ                                                   
         JNE   *+2                                                              
         J     PJOB020                                                          
                                                                                
PJOB006  LA    R4,SVSOREL                                                       
         MVC   SVCPJF,SORAACT                                                   
         CLI   SORLN,SORAL2Q                                                    
         JL    PJOB020                                                          
         MVC   SVCWCF,SORAWRK                                                   
         DROP  R4                                                               
                                                                                
PJOB020  CLI   SVGINEL,GINELQ      IO logic via GIN passives                    
         JE    PJOB150                                                          
         J     PJOB100                                                          
*&&                                                                             
                                                                                
PJOB040  LHI   R1,AE$INREC                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROJOBH                                                          
                                                                                
PJOB100  MVC   SVWCS,FFS                                                        
         CLI   SVFFTEL,FFTELQ                                                   
         JE    PJOB108                                                          
         MVC   SVWCS(L'SORAWRK),SVSOREL+SORAWRK-SORELD                          
         MVI   NUMOFWCS,1                                                       
         J     PJOB114                                                          
                                                                                
PJOB108  LA    RE,SVWCS                                                         
         LA    R1,SVFFTEL+FFTWORK-FFTELD                                        
         LLC   RF,SVFFTEL+FFTLN-FFTELD                                          
         LA    RF,SVFFTEL(RF)                                                   
                                                                                
PJOB110  MVC   0(L'FFTWORK,RE),0(R1)     Save work codes                        
         AHI   RE,L'FFTWORK                                                     
         AHI   R1,L'FFTWORK+L'FFTWAMT                                           
         CR    R1,RF                                                            
         JL    PJOB110                                                          
                                                                                
         CLI   SVFFTEL+FFTDLEN-FFTELD,L'FFTWORK                                 
         JH    PJOB112             DETECT AND SKIP SHORT ELEMENT                
         CLI   SVFFTEL+FFTLN-FFTELD,L'FFTWORK+L'FFTDLEN+FFTLN1Q                 
         JNE   PJOB040             (UNLESS MORE THAN 1 ENTRY)                   
         MVI   NUMOFWCS,1                                                       
         J     PJOB114                                                          
                                                                                
PJOB112  DS    0H                  DETERMINE NUMBER OF WORK CODES               
         LLC   R1,SVFFTEL+FFTDLEN-FFTELD                                        
         SRA   R1,3                i.e. devide by 8 = FFTWORK+FFTWAMT           
         JNH   PJOB040                                                          
         STC   R1,NUMOFWCS                                                      
                                                                                
         USING RNSPASD,R4                                                       
PJOB114  LA    R4,IOKEY            GET SJ INVOICE TRANSACTION(S)                
         XC    RNSPKEY,RNSPKEY     -----------------------------                
         MVI   RNSPTYP,RNSPTYPQ    (none bt 72 via RNS passives)                
         MVI   RNSPSUB,RNSPSUBQ                                                 
         MVC   RNSPCPY,TRNKCPY                                                  
         MVI   RNSPIND,RNSPRFQ                                                  
         MVC   RNSPREF,SVRREF                                                   
         MVC   RNSPBTY,TRNTYPE                                                  
         MVC   COMPKEY,RNSPKEY                                                  
                                                                                
         GOTO1 CDATAMGR,MYDMCB,DMHIGH,DMACCDIR,RNSPKEY,RNSPKEY                  
         J     PJOB131                                                          
                                                                                
PJOB130  LA    R4,IOKEY                                                         
         GOTO1 CDATAMGR,MYDMCB,DMSEQ,DMACCDIR,RNSPKEY,RNSPKEY                   
                                                                                
PJOB131  JNE   PJOB040                                                          
         CLC   RNSPKEY(RNSPFIL-RNSPASD),COMPKEY                                 
         JNE   PJOB200             Still within this reference number?          
         MVC   COMPKEY,RNSPASD                                                  
                                                                                
         LA    RF,DMACCMST                                                      
*&&UK                                                                           
         TM    RNSPSTA,TRNSARCH                                                 
         JZ    PJOB132                                                          
         LA    RF,DMACCARC                                                      
*&&                                                                             
                                                                                
PJOB132  GOTO1 CDATAMGR,MYDMCB,DMGETR,(RF),RNSPKDA,IOAREA2,MYDMWRK              
         JNE   PJOB040                                                          
                                                                                
JOB      USING TRNRECD,R5                                                       
         LA    R5,IOAREA2          check applicable record                      
         CLC   JOB.TRNKULA(2),PRODUL                                            
         JNE   PJOB130                                                          
         CLC   JOB.TRNKACT,SVCPJF                                               
         JNE   PJOB130                                                          
                                                                                
         LA    R1,SVWCS                                                         
                                                                                
PJOB133  CLC   JOB.TRNKWORK,0(R1)                                               
         JE    PJOB134                                                          
         AHI   R1,L'FFTWORK                                                     
         CLC   0(L'FFTWORK,R1),FFS                                              
         JNE   PJOB133                                                          
         J     PJOB130                                                          
                                                                                
PJOB134  CLC   JOB.TRNKCCPY,TRNKCPY                                             
         JNE   PJOB130                                                          
         CLC   JOB.TRNKULC,TRNKULA                                              
         JNE   PJOB130                                                          
         CLC   JOB.TRNKDATE,TRNKDATE                                            
         JNE   PJOB130                                                          
                                                                                
         CLI   JOB.TRNRFST,TRNELQ                                               
         JNE   *+2                                                              
                                                                                
NAR      USING TRNELD,R1                                                        
         LA    R1,JOB.TRNRFST                                                   
         MVI   NEWELEM,FFQ                                                      
         CLI   NAR.TRNLN,TRNLN1Q   GET NARRATIVE                                
         JNH   PJOB135                                                          
         CLI   NAR.TRNLN,TRNLN1Q+L'SVNARR                                       
         JH    *+2                                                              
         LLC   RF,NAR.TRNLN                                                     
         SHI   RF,TRNLN1Q+1                                                     
         STC   RF,NEWELEM                                                       
         MVC   NEWELEM+1(0),NAR.TRNNARR                                         
         EX    RF,*-6                                                           
         DROP  NAR                                                              
                                                                                
PJOB135  CLC   SVNARL,NEWELEM      COMPARE NARRATIVE                            
         JNE   PJOB130                                                          
         CLI   SVNARL,FFQ                                                       
         JE    PJOB136                                                          
         LLC   RF,SVNARL                                                        
         CLC   SVNARR(0),NEWELEM+1                                              
         EX    RF,*-6                                                           
         JNE   PJOB130                                                          
                                                                                
         USING TRSELD,R1                                                        
PJOB136  LA    R1,JOB.TRNRFST      CHECK ELEMENT DATA                           
         LA    RE,JOB.TRNKREF                                                   
         XC    NEWELEM,NEWELEM                                                  
                                                                                
PJOB137  LLC   RF,TRSLN                                                         
         AR    R1,RF                                                            
         CLI   TRSEL,0                                                          
         JE    PJOB139                                                          
         CLI   TRSEL,TRSELQ                                                     
         JNE   PJOB138                                                          
         LLC   RF,TRSLN                                                         
         SHI   RF,1                                                             
         MVC   NEWELEM(0),TRSELD                                                
         EX    RF,*-6                                                           
     CLC NEWELEM+TRSADATE-TRSELD(L'TRSADATE),SVTRSEL+TRSADATE-TRSELD            
         JNE   PJOB130                                                          
     CLC NEWELEM+TRSATIME-TRSELD(L'TRSATIME),SVTRSEL+TRSATIME-TRSELD            
*        JE    PJOB137                                                          
         JE    PJOB137A      NMAL                                               
         XR    R0,R0                                                            
         ICM   R0,B'0111',NEWELEM+TRSATIME-TRSELD                               
         XR    RF,RF                                                            
         ICM   RF,B'0111',SVTRSEL+TRSATIME-TRSELD                               
         SR    R0,RF                                                            
         LPR   R0,R0                                                            
         CHI   R0,1                ALLOW TO BE OUT FOR 1 SECOND                 
         JH    PJOB130                                                          
                                                                                
PJOB137A CLC   TRSBSEQ,SVTRSEL+TRSBSEQ-TRSELD         NMAL                      
         JNE   PJOB130                                NMAL                      
         J     PJOB137                                                          
                                                                                
         USING FFTELD,R1                                                        
PJOB138  CLI   FFTEL,FFTELQ                                                     
         JNE   PJOB137                                                          
         CLI   FFTTYPE,FFTTKREF                                                 
         JNE   PJOB137                                                          
         LA    RE,FFTDATA                                                       
         J     PJOB137                                                          
         DROP  R1                                                               
                                                                                
PJOB139  CLC   SVRREF,0(RE)                                                     
         JNE   *+2                 Why? This is via RNSPASD ...                 
                                                                                
AMT      USING TRNELD,RF                                                        
         LA    RF,JOB.TRNRFST                                                   
DUP      USING SCIELD,R1                                                        
         LA    R1,JOB.TRNRFST      CHECK FOR DSPCA-2642 'DUPE' CASE             
         ZAP   MYDUB,AMT.TRNAMNT                                                
         CLI   ISPAYREV,YESQ                                                    
         JNE   PJOB140                                                          
         MP    MYDUB,PMONE                                                      
                                                                                
PJOB140  LLC   R0,DUP.SCILN                                                     
         AR    R1,R0                                                            
         CLI   DUP.SCIEL,0                                                      
         JE    PJOB146                                                          
         CLI   DUP.SCIEL,SCIELQ                                                 
         JNE   PJOB140                                                          
         CLI   DUP.SCITYPE,SCITCPAD                                             
         JNE   PJOB140                                                          
                                                                                
         ZAP   MYDUBS,DUP.SCIAMNT                                               
         AP    MYDUBS,MYDUB                                                     
                                                                                
         CP    AMT.TRNAMNT,PZERO                                                
         JNH   PJOB142                                                          
         CP    MYDUBS,AMT.TRNAMNT                                               
         JNH   PJOB146                                                          
         J     PJOB144                                                          
                                                                                
PJOB142  CP    MYDUBS,AMT.TRNAMNT                                               
         JNL   PJOB146                                                          
         DROP  DUP                                                              
                                                                                
PJOB144  AP    JPYDDUP,PONE        => COUNT IT AND CARRY ON                     
                                                                                
         J     PJOB130             ENABLE TO SKIP                               
                                                                                
PJOB146  DS    0H                                                               
         J     PJOB160                                                          
         DROP  JOB                                                              
                                                                                
*&&UK                                                                           
         USING GINPASD,R4                                                       
PJOB150  LA    R4,IOKEY            Get SJ invoice transaction(s)                
         XC    GINPKEY,GINPKEY     -----------------------------                
         MVI   GINPTYP,GINPTYPQ    (bt72 via GIN passives)                      
         MVC   GINPCPY,TRNKCPY                                                  
         MVC   GINPINV,SVGINEL+GININV-GINELD                                    
         MVC   GINPISN,SVGINEL+GINISN-GINELD                                    
         MVI   GINPPTYP,GINTMAC                                                 
         MVC   COMPKEY,GINPKEY                                                  
         MVI   SVGININD,0                                                       
                                                                                
         GOTO1 CDATAMGR,MYDMCB,DMHIGH,DMACCDIR,GINPKEY,GINPKEY                  
         J     PJOB153                                                          
                                                                                
PJOB152  LA    R4,IOKEY                                                         
         GOTO1 CDATAMGR,MYDMCB,DMSEQ,DMACCDIR,GINPKEY,GINPKEY                   
                                                                                
PJOB153  JNE   PJOB040                                                          
         CLC   GINPKEY(GINPISN-GINPKEY),COMPKEY                                 
         JNE   PJOB200             Still within this GIN number?                
         MVC   COMPKEY,GINPASD                                                  
                                                                                
         OC    GINPISN,GINPISN     Select debit postings only (ACMRK03)         
         JNZ   PJOB154                                                          
         CLI   GINPPTYP,GINTMAC                                                 
         JE    PJOB152                                                          
                                                                                
PJOB154  CLC   GINPISN,SVGINEL+GINISN-GINELD                                    
         JE    PJOB155             Match on item sequence unless there          
         TM    SVGININD,SVGINIMQ   was no SJ (DSPCA-2628) on main GIN           
         JNZ   PJOB152                                                          
         OI    SVGININD,SVGINISQ                                                
                                                                                
PJOB155  CLC   GINPULA(2),PRODUL   SJ?                                          
         JNE   PJOB152                                                          
                                                                                
         CLC   SVCPJF,SPACES       any filter from creditor trx?                
         JNH   PJOB156                                                          
         CLC   GINPACT,SVCPJF                                                   
         JNE   PJOB152                                                          
                                                                                
PJOB156  LA    RF,DMACCMST                                                      
         TM    GINPKSTA,TRNSARCH                                                
         JZ    PJOB157                                                          
         LA    RF,DMACCARC                                                      
                                                                                
PJOB157  GOTO1 CDATAMGR,MYDMCB,DMGETR,(RF),GINPKDA,IOAREA2,MYDMWRK              
         JNE   PJOB040                                                          
                                                                                
JOB      USING TRNRECD,R5                                                       
         LA    R5,IOAREA2          check applicable record                      
         CLC   JOB.TRNKULA(2),PRODUL                                            
         JNE   *+2                 (contradicts GINPULA check)                  
         CLC   SVCWCF,SPACES                                                    
         JNH   PJOB158                                                          
         CLC   SVCWCF,JOB.TRNKWORK w/c check if passed                          
         JNE   PJOB152                                                          
                                                                                
PJOB158  DS    0H                                                               
         DROP  JOB                                                              
*&&                                                                             
                                                                                
PJOB160  MVI   REREAD,NOQ          Process main SJ invoice transaction          
                                                                                
*&&UK                                                                           
         CLI   SVGINEL,GINELQ                                                   
         JNE   PJOB161                                                          
         CLC   IOKEY+GINPISN-GINPASD(L'GINPISN),SVGINEL+GINISN-GINELD           
         JNE   PJOB161                                                          
         OI    SVGININD,SVGINIMQ   Indicate we found Sj in main GIN             
*&&                                                                             
                                                                                
PJOB161  LA    R3,IOAREA2                                                       
         AHI   R3,TRNRFST-TRNRECD                                               
         ZAP   MYDUB,TRNAMNT                                                    
         CLI   ISPAYREV,YESQ                                                    
         JNE   PJOB162                                                          
         MP    MYDUB,PMONE                                                      
                                                                                
PJOB162  GOTOR GETJ2J              (process any J2J and W/Os)                   
         JE    PJOB170                                                          
         J     PROJOBH                                                          
         DC    H'0'                                                             
                                                                                
         USING J2JBD,R4                                                         
PJOB170  LAY   R4,J2JBUFF          Get SJ transactions via J2J buffer           
         DS    0H                  ----------------------------------           
                                                                                
PJOB171  CLI   J2JBIND,EOTQ                                                     
         JE    PJOB190                                                          
                                                                                
*&&UK*&& TM    J2JBSTA,TRNSARCH                                                 
*&&UK*&& JNZ   PJOB173                                                          
         CLI   JPYDUPD,YESQ                                                     
         JNE   PJOB172                                                          
         GOTO1 CDATAMGR,MYDMCB,('R4UPDQ',DMGETR),DMACCMST,J2JBDA,      *        
               IOAREA2,MYDMWRK                                                  
         JNE   *+2                                                              
         J     PJOB175                                                          
                                                                                
PJOB172  GOTO1 CDATAMGR,MYDMCB,DMGETR,DMACCMST,J2JBDA,IOAREA2,MYDMWRK           
         JNE   *+2                                                              
         J     PJOB175                                                          
                                                                                
*&&UK                                                                           
PJOB173  DS    0H                  Get it for PROMOTE or none updative          
         GOTO1 CDATAMGR,MYDMCB,DMGETR,DMACCARC,J2JBDA,IOAREA2,MYDMWRK           
         JNE   *+2                                                              
         CLI   JPYDUPD,YESQ                                                     
         JNE   PJOB175                                                          
                                                                                
         LAY   R1,IOAREA2          Promote archived transaction                 
         MVC   IOKEY,0(R1)                                                      
         MVI   REREAD,YESQ                                                      
         LR    R0,R1                                                            
         ICM   RF,B'1111',JPYDPRO  V(PROMOTE)                                   
         GOTOR (RF),MYDMCB,(R0),COMFACSD                                        
         GOTO1 CDATAMGR,MYDMCB,DMREAD,DMACCDIR,IOKEY,IOKEY                      
         JE    PJOB174                                                          
         LHI   R1,AE$ENDOF                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROJOBH                                                          
                                                                                
PJOB174  MVC   J2JBDA,IOKEY+TRNKDA-TRNRECD                                      
         GOTO1 CDATAMGR,MYDMCB,('R4UPDQ',DMGETR),DMACCMST,J2JBDA,      *        
               IOAREA2,MYDMWRK                                                  
         JNE   *+2                                                              
*&&                                                                             
                                                                                
JOB      USING TRNRECD,R5                                                       
         USING TRNELD,R3                                                        
PJOB175  LA    R5,IOAREA2          Update SJ invoice transaction                
         LA    R3,JOB.TRNRFST      -----------------------------                
         ZAP   MYDUB,TRNAMNT                                                    
                                                                                
         USING SCIELD,R3                                                        
PJOB176  LLC   RF,SCILN            look for credit pay details                  
         AR    R3,RF                                                            
         CLI   SCIEL,0                                                          
         JE    PJOB178                                                          
         CLI   SCIEL,SCIELQ                                                     
         JNE   PJOB176                                                          
         CLI   SCITYPE,SCITCPAD                                                 
         JNE   PJOB176                                                          
         J     PJOB180                                                          
                                                                                
PJOB178  LA    R3,NEWELEM          add dummy element                            
         XC    NEWELEM,NEWELEM                                                  
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCITCPLQ                                                   
         MVI   SCITYPE,SCITCPAD                                                 
         ZAP   SCIAMNT,PZERO                                                    
         XC    SCITCPNO,SCITCPNO                                                
         XC    SCITCPDT,SCITCPDT                                                
         GOTOR CHELLO,MYDMCB,(C'P',DMACCMST),JOB.TRNRECD,SCIELD,0               
         CLI   12(R1),0                                                         
         JE    PJOB175                                                          
         LHI   R1,AE$RECTB                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROJOBH             make it a serious error                      
                                                                                
PJOB180  MVC   USEDATE,SVMPYEL+MPYDTE-MPYELD                                    
         MVC   USENUM,SVMPYEL+MPYNO-MPYELD                                      
         CLI   ISPAYREV,YESQ                                                    
         JNE   PJOB182                                                          
         XC    USEDATE,USEDATE                                                  
         MVC   USENUM,SPACES                                                    
         MP    MYDUB,PMONE                                                      
                                                                                
PJOB182  DS    0H                                                               
         AP    SCIAMNT,MYDUB       changing to get same value                   
         MVC   SCITCPNO,USENUM                                                  
         MVC   SCITCPDT,USEDATE                                                 
         DROP  R3                                                               
                                                                                
         AP    JPYDTRX,PONE        add to overall count                         
                                                                                
         CLI   JPYDUPD,YESQ        and put back record                          
         JNE   PJOB184                                                          
         GOTO1 CDATAMGR,MYDMCB,DMPUTR,DMACCMST,J2JBDA,IOAREA2,MYDMWRK           
         JE    PJOB184                                                          
         LHI   RF,AE$FATAL                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROJOBH             serious error                                
                                                                                
PJOB184  AHI   R4,J2JBLNQ          next J2J entry                               
         J     PJOB171                                                          
         DROP  R4                                                               
                                                                                
PJOB190  CLI   REREAD,YESQ         reestablish RNS/GIN IO sequence              
         JNE   PJOB192                                                          
         MVC   IOKEY,COMPKEY                                                    
         GOTO1 CDATAMGR,MYDMCB,DMHIGH,DMACCDIR,IOKEY,IOKEY                      
         JE    PJOB192                                                          
         LHI   R1,AE$ENDOF                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROJOBH                                                          
                                                                                
PJOB192  DS    0H                                                               
*&&US*&& J     PJOB130             NEXT JOB INVOICE TRANSACTION                 
*&&UK                                                                           
         CLI   SVGINEL,GINELQ      carry on reading sequential for              
         JNE   PJOB130             next JOB invoice transaction                 
         J     PJOB152                                                          
*&&                                                                             
         USING JOBBD,R5                                                         
PJOB200  LAY   R5,JOBBUFF          Process buffered Job account record          
         DS    0H                  -----------------------------------          
                                                                                
PJOB202  CLI   JOBBACT,EOTQ        done for this creditor transaction           
         JE    PROJOBE             at end of job buffer                         
                                                                                
         CP    JOBBAMT,PZERO       skip if no value to add                      
         JE    PJOB250                                                          
                                                                                
         USING ACTRECD,R2                                                       
         LA    R2,IOKEY                                                         
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,IOAREA1+TRNKCPY-TRNRECD                                  
         MVC   ACTKULA(2),PRODUL                                                
         MVC   ACTKACT,JOBBACT                                                  
                                                                                
         LHI   RF,0                                                             
         CLI   JPYDUPD,YESQ                                                     
         JNE   PJOB204                                                          
         LHI   RF,R4UPDQ                                                        
                                                                                
PJOB204  GOTO1 CDATAMGR,MYDMCB,((RF),DMREAD),DMACCDIR,ACTKEY,ACTKEY             
         JE    PJOB206                                                          
         LHI   RF,AE$ACTNF                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROJOBH             serious error                                
                                                                                
PJOB206  LHI   RF,0                                                             
         CLI   JPYDUPD,YESQ                                                     
         JNE   PJOB208                                                          
         LHI   RF,R4UPDQ                                                        
                                                                                
PJOB208  GOTO1 CDATAMGR,MYDMCB,((RF),DMGETR),DMACCMST,ACTKDA,IOAREA2,  *        
               MYDMWRK                                                          
         JNE   *+2                                                              
                                                                                
         USING SCIELD,R3                                                        
PJOB240  LA    R2,IOAREA2                                                       
         LA    R3,ACTRFST                                                       
                                                                                
PJOB242  CLI   SCIEL,0                                                          
         JE    PJOB246                                                          
         CLI   SCIEL,SCIELQ                                                     
         JNE   PJOB244                                                          
         CLI   SCITYPE,SCITCPAT                                                 
         JE    PJOB248                                                          
                                                                                
PJOB244  LLC   R1,SCILN                                                         
         AR    R3,R1                                                            
         J     PJOB242                                                          
                                                                                
PJOB246  LA    R3,NEWELEM                                                       
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITCPAT                                                 
         ZAP   SCIAMNT,PZERO                                                    
         GOTOR CHELLO,MYDMCB,(C'P',DMACCMST),ACTRECD,SCIELD,0                   
         CLI   12(R1),0                                                         
         JE    PJOB240                                                          
         LHI   R1,AE$ESTTB                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROJOBH             make it a serious error                      
                                                                                
PJOB248  AP    SCIAMNT,JOBBAMT                                                  
         AP    JPYDACT,PONE                                                     
                                                                                
         CLI   JPYDUPD,YESQ                                                     
         JNE   PJOB250                                                          
         LA    RF,IOKEY+ACTKDA-ACTRECD                                          
         GOTO1 CDATAMGR,MYDMCB,DMPUTR,DMACCMST,(RF),IOAREA2,MYDMWRK             
         JE    PJOB250                                                          
         LHI   RF,AE$DSKER                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROJOBH             serious error                                
                                                                                
PJOB250  AHI   R5,JOBBLNQ                                                       
         J     PJOB202                                                          
         DROP  JOB,R2,R3,R5                                                     
                                                                                
PROJOBL  LHI   R1,0                                                             
         J     PROJOBX                                                          
                                                                                
PROJOBH  LHI   R1,2                                                             
         J     PROJOBX                                                          
                                                                                
PROJOBE  LHI   R1,1                                                             
                                                                                
PROJOBX  DS    0H                                                               
         CHI   R1,1                                                             
         XIT1                                                                   
                                                                                
                                                                                
*** Get job 2 job transactions ***                                              
                                                                                
         USING J2JBD,R4                                                         
GETJ2J   NTR1                                                                   
                                                                                
         LAY   R4,J2JBUFF          initialise J2J buffer                        
         MVI   J2JBIND,EOTQ                                                     
                                                                                
         USING TRNRECD,R2                                                       
         LAY   R2,IOAREA2          incoming job transaction                     
         MVI   REREAD,NOQ                                                       
                                                                                
         JAS   RE,ADDJ2J           add current record to buffer                 
         JH    GETJ2JH                                                          
         JAS   RE,ADDJOB           add amount to JOB buffer                     
         JH    GETJ2JH                                                          
                                                                                
         USING TRNELD,R3                                                        
GJ2J02   LA    R3,TRNRFST                                                       
         ZAP   MYDUB,TRNAMNT                                                    
         ZAP   MYDUBS,TRNAMNT                                                   
         MVI   MYBYTE,0                                                         
         XC    SVOSERNO,SVOSERNO                                                
                                                                                
         USING PTAELD,R3                                                        
GJ2J04   LLC   R0,PTALN            look for PTA transfer elements               
         AR    R3,R0                                                            
         CLI   PTAEL,0                                                          
         JE    GJ2J10                                                           
         CLI   PTAEL,SERELQ                                                     
         JE    GJ2J05                                                           
*&&UK                                                                           
         CLI   PTAEL,PXDELQ                                                     
         JE    GJ2J08                                                           
         CLI   PTAEL,PTAELQ                                                     
         JNE   GJ2J04                                                           
         TM    PTASTAT1,PTASPEND   skip any pending/draft                       
         JNZ   GJ2J04                                                           
         CLI   PTATYPE,PTATTRFT                                                 
         JE    GJ2J06                                                           
*&&                                                                             
         J     GJ2J04                                                           
                                                                                
GJ2J05   MVC   SVOSERNO,PTAEL+SERNM-SERELD                                      
         J     GJ2J04                                                           
                                                                                
*&&UK                                                                           
GJ2J06   OI    MYBYTE,TAWITRQ      Transfers                                    
         SP    MYDUB,PTANET        - reduce overall amount by it                
* >>> ACCLB68 see SNDRXFR                                                       
         J     GJ2J04                                                           
                                                                                
GJ2J07   OI    MYBYTE,TAWIWOQ      Write offs                                   
         SP    MYDUB,PTANET        - reduce overall amount by it                
         J     GJ2J04                                                           
         DROP  R3                                                               
                                                                                
         USING PXDELD,R3                                                        
GJ2J08   CLI   PXDTYPE,PXDTFROM                                                 
         JNE   GJ2J04                                                           
         OI    MYBYTE,TAWITRQ      Transfers                                    
         SP    MYDUB,MYDUBS                                                     
         J     GJ2J04                                                           
         DROP  R3                                                               
*&&                                                                             
                                                                                
GJ2J10   DS    0H                                                               
*&&UK                                                                           
         CLI   MYBYTE,0            Skip if no applicable PTA activity           
         JE    GETJ2JE                                                          
*&&                                                                             
                                                                                
         MVI   REREAD,YESQ                                                      
         MVC   IOKEYMEM,TRNKEY                                                  
                                                                                
         MVI   MYMODE,TRNTJBTX     look for J2J transfers, first                
         J     GJ2J22                                                           
                                                                                
GJ2J20   CLI   MYMODE,TRNTJBTX     Done?                                        
         JNE   GJ2J50                                                           
         MVI   MYMODE,TRNTBLTX     then look for w/os (inactive)                
         J     GJ2J50                                                           
                                                                                
         USING RNSPASD,R5                                                       
GJ2J22   LA    R5,IOKEY            none bt 72 via RNS passives                  
         XC    RNSPKEY,RNSPKEY                                                  
         MVI   RNSPTYP,RNSPTYPQ                                                 
         MVI   RNSPSUB,RNSPSUBQ                                                 
         MVC   RNSPCPY,TRNKCPY                                                  
         MVI   RNSPIND,RNSPRFQ                                                  
         MVC   RNSPREF,SVRREF                                                   
         MVC   RNSPBTY,MYMODE                                                   
         MVC   TESTKEY,RNSPKEY                                                  
                                                                                
         GOTO1 CDATAMGR,MYDMCB,DMHIGH,DMACCDIR,RNSPKEY,RNSPKEY                  
         J     GJ2J26                                                           
                                                                                
GJ2J24   LA    R5,IOKEY                                                         
         GOTO1 CDATAMGR,MYDMCB,DMSEQ,DMACCDIR,RNSPKEY,RNSPKEY                   
                                                                                
GJ2J26   JNE   GJ2J60                                                           
         CLC   RNSPKEY(RNSPFIL-RNSPASD),TESTKEY                                 
         JNE   GJ2J20              Still within this reference number?          
                                                                                
         LA    RF,DMACCMST                                                      
*&&UK                                                                           
         TM    RNSPSTA,TRNSARCH                                                 
         JZ    GJ2J28                                                           
         LA    RF,DMACCARC                                                      
*&&                                                                             
                                                                                
GJ2J28   GOTO1 CDATAMGR,MYDMCB,DMGETR,(RF),RNSPKDA,IOAREA2,MYDMWRK              
         JNE   GJ2J60                                                           
                                                                                
         USING TRNRECD,R2                                                       
         LA    R2,IOAREA2          check applicable record                      
         CLC   TRNKULA(2),PRODUL                                                
         JNE   GJ2J24                                                           
         CLC   TRNKCULC,IOKEYMEM+TRNKCULC-TRNRECD                               
         JNE   GJ2J24                                                           
         CLC   TRNKDATE,IOKEYMEM+TRNKDATE-TRNRECD                               
         JNE   GJ2J24                                                           
         TM    TRNRSTAT,TRNSDRFT                                                
         JNZ   GJ2J24                                                           
                                                                                
         USING TRNELD,R3                                                        
         LA    R3,TRNRFST                                                       
                                                                                
         TM    TRNSTAT,TRNSDR      debits only                                  
         JZ    GJ2J24                                                           
                                                                                
         USING PTAELD,RF                                                        
         LR    RF,R3                                                            
         MVI   MYPTAIND,0                                                       
                                                                                
GJ2J30   LLC   R1,PTALN            look for PTA transfer elements               
         AR    RF,R1                                                            
         CLI   PTAEL,0                                                          
         JE    GJ2J36                                                           
*&&UK*&& CLI   PTAEL,GPXELQ        original SERNM element                       
*&&US*&& CLI   PTAEL,ASKELQ                                                     
         JE    GJ2J35                                                           
         J     GJ2J30                                                           
                                                                                
*&&US                                                                           
         USING ASKELD,RF                                                        
GJ2J35   OI    MYPTAIND,X'01'      ASKEL found                                  
         CLC   ASKKEY,IOKEYMEM                                                  
         JNE   GJ2J30                                                           
         OI    MYPTAIND,X'10'      ASKKEY match                                 
         J     GJ2J30                                                           
         DROP  RF                                                               
*&&                                                                             
*&&UK                                                                           
         USING GPXELD,RF                                                        
GJ2J35   CLI   GPXTYP,GPXPTSQ                                                   
         JNE   GJ2J30                                                           
         OI    MYPTAIND,X'01'      GPXEL found                                  
         CLC   SVOSERNO,GPXDATA                                                 
         JNE   GJ2J30                                                           
         OI    MYPTAIND,X'10'      GPXDATA match                                
         J     GJ2J30                                                           
         DROP  RF                                                               
*&&                                                                             
                                                                                
GJ2J36   ZAP   MYDUB,TRNAMNT       save amount                                  
         CLI   ISPAYREV,YESQ                                                    
         JNE   GJ2J37                                                           
         MP    MYDUB,PMONE                                                      
                                                                                
GJ2J37   DS    0H                                                               
         TM    MYPTAIND,X'10'+X'01'                                             
         JNO   GJ2J24                                                           
                                                                                
GJ2J40   JAS   RE,ADDJ2J           add transaction to J2J buffer                
         JH    GETJ2JH                                                          
         JAS   RE,ADDJOB           add amount to JOB buffer                     
         JH    GETJ2JH                                                          
         J     GJ2J24              (next passive)                               
                                                                                
GJ2J50   DS    0H                  end of this J2J                              
         J     GETJ2JE                                                          
                                                                                
GJ2J60   LHI   R1,AE$ENDOF         take care of IO error                        
         STCM  R1,B'0011',ERRCODE                                               
                                                                                
GETJ2JH  LHI   R1,2                                                             
         J     GETJ2JX                                                          
                                                                                
GETJ2JL  LHI   R1,0                                                             
         J     GETJ2JX                                                          
                                                                                
GETJ2JE  LHI   R1,1                                                             
                                                                                
GETJ2JX  CHI   R1,1                                                             
         XIT1                                                                   
         DROP  R2,R3,R5                                                         
                                                                                
*** Add current transaction to J2J buffer ***                                   
                                                                                
         USING TRNRECD,R2                                                       
ADDJ2J   ST    RE,SAVERE                                                        
                                                                                
         USING TRNELD,R1                                                        
         LA    R1,TRNRFST                                                       
         MVI   J2JBIND,ACTIVQ                                                   
         MVC   J2JBSTA,IOKEY+TRNKSTAT-TRNRECD                                   
         MVC   J2JBDA,IOKEY+TRNKDA-TRNRECD                                      
         AHI   R4,J2JBLNQ                                                       
         MVI   J2JBIND,EOTQ                                                     
         LAY   RF,J2JBUFF                                                       
         AHI   RF,J2JBEND-J2JBUFF                                               
         CR    R4,RF                                                            
         JL    ADDJ2JX                                                          
         LHI   R1,AE$TMIDT         (increase J2JBMAXQ)                          
         STCM  R1,B'0011',ERRCODE                                               
         L     RE,SAVERE                                                        
         CHI   R1,0                                                             
         BR    RE                                                               
         DROP  R1                                                               
                                                                                
ADDJ2JX  L     RE,SAVERE                                                        
         CR    RE,RE                                                            
         BR    RE                                                               
         DROP  R2,R4                                                            
                                                                                
                                                                                
                                                                                
         USING ACTRECD,R2                                                       
ADDJOB   ST    RE,SAVERE                                                        
                                                                                
         USING JOBBD,RF                                                         
         LAY   RF,JOBBUFF                                                       
         LHI   R1,JOBBMAXQ                                                      
                                                                                
ADDJOB2  CLI   JOBBACT,EOTQ                                                     
         JE    ADDJOB4                                                          
         CLC   JOBBACT,ACTKACT                                                  
         JE    ADDJOB6                                                          
                                                                                
         AHI   RF,JOBBLNQ                                                       
         JCT   R1,ADDJOB2                                                       
         LHI   R1,AE$TMIDT         (increase JOBBMAXQ)                          
         STCM  R1,B'0011',ERRCODE                                               
         L     RE,SAVERE                                                        
         CHI   R1,0                                                             
         BR    RE                                                               
                                                                                
ADDJOB4  MVC   JOBBACT,ACTKACT                                                  
         ZAP   JOBBAMT,PZERO                                                    
         MVI   JOBBACT+JOBBLNQ,EOTQ                                             
                                                                                
ADDJOB6  AP    JOBBAMT,MYDUB                                                    
         DROP  RF                                                               
                                                                                
ADDJOBX  L     RE,SAVERE                                                        
         CR    RE,RE                                                            
         BR    RE                                                               
         DROP  R2                                                               
                                                                                
*** Get REB and update with payment details ***                                 
                                                                                
*&&UK                                                                           
         USING REBRECD,R2                                                       
PROREB   NTR1                                                                   
                                                                                
         LA    R2,IOKEY            read for invoice record                      
         XC    REBKEY,REBKEY                                                    
         MVI   REBKTYP,REBKTYPQ                                                 
         MVI   REBKSUB,REBKSUBQ                                                 
         MVC   REBKCPY,IOAREA1+TRNKCPY-TRNRECD                                  
         MVC   REBKNUM,SVREBNO                                                  
         GOTO1 CDATAMGR,MYDMCB,DMREAD,DMACCDIR,REBKEY,REBKEY                    
         JE    PREB02                                                           
         LHI   RF,AE$ILNEX                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROREBL             ignore item                                  
                                                                                
PREB02   MVI   MYBYTE,0            get invoice record (for update)              
         CLI   JPYDUPD,YESQ                                                     
         JNE   PREB04                                                           
         MVI   MYBYTE,R4UPDQ                                                    
         GOTO1 CDATAMGR,MYDMCB,(MYBYTE,DMGETR),DMACCMST,REBKDA,        *        
               IOAREA2,MYDMWRK                                                  
         JNE   *+2                                                              
                                                                                
         USING RBDELD,R3                                                        
PREB04   LA    R2,IOAREA2                                                       
         LA    R3,REBRFST                                                       
         ZAP   MYDUBS,PZERO                                                     
                                                                                
PREB06   CLI   RBDEL,0                                                          
         JE    PREB10                                                           
         CLI   RBDEL,RBDELQ                                                     
         JNE   PREB08                                                           
         CLI   RBDTYP,RBDTHDR                                                   
         JNE   PREB08                                                           
         ZAP   MYDUBS,RBDTIAMT                                                  
         J     PREB10                                                           
                                                                                
PREB08   LLC   R1,RBDLN                                                         
         AR    R3,R1                                                            
         J     PREB06                                                           
         DROP  R3                                                               
                                                                                
         USING SCIELD,R3                                                        
PREB10   LA    R3,REBRFST                                                       
                                                                                
PREB16   CLI   SCIEL,0                                                          
         JE    PREB20                                                           
         CLI   SCIEL,SCIELQ                                                     
         JNE   PREB18                                                           
         CLI   SCITYPE,SCITCPAD                                                 
         JE    PREB22                                                           
                                                                                
PREB18   LLC   R1,SCILN                                                         
         AR    R3,R1                                                            
         J     PREB16                                                           
                                                                                
PREB20   LA    R3,NEWELEM          add dummy element                            
         XC    NEWELEM,NEWELEM                                                  
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCITCPLQ                                                   
         MVI   SCITYPE,SCITCPAD                                                 
         ZAP   SCIAMNT,PZERO                                                    
         XC    SCITCPNO,SCITCPNO                                                
         XC    SCITCPDT,SCITCPDT                                                
         GOTOR CHELLO,MYDMCB,(C'P',DMACCMST),REBRECD,SCIELD,0                   
         CLI   12(R1),0                                                         
         JE    PREB10                                                           
         LHI   R1,AE$RECTB                                                      
         STCM  R1,B'0011',ERRCODE                                               
         J     PROREBH             make it a serious error                      
                                                                                
PREB22   AP    SCIAMNT,SVREBAMT                                                 
         CLC   SVMPYEL+MPYNO-MPYELD(L'MPYNO),SPACES                             
         JNH   PREB24                                                           
         MVC   SCITCPNO,SVMPYEL+MPYNO-MPYELD                                    
         MVC   SCITCPDT,SVMPYEL+MPYDTE-MPYELD                                   
                                                                                
PREB24   CP    MYDUBS,PZERO        positive or negative invoice?                
         JL    PREB26                                                           
                                                                                
         CP    SCIAMNT,PZERO                                                    
         JH    PREB30                                                           
         MVC   SCITCPNO,SPACES                                                  
         XC    SCITCPDT,SCITCPDT                                                
         J     PREB30                                                           
                                                                                
PREB26   CP    SCIAMNT,PZERO                                                    
         JL    PREB30                                                           
         MVC   SCITCPNO,SPACES                                                  
         XC    SCITCPDT,SCITCPDT                                                
         DROP  R3                                                               
                                                                                
PREB30   CLI   JPYDUPD,YESQ        and put back record                          
         JNE   PREB32                                                           
         LA    RF,IOKEY+REBKDA-REBRECD                                          
         GOTO1 CDATAMGR,MYDMCB,DMPUTR,DMACCMST,(RF),IOAREA2,MYDMWRK             
         JE    PREB32                                                           
         LHI   RF,AE$FATAL                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROREBH             serious error                                
                                                                                
PREB32   AP    JPYDINV,PONE        all done                                     
         J     PROREBE                                                          
                                                                                
PROREBL  LHI   R1,0                                                             
         J     PROREBX                                                          
                                                                                
PROREBH  LHI   R1,2                                                             
         J     PROREBX                                                          
                                                                                
PROREBE  LHI   R1,1                                                             
                                                                                
PROREBX  DS    0H                                                               
         CHI   R1,1                                                             
         XIT1                                                                   
         DROP  R2                                                               
*&&                                                                             
*                                                                               
**********************************************************************          
*** Get Expense record and update with payment details             ***          
**********************************************************************          
         USING EXNPASD,R2                                                       
PROEXP   NTR1 ,              ##    READ EXPENSE PASSIVE BY CLAIM ##             
         LA    R2,IOKEY            ADDRESS WORK AREA                            
         XC    EXNPAS,EXNPAS       CLEAR KEY WORK AREA                          
         MVI   EXNPTYP,EXNPTYPQ    RECORD TYPE '37'                             
         MVI   EXNPSUB,EXNPSUBQ    SUB TYPE '20'                                
         MVC   EXNPCPY,IOAREA1+TRNKCPY-TRNRECD  SAVE COMPANY CODE               
         MVI   EXNPTYPE,EXNPTLQ    EXPENSE TYPE - LIVE                          
         MVC   EXNPNUM,SVCLMNUM    READ FOR CLAIM NUMBER DETAILS                
*                                                                               
         GOTO1 CDATAMGR,MYDMCB,DMREAD,DMACCDIR,EXNPAS,EXNPAS                    
         JE    PEXP02              FOUND, CONTINUE                              
*                                                                               
         LHI   RF,AE$RECNF         NO RECORD FOUND, ERROR                       
         STCM  RF,B'0011',ERRCODE  SAVE ERROR CODE                              
         J     PROEXPL             IGNORE ITEM                                  
*                                                                               
PEXP02   MVC   SVDA,EXNPDA         SAVE DISK ADDRESS FOR MASTER READ            
         XC    IOKEY,IOKEY         CLEAR WORK KEY                               
         MVI   MYBYTE,0            SET DEFULT TO READ ONLY                      
         CLI   JPYDUPD,YESQ        GET EXPENSE RECORD (FOR UPDATE)              
         JNE   *+8                 NO, CONTINUE                                 
         MVI   MYBYTE,R4UPDQ       SET - READ FOR UPDATE                        
         J     PEXP10              READ MASTER RECORD                           
         DROP  R2                                                               
*                                                                               
A        USING EXCRECD,IOKEY       MAP WITH EXPENSE RECORD                      
PEXP04   GOTO1 CDATAMGR,MYDMCB,(MYBYTE,DMHIGH),DMACCDIR,               +        
               A.EXCKEY,A.EXCKEY                                                
*                                                                               
         JE    PEXP08              RECORD FOUND, CONTINUE                       
         DC    H'00'                                                            
*                                                                               
PEXP06   GOTO1 CDATAMGR,MYDMCB,(MYBYTE,DMSEQ),DMACCDIR,                +        
               A.EXCKEY,A.EXCKEY                                                
*                                                                               
         JE    PEXP08              RECORD FOUND, CONTINUE                       
         DC    H'00'                                                            
*                                                                               
PEXP08   CLC   A.EXCKEY(L'A.EXCKEY-1),IOKEYMEM   SAME KEY ?                     
         JNE   PROEXPE             EXIT, CONTINUE                               
*                                                                               
         MVC   SVDA,A.EXCKDA       SAVE DISK ADDRESS FOR MASTER READ            
*                                                                               
PEXP10   GOTO1 CDATAMGR,MYDMCB,(MYBYTE,DMGETR),DMACCMST,SVDA,          +        
               IOAREA2,MYDMWRK                                                  
         JNE   *+2                 ABEND, IF NO RECORD FOUND                    
*                                  RECORD FOUND, CONTINUE                       
         USING EXCRECD,R2                                                       
         LA    R2,IOAREA2          ADDRESS RECORD                               
         MVC   IOKEYMEM,EXCKEY     SAVE KEY DETAILS FOR VALIDATION              
         LA    R3,EXCRFST          POINT TO START  OF ELEMENT                   
*                                                                               
         USING CIDELD,R3           CLAIM ITEM DATA ELEMENT                      
PEXP12   CLI   CIDEL,0             MORE ELEMENT TO PROCESS?                     
         JE    PEXP20              NO, READ NEXT EXPENSE RECORD                 
*                                                                               
         CLI   CIDEL,CIDELQ        CLAIM ITEM DATA ELEMENT ?                    
         JNE   PEXP18              NO, PROCESS NEXT ELEMENT                     
*                                  YES                                          
         CLI   CIDTYPE,CIDTYPQ     EXPENSE PAYMENT TYPE?                        
         JNE   PEXP18              NO, CONTINUE                                 
*                                  YES                                          
         CLC   CIDSEQ,SVITMNUM     ITEM NUMBER MATCHED?                         
         JE    PEXP50              YES,CONTINUE                                 
*                                  NO, PROCESS NEXT ELEMENT                     
PEXP18   LLC   R1,CIDLN            LENGTH OF THE ELEMENT                        
         AR    R3,R1               POINT TO THE NEXT ELEMENT                    
         J     PEXP12              VALIDATE NEXT ELEMENT                        
*                                                                               
PEXP20   OC    A.EXCKEY,A.EXCKEY   1ST TIME HERE ?                              
         JNZ   PEXP06              NO,READ NEXT SEQUENTIAL RECORD               
*                                                                               
         MVC   A.EXCKEY,IOKEYMEM   YES, SAVE KEY DETAILS                        
         LLC   R0,A.EXCKSEQ        CURRENT SEQUENT NUMBER                       
         AHI   R0,1                ADD 1 TO IT                                  
         STC   R0,A.EXCKSEQ        SAVE UPDATED SEQUENCE NUMBER                 
         J     PEXP04              READ HIGH FOR THE KEY                        
*                                                                               
PEXP50   CLI   JARPREV,YESQ        REVERSAL TRANSACTION ?                       
         JNE   PEXP52              NO, MUST BE PAYMENT                          
*                                  YES, UNDO PAYMENT DETAILS                    
         ZAP   CIDPYAMT,PZERO      SET AMOUNT TO ZERO                           
         MVC   CIDPYMTR,SPACES     SET REFERENCE # TO SPACES                    
         XC    CIDPYMDT,CIDPYMDT   SET DATE TO ZERO                             
         J     PEXP58                                                           
*                                                                               
PEXP52   CLC   SVMPYEL+MPYNO-MPYELD(L'MPYNO),SPACES  PAYMENT DETAILS            
         JNH   PROEXPE                               PRESENT                    
*                                                                               
         ZAP   CIDPYAMT,SVEXPAMT   SAVE AMOUNT DETAILS                          
         MVC   CIDPYMTR,SVMPYEL+MPYNO-MPYELD  Save reference number             
         MVC   CIDPYMDT,SVMPYEL+MPYDTE-MPYELD SAVE DATE DETAILS                 
         DROP  R3                                                               
*                             ##   UPDATE RECORD DETAILS    ##                  
PEXP58   CLI   JPYDUPD,YESQ        UPDATE CALL ?                                
         JNE   PEXP70              NO , EXIT                                    
*                                                                               
PEXP60   GOTO1 CDATAMGR,MYDMCB,DMPUTR,DMACCMST,SVDA,IOAREA2,MYDMWRK             
         JE    PEXP70                                                           
*                                                                               
         LHI   RF,AE$FATAL                                                      
         STCM  RF,B'0011',ERRCODE                                               
         J     PROEXPH             SERIOUS ERROR                                
                                                                                
PEXP70   AP    SVEXPNUM,PONE       ADD 1 TO COUNT                               
         J     PROEXPE                                                          
*                                                                               
PROEXPL  LHI   R1,0                                                             
         J     PROEXPX                                                          
                                                                                
PROEXPH  LHI   R1,2                                                             
         J     PROEXPX                                                          
                                                                                
PROEXPE  LHI   R1,1                                                             
                                                                                
PROEXPX  DS    0H                                                               
         CHI   R1,1                                                             
         XIT1                                                                   
*                                                                               
* Literals, constants and equates                                               
                                                                                
YESQ     EQU   C'Y'                                                             
NOQ      EQU   C'N'                                                             
EOTQ     EQU   X'FF'                                                            
ACTIVQ   EQU   X'01'                                                            
R4UPDQ   EQU   X'80'                                                            
REBQ     EQU   C'R'                                                             
INVQ     EQU   C'I'                                                             
FFQ      EQU   X'FF'                                                            
                                                                                
TAWITRQ  EQU   X'80'                                                            
TAWIWOQ  EQU   X'40'                                                            
                                                                                
PZERO    DC    PL1'0'                                                           
PONE     DC    PL1'1'                                                           
PMONE    DC    PL1'-1'                                                          
PRODUL   DC    CL2'SJ'                                                          
SPACES   DC    42C' '                                                           
ZEROES   DC    42X'00'                                                          
FFS      DC    42X'FF'                                                          
DMREAD   DC    CL8'DMREAD'                                                      
DMHIGH   DC    CL8'DMRDHI'                                                      
DMSEQ    DC    CL8'DMRSEQ'                                                      
DMGETR   DC    CL8'GETREC'                                                      
DMPUTR   DC    CL8'PUTREC'                                                      
DMACCDIR DC    CL8'ACCDIR'                                                      
DMACCMST DC    CL8'ACCMST'                                                      
DMACCARC DC    CL8'ACCARC'                                                      
                                                                                
         LTORG                                                                  
                                                                                
         DROP  R6,R7,R9                                                         
                                                                                
* Working storage layout and other DSECTs                                       
                                                                                
WORKD    DSECT                                                                  
MYDUB    DS    D                                                                
MYDUBS   DS    D                                                                
MYAMTT   DS    D                                                                
MYAMTW   DS    D                                                                
MYRELO   DS    A                                                                
SAVERE   DS    A                                                                
SVDA     DS    XL4                                                              
MYDMCB   DS    6A                                                               
MYDMWRK  DS    XL64                                                             
                                                                                
SAVER6   DS    F                                                                
LOCJARAY DS    XL(JARAYLQ+1)                                                    
                                                                                
ISCREDIT DS    CL1                                                              
ISPAYREV DS    CL1                                                              
NUMOFWCS DS    XL1                                                              
MYBYTE   DS    XL1                                                              
MYPTAIND DS    XL1                                                              
MYMODE   DS    XL1                                                              
ERRCODE  DS    XL2                                                              
SVERROR  DS    XL2                                                              
ANYERR   DS    CL1                                                              
REREAD   DS    CL1                                                              
NEWELEM  DS    XL121                                                            
                                                                                
SVGININD DS    XL1                                                              
SVGINIMQ EQU   X'80'                                                            
SVGINISQ EQU   X'08'                                                            
SVJOBUSE DS    CL1                                                              
SVMPYEL  DS    XL(MPYLN4Q)                                                      
SVTRSEL  DS    XL(TRSLN2Q)                                                      
SVCPJEL  DS    XL(CPJLN2Q)                                                      
SVSOREL  DS    XL(SORAL2Q)                                                      
SVFFTEL  DS    XL((FFTWORK-FFTELD)+6*(L'FFTWORK+L'FFTWAMT))                     
SVGINEL  DS    XL(GINLN2Q)                                                      
SVRREF   DS    CL(L'TRNREF)                                                     
SVCPJF   DS    CL12                                                             
SVCWCF   DS    CL2                                                              
SVWCS    DS    XL21                                                             
SVAFCAMT DS    PL6                                                              
SVTRNAMT DS    PL6                                                              
SVAFCAM2 DS    PL6                                                              
SVCRDTYP DS    XL1                                                              
SVREBUSE DS    CL1                                                              
SVREBNO  DS    CL(L'REBKNUM)                                                    
SVREBAMT DS    PL6                                                              
SVNARL   DS    XL1                                                              
SVNARR   DS    CL200                (was 120)                                   
SVOKREF  DS    CL6                                                              
*                                                                               
SVEXPUSE DS    CL1                 EXPENSE TYPE RECORD                          
SVCLMNUM DS    CL(L'EXCKREF)       CLAIM NUMBER                                 
SVITMNUM DS    XL1                 CLAIM ITEM NUMBER                            
SVEXPNUM DS    PL4                 NUMBER OF EXPENSE RECORDS                    
SVEXPAMT DS    PL6                 SAVE EXPENSE AMOUNT                          
*                                                                               
USEDATE  DS    XL2                                                              
USENUM   DS    CL6                                                              
                                                                                
CLPROJO  DS    0CL18                                                            
CLCLINT  DS    CL6                                                              
CLPROD   DS    CL6                                                              
CLJOB    DS    CL6                                                              
                                                                                
COMPKEY  DS    XL42                                                             
TESTKEY  DS    XL42                                                             
                                                                                
SVOSERNO DS    XL4                                                              
IOKEYMEM DS    XL42                                                             
IOKEY    DS    XL64                                                             
IOAREA1  DS    XL2016                                                           
IOAREA2  DS    XL2016                                                           
                                                                                
         DS    0H                                                               
JOBBUFF  DS    XL(JOBBLNQ*JOBBMAXQ+1)                                           
                                                                                
         DS    0H                                                               
J2JBUFF  DS    XL(J2JBLNQ*J2JBMAXQ+1)                                           
J2JBEND  DS    0X                                                               
                                                                                
WORKX    DS    0H                                                               
                                                                                
JOBBD    DSECT                                                                  
JOBBACT  DS    CL12                                                             
JOBBAMT  DS    PL6                                                              
JOBBLNQ  EQU   *-JOBBD                                                          
*JOBBMAXQ EQU   50+1                                                            
JOBBMAXQ EQU   500+1                                                            
                                                                                
J2JBD    DSECT                                                                  
J2JBIND  DS    XL01                                                             
J2JBSTA  DS    XL01                                                             
J2JBDA   DS    XL04                                                             
J2JBLNQ  EQU   *-J2JBD                                                          
*2JBMAXQ EQU   100+1                                                            
J2JBMAXQ EQU   500+1                                                            
                                                                                
* Included books                                                                
                                                                                
       ++INCLUDE ACPAY2JD                                                       
       ++INCLUDE ACMSGEQUS                                                      
                                                                                
         PRINT OFF                                                              
                                                                                
       ++INCLUDE DDCTRYEQUS                                                     
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE ACGENFILE                                                      
                                                                                
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACPAY2JOB 05/14/20'                                      
         END                                                                    
