*          DATA SET CTREPGEN   AT LEVEL 016 AS OF 05/01/02                      
*CATALP CTREPGEN                                                                
***********************************************************************         
*  TTL:      CTREPGEN:  CONTROL FILE COMTENGEN REPORT                 *         
*  PURPOSE:  PRODUCE REPORT SHOWING SORTED LUID'S AND THEIR           *         
*            CORRESPONDING LINE AND PORT.  SECOND REPORT WILL BE      *         
*            SAME, EXCEPT ORDERED BY LINE/PORT.                       *         
*            ALSO, PRODUCE EXCEPTION REPORT SHOWING THOSE TERMINALS   *         
*            WHICH APPEAR IN THE GEN AND NOT IN CTFILE.               *         
***********************************************************************         
CTREPGEN CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,CTREPGEN,=V(REGSAVE)                                           
*                                                                               
         ENTRY UTL                                                              
         ENTRY SSB                                                              
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
*                                                                               
         GOTO1 =V(STXITER),DMCB,A(DUMPLIST)                                     
         B     MAIN                                                             
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(CTREPGEN,65000)                                                
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG   *                                                                
*                                                                               
         EJECT                                                                  
MAIN     DS    0H                                                               
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DMOPEN',=C'CONTROL',FLIST,AIO                
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
         MVC   P(72),=C'THE FOLLOWING TERMINALS EXIST IN GENXXX AND AREX        
                NOT IN THE CONTROL FILE:'                                       
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVI   PFLAG,0                                                          
         LA    R9,GENTBL                                                        
M10      DS    0H                                                               
         MVI   EOF,0                                                            
         CLI   0(R9),X'00'                                                      
         BE    M60                                                              
         MVC   PANNAME(L'GENTBL),0(R9)                                          
         LA    R9,L'GENTBL(R9)                                                  
*                                                                               
         GOTO1 =V(PANIC),DMCB,(X'00',=C'READ'),=C'PAN',PANNAME,CARD             
         TM    DMCB+8,X'10'        TEST PAN BOOK FOUND                          
         BZ    *+6                                                              
         DC    H'0'                DIE, BOOK NOT FOUND                          
*                                                                               
M20      CLC   =C'.DDSSTART ',CARD                                              
         BE    M30                                                              
         CLC   =C'*DDSSTART ',CARD                                              
         BE    M30                                                              
         GOTO1 =V(PANIC),DMCB,(X'00',=C'READ'),=C'PAN',PANNAME,CARD             
         TM    DMCB+8,X'80'        TEST END OF BOOK                             
         BZ    M20                                                              
         DC    H'0'                DIE, START CARD NOT FOUND                    
         EJECT                                                                  
M30      BAS   R7,RDCARD                                                        
         CLI   EOF,1               END OF DATA INDICATOR                        
         BE    M10                                                              
*                                                                               
M40      BAS   RE,CKMACRO                                                       
         CLI   EOF,1                                                            
         BE    M10                                                              
*                                                                               
M50      GOTO1 =V(PANIC),DMCB,(X'00',=C'READ'),=C'PAN',PANNAME,CARD             
         B     M30                 READ NEXT SET                                
         EJECT                                                                  
M60      DS    0H                                                               
         ZAP   PAGE,=P'1'                                                       
         ZAP   LINE,=P'75'         FORCE NEW PAGE                               
         MVC   TITLE(21),=C'COMTENGEN BY TERMINAL'                              
*                                                                               
M80      BAS   RE,BOX1             SET UP FIRST BOXES                           
         MVC   P,SPACES                                                         
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R6,15,4(R1)                                                      
         BZ    M140                                                             
*                                                                               
M100     MVI   LCNT,0              COUNT OF LINES PRINTED                       
         CLI   PFLAG,1             ARE WE ON TYPE 1 OR 2?                       
         BE    *+14                                                             
         MVC   P(118),H1           HEADINGS FOR TYPE 1                          
         B     *+10                                                             
         MVC   P(118),H2           HEADINGS FOR TYPE 2                          
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R2,6(R6)                                                         
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         CLI   PFLAG,1                                                          
         BNE   M106                                                             
*                                                                               
         LA    R2,15(R6)                                                        
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         LA    R2,24(R6)                                                        
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         MVC   LNE,1(R6)                                                        
         MVC   CLUSTER,10(R6)                                                   
*                                                                               
M106     L     R5,APRNT                                                         
         MVC   0(L'LUIDOUT,R5),0(R6)  MOVE FIRST LINE TO PRNT AREA              
         LA    R5,L'LUIDOUT(R5)    POINT AT NEXT LINE                           
         LA    R7,179              45 LINES * 4 COLS - 1 DONE                   
*                                                                               
M110     GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R6,15,4(R1)                                                      
         BZ    M140                                                             
         CLI   PFLAG,1             ARE WE ON TYPE 2?                            
         BE    M120                                                             
         CLI   0(R6),C'1'          STILL SORTED ON LUID?                        
         BNE   M140                                                             
M120     DS    0H                                                               
*                                                                               
         LA    R2,6(R6)                                                         
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         CLI   PFLAG,1                                                          
         BNE   M125                                                             
*                                                                               
         LA    R2,15(R6)                                                        
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         LA    R2,24(R6)                                                        
         GOTO1 =V(HEXOUT),DMCB,(R2),HOLD,1,=C'TOG'                              
         MVC   0(2,R2),HOLD                                                     
*                                                                               
         CLC   LNE,1(R6)                                                        
         BNE   M123                SHOW & SAVE LINE AND CLUSTER                 
         MVC   1(8,R6),SPACES                                                   
         CLC   CLUSTER,10(R6)                                                   
         BNE   M124                SHOW & SAVE CLUSTER                          
         MVC   10(8,R6),SPACES                                                  
         B     M125                                                             
M123     MVC   LNE,1(R6)                                                        
M124     MVC   CLUSTER,10(R6)                                                   
M125     DS    0H                                                               
         MVC   0(L'LUIDOUT,R5),0(R6) MOVE DATA TO PAGE AREA                     
         LA    R5,L'LUIDOUT(R5)    NEXT SLOT IN PAGE AREA                       
         BCT   R7,M110             208 SLOTS ON A PAGE                          
*                                                                               
         L     R5,APRNT            START AT BEGINNING OF PAGE AREA              
         LA    R4,P                POINT AT PRINT LINE                          
         LA    R4,1(R4)            SECOND COL                                   
         LA    R7,45               LINES PER PAGE                               
*                                                                               
M130     ST    R5,HOLD             SAVE OFF OF CURRENT LINE                     
         MVC   0(L'LUIDOUT-1,R4),1(R5)   MOVE DATA TO PRINTER                   
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   30(L'LUIDOUT-1,R4),1(R5)                                         
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   60(L'LUIDOUT-1,R4),1(R5)                                         
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   90(L'LUIDOUT-1,R4),1(R5)                                         
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         ZIC   R1,LCNT             GET LINE COUNT                               
         LA    R1,1(R1)            INC LINE COUNT                               
         CH    R1,=H'5'            MORE THAN 5 LINES?                           
         BE    *+12                YES? PRINT BLANK LINE                        
         STC   R1,LCNT             NO? STORE NEW COUNT AND CONT                 
         B     M135                                                             
         XC    LCNT,LCNT           CLEAR LINE COUNT                             
         CH    R7,=H'1'            IS THIS THE LAST LINE?                       
         BE    M135                YES? DONT PRINT BLANK                        
         GOTO1 =V(PRINTER)                                                      
*                                                                               
M135     L     R5,HOLD             GET BACK CURRENT LINE                        
         LA    R5,L'LUIDOUT(R5)    POINT AT NEXT LINE                           
         BCT   R7,M130             CONTINUE ON PAGE                             
         EJECT                                                                  
*                                                                               
* CLEAR OUT PRINT AREA (SPACES)                                                 
*                                                                               
         L     R1,=X'40000000'     'FROM' LENGTH (0) AND PADDING                
         L     RE,APRNT            'TO' ADDRESS                                 
         LH    RF,=H'5040'         'TO' LENGTH = SIZE OF DATA ON PAGE           
         MVCL  (RE),(R0)           MOVE SPACES TO PAGE AREA                     
*                                                                               
         MVI   LCNT,0              SET LINE COUNT BACK TO ZERO                  
         CLI   PFLAG,1             ARE WE ON TYPE 1 OR 2                        
         BE    *+14                                                             
         MVC   P(118),H1           TYPE 1 HEADINGS                              
         B     *+10                                                             
         MVC   P(118),H2           TYPE 2 HEADINGS                              
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         L     R5,APRNT            PT R5 AT BEGINNING OF PRNT                   
         LA    R7,180              45 LINES * 4 COLS                            
         B     M110                SET UP NEXT PAGE                             
*                                                                               
M140     L     R5,APRNT            SAME AS ABOVE, JUST FOR LAST LINE            
         LA    R4,P                                                             
         LA    R4,1(R4)                                                         
         LA    R7,45                                                            
         XC    LCNT,LCNT                                                        
M150     ST    R5,HOLD                                                          
         MVC   0(L'LUIDOUT-1,R4),1(R5)                                          
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   30(L'LUIDOUT-1,R4),1(R5)                                         
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   60(L'LUIDOUT-1,R4),1(R5)                                         
         LA    R5,L'LUIDOUT*45(R5)                                              
         MVC   90(L'LUIDOUT-1,R4),1(R5)                                         
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         ZIC   R1,LCNT                                                          
         LA    R1,1(R1)                                                         
         CH    R1,=H'5'                                                         
         BE    *+12                                                             
         STC   R1,LCNT                                                          
         B     M155                                                             
         XC    LCNT,LCNT                                                        
         CH    R7,=H'1'                                                         
         BE    M155                                                             
         GOTO1 =V(PRINTER)                                                      
*                                                                               
M155     L     R5,HOLD             RESTORE R5                                   
         LA    R5,L'LUIDOUT(R5)    ADVANCE TO NEXT LINE                         
         BCT   R7,M150                                                          
         CLI   PFLAG,1             HAVE WE BEEN HERE BEFORE                     
         BE    M160                IF SO, EXIT                                  
         MVC   TITLE,SPACES                                                     
         MVC   TITLE(17),=C'COMTENGEN BY LINE'                                  
         MVI   PFLAG,1             ELSE SET FLAG                                
         ZAP   PAGE,=P'1'                                                       
         ZAP   LINE,=P'75'         FORCE NEW PAGE                               
         B     M100                PRINT ON OTHER ORDER                         
M160     GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(SORTER),DMCB,=C'END'                                          
*                                                                               
         XBASE                                                                  
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* RDCARD: READ CARD.  1 CARD SHOULD HAVE ACTUALLY BEEN READ, BUT NOT  *         
*         PROCESSED BEFORE CALLING THIS ROUTINE.  A BETTER NAME WOULD *         
*         REALLY BE 'PROCESS CARD'.  THIS ROUTINE WILL SKIP OVER A    *         
*         COMMENT CARD AND BRING UP THE NEXT CARD.  THE PROCESSED     *         
*         CARD AND ALL OF IT'S CONTINUATION CARDS WILL BE IN BUFFER,  *         
*         WITH ALL NON-MEANINGFUL WHITE SPACE REMOVED.                *         
* IMPORTANT: THIS ROUTINE IS DESTRUCTIVE TO R1,R2, AND R3             *         
*            IT MUST BE CALLED WITH BAL   R7,RDCARD                   *         
***********************************************************************         
         SPACE                                                                  
RDCARD   DS    0H                                                               
RC01     CLC   =C'.DDSEND ',CARD                                                
         BE    *+14                                                             
         CLC   =C'*DDSEND ',CARD                                                
         BNE   RC01A                                                            
         MVI   EOF,1                                                            
         B     RCX                                                              
RC01A    XC    BUFFER,BUFFER                                                    
         LA    R2,BUFFER           PTR TO NEXT AVAIL SPACE IN BUFR              
RC02     LA    R1,CARD             PT TO FIRST NON-WHITE SPACE ON CARD          
         LA    R3,71               LEN OF TXT + END WHITE SPACE                 
RC04     CLI   0(R1),C' '          GET RID OF LEADING BLANKS                    
         BNE   RC06                                                             
         LA    R1,1(R1)                                                         
         BCTR  R3,0                                                             
         B     RC04                                                             
RC06     EX    R3,RC0X             MOVE CARD TO BUFFER                          
         B     *+10                BRANCH 'ROUND EX'D INS                       
*                                                                               
RC0X     MVC   0(0,R2),0(R1)       EXECUTE FOR VARIABLE LEN                     
*                                                                               
RC08     CLI   0(R2),C' '          IGNORE EOLN COMMENTS                         
         BNE   RC09                                                             
         CLI   1(R2),C'.'                                                       
         BE    RC091                                                            
         CLI   1(R2),C'*'                                                       
         BE    RC091                                                            
RC09     LA    R2,1(R2)                                                         
         BCT   R3,RC08                                                          
RC091    BCTR  R2,0                DON'T WANT TO SEE CONT. CHAR                 
RC10     CLI   0(R2),C' '          REUSE WHITE SPACE                            
         BNE   RC20                                                             
         BCTR  R2,0                DEC R2                                       
         B     RC10                                                             
RC20     LA    R2,1(R2)                                                         
         CLI   CARD+71,C' '        IS CONTINUATION COL BLANK?                   
         BE    RC30                NO? THEN PROCESS CARD                        
         GOTO1 =V(PANIC),DMCB,(X'00',=C'READ'),=C'PAN',PANNAME,CARD             
         B     RC02                ADD CONT. CARD TO BUFFER                     
RC30     CLI   BUFFER,C'.'         COMMENT MARKER                               
         BE    RC40                                                             
         CLI   BUFFER,C'*'         COMMENT MARKER                               
         BNE   RCX                                                              
RC40     GOTO1 =V(PANIC),DMCB,(X'00',=C'READ'),=C'PAN',PANNAME,CARD             
         B     RC01                READ NEXT SET                                
RCX      BR    R7                                                               
         EJECT                                                                  
***********************************************************************         
* CKMACRO: SEE IF THIS CARD IS ONE WE WANT, IE:                       *         
*          LINE/MAFLN, CLUSTER/PU/MAFCU, TERMINAL/LU/MAFTERM          *         
* NOTE: R2 MUST STILL POINT TO END OF BUFFER                          *         
***********************************************************************         
         SPACE                                                                  
CKMACRO  NTR1                                                                   
         LA    R3,BUFFER                                                        
         BAS   R7,SCNMAC                                                        
         CLC   0(4,R3),=C'LINE'    CK FOR CARDS BEG W/THIS MACRO                
*         BE    CM10                                                            
*         CLC   0(5,R3),=C'MAFLN'   CK FOR CARDS BEG W/THIS MACRO               
         BNE   CM20                                                             
CM10     MVC   LLINE,BUFFER                                                     
         MVC   ALINE,BUFFER                                                     
         GOTO1 =V(HEXIN),DMCB,BUFFER+5,ALINE+5,2                                
         MVI   ALINE+6,0                                                        
         B     CMX                                                              
CM20     CLC   0(7,R3),=C'CLUSTER' CK FOR CARDS BEG W/THIS MACRO                
         BE    CM30                                                             
         CLC   0(2,R3),=C'PU'      CK FOR CARDS BEG W/THIS MACRO                
*         BE    CM30                                                            
*         CLC   0(5,R3),=C'MAFCU'   CK FOR CARDS BEG W/THIS MACRO               
         BNE   CM50                                                             
CM30     MVC   LCLUSTER,BUFFER                                                  
         MVC   ACLUSTER,BUFFER                                                  
         GOTO1 =V(HEXIN),DMCB,BUFFER+5,ACLUSTER+5,2                             
         MVI   ACLUSTER+6,0                                                     
         B     CMX                                                              
CM50     CLC   0(8,R3),=C'TERMINAL' CK FOR CARDS BEG W/THIS MACRO               
         BE    CM60                                                             
         CLC   0(2,R3),=C'LU'      CK FOR CARDS BEG W/THIS MACRO                
*         BE    CM60                                                            
*         CLC   0(7,R3),=C'MAFTERM' CK FOR CARDS BEG W/THIS MACRO               
         BNE   CMX                                                              
CM60     MVC   LLUID,BUFFER                                                     
         MVC   ALUID,BUFFER                                                     
         XC    KEY,KEY                                                          
         MVI   KEY,C'T'                                                         
         MVC   KEY+7(8),LLUID                                                   
**NOP    CLI   LLUID+7,C'I'                                                     
**NOP    BNE   CMX                                                              
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'CTFILE',KEY,AIO                   
         TM    DMCB+8,X'10'                                                     
         BZ    CM70                                                             
         BAS   RE,ERROR                                                         
         B     CMX                                                              
CM70     DS    0H                                                               
*                                                                               
         GOTO1 =V(HEXIN),DMCB,LLUID+5,HOLD,2                                    
         MVC   LLUID+5(1),HOLD                                                  
         MVI   LLUID+6,0                                                        
*                                                                               
         GOTO1 =V(HEXIN),DMCB,ALUID+5,HOLD,2                                    
         MVC   ALUID+5(1),HOLD                                                  
         MVI   ALUID+6,0                                                        
*                                                                               
*  GOTO SORTER 2X, ONCE WITH LEADING 1(LUID), ONCE WITH LEADING 2(LADR)         
*                                                                               
         GOTO1 =V(SORTER),DMCB,=C'PUT',LUIDOUT                                  
         GOTO1 =V(SORTER),DMCB,=C'PUT',ADDROUT                                  
*                                                                               
CMX      B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ERROR: SAVE VTAM NAME AND NODE, IN GEN AND NOT CTFILE               *         
***********************************************************************         
         SPACE                                                                  
ERROR    NTR1                                                                   
         NOP   ER10                IGNORE FILTERS                               
         CLC   =C'DD',LLUID        NOT ERRORS ON DD LINES                       
         BE    ERX                                                              
         CLC   =C'MHER',LLUID      OR MHER                                      
         BE    ERX                                                              
         CLC   =C'HDTO',LLUID      OR HDTO                                      
         BE    ERX                                                              
         CLC   =C'SMTA',LLUID      OR SMTA                                      
         BE    ERX                                                              
         CLC   =C'JW',LLUID        OR JW***NN* WHERE NN > 31                    
         BNE   ER10                                                             
         CLC   LLUID+5(2),=C'31'                                                
         BH    ERX                                                              
ER10     DS    0H                                                               
         MVC   P(8),LLUID          SAVE VTAM NAME                               
         OC    P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
ERX      B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SCNMAC: SCAN TO MACRO OR TO PARAMETER                               *         
* NOTE: R3 SHOULD POINT TO BEGINNING OF BUFFER OR AT MACRO            *         
*       THIS ROUTINE MUST BE CALLED WITH BAL  R7,SCNMAC               *         
***********************************************************************         
         SPACE                                                                  
SCNMAC   CLI   0(R3),C' '          SCAN PAST LABEL (IF ANY)                     
         BE    SM20                                                             
         LA    R3,1(R3)                                                         
         B     SCNMAC                                                           
SM20     CLI   0(R3),C' '          SCAN PAST WHITE SPACE (IF ANY)               
         BNE   SMX                 CHANGE BACK TO 30                            
         LA    R3,1(R3)                                                         
         B     SM20                                                             
SMX      BR    R7                                                               
         EJECT                                                                  
***********************************************************************         
* BOX1: SET UP BOXES FOR TYPE 1                                       *         
***********************************************************************         
         SPACE                                                                  
BOX1     NTR1                                                                   
         L     R4,=V(BOXAREA)      A(BOX DSECT)                                 
         USING BOXD,R4                                                          
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+2,C'T'                                                   
         MVI   BOXROWS+4,C'M'      UNDER HEADINGS                               
         MVI   BOXROWS+58,C'B'                                                  
         MVC   BOXCOLS,SPACES                                                   
*                                                                               
         MVI   BOXCOLS,C'L'        LEFT BORDER                                  
         MVI   BOXCOLS+09,C'C'     AFTER LUID1                                  
         MVI   BOXCOLS+18,C'C'     AFTER LINE1                                  
         MVI   BOXCOLS+29,C'C'     AFTER CLUSTER1                               
*                                                                               
         MVI   BOXCOLS+39,C'C'     AFTER LUID2                                  
         MVI   BOXCOLS+48,C'C'     AFTER LINE2                                  
         MVI   BOXCOLS+59,C'C'     AFTER CLUSTER2                               
*                                                                               
         MVI   BOXCOLS+69,C'C'     AFTER LUID3                                  
         MVI   BOXCOLS+78,C'C'     AFTER LINE3                                  
         MVI   BOXCOLS+89,C'C'     AFTER CLUSTER3                               
*                                                                               
         MVI   BOXCOLS+99,C'C'     AFTER LUID4                                  
         MVI   BOXCOLS+108,C'C'    AFTER LINE4                                  
         MVI   BOXCOLS+119,C'R'    AFTER CLUSTER4                               
*                                                                               
         MVI   BOXWT,1                                                          
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXINIT,0                                                        
         MVI   BOXOFF,0                                                         
         DROP  R4                                                               
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
SSB      DC    F'0'                                                             
UTL      DC    F'0',X'0A'                                                       
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,28,A),FORMAT=CH,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=28'                                    
*                                                                               
FLIST    DS    0H                                                               
         DC    CL8' CTFILE '                                                    
         DC    CL8'X       '                                                    
*                                                                               
APRNT    DC    A(PRNT)             ADDR OF OUTPUT AREA                          
AIO      DC    A(IO)               ADDR OF IO AREA                              
*                                                                               
HOLD     DS    F                                                                
DUB      DS    D                                                                
PFLAG    DS    X                                                                
EOF      DC    X'00'               WHEN 1, DONE                                 
LCNT     DS    X                   LINE COUNT                                   
KEY      DS    CL28                KEY INTO CTFILE (VTAM)                       
WORK     DS    CL17                                                             
CARD     DS    CL80                EACH CARD READ INDIVIDUALLY HERE             
PANNAME  DC    CL10' '                                                          
DMCB     DS    6F                                                               
LNE      DS    CL8                                                              
CLUSTER  DS    CL8                                                              
*                                                                               
LUIDOUT  DS    0CL27                                                            
LTYPE    DC    C'1'                1=LUID KEY, 2=LINE ADDR KEY                  
LLUID    DS    CL8                 LUID                                         
         DC    C' '                                                             
LLINE    DS    CL8                 LINE NAME                                    
         DC    C' '                                                             
LCLUSTER DS    CL8                 CLUSTER                                      
*                                                                               
ADDROUT  DS    0CL27                                                            
ATYPE    DC    C'2'                1=LUID KEY, 2=LINE ADDR KEY                  
ALINE    DS    CL8                 LINE NAME                                    
         DC    C' '                                                             
ACLUSTER DS    CL8                 CLUSTER                                      
         DC    C' '                                                             
ALUID    DS    CL8                 LUID                                         
*                                                                               
H1       DC    C' LUID     LINE     CLUSTER     LUID     LINE     CLUSTX        
               ER     LUID     LINE     CLUSTER     LUID     LINE     CX        
               LUSTER   '                                                       
H2       DC    C' LINE     CLUSTER  LUID        LINE     CLUSTER  LUID X        
                      LINE     CLUSTER  LUID        LINE     CLUSTER  LX        
               UID      '                                                       
*                                                                               
GENTBL   DS    0CL6                                                             
         DC    C'GENDDN'                                                        
         DC    C'GENTOR'                                                        
         DC    C'GENJWN'                                                        
**NOP    DC    C'GENBLN'           PER CLAR 24JUL96                             
         DC    C'GENATL'                                                        
         DC    C'GENDAL'                                                        
         DC    C'GENDET'                                                        
         DC    C'GENCHI'                                                        
         DC    C'GENLAX'                                                        
         DC    X'00'                                                            
*                                                                               
         DC    C'*BUFFER*'                                                      
BUFFER   DS    50CL80              LOTS OF SPACE FOR CONT. CARDS                
PRNT     DS    180CL28                                                          
IO       DS    XL2000              IO AREA FOR DATAMGR                          
         DS    0D                                                               
*                                                                               
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE DDBIGBOX                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016CTREPGEN  05/01/02'                                      
         END                                                                    
