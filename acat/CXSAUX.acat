*          DATA SET CXSAUX     AT LEVEL 006 AS OF 05/01/02                      
*CATALP CXSAUX                                                                  
         TITLE 'CXSAUX - CONTROL SECURITY - SQL CONVERT'                        
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSAUX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSAUX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING CXSAUD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   CXSAUACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
UXTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSAUTYP,CXSAUTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSAUACT,CXSAUACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'CXSAUDAT+L'CXSAUTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),CXSAUDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXAID    NI    XFLAG,REQUIRED      AGENCY ALPHA ID - REQUIRED                   
         GOTO1 XMXCNVX,DMCB,(L'CXSAUAID,CXSAUAID),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXUIN    NI    XFLAG,REQUIRED      USER ID NUMBER - REQUIRED                    
         GOTO1 XMXCNVX,DMCB,(L'CXSAUUIN,CXSAUUIN),(XFLAG,(R3)),(1,INT)          
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXSYS    NI    XFLAG,REQUIRED      SYSTEM CODE - REQUIRED                       
         GOTO1 XMXCNVX,DMCB,(L'CXSAUSYS,CXSAUSYS),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXPGM    NI    XFLAG,REQUIRED      PROGRAM CODE - REQUIRED                      
         GOTO1 XMXCNVX,DMCB,(L'CXSAUPGM,CXSAUPGM),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXREC    NI    XFLAG,REQUIRED      RECORD CODE - REQUIRED                       
         CLI   CXSAUACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSAUREC,CXSAUREC),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXACN    NI    XFLAG,REQUIRED      ACTION CODE - REQUIRED                       
         CLI   CXSAUACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSAUACN,CXSAUACN),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXACV    NI    XFLAG,REQUIRED      ACCESS VALUE - REQUIRED                      
         CLI   CXSAUACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSAUACV,CXSAUACV),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
DXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
SMAL     DC    CL8'SMALLINT'                                                    
INT      DC    CL8'INT     '                                                    
REAL     DC    CL8'REAL    '                                                    
FLOT     DC    CL8'FLOAT   '                                                    
BIT      DC    CL8'BIT     '                                                    
MONY     DC    CL8'MONEY   '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C';'                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSAUD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006CXSAUX    05/01/02'                                      
         END                                                                    
