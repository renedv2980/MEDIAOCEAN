*          DATA SET SPAORTST   AT LEVEL 023 AS OF 05/01/02                      
*CATALP SPBA02                                                                  
         TITLE 'SPBA02 - SPOTPAK/ACCOUNTING BILL POSTING'                       
*                                                                               
* QOPT1    Y=TEST                                                               
* QOPT2    Y=LIST NON-POSTED                                                    
* QOPT3    Y=LIST PREVIOUS POSTINGS                                             
* QOPT4    D=DUMP WORKER FILE                                                   
* QOPT5    R=RERUN, OK TO RE-CREATE WORKER FILE                                 
* QOPT5+1  R=REPOST BILLS                                                       
* QOPT5+2    NETPAK SUB-MEDIA (C,S,ETC)                                         
*                                                                               
         PRINT NOGEN                                                            
SPBA02   CSECT                                                                  
         NMOD1 0,SPBA02                                                         
         SPACE 2                                                                
         LA    R9,4095(RB)         **2ND BASE REG                               
         LA    R9,1(R9)                                                         
         USING SPBA02+4096,R9                                                   
         L     RA,0(R1)                                                         
         LA    R8,1(RA)                                                         
         LA    R8,4095(R8)                                                      
         USING SPWORKD,RA,R8                                                    
         LA    RC,SPACEND                                                       
         USING SPBAWRKD,RC                                                      
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)                                                     
         MVC   4(1,RF),ACCTSE      SET ACCT SYS NUM                             
*                                                                               
         RELOC RELO                                                             
*                                                                               
         SPACE 2                                                                
         CLI   MODE,PROCBILL                                                    
         BE    PRBL                                                             
         CLI   MODE,CLTFRST                                                     
         BE    FBILC                                                            
         CLI   MODE,PRDFRST                                                     
         BE    FBILP                                                            
         CLI   MODE,PRDLAST                                                     
         BE    LBILP                                                            
         CLI   MODE,CLTLAST                                                     
         BE    LBILC                                                            
         CLI   MODE,REQFRST                                                     
         BE    REQF                                                             
         CLI   MODE,REQLAST                                                     
         BE    REQL                                                             
         CLI   MODE,RUNLAST                                                     
         BE    RUNL                                                             
         CLI   MODE,RUNFRST                                                     
         BE    RUNF                                                             
*                                                                               
EXIT     DS    0H                                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      RESTORE SYSTEM NUM                           
         XIT1                                                                   
*                                                                               
NEWRETDT DC    C'830801'           START DATE FOR 'NEW RETAIL'                  
         EJECT                                                                  
         SPACE 3                                                                
*        RUN FIRST                                                              
         SPACE 2                                                                
RUNF     DS    0H                                                               
         MVI   POSTSW,C'N'         CLEAR POSTED SWITCH                          
         MVC   ZEROAMTS(6),=PL6'0'                                              
         MVC   ZEROAMTS+6(L'ZEROAMTS-6),ZEROAMTS                                
         MVC   RAMTS,ZEROAMTS                                                   
         MVC   RQAMTS,ZEROAMTS                                                  
*                                                                               
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         ZAP   MAPRECNT,=P'0'                                                   
         ZAP   MAPCASH,=P'0'                                                    
         B     EXIT                                                             
         DROP  R5                                                               
         SPACE 3                                                                
*        FIRST FOR CLI                                                          
         SPACE 2                                                                
FBILC    DS    0H                                                               
         BC    0,FBILR10                                                        
         OI    *-3,X'F0'           FIRST TIME ONLY                              
*                                                                               
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         XC    PROGPROB,PROGPROB   'B' PROFILE                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SBAB'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE                          
         MVC   WORK+4(2),QAGY                                                   
         MVC   WORK+6(1),QMED                                                   
         MVC   WORK+7(3),CLT                                                    
         L     RF,ADCLT                                                         
         CLI   COFFICE-CLTHDR(RF),C' '                                          
         BNH   *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE-CLTHDR(RF)                                    
         GOTO1 GETPROF,DMCB,WORK,PROGPROB,DATAMGR                               
*                                                                               
*                                  OPEN ACCFILE                                 
         CLI   PROGPROB+2,0        TEST ACC SYS OVERRIDE                        
         BE    *+10                                                             
         MVC   ACCTSE,PROGPROB+2                                                
         L     RF,UTL                                                           
         MVC   4(1,RF),ACCTSE                                                   
         CLI   ACCTSE,0           TEST ACC CONNECTION                           
         BNE   FBILR2                                                           
         MVC   P(29),=C'**NO ACCFILE FOR REQUEST ID**'                          
         GOTO1 REPORT                                                           
         GOTO1 AENDREQ                                                          
*                                                                               
FBILR2   DS    0H                                                               
         L     R4,ADBUY          USE AS WORK AREA FOR OPEN                      
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'ACCFIL ',FILELIST,(R4)                  
*                                                                               
         XC    WORK,WORK                                                        
*                                                                               
         GOTO1 DATCON,DMCB,TODAY,(5,TODAYD)                                     
*                                                                               
         LA    R4,WORK                                                          
         USING UKRECD,R4                                                        
*                                                                               
         MVC   UKUSRID,RCORIGID                                                 
         OC    PROGPROB+0(2),PROGPROB+0    TEST OVERRIDE ID                     
         BZ    *+10                                                             
         MVC   UKUSRID,PROGPROB+0                                               
         MVC   UKSYSPRG,=C'SBA'                                                 
         MVI   NETPSW,C'N'                                                      
         CLI   QMED,C'N'           IF NETWORK REQ                               
         BNE   FBILR3                                                           
         OC    SPOTPROF+9(2),SPOTPROF+9    AND NETPAK AGENCY                    
         BZ    FBILR3                                                           
         MVI   UKSYSPRG,C'N'       SET ID TO NBA                                
         MVI   NETPSW,C'Y'                                                      
*                                                                               
FBILR3   DS    0H                                                               
         L     RF,LOGOC                                                         
         USING LOGOD,RF                                                         
         CLI   LOGOJOB+7,C' '      LAST CHAR OF JOB                             
         BNH   *+10                                                             
         MVC   UKSUBPRG,LOGOJOB+7  USE AS SUB PRG                               
         DROP  RF                                                               
*                                                                               
         PACK  FULL(2),TODAY+4(3)                                               
         MVC   UKDAY,FULL                                                       
         MVI   UKCLASS,C'P'                                                     
         DROP  R4                                                               
*                                                                               
         MVC   WID,WORK                                                         
         LA    R0,MAIN                                                          
         LA    R1,MAOUT                                                         
         L     R2,GETPROF                                                       
         L     R3,DATAMGR                                                       
         STM   R0,R3,MAPARS                                                     
         MVC   PAGE,=H'1'                                                       
         MVI   FORCEHED,C'Y'                                                    
         L     RF,=V(MALINK)                                                    
         A     RF,RELO                                                          
         ST    RF,AMALINK                                                       
         L     RF,=V(MAPOST)                                                    
         A     RF,RELO                                                          
         ST    RF,AMAPOST                                                       
*                                                                               
*                                  SET PERMANENT FIELDS IN MAPOSTD              
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         ZAP   MAPRECNT,=P'0'                                                   
         ZAP   MAPCASH,=P'0'                                                    
         L     RF,=A(WRKRBUFF)                                                  
         A     RF,RELO                                                          
         ST    RF,MAPFILE                                                       
         LA    RF,SVPOST           A(SAVE POST AREA)                            
         ST    RF,MAPSVPST                                                      
*                                                                               
         MVC   MAPWRKR,WORKER                                                   
         MVC   MAPRECUP,RECUP                                                   
         MVC   MAPID,WID                                                        
*                                  SEE IF ID ALREADY ON FILE                    
         GOTO1 WORKER,DMCB,=C'INDEX',MAPFILE,WID                                
*                                                                               
         TM    DMCB+8,X'90'        REC NOT FOUND OR EOF                         
         BNZ   FBILR4                                                           
         CLI   QOPT5,C'R'          TEST RERUN OK                                
         BE    FBILR4                                                           
*                                                                               
FBILR3D  DS    0H                                                               
         MVC   P(37),=C'**RE-RUN ATTEMPT - REQUEST BYPASSED**'                  
         GOTO1 REPORT                                                           
         MVI   RERUNSW,C'Y'                                                     
         GOTO1 AENDREQ                                                          
*                                                                               
         DROP  R5                                                               
FBILR4   DS    0H                                                               
         MVI   RERUNSW,C'N'                                                     
         MVC   RQAMTS,ZEROAMTS                                                  
FBILR10  DS    0H                                                               
         CLI   PROGPROF+8,0        CORRECT CD PASSALONG OPTION                  
         BNE   *+8                                                              
         MVI   PROGPROF+8,C'Y'      (DEFAULTS TO YES)                           
*                                                                               
         CLI   SVCOMP,0            TEST MIXED COMPANY CODES                     
         BNE   *+10                                                             
         MVC   SVCOMP,PROGPROF+0                                                
         CLI   PROGPROF+0,0                                                     
         BE    FBILR10F                                                         
         CLC   SVCOMP,PROGPROF+0                                                
         BE    FBILR10F                                                         
         MVC   P(23),=C'**MIXED COMPANY CODES**'                                
         MVC   P2(80),QRECORD                                                   
         GOTO1 AENDREQ                                                          
*                                                                               
FBILR10F DS    0H                                                               
         CLI   RERUNSW,C'Y'                                                     
         CLI   RERUNSW,C'Y'                                                     
         BE    FBILR3D                                                          
         MVC   TESTOPT,QOPT1                                                    
         MVC   LISTOPT,QOPT2                                                    
         MVC   PREVOPT,QOPT3                                                    
         MVC   DMPOPT,QOPT4                                                     
         MVI   RCSUBPRG,0                                                       
         CLI   PREVOPT,C'Y'                                                     
         BNE   *+8                                                              
         MVI   RCSUBPRG,1                                                       
         XC    BIGNDTE,BIGNDTE     'IGNORE' DATE                                
         CLI   QPROG+52,C' '                                                    
         BE    FBILR11                                                          
         GOTO1 DATCON,DMCB,QPROG+52,(2,BIGNDTE)                                 
*                                                                               
FBILR11  DS    0H                                                               
         B     FBILR20                                                          
*                                                                               
FILELIST DC    CL8'NACCFIL '                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL10'X'                                                          
         SPACE 3                                                                
FBILR20  DS    0H                                                               
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
         XC    PROGPROA,PROGPROA   'A' PROFILE                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SBAA'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE FOR PAGE A               
         MVC   WORK+4(2),QAGY                                                   
         MVC   WORK+6(1),QMED                                                   
         MVC   WORK+7(3),CLT                                                    
         L     RF,ADCLT                                                         
         CLI   COFFICE-CLTHDR(RF),C' '                                          
         BNH   *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE-CLTHDR(RF)                                    
         GOTO1 GETPROF,DMCB,WORK,PROGPROA,DATAMGR                               
*                                                                               
         MVC   MAIN,SPACES                                                      
         MVC   MAUTL,UTL           PASS UTL AND SE'S TO MALINK                  
         MVC   MASAVSE,SAVSYS                                                   
         MVC   MAACCSE,ACCTSE                                                   
         MVI   MASYS,C'S'                                                       
         MVC   MAAGY(3),QAGY                                                    
         CLI   QOPT5+2,C' '        TEST ANY NETPAK SUB-MEDIA                    
         BNH   *+10                (NOTE- FILTERS BASED ON BLMED)               
         MVC   MAMED,QOPT5+2                                                    
         MVC   MACLT,CLT                                                        
         L     RF,ADCLT                                                         
         MVC   MAOFF,COFFICE-CLTHDR(RF)                                         
         OI    MAOFF,C' '                                                       
         MVC   MAACOFF,CACCOFC-CKEY(RF)                                         
         OC    MAACOFF,SPACES                                                   
         MVC   MAPROA,PROGPROA                                                  
         MVC   MAPROB,PROGPROB                                                  
         MVC   CAMTS,ZEROAMTS                                                   
*                                  CHECK CLIENT STATUS                          
         GOTO1 AMALINK,MAPARS                                                   
         TM    MAOUT+MALNOPST-MALINKD,X'80'                                     
         BZ    FBILC4                                                           
         CLI   MAPROB+3,C'Y'       BUT IF GETTING BA PROF FOR CLT               
         BE    FBILC4D             GIVE CLIENT MESSAGE                          
*                                  NO POSTINGS FOR AGY                          
         MVC   P(80),QRECORD                                                    
         MVC   P2(34),=C'**AGENCY NOT SET UP FOR POSTINGS**'                    
         BAS   RE,PRNT                                                          
FBILC3   DS    0H                                                               
         GOTO1 AENDREQ                                                          
*                                                                               
FBILC4   DS    0H                                                               
         TM    MAOUT+MALNOPST-MALINKD,X'40'                                     
         BZ    FBILC6                                                           
*                                  NO POSTINGS FOR CLIENT                       
FBILC4D  DS    0H                                                               
         CLI   LISTOPT,C'Y'        TEST LISTING ALL                             
         BE    FBILC6              YES                                          
         CLC   QCLT,=C'ALL'     ELSE BYPASS CLIENT                              
         BE    FBILC5                                                           
         CLI   QCLT,C'*'                                                        
         BE    FBILC5                                                           
*                                  UNLESS 1 CLT REQ                             
         MVC   P(80),QRECORD                                                    
         MVC   P2(31),=C'**NO POSTINGS FOR THIS CLIENT**'                       
         BAS   RE,PRNT                                                          
         B     FBILC3                                                           
FBILC5   DS    0H                                                               
         MVI   MODE,CLTLAST        SKIP INACTIVE CLIENT                         
         B     EXIT                                                             
FBILC6   DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
*        FIRST FOR PRODUCT                                                      
         SPACE 2                                                                
FBILP    DS    0H                                                               
         MVC   PAMTS,ZEROAMTS                                                   
         LA    RE,SVPOST           CLEAR SAVE POST AREA                         
         LA    RF,L'SVPOST                                                      
         XCEF                                                                   
         B     EXIT                                                             
         EJECT                                                                  
*        LAST FOR PRODUCT                                                       
         SPACE 2                                                                
LBILP    DS    0H                                                               
*                                                                               
         CLC   PAMTS,ZEROAMTS                                                   
         BE    EXIT                                                             
         LA    R3,PAMTS                                                         
         LA    R4,CAMTS                                                         
         BAS   RE,PBROLL                                                        
*                                                                               
         BAS   RE,PRNT                                                          
         MVC   P+10(20),=C'** PRODUCT TOTALS **'                                
         LA    R3,PAMTS                                                         
         BAS   RE,TOTPRNT                                                       
         MVC   PAMTS,ZEROAMTS                                                   
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST  FOR CLIENT                                                       
         SPACE 2                                                                
LBILC    DS    0H                                                               
         CLC   CAMTS,ZEROAMTS                                                   
         BE    EXIT                                                             
         LA    R3,CAMTS                                                         
         LA    R4,RQAMTS                                                        
         BAS   RE,PBROLL                                                        
*                                                                               
         BAS   RE,PRNT                                                          
         MVC   P+10(19),=C'** CLIENT TOTALS **'                                 
         LA    R3,CAMTS                                                         
         BAS   RE,TOTPRNT                                                       
*                                                                               
         MVC   CAMTS,ZEROAMTS                                                   
         B     EXIT                                                             
         SPACE 3                                                                
*        FIRST FOR REQ                                                          
REQF     DS    0H                                                               
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         XC    B1XPROF,B1XPROF      B1X PROFILE                                 
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SB1X'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE FOR PAGE A               
         MVC   WORK+4(2),AGY                                                    
         MVC   WORK+6(1),MED                                                    
         GOTO1 GETPROF,DMCB,WORK,B1XPROF,DATAMGR                                
*                                                                               
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST FOR REQ                                                           
REQL     DS    0H                                                               
         CLC   RQAMTS,ZEROAMTS                                                  
         BE    EXIT                                                             
         LA    R3,RQAMTS                                                        
         LA    R4,RAMTS                                                         
         BAS   RE,PBROLL                                                        
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   P+10(20),=C'** REQUEST TOTALS **'                                
         LA    R3,RQAMTS                                                        
         BAS   RE,TOTPRNT                                                       
*                                                                               
         MVC   RQAMTS,ZEROAMTS                                                  
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST FOR RUN                                                           
         SPACE 2                                                                
RUNL     DS    0H                                                               
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   P+10(19),=C'** REPORT TOTALS **'                                 
         LA    R3,RAMTS                                                         
         BAS   RE,TOTPRNT                                                       
*                                                                               
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         CLI   POSTSW,C'Y'         TEST ANY POSTINGS                            
         BNE   RUNL10                NO                                         
*                                  CLOSE POSTING FILE                           
         GOTO1 AMAPOST,DMCB,(X'FF',POSTWORK)                                    
         CLI   DMPOPT,C'D'         DUMP RECORDS                                 
         BNE   RUNL10                                                           
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   P(10),=C'WORKER ID='                                             
         GOTO1 HEXOUT,DMCB,WID,P+11,8,=C'N'                                     
         MVC   P+29(8),WID                                                      
         TR    P+29(8),TRTAB                                                    
         BAS   RE,PRNT                                                          
         BAS   RE,PRNT                                                          
         GOTO1 WORKER,DMCB,=C'INDEX',MAPFILE,WID                                
         TM    DMCB+8,X'FF'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
RUNL4    DS    0H                                                               
         GOTO1 WORKER,DMCB,=C'READ',MAPFILE,WID,MAOUT                           
         TM    DMCB+8,X'80'                                                     
         BNZ   RUNL10                                                           
         BAS   RE,PRNT                                                          
         LA    R6,MAOUT                                                         
         LH    R2,0(R6)            LENGTH                                       
         LA    R3,0(R6,R2)         EOR                                          
*                                                                               
RUNL6    DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R6                                                            
         BNP   RUNL4                                                            
         CH    R4,=H'32'                                                        
         BNH   *+8                                                              
         LA    R4,32                                                            
         XC    WORK,WORK                                                        
         GOTO1 HEXOUT,DMCB,(R6),WORK,(R4),=C'N'                                 
*                                                                               
         MVC   P+01(8),WORK+00                                                  
         MVC   P+10(8),WORK+08                                                  
         MVC   P+19(8),WORK+16                                                  
         MVC   P+28(8),WORK+24                                                  
         MVC   P+40(8),WORK+32                                                  
         MVC   P+49(8),WORK+40                                                  
         MVC   P+58(8),WORK+48                                                  
         MVC   P+67(8),WORK+56                                                  
*                                                                               
         MVC   WORK(32),0(R6)                                                   
         TR    WORK(32),TRTAB                                                   
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+79(0),WORK                                                     
         LA    R4,1(R4)                                                         
         BAS   RE,PRNT                                                          
         LA    R6,0(R6,R4)                                                      
         B     RUNL6                                                            
*                                                                               
RUNL10   DS    0H                                                               
*                                                                               
         L     R3,LOGOC                                                         
         USING LOGOD,R3                                                         
         SPACE 1                                                                
         MVI   LOGOTYPE,C'E'                                                    
         GOTO1 LOGO,DMCB,(R3)                                                   
         SPACE 1                                                                
         MVI   LOGOEND,C'X'                                                     
         MVI   LOGOTYPE,C'S'                                                    
         MVC   LOGONAME,=CL33'******** INTERNAL CONTROL *******'                
         MVC   LOGOADD,=CL33'******** DO NOT SEND OUT ******'                   
         MVC   LOGOADD2,SPACES                                                  
         MVC   LOGOADD3,SPACES                                                  
         MVC   LOGO1,=C'CONTROL'                                                
         GOTO1 LOGO,DMCB,(R3)                                                   
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         EDIT  (P6,MAPCASH),(12,P+40),2,MINUS=YES                               
         MVC   P+25(13),=C'TOTAL POSTING'                                       
         MVI   RCSUBPRG,10                                                      
         GOTO1 REPORT                                                           
         B     EXIT                                                             
         SPACE 3                                                                
TRTAB    DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     00-0F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     10-1F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     20-2F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     30-3F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     40-4F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B5B5C5D4B4B'     50-5F                    
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D4B6F'     60-6F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B7B4B7D7E4B'     70-7F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     80-8F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     90-9F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     A0-AF                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     B0-BF                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'     C0-CF                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'     D0-DF                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'     E0-EF                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'     F0-FF                    
*                                                                               
         EJECT                                                                  
*        PROCESS BILL                                                           
         SPACE 2                                                                
PRBL     DS    0H                                                               
         MVC   SVBILDA,KEY+14      SAVE BILL DISK ADDR FOR LATER REREAD         
         L     R7,ADEST            GET ESTIMATE RECORD                          
         L     RF,ADBILL                                                        
         CLC   0(8,RF),0(R7)       TEST HAVE ALREADY                            
         BE    PRB2                                                             
         XC    0(13,R7),0(R7)      CLEAR EST HEADER                             
         MVC   WORK(32),KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(8),0(RF)                                                     
         CLI   KEY+7,0             SKIP IF EST = 0                              
         BE    PRB2                                                             
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   PRB1                                                             
         GOTO1 GETEST                                                           
*                                                                               
PRB1     DS    0H                                                               
         MVC   KEY(32),WORK                                                     
         GOTO1 HIGH                                                             
         L     RF,UTL              RESET TO ACCT SYS NO                         
         MVC   4(1,RF),ACCTSE                                                   
*                                                                               
PRB2     DS    0H                                                               
         OC    0(13,R7),0(R7)      TEST HAVE EST HEADER                         
         BZ    PB99                                                             
         L     R2,ADBILL                                                        
         USING BILLREC,R2                                                       
*                                                                               
*                                  **NO-OP**                                    
*        CLC   BDATE,TODAY         SKIP TODAY'S BILLS                           
*        BNL   PB99                                                             
*                                                                               
*                                                                               
         CLI   QOPT5+2,C' '        TEST NETPAK SUB-MEDIA                        
         BNH   PRB2D                                                            
         CLC   BLMED,QOPT5+2                                                    
         BNE   PB99                                                             
*                                                                               
PRB2D    DS    0H                                                               
         LA    RF,BQDATE           FILTER ON INVOICE DATE                       
         CLI   PROGPROF+4,C'I'                                                  
         BE    *+8                                                              
         LA    RF,BDATE            OR BILL RUN DATE                             
         CLC   0(6,RF),QSTART                                                   
         BL    PB99                                                             
         CLI   QEND,C' '                                                        
         BE    PB3                                                              
         CLC   0(6,RF),QEND                                                     
         BH    PB99                                                             
*                                                                               
PB3      DS    0H                                                               
         MVI   MAAORSW,C'N'        RESET AOR SWITCH HERE                        
         CLI   QOPT5+1,C'R'        TEST TO RE-POST ALL                          
         BE    *+14                                                             
         CLC   BILPOST,BIGNDTE     IF POSTED ON IGNORE DATE                     
         BNE   *+10                                                             
         XC    BILPOST,BILPOST TREAT AS UN-POSTED                               
         OC    BILPOST,BILPOST   TEST PREVIOUSLY POSTED                         
         BNZ   PB6               YES                                            
         CLI   PREVOPT,C'Y'      ONLY PREV POSTINGS                             
         BE    PB99                                                             
*                                                                               
PB4      DS    0H                                                               
         MVC   MAPRD,BKEYPRD                                                    
         MVC   MABLTYP,BTYPE+1     SET BILL TYPE 4,5,6,7                        
*                                  SET 'OTHERS'                                 
         L     RF,ADEST                                                         
         MVC   MAOTHER(3),EPROF-ESTHDR(RF)   PASS ESTIMATE FILTERS              
*                                                                               
         MVC   MARETYP,BRETAIL    RETAIL TYPE                                   
*                                  TEMP FIX FOR BAD BILLS                       
         CLI   BRETACCT+9,C'-'                                                  
         BNE   *+10                                                             
         MVC   BRETACCT,=C'    000001  '                                        
*                                                                               
         MVC   MARET,BRETACCT      RETAIL ACCT                                  
*                                                                               
PB5      DS    0H                  TEST AOR STATUS                              
         MVI   MAAORSW,C'N'                                                     
         OC    BILPOST,BILPOST     SKIP IF ALREADY POSTED                       
         BNZ   PB6                                                              
         TM    BILSTAT,X'20'       DO LOOK UP FOR ALL TRUE AORS                 
         BNZ   PB5A04                                                           
         CLI   PROGPROA+7,1        FOR AOR SPECIAL AOR POSTING                  
         BL    PB5M                                                             
         CLI   PROGPROA+7,10       OPTION 1-10 (OGILVY STYLE)                   
         BH    PB5M                                                             
         TM    BILSTAT,X'10'       DO FOR AOR CLIENT BILLS ALSO                 
         BZ    PB5M                                                             
*                                                                               
PB5A04   DS    0H                  READ FOR AOR DATA                            
         MVC   WORK(32),KEY        SAVE KEY                                     
         L     RF,UTL              RESTORE SYSTEM CODE                          
         MVC   4(1,RF),SAVSYS                                                   
         XC    X(32),X                                                          
         LA    R7,X                                                             
         USING AORKEY,R7                                                        
         MVC   AORKTYP,=X'0D45'   RECORD TYPE                                   
         MVC   AORKAGMD,BAGYMD                                                  
         MVC   AORKCLT,BCLT                                                     
         MVC   AORKPRD,PRD                                                      
         MVC   AORKEST+1(1),BKEYEST     ESTIMATE NUMBER                         
*                                                                               
         MVI   AORKDPT,X'FF'       DEFAULT DAYPART                              
         CLI   BLDPT,C' '          TEST BILL FOR ONE DAYPART                    
         BNH   *+10                                                             
         MVC   AORKDPT(1),BLDPT                                                 
         MVI   AORKSTYP,X'FF'      DEFAULT STATION TYPE                         
         CLI   BLMED,C' '          TEST BILL FOR ONE STATION TYPE               
         BNH   *+10                                                             
         MVC   AORKSTYP(1),BLMED                                                
         MVI   AORKEXT+2,X'FF'     3RD POSITION NOT USED                        
*                                                                               
         CLC   X(13),SVAORKEY      TEST SEARCHING FOR SAME DATA                 
         BE    PB5A30              YES                                          
         MVC   SVAORKEY,X                                                       
         L     RF,ADBUY            CLEAR AOR REC                                
         XC    0(60,RF),0(RF)                                                   
*                                                                               
         MVC   KEY,X                                                            
         LA    R7,KEY                                                           
         GOTO1 HIGH                                                             
         CLC   KEY(8),KEYSAVE      TEST THRU PRD                                
         BNE   PB5A21              NOTHING FOR PRODUCT                          
         CLC   KEY(10),KEYSAVE     TEST THRU EST                                
         BE    PB5A08                                                           
         CLC   KEY+8(2),=X'FFFF'   DID WE FIND DEFAULT                          
         BE    PB5A08              YES, USE IT                                  
*                                                                               
PB5A07   DS    0H                                                               
         MVC   KEYSAVE+8(2),=X'FFFF'  TRY DEFAULT EST                           
         MVC   KEY,KEYSAVE                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(10),KEYSAVE                                                  
         BNE   PB5A21              NOTHING THERE EITHER                         
*                                                                               
PB5A08   DS    0H                                                               
         CLC   KEY+10(1),KEYSAVE+10   DAYPART                                   
         BE    PB5A10                                                           
         CLI   KEY+10,X'FF'           DID WE FIND DEFAULT                       
         BE    PB5A10                                                           
         CLI   KEYSAVE+10,X'FF'       DID WE TRY FOR IT                         
         BE    PB5A19                                                           
*                                                                               
PB5A09   DS    0H                                                               
         MVI   KEYSAVE+10,X'FF'       TRY DEFAULT                               
         MVC   KEY,KEYSAVE                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(11),KEYSAVE                                                  
         BNE   PB5A19                NOTHING                                    
*                                                                               
PB5A10   DS    0H                                                               
         CLC   KEY+11(1),KEYSAVE+11    STATION TYPE                             
         BE    PB5A20                                                           
         CLI   KEY+11,X'FF'            DID WE FIND DEFAULLT                     
         BE    PB5A20                                                           
         CLI   KEYSAVE+11,X'FF'        DID WE TRY FOR IT                        
         BE    PB5A18                                                           
         MVI   KEYSAVE+11,X'FF'        TRY DEFAULT                              
         MVC   KEY,KEYSAVE                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(12),KEYSAVE                                                  
         BE    PB5A20                                                           
*                                                                               
PB5A18   DS    0H                  NOTHING FOUND                                
         CLI   KEYSAVE+10,X'FF'    WERE WE LOOKING UNDER ALL DPTS               
         BE    PB5A19              YES                                          
*                                  NO, DO IT NOW                                
         MVI   KEYSAVE+11,X'FF'    RESET STATION TYPE                           
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     PB5A09                                                           
*                                                                               
PB5A19   DS    0H                                                               
         CLC   KEYSAVE+8(2),=X'FFFF'   WERE WE LOOKING UNDER ALL ESTS           
         BE    PB5A21              YES, NOTHING MORE TO DO                      
*                                  NO, DO IT NOW                                
         MVI   KEYSAVE+10,X'FF'     RESET DAYPART                               
         CLI   BLDPT,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+10(1),BLDPT                                              
         MVI   KEYSAVE+11,X'FF'    RESET STATION TYPE                           
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     PB5A07                                                           
*                                                                               
PB5A20   DS    0H                                                               
         B     PB5A22                                                           
*                                                                               
PB5A21   DS    0H                                                               
         MVC   KEY,WORK            RESTORE SEQ                                  
         GOTO1 HIGH                                                             
         L     RF,UTL              RESTORE ACC SYS CODE                         
         MVC   4(1,RF),ACCTSE                                                   
         MVI   MAAORSW,C'E'       AOR ERROR                                     
         B     PB5F                                                             
*                                                                               
PB5A22   DS    0H                                                               
         L     R7,ADBUY                                                         
         ST    R7,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
PB5A30   DS    0H                                                               
         L     R7,ADBUY            AOR RECORD                                   
         LA    R2,AORELS                                                        
         CLI   0(R2),X'03'                                                      
         BE    PB5B                                                             
         MVI   ELCODE,X'03'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   PB5A21                                                           
*                                                                               
PB5B     DS    0H                                                               
         USING AORELEM,R2                                                       
         CLC   AORRCVBL,SPACES     ACCT MUST BE THERE                           
         BNH   PB5A21                                                           
         MVC   MAAORRCV,AORRCVBL                                                
         MVC   MAAORCOM,AORCOMM                                                 
*                                                                               
         MVC   KEY,WORK            RESTORE SEQ                                  
         GOTO1 HIGH                                                             
         L     RF,UTL              RESTORE ACC SYS CODE                         
         MVC   4(1,RF),ACCTSE                                                   
         DROP  R7                                                               
         DROP  R2                                                               
*                                                                               
PB5D     DS    0H                                                               
         L     R2,ADBILL                                                        
         USING BILLREC,R2                                                       
         MVI   MAAORSW,C'Y'        TRUE AOR BILL                                
         TM    BILSTAT,X'20'                                                    
         BNZ   PB5F2                                                            
         MVI   MAAORSW,C'C'        ELSE CLIENT AOR BILL                         
*                                                                               
PB5F     DS    0H                                                               
         L     R2,ADBILL                                                        
         USING BILLREC,R2                                                       
         TM    BILSTAT,X'20'      FOR TRUE AORS, LOOK FOR ORIGINAL              
         BZ    PB5M                                                             
PB5F2    DS    0H                                                               
         LA    R5,SVPOST           LOOK AT SAVED POST AREA                      
         USING MAPOSTD,R5          MED/CLT/PRD WILL BE OK                       
         CLI   MAPAOR,C'Y'         PREVIOUS CANNOT ALSO BE AOR                  
         BE    PB5G                                                             
         CLC   MAPEST,EST          EST MUST AGREE                               
         BNE   PB5G                                                             
         CLC   MAPDATE,BQDATE      AND DATE                                     
         BNE   PB5G                                                             
         CLC   MAPSERV,BMONSERV    AND MOS                                      
         BNE   PB5G                                                             
         B     PB5H                                                             
         DROP  R5                                                               
*                                                                               
PB5G     DS    0H                                                               
         DC    H'0'                MISSING AOR ORIGINAL                         
*                                                                               
PB5H     DS    0H                                                               
PB5M     DS    0H                  GET ACCOUNT DATA                             
         GOTO1 AMALINK,MAPARS      GET ACCT DATA                                
         CLI   MASTAT,0            TEST OK TO POST                              
         BE    PB6                 YES                                          
         CLI   LISTOPT,C'Y'        NO - TEST LISTING NON-POSTS                  
         BNE   PB98                NO                                           
*                                                                               
PB6      DS    0H                  SET BILL AMOUNTS                             
         MVC   BAC,BACTUAL         FOR AOR BILLS, AC = ACTUAL                   
         MVC   BACT,BACTUAL                                                     
         MVC   BARAMT,BACTUAL                                                   
         XC    BNT,BNT             NO NET,GROSS, OR CD                          
         XC    BGRS,BGRS                                                        
         XC    BCD,BCD                                                          
*                                                                               
         TM    BILSTAT,X'20'       IF AOR, SKIP FUTHER CALCS                    
         BNZ   PB30                                                             
         XC    BAMTS,BAMTS                                                      
         PACK  DUB,BAMT                                                         
         CVB   R0,DUB                                                           
         ST    R0,BGRS             GROSS                                        
         SR    R0,R0                                                            
         ST    R0,BCD              CD                                           
         MVC   BACT,BACTUAL                                                     
         MVC   BARAMT,BACTUAL                                                   
         L     R1,BACT                                                          
         TM    BILSTAT,X'02'       TEST COMM ONLY BILL                          
         BNZ   *+10                IF SO SET NET=0 (COMM=ACT)                   
         MVC   BNT,BNET                                                         
         S     R1,BNT                                                           
         ST    R1,BAC              AC                                           
         MVI   RETAIL,C'N'                                                      
         CLI   BRETAIL,0                                                        
         BE    PB30                                                             
*                                  RETAIL BILLS                                 
         CLI   BRETAIL,X'81'       SKIP ALL CORP CONTROL                        
         BE    PB99                                                             
         OC    BAMTS,BAMTS         SKIP ZERO BILLS                              
         BZ    PB99                                                             
*                             BUILD PRINT LINE                                  
PB30     DS    0H                                                               
         LA    R7,P                                                             
         USING BLINED,R7                                                        
*                                                                               
         MVC   BLPRD,BKEYPRD      PRD                                           
*                                                                               
         ZIC   R0,BKEYEST                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BLEST,DUB                                                        
*                                                                               
         GOTO1 DATCON,DMCB,(3,BKEYYSRV),(6,BLPER)                               
*                                                                               
         GOTO1 (RF),(R1),BQDATE,(5,BLINVD)                                      
*                                                                               
         MVC   DINVNO,BINVNO       SET DISPLAY INVOICE NUMBER                   
         CLI   B1XPROF+4,0         TEST ANY SPECIAL BASE YEAR                   
         BE    PB33                                                             
         PACK  DUB,BDATE(2)        YEAR OF BILL                                 
         CVB   R0,DUB                                                           
         ZIC   RF,B1XPROF+4        INVOICE BASE YEAR                            
         SR    R0,RF               DIFFERENCE                                   
         BNP   PB33                                                             
         MH    R0,=H'12'                                                        
         PACK  DUB,BDATE+2(2)      MONTH                                        
         CVB   RF,DUB                                                           
         AR    R0,RF                                                            
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DINVNO(2),DUB       SET NEW INVOICE 'MONTH'                      
*                                                                               
PB33     DS    0H                                                               
         MVC   BLINVNO(2),DINVNO                                                
         MVI   BLINVNO+2,C'-'                                                   
         MVC   BLINVNO+3(4),BINVNO+2                                            
*                                                                               
         L     R0,BACT             ACTUAL                                       
         LA    R6,BLACTUAL                                                      
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BNT              NET                                          
         LA    R6,BLNET                                                         
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BAC              AGY COMM                                     
         LA    R6,BLAC                                                          
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BARAMT                                                        
         LA    R6,BLARAMT                                                       
         BAS   RE,PBEDT                                                         
*                                                                               
         LA    R6,MAOUT                                                         
         USING MALINKD,R6                                                       
         OC    BILPOST,BILPOST        TEST PREVIOUSLY POSTED                    
         BZ    PB34                     NO                                      
*                                       YES-SHOW DATE                           
         GOTO1 DATCON,DMCB,(2,BILPOST),(5,BLPOST)                               
         B     PB34D                                                            
*                                       NOT PREV POSTED                         
PB34     DS    0H                                                               
         CLI   MAAORSW,C'E'        AOR ERROR                                    
         BNE   *+14                                                             
         MVC   BLPOST(20),=C'**MISSING AOR INFO**'                              
         B     PB34D                                                            
*                                                                               
         L     RF,MALRPTR          POINTER TO ACTUAL RECEIVABLE ACCT            
         CLC   0(15,RF),SPACES       CAN I POST NOW                             
         BNE   PB34A2                   YES                                     
*                                                                               
         MVC   BLPOST(2),=C'**'                                                 
         MVC   BLPOST+2(L'BLPOST-5),MALKYERR   SET ERROR KEY                    
         LA    RF,BLPOST+L'BLPOST-3                                             
         MVI   0(RF),C'='                                                       
         LA    RF,1(RF)                                                         
         ST    RF,DMCB+4                                                        
         GOTO1 HEXOUT,DMCB,MALNOPST,,1,=C'N'                                    
         B     PB34D                                                            
*                                                                               
PB34A2   DS    0H                                                               
         CLI   PROGPROA+0,C'Y'     TEST PRODUCTION POSTING                      
         BNE   PB34A4                                                           
         MVC   BLPOST(6),3(RF)     SHOW AS 6/6                                  
         MVC   BLPOST+132(6),3+6(RF)                                            
         B     PB34B                                                            
*                                                                               
PB34A4   DS    0H                                                               
         MVC   BLPOST(7),3(RF)                                                  
         MVC   BLPOST+132(5),3+7(RF)      IF LONGER THAN 7                      
         CLI   RETAIL,C'Y'         AND RETAIL                                   
         BNE   PB34B                                                            
         CLI   RETPROF+12,C'0'     (OPT TO TREAT AS NOT RETAIL)                 
         BE    PB34B                                                            
         MVC   BLPOST(7),SPACES                                                 
         MVC   BLPOST(4),3(RF)     RETAIL AS 4/8                                
         MVC   BLPOST+132(8),3+4(RF)                                            
         MVI   BLPOST-2,C'R'                                                    
*                                                                               
PB34B    DS    0H                                                               
         CLI   MAAORSW,C'Y'        TEST AOR                                     
         BNE   PB34C                                                            
         MVI   BLPOST-2,C'A'       FLAG AS AOR                                  
         CLC   =C'SR',1(RF)        OK IF RECEIVABLE TYPE                        
         BE    PB34C                                                            
         MVC   BLPOST(8),1(RF)     ELSE SHOW FULL ACCOUNT                       
         MVC   BLPOST+132(6),9(RF)                                              
*                                                                               
PB34C    DS    0H                                                               
         MVC   BLPOST+9(8),INCCNTR+3                                            
         MVC   BLPOST+132+9(4),INCCNTR+11                                       
         MVC   BLPOST+18(8),COSTCLT+3                                           
         MVC   BLPOST+132+18(4),COSTCLT+11                                      
*                                  DO POSTING                                   
PB34D    DS    0H                                                               
*                                  SET MAPOSTD FIELDS                           
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         MVI   MAPCDSW,C'Y'        SET DUE IS LESS CD.                          
         MVC   MAPAC,BAC                                                        
         CLI   MAAORSW,C'Y'        FOR AOR BILLS                                
         BNE   PB34D2                                                           
         ICM   R0,15,BAORCOM       'AC' IS FROM ORIGINAL BILL                   
         LCR   R0,R0               COMPLEMENT                                   
         ST    R0,MAPAC                                                         
*                                                                               
PB34D2   DS    0H                                                               
         MVC   MAPCD,BCD                                                        
         MVC   MAPGRS,BGRS                                                      
         MVC   MAPACT,BACTUAL      USE WHOLE ACTUAL                             
         MVC   MAPARAMT,BACTUAL                                                 
         MVC   MAPNET,BNT                                                       
         MVC   MAPCD,BCD                                                        
         MVC   MAPINV,DINVNO                                                    
         L     RF,ADCLT                                                         
         MVC   MAPOFF,COFFICE-CKEY(RF)                                          
         CLI   PROGPROF+9,C'A'     TEST OVERRIDE OFF. NO.                       
         BL    *+10                (A-Z,1-9)                                    
         MVC   MAPOFF,PROGPROF+9                                                
         MVC   MAPDATE,BQDATE                                                   
*                                                                               
*              SET OFFICE FOR ACCOUNTING POSTINGS                               
         MVC   MAPACOFC,SPACES                                                  
         MVC   MAPACOFC(1),MAPOFF                                               
         CLI   CACCOFC-CKEY(RF),C' '    2 BYTE ACCOUNTING OFFICE                
         BNH   *+10                                                             
         MVC   MAPACOFC,CACCOFC-CKEY(RF)                                        
         OC    MAPACOFC,SPACES                                                  
         CLI   PROGPROF+9,C'A'     TEST OVERRIDE OFF. NO.                       
         BL    *+14                (A-Z,1-9)                                    
         MVC   MAPACOFC(1),PROGPROF+9   FOR ACC OFFICE ALSO                     
         MVI   MAPACOFC+1,C' '                                                  
*                                                                               
         MVC   MAPMOS,MAPDATE      MOS = BILL MONTH                             
*                                                                               
         CLI   PROGPROF+2,0        TEST ANY ADJUSTMENT                          
         BE    PB35                NO                                           
*                                                                               
         PACK  DUB,MAPDATE+4(2)                                                 
         CVB   R4,DUB                                                           
         STC   R4,BYTE                                                          
*                                                                               
         L     R4,=F'-30'          IF PROFILE 15 OR LESS                        
         CLI   PROGPROF+2,15       BILLS BEFORE THAT DATE                       
         BH    PB34G               GO TO PREVIOUS MONTH                         
         CLC   BYTE,PROGPROF+2                                                  
         BH    PB35                                                             
         B     PB34H                                                            
*                                                                               
PB34G    DS    0H                                                               
         LA    R4,30                      IF PROFILE 16 OR MORE                 
         CLC   BYTE,PROGPROF+2        BILLS AFTER THAT DATE                     
         BNH   PB35                       GO TO NEXT MONTH                      
*                                                                               
PB34H    DS    0H                                                               
         MVC   WORK(4),MAPDATE      ADJUST MONTH                                
         MVC   WORK+4(2),=C'15'                                                 
         GOTO1 ADDAY,DMCB,WORK,WORK,(R4)                                        
         MVC   MAPMOS,WORK                                                      
*                                                                               
PB35     DS    0H                                                               
         MVC   MAPSERV,BMONSERV    MEDIA MONTH OF SERVICE                       
         MVC   MAPMED,QMED                                                      
         MVC   MAPPROF,PROGPROF                                                 
         MVC   MAPPROA,PROGPROA                                                 
         MVC   MAPSYS,MASYS                                                     
         MVC   MAPEST,BLEST                                                     
         MVC   MAPCLT,CLT                                                       
         MVC   MAPCLTN,CLTNM                                                    
         MVC   MAPPRD,BLPRD                                                     
         MVC   MAPPRDN,PRDNM                                                    
         MVC   MAPBPER,BLPER                                                    
         MVI   MAPRTAIL,0                                                       
         CLC   BDATE,NEWRETDT      TREAT NEW RETAIL AS NORMAL BILL              
         BNL   *+10                                                             
         MVC   MAPRTAIL,BRETAIL    SET RETAIL TYPE                              
         GOTO1 DATCON,DMCB,(3,BDUEDATE),(2,MAPDUED)                             
         MVC   MAPAOR,MAAORSW                                                   
*                                                                               
         L     RF,ADEST                                                         
         MVC   MAPDESC,EDESC-ESTHDR(RF)                                         
         MVC   MAPNARR,SPACES                                                   
         CLI   PROGPROF+11,C'Y'                                                 
         BNE   PB35M                                                            
         MVC   MAPNARR(20),EDESC-ESTHDR(RF)                                     
*                                                                               
PB35M    DS    0H                                                               
         MVC   MAPSTATN,SPACES     STATION/NETWORK (OVERLAYS BLMGR)             
         CLI   BRETAIL,0           USE ONLY IF NON-RETAIL                       
         BNE   PB35P                                                            
         CLI   BLSTATN+1,C'0'      NUMERIC WOULD MEAN ITS A MGR                 
         BNL   PB35P               (TEST MGR, NOT SCHEME)                       
         CLI   BLSTATN+1,C' '      BUT TEST IF PRESENT                          
         BNH   PB35P                                                            
         MVC   MAPSTATN,BLSTATN     SET STATION/NETWORK                         
*                                                                               
PB35P    DS    0H                                                               
         DROP  R5                                                               
*                                                                               
PB36     DS    0H                  DO POSTING                                   
         OC    BILPOST,BILPOST   SKIP PREVIOUSLY POSTED                         
         BNZ   PB40                                                             
         CLI   TESTOPT,C'Y'        SKIP IF TESTING                              
         BE    PB40                                                             
         CLI   MAAORSW,C'E'        OR IF AOR ERROR                              
         BE    PB40                                                             
         L     RF,MALRPTR          OR IF ANY ACCOUNTS NOT OK                    
         CLC   0(15,RF),SPACES                                                  
         BE    PB40                                                             
         GOTO1 AMAPOST,DMCB,POSTWORK,MAOUT                                      
         MVI   POSTSW,C'Y'         SET HAVE POSTINGS                            
*                                                                               
*                                                                               
PB40     DS    0H                                                               
         OC    BILPOST,BILPOST   SKIP PREVIOUSLY POSTED                         
         BZ    PB40B                                                            
         CLI   PREVOPT,C'Y'      UNLESS ASKED FOR                               
         BE    PB40B                                                            
         MVC   P,SPACES                                                         
         B     PB98                                                             
PB40B    DS    0H                                                               
         BAS   RE,PRNT                                                          
*                                                                               
         LA    R0,6                CONVERT TO PACKED FOR TOTALING               
         LA    R4,BAMTSP                                                        
         LA    R3,BAMTS                                                         
*                                                                               
PB40D    DS    0H                                                               
         L     RF,0(R3)                                                         
         CVD   RF,DUB                                                           
         ZAP   0(6,R4),DUB                                                      
         LA    R3,4(R3)                                                         
         LA    R4,6(R4)                                                         
         BCT   R0,PB40D                                                         
*                                                                               
         LA    R4,PPREV            PREV TOTS                                    
         OC    BILPOST,BILPOST                                                  
         BNZ   PB42                                                             
         LA    R4,PNOT             NOT POSTEDS                                  
         L     RF,MALRPTR          POINTER TO ACTUAL RCVBL ACC                  
         CLC   0(15,RF),SPACES     TEST NOT POSTED                              
         BE    PB42                                                             
         CLI   MAAORSW,C'E'        OR AOR ERROR                                 
         BE    PB42                                                             
         LA    R4,PPOST            CURRENT POSTINGS                             
*                                                                               
PB42     DS    0H                                                               
         LA    R3,BAMTSP           ROLL TO TOTALS                               
         LA    R0,6                                                             
         BAS   RE,PBROLL2                                                       
*                                  MARK FILE                                    
         CLI   TESTOPT,C'Y'                                                     
         BE    PB98                                                             
         L     RF,MALRPTR          POINTER TO ACTUAL RCVBL ACC                  
         CLC   0(15,RF),SPACES     TEST POSTED                                  
         BE    PB98                                                             
*                                                                               
         MVC   KEY+14(4),SVBILDA   SET BILL DISK ADDR                           
         GOTO1 GETBILL             RE-READ BILL RECORD                          
         MVC   BILPOST,TODAYP      MARK BILL RECORD                             
         GOTO1 PUTBILL                                                          
*                                                                               
PB98     DS    0H                                                               
         LA    RF,SVPOST           SAVE PREVIOUS MAPOST                         
         LA    RE,POSTWORK                                                      
         LA    R1,L'SVPOST                                                      
         MOVE  ((RF),(R1)),(RE)                                                 
PB99     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
         SPACE 3                                                                
PBEDT    DS    0H                                                               
         EDIT  (R0),(14,0(R6)),2,COMMAS=YES,CR=YES                              
         BR    RE                                                               
         SPACE 2                                                                
PBROLL   DS    0H                                                               
         LA    R0,18                                                            
PBROLL2  DS    0H                                                               
         AP    0(6,R4),0(6,R3)                                                  
         LA    R3,6(R3)                                                         
         LA    R4,6(R4)                                                         
         BCT   R0,PBROLL2                                                       
         BR    RE                                                               
         EJECT                                                                  
TOTPRNT  NTR1                                                                   
         SPACE 2                                                                
         ST    R3,ATOTS                                                         
         LA    RF,3                                                             
         CLI   PREVOPT,C'Y'                                                     
         BNE   *+8                                                              
         LA    RF,4(RF)                                                         
         CLI   LISTOPT,C'Y'                                                     
         BNE   *+8                                                              
         LA    RF,3(RF)                                                         
         STC   RF,LNEED                                                         
*                                                                               
         BAS   RE,PRNT                                                          
         L     R4,ATOTS                                                         
         CLI   MODE,RUNLAST                                                     
         BE    TP3                                                              
         CLI   PREVOPT,C'Y'                                                     
         BE    TP4                                                              
TP3      DS    0H                                                               
         MVC   P+13(15),=C'POSTED THIS RUN'                                     
         BAS   RE,TPFMT                                                         
         LA    R4,TOTL(R4)                                                      
         CLI   MODE,RUNLAST                                                     
         BNE   TP6                                                              
         MVC   P+13(17),=C'PREVIOUSLY POSTED'                                   
         B     TP5                                                              
TP4      DS    0H                                                               
         MVC   P+13(12),=C'TOTAL POSTED'                                        
         LA    R4,TOTL(R4)                                                      
TP5      DS    0H                                                               
         BAS   RE,TPFMT                                                         
TP6      DS    0H                                                               
         CLI   MODE,RUNLAST                                                     
         BE    TP7                                                              
         CLI   LISTOPT,C'Y'                                                     
         BNE   TP10                                                             
TP7      DS    0H                                                               
         LA    R4,TOTL(R4)                                                      
         MVC   P+13(10),=C'NOT POSTED'                                          
         BAS   RE,TPFMT                                                         
*                                                                               
TP10     DS    0H                                                               
         BAS   RE,PRNT                                                          
         B     EXIT                                                             
         SPACE 3                                                                
TPFMT    NTR1                                                                   
         SPACE 2                                                                
         LA    R7,P                                                             
         USING BLINED,R7                                                        
         LA    R1,24(R4)                                                        
         LA    R6,BLACTUAL                                                      
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R1,30(R4)                                                        
         LA    R6,BLNET                                                         
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R1,12(R4)                                                        
         LA    R6,BLAC                                                          
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R1,0(R4)                                                         
         LA    R6,BLARAMT                                                       
         BAS   RE,TPEDT                                                         
*                                                                               
         BAS   RE,PRNT                                                          
         B     EXIT                                                             
         SPACE 3                                                                
TPEDT    DS    0H                                                               
         EDIT  (P6,0(R1)),(16,WORK),2,COMMAS=YES,CR=YES                         
         LR    RF,R6                                                            
         SH    RF,=H'3'                                                         
         CLI   WORK,C' '                                                        
         BNE   TPEDT6                                                           
         CLI   WORK+1,C' '                                                      
         BE    TPEDT2                                                           
         CLI   1(RF),C' '                                                       
         BNE   TPEDT6                                                           
TPEDT2   DS    0H                                                               
         CLI   WORK+3,C' '                                                      
         BE    TPEDT4                                                           
         CLI   2(RF),C' '                                                       
         BNE   TPEDT6                                                           
TPEDT4   DS    0H                                                               
         OC    1(16,RF),WORK                                                    
         BR    RE                                                               
TPEDT6   DS    0H                                                               
         MVC   133(16,RF),WORK                                                  
         BR    RE                                                               
         EJECT                                                                  
*                             PRINTING                                          
         SPACE 2                                                                
PRNT     NTR1                                                                   
         SPACE 2                                                                
         CLI   FORCEHED,C'Y'                                                    
         BE    PRNT1                                                            
         ZIC   RE,LNEED                                                         
         ZIC   RF,LINE                                                          
         AR    RE,RF                                                            
         STC   RE,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BL    PRNT2                                                            
*                                  NEW PAGE                                     
         MVI   FORCEHED,C'Y'                                                    
PRNT1    DS    0H                                                               
         LA    R4,HEAD4                                                         
         CLI   TESTOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(15,R4),=C'** TEST MODE **'                                     
         LA    R4,132(R4)                                                       
         CLI   LISTOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(29,R4),=C'** UNPOSTED BILLS INCLUDED **'                       
         LA    R4,132(R4)                                                       
         CLI   PREVOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(32,R4),=C'** PREVIOUS POSTINGS INCLUDED **'                    
         LA    R4,132(R4)                                                       
*                                                                               
         MVC   WORK(4),QPROG+29                                                 
         CLI   WORK,C' '                                                        
         BE    PRNT1B                                                           
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,WORK,(6,HEAD5+77)                                    
         MVC   HEAD5+47(29),=C'ACCOUNTING MONTH OF SERVICE ='                   
PRNT1B   DS    0H                                                               
*                                                                               
PRNT2    DS    0H                                                               
         MVC   HEAD1+52(7),=C'SPOTPAK'                                          
         MVC   HEAD2+52(8),=C'--------'                                         
         CLI   NETPSW,C'Y'                                                      
         BNE   *+14                                                             
         MVC   HEAD1+52(7),=C' NETPAK'                                          
         MVI   HEAD2+52,C' '                                                    
         GOTO1 REPORT                                                           
         B     EXIT                                                             
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
NEXTEL2  DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
         DS    0D                                                               
MAOUT    DS    CL2000                                                           
         SPACE 2                                                                
WRKRBUFF CSECT                                                                  
         DS    4096X                                                            
         SPACE 3                                                                
BLINED   DSECT                                                                  
BLINE    DS    0CL132                                                           
BLPRD    DS    CL3                                                              
         DS    CL1                                                              
BLEST    DS    CL3                                                              
         DS    CL1                                                              
BLPER    DS    CL6                                                              
         DS    CL1                                                              
BLINVD   DS    CL8                                                              
         DS    CL1                                                              
BLINVNO  DS    CL7                                                              
BLACTUAL DS    CL14                                                             
BLNET    DS    CL14                                                             
BLAC     DS    CL14                                                             
BLARAMT  DS    CL14                                                             
BLCD     DS    CL14                                                             
         DS    CL2                                                              
BLPOST   DS    CL29                                                             
         SPACE 2                                                                
         EJECT                                                                  
SPBAWRKD DSECT                                                                  
TODAYD   DS    CL8                                                              
BIGNDTE  DS    XL2                                                              
LNEED    DS    X                                                                
SAVSYS   DS    X                                                                
ELCODE   DS    X                                                                
POSTSW   DS    C                                                                
WID      DS    CL16                                                             
TESTOPT  DS    C                                                                
LISTOPT  DS    C                                                                
PREVOPT  DS    C                                                                
DMPOPT   DS    C                                                                
RETAIL   DS    C                                                                
RERUNSW  DS    C                                                                
ATOTS    DS    A                                                                
AMALINK  DS    A                                                                
AMAPOST  DS    V                                                                
RELO     DS    A                                                                
PROGPROA DS    XL16                                                             
PROGPROB DS    XL16                                                             
SVJOB    DS    CL6                                                              
SVOPREB  DS    F                                                                
SVCNACT  DS    F                                                                
SVAORKEY DS    CL13                                                             
B1XPROF  DS    XL16                                                             
DINVNO   DS    CL6                                                              
SVBILDA  DS    XL4                                                              
SVCOMP   DS    X                                                                
NETPSW   DS    C                                                                
X        DS    XL256                                                            
*                                                                               
AMTS     DS    0F                                                               
*                                                                               
BAMTS    DS    0CL24                                                            
BARAMT   DS    F                                                                
BGRS     DS    F                                                                
BAC      DS    F                                                                
BCD      DS    F                                                                
BACT     DS    F                                                                
BNT      DS    F                                                                
*                                                                               
TOTL     EQU   36                                                               
BAMTSP   DS    CL(TOTL)                                                         
ZEROAMTS DS    CL(TOTL*3)                                                       
*                                                                               
PAMTS    DS    0CL(TOTL*3)                                                      
PPOST    DS    CL(TOTL)                                                         
PPREV    DS    CL(TOTL)                                                         
PNOT     DS    CL(TOTL)                                                         
*                                                                               
CAMTS    DS    0CL(TOTL*3)                                                      
CPOST    DS    CL(TOTL)                                                         
CPREV    DS    CL(TOTL)                                                         
CNOT     DS    CL(TOTL)                                                         
*                                                                               
RQAMTS   DS    0CL(TOTL*3)                                                      
RQPOST   DS    CL(TOTL)                                                         
RQPREV   DS    CL(TOTL)                                                         
RQNOT    DS    CL(TOTL)                                                         
*                                                                               
RAMTS    DS    0CL(TOTL*3)                                                      
RPOST    DS    CL(TOTL)                                                         
RPREV    DS    CL(TOTL)                                                         
RNOT     DS    CL(TOTL)                                                         
AMTSX    EQU   *                                                                
*                                                                               
*              NOTE- THESE 132 BYTES ARE PASSED TO MALINK. ANY CHANGES          
*                    HERE MUST BE REFLECTED THERE.                              
MAIN     DS    0CL132                                                           
MASYS    DS    CL1                                                              
MAAGY    DS    CL2                                                              
MAMED    DS    CL1                                                              
MACLT    DS    CL3                                                              
MAPRD    DS    CL3                                                              
MAJOB    DS    CL6                                                              
MAOFF    DS    CL1                                                              
MARETYP  DS    XL1                                                              
MARET    DS    CL12                                                             
MAOTHER  DS    CL20                                                             
MABLTYP  DS    CL1                                                              
MAAORRCV DS    CL14                                                             
MAAORCOM DS    CL14                                                             
MAAORSW  DS    CL1                                                              
MAPROA   DS    XL16                                                             
MAFINAN  DS    CL1                                                              
MASAVSE  DS    CL1                                                              
MAACCSE  DS    CL1                                                              
         DS    CL1                                                              
MAUTL    DS    F                                                                
MAACOFF  DS    CL2                                                              
MAPROB   DS    XL16                                                             
         DS    CL10                                                             
*                                                                               
         DS    0F                                                               
MAPARS   DS    0CL24                                                            
         DS    F                                                                
MASTAT   DS    X                                                                
         DS    XL3                                                              
         DS    4F                                                               
*                                                                               
POSTWORK DS    XL(MAPEND-MAPOSTD)                                               
SVPOST   DS    XL(MAPEND-MAPOSTD)                                               
*                                                                               
         SPACE 2                                                                
*                                                                               
       ++INCLUDE MALINKD                                                        
         SPACE 2                                                                
*                                                                               
       ++INCLUDE MAPOSTD                                                        
         SPACE 2                                                                
*                                                                               
       ++INCLUDE DMWRKRK                                                        
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENAGY                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE ACGENBOTH                                                      
*                                                                               
         PRINT ON                                                               
       ++INCLUDE SPGENBILL                                                      
*                                                                               
       ++INCLUDE SPGENAOR                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023SPAORTST  05/01/02'                                      
         END                                                                    
