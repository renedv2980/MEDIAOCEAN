*          DATA SET DDCARDTOLD AT LEVEL 003 AS OF 05/01/02                      
*CATALP CARDTOLD                                                                
         SPACE 2                                                                
*INPUT CARD LAYOUT                                                              
*                                                                               
*COLUMN  DATA                                                                   
*------  ---------------------                                                  
*1       R=START OF NEW RECORD                                                  
*3       C=CHAR FIELD (SPACES FILL),X=HEX FIELD (ZERO FILL)                     
*5-7     LENGTH OF FIELD (1-255)                                                
*10-80   DATA                                                                   
         TITLE 'CREATES LOAD TAPE FROM CARDS'                                   
         PRINT NOGEN                                                            
CARDTOLD CSECT                                                                  
         NBASE 0,CARDTOLD,=V(REGSAVE)                                           
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE(17),=C'CARD TO LOAD TAPE'                                  
         MVC   SUB1(10),=C'INPUT CARD'                                          
         MVC   SUB1+90(6),=C'ERRORS'                                            
         MVC   SUB2(10),=C'----------'                                          
         MVC   SUB2+90(6),=C'------'                                            
         LA    R8,IO+4                                                          
         LA    R9,LOAD                                                          
*&&DO*&& OPEN  (9)                                                              
*&&OS*&& OPEN  ((9),OUTPUT)                                                     
         LA    R7,4                                                             
         EJECT                                                                  
*READ CARD LOOP                                                                 
*                                                                               
READ     CLI   READMODE,C'P'                                                    
         BE    READPAN                                                          
         GOTO1 =V(CARDS),PARA,P,=C'RE00'                                        
         CLI   P,C'P'                                                           
         BNE   READ1                                                            
         MVI   READMODE,C'P'                                                    
         MVC   PANBOOK,P+1                                                      
         B     READPAN                                                          
         SPACE 2                                                                
READ1    DS    0H                                                               
         CLC   P(2),=C'/*'                                                      
         BNE   READ2                                                            
         BAS   RE,WRITEREC                                                      
*&&DO*&& CLOSE (9)                                                              
*&&OS*&& CLOSE ((9))                                                            
         XBASE                                                                  
         SPACE 2                                                                
READPAN  GOTO1 =V(PANIC),PARA,(X'80',=C'READ'),=C'PAN',PANBOOK,P                
         CLI   PARA+8,0                                                         
         BE    READ2                                                            
         MVI   READMODE,C'C'                                                    
         B     READ                                                             
         SPACE 2                                                                
READ2    CLI   P,C'L'                                                           
         BE    LENGTH                                                           
         CLI   P,C'*'                                                           
         BE    ERROR                                                            
         CLI   P,C'R'                                                           
         BNE   FIELD                                                            
         CH    R7,=H'4'                                                         
         BE    FIELD                                                            
         BAS   RE,WRITEREC                                                      
         LA    R7,4                                                             
         LA    R8,IO+4                                                          
         SPACE 2                                                                
FIELD    MVC   P+90(14),=C'INVALID LENGTH'                                      
         CLI   P+6,C'9'                                                         
         BH    ERROR                                                            
         CLI   P+6,C'0'                                                         
         BL    ERROR                                                            
         CLC   P+4(3),=C'255'                                                   
         BH    ERROR                                                            
         PACK  DUB,P+4(3)                                                       
         CVB   R2,DUB                                                           
         LR    R3,R2                                                            
         BCTR  R3,R0                                                            
         LTR   R2,R2                                                            
         BZ    ERROR                                                            
         CLI   P+2,C'X'                                                         
         BE    HEXFIELD                                                         
         CLI   P+2,C'B'                                                         
         BE    BINFIELD                                                         
         MVI   0(R8),C' '          CHARACTER FIELD CLEARED TO BLANKS            
         LTR   R3,R3                                                            
         BZ    FIELD2                                                           
         BCTR  R3,R0                                                            
         EX    R3,FILL                                                          
         LA    R3,1(R3)                                                         
         CH    R3,=H'62'                                                        
         BNH   *+8                                                              
         LA    R3,62                                                            
         SPACE 2                                                                
FIELD2   EX    R3,MOVE                                                          
         SPACE 2                                                                
FIELDOK  MVC   P+90(30),SPACES                                                  
         GOTO1 =V(PRINTER)                                                      
         AR    R7,R2                                                            
         AR    R8,R2                                                            
         B     READ                                                             
         EJECT                                                                  
*PROCESSING LENGTH CARDS                                                        
*                                                                               
LENGTH   OI    P+6,X'F0'                                                        
         PACK  DUB,P+4(3)                                                       
         CVB   R2,DUB                                                           
         ST    R2,DISP                                                          
         B     ERROR                                                            
         SPACE 2                                                                
*PROCESSING OF BINARY FIELDS                                                    
*                                                                               
BINFIELD MVC   P+90(30),=CL30'INVALID BINARY FIELD'                             
         CH    R2,=H'4'                                                         
         BH    ERROR                                                            
         LA    R4,P+9                                                           
         SR    R5,R5                                                            
         SPACE 2                                                                
BINLOOP  CLI   0(R4),C' '                                                       
         BE    BINEND                                                           
         CLI   0(R4),C'0'                                                       
         BL    ERROR                                                            
         CLI   0(R4),C'9'                                                       
         BH    ERROR                                                            
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         B     BINLOOP                                                          
         SPACE 2                                                                
BINEND   LTR   R5,R5                                                            
         BZ    ERROR                                                            
         CH    R5,=H'10'                                                        
         BH    ERROR                                                            
         BCTR  R5,R0                                                            
         EX    R5,DATAPACK                                                      
         CVB   R6,DUB                                                           
         ST    R6,DUB                                                           
         MVC   0(1,R8),DUB+3                                                    
         CH    R2,=H'1'                                                         
         BE    FIELDOK                                                          
         MVC   0(2,R8),DUB+2                                                    
         CH    R2,=H'2'                                                         
         BE    FIELDOK                                                          
         MVC   0(3,R8),DUB+1                                                    
         CH    R2,=H'3'                                                         
         BE    FIELDOK                                                          
         MVC   0(4,R8),DUB                                                      
         B     FIELDOK                                                          
         SPACE 2                                                                
DATAPACK PACK  DUB,P+9(0)                                                       
         EJECT                                                                  
*HANDLING HEXADECIMAL FIELDS                                                    
*                                                                               
HEXFIELD EX    R3,CLEAR                                                         
         LA    R4,P+9                                                           
         SR    R5,R5                                                            
         CLI   0(R4),C' '                                                       
         BE    FIELDOK                                                          
HEXLOOP  CLI   0(R4),C' '          SCAN FOR SPACE DELIMITER                     
         BE    HEXFOUND                                                         
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         B     HEXLOOP                                                          
         SPACE 2                                                                
HEXFOUND GOTO1 =V(HEXIN),PARA,P+9,(R8),(R5)                                     
         MVC   P+90(17),=C'INVALID HEX FIELD'                                   
         OC    PARA+12(4),PARA+12                                               
         BZ    ERROR                                                            
         B     FIELDOK                                                          
         SPACE 2                                                                
CLEAR    XC    0(0,R8),0(R8)                                                    
FILL     MVC   1(0,R8),0(R8)                                                    
MOVE     MVC   0(0,R8),P+9                                                      
         SPACE 2                                                                
ERROR    GOTO1 =V(PRINTER)                                                      
         B     READ                                                             
         EJECT                                                                  
*WRITE COMPLETED RECORDS                                                        
*                                                                               
WRITEREC NTR1                                                                   
         STH   R7,IO                                                            
         LA    R6,IO                                                            
         L     R2,DISP             OPTIONAL LENGTH FIELD                        
         SH    R7,=H'4'                                                         
         STH   R7,DUB                                                           
         LA    R2,3(R2,R6)                                                      
         OC    DISP,DISP                                                        
         BZ    *+10                                                             
         MVC   0(2,R2),DUB                                                      
         PUT   (R9),(R6)                                                        
         XIT1                                                                   
         EJECT                                                                  
*DTF/DCB FOR LOAD TAPE                                                          
*                                                                               
*&&DO                                                                           
LOAD     DTFMT DEVADDR=SYS007,IOAREA1=LOADA,FILABL=NO,RECFORM=VARBLK,  X        
               REWIND=UNLOAD,TYPEFLE=OUTPUT,BLKSIZE=8200,WORKA=YES,    X        
               IOAREA2=LOADA2                                                   
*&&                                                                             
         SPACE 2                                                                
*&&OS                                                                           
LOAD     DCB   DDNAME=LOAD,DSORG=PS,MACRF=(PM),                        X        
               RECFM=VB,BLKSIZE=8200,LRECL=4004,BUFNO=2                         
*&&                                                                             
         SPACE 2                                                                
DUB      DS    D                                                                
PARA     DS    6F                                                               
DISP     DC    F'0'                                                             
READMODE DC    C'C'                                                             
PANBOOK  DC    CL12' '                                                          
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
IO       DC    F'0'                                                             
         DS    2000C                                                            
*                                                                               
LOADA    DS    8208C                                                            
LOADA2   DS    8208C                                                            
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDCARDTOLD05/01/02'                                      
         END                                                                    
