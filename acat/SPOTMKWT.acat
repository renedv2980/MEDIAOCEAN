*          DATA SET SPOTMKWT   AT LEVEL 025 AS OF 03/21/17                      
*CATALP SPOTMKWT                                                                
         SPACE 2                                                                
********************************************************************            
*                                                                  *            
* SPOTMKWT                                                         *            
* --------                                                         *            
*                                                                  *            
* ROUTINE TO GET A MARKET WEIGHT                                   *            
*                                                                  *            
* INPUT  : SBQMKTWT = N  NO MARKET WEIGHTING                       *            
*                     Y  REGULAR MARKET WEIGHTING                  *            
*                     D  PRIMARY DEMO MARKET WEIGHTING             *            
*          SBMKTREC = MARKET RECORD                                *            
*          SBAMWTAB = A(MARKET WEIGHT TABLE) IF SBQMKTWT=D         *            
*                     TABLE AS DEFINED BY MWTABD IN SPOTBLOCK      *            
*                     (SBBPRD MUST BE SET TO CURRENT PRODUCT CODE  *            
*                      IF SBQPRD NOT POL, AND SBBEST MUST BE SET   *            
*                      TO CURRENT ESTIMATE NUMBER)                 *            
*                                                                  *            
* OUTPUT : SBMKTWGT = MARKET WEIGHT, OR IF NOT FOUND, ZERO         *            
*                                                                  *            
********************************************************************            
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-6939   02/28/17 SUPPORT COMSCORE DEMO LOOKUPS FOR 17.1.5  *         
***********************************************************************         
         TITLE 'SPOTMKWT - SPOTPAK GET MARKET WEIGHT ROUTINE'                   
SPOTMKWT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**SPMW**,RR=RE,CLEAR=YES                             
         USING WORKD,RC            RC = A(LOCAL WORKING STORAGE)                
         L     RA,0(R1)                                                         
         USING SBLOCKD,RA          RA = A(SPOTBLOCK)                            
         L     R9,SBCOMFAC         R9 = A(COMFACS)                              
         USING COMFACSD,R9                                                      
         STM   R9,RC,SAVER9C                                                    
*                                                                               
         XC    SBMKTWGT,SBMKTWGT                                                
         CLI   SBQMKTWT,C'Y'       CHECK MARKET WEIGHTING REQUIRED              
         BE    *+12                                                             
         CLI   SBQMKTWT,C'D'                                                    
         BNE   SMX                                                              
         CLI   SBQMKTWT,C'D'       TEST PRIMARY DEMO WEIGHTING                  
         BE    SM10                                                             
         LA    R2,SBMKTREC         NO - GET WEIGHT FROM MARKET RECORD           
         USING MKTRECD,R2                                                       
         CLC   MKTWT,=C'    '      CHECK FOR ZERO WEIGHT                        
         BNH   SMX                                                              
         PACK  DUB,MKTWT                                                        
         CVB   R0,DUB                                                           
         STCM  R0,15,SBMKTWGT      SET MARKET WEIGHT                            
         B     SMX                                                              
*                                                                               
SM10     OC    SBAMWTAB,SBAMWTAB   CHECK TABLE EXISTS                           
         BZ    SMX                                                              
         BAS   RE,GETMKTWT         YES                                          
*                                                                               
SMX      B     XIT                                                              
*                                                                               
XIT      XIT1  ,                                                                
         EJECT                                                                  
* GET PRIMARY DEMO MARKET WEIGHT                                                
*                                                                               
GETMKTWT NTR1                                                                   
         MVI   RTGSVC,C'1'         SET THE RATING SERVICE                       
         CLI   SBCPROF+3,C'1'      CHECK THE CLIENT PROFILE                     
         BE    *+8                                                              
         MVI   RTGSVC,C'0'                                                      
         LA    R4,SBPDEMOS         R4 = A(PRIMARY DEMO)                         
         ICM   R1,15,SBAESTTB      TEST FOR PRODUCT/ESTIMATE TABLE              
         BZ    MW10                                                             
         LA    RE,255              YES - FIND ENTRY FOR THIS PRD/EST            
         CLC   SBQPRD,=C'POL'                                                   
         BE    *+8                                                              
         IC    RE,SBBPRD                                                        
         BCTR  RE,0                                                             
         SLL   RE,8                                                             
         SR    RF,RF                                                            
         ICM   RF,1,SBBEST                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  RF,0                                                             
         AR    RE,RF                                                            
         LA    R1,0(R1,RE)                                                      
         SR    RE,RE                                                            
         ICM   RE,1,0(R1)          POINTER TO ESTIMATE BUFFER                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  RE,0                                                             
         MHI   RE,ESTBUFFL                                                      
         ICM   R1,15,SBAESTBF                                                   
         BZ    MW10                                                             
         LA    RE,0(R1,RE)                                                      
         LA    R4,EBDEMOS-ESTBUFFD(RE)                                          
*                                                                               
MW10     MVC   DEMO,0(R4)          SET PRIMARY DEMO                             
         CLI   DEMO+2,0            COMSCORE DEMO?                               
         BNE   *+8                 NO                                           
         MVI   DEMO+2,1            YES - SET FOR HOMES LOOKUP                   
         MVI   DEMO+1,C'U'         FORCE FOR UNIVERSE LOOKUP                    
         LA    R0,MWTENTS          SEARCH MARKET WEIGHT TAB FOR ENTRY           
         L     R5,SBAMWTAB                                                      
         USING MWTABD,R5                                                        
*                                                                               
MW12     OC    MWTDEM,MWTDEM                                                    
         BZ    MW20                NOT FOUND                                    
         CLC   MWTSVC,RTGSVC                                                    
         BNE   *+14                                                             
         CLC   MWTDEM,DEMO                                                      
         BE    MW30                FOUND                                        
         LA    R5,MWTABL(R5)                                                    
         BCT   R0,MW12                                                          
*                                                                               
         L     RE,SBALMWEN         TABLE FULL                                   
         LA    RE,MWTABL(RE)       RE=A(OLDEST ENTRY)                           
         CR    RE,R5               TEST BEYOND END OF TABLE                     
         BL    *+8                                                              
         L     RE,SBAMWTAB         YES-START AT FIRST ENTRY AGAIN               
         LR    R5,RE                                                            
         LA    RF,MWTABL           CLEAR THE ENTRY                              
         XCEF                                                                   
*                                                                               
MW20     MVC   MWTSVC,RTGSVC       NEW TABLE ENTRY                              
         MVC   MWTDEM,DEMO                                                      
         ST    R5,SBALMWEN         SAVE A(MOST RECENT TABLE ENTRY)              
         ST    R5,SVAMWTEN                                                      
         BAS   RE,RDUNIV           READ MARKET UNIVERSES                        
         MVC   MWTUSPOP,USTOTAL    SET US POPULATION                            
         OC    USTOTAL,USTOTAL                                                  
         BNZ   MW30                                                             
         MVC   MWTUSPOP,=F'1'                                                   
*                                                                               
MW30     XC    SBMKTWGT,SBMKTWGT                                                
         MVC   USTOTAL,MWTUSPOP                                                 
         LA    R2,SBMKTREC                                                      
         USING MKTRECD,R2                                                       
         LA    RE,MKTRSM1          DETERMINE RATING SERVICE MKT NUM             
         CLC   MKTRS1,RTGSVC                                                    
         BE    MW32                                                             
         LA    RE,MKTRSM2                                                       
         CLC   MKTRS2,RTGSVC                                                    
         BNE   MWX                                                              
         DROP  R2                                                               
*                                                                               
MW32     LA    R0,MWTNMKT          LOOK FOR MARKET IN TABLE                     
         CLC   MWTMKT,0(RE)                                                     
         BE    MW34                                                             
         LA    R5,L'MWTMKENT(R5)                                                
         BCT   R0,*-14                                                          
         B     MWX                                                              
*                                                                               
MW34     ICM   RF,15,MWTMKUNV      TEST ZERO UNIVERSE                           
         BZ    MWX                                                              
         TM    MWTMKUNV,MWTMKWGT   TEST MARKET WEIGHT CALCULATED                
         BO    MW40                                                             
         OI    MWTMKUNV,MWTMKWGT                                                
         SR    RE,RE               NO - CONVERT TO PCT OF US POP                
         M     RE,=F'20000'                                                     
         D     RE,USTOTAL                                                       
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
***      LTR   RF,RF               MWTMKWGT NOW SET & MWTMKUNV                  
***      BZ    MWX                 CAN BE NON-ZERO!                             
         STCM  RF,7,MWTMKUNV+1     SAVE WEIGHT IN TABLE                         
*                                                                               
MW40     STCM  RF,7,SBMKTWGT+1     SET MARKET WEIGHT                            
*                                                                               
MWX      B     XIT                                                              
         EJECT                                                                  
* READ ALL MARKET UNIVERSES                                                     
*                                                                               
RDUNIV   NTR1                                                                   
         MVI   BOOK+1,11           ALWAYS USE NOVEMBER BOOK                     
         GOTO1 CDATCON,DMCB,(5,0),(3,FULL)   OF PRIOR YEAR                      
         LLC   R5,FULL                                                          
         BCTR  R5,0                                                             
         CLI   FULL+1,1            TWO YEARS BACK FOR JANUARY                   
         BNE   *+8                                                              
         BCTR  R5,0                                                             
         LTR   R5,R5                                                            
         BNM   *+8                                                              
         AHI   R5,100                                                           
         STC   R5,BOOK                                                          
*                                                                               
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBGETMKB                                                 
         CLI   SBQMED,C'T'         MEDIA MUST BE T                              
         BNE   RUX                                                              
         MVI   DBSELMED,C'T'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELAGY,SBQAGY                                                  
         CLI   RTGSVC,C'0'                                                      
         BE    *+8                                                              
         MVI   DBSELSRC,C'A'                                                    
         MVC   DBSELBK,BOOK                                                     
         MVC   DBCOMFCS,SBCOMFAC                                                
         LA    RE,DEMREC          READ MARKETS AND SAVE THE NUMBERS             
         ST    RE,DBAREC                                                        
         GOTO1 CDEMAND,DMCB,DBLOCK,SVMRKT                                       
*                                                                               
* FOR THE NOV/05 & NOV/12 BOOK ONLY, ADD A FEW EXTRA MKTS TO THE TABLE          
* BECAUSE THEY DON'T EXIST IN THE NSI DATA (HURRICANES)                         
*                                                                               
         CLC   BOOK,=X'690B'       NOV/05 BOOK?                                 
         BE    *+14                YES                                          
         CLC   BOOK,=X'700B'       NOV/12 BOOK?                                 
         BNE   RU5                 NO                                           
*                                                                               
         L     R5,SVAMWTEN         POINT TO MARKET WEIGHT TABLE ENTRY           
         USING MWTABD,R5                                                        
         LA    R0,MWTNMKT                                                       
*                                                                               
RU1      OC    MWTMKT,MWTMKT       SAVE MARKET NUMBERS                          
         BZ    RU2                                                              
         LA    R5,L'MWTMKENT(R5)                                                
         BCT   R0,RU1                                                           
         DC    H'0'                NEED ROOM FOR MORE MARKETS                   
*                                                                               
RU2      CLC   BOOK,=X'690B'       NOV/05 BOOK?                                 
         BE    RU3                 YES                                          
         MVC   MWTMKT,=H'101'      ADD NEW YORK                                 
         B     RU5                 DONE                                         
*                                                                               
RU3      CHI   R0,4                NEED TO ADD 4 MARKETS                        
         BNL   *+6                                                              
         DC    H'0'                                                             
         MVC   MWTMKT,=H'128'      ADD MIAMI                                    
         LA    R5,L'MWTMKENT(R5)                                                
         MVC   MWTMKT,=H'148'      AND WEST PALM BEACH                          
         LA    R5,L'MWTMKENT(R5)                                                
         MVC   MWTMKT,=H'222'      AND NEW ORLEANS                              
         LA    R5,L'MWTMKENT(R5)                                                
         MVC   MWTMKT,=H'346'      AND BILOXI                                   
         DROP  R5                                                               
*                                                                               
RU5      L     R5,SVAMWTEN         READ THE UNIVERSES                           
         USING MWTABD,R5                                                        
         XC    USTOTAL,USTOTAL                                                  
*                                                                               
RU10     OC    MWTMKT,MWTMKT                                                    
         BZ    RUX                                                              
         ST    R5,SVAMWTMK         SAVE TABLE ADDRESS                           
         XC    DBLOCK,DBLOCK       SET UP TO READ MARKETS                       
         MVC   DBFILE,=C'TP '                                                   
         CLI   SBQMED,C'T'                                                      
         BNE   RUX                                                              
         MVI   DBSELMED,C'T'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELAGY,SBQAGY                                                  
         CLI   RTGSVC,C'0'                                                      
         BE    *+8                                                              
         MVI   DBSELSRC,C'A'                                                    
         MVC   DBSELBK,BOOK                                                     
         MVC   DBCOMFCS,SBCOMFAC                                                
         LA    RE,DEMREC          READ MARKETS AND SAVE THE NUMBERS             
         ST    RE,DBAREC                                                        
         MVC   DBSELRMK,MWTMKT                                                  
*                                                                               
         CLC   DBSELBK,=X'700B'    NOV/12 BOOK?                                 
         BNE   RU15                NO                                           
         CLC   DBSELRMK,=H'101'    NEW YORK MARKET?                             
         BNE   RU15                NO                                           
         MVC   DBSELBK,=X'700A'    YES - USE THE OCT/12 BOOK INSTEAD            
*                                                                               
* FOR THE FOLLOWING MARKETS, SUBSTITUTE THE NOV/04 DATA FOR NOV/05              
*                                                                               
RU15     CLC   DBSELBK,=X'690B'    NOV/05 BOOK?                                 
         BNE   RU30                                                             
         CLC   DBSELRMK,=H'128'    IF MARKET IS MIAMI                           
         BE    RU20                                                             
         CLC   DBSELRMK,=H'148'    OR WEST PALM BEACH                           
         BE    RU20                                                             
         CLC   DBSELRMK,=H'346'    OR BILOXI                                    
         BE    RU20                                                             
         CLC   DBSELRMK,=H'222'    OR NEW ORLEANS                               
         BNE   RU30                                                             
RU20     MVC   DBSELBK,=X'680B'    USE THE NOV/04 BOOK INSTEAD                  
*                                                                               
RU30     MVI   DBSELDAY,X'40'      SET TO READ MON 5-515P                       
         MVC   DBSELTIM(2),=H'1700'                                             
         MVC   DBSELTIM+2(2),=H'1715'                                           
         MVI   DBFUNCT,DBGETTOT                                                 
         GOTO1 CDEMAND,DMCB,DBLOCK,SVUNIV                                       
         LA    R5,L'MWTMKENT(R5)                                                
         B     RU10                                                             
*                                                                               
RUX      B     XIT                                                              
         EJECT                                                                  
* SAVE MARKET NUMBER FROM RATING SERVICE MARKET RECORD                          
*                                                                               
         DS    0D                                                               
         DROP  RB                                                               
         USING *,RF                                                             
SVMRKT   NTR1  BASE=SAVERB                                                      
         LM    R9,RC,SAVER9C                                                    
         DROP  RF                                                               
         USING SPOTMKWT,RB                                                      
         L     R4,DBAREC                                                        
         USING DMKEY,R4                                                         
         OC    DMRMKT,DMRMKT                                                    
         BZ    SVX                                                              
         L     R5,SVAMWTEN         POINT TO MARKET WEIGHT TABLE ENTRY           
         USING MWTABD,R5                                                        
         LA    R0,MWTNMKT                                                       
*                                                                               
SV10     OC    MWTMKT,MWTMKT       SAVE MARKET NUMBERS                          
         BZ    SV20                                                             
         CLC   DMRMKT,MWTMKT       CHECK FOR DUPLICATE                          
         BE    SVX                                                              
         LA    R5,L'MWTMKENT(R5)                                                
         BCT   R0,SV10                                                          
         DC    H'0'                NEED ROOM FOR MORE MARKETS                   
*                                                                               
SV20     MVC   MWTMKT,DMRMKT                                                    
*                                                                               
SVX      B     XIT                                                              
         EJECT                                                                  
* SAVE UNIVERSE IN MARKET TABLE                                                 
*                                                                               
         DS    0D                                                               
         DROP  RB                                                               
         USING *,RF                                                             
SVUNIV   NTR1  BASE=SAVERB                                                      
         LM    R9,RC,SAVER9C                                                    
         DROP  RF                                                               
         USING SPOTMKWT,RB                                                      
         GOTO1 CDEMOUT,DMCB,(C'D',DEMO),DBLOCK,FULL                             
         L     R5,SVAMWTMK         SAVE UNIVERSE IN MARKET TABLE                
         USING MWTABD,R5                                                        
         MVC   MWTMKUNV,FULL                                                    
*                                                                               
         CLI   DBSELSRC,C'N'       FOR NSI                                      
         BNE   SVUNIV4                                                          
         LA    RE,SMATAB           DON'T ADD IN SMA MARKETS                     
*                                                                               
SVUNIV2  CLI   0(RE),X'FF'                                                      
         BE    SVUNIV4                                                          
         CLC   0(2,RE),DBSELRMK                                                 
         BE    SVUNIVX                                                          
         LA    RE,2(RE)                                                         
         B     SVUNIV2                                                          
*                                                                               
SVUNIV4  L     R1,FULL             DO US TOTAL                                  
         A     R1,USTOTAL                                                       
         ST    R1,USTOTAL                                                       
*                                                                               
SVUNIVX  B     XIT                                                              
         SPACE 2                                                                
SMATAB   DC    H'185'              AKRON                                        
         DC    H'199'              SARASOTA                                     
         DC    H'165'              ELMIRA                                       
         DC    H'260'              ANNISTON                                     
         DC    H'226'              VICTORIA                                     
         DC    H'342'              ENSIGN-GARDEN CITY                           
         DC    H'290'              GREAT BEND                                   
         DC    H'341'              HAYS - GOODLAND                              
         DC    X'FF'                                                            
         EJECT                                                                  
* DSECT TO COVER SPOTMKWT WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
SAVER9C  DS    0XL16                                                            
SAVER9   DS    A                                                                
SAVERA   DS    A                                                                
SAVERB   DS    A                                                                
SAVERC   DS    A                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    8F                                                               
FULL     DS    F                                                                
*                                                                               
SVAMWTEN DS    A                                                                
SVAMWTMK DS    A                                                                
*                                                                               
USTOTAL  DS    F                                                                
RTGSVC   DS    C                                                                
DEMO     DS    XL3                                                              
BOOK     DS    XL2                                                              
*                                                                               
DEMREC   DS    XL1000                                                           
*                                                                               
       ++INCLUDE DEDBLOCK                                                       
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
SBLOCKD  DSECT                                                                  
       ++INCLUDE SPOTBLOCK                                                      
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* SPGENMKT                                                                      
         PRINT OFF                                                              
MKTRECD  DSECT                                                                  
       ++INCLUDE SPGENMKT                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025SPOTMKWT  03/21/17'                                      
         END                                                                    
