*          DATA SET ACCLBTXT   AT LEVEL 043 AS OF 05/01/02                      
*CATALP ACCLBTXT                                                                
         SPACE 2                                                                
************************************************************                    
*                                                          *                    
* ACCLBTXT - GET PCBILLING SYSTEM EXTRACTED DATA           *                    
*                                                          *                    
* ON ENTRY, P1  BYTES 0   = N/D                            *                    
*               BYTES 1-3 = A(BFMREC)                      *                    
*           P2  BYTES 0   = N/D                            *                    
*               BYTES 1-3 = A(CXBLOCK)                     *                    
*                                                          *                    
************************************************************                    
         TITLE 'ACCLBTXT - GET SYSTEM EXTRACTED DATA FOR BFMRECS'               
ACCLBTXT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CLBT**,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         MVC   PARMS(PARML),0(R1)                                               
         MVC   PLANG,0(R1)         SAVE PASSED LANGUAGE                         
         ST    RE,RELO                                                          
         MVC   USERRD,4(RD)        SAVE POINTER TO CALLER'S REGS                
*                                                                               
TEXT1    L     R8,ATXBLOCK                                                      
         USING TXBLOCKD,R8                                                      
         L     R9,TXACOM                                                        
         USING COMFACSD,R9                                                      
         MVC   DMCB(4),=C'LL  '                                                 
         CLI   PLANG,0                                                          
         BE    *+10                                                             
         MVC   DMCB+3(1),PLANG                                                  
         GOTO1 CDICTATE,DMCB,,DDIN,DDOUT                                        
         LA    R1,IO               SET IO AREA ADCONS                           
         ST    R1,AREC                                                          
         LA    R1,TEXTREC                                                       
         ST    R1,ATEXT                                                         
*                                                                               
TEXT2    XC    TXACTUAL,TXACTUAL                                                
         BAS   RE,CLROUT                                                        
*                                                                               
TEXT4    ZIC   R1,TXSELFUN         GET FUNCTION                                 
         BCTR  R1,0                                                             
         SLL   R1,2                DISPLACEMENT INTO FUNCTION TABLE             
         LA    R1,FUNCTAB(R1)                                                   
         L     RF,0(R1)                                                         
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
TEXTX    XMOD1 1                                                                
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
* SUB-ROUTINE TO READ A TEXT RECORD AND TO PROCESS IT                           
*                                                                               
TEXTS    NTR1  ,                                                                
         BAS   RE,READTXT          READ TEXT RECORD                             
         BNE   *+8                 DID NOT FIND IT                              
         BAS   RE,PROCTXT                                                       
         B     EXIT                                                             
         SPACE 2                                                                
* SUB-ROUTINE TO PROCESS A PANEL                                                
*                                                                               
PANEL    NTR1  ,                                                                
         BAS   RE,PRPAN                                                         
         BAS   RE,CAPOUT                                                        
         BAS   RE,GO                                                            
         B     EXIT                                                             
         SPACE 2                                                                
* SUB-ROUTINE TO CONTROL FOOTER PROCESSING                                      
*                                                                               
CONFOOT  NTR1  ,                                                                
         LA    RF,FOOT                                                          
         CLI   TXSELWHR,C'F'       TEST FOR FOOTER                              
         BE    *+8                                                              
         LA    RF,HEAD             USE HEADER LOGIC FOR SUB-FOOTER              
         BASR  RE,RF                                                            
         B     EXIT                                                             
         EJECT                                                                  
* SUB-ROUTINE TO PROCESS A TEXT RECORD, AT ENTRY ATEXT POINTS                   
* TO TEXT RECORD                                                                
*                                                                               
PROCTXT  NTR1  ,                                                                
         L     R6,ATEXT                                                         
         MVI   ELCODE,ACTHELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PROCTXTX                                                         
         USING ACTHD,R6                                                         
         OC    ACTHCOM,ACTHCOM                                                  
         BZ    PROCTXT4                                                         
*                                                                               
PROCTXT2 MVC   TXACTCOM,ACTHCOM    EXTRACT COMMENT NUMBER                       
         BAS   RE,GETCOM                                                        
         BAS   RE,CAPOUT                                                        
         BAS   RE,GO               CALL HOOK IF PASSED                          
         B     PROCTXTX                                                         
*                                                                               
PROCTXT4 MVC   TXACTPAN,ACTHPAN    EXTRACT ACTUAL PANEL CODE                    
         CLC   ACTHPAN,DEFAULT     TEST FOR DEFAULT PANEL                       
         BE    PROCTXT6            YES                                          
*                                                                               
         BAS   RE,GETPAN           PROCESS A PANEL DRIVEN RECORD                
         BAS   RE,PRPAN                                                         
         BAS   RE,CAPOUT                                                        
         BAS   RE,GO                                                            
         B     PROCTXTX                                                         
*                                                                               
PROCTXT6 BAS   RE,GETTXT           DEFAULT TEXT RECORD                          
         BAS   RE,CAPOUT                                                        
         BAS   RE,GO                                                            
*                                                                               
PROCTXTX B     EXIT                                                             
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO CLEAR THE OUTPUT PRINT BLOCK                    *              
******************************************************************              
         SPACE 1                                                                
CLROUT   ST    RE,SAVERE                                                        
         L     RE,TXAPRT                                                        
         L     R1,TXLPRT                                                        
         BCTR  R1,0                                                             
         SR    R0,R0                                                            
         ICM   R0,1,TXSELMAX                                                    
         BNZ   *+8                                                              
         LA    R0,MAXLIN                                                        
*                                                                               
CLROUT2  EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),SPACES                                                   
         LA    RE,1(R1,RE)                                                      
         BCT   R0,CLROUT2                                                       
*                                                                               
CLROUTX  L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
********************************************************                        
* SUB-ROUTINE TO FORCE ALL CAPITALS OUTPUT             *                        
********************************************************                        
         SPACE 1                                                                
CAPOUT   CLI   TXSELCAP,C'Y'       TEST FOR FORCING ALL CAPS OUTPUT             
         BNER  RE                                                               
         CLI   TXACTNUM,0          TEST FOR OUTPUT                              
         BER   RE                  NONE-EXIT                                    
         ST    RE,SAVERE                                                        
         ZIC   R0,TXACTNUM         R0=LOOP COUNTER                              
         L     R1,TXLPRT                                                        
         BCTR  R1,0                                                             
         L     RE,TXAPRT                                                        
         CLI   ATRUP,LANGGER       IS THE CONECTED LANGUAGE GERMAN?             
         BNE   CAPOUT2                                                          
         CLI   PLANG,X'00'         IS LANGUAGE CODE PASSED?                     
         BE    CAPOUT3             NO                                           
         CLI   PLANG,LANGGER                                                    
         BE    CAPOUT3                                                          
*                                                                               
CAPOUT2  EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,RE),SPACES                                                   
         LA    RE,1(R1,RE)                                                      
         BCT   R0,CAPOUT2                                                       
         B     CAPOUTX                                                          
*                                                                               
CAPOUT3  L     RF,ATRUP                                                         
CAPOUT3A EX    R1,*+8                                                           
         B     *+10                                                             
         TR    0(0,RE),0(RF)                                                    
         LA    RE,1(R1,RE)                                                      
         BCT   R0,CAPOUT3A                                                      
*                                                                               
CAPOUTX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
****************************************************************                
* SUB-ROUTINE TO READ THE TEXT RECORD--CALLED FROM MAIN LINE-- *                
* ON EXIT, CC=EQ IF RECORD FOUND, CC=NEQ IF RECORD NOT FOUND   *                
****************************************************************                
         SPACE 1                                                                
READTXT  NTR1  ,                                                                
         LA    R4,KEY                                                           
         USING ACTXKEY,R4                                                       
READTXT1 XC    ACTXKEY,ACTXKEY                                                  
         MVI   ACTXRTYP,ACTXEQU                                                 
         MVI   ACTXSREC,ACTXSEQU                                                
         MVC   ACTXCUL,TXSELCUL                                                 
         MVC   ACTXOG,TXSELOG                                                   
         MVC   ACTXOFC,TXSELOFC                                                 
         MVC   ACTXCLI,TXSELCLI                                                 
         MVC   ACTXPROD,TXSELPRO                                                
         MVC   ACTXJOB,TXSELJOB                                                 
         MVC   ACTXCAT,TXSELCAT                                                 
         MVC   ACTXWORK,TXSELWC                                                 
         MVC   ACTXSUFF,TXSELSUF                                                
         MVC   ACTXFORM,TXSELFRM                                                
         MVC   ACTXLANG,PLANG                                                   
         MVC   ACTXWHER,TXSELWHR                                                
         CLI   ACTXWHER+1,X'41'    TEST FOR SECOND BYTE                         
         BL    READTXT2            NO                                           
         XC    ACTXWHER(1),ACTXWHER+1 SWITCH THE TWO BYTES AROUND               
         XC    ACTXWHER+1(1),ACTXWHER                                           
         XC    ACTXWHER(1),ACTXWHER+1                                           
*                                                                               
READTXT2 MVC   AIO,ATEXT                                                        
         BAS   RE,HIGH                                                          
         CLC   ACTXKEY,KEYSAVE     TEST IF RECORD FOUND                         
         BE    READTXT3                                                         
         CLI   KEYSAVE+(ACTXLANG-ACTXKEY),0                                     
         BNE   *+10                                                             
         LTR   RB,RB                                                            
         B     READTXT3                                                         
         MVC   ACTXKEY,KEYSAVE                                                  
         MVI   ACTXLANG,0                                                       
         B     READTXT2                                                         
*                                                                               
READTXT3 B     EXIT                                                             
         EJECT                                                                  
****************************************************************                
* SUB-ROUTINE TO GET STANDARD COMMENT AND TO OUTPUT TO BLOCK   *                
* AT ENTRY, TXACTCOM=COMMENT NUMBER                            *                
****************************************************************                
         SPACE 1                                                                
GETCOM   NTR1  ,                                                                
         XC    KEY,KEY                                                          
         MVI   KEY,X'0C'                                                        
         MVC   KEY+1(1),TXSELCUL                                                
         MVC   KEY+2(6),TXACTCOM                                                
         MVC   AIO,AREC                                                         
         BAS   RE,HIGH                                                          
         CLC   KEY,KEYSAVE                                                      
         BNE   GETCOMX             DID NOT FIND COMMENT                         
*                                                                               
GETCOM2  L     R2,TXAPRT                                                        
         L     R3,TXLPRT                                                        
         SR    R0,R0                                                            
         ICM   R0,1,TXSELMAX                                                    
         BNZ   *+8                                                              
         LA    R0,MAXLIN                                                        
         SR    R4,R4               R4=LINE COUNTER                              
*                                                                               
         MVI   ELCODE,X'3E'                                                     
         L     R6,AIO                                                           
         BAS   RE,GETEL                                                         
*                                                                               
GETCOM4  BNE   GETCOMX                                                          
         USING ACOMMD,R6                                                        
         ZIC   R1,ACOMLEN                                                       
         SH    R1,=Y(ACOMMENT-ACOMMD+1)                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),ACOMMENT                                                 
*                                                                               
GETCOM6  LA    R2,0(R3,R2)         NEXT PRINT LINE                              
         LA    R4,1(R4)                                                         
         STC   R4,TXACTNUM         UPDATE ACTUAL NUMBER COUNT                   
         BCT   R0,GETCOM8                                                       
         B     GETCOMX             HAVE HIT MAXIMUM OUTPUT LINES                
*                                                                               
GETCOM8  BAS   RE,NEXTEL                                                        
         B     GETCOM4                                                          
*                                                                               
GETCOMX  B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO GET REGULAR TEXT AND TO GENERATE OUTPUT         *              
******************************************************************              
         SPACE 1                                                                
GETTXT   NTR1  ,                                                                
         L     R2,TXAPRT           R2=A(PRINT LINE)                             
         L     R3,TXLPRT           R3=WIDTH OF PRINT LINE                       
         SR    R5,R5               R5=SEQUENCE NUMBER COUNTER                   
*                                                                               
         L     R6,ATEXT            R6=A(TEXT RECORD)                            
         MVI   ELCODE,ACTFELQ      GET TEXT FIELD ELEMENTS                      
         BAS   RE,GETEL                                                         
         USING ACTFD,R6                                                         
*                                                                               
GETTXT2  BNE   GETTXTX             EOR                                          
*                                                                               
GETTXT3  LA    R5,1(R5)            INCREMENT SEQUENCE NUMBER                    
         STC   R5,TXACTNUM                                                      
         CLM   R5,1,ACTFSEQ                                                     
         BE    GETTXT6                                                          
*                                                                               
GETTXT4  CLI   TXSELMAX,0          INTERVENING BLANK LINE                       
         BE    *+14                                                             
         CLC   TXACTNUM,TXSELMAX                                                
         BE    GETTXTX             OUTPUT BLOCK IS FILLED                       
         LA    R2,0(R3,R2)         NEXT PRINT LINE                              
         B     GETTXT3                                                          
*                                                                               
GETTXT6  ZIC   R1,ACTFLEN                                                       
*&&UK*&& LA    RF,0(R1,R6)         RF=A(NEXT ELEMENT)                           
         SH    R1,=Y(ACTFTEXT-ACTFD+1)                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),ACTFTEXT                                                 
*&&UK                                                                           
         CLI   0(RF),ACTFELQ                                                    
         BNE   GETTXT10                                                         
         CLI   ACTFTEXT-ACTFD(RF),C'+' CONTINUATION OF THIS LINE?               
         BNE   GETTXT10                                                         
         SR    RE,RE                                                            
         IC    RE,ACTFLEN-ACTFD(RF)                                             
         SH    RE,=Y(ACTFTEXT-ACTFD+2)                                          
         LA    R1,2(R1,RE)         R1=COMBINED LENGTH                           
         CR    R1,R3                                                            
         BH    GETTXT10            HARD LUCK - COMBINED LINE TOO LONG           
         SR    R1,RE                                                            
         LA    R1,0(R1,R2)                                                      
         BCTR  R1,0                R1=A(NEXT PRINT POSN)                        
         EX    RE,*+4                                                           
         MVC   0(0,R1),ACTFTEXT+1-ACTFD(RF)                                     
         BAS   RE,NEXTEL           BUMP PAST THE CONTINUATION ELEMENT           
*&&                                                                             
*                                                                               
GETTXT10 CLI   TXSELMAX,0                                                       
         BE    *+14                                                             
         CLC   TXACTNUM,TXSELMAX                                                
         BE    GETTXTX                                                          
         LA    R2,0(R3,R2)                                                      
         BAS   RE,NEXTEL                                                        
         B     GETTXT2                                                          
*                                                                               
GETTXTX  B     EXIT                                                             
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO GET ALL THE PANEL DATA RECORDS AND BUILD        *              
* A STREAM OF ELEMENTS AT TXAPAN                                 *              
******************************************************************              
         SPACE 1                                                                
GETPAN   NTR1  ,                                                                
         CLC   TXACTPAN,TXLPAN     TEST FOR CHANGE ON PANELS                    
         BE    GETPANX             NO-EXIT RIGHT AWAY                           
*                                                                               
GETPAN1  XC    TXLPAN,TXLPAN       CLEAR LAST PANEL                             
         L     R2,TXAPAN                                                        
         XC    0(2,R2),0(R2)                                                    
         LA    R4,KEY                                                           
         USING ACPNKEY,R4                                                       
         XC    ACPNKEY,ACPNKEY                                                  
         MVI   ACPNRTYP,ACPNEQU                                                 
         MVI   ACPNSREC,ACPNSEQU                                                
         MVC   ACPNCUL,TXSELCUL                                                 
         MVC   ACPNCODE,TXACTPAN                                                
         MVI   ACPNNUM,1                                                        
         MVC   ACPNLANG,PLANG                                                   
*                                                                               
GETPAN2  MVC   AIO,AREC                                                         
         BAS   RE,HIGH                                                          
         CLC   ACPNKEY,KEYSAVE                                                  
         BE    GETPAN3                                                          
         CLI   KEYSAVE+ACPNLANG-ACPNKEY,0                                       
         BE    GETPANX                                                          
         MVC   ACPNKEY,KEYSAVE                                                  
         MVI   ACPNLANG,0                                                       
         B     GETPAN2                                                          
*                                                                               
GETPAN3  MVC   TXLPAN,TXACTPAN                                                  
         L     R6,AREC                                                          
         MVI   ELCODE,ACFDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING ACFDD,R6                                                         
*                                                                               
GETPAN4  CLI   ACFDSEQ,X'FF'       TEST FOR TRAILER ELEMENT                     
         BE    GETPANX             ALL DONE                                     
*                                                                               
         ZIC   R1,ACFDLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),ACFDD       EXTRACT FIELD ELEMENT                        
         LA    R2,1(R1,R2)                                                      
         MVI   0(R2),0                                                          
*                                                                               
         BAS   RE,NEXTEL           LOOK FOR ANOTHER ELEMENT                     
         BE    GETPAN4             YES-FOUND ONE                                
*                                                                               
GETPAN6  ZIC   R1,ACPNNUM                                                       
         LA    R1,1(R1)            NEXT DATA RECORD                             
         STC   R1,ACPNNUM                                                       
         MVC   ACPNLANG,PLANG      SET LANGUAGE CODE IF PASSED                  
         B     GETPAN2                                                          
*                                                                               
GETPANX  B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
*****************************************************************               
* SUB-ROUTINE TO READ TEXT RECORDS FOR THE FOOTER AREA AND      *               
* RETURN THE RECORD USING THE HOOK                              *               
* ROUTINE READS UPWARD FROM JOB TO OFFICE GROUP RETURNING ALL   *               
* TEXT IT FINDS                                                 *               
*****************************************************************               
         SPACE 1                                                                
FOOT     NTR1  ,                                                                
         OC    TXAHOOK,TXAHOOK                                                  
         BNZ   *+6                                                              
         DC    H'0'                TAKE A DUMP                                  
*                                                                               
         MVI   ELCODE,ACJBELQ      GET JOB ELEMENT                              
         L     R6,TXAJOB                                                        
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING ACJOBD,R6                                                        
         TM    ACJBSTAT,ACJBNEWQ   TEST JOB IS ON NEW ESTIMATES                 
         BO    FOOT1                                                            
         BAS   RE,OLDFOOT                                                       
         B     FOOTX                                                            
         DROP  R6                                                               
*                                                                               
FOOT1    BAS   RE,SETMED                                                        
         LA    R4,KEY                                                           
         USING ACTXKEY,R4                                                       
         XC    ACTXKEY,ACTXKEY                                                  
         MVC   ACTXLANG,PLANG                                                   
         MVI   ACTXRTYP,ACTXEQU                                                 
         MVI   ACTXSREC,ACTXSEQU                                                
         MVC   ACTXCUL,TXSELCUL                                                 
         MVC   ACTXCLI,TXSELCLI                                                 
         MVC   ACTXPROD,TXSELPRO                                                
         MVC   ACTXJOB,TXSELJOB                                                 
         MVC   ACTXFORM,TXSELFRM                                                
         MVC   ACTXWHER,TXSELWHR                                                
         CLI   ACTXWHER+1,X'41'    TEST FOR TWO BYTE CODE                       
         BL    FOOT2                                                            
*                                                                               
         MVC   DUB(1),ACTXWHER     SWITCH THE ORDER OF TWO BYTES                
         MVC   ACTXWHER(1),ACTXWHER+1                                           
         MVC   ACTXWHER+1(1),DUB                                                
*                                                                               
FOOT2    MVC   TEXTKEY,ACTXKEY     SAVE TEXT KEY                                
         MVC   AIO,ATEXT                                                        
         BAS   RE,HIGH             READ FOR JOB LEVEL TEXT                      
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   FOOT3                                                            
         BAS   RE,PROCTXT                                                       
         B     FOOT4                                                            
*                                                                               
FOOT3    CLI   TEXTKEY+(ACTXLANG-ACTXKEY),0  IS LANGUAGE CODE PASSED            
         BE    FOOT4               NO                                           
         MVC   ACTXKEY,TEXTKEY                                                  
         MVI   ACTXLANG,0                                                       
         BAS   RE,HIGH             READ FOR JOB LEVEL TEXT                      
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   FOOTX                                                            
         BAS   RE,PROCTXT                                                       
         B     FOOTX               STOP NOW                                     
*                                                                               
FOOT4    MVC   ACTXKEY,TEXTKEY     RESTORE ORIGINAL TEXT KEY                    
         XC    ACTXJOB,ACTXJOB     READ FOR PRODUCT LEVEL TEXT                  
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL            DEAL WITH READ FOR THIS LEVEL                
*                                                                               
FOOT6    MVC   ACTXKEY,TEXTKEY     RESTORE LAST KEY                             
         XC    ACTXPROD,ACTXPROD                                                
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
*                                                                               
FOOT8    MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXCLI,ACTXCLI                                                  
         MVC   ACTXOFC,TXSELOFC    READ FOR OFFICE LEVEL                        
         MVC   TEXTKEY,ACTXKEY                                                  
         OC    TXSELOFC,TXSELOFC   ANY OFFICE PRESENT?                          
         BNZ   *+8                                                              
         MVI   MEDFLAG,MEDLEVQ     MEDIA LEVEL READ                             
         BAS   RE,LEVEL                                                         
*                                                                               
FOOT10   MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXOFC,ACTXOFC                                                  
         MVC   ACTXOG,TXSELOG      READ OFFICE GROUP LEVEL                      
         MVC   TEXTKEY,ACTXKEY                                                  
         OC    TXSELOG,TXSELOG     ANY OFFICE GROUP PRESENT?                    
         BNZ   *+12                                                             
         TM    MEDFLAG,MEDLEVQ                                                  
         BO    FOOTX                                                            
         BAS   RE,LEVEL                                                         
*                                                                               
FOOTX    B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
**************************************************************                  
* SUB-ROUTINE TO GET THE OLD ESTIMATE FOOTER COMMENTS        *                  
**************************************************************                  
         SPACE 1                                                                
OLDFOOT  NTR1  ,                                                                
         L     R6,TXAJOB                                                        
         BAS   RE,OLDCOMM                                                       
         L     R6,TXAPRO                                                        
         BAS   RE,OLDCOMM                                                       
         L     R6,TXACLI                                                        
         BAS   RE,OLDCOMM                                                       
         B     EXIT                                                             
         SPACE 2                                                                
******************************************************************              
* SUB-ROUTINE TO PROCESS COMMENT ELEMENTS ON A CLIENT, PRODUCT   *              
* OR JOB RECORD, AT ENTRY R6=A(RECORD)                           *              
******************************************************************              
         SPACE 1                                                                
OLDCOMM  NTR1  ,                                                                
         MVI   ELCODE,X'3E'                                                     
         BAS   RE,GETEL                                                         
         B     OLDCOMM2                                                         
*                                                                               
OLDCOMM1 BAS   RE,NEXTEL                                                        
*                                                                               
OLDCOMM2 BNE   EXIT                                                             
         USING ACOMMD,R6                                                        
*                                                                               
         CLI   ACOMTYPE,C'M'       TEST MANUAL COMMENTS                         
         BE    OLDCOMM3                                                         
         CLI   ACOMTYPE,0          TEST FOR REGULAR COMMENT                     
         BNE   OLDCOMM6            NO-STANDARD COMMENT REFERENCE                
*                                                                               
OLDCOMM3 XC    TXACTUAL,TXACTUAL                                                
         BAS   RE,CLROUT                                                        
         MVI   TXACTNUM,1                                                       
         L     R4,TXAPRT                                                        
         ZIC   R1,ACOMLEN                                                       
         SH    R1,=Y(ACOMMENT-ACOMMD-1)                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),ACOMMENT                                                 
         BAS   RE,GO                                                            
         B     OLDCOMM1                                                         
*                                                                               
OLDCOMM6 TM    ACOMTYPE,X'40'      TEST ESTIMATE COMMENT                        
         BZ    OLDCOMM1            SKIP ELEMENT                                 
*                                                                               
         XC    TXACTUAL,TXACTUAL                                                
         BAS   RE,CLROUT                                                        
         MVC   TXACTCOM,ACOMMENT   EXTRACT COMMENT NUMBER                       
         BAS   RE,GETCOM                                                        
         CLI   TXACTNUM,0          TEST ANYTHING FOUND                          
         BE    *+8                                                              
         BAS   RE,GO                                                            
         B     OLDCOMM1                                                         
         DROP  R6                                                               
         EJECT                                                                  
*****************************************************************               
* SUB-ROUTINE TO READ TEXT RECORDS FOR THE HEADER AREA.         *               
* ROUTINE READ FOR JOB LEVEL TEXT AND CONTINUES UPWARD TO       *               
* OFFICE GROUP STOPPING WHEN IT FINDS A TEXT RECORD AT A LEVEL. *               
* LOGIC IS ALSO USED FOR SUB-FOOTERS                            *               
*****************************************************************               
         SPACE 1                                                                
HEAD     NTR1  ,                                                                
         MVI   ELCODE,ACJBELQ      GET JOB ELEMENT                              
         L     R6,TXAJOB                                                        
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING ACJOBD,R6                                                        
         TM    ACJBSTAT,ACJBNEWQ   TEST JOB IS ON NEW ESTIMATES                 
         BO    HEAD1                                                            
         BAS   RE,OLDHEAD                                                       
         B     HEADX                                                            
         DROP  R6                                                               
*                                                                               
HEAD1    BAS   RE,SETMED                                                        
         LA    R4,KEY                                                           
         USING ACTXKEY,R4                                                       
         XC    ACTXKEY,ACTXKEY     BUILD JOB LEVEL TEXT KEY                     
         MVC   ACTXLANG,PLANG                                                   
HEAD1A   MVI   ACTXRTYP,ACTXEQU                                                 
         MVI   ACTXSREC,ACTXSEQU                                                
         MVC   ACTXCUL,TXSELCUL                                                 
         MVC   ACTXCLI,TXSELCLI                                                 
         MVC   ACTXPROD,TXSELPRO                                                
         MVC   ACTXJOB,TXSELJOB                                                 
         MVC   ACTXFORM,TXSELFRM                                                
         MVC   ACTXWHER,TXSELWHR                                                
         CLI   ACTXWHER+1,X'41'    TEST FOR TWO BYTE CODE                       
         BL    HEAD2                                                            
*                                                                               
         MVC   DUB(1),ACTXWHER     SWITCH THE ORDER OF TWO BYTES                
         MVC   ACTXWHER(1),ACTXWHER+1                                           
         MVC   ACTXWHER+1(1),DUB                                                
*                                                                               
HEAD2    MVC   TEXTKEY,ACTXKEY     SAVE ORIGINAL KEY                            
         MVC   AIO,ATEXT                                                        
         BAS   RE,HIGH             READ FOR JOB LEVEL TEXT                      
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   HEAD3                                                            
         BAS   RE,PROCTXT                                                       
         B     HEADX               STOP NOW                                     
*                                                                               
HEAD3    CLI   TEXTKEY+(ACTXLANG-ACTXKEY),0  IS LANGUAGE CODE PASSED            
         BE    HEAD4               NO                                           
         MVC   ACTXKEY,TEXTKEY                                                  
         MVI   ACTXLANG,0                                                       
         BAS   RE,HIGH             READ FOR JOB LEVEL TEXT                      
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   HEADX                                                            
         BAS   RE,PROCTXT                                                       
         B     HEADX               STOP NOW                                     
*                                                                               
HEAD4    MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXJOB,ACTXJOB     READ FOR PRODUCT LEVEL TEXT                  
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
         BE    HEADX               STOP IF FOUND SOMETHING                      
*                                                                               
HEAD6    MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXPROD,ACTXPROD                                                
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
         BE    HEADX                                                            
*                                                                               
HEAD8    MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXCLI,ACTXCLI                                                  
         MVC   ACTXOFC,TXSELOFC    READ FOR OFFICE LEVEL                        
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
         BE    HEADX                                                            
*                                                                               
HEAD10   MVC   ACTXKEY,TEXTKEY                                                  
         XC    ACTXOFC,ACTXOFC                                                  
         MVC   ACTXOG,TXSELOG      READ OFFICE GROUP LEVEL                      
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
         BE    HEADX                                                            
*                                                                               
HEAD12   MVC   ACTXKEY,TEXTKEY     READ AGENCY LEVEL                            
         XC    ACTXOG,ACTXOG                                                    
         MVC   TEXTKEY,ACTXKEY                                                  
         BAS   RE,LEVEL                                                         
         BE    HEADX                                                            
*                                                                               
HEADX    B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
********************************************************************            
* SUB-ROUTINE TO GET THE HEADER AREA TEXT FOR AN OLD ESTIMATE JOB  *            
********************************************************************            
         SPACE 1                                                                
OLDHEAD  NTR1  ,                                                                
         L     R6,TXAJOB           LOOK FIRST FOR JOB LEVEL TEXT                
         BAS   RE,GETNARR                                                       
         BE    OLDHEADX            YES-FOUND SOMETHING                          
*                                                                               
         L     R6,TXAPRO           NOW TRY PRODUCT                              
         BAS   RE,GETNARR                                                       
         BE    OLDHEADX                                                         
*                                                                               
         L     R6,TXACLI           LAST TRY CLIENT                              
         BAS   RE,GETNARR                                                       
*                                                                               
OLDHEADX B     EXIT                                                             
         SPACE 2                                                                
* SUB-ROUTINE TO GET ANY NARRATION FOR THE HEADER                               
* ON ENTRY, R6=A(CLI, PRO, OR JOB RECORD)                                       
* ON EXIT, CC=EQ IF SOME FOUND, CC=NEQ IF NONE FOUND                            
*                                                                               
GETNARR  NTR1  ,                                                                
         MVI   ELCODE,ACPRELQ      GET PROFILE ELEMENT                          
         BAS   RE,GETEL                                                         
         BNE   NOTOK               NO PROFILE ELEMENT                           
         USING ACPROFD,R6                                                       
         CLI   ACPRLEN,ACPRNARR-ACPROFD TEST FOR ANY NARRATION                  
         BE    NOTOK               NO-NONE                                      
*                                                                               
         MVI   TXACTNUM,1                                                       
         L     R4,TXAPRT                                                        
         MVC   0(50,R4),ACPRNARR                                                
         CLI   ACPRLEN,ACPRNARR+50-ACPROFD                                      
         BE    GETNARRX                                                         
*                                                                               
         MVI   TXACTNUM,2                                                       
         A     R4,TXLPRT           NEXT PRINT LINE                              
         MVC   0(50,R4),ACPRNARR+50                                             
         CLI   ACPRLEN,ACPRNARR+100-ACPROFD                                     
         BE    GETNARRX                                                         
*                                                                               
         MVI   TXACTNUM,3                                                       
         A     R4,TXLPRT                                                        
         MVC   0(50,R4),ACPRNARR+100                                            
*                                                                               
GETNARRX B     OK                                                               
         DROP  R6                                                               
         EJECT                                                                  
*******************************************************************             
* SUB-ROUTINE TO READ FOR TEXT RECORDS AT A GIVEN LEVEL           *             
* CALLED FROM HEAD AND FOOT.  AT ENTRY, TEXTKEY CONTAINS LEVEL'S  *             
* KEY.  ON EXIT, CC=EQ IF TEXT RETURNED, CC=NEQ FOR NO TEXT       *             
*******************************************************************             
         SPACE 1                                                                
LEVEL    NTR1  ,                                                                
         XC    TXACTUAL,TXACTUAL                                                
         BAS   RE,CLROUT                                                        
         LA    R4,KEY                                                           
         USING ACTXKEY,R4                                                       
         MVI   TXSW,C'N'                                                        
         MVC   AIO,ATEXT                                                        
         BAS   RE,HIGH             READ RECORD AT LEVEL                         
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   *+8                                                              
         MVI   TXSW,C'Y'           SET SWITCH IF FOUND                          
*                                                                               
LEVEL2   MVC   ACTXKEY,TEXTKEY                                                  
         MVC   ACTXMED,MEDCODE     READ FOR MEDIA                               
         MVC   AIO,AREC                                                         
         BAS   RE,HIGH                                                          
         CLC   ACTXKEY,KEYSAVE                                                  
         BE    LEVEL6              FOUND A MEDIA RELATED TEXT                   
         CLI   MGCODE,0            TEST IF MEDIA BELONGS TO MED GRP             
         BE    LEVEL8              NO                                           
*                                                                               
LEVEL4   MVC   ACTXKEY,TEXTKEY                                                  
         MVC   ACTXMG,MGCODE       READ FOR MEDIA GROUP                         
         BAS   RE,HIGH                                                          
         CLC   ACTXKEY,KEYSAVE                                                  
         BNE   LEVEL8                                                           
*                                                                               
LEVEL6   L     R0,ATEXT            MOVE RECORD TO ATEXT                         
         L     RE,AREC                                                          
         LH    RF,ACLENGTH-ACKEYD(RE)                                           
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
         MVC   AIO,ATEXT                                                        
         MVI   TXSW,C'Y'                                                        
*                                                                               
LEVEL8   CLI   TXSW,C'Y'           TEST IF ANYTHING FOUND                       
         BNE   *+8                 NO                                           
         BAS   RE,PROCTXT          YES-HANDLE THE RECORD                        
*                                                                               
LEVELX   CLI   TXACTNUM,0                                                       
         BE    NOTOK                                                            
         B     OK                                                               
         DROP  R4                                                               
         EJECT                                                                  
******************************************************************              
* SUB-ROUTINE TO PROCESS THE PANEL TABLE                         *              
*                                                                *              
* AT ENTRY, TXAPAN=A(PANEL ELEMENTS)                             *              
******************************************************************              
         SPACE 1                                                                
PRPAN    NTR1  ,                                                                
         L     R5,TXAPAN           R5=A(PANEL FIELD ELEMENTS)                   
         USING ACFDD,R5                                                         
*                                                                               
PRPAN2   CLI   ACFDEL,0            TEST FOR EOR                                 
         BE    PRPAN40                                                          
*                                                                               
         CLI   ACFDPLIN,0          TEST FOR SCREEN SPEC ONLY                    
         BE    PRPAN30             YES                                          
         OC    ACFDFLD,ACFDFLD     TEST FOR FIELD KEYWORD                       
         BZ    PRPAN10                                                          
*                                                                               
PRPAN4   MVI   DUB,ACTFTPAN        TEXT TYPE                                    
         MVC   DUB+1(L'ACTFFLD),ACFDFLD FIELD KEYWORD                           
         LA    R0,L'ACTFFLD+1                                                   
         GOTO1 CHELLO,DMCB,(C'G',ACCOUNT),('ACTFELQ',ATEXT),((R0),DUB)          
         CLI   12(R1),0                                                         
         BNE   PRPAN30             DID NOT FIND TEXT                            
*                                                                               
         L     R6,12(R1)           R6=A(TEXT ELEMENT)                           
         USING ACTFD,R6                                                         
         ZIC   R1,ACTFLEN                                                       
         SH    R1,=Y(ACTFTEXT-ACTFD+1)                                          
         BM    PRPAN30                                                          
         BAS   RE,SETOUT                                                        
         EX    R1,*+8                                                           
         B     PRPAN30                                                          
         MVC   0(0,R3),ACTFTEXT    EXTRACT TEXT                                 
*                                                                               
PRPAN10  CLI   ACFDPDTY,C'C'       TEST FOR CHARACTER STRING                    
         BNE   PRPAN20                                                          
*                                                                               
         ZIC   R1,ACFDPDLN         PRINT DATA LENGTH                            
         ZIC   R4,ACFDLEN                                                       
         LA    R4,0(R5,R4)                                                      
         SR    R4,R1               R4=A(PRINT DATA)                             
         BCTR  R1,0                                                             
*                                                                               
         BAS   RE,SETOUT                                                        
         EX    R1,*+8                                                           
         B     PRPAN30                                                          
         MVC   0(0,R3),0(R4)                                                    
*                                                                               
PRPAN20  CLI   ACFDPDTY,C'I'       TEST INTERNALLY GENERATED DATA               
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 INT,DMCB,(R5)                                                    
*                                                                               
PRPAN30  ZIC   R0,ACFDLEN                                                       
         AR    R5,R0                                                            
         B     PRPAN2              NEXT ELEMENT                                 
*                                                                               
PRPAN40  L     R4,TXLPRT           SET N'OUTPUT LINES                           
         SR    R1,R1                                                            
         ICM   R1,1,TXSELMAX                                                    
         BNZ   *+8                                                              
         LA    R1,MAXLIN                                                        
         LR    R3,R1               SAVE LINE COUNTER                            
         BCTR  R1,0                                                             
         MR    R0,R4               COMPUTE DISP TO LAST LINE                    
         L     R2,TXAPRT                                                        
         AR    R2,R1               COMPUTE A(LAST LINE)                         
         LR    R1,R4                                                            
         BCTR  R1,0                                                             
*                                                                               
PRPAN42  EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),SPACES                                                   
         BE    PRPAN44                                                          
         STC   R3,TXACTNUM                                                      
         B     PRPANX                                                           
*                                                                               
PRPAN44  SR    R2,R4               BACK UP ONE LINE                             
         BCT   R3,PRPAN42                                                       
         MVI   TXACTNUM,0                                                       
*                                                                               
PRPANX   B     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
*******************************************************************             
* SUB-ROUTINE TO OUTPUT INTERNALLY GENERATED DATA--CALLED FROM    *             
* PRPAN                                                           *             
* AT ENTRY, P1=A(FIELD DEFINITION ELEMENT)                        *             
*******************************************************************             
         SPACE 1                                                                
INT      NTR1  ,                                                                
         L     R5,0(R1)                                                         
         USING ACFDD,R5                                                         
         ZIC   R4,ACFDLEN          R4=ELEMENT LENGTH                            
         LA    R3,0(R4,R5)                                                      
         BCTR  R3,0                R3=A(END OF ELEMENT)                         
         ZIC   R1,ACFDPDLN         R1=L'PRINT SPEC DATA                         
         SR    R4,R1               R4=DISP TO PRINT SPEC DATA                   
         LA    R4,0(R5,R4)         R4=A(PRINT SPEC DATA)                        
         CLI   0(R4),C'&&'         TEST IF DATA STARTS WITH AMPERSAND           
         BE    INT2                YES-OLD STYLE ELEMENT                        
         B     INT10               NO-NEW STYLE ELEMENT                         
*                                                                               
INT2     LA    R0,INTNTRYS                                                      
         LA    RE,INTTAB                                                        
*                                                                               
INT4     CLC   ACFDPRT+1(2),0(RE)  MATCH ON INTERNAL CODE                       
         BE    INT6                                                             
         LA    RE,L'INTTAB(RE)                                                  
         BCT   R0,INT4                                                          
         B     INTX                                                             
*                                                                               
INT6     LH    RF,2(RE)                                                         
         LA    RF,ACGETTXT(RF)                                                  
         BASR  RE,RF                                                            
         B     INTX                                                             
*                                                                               
* HANDLE NEW STYLE MINI-ELEMENTS FOR INTERNAL DATA                              
*                                                                               
INT10    SR    R2,R2               CLEAR LENGTH REGISTER FOR BXLE               
         MVC   ROUTINE,SPACES                                                   
         MVI   PARAML,0                                                         
         MVC   PARAMS,SPACES                                                    
*                                                                               
INT12    LA    R6,ROUTINE                                                       
         CLI   0(R4),X'02'         TEST FOR ROUTINE NAME                        
         BE    INT14               YES                                          
         LA    R6,PARAMS                                                        
         CLI   0(R4),X'03'         TEST FOR PARAMETERS                          
         BNE   INT13               NO                                           
*                                                                               
         ZIC   RE,1(R4)                                                         
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         STC   RE,PARAML           SET PARAMETER LENGTH                         
         B     INT14                                                            
*                                                                               
INT13    IC    R2,1(R4)            GET MINI-ELEMENT LENGTH                      
         BXLE  R4,R2,INT12                                                      
         B     INT15               NO MORE MINI-ELEMENTS TO LOOK AT             
*                                                                               
INT14    ZIC   R1,1(R4)                                                         
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     INT13                                                            
         MVC   0(0,R6),2(R4)                                                    
*                                                                               
INT15    LA    RE,INTTAB2                                                       
         LA    R0,INTNTRY2                                                      
*                                                                               
INT16    CLC   ROUTINE,0(RE)                                                    
         BE    INT20                                                            
         LA    RE,L'INTTAB2(RE)                                                 
         BCT   R0,INT16                                                         
         B     INTX                                                             
*                                                                               
INT20    LH    RF,L'ROUTINE(RE)                                                 
         LA    RF,ACGETTXT(RF)                                                  
         BASR  RE,RF                                                            
*                                                                               
INTX     B     EXIT                                                             
         EJECT                                                                  
* ROUTINE TO OUTPUT PRODUCTION ESTIMATE TITLE                                   
*                                                                               
PE       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         LR    R4,R3               SAVE OUTPUT POINTER IN R4                    
*                                                                               
PE2      BAS   RE,SETMED                                                        
         BE    PE6                                                              
         ICM   R6,15,AESTNAM                                                    
         BZ    PE4                                                              
         USING ACMND,R6                                                         
         ZIC   R1,ACMNLEN                                                       
         SH    R1,=Y(ACMNAME-ACMND+1)                                           
         EX    R1,*+8                                                           
         B     PEX                                                              
         MVC   0(0,R3),ACMNAME                                                  
*                                                                               
         USING ACMEDIAD,R6                                                      
PE4      L     R6,AMEDEL                                                        
         MVC   0(L'ACMDDESC-3,R3),ACMDDESC+3  ELSE USE DESCRIPTION              
         LA    R4,L'ACMDDESC-1(R3)                                              
         LA    R1,L'ACMDDESC-3                                                  
         CLI   0(R4),C' '                                                       
         BH    PE5                                                              
         BCTR  R4,0                                                             
         BCT   R1,*-10                                                          
         LR    R4,R3                                                            
         B     PE6                                                              
*                                                                               
PE5      LA    R4,2(R4)            BUMP PAST LAST CHARACTER + 1 SPACE           
*                                                                               
PE6      MVC   0(19,R4),AC@PRDES                                                
*                                                                               
PEX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT TODAY'S DATE                                                
*                                                                               
TD       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         MVC   0(L'AC@DATE,R3),AC@DATE                                          
         LA    R3,6(R3)                                                         
         B     TD2                                                              
*                                                                               
TD1      ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
TD2      ICM   R6,15,TXAMAST                                                    
         BZ    TD3                                                              
         USING MASTD,R6                                                         
         GOTO1 CDATCON,DMCB,(4,MCDATE),(17,0(R3))                               
         B     TDX                                                              
         DROP  R6                                                               
*                                                                               
TD3      GOTO1 CDATCON,DMCB,(2,TXSELTDT),(17,0(R3))                             
*                                                                               
TDX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO PRINT THE RUN TIME ONLY WHEN RUNNING SOON                          
*                                                                               
RTS      ICM   R6,15,TXAMAST                                                    
         BZR   RE                                                               
         USING MASTD,R6                                                         
         OC    MCREMPQK,MCREMPQK   TEST IF RUNNING SOON                         
         BNZ   RT                  YES                                          
         BR    RE                                                               
         DROP  R6                                                               
         SPACE 2                                                                
* ROUTINE TO PRINT THE RUN AT TIME                                              
*                                                                               
RT       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         TIME  DEC                                                              
         SRL   R0,16               SHIFT OUT SECONDS/TENTHS/HUNDRETHS           
         SLL   R0,4                                                             
         XC    DUB,DUB                                                          
         STCM  R0,7,DUB+5                                                       
         OI    DUB+7,X'0F'         MAKE TIME PACKED                             
         CVB   R0,DUB                                                           
*                                                                               
RT1      EQU   *                                                                
*&&US*&& A     R0,=F'800'          ADD 8 HOURS TO DDS CLOCK                     
         C     R0,=F'2400'         TEST IF AT OR PAST MIDNIGHT                  
         BL    RT4                 NO                                           
*                                                                               
RT2      MVI   AMPM,C'A'           SET TO AM                                    
         L     R1,=F'2400'         SUBTRACT 24 HOURS FROM TIME                  
         C     R0,=F'2500'         UNLESS ITS BETWEEN MIDNIGHT AND 1 AM         
         BNL   *+8                 NO                                           
         L     R1,=F'1200'         YES-SUBTRACT 12 HOURS THEN                   
         SR    R0,R1                                                            
         B     RT6                 PRINT IT OUT                                 
*                                                                               
RT4      MVI   AMPM,C'A'           SET TO AM                                    
         C     R0,=F'1200'         TEST IF BEFORE NOON                          
         BL    RT6                 NO                                           
         MVI   AMPM,C'P'           SET TO PM                                    
         C     R0,=F'1300'         TEST BETWEEN NOON AND 1 PM                   
         BL    *+8                 YES-DO NOT SUBTRACT 12 HOURS                 
         S     R0,=F'1200'         YES-SUBTRACT 12 HOURS                        
*                                                                               
RT6      MVI   AMPM+1,C'M'                                                      
         MVC   0(6,R3),AC@RUNAT                                                 
         LA    R3,7(R3)                                                         
         EDIT  (R0),(5,0(R3)),2                                                 
         MVI   2(R3),C':'          USE A COLON IN BETWEEN HOURS/MINUTES         
         MVC   5(2,R3),AMPM        ATTACH AM OR PM                              
*                                                                               
RTX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT PAGE NUMBER                                                 
*                                                                               
PG       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         LH    R0,TXPAGE                                                        
         MVC   0(L'AC@PAGE,R3),AC@PAGE                                          
         EDIT  (R0),(4,6(R3)),ALIGN=LEFT                                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT ORIGIN NAME                                                 
*                                                                               
AN       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     RE,TXAORIG                                                       
         MVC   0(33,R3),0(RE)      EXTRACT ORIGIN NAME                          
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT ORIGIN ADDRESS                                              
*                                                                               
AA       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     RE,TXAORIG                                                       
         MVC   0(33,R3),33(RE)     ADDRESS FOLLOWS NAME                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT COMPANY NAME                                                
*                                                                               
CON      ST    RE,SAVERE                                                        
         L     R6,TXACOMP                                                       
         BAS   RE,GETNAME                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT COMPANY ADDRESS                                             
*                                                                               
COA      ST    RE,SAVERE                                                        
         L     R6,TXACOMP                                                       
         MVI   ELCODE,ACADELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   COAX                                                             
         BAS   RE,GETADD                                                        
*                                                                               
COAX     L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT CLIENT CODE                                                 
*                                                                               
CC       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         BAS   RE,SETLEN                                                        
         L     R4,TXACLI                                                        
         USING ACKEYD,R4                                                        
         ZIC   R1,LCLI                                                          
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACKEYACC+3                                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT CLIENT NAME                                                 
*                                                                               
CN       ST    RE,SAVERE                                                        
         L     R6,TXACLI                                                        
         BAS   RE,GETNAME                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT PRODUCT CODE                                                
*                                                                               
PC       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         BAS   RE,SETLEN                                                        
         L     R4,TXAPRO                                                        
         USING ACKEYD,R4                                                        
         ZIC   R1,LPRO                                                          
         BCTR  R1,0                                                             
         ZIC   RE,LCLI             RE=L'CLIENT CODE                             
         LA    RE,ACKEYACC+3(RE)   INDEX TO PRODUCT FIELD                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT PRODUCT NAME                                                
*                                                                               
PN       ST    RE,SAVERE                                                        
         L     R6,TXAPRO                                                        
         BAS   RE,GETNAME                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT JOB CODE                                                    
*                                                                               
JC       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         BAS   RE,SETLEN                                                        
         L     R4,TXAJOB                                                        
         USING ACKEYD,R4                                                        
         ZIC   R1,LJOB                                                          
         ZIC   RE,LCLIPRO                                                       
         LA    RE,ACKEYACC+3(RE)   INDEX TO JOB CODE                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT JOB NAME                                                    
*                                                                               
JN       ST    RE,SAVERE                                                        
         L     R6,TXAJOB                                                        
         BAS   RE,GETNAME                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT JOB OPEN DATE                                               
*                                                                               
OP       ST    RE,SAVERE                                                        
         L     R6,TXAJOB                                                        
         MVI   ELCODE,ACJBELQ      JOB ELEMENT                                  
         BAS   RE,GETEL                                                         
         BNE   OPX                                                              
         USING ACJOBD,R6                                                        
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACJBOPND),(17,(R3))                              
*                                                                               
OPX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE TO OUTPUT JOB CLOSE DATE                                              
*                                                                               
CL       ST    RE,SAVERE                                                        
         L     R6,TXAJOB                                                        
         MVI   ELCODE,ACJBELQ      JOB ELEMENT                                  
         BAS   RE,GETEL                                                         
         BNE   CLX                                                              
         USING ACJOBD,R6                                                        
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACJBCLOS),(17,(R3))                              
*                                                                               
CLX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR BILLING ADDRESS                                                   
*                                                                               
BA       ST    RE,SAVERE                                                        
         L     R6,TXAJOB                                                        
         MVI   ELCODE,ACADELQ      ADDRESS ELEMENT                              
         BAS   RE,GETEL                                                         
         BE    BA2                                                              
         L     R6,TXAPRO                                                        
         BAS   RE,GETEL                                                         
         BE    BA2                                                              
         L     R6,TXACLI                                                        
         BAS   RE,GETEL                                                         
         BE    BA2                                                              
         B     BAX                                                              
*                                                                               
BA2      BAS   RE,GETADD                                                        
*                                                                               
BAX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR PRINT ON BILLS INFORMATION                                        
*                                                                               
PB       ST    RE,SAVERE                                                        
         L     R6,TXAJOB           TRY JOB FIRST                                
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,PB2              SET RETURN FOR PRODUCT                       
         BE    PB10                                                             
*                                                                               
PB2      L     R6,TXAPRO           THEN PRODUCT                                 
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,PB4              SET RETURN FOR CLIENT                        
         BE    PB10                                                             
*                                                                               
PB4      L     R6,TXACLI           THEN CLIENT                                  
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,PBX                                                           
         BNE   PBX                 NOTHING TO PRINT                             
*                                                                               
         USING ACPROFD,R6                                                       
PB10     OC    ACPRBLPR,ACPRBLPR   TEST FOR ANYTHING TO PRINT                   
         BZR   RE                  NO                                           
         CLC   ACPRBLPR,SPACES     TEST FOR ANYTHING TO PRINT                   
         BER   RE                  NO                                           
         BAS   RE,SETOUT                                                        
         MVC   0(L'ACPRBLPR,R3),ACPRBLPR                                        
*                                                                               
PBX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR MEDIA CODE                                                        
*                                                                               
MC       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         BAS   RE,SETLEN                                                        
         L     R4,TXAJOB                                                        
         USING ACKEYD,R4                                                        
         ZIC   R1,LCLIPRO                                                       
         LA    RE,ACKEYACC+3(R1)   INDEX TO JOB CODE                            
         MVC   0(1,R3),0(RE)       EXTRACT MEDIA CODE                           
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR MEDIA NAME                                                        
*                                                                               
MN       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         BAS   RE,SETMED                                                        
         BE    MNX                 COULD NOT FIND MEDIA ELEM                    
         L     R6,AMEDEL                                                        
         USING ACMEDIAD,R6                                                      
         MVC   0(L'ACMDDESC-3,R3),ACMDDESC+3                                    
         CLI   PLANG,0             IS LANGUAGE CODE PASSED?                     
         BE    MNX                 NO                                           
         SR    RE,RE               YES - LOOK FOR EXTRA NAME ELEMENT            
         IC    RE,1(R6)                                                         
         AR    R6,RE                                                            
         USING ACXNMD,R6                                                        
         CLI   ACXNMEL,X'56'                                                    
         BNE   MNX                                                              
         CLI   PLANG,X'01'         IS ENGLISH REQUIRED                          
         BNE   *+16                                                             
         CLI   ACXNMSTT,0                                                       
         BNE   MNX                                                              
         B     *+14                                                             
         CLC   ACXNMSTT,PLANG                                                   
         BNE   MNX                                                              
         MVC   0(L'ACMDDESC-3,R3),SPACES                                        
         IC    RE,ACXNMSL                                                       
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACXNMSN                                                  
*                                                                               
MNX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR ORIGINAL ESTIMATE APPROVER                                        
*                                                                               
OEA      ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBORGVER,0          TEST FOR AN ORIGINAL ESTIMATE                
         BER   RE                  NO                                           
         ICM   R6,15,JBAOE                                                      
         BZR   RE                  NO ORIGINAL ESTIMATE BUFFER                  
*                                                                               
         MVI   ELCODE,ACEAELQ      LOOK FOR ESTIMATE APPROVED ELEM              
         BAS   RE,GETEL                                                         
         BNE   OEAX                                                             
         USING ACEAD,R6                                                         
         BAS   RE,SETOUT                                                        
         MVC   0(L'ACEAPPBY,R3),ACEAPPBY                                        
*                                                                               
OEAX     L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR ORIGINAL ESTIMATE PREPARER                                        
*                                                                               
OEP      ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBORGVER,0          TEST FOR AN ORIGINAL ESTIMATE                
         BER   RE                  NO                                           
         ICM   R6,15,JBAOE                                                      
         BZR   RE                  NO ORIGINAL ESTIMATE BUFFER                  
*                                                                               
         MVI   ELCODE,ACEPELQ      LOOK FOR ESTIMATE PREPARER ELEM              
         BAS   RE,GETEL                                                         
         BNE   OEPX                                                             
         USING ACEPD,R6                                                         
         BAS   RE,SETOUT                                                        
         MVC   0(L'ACEPREP,R3),ACEPREP                                          
*                                                                               
OEPX     L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR CURRENT ESTIMATE APPROVER                                         
*                                                                               
CEA      ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBCURVER,0          TEST FOR A CURRENT ESTIMATE                  
         BER   RE                  NO                                           
         ICM   R6,15,JBACE                                                      
         BZR   RE                  NO CURRENT ESTIMATE BUFFER                   
*                                                                               
         MVI   ELCODE,ACEAELQ      LOOK FOR ESTIMATE APPROVED ELEM              
         BAS   RE,GETEL                                                         
         BNE   CEAX                                                             
         USING ACEAD,R6                                                         
         BAS   RE,SETOUT                                                        
         MVC   0(L'ACEAPPBY,R3),ACEAPPBY                                        
*                                                                               
CEAX     L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR CURRENT ESTIMATE PREPARER                                         
*                                                                               
CEP      ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBCURVER,0          TEST FOR A CURRENT ESTIMATE                  
         BER   RE                  NO                                           
         ICM   R6,15,JBACE                                                      
         BZR   RE                  NO CURRENT ESTIMATE BUFFER                   
*                                                                               
         MVI   ELCODE,ACEPELQ      LOOK FOR ESTIMATE PREPARER ELEM              
         BAS   RE,GETEL                                                         
         BNE   CEPX                                                             
         USING ACEPD,R6                                                         
         BAS   RE,SETOUT                                                        
         MVC   0(L'ACEPREP,R3),ACEPREP                                          
*                                                                               
CEPX     L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R7                                                               
         EJECT                                                                  
         SPACE 2                                                                
* ROUTINE FOR ORIGINAL ESTIMATE APPROVED DATE                                   
*                                                                               
OEAD     ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBORGVER,0          TEST FOR AN ORIGINAL ESTIMATE                
         BER   RE                  NO                                           
         ICM   R6,15,JBAOE                                                      
         BZR   RE                  NO ORIGINAL ESTIMATE BUFFER                  
*                                                                               
         MVI   ELCODE,ACEAELQ      LOOK FOR ESTIMATE APPROVED ELEM              
         BAS   RE,GETEL                                                         
         BNE   OEADX                                                            
         USING ACEAD,R6                                                         
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACEADATE),(17,(R3))                              
*                                                                               
OEADX    L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR ORIGINAL ESTIMATE PREPARED DATE                                   
*                                                                               
OEPD     ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBORGVER,0          TEST FOR AN ORIGINAL ESTIMATE                
         BER   RE                  NO                                           
         ICM   R6,15,JBAOE                                                      
         BZR   RE                  NO ORIGINAL ESTIMATE BUFFER                  
*                                                                               
         MVI   ELCODE,ACEPELQ      LOOK FOR ESTIMATE PREPARER ELEM              
         BAS   RE,GETEL                                                         
         BNE   OEPDX                                                            
         USING ACEPD,R6                                                         
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACEPDATE),(17,(R3))                              
*                                                                               
OEPDX    L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR CURRENT ESTIMATE APPROVED DATE                                    
*                                                                               
CEAD     ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBCURVER,0          TEST FOR A CURRENT ESTIMATE                  
         BER   RE                  NO                                           
         ICM   R6,15,JBACE                                                      
         BZR   RE                  NO CURRENT ESTIMATE BUFFER                   
*                                                                               
         MVI   ELCODE,ACEAELQ      LOOK FOR ESTIMATE APPROVED ELEM              
         BAS   RE,GETEL                                                         
         BNE   CEADX                                                            
         USING ACEAD,R6                                                         
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACEADATE),(17,(R3))                              
*                                                                               
CEADX    L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR CURRENT ESTIMATE PREPARED DATE                                    
*                                                                               
CEPD     ST    RE,SAVERE                                                        
         ICM   R7,15,TXAJBLK                                                    
         BZR   RE                                                               
         USING JBLOCKD,R7                                                       
         CLI   JBCURVER,0          TEST FOR A CURRENT ESTIMATE                  
         BER   RE                  NO                                           
         ICM   R6,15,JBACE                                                      
         BZR   RE                  NO CURRENT ESTIMATE BUFFER                   
*                                                                               
         MVI   ELCODE,ACEPELQ      LOOK FOR ESTIMATE PREPARER ELEM              
         BAS   RE,GETEL                                                         
         BNE   CEPDX                                                            
         USING ACEPD,R6                                                         
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(1,ACEPDATE),(17,(R3))                              
*                                                                               
CEPDX    L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R7                                                               
         EJECT                                                                  
* ROUTINE FOR USER FIELD REFERENCES                                             
*                                                                               
UF       ST    RE,SAVERE                                                        
         MVC   USERCODE,PARAMS                                                  
         L     R6,TXAJOB                                                        
         MVI   ELCODE,ACUFELQ      NOW LOOK FOR USER FIELD ELEMENTS             
         BAS   RE,GETEL                                                         
*                                                                               
UF2      BNE   UFX                                                              
*                                                                               
         USING ACUFD,R6                                                         
         CLC   USERCODE,ACUFCODE                                                
         BE    UF4                 NO MATCH                                     
         BAS   RE,NEXTEL                                                        
         B     UF2                                                              
*                                                                               
UF4      BAS   RE,SETOUT                                                        
         ZIC   R1,ACUFLEN                                                       
         SH    R1,=Y(ACUFDATA-ACUFD)                                            
         BZ    UFX                                                              
         CLI   TXSELFRM,C'E'                                                    
         BNE   *+12                                                             
         TM    ACUFSTAT,X'20'      TEST TO SHOW ON ESTIMATES                    
         BZ    UFX                                                              
         MVC   0(L'ACUFDESC,R3),ACUFDESC  DATA DESCRIPTION                      
         LA    R3,L'ACUFDESC+1(R3)                                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACUFDATA    ELEMENT DATA                                 
*                                                                               
UFX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO DISPLAY ALL THE USER FIELDS                                    
*                                                                               
UFALL    ST    RE,SAVERE                                                        
         MVC   BYTE,ACFDPCOL       SAVE COLUMN                                  
         BAS   RE,SETOUT                                                        
         ST    R3,AFSTCOL          SET FIRST COLUMN POINTER                     
         MVC   ACFDPCOL,PARAMS                                                  
         BAS   RE,SETOUT                                                        
         ST    R3,ASECCOL                                                       
         MVC   ACFDPCOL,BYTE                                                    
*                                                                               
         L     R6,TXAJOB                                                        
         MVI   ELCODE,ACUFELQ                                                   
         SR    R4,R4                                                            
         BAS   RE,GETEL                                                         
         B     UFALL2                                                           
*                                                                               
UFALL1   BAS   RE,NEXTEL                                                        
*                                                                               
         USING ACUFD,R6                                                         
*                                                                               
UFALL2   BNE   UFALLX                                                           
         CLI   TXSELFRM,C'E'                                                    
         BNE   *+12                                                             
         TM    ACUFSTAT,X'20'      TEST TO SHOW ON ESTIMATE                     
         BZ    UFALL1                                                           
*                                                                               
         ZIC   R1,ACUFLEN                                                       
         SH    R1,=Y(ACUFDATA-ACUFD)                                            
         BZ    UFALL1                                                           
*                                                                               
         LA    R4,1(R4)            INCREMENT FIELD NUMBER                       
         STC   R4,BYTE                                                          
         LA    R5,AFSTCOL                                                       
         TM    BYTE,X'01'          TEST FOR ODD NUMBER                          
         BO    *+8                 YES-USE FIRST COLUMN                         
         LA    R5,ASECCOL                                                       
*                                                                               
         L     R3,0(R5)            GET OUTPUT POINTER                           
         MVC   0(L'ACUFDESC,R3),ACUFDESC MOVE OUT DESCRIPTION                   
         LA    R3,L'ACUFDESC+1(R3) BUMP PAST DESCRIPTION                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     UFALL4                                                           
         MVC   0(0,R3),ACUFDATA                                                 
*                                                                               
UFALL4   SH    R3,=Y(L'ACUFDESC+1) BACK UP TO START OF DESC                     
         A     R3,TXLPRT           UPDATE POINTER                               
         ST    R3,0(R5)                                                         
         B     UFALL1                                                           
*                                                                               
UFALLX   L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO DISPLAY THE USER FIELD LIST                                    
*                                                                               
UFL      ST    RE,SAVERE                                                        
         MVC   BYTE,ACFDPCOL       SAVE COLUMN                                  
         BAS   RE,SETOUT                                                        
         ST    R3,AFSTCOL          SET FIRST COLUMN POINTER                     
         MVC   ACFDPCOL,PARAMS                                                  
         BAS   RE,SETOUT                                                        
         ST    R3,ASECCOL                                                       
         MVC   ACFDPCOL,BYTE                                                    
         LA    R2,PARAMS+1         R2=A(USER FIELD                              
*                                                                               
UFL2     MVC   USERCODE,0(R2)                                                   
         CLC   USERCODE,SPACES                                                  
         BE    UFLX                ALL DONE                                     
         MVI   ELCODE,ACUFELQ                                                   
         L     R6,TXAJOB                                                        
         BAS   RE,GETEL                                                         
*                                                                               
UFL3     BNE   UFL6                                                             
         USING ACUFD,R6                                                         
         CLC   USERCODE,ACUFCODE                                                
         BE    UFL4                                                             
         BAS   RE,NEXTEL                                                        
         B     UFL3                                                             
*                                                                               
UFL4     CLI   TXSELFRM,C'E'       TEST FOR ESTIMATE                            
         BNE   *+12                                                             
         TM    ACUFSTAT,X'20'                                                   
         BZ    UFL6                                                             
         ZIC   R1,ACUFLEN                                                       
         SH    R1,=Y(ACUFDATA-ACUFD)                                            
         BZ    UFL6                                                             
*                                                                               
         LA    R4,1(R4)            INCREMENT FIELD COUNT                        
         STC   R4,BYTE                                                          
         LA    R5,AFSTCOL                                                       
         TM    BYTE,X'01'          TEST FOR ODD NUMBER                          
         BO    *+8                                                              
         LA    R5,ASECCOL                                                       
         L     R3,0(R5)                                                         
         MVC   0(L'ACUFDESC,R3),ACUFDESC                                        
         LA    R3,L'ACUFDESC+1(R3)                                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACUFDATA                                                 
         A     R3,TXLPRT                                                        
         ST    R3,0(R5)                                                         
*                                                                               
UFL6     LA    R2,2(R2)            NEXT USER FIELD CODE                         
         B     UFL2                                                             
*                                                                               
UFLX     L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR BILL NUMBER                                                       
*                                                                               
BN       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         MVC   0(L'TXSELBIL,R3),TXSELBIL                                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR BILL DATE                                                         
*                                                                               
BD       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(2,TXSELBDT),(17,0(R3))                             
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR DUE DATE                                                          
*                                                                               
DD       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         GOTO1 CDATCON,DMCB,(2,TXSELDDT),(17,0(R3))                             
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR BILLING CURRENCY                                                  
*                                                                               
CUR      ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         MVC   0(L'TXSELCUR,R3),TXSELCUR                                        
         L     RE,SAVERE                                                        
         CLC   TXSELCUR,=C'TRL'    TEST THIS IS TURKISH LIRE                    
         BNER  RE                                                               
         MVC   L'TXSELCUR+1(7,R3),=C'(000''S)'                                  
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR CLIENT VAT NUMBER                                                 
*                                                                               
CV       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     RF,TXAPRO                                                        
         AH    RF,=Y(49)                                                        
         SR    RE,RE                                                            
         USING FFTELD,RF                                                        
CV10     CLI   FFTEL,0                                                          
         BE    CV20                E-O-R TRY CLIENT                             
         CLI   FFTEL,FFTELQ                                                     
         BNE   *+12                                                             
         CLI   FFTTYPE,FFTTAXNO                                                 
         BE    CV40                                                             
         IC    RE,FFTLN                                                         
         AR    RF,RE                                                            
         B     CV10                                                             
*                                                                               
CV20     L     RF,TXACLI                                                        
         AH    RF,=Y(49)                                                        
CV25     CLI   FFTEL,0                                                          
         BE    CV50                                                             
         CLI   FFTEL,FFTELQ                                                     
         BNE   *+12                                                             
         CLI   FFTTYPE,FFTTAXNO                                                 
         BE    CV40                                                             
         IC    RE,FFTLN                                                         
         AR    RF,RE                                                            
         B     CV25                                                             
*                                                                               
CV40     IC    RE,FFTDLEN                                                       
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   0(0,R3),FFTDATA                                                  
*                                                                               
CV50     L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  RF                                                               
         SPACE 2                                                                
* ROUTINE FOR OFFICE ADDRESS (IDI DESTINATION ADDRESS)                          
*                                                                               
OA       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         ICM   RE,15,TXADEST                                                    
         BZ    OAX                 ADDRESS NOT PASSED                           
         LA    R0,3                                                             
         MVC   0(33,R3),33(RE)                                                  
         LA    RE,33(RE)                                                        
         A     R3,TXLPRT                                                        
         BCT   R0,*-14                                                          
OAX      L     RE,SAVERE                                                        
         BR    RE                                                               
*                                  JOB COMMENTS                                 
JCO      ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         ST    R3,AFSTCOL                                                       
         MVI   ELCODE,X'3E'                                                     
         L     R6,TXAJOB                                                        
         BAS   RE,GETEL                                                         
JCO2     BNE   JCOX                                                             
         USING ACOMMD,R6                                                        
         L     R3,AFSTCOL                                                       
         SR    RE,RE                                                            
         IC    RE,ACOMLEN                                                       
         SH    RE,=Y(ACOMMENT+1-ACOMMD)                                         
         EX    RE,*+4                                                           
         MVC   0(0,R3),ACOMMENT                                                 
         A     R3,TXLPRT                                                        
         ST    R3,AFSTCOL                                                       
         BAS   RE,NEXTEL                                                        
         B     JCO2                                                             
JCOX     L     RE,SAVERE                                                        
         BR    RE                                                               
*                                  DEBTORS CODE                                 
DC       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         MVI   ELCODE,X'24'        GET PROFILE ELEMENT FROM JOB                 
         L     R6,TXAJOB                                                        
         BAS   RE,GETEL                                                         
         BNE   *+8                                                              
         BAS   RE,DC04                                                          
         L     R6,TXAPRO           GET PROFILE ELEMENT FROM PRODUCT             
         BAS   RE,GETEL                                                         
         BNE   *+8                                                              
         BAS   RE,DC04                                                          
         L     R6,TXACLI           GET PROFILE ELEMENT FROM CLIENT              
         BAS   RE,GETEL                                                         
         BNE   DCX                                                              
         USING ACPROFD,R6                                                       
DC04     CLC   ACPRRECV,SPACES                                                  
         BNHR  RE                                                               
         MVC   0(L'ACPRRECV-3,R3),ACPRRECV+3                                    
DCX      L     RE,SAVERE                                                        
         BR    RE                                                               
*                                  BILL STATUS                                  
BST      ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         CLI   TXSELDFT,C'D'                                                    
         BNE   BSTX                                                             
         MVC   0(L'AC@DRAFT,R3),AC@DRAFT                                        
BSTX     L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R6                                                               
*                                                                               
AG1T     MVI   BYTE,1              ACCOUNT GROUP 1 TYPE                         
         B     *+8                                                              
AG2T     MVI   BYTE,2              ACCOUNT GROUP 2 TYPE                         
         ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     R1,TXACOMP                                                       
         LA    R1,ACCORFST(R1)                                                  
         SR    R0,R0                                                            
         USING FFTELD,R1                                                        
AG1T02   IC    R0,FFTLN                                                         
         AR    R1,R0                                                            
         CLI   FFTEL,0                                                          
         BE    AG1TX                                                            
         CLI   FFTEL,FFTELQ                                                     
         BNE   AG1T02                                                           
         CLI   FFTTYPE,FFTTAAGR                                                 
         BNE   AG1T02                                                           
         CLC   FFTSEQ,BYTE                                                      
         BNE   AG1T02                                                           
         ICM   RF,1,FFTDLEN                                                     
         BZ    AG1TX                                                            
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   0(0,R3),FFTDATA                                                  
AG1TX    L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
AG1C     MVI   BYTE,AGRLN1Q        ACCOUNT GROUP 1 CODE                         
         B     *+8                                                              
AG2C     MVI   BYTE,AGRLN2Q        ACCOUNT GROUP 2 CODE                         
         ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     R1,TXAPRO                                                        
         LA    R1,ACCORFST(R1)                                                  
         SR    R0,R0                                                            
         USING AGRELD,R1                                                        
AG1C02   IC    R0,AGRLN                                                         
         AR    R1,R0                                                            
         CLI   AGREL,0                                                          
         BE    AG1CX                                                            
         CLI   AGREL,AGRELQ                                                     
         BNE   AG1C02                                                           
         CLC   AGRLN,BYTE          TEST DATA CARRIED IN THIS ELEMENT            
         BL    AG1CX                                                            
         IC    R0,BYTE                                                          
         SH    R0,=Y(L'AGRAGR1)                                                 
         AR    R1,R0               INDEX TO 1ST/2ND ACCOUNT GROUP CODE          
         MVC   0(L'AGRAGR1,R3),0(R1)                                            
AG1CX    L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
AG1N     MVI   BYTE,AGRLN1Q        ACCOUNT GROUP 1 CODE                         
         B     *+8                                                              
AG2N     MVI   BYTE,AGRLN2Q        ACCOUNT GROUP 2 CODE                         
         ST    RE,SAVERE                                                        
         BAS   RE,SETOUT                                                        
         L     R1,TXAPRO                                                        
         LA    RF,KEY              BUILD KEY FOR ACCOUNT GROUP RECORD           
         USING AGRRECD,RF                                                       
         XC    AGRKEY,AGRKEY                                                    
         MVI   AGRKTYP,AGRKTYPQ                                                 
         MVC   AGRKCPY,ACTKCPY-ACTRECD(R1)                                      
         MVI   AGRKGTYP,AGRKGTY1                                                
         CLI   BYTE,AGRLN1Q        TEST ACCOUNT GROUP 1/2 CODE WANTED           
         BE    *+8                                                              
         MVI   AGRKGTYP,AGRKGTY2                                                
         LA    R1,ACCORFST(R1)                                                  
         SR    R0,R0                                                            
         USING AGRELD,R1                                                        
AG1N02   IC    R0,AGRLN                                                         
         AR    R1,R0                                                            
         CLI   AGREL,0                                                          
         BE    AG1NX                                                            
         CLI   AGREL,AGRELQ                                                     
         BNE   AG1N02                                                           
         CLC   AGRLN,BYTE          TEST DATA CARRIED IN THIS ELEMENT            
         BL    AG1NX                                                            
         IC    R0,BYTE                                                          
         SH    R0,=Y(L'AGRAGR1)                                                 
         AR    R1,R0               INDEX TO 1ST/2ND ACCOUNT GROUP CODE          
         MVC   AGRKAGR,0(R1)       ACCOUNT GROUP 1/2 CODE INTO KEY              
         MVC   AIO,AREC                                                         
         BAS   RE,READ                                                          
         BNE   AG1NX                                                            
         L     R1,AREC                                                          
         LA    R1,ACCORFST(R1)                                                  
         SR    R0,R0                                                            
         USING NAMELD,R1                                                        
AG1N04   CLI   NAMEL,0                                                          
         BE    AG1NX                                                            
         CLI   NAMEL,NAMELQ                                                     
         BE    AG1N06                                                           
         IC    R0,NAMLN                                                         
         AR    R1,R0                                                            
         B     AG1N04                                                           
AG1N06   SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+4                                                           
         MVC   0(0,R3),NAMEREC                                                  
AG1NX    L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R1,RF                                                            
         SPACE 2                                                                
* SUB-ROUTINE TO OUTPUT THE CLIENT/PRODUCT/JOB NAME                             
*                                                                               
GETNAME  NTR1  ,                                                                
         MVI   ELCODE,ACNMELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GETNAMEX                                                         
         USING ACNAMED,R6                                                       
         BAS   RE,SETOUT                                                        
         ZIC   R1,ACNMLEN                                                       
         SH    R1,=Y(ACNMNAME-ACNAMED+1)                                        
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACNMNAME                                                 
*                                                                               
GETNAMEX B     EXIT                                                             
         SPACE 2                                                                
* ROUTINE FOR OTHER INFO                                                        
*                                                                               
OI       ST    RE,SAVERE                                                        
         L     R6,TXAJOB           TRY JOB FIRST                                
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,OI2              SET RETURN FOR PRODUCT                       
         BE    OI10                                                             
*                                                                               
OI2      L     R6,TXAPRO           THEN PRODUCT                                 
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,OI4              SET RETURN FOR CLIENT                        
         BE    OI10                                                             
*                                                                               
OI4      L     R6,TXACLI           THEN CLIENT                                  
         MVI   ELCODE,ACPRELQ                                                   
         BAS   RE,GETEL                                                         
         LA    RE,OIX                                                           
         BNE   OIX                 NOTHING TO PRINT                             
*                                                                               
         USING ACPROFD,R6                                                       
OI10     CLI   ACPRLEN,(ACPRNARR-ACPROFD)                                       
         BNHR  RE                  NO OTHER INFORMATION                         
         BAS   RE,SETOUT           R3=A(1ST PRINT LINE)                         
         MVC   0(L'ACPRBLPR,R3),ACPRNARR+0                                      
         A     R3,TXLPRT           R3=A(2ND PRINT LINE)                         
         CLI   ACPRLEN,(ACPRNARR-ACPROFD)+(1*L'ACPRBLPR)                        
         BNH   OIX                                                              
         MVC   0(L'ACPRBLPR,R3),ACPRNARR+(1*L'ACPRBLPR)                         
         A     R3,TXLPRT           R3=A(3RD PRINT LINE)                         
         CLI   ACPRLEN,(ACPRNARR-ACPROFD)+(2*L'ACPRBLPR)                        
         BNH   OIX                                                              
         MVC   0(L'ACPRBLPR,R3),ACPRNARR+(2*L'ACPRBLPR)                         
*                                                                               
OIX      L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
* ROUTINE FOR ESTIMATE NUMBER                                                   
*                                                                               
EN       ST    RE,SAVERE                                                        
         BAS   RE,SETOUT           R3=A(1ST PRINT LINE)                         
         MVC   0(L'TXSELENO,R3),TXSELENO                                        
ENX      L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
* SUB-ROUTINE TO GET THE ADDRESS, AT ENTRY, R6=A(ADDRESS ELEMENT)               
*                                                                               
         USING ACADDD,R6                                                        
GETADD   NTR1                                                                   
         BAS   RE,SETOUT                                                        
         SR    R0,R0                                                            
         ZIC   R1,ACADLEN                                                       
         SH    R1,=Y(ACADADD-ACADDD)                                            
         LA    RF,L'ACADADD                                                     
         DR    R0,RF                                                            
         LA    R2,ACADADD                                                       
*                                                                               
GETADD2  MVC   0(L'ACADADD,R3),0(R2)                                            
         LA    R2,L'ACADADD(R2)    NEXT ADDRESS LINE                            
         A     R3,TXLPRT           NEXT PRINT LINE                              
         BCT   R1,GETADD2                                                       
*                                                                               
GETADDX  B     EXIT                                                             
         SPACE 2                                                                
* SUB-ROUTINE TO SET OUTPUT ADDRESS IN R3                                       
*                                                                               
         USING ACFDD,R5                                                         
SETOUT   L     R2,TXLPRT           R2=L'PRINT LINE                              
         ZIC   R3,ACFDPLIN         R3=PRINT LINE (RELATIVE)                     
         BCTR  R3,0                                                             
         MR    R2,R2               COMPUTE DISPLACEMENT TO PRINT LINE           
         ZIC   R2,ACFDPCOL                                                      
         BCTR  R2,0                                                             
         AR    R3,R2                                                            
         A     R3,TXAPRT                                                        
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO SET A JOB'S MEDIA AND MEDIA GROUP CODES                        
* ON EXIT, MEDCODE AND MGCODE SET AND AMEDEL=A(MEDIA ELEMENT)                   
*          AESTNAM=A(ESTIMATE NAME ELEMENT)                                     
*          CC=NEQ IF MEDIA ELEMENT FOUND, EQ=MEDIA NOT FOUND                    
*                                                                               
SETMED   NTR1  ,                                                                
         BAS   RE,SETLEN                                                        
         L     R4,TXAJOB                                                        
         USING ACKEYD,R4                                                        
         ZIC   R1,LCLIPRO                                                       
         LA    R1,ACKEYACC+3(R1)                                                
         MVC   MEDCODE,0(R1)                                                    
         XC    AMEDEL,AMEDEL                                                    
         XC    AESTNAM,AESTNAM                                                  
*                                                                               
SETMED2  L     R6,TXAMED                                                        
         SR    R0,R0                                                            
*                                                                               
         USING ACMEDIAD,R6                                                      
SETMED4  CLI   ACMDEL,0                                                         
         BE    SETMEDX                                                          
         CLI   ACMDEL,ACMDELQ                                                   
         BNE   SETMED5                                                          
         CLC   ACMDCODE,MEDCODE                                                 
         BNE   SETMED6                                                          
         ST    R6,AMEDEL                                                        
         MVC   MGCODE,ACMDGRP      EXTRACT MEDIA GROUP CODE                     
         B     SETMED6                                                          
         SPACE 1                                                                
         USING ACMND,R6                                                         
SETMED5  CLI   ACMNEL,ACMNELQ                                                   
         BNE   SETMED6                                                          
         CLC   ACMNCODE,MEDCODE    TEST FOR RIGHT MEDIA                         
         BNE   SETMED6                                                          
         CLI   ACMNTYPE,ACMNEST                                                 
         BNE   SETMED6                                                          
         ST    R6,AESTNAM                                                       
         SPACE 1                                                                
SETMED6  IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SETMED4                                                          
         SPACE 1                                                                
SETMEDX  OC    AMEDEL,AMEDEL       SET CC ON EXIT                               
         B     EXIT                                                             
         DROP  R4,R6                                                            
         SPACE 2                                                                
* SUB-ROUTINE TO SET THE LENGTH OF KEY COMPONENTS IN LEDGER RECORD              
*                                                                               
SETLEN   ST    RE,SAVESETL                                                      
         MVI   ELCODE,ACHRELQ                                                   
         L     R6,TXALEDG                                                       
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING ACHEIRD,R6                                                       
         MVC   LCLI,ACHRLEVA                                                    
*                                                                               
         MVC   LCLIPRO,ACHRLEVB    L'CLIENT+PRODUCT                             
         ZIC   R1,ACHRLEVB                                                      
         ZIC   R0,ACHRLEVA                                                      
         SR    R1,R0                                                            
         STC   R1,LPRO                                                          
*                                                                               
         ZIC   R1,ACHRLEVC                                                      
         ZIC   R0,ACHRLEVB                                                      
         SR    R1,R0                                                            
         STC   R1,LJOB                                                          
*                                                                               
SETLENX  L     RE,SAVESETL                                                      
         BR    RE                                                               
         EJECT                                                                  
* SUB-ROUTINE TO CALL USER HOOK                                                 
*                                                                               
GO       OC    TXAHOOK,TXAHOOK                                                  
         BZR   RE                                                               
         CLI   TXACTNUM,0          TEST FOR ANY OUTPUT                          
         BER   RE                  NO                                           
         NTR1  ,                                                                
         L     RF,TXAHOOK                                                       
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)        RESTORE CALLER'S REGS                        
         BASR  RE,RF                                                            
         XIT1  ,                                                                
         SPACE 2                                                                
OK       CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
NOTOK    LTR   RB,RB                                                            
         B     EXIT                                                             
         SPACE 2                                                                
         GETEL (R6),49,ELCODE                                                   
         SPACE 2                                                                
* DATA MANAGER INTERFACE                                                        
*                                                                               
READ     NTR1  ,                                                                
         MVC   COMMAND,DMREAD                                                   
         B     ACCFILE                                                          
         SPACE 1                                                                
SEQ      NTR1  ,                                                                
SEQSKIP  MVC   COMMAND,DMRSEQ                                                   
         OI    DMINBTS,X'08'       PASS DELETES                                 
         B     ACCFILE                                                          
         SPACE 1                                                                
HIGH     NTR1  ,                                                                
         MVC   COMMAND,DMRDHI                                                   
         SPACE 1                                                                
ACCFILE  MVC   KEYSAVE,KEY                                                      
         GOTO1 CDATAMGR,DMCB,(DMINBTS,COMMAND),ACCOUNT,KEY,AIO,0                
         L     RE,AIO              EXTRACT KEY FROM RECORD                      
         MVC   KEY,0(RE)                                                        
         CLC   COMMAND,DMREAD      TEST FOR READ                                
         BE    ACCFILE2            YES                                          
         TM    ACSTATUS-ACKEYD(RE),X'80' TEST FOR DELETED RECORD                
         BO    SEQSKIP             SKIP DELETED RECORD                          
         SPACE 1                                                                
ACCFILE2 NI    DMINBTS,X'FF'-X'08' TURN OFF PASS DELETES                        
         TM    DMCB+8,X'12'                                                     
         BNZ   NOTOK               RECORD NOT FOUND/DELETED                     
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     OK                                                               
         EJECT                                                                  
* PATCH AREA                                                                    
*                                                                               
PATCH    DS    0H                                                               
         DC    XL32'00'                                                         
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
DEFAULT  DC    C'9999'                                                          
ACCOUNT  DC    C'ACCOUNT'                                                       
DMRDHI   DC    C'DMRDHI'                                                        
DMREAD   DC    C'DMREAD'                                                        
DMRSEQ   DC    C'DMRSEQ'                                                        
SPACES   DC    CL198' '                                                         
         SPACE 2                                                                
* TABLE OF FUNCTIONS AND BRANCH ADDRESSES                                       
*                                                                               
FUNCTAB  DS    0A                                                               
         DC    A(TEXTS)            1=TXGETTXT                                   
         DC    A(PANEL)            2=TXPROPAN                                   
         DC    A(HEAD)             3=TXGETHD                                    
         DC    A(CONFOOT)          4=TXGETFT                                    
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
*                                                                               
DDIN     DS    0C                                                               
         DCDDL AC#DATE,5                                                        
         DCDDL AC#PAGE,5                                                        
         DCDDL AC#RUNAT,6                                                       
         DCDDL AC#PRDES,19                                                      
         DCDDL AC#DRAFT,8                                                       
         DC    X'00'                                                            
         SPACE 2                                                                
* TABLE OF INTERNALLY GENERATED DATA AND DISPLACEMENTS TO ROUTINES              
*                                                                               
         DS    0H                                                               
INTTAB   DS    0CL4                                                             
         DC    C'PE',Y(PE-ACGETTXT)                                             
         DC    C'AN',Y(AN-ACGETTXT)                                             
         DC    C'AA',Y(AA-ACGETTXT)                                             
         DC    C'TD',Y(TD-ACGETTXT)                                             
         DC    C'PG',Y(PG-ACGETTXT)                                             
         DC    C'CC',Y(CC-ACGETTXT)                                             
         DC    C'CN',Y(CN-ACGETTXT)                                             
         DC    C'PC',Y(PC-ACGETTXT)                                             
         DC    C'PN',Y(PN-ACGETTXT)                                             
         DC    C'JC',Y(JC-ACGETTXT)                                             
         DC    C'JN',Y(JN-ACGETTXT)                                             
         DC    C'OP',Y(OP-ACGETTXT)                                             
         DC    C'CL',Y(CL-ACGETTXT)                                             
         DC    C'BA',Y(BA-ACGETTXT)                                             
         DC    C'PB',Y(PB-ACGETTXT)                                             
         DC    C'MC',Y(MC-ACGETTXT)                                             
         DC    C'MN',Y(MN-ACGETTXT)                                             
         DC    C'UF',Y(UF-ACGETTXT)                                             
INTNTRYS EQU   (*-INTTAB)/L'INTTAB                                              
         SPACE 2                                                                
         DS    0H                                                               
INTTAB2  DS    0CL(L'ROUTINE+2)                                                 
         DC    CL8'PE',Y(PE-ACGETTXT)                                           
         DC    CL8'AN',Y(AN-ACGETTXT)                                           
         DC    CL8'AA',Y(AA-ACGETTXT)                                           
         DC    CL8'TD',Y(TD-ACGETTXT)                                           
         DC    CL8'RT',Y(RT-ACGETTXT)                                           
         DC    CL8'RTS',Y(RTS-ACGETTXT)                                         
         DC    CL8'PG',Y(PG-ACGETTXT)                                           
         DC    CL8'CC',Y(CC-ACGETTXT)                                           
         DC    CL8'CN',Y(CN-ACGETTXT)                                           
         DC    CL8'PC',Y(PC-ACGETTXT)                                           
         DC    CL8'PN',Y(PN-ACGETTXT)                                           
         DC    CL8'JC',Y(JC-ACGETTXT)                                           
         DC    CL8'JN',Y(JN-ACGETTXT)                                           
         DC    CL8'OP',Y(OP-ACGETTXT)                                           
         DC    CL8'CL',Y(CL-ACGETTXT)                                           
         DC    CL8'BA',Y(BA-ACGETTXT)                                           
         DC    CL8'PB',Y(PB-ACGETTXT)                                           
         DC    CL8'MC',Y(MC-ACGETTXT)                                           
         DC    CL8'MN',Y(MN-ACGETTXT)                                           
         DC    CL8'OEA',Y(OEA-ACGETTXT)                                         
         DC    CL8'OEP',Y(OEP-ACGETTXT)                                         
         DC    CL8'CEA',Y(CEA-ACGETTXT)                                         
         DC    CL8'CEP',Y(CEP-ACGETTXT)                                         
         DC    CL8'OEAD',Y(OEAD-ACGETTXT)                                       
         DC    CL8'OEPD',Y(OEPD-ACGETTXT)                                       
         DC    CL8'CEAD',Y(CEAD-ACGETTXT)                                       
         DC    CL8'CEPD',Y(CEPD-ACGETTXT)                                       
         DC    CL8'UF',Y(UF-ACGETTXT)                                           
         DC    CL8'UFALL   ',Y(UFALL-ACGETTXT)                                  
         DC    CL8'UFLIST  ',Y(UFL-ACGETTXT)                                    
         DC    CL8'CON',Y(CON-ACGETTXT)                                         
         DC    CL8'COA',Y(COA-ACGETTXT)                                         
         DC    CL8'TDO',Y(TD1-ACGETTXT)                                         
*                                  NEW BILLING DATA TYPES                       
         DC    CL8'BN',Y(BN-ACGETTXT)                                           
         DC    CL8'BD',Y(BD-ACGETTXT)                                           
         DC    CL8'DD',Y(DD-ACGETTXT)                                           
         DC    CL8'CUR',Y(CUR-ACGETTXT)                                         
         DC    CL8'CV',Y(CV-ACGETTXT)                                           
         DC    CL8'JCO',Y(JCO-ACGETTXT)                                         
         DC    CL8'OA',Y(OA-ACGETTXT)                                           
         DC    CL8'DC',Y(DC-ACGETTXT)                                           
         DC    CL8'BST',Y(BST-ACGETTXT)                                         
         DC    CL8'AG1T',Y(AG1T-ACGETTXT)                                       
         DC    CL8'AG1C',Y(AG1C-ACGETTXT)                                       
         DC    CL8'AG1N',Y(AG1N-ACGETTXT)                                       
         DC    CL8'AG2T',Y(AG2T-ACGETTXT)                                       
         DC    CL8'AG2C',Y(AG2C-ACGETTXT)                                       
         DC    CL8'AG2N',Y(AG2N-ACGETTXT)                                       
         DC    CL8'OI',Y(OI-ACGETTXT)                                           
         DC    CL8'EN',Y(EN-ACGETTXT)                                           
*                                                                               
INTNTRY2 EQU   (*-INTTAB2)/L'INTTAB2                                            
         EJECT                                                                  
* DSECT TO COVER ACGETTXT WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
USERRD   DS    A                                                                
APARM    DS    A                                                                
PARMS    DS    0A                                                               
ATXBLOCK DS    A                                                                
ATRUP    DS    A                   TRANSLATE UPPER CASE TABLE GERMANY           
PARML    EQU   *-PARMS                                                          
SAVERE   DS    A                                                                
SAVESETL DS    A                   SETLEN SR RETURN POINT                       
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
BYTE     DS    C                                                                
*                                  SETLEN OUTPUT AREA                           
LCLI     DS    X                   L'CLIENT CODE                                
LCLIPRO  DS    X                   L'CLIENT CODE + L'PRODUCT CODE               
LPRO     DS    X                   L'PRODUCT CODE                               
LJOB     DS    X                   L'JOB CODE                                   
*                                                                               
HALF     DS    H                                                                
AMPM     DS    CL2                 AM OR PM NOTATION                            
*                                                                               
ROUTINE  DS    CL8                 INTERNAL DATA ROUTINE NAME                   
PARAML   DS    X                   LENGTH OF PARAMETERS TO ROUTINE              
PARAMS   DS    CL80                PARAMETERS TO INTERNAL ROUTINE               
PLANG    DS    X                   PASSED LANGUAGE CODE                         
*                                                                               
MEDFLAG  DS    X                   MEDIA FLAG                                   
MEDLEVQ  EQU   1                   MEDIA LEVEL READ                             
AFSTCOL  DS    A                                                                
ASECCOL  DS    A                                                                
USERCODE DS    CL(L'ACUFCODE)                                                   
MEDCODE  DS    CL(L'ACMDCODE)                                                   
MGCODE   DS    CL(L'ACMDGRP)                                                    
AMEDEL   DS    A                                                                
AESTNAM  DS    A                   A(ESTIMATE NAME ELEMENT)                     
TXSW     DS    C                                                                
ELCODE   DS    X                                                                
DMINBTS  DS    X                                                                
COMMAND  DS    CL6                                                              
*                                                                               
AIO      DS    A                   A(IO AREA FOR READ)                          
AREC     DS    A                   A(GENERAL RECORD AREA)                       
ATEXT    DS    A                   A(TEXT RECORD)                               
TEXTKEY  DS    CL(L'ACTXKEY)       TEXT KEY TO READ WITH                        
*                                                                               
KEY      DS    CL(ACLENGTH-ACKEYD)                                              
KEYSAVE  DS    CL(L'KEY)                                                        
*                                                                               
DDOUT    DS    0C                                                               
         DSDDL PRINT=YES                                                        
*                                                                               
IO       DS    CL2000                                                           
TEXTREC  DS    CL2000                                                           
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* EQUATES                                                                       
*                                                                               
MAXLIN   EQU   20                                                               
         SPACE 2                                                                
TXBLOCKD DSECT                                                                  
       ++INCLUDE ACTXBLOCK                                                      
         SPACE 2                                                                
* ACGENBOTH                                                                     
* ACGENFILE                                                                     
* ACJOBBLOCK                                                                    
* DDCOMFACS                                                                     
* DDMASTD                                                                       
* ACDDEQUS                                                                      
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENFILE                                                      
JBLOCKD  DSECT                                                                  
       ++INCLUDE ACJOBBLOCK                                                     
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE ACDDEQUS                                                       
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'043ACCLBTXT  05/01/02'                                      
         END                                                                    
