*          DATA SET PZADDINVS  AT LEVEL 012 AS OF 05/01/02                      
*CATALP PZADDINV                                                                
*        TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - INIT'                     
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - INIT'                     
***********************************************************************         
*                                                                     *         
*        USED WITH PPZ502 - LINK WITH LKPPZ502                        *         
*                                                                     *         
***********************************************************************         
*                                                                     *         
*  BOBY NOV/94    CREATION                                            *         
*                                                                     *         
***********************************************************************         
         PRINT NOGEN                                                            
PZADDINV CSECT                                                                  
         NMOD1 PZADIDL,**PZADI                                                  
*                                                                               
         LA    RA,COMSUBS          ESTABLISH COMMON SUBROUTINES                 
         USING COMSUBS,RA                                                       
*                                                                               
         USING PZADID,RC           ESTABLISH WORKING STORAGE                    
*                                                                               
         L     R7,0(R1)            A(EZBLOCK)                                   
         USING EZBLOCKD,R7         ESTABLISH INCOMING EZBLOCK                   
*                                                                               
         L     R8,4(R1)            A(PPWORKD)                                   
         USING PPWORKD,R8          ESTABLISH REPORT WORKING STORAGE             
         L     R9,PPWORK2C         POINT TO SECOND PART OF STORAGE              
         USING PPWORK2D,R9         ESTABLISH SECOND REPORT WORKING STG          
*                                                                               
         MVI   EZERRCD,0           CLEAR ERROR                                  
*                                                                               
         CLI   ADSCTL,ADS1STQ      SKIP IF NOT FIRST TIME                       
         BNE   *+12                                                             
         BAS   RE,MININIT             INIT STORAGE AREAS                        
         MVI   ADSCTL,0               CLEAR FIRST TIME SWITCH                   
*                                                                               
         LA    R5,MINBLKBF         USE BUFFER MINIO KEY                         
         USING MINBLKD,R5                                                       
*                                                                               
         CLI   EZMODE,EZINVP       PROC INV                                     
         BE    PRINV                                                            
*                                                                               
         CLI   EZMODE,EZSPTP       PROCESS PRINT DETAIL                         
         BE    PRANN                                                            
*                                                                               
         CLI   EZMODE,EZINVL       LAST FOR INVOICE                             
         BE    LINV                                                             
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRINV'                    
***********************************************************************         
*                                                                     *         
*        PROCESS INVOICE HEADER                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRINV    DS    0H                                                               
*                                                                               
         XC    EZIHCINV,EZIHCINV                                                
*                                                                               
         OC    EZIHLADC,EZIHLADC   TEST HAVE ADVERTISER                         
         BZ    PRERRANF                                                         
*                                                                               
         TM    EZIHLKST,EZIHLANF   AND IS OK PRINTPAK CODE                      
         BO    PRERRANF                                                         
*                                                                               
         LA    R0,SAVINVH          A(INVOICE HEADER SAVE AREA)                  
         L     RE,EZWKRREC         A(INVOICE HEADER RECORD)                     
         LH    R1,0(RE)            INVOICE HEADER LENGTH                        
         CH    R1,=Y(L'SAVINVH)                                                 
         BNH   *+6                                                              
         DC    H'0'                INVOICE HEADER RECORD TOO LONG               
         LR    RF,R1               LENGTH OF SAVEAREA TO USE                    
         MVCL  R0,RE               SAVE INVOICE HEADER RECORD                   
*                                                                               
         L     R0,=A(SAVWKBUF)     A(WORKER BUFFER SAVE AREA)                   
         LH    R1,=Y(L'SAVWKBUF)   SAVE AREA LENGTH                             
         L     RE,EZWKRBUF         A(WORKER BUFFER)                             
         LR    RF,R1               LENGTH OF SAVEAREA TO USE                    
         MVCL  R0,RE               SAVE WORKER BUFFER AREA                      
*                                                                               
*        BUILD MASTER INVOICE KEY                                               
*                                                                               
         LA    R5,MINBLK           USE FILE MINIO BLOCK                         
         USING MINBLKD,R5                                                       
*                                                                               
         MVI   MINOPEN,C'N'        FORCE NEW RECORD                             
*                                                                               
         XC    MINMKEY,MINMKEY                                                  
         LA    R4,MINMKEY          ESTABLISH NEW INVOICE RECORD KEY             
         USING PINVKEY,R4                                                       
*                                                                               
         MVC   PINVAGY,EZAGY       AGENCY ALPHA CODE                            
         MVC   PINVMED,EZMED       MEDIA                                        
         MVI   PINVTYPE,PINVTYPQ   RECORD TYPE                                  
         MVC   PINVCLT,EZIHLADC    CLIENT                                       
         MVC   PINVPRD,EZIHLPRC    PRODUCT                                      
*                                                                               
         OC    PINVPRD,PINVPRD     IF PRODUCT IS MISSING                        
         BNZ   *+10                                                             
         MVC   PINVPRD,=3X'FF'        SET TO VARIOUS                            
*                                                                               
         MVC   PINVYS,EZIHBMOS     YEAR OF SERVICE                              
*                                                                               
         MVC   MINMKEY-MINBLKD+MINBLKBF,PINVKEY  COPY KEY TO BFR BLK            
*                                                                               
         MVI   MINOPEN-MINBLKD+MINBLKBF,C'N'     FORCE NEW RECORD               
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINOPN',MINBLKBF) OPEN BUFFER MINIO BLCK         
*                                                                               
         CLI   MINERR,0            NO ERRORS TOLERTED                           
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINOPN',MINBLKD)    OPEN FILE MINIO BLCK         
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,PINVKEY             DUMP KEY                                  
         BAS   RE,DMPREC                                                        
*                                                                               
         CLI   MINERR,MINESNF      SKIP IF RECORD NOT FOUND                     
         BE    PRHDRSQX                                                         
*                                                                               
         CLI   MINERR,0            NO OTHER ERRORS TOLERTED                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*        FIND NEXT AVAILABLE HEADER SEQUENCE NUMBER                             
*        OR CURRENT SEQUENCE NUMBER IF REPLACING INVOICE                        
*                                                                               
         MVI   HDRSQ,0             INIT HEADER SEQUENCE NUMBER                  
         MVI   HDRSQOLD,0          INIT OLD HEADER SEQUENCE NUMBER              
         XC    DTLSQ,DTLSQ         INIT DETAIL SEQUENCE NUMBER                  
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          ESTABLISH ELEMENT WORKAREA                   
         USING PIMHDREL,R2         AS INVOICE HEADER ELEMENT                    
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET ELEMENT IDENTIFIER                       
         MVI   PIMHDRLN,PIMHDRSQ-PIMHDREL  LOOK AT ALL HEADER ELMS              
*                                                                               
PRHDRLP  DS    0H                                                               
*                                                                               
         GOTO1 GETEL,MNPRMS,MINBLKD,PINVELM,0 FIND HEADER ELM                   
*                                                                               
         ICM   R2,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    PRHDRDN             EOF                                          
*                                                                               
         CLI   HDRSQ,0             IF NO HEADER SEQUENCE FOUND YET              
         BNE   *+10                                                             
         MVC   HDRSQ,PIMHDRSQ         SAVE THE ONE FROM FOUND ELEMENT           
*                                                                               
         CLC   PIMEST,EZIHCVES     IF SAME ESTIMATE                             
         BNE   PRHDRCN                                                          
         CLC   PIMPRD,EZIHCVPR        PRODUCT                                   
         BNE   PRHDRCN                                                          
         CLC   PIMINVNO(10),EZIHINV   INVOICE NUMBER                            
         BNE   PRHDRCN                                                          
*                                                                               
         MVC   HDRSQOLD,PIMHDRSQ      SAVE SEQUENCE NUMBER                      
*                                                                               
         CLI   EZRPLOPT,C'Y'       IF NOT ALLOWED TO REPLACE INVOICE            
         BNE   PRERRDUP                                                         
*                                                                               
PRHDRCN  DS    0H                                                               
*                                                                               
         SR    RF,RF               BUMP SEQUENCE NUMBER                         
         IC    RF,PIMHDRSQ                                                      
         LA    RF,1(RF)                                                         
         STC   RF,PIMHDRLN-PIMHDREL+PINVELM                                     
*                                                                               
         B     PRHDRLP                                                          
*                                                                               
PRHDRDN  DS    0H                                                               
*                                                                               
         OC    HDRSQOLD,HDRSQOLD   IF INVOICE ALREADY ON FILE                   
         BZ    *+14                                                             
         MVC   HDRSQ,HDRSQOLD         USE ITS SEQUENCE NUMBER                   
         B     PRHDRSQX                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,HDRSQ            ELSE DECREMENT FIRST ON FILE                 
         BCTR  RF,0                                                             
         STC   RF,HDRSQ                                                         
*                                                                               
PRHDRSQX DS    0H                                                               
*                                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRPIMHDR'                 
***********************************************************************         
*                                                                     *         
*        ADD INVOICE HEADER ELEMENT                                   *         
*        ADD ACTIVITY       ELEMENT                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRPIMHDR DS    0H                                                               
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD INVOICE HEADER ELEMENT                 
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   ELEMENT ID                                   
         MVI   PIMHDRLN,PIMHDRLQ   ELEMENT LENGTH                               
         MVC   PIMHDRSQ,HDRSQ      HEADER SEQUENCE NUMBER                       
*                                                                               
         MVC   PIMEST,EZIHCVES     ESTIMATE                                     
         MVC   PIMINVDT,EZIHCVDT   INVOICE DATE                                 
         MVC   PIMINVNO(10),EZIHINV  INVOICE NUMBER                             
         GOTO1 DTCNV,DMCB,(4,EZIHDISD),(3,PIMSTDT)   INVOICE START DATE         
         GOTO1 DTCNV,DMCB,(4,EZIHDIED),(3,PIMENDDT) INVOICE END    DATE         
         GOTO1 DTCNV,DMCB,(5,0),PIMCREDT    DATE INVOICE ADDED                  
         MVC   PIMACTDT,PIMCREDT   DATE OF LAST ACTIVITY                        
*                                                                               
         GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
         DROP  R2                                                               
*                                  SET ACTIVITY ELEM                            
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD ACTIVITY ELEMENT                       
         USING ACTVD,R2                                                         
*                                                                               
         MVI   ACTVEL,X'F1'                                                     
         MVI   ACTVLEN,20                                                       
         MVC   ACTVADDT,TODAYB     DATE                                         
         MVC   ACTVADID,RCORIGID   USER ID                                      
*                                                                               
         GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
*                                                                               
         DROP  R2                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
PRERRANF DS    0H                  CLIENT NOT FOUND                             
*                                                                               
         MVI   EZERRCD,EZERRANF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRPNF DS    0H                  PRODUCT NOT FOUND                            
*                                                                               
         MVI   EZERRCD,EZERRPNF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRDUP DS    0H                  DUPLICATE INVOICE                            
*                                                                               
         MVI   EZERRCD,EZERRDUP                                                 
         B     PRERRX                                                           
*                                                                               
PRERRX   DS    0H                                                               
*                                                                               
         BAS   RE,GETERRM                                                       
         MVI   EZMODE,EZINVL       SET TO SKIP TO NEXT INV                      
*                                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRANN'                    
***********************************************************************         
*                                                                     *         
*        PROCESS ANNOUNCEMENT                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRANN    DS    0H                                                               
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM                                                       
         USING PIMDTLEL,R2         ESTABLISH DETAIL ELEMENT                     
*                                                                               
*        BUILD DETAIL ELEMENT KEY                                               
*                                                                               
         MVI   PIMDTLEL,PIMDTLEQ   SET DETAIL ELEMENT CODE                      
         MVI   PIMDTLLN,PIMDTLLQ   SET DETAIL ELEMENT LENGTH                    
         MVC   PIMDTLS1,HDRSQ      SET HEADER SEQUENCE NUMBER                   
         SR    RF,RF                                                            
         ICM   RF,3,DTLSQ          BUMP TO NEXT SEQUNCE NUMBER                  
         LA    RF,1(RF)                                                         
         STCM  RF,3,PIMDTLS2                                                    
*                                                                               
*        BUILD DETAIL ELEMENT                                                   
*                                                                               
         MVC   PIMIDATE,EZIHBIDT   INVOICE DATE                                 
         MVC   PIMIEST,EZIHBEST    INVOICE ESTIMATE NUMBER                      
         MVC   PIMBZONE,EZSPLZON   ZONE                                         
         MVC   PIMBEDTN,EZSPLEDC   EDITION                                      
*                                                                               
         GOTO1 DMCB,(2,EZSPCDT),(3,PIMBDATE)   INSERTION DATE                   
*                                                                               
         MVC   PIMBEST,EZIHBEST    BUY ESTIMATE                                 
         MVI   PIMBLINE,0          BUY LINE                                     
         MVC   PIMSPACE,EZSPSPCE   SPACE DESCRIPTION                            
*                                                                               
         ICM   RF,15,EZSPBLNS      IF LINES PRESENT                             
         BZ    *+12                                                             
         MVI   PIMUIND,C'L'           SET UNITS AS LINES                        
         B     PRANUNT                GO FORMAT UNITS                           
*                                                                               
         ICM   RF,15,EZSPBICS      IF INCHES PRESENT                            
         BNE   PRANUNTX                 (LOWERCASE I)                           
         MVI   PIMUIND,X'89'          SET UNITS AS INCHES                       
*                                                                               
PRANUNT  DS    0H                  SPACE DESCRIPTION IN UNITS                   
*                                                                               
         ICM   RE,15,EZSPBCLS      IF NUMBER OF COLUMNS PRESENT                 
         BZ    *+6                                                              
         MR    RE,RE                  CALCULATE TOTAL UNITS                     
*                                                                               
         CVD   RF,DUB              RF HAS TOTAL UNITS                           
         ZAP   PIMUNITS,DUB        SET UNITS TOTAL                              
*                                                                               
PRANUNTX DS    0H                                                               
*                                                                               
         ICM   RF,15,EZSPBGRS      SET COST                                     
         CVD   RF,DUB                                                           
         ZAP   PIMCOST,DUB                                                      
*                                                                               
         MVI   PIMCSIND,C' '       COST IS GROSS                                
         MVC   PIMPRD,EZSPLPRC     PRODUCT CODE                                 
*                                                                               
         GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
*                                                                               
PRAX     DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - LINV'                     
***********************************************************************         
*                                                                     *         
*        LAST FOR INVOICE                                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
LINV     DS    0H                                                               
*                                                                               
         LA    R5,MINBLK           ESTABLISH INV FILE MINIO BLOCK               
         USING MINBLKD,R5                                                       
*                                                                               
         MVI   MINOPEN,C'N'        FORCE OPENING OF RECORD SET                  
         MVI   MINDELSW,C'N'       PROCESS DELETED RECORDS                      
*                                                                               
         CLI   EZWRITE,C'Y'        IF WRITES NOT ALLOWED                        
         BE    *+8                                                              
         MVI   MINWRITE,C'N'          DISABLE WRITES                            
*                                                                               
         MVC   MINMKEY,MINMKEY-MINBLKD+MINBLKBF COPY BUFFER KEY                 
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINOPN',MINBLKD)   OPEN MINIO SET                
*                                                                               
         CLI   MINERR,MINESNF      SKIP IF RECORD NOT FOUND                     
         BE    LINVOLDX                                                         
*                                                                               
         TM    MINSTAT,MINDELQ     IF RECORD SET IS DELETED                     
         BNO   LINVDELX                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINRSF',MINBLKD)  RESTORE MINIO SET              
*                                                                               
LINVOLD  DS    0H                  RECORD ALREADY EXISTS                        
*                                                                               
         LA    R2,PINVELM          ESTABLISH AS HEADER ELEMENT                  
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET KEY FOR THIS INVOICE                     
         MVC   PIMHDRSQ,HDRSQ      SET SEQUENCE NUMBER                          
*                                                                               
         GOTO1 GETEL,DMCB,MINBLKD,PINVELM,0 READ FOR HEADER                     
*                                                                               
         ICM   RF,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    LINVDELX            NONE FOUND                                   
*                                                                               
         GOTO1 DELEL,DMCB,MINBLKD,PINVELM,0   DELETE HEADER                     
*                                                                               
*        DELETE ALL DETAILS AND COMMENTS FOR INVOICE                            
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY                             
*                                                                               
         MVI   MINEKEY,PIMDTLEQ    SET DETAIL ELEMENT ID                        
         MVC   MINEKEY+1(1),HDRSQ SET HEADER SEQUENCE NUMBER                    
*                                                                               
         MVI   MINFILTL,PIMDTLS2-PIMDTLEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
LINVDTLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   LINVDTDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
LINVDTCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     LINVDTLP                                                         
*                                                                               
LINVDTDN DS    0H                                                               
*                                                                               
*        DELETE COMMENTS                                                        
*                                                                               
         MVI   MINEKEY,PIMCOMEQ    SET COMMENT ELEMENT ID                       
         MVC   MINEKEY+1(1),HDRSQ SET HEADER SEQUENCE NUMBER                    
*                                                                               
         MVI   MINFILTL,PIMCOMS2-PIMCOMEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
LINVCMLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   LINVCMDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
LINVCMCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     LINVCMLP                                                         
*                                                                               
LINVCMDN DS    0H                                                               
*                                                                               
LINVDELX DS    0H                                                               
*                                                                               
LINVOLDX DS    0H                                                               
*                                                                               
*        COPY BUFFER TO FILE                                                    
*                                                                               
LINVCPY  DS    0H                                                               
*                                                                               
         LA    R6,MINBLK           POINT TO FILE MINIO BLOCK                    
         LA    R5,MINBLKBF         POINT TO BUFFER MINIO RECORD                 
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,MINMKEY-MINBLKD+MINBLKBF   DUMP KEY                           
         BAS   RE,DMPREC                                                        
*                                                                               
         LA    R2,PINVELM          POINT TO ELEMENT WORKAREA                    
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY                             
         XC    PINVELM,PINVELM     INIT ELEMENT WORKAREA                        
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINHI',MINBLKBF)  READ FIRST ELEMENT             
*                                                                               
LINVCPLP DS    0H                                                               
*                                                                               
         CLI   MINERR,MINEEOF      DONE IF AT END OF FILE                       
         BE    LINVCPDN                                                         
*                                                                               
         CLI   MINERR,0            NO OTHER ERRORS TOLERATED                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   RF,15,MINELEM-MINBLKD+MINBLKBF  POINT TO FOUND RECORD            
         BZ    LINVCPDN            END OF RECORD                                
*                                                                               
         MVC   PINVELM,0(RF)       COPY ELEMENT                                 
*                                                                               
         GOTO1 ADDEL,DMCB,MINBLK,PINVELM,0  ADD TO FILE RECORD                  
*                                                                               
         CLI   MINERR,0            NO ERRORS TOLERATED                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINSEQ',MINBLKBF)  NEXT BUFFER ELEMENT           
*                                                                               
LINVCPC1 DS    0H                                                               
*                                                                               
         B     LINVCPLP                                                         
*                                                                               
LINVCPDN DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINCLS',MINBLK)    CLOSE FILE   RECORD           
         GOTO1 VMINIO,MNPRMS,('MINCLS',MINBLKBF)  CLOSE BUFFER RECORD           
*                                                                               
LINVCPYX DS    0H                                                               
*                                                                               
LINV90   DS    0H                  MARK INVOICE AS CONVERTED                    
*                                                                               
         CLI   EZTEST,C'Y'         TEST TO SUPPRESS MARKING                     
         BE    LINV96                                                           
*                               SET CONVERSION DATA IN INVOICE HEADER           
         LA    R3,SAVINVH+5        SAVED INVOICE HEADER                         
*                                  SKIP RECLEN(2),RECCODE(2),DELIM(1)           
         OI    0(R3),EZIHCVQ       INDICATE A CONVERTED INVOICE                 
         NI    0(R3),X'FF'-EZIHRCVQ  TURN OFF ANY RECONVERTED INDICATOR         
*                                                                               
         GOTO1 DTCNV,DMCB,(0,TODAY),(1,1(R3)) TODAY PWOS                        
*                                                                               
         MVC   4(2,R3),EZIHLADC    CLIENT                                       
         MVC   6(1,R3),EZIHLPRC    PRODUCT                                      
         OI    11(R3),X'80'        STOP TRAILING BLANK DELETION                 
*                                                                               
*                               NOTE- THE RDBLOCK IS NEEDED TO ENSURE           
*                                     WE WRITE BACK THE LATEST VERSION          
*                                     OF THE BLOCK                              
*                                                                               
         MVC   DMCB+16(4),=A(SAVWKBUF)                                          
*                                                                               
         CLI   EZWRITE,C'Y'        TEST IF WRITE ALLOWED                        
         BNE   LINV96                                                           
*                                                                               
*        FIND BLOCK POINTER FOR SAVED INVOICE HEADER                            
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'RDBLOCK',EZWKRFIL,EZWKRIND,SAVINVH              
*                                                                               
*        WRITE BACK UPDATED INVOICE HEADER                                      
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'WRITE',EZWKRFIL,EZWKRIND,SAVINVH                
*                                                                               
LINV96   DS    0H                                                               
*                                                                               
LINVX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PZADDINV - PROGRAM INITIALIZATION - MININIT'                    
***********************************************************************         
*                                                                     *         
*        MININIT - PROGRAM INITIALIZATION                             *         
*              INIT PRINT INVOICE RECORD MINIO BLOCK                  *         
*              INIT PRINT INVOICE RECORD BUFFER MINIO BLOCK           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MININIT  NTR1                                                                   
*                                                                               
         L     RF,EZCOMFCS         GET MINIO ADDRESS                            
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF) SET A(DATAMGR)                    
*                                                                               
         L     RF,CCALLOV-COMFACSD(RF) A(CALLOV)                                
         ICM   R0,15,=X'D9000A74'  MINIO PHASE NAME                             
         GOTO1 (RF),DMCB,0,(R0)                                                 
         MVC   VMINIO,0(R1)        SET MINIO ADDRESS                            
*                                                                               
         L     RF,=V(PERVERT)                                                   
         ST    RF,VPERVERT                                                      
*                                                                               
*        INITIALIZE MINIO VALUES                                                
*                                                                               
         LA    R0,MINBLK           CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R5,MINBLK           ESTABLISH MINIO BLOCK                        
         USING MINBLKD,R5                                                       
*                                                                               
         MVC   MINRECUP,=V(RECUP)  A(RECUP)                                     
         MVC   MINCOMF,EZCOMFCS    A(COMFACS)                                   
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,INVFILE      FILE NAME                                    
         MVC   MINDIR,INVDIR       DIR NAME                                     
         MVI   MINFKLEN,L'PINVKEY  KEY LENGTH                                   
         MVI   MINEKLEN,L'PINVMINI ELEMENT KEY LENGTH                           
         MVI   MINEKDSP,L'PINVMAST DISPLACEMENT TO ELEMENT KEY                  
         MVI   MINNCTL,L'PINVSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(L'PINVMBF1) MAXIMUM RECORD LENGTH                  
*                                                                               
         L     RF,=A(PINVMBF1)     POINT TO START OF MINIO BUFFERS              
         ST    RF,MINBUFF          A(FIRST BUFFER)                              
*                                                                               
         MVI   MINNBUF,2           USE TWO BUFFERS                              
*                                                                               
         L     RF,=A(PINVMRTB)                                                  
         ST    RF,MINRTAB          A(AREA FOR RECORD TABLE)                     
*                                                                               
         MVC   MINRTABL,=Y(L'PINVMRTB) LENGTH OF RECORD TABLE                   
*                                                                               
         L     RF,=A(PINVMELM)      A(AREA FOR ELEM OR CLUSTER)                 
         ST    RF,MINELEM                                                       
*                                                                               
         XC    0(L'PINVMELM,RF),0(RF)  CLEAR MINELEM AREA                       
*                                                                               
         MVC   MINMAXEL,=Y(L'PINVMELM)   MAX LENGTH OF ELEM OR CLUSTER          
*                                                                               
***********************************************************************         
*                                                                     *         
*        SET UP BUFFER FOR BUILDING INVOICE MINIO RECORD              *         
*            IT WILL BE USED TO HOLD RECORDS WHILE TAPE IS            *         
*            BEING PROCESSED. AT END OF INV ON TAPE RECORDS WILL      *         
*            BE WRITTEN BACK TO FILE FROM THE BUFFERS. NEEDED TO      *         
*            PREVENT MINIO FROM WRITING TO FILE PREMATURELY. ALSO     *         
*            ALLOWS FOR TRUE TEST MODE.                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
*                                                                               
*        MINIO PARAMETERS FOR INVOICE BUFFER                                    
*                                                                               
PINVBBFL EQU   128000              MINIO BUFFER LENGTH                          
PINVBNBF EQU   1                   NUMBER OF BUFFERS                            
PINVBELL EQU   256                 ELEMENT AREA LENGTH                          
PINVBTBL EQU   ((6+L'PINVMINI)*1) TABLE AREA LENGTH - MAX 1 RECORD              
*                                                                               
         LA    R5,MINBLKBF         POINT TO BUFFER MINIO BLOCK                  
         USING MINBLKD,R5                                                       
         XC    MINBLKD(MINNRECS-MINBLKD),MINBLKD INIT MINIO AREA                
*                                                                               
         MVC   MINRECUP,=V(RECUP)  A(RECUP)                                     
         MVC   MINCOMF,EZCOMFCS    A(COMFACS)                                   
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,INVFILE      FILE NAME                                    
         MVC   MINDIR,INVDIR       DIR NAME                                     
         MVI   MINFKLEN,L'PINVKEY  KEY LENGTH                                   
         MVI   MINEKLEN,L'PINVMINI   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'PINVMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'PINVSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCM3,=AL3(PINVBBFL) MAX RECORD LENGTH                        
         MVI   MINNBUF,PINVBNBF    USE ONE BUFFER                               
*                                                                               
         SR    RF,RF                                                            
         SR    RE,RE                                                            
         IC    RF,MINNBUF          NUMBER OF MINIO BLOCKS                       
         ICM   RE,7,MINFRCM3       LENGTH OF A MINIO BUFFER                     
         MR    RE,RE               BUFFER AREA NEEDED                           
         LA    RF,PINVBELL+PINVBTBL(RF) ADD ON ELM AND TABLE LENGTHS            
*                                                                               
         LA    R3,8(RF)            MAKE EVEN NO. OF DOUBLE WORDS                
         SRL   R3,3                                                             
         SLL   R3,3                                                             
*                                                                               
         LA    R4,MINBUFF          RETURN ADDRESS HERE                          
*                                                                               
         GETMAIN EC,LV=(R3),A=(R4) GET CORE                                     
         LTR   RF,RF               CHECK FOR ERRORS                             
         BZ    *+6                                                              
         DC    H'0'                NO BUFFER AREA AVAILABLE                     
*                                                                               
         ICM   R1,15,MINBUFF       BUFFER ADDRESS                               
         L     RF,=AL4(PINVBBFL)                                                
         AR    R1,RF               A(ELEMENT AREA)                              
         ST    R1,MINELEM                                                       
         LA    R1,PINVBELL(R1)     A(RECORD TABLE)                              
         ST    R1,MINRTAB                                                       
*                                                                               
         MVC   MINRTABL,=Y(PINVBTBL)  LENGTH OF RECORD TABLE                    
*                                                                               
         MVC   MINMAXEL,=Y(PINVBELL)  MAX LENGTH OF ELEM OR CLUSTER             
*                                                                               
         MVI   MINMODE,C'B'        SET FOR BUFFER MODE                          
*                                                                               
MININITX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMELM'                   
***********************************************************************         
*                                                                     *         
*        COMELM - FIND/SET CMML ELEMENTS IN BUFFER                    *         
*                                                                     *         
*NTRY    R5 ==>  A(MINBLK)                                            *         
*                                                                     *         
*EXIT                                                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
COMELM   NTR1                                                                   
*                                                                               
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         CLC   EZICCOM,=CL256' '   SKIP IF NO COMMENT AVAILABLE                 
         BNH   COMELMX                                                          
*                                                                               
         XC    PINVCELM,PINVCELM                                                
         LA    R2,PINVCELM         ESTABLISH COMMENT ELEMENT                    
         USING PIMCOMEL,R2                                                      
*                                                                               
         MVI   PIMCOMEL,PIMCOMEQ   SET ELEMENT ID                               
         MVI   PIMCOMLN,PIMCOMOV   SET DEFAULT MINIMUM LENGTH                   
****     MVC   PIMCOMS1,           SET HEADER SEQUENCE NUMBER                   
****     MVC   PIMCOMS2,           DETAIL SEQUENCE NUMBER                       
*                                                                               
         LA    R0,PIMCOMEL         MINIMUM COMMENT ELM LENGTH                   
         LA    R1,PIMCOML1         POINT TO FIRST COMMENT LENGTH BYTE           
         LA    R4,PIMCOMTX         POINT TO START OF COMMENTS IN ELM            
         LA    R5,3                MAX 3 COMMENT SEGMENTS                       
         LA    RE,EZICCOM          POINT TO START OF COMMENT                    
*                                                                               
COMELMLP DS    0H                                                               
*                                                                               
         CLC   0(39,RE),=CL256' '  SKIP IF SEGMENT NOT PRESENT                  
         BNH   COMELMCN                                                         
*                                                                               
         LA    RF,40               MAX POSITIONS TO CHECK                       
         LA    R3,39(RE)           POINT TO END+1 OF COMMENT SEGMENT            
*                                                                               
         CLI   0(R3),C' '          FIND LAST SPACE                              
         BNH   *+18                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         LA    R3,39(RE)           NO SPACES IN COMMENT - RESET POINTER         
         B     COMELML1                                                         
*                                                                               
         BCTR  R3,0                BACK UP A POSITION                           
         BCTR  RF,0                RECTIFY THE COUNT                            
*                                                                               
         CLI   0(R3),C' '          FIND PREVIOUS NON-SPACE                      
         BH    *+12                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
COMELML1 DS    0H                                                               
*                                                                               
         SR    R3,RE               LENGTH OF COMMENT SEGMENT                    
*                                                                               
         STC   R3,0(R1)            SET COMMENT LENGTH                           
         LA    R1,1(R1)            BUMP COMMENT LENGTH POINTER                  
*                                                                               
         AR    R0,R3               INCREMENT ELEMENT LENGTH COUNTER             
*                                                                               
         BCTR  R3,0                DECREMENT FOR EXECUTE                        
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(RE)       MOVE COMMENT SEGMENT TO ELEMENT              
*                                                                               
COMELMCN DS    0H                                                               
*                                                                               
         LA    R4,1(R3,RE)         BUMP TO NEXT SEGMENT IN ELEMENT              
         LA    RE,1(R3,RE)         BUMP TO NEXT INCOMING SEGMENT                
*                                                                               
         CLI   0(RE),C' '          IF SEGMENT STARTS WITH SPACE                 
         BH    *+8                                                              
         LA    RE,1(RE)               BUMP PAST IT                              
*                                                                               
         BCT   R5,COMELMLP                                                      
*                                                                               
COMELMDN DS    0H                                                               
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,PINVCELM,0  ADD TO RECORD                     
*                                                                               
COMELMX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
HDRSQ    DC    X'00'               HEADER SEQUENCE NUMBER                       
HDRSQOLD DC    X'00'               HEADER SEQUENCE NUMBER FROM FILE             
DTLSQ    DC    XL2'00'             DETAIL SEQUENCE NUMBER                       
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMSUBS'                  
***********************************************************************         
*                                                                     *         
*        COMSUBS - CODE AND DATA ADDRESSABLE FROM ALL CSECTS          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
COMSUBS  DS    0H                                                               
*                                                                               
*        DMPREC - HEX DUMP OF ELEMENT WORK AREA                                 
*                                                                               
*NTRY    R1 ==>  ELEMENT TO BE ADDED                                            
*                                                                               
DMPREC   NTR1                                                                   
*                                                                               
         LR    R5,R1               POINT TO ELEMENT WORKAREA                    
         SR    R2,R2                                                            
         ICM   R2,1,1(R5)          ELEMENT LENGTH                               
*                                                                               
         CLC   =X'0E03',0(R5)      IF DUMPING KEY                               
         BNE   *+8                                                              
         LA    R2,L'PINVKEY           RESET DUMP LENGTH                         
*                                                                               
         LA    R3,0(R5,R2)         EOR                                          
*                                                                               
DMPREC2  DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R5                                                            
         BNP   DMPRECX                                                          
         CH    R4,=H'32'                                                        
         BNH   *+8                                                              
         LA    R4,32                                                            
         XC    WORK,WORK                                                        
         GOTO1 HEXOUT,DMCB,(R5),WORK,(R4),=C'N'                                 
*                                                                               
         MVC   P+01(8),WORK+00                                                  
         MVC   P+10(8),WORK+08                                                  
         MVC   P+19(8),WORK+16                                                  
         MVC   P+28(8),WORK+24                                                  
         MVC   P+37(8),WORK+32                                                  
         MVC   P+46(8),WORK+40                                                  
         MVC   P+55(8),WORK+48                                                  
         MVC   P+64(8),WORK+56                                                  
*                                                                               
         MVC   WORK(32),0(R5)                                                   
         TR    WORK(32),TRTAB                                                   
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+75(0),WORK                                                     
         LA    R4,1(R4)                                                         
         GOTO1 REPORT                                                           
         LA    R5,0(R5,R4)                                                      
         B     DMPREC2                                                          
*                                                                               
DMPRECX  DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
TRTAB    DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     00-0F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     10-1F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     20-2F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     30-3F                    
         DC    X'404B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     40-4F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B5B5C5D4B4B'     50-5F                    
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D4B6F'     60-6F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B7B4B7D7E4B'     70-7F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     80-8F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     90-9F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     A0-AF                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     B0-BF                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'     C0-CF                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'     D0-DF                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'     E0-EF                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'     F0-FF                    
         EJECT                                                                  
* CHECK STATION FOR VALIDITY - 1ST 4 MUST NOT BE - (MINUS) *                    
*       UNLESS ALL NUMERIC                                 *                    
         SPACE                                                                  
         DS    0H                                                               
CKSTA    NTR1                                                                   
         LA    R0,4                                                             
         LA    R1,EZBTSTN                                                       
         CLI   EZBTSTN,C'Z'                                                     
         BH    CKSTA20                                                          
CKSTA10  CLI   0(R1),C'-'          MINUS NOT ALLOWED ANYWHERE                   
         BE    CKSTA40              ERROR                                       
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA10                                                       
         B     CKSTA30             GO CHECK MEDIA                               
         SPACE                                                                  
CKSTA20  CLI   0(R1),C'0'                                                       
         BL    CKSTA24                                                          
         CLI   0(R1),C'9'                                                       
         BH    CKSTA40             ERROR                                        
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA20                                                       
         B     CKSTA30             GO CK MEDIA                                  
         SPACE                                                                  
CKSTA24  CLI   0(R1),C' '          TRAILING BLANKS OK                           
         BNE   CKSTA40              ERROR                                       
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA24                                                       
         SPACE                                                                  
* CHECK VAKLID MEDIA - AM/FM(RADIO), T/L(TV), C/S/N (NETWORK) *                 
         SPACE                                                                  
CKSTA30  LA    R0,9                                                             
         LA    R1,=C'AFRTLXCSN'                                                 
CKSTA34  CLC   EZBTSTN+5(1),0(R1)                                               
         BE    CKSTAX                                                           
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA34                                                       
CKSTA40  MVI   EZERRCD,EZERRSTA                                                 
         BAS   RE,GETERRM                                                       
         MVI   EZMODE,EZINVL       SET TO SKIP TO NEXT INV                      
         SPACE                                                                  
CKSTAX   B     EXIT                                                             
         EJECT                                                                  
*        GETERRM - GET ERROR MESSAGE AND LEVEL                                  
         SPACE 2                                                                
GETERRM  NTR1                                                                   
         L     R3,=A(ERRTAB)                                                    
         MVC   EZERRMSG,SPACES                                                  
         MVI   EZERRLEV,0                                                       
*                                                                               
GERM2    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BE    GERMX                                                            
*                                                                               
         CLC   EZERRCD,0(R3)                                                    
         BE    GERM4                                                            
         LA    R3,ERRTABL(R3)                                                   
         B     GERM2                                                            
*                                                                               
GERM4    DS    0H                                                               
         MVC   EZERRLEV,1(R3)                                                   
         MVC   EZERRMSG,2(R3)                                                   
         OC    EZERRLVR,1(R3)      'OR' UP ERROR - RECORD                       
         OC    EZERRLVI,1(R3)                    - INVOICE                      
         OC    EZERRLVB,1(R3)                    - BATCH                        
*                                                                               
GERMX    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
SAVBUF   DS    0H                  SAVE WRKR BUFFER                             
         L     RF,EZWKRBUF                                                      
         L     R1,=A(SAVWKBUF)                                                  
*                                                                               
         LA    R0,16               16 X 256 BYTES = 4096                        
         MVC   0(256,R1),0(RF)                                                  
         LA    R1,256(R1)                                                       
         LA    RF,256(RF)                                                       
         BCT   R0,*-14                                                          
         BR    RE                                                               
         EJECT                                                                  
         SPACE 2                                                                
*                                  ERROR TABLE                                  
*                                  CODE(1),LEVEL(1),MESSAGE(40)                 
*                                                                               
ERRTAB   DS    0C                                                               
         DC    AL1(EZERRDUP)       DUPLICATE INVOICE                            
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVOICE ALREADY ON FILE'                                    
*                                                                               
         DC    AL1(EZERROVF)       OVERFLOW                                     
         DC    AL1(0)              LEVEL                                        
         DC    CL40'TOO MANY PRINTS IN INVOICE'                                 
*                                                                               
         DC    AL1(EZERRANF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'CLIENT NOT ON FILE'                                         
*                                                                               
         DC    AL1(EZERRPNF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRODUCT NOT ON FILE'                                        
*                                                                               
         DC    AL1(EZERRSPL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRINT DETAIL PRINT LENGTH ZERO'                             
*                                                                               
         DC    AL1(EZERRMUL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'COMBINED INVOICES, NO RECONVERT'                            
*                                                                               
         DC    X'FFFF'                                                          
*                                                                               
INVFILE  DC    CL8'XPRFIL '        INVOICE FILE NAME                            
INVDIR   DC    CL8'XPRDIR'         INVOICE DIRECTORY NAME                       
MAXRECSZ DC    H'1900'                                                          
ERRTABL  EQU   42                                                               
*                                                                               
VDATAMGR DS    A                   A(VDATAMGR)                                  
VMINIO   DS    A                   A(MINIO)                                     
VPERVERT DS    A                   A(PERVERT)                                   
*                                                                               
ADSCTL   DC    C'1'                CONTROL SWITCH                               
ADS1STQ  EQU   C'1'                FIRST TIME TO PROGRAM                        
*                                                                               
         SPACE 2                                                                
*                                                                               
         TITLE 'ROUTINE TO ADD ELEMENT TO A MINIO BLOCK - ADDEL'                
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT TO A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
ADDEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         MVC   MINELEM,4(R1)       POINT BLOCK TO NEW ELEMENT                   
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         L     R1,4(R1)               POINT TO ELEMENT TO BE ADDED              
         BAS   RE,DMPREC              GO DUMP ELEMENT                           
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINADD',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
ADDELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO DELETE AN ELEMENT IN A MINIO BLOCK - ADDEL'          
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT FROM A RECORD                          *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
DELEL    NTR1  0H                                                               
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         L     R2,4(R1)            POINT TO ELEMENT TO BE DELETED               
*                                                                               
         ST    R2,MINELEM          POINT BLOCK TO ELEMENT                       
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET KEY LENGTH                               
*                                                                               
         CLM   RF,1,MINEKLEN       DEFAULT TO MINIO KEY LENGTH                  
         BNH   *+8                                                              
         IC    RF,MINEKLEN                                                      
*                                                                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
*                                                                               
         EX    RF,DELELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     DELELX                                                           
*                                                                               
DELELMV  MVC   MINEKEY+1(0),2(R2)   SETS REST OF ELEMENT KEY                    
*                                                                               
*                                                                               
DELELX   XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO GET AN ELEMENT IN A MINIO BLOCK - GETEL'             
***********************************************************************         
* ROUTINE TO GET AN ELEMENT IN A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
GETEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT O ELEMENT KEY                          
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         LA    R0,MINRD            DEFAULT TO DIRECT READ                       
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    GETEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   GETEL10                                                          
*                                                                               
         LA    R0,MINHI            SET FOR READ HI/EQUAL                        
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
GETEL10  DS    0H                                                               
*                                                                               
         ZIC   RF,MINEKLEN         GET KEY LENGTH                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
         EX    RF,GETELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   GETELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     GETELX                                                           
*                                                                               
GETELMV  MVC   MINEKEY+1(0),2(R2)  SETS REST OF ELEMENT KEY                     
*                                                                               
GETELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO GET NEXT ELEMENT IN A MINIO BLOCK - SEQEL'           
***********************************************************************         
* ROUTINE TO GET NEXT SEQUENTIAL ELEMENT IN A MINIO RECORD            *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
*                 ASSUMES LAST ACTION WAS A GETEL AND MINIO BLOCK     *         
*                  IS ALREADY SET UP FOR SEQUENTIAL READ              *         
*                                                                     *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
SEQEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT TO ELEMENT KEY                         
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    SEQEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   SEQEL10                                                          
*                                                                               
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
SEQEL10  DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           SET FOR READ READ SEQUENTIAL                 
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   SEQELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     SEQELX                                                           
*                                                                               
SEQELX   XIT1                                                                   
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO PUT AN ELEMENT TO A MINIO BLOCK - PUTEL'             
***********************************************************************         
* ROUTINE TO PUT AN ELEMENT IN A RECORD                               *         
*       DELETES AND ADDS NEW ELEMENT WILL NOT GET UPSET IF THERE      *         
*         IS NO PRIOR ELEMENT                                         *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PUTEL    NTR1  0H                                                               
*                                                                               
         GOTO1 GETEL,(R1)          FIND ELEMENT                                 
*                                                                               
         OC    8(4,R1),8(R1)       ADD ELEMENT IF NONE ALREADY                  
         BZ    PUTELADD            ELSE DELETE OLD ELEMENT                      
*                                                                               
         GOTO1 DELEL,(R1)          DELETE ELEMENT                               
*                                                                               
PUTELADD DS    0H                                                               
*                                                                               
         GOTO1 ADDEL,(R1)          ADD ELEMENT TO RECORD                        
*                                                                               
PUTELX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*                                                                               
SAVINVH  DS    XL512               SAVE AREA FOR INVOICE HEADER                 
         DS    0D                                                               
MINBLK   DS    XL(MINBLKL)         MINIO BLOCK FOR INVOICE RECORD               
         DS    0D                                                               
MINBLKBF DS    XL(MINBLKL)         MINIO BLOCK FOR BUFFER INVOICE REC           
*                                                                               
         DS    0D                                                               
PINVMELM DS    XL128               ELEMENT RETRIEVAL AREA                       
PINVMRTB DS    XL(200*(6+L'PINVMINI))  RECORD TABLE                             
*                                                                               
         DS    0D                                                               
PINVMBF1 DS    XL4096              FIRST  MINIO BUFFER                          
PINVMBF2 DS    XL4096              SECOND MINIO BUFFER                          
*                                                                               
SAVWKBUF DS    XL4096              WORKER FILE BUFFER SAVEAREA                  
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        WORKAREAS                                                    *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PZADID   DSECT                                                                  
PINVELM  DS    XL256               ELEMENT WORKAREA                             
PINVCELM DS    XL256               COMMENT ELM WORKAREA                         
ERR      DS    XL1                                                              
         DS    0D                                                               
*                                                                               
*        MINIO ROUTINES WORKAREAS                                               
*                                                                               
MNPRMS   DS    6A                  PARAMETER BLOCK                              
*                                                                               
PZADIDL  EQU   *-PZADID                                                         
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        OTHER INCLUDED DSECTS                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
* PZBLOCK                                                                       
         PRINT OFF                                                              
       ++INCLUDE PZBLOCK                                                        
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* DDACTIVD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDACTIVD                                                       
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DDMINBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDMINBLK                                                       
         PRINT ON                                                               
* DMWRKRD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRD                                                        
         PRINT ON                                                               
* PPREPWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
* PPREPWORK2                                                                    
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK2                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012PZADDINVS 05/01/02'                                      
         END                                                                    
