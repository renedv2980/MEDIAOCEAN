*          DATA SET DDEDITOR   AT LEVEL 008 AS OF 05/01/02                      
*CATALP EDITOR                                                                  
         TITLE 'DDEDITOR - T00A71 - GENERAL EDITING ROUTINE'                    
EDITOR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 EDITDX-EDITD,**EDIT**                                            
         USING EDITD,RC                                                         
         L     R9,0(R1)            PARAMETER 1 A(EDIT BLOCK)                    
         MVC   EBLOCK,0(R9)        COPY BLOCK AS I WILL CHANGE IT               
         EJECT                                                                  
*              GET INPUT FIELD PACKED INTO DUB                                  
         SPACE 1                                                                
         L     R2,EBAIN                                                         
         ZIC   R1,EBLIN                                                         
         BCTR  R1,0                                                             
         CLI   EBTIN,C'B'          SELECT INPUT ROUTINE                         
         BE    BINARY                                                           
         CLI   EBTIN,0                                                          
         BE    BINARY                                                           
         CLI   EBTIN,C'U'                                                       
         BE    UNSBIN                                                           
         CLI   EBTIN,C'S'                                                       
         BE    SCALED                                                           
         CLI   EBTIN,C'M'                                                       
         BE    MASKED                                                           
         CLI   EBTIN,C'P'                                                       
         BE    PACKED                                                           
         CLI   EBTIN,C'E'                                                       
         BE    EBCDIC                                                           
         CLI   EBTIN,C'W'                                                       
         BE    PWOS                                                             
         DC    H'0'                                                             
         SPACE 1                                                                
BINARY   CLI   EBLIN,2             BINARY                                       
         BL    BINARY1                                                          
         BE    BINARY2                                                          
         CLI   EBLIN,4                                                          
         BL    BINARY3                                                          
         BE    BINARY4                                                          
         DC    H'0'                                                             
         SPACE 1                                                                
BINARY1  ZIC   R1,0(R2)                                                         
         B     BINALL                                                           
         SPACE 1                                                                
BINARY2  LH    R1,0(R2)                                                         
         B     BINALL                                                           
         SPACE 1                                                                
BINARY3  L     R1,0(R2)                                                         
         SRA   R1,8                                                             
         B     BINALL                                                           
         SPACE 1                                                                
BINARY4  L     R1,0(R2)                                                         
         SPACE 1                                                                
BINALL   CVD   R1,DUB                                                           
         B     OUT                                                              
         SPACE 1                                                                
UNSBIN   TR    EBLIN,ICMMASK       UNSIGNED BINARY 1-3 BYTES                    
         IC    R1,EBLIN            R1=ICM MASK                                  
         EX    R1,*+8                                                           
         B     BINALL                                                           
         ICM   R1,0,0(R2)                                                       
         SPACE 1                                                                
SCALED   MVC   EBSCIN,0(R2)        SCALED - TAKE SCALE FROM FIRST BYTE          
         LA    R2,1(R2)            THEN TREAT AS PACKED                         
         BCTR  R1,0                                                             
         B     PACKED                                                           
         SPACE 1                                                                
MASKED   OC    EBOPT,0(R2)         MASKED - FIRST BYTE = EDIT OPT               
         LA    R2,1(R2)            THEN TREAT AS PACKED                         
         BCTR  R1,0                                                             
         SPACE 1                                                                
PACKED   EX    R1,*+8              PACKED                                       
         B     OUT                                                              
         ZAP   DUB,0(0,R2)                                                      
         SPACE 1                                                                
EBCDIC   EX    R1,*+8              EBCDIC                                       
         B     OUT                                                              
         PACK  DUB,0(0,R2)                                                      
         SPACE 1                                                                
PWOS     ZIC   R1,EBLIN            PWOS                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         UNPK  WORK(16),0(0,R2)    (UNPACK LEN+1)                               
         PACK  DUB,WORK(15)                                                     
         EJECT                                                                  
*              NOW CONTROL THE OUTPUT                                           
         SPACE 1                                                                
OUT      BAS   RE,SCALE            CONVERT SCALE TO ROUND                       
         CP    DUB,=P'0'                                                        
         BNE   OUT2                                                             
         TM    EBOPT,X'20'         OPTION TO SHOW ZERO                          
         BNO   XIT                                                              
         SPACE 1                                                                
OUT2     CLI   EBROUND,0           ROUND OPTIONS                                
         BE    OUT6                                                             
         ZIC   R3,EBROUND          POSITIVE = ROUND TO RIGHT                    
         TM    EBROUND,X'80'                                                    
         BO    OUT4                                                             
         LCR   R3,R3                                                            
         SRP   DUB,0(R3),5                                                      
         B     OUT6                                                             
         SPACE 1                                                                
OUT4     SLL   R3,25               NEGATIVE = SHIFT TO LEFT                     
         SRL   R3,25                                                            
         SRP   DUB,0(R3),0                                                      
         EJECT                                                                  
*              NOW BUILD THE EDIT MASK                                          
         SPACE 1                                                                
OUT6     CP    DUB,=P'0'           MAY BE SCALED TO ZERO                        
         BNE   OUT8                                                             
         TM    EBOPT,X'20'         OPTION TO SHOW ZERO                          
         BNO   XIT                                                              
         SPACE 1                                                                
OUT8     LA    R3,NUMMASKS         START WITH NUMERIC                           
         TM    EBOPT,X'80'                                                      
         BNO   *+8                                                              
         LA    R3,COMMASKS         OR COMMA MASKS                               
         SPACE 1                                                                
         ZIC   R4,EBDECS           DEAL WITH DECIMAL POINTS                     
         CH    R4,=H'7'            MAX 6                                        
         BL    *+6                                                              
         DC    H'0'                                                             
         MH    R4,=H'24'                                                        
         TM    EBOPT,X'01'         TEST IF SWAP DEC PTS AND COMMAS              
         BZ    *+8                                                              
         AH    R4,=Y(EURONUMS-NUMMASKS) ADD OFFSET                              
         AR    R3,R4                                                            
         MVC   WORK,0(R3)          MOVE IN SELECTED MASK                        
         MVI   WORK,C' '                                                        
         MVC   POINT,0(R3)                                                      
         SPACE 1                                                                
         TM    EBOPT,X'40'         FOR MINUS=YES                                
         BO    OUT12                                                            
         TM    EBOPT,X'10'         OR BRACKETS                                  
         BO    OUT12                                                            
         TM    EBOPT,X'08'         OR BRACKETS ON MINUS                         
         BNO   OUT14                                                            
         SPACE 1                                                                
OUT12    MVC   WORK(23),WORK+1     SLIDE TO LEFT                                
         MVI   WORK+23,C'-'        AND ATTACH A MINUS SIGN                      
         ZIC   R1,POINT                                                         
         BCTR  R1,0                                                             
         STC   R1,POINT                                                         
         SPACE 1                                                                
OUT14    CLI   EBTRAIL,X'41'       WAS A TRAILING CHARACTER NEEDED              
         BL    OUT16                                                            
         MVC   WORK(23),WORK+1     SLIDE TO LEFT                                
         MVC   WORK+23(1),EBTRAIL  AND ATTACH THE CHARACTER                     
         ZIC   R1,POINT                                                         
         BCTR  R1,0                                                             
         STC   R1,POINT                                                         
         SPACE 1                                                                
OUT16    CLI   EBFILL,0            OPTION FILL CHARACTER                        
         BE    *+10                                                             
         MVC   WORK(1),EBFILL                                                   
         EJECT                                                                  
*              NOW EDIT AND RELEASE OUTPUT                                      
         SPACE 1                                                                
OUT20    ZIC   R1,POINT                                                         
         LA    R1,WORK(R1)                                                      
         BCTR  R1,0                                                             
         EDMK  WORK,DUB            THE BIG MOMENT HAS ARRIVED                   
         CLI   EBFLOAT,C' '        OPTIONAL FLOAT CHARACTER                     
         BNH   OUT22                                                            
         CP    DUB,=P'0'                                                        
         BE    OUT22               (SKIP FLOAT FOR ZEROS)                       
         CLI   0(R1),X'41'                                                      
         BL    *+6                                                              
         BCTR  R1,0                                                             
         MVC   0(1,R1),EBFLOAT                                                  
         CLI   EBFLOAT,C'-'        IF FLOAT IS MINUS SIGN                       
         BNE   OUT22                                                            
         CP    DUB,=P'0'                                                        
         BNL   OUT21                                                            
         TM    EBOPT,X'58'                                                      
         BZ    OUT22                                                            
         MVI   WORK+23,C' '                                                     
         B     OUT22                                                            
OUT21    MVI   0(R1),C' '          ONLY SHOW IF NEGATIVE                        
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
OUT22    TM    EBOPT,X'08'         CONDITIONAL BRACKETS                         
         BNO   OUT24                                                            
         CP    DUB,=P'0'                                                        
         BL    OUT26                                                            
         B     OUT28                                                            
         SPACE 1                                                                
OUT24    TM    EBOPT,X'10'         UNCONDITIONAL BRACKET                        
         BNO   OUT28                                                            
         SPACE 1                                                                
OUT26    BCTR  R1,0                PUT IN THE BRACKETS                          
         MVI   0(R1),C'('                                                       
         MVI   WORK+23,C')'                                                     
         SPACE 1                                                                
OUT28    CLI   EBTRAIL,X'41'       TRAILING CHARACTER                           
         BL    OUT30                                                            
         MVC   WORK+23(1),EBTRAIL  ATTACH THE CHARACTER                         
         SPACE 1                                                                
OUT30    ST    R1,EDSTART                                                       
         L     R3,EBAOUT           R3=A(OUTPUT)                                 
         ZIC   R1,EBLOUT           R1=LENGTH                                    
         BCTR  R1,0                -1                                           
         LA    R2,WORK+23                                                       
         SR    R2,R1               R2=A(MATCHING DATA IN WORK)                  
         EX    R1,DATOUT                                                        
         CLI   EBFILL,0                                                         
         BNE   XIT                                                              
         BCTR  R2,0                BACK UP TO SEE IF WE GOT ALL DATA            
         CLI   0(R2),C' '                                                       
         BE    ALIGN                                                            
         TM    EBTRIM,X'80'        NO - CHECK FOR TRIM INSTRUCTIONS             
         BO    OUT32                                                            
         TM    EBTRIM,X'40'                                                     
         BO    OUT34                                                            
         BCTR  R3,0                NONE SO OUTPUT 1 MORE CHARACTER              
         MVC   0(1,R3),0(R2)       AND CALL IT A DAY                            
         B     XIT                                                              
         EJECT                                                                  
*              TRIM DATA DOWN FOR ANOTHER SHOT                                  
         SPACE 1                                                                
OUT32    NI    EBTRIM,X'7F'        OPTION TO TRIM OFF DECIMAL POINTS            
         MVC   EBROUND,EBDECS                                                   
         MVI   EBDECS,0                                                         
         B     OUT2                                                             
         SPACE 1                                                                
OUT34    LA    R2,1(R2)            OPTION TO PUT ON ROW BELOW                   
         EX    R1,CLEAR            CLEAR THIS ONE FIRST                         
         LA    R4,1(R2)                                                         
         S     R4,EDSTART          R4=OVERFLOW+1                                
         SR    R2,R4                                                            
         SR    R3,R4               BACK UP IN OUTPUT FIELD                      
         AR    R1,R4                                                            
         EX    R1,SPACOMP          AND SEE IF THERE IS DATA TO THE LEFT         
         BE    OUTEND                                                           
         ZIC   R0,EBPWIDTH         YES - SO GET DOWN TO NEXT LINE               
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         LA    R0,132              (DEFAULT IS 132 WIDTH)                       
         AR    R3,R0                                                            
         SPACE 1                                                                
OUTEND   EX    R1,DATOUT                                                        
         B     XIT                                                              
         SPACE 1                                                                
SPACOMP  CLC   0(0,R3),SPACES                                                   
CLEAR    MVC   0(0,R3),SPACES                                                   
DATOUT   MVC   0(0,R3),0(R2)                                                    
         EJECT                                                                  
*              POSSIBLE ALIGNMENT                                               
         SPACE 1                                                                
ALIGN    CLI   EBALIGN,C'L'        OPTION TO LEFT ALIGN                         
         BNE   XIT                                                              
         L     R3,EBAOUT                                                        
         LA    R2,1(R3)                                                         
         ZIC   R0,EBLOUT                                                        
         LR    R1,R0                                                            
         LA    RE,0(R3,R1)                                                      
         ICM   RE,8,0(RE)                                                       
         MVI   0(RE),C' '                                                       
         BCTR  R1,0                                                             
         SPACE 1                                                                
ALIGN2   CLI   0(R3),C' '                                                       
         BNE   *+12                                                             
         EX    R1,DATOUT                                                        
         BCT   R0,ALIGN2                                                        
         STCM  RE,8,0(RE)                                                       
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              CONVERT SCALING TO ROUND FACTOR                                  
         SPACE 1                                                                
SCALE    NTR1                                                                   
         CLI   EBSCIN,0            DONT BOTHER IF BOTH SCALES ARE ZERO          
         BNE   SCALE2                                                           
         CLI   EBSCOUT,0                                                        
         BE    XIT                                                              
         SPACE 1                                                                
SCALE2   ZIC   R1,EBSCIN           GET INPUT SCALE INTO R1                      
         TM    EBSCIN,X'80'                                                     
         BNO   SCALE4                                                           
         SLL   R1,25                                                            
         SRL   R1,25                                                            
         LCR   R1,R1                                                            
         SPACE 1                                                                
SCALE4   ZIC   R0,EBSCOUT          AND OUTPUT SCALE TO R0                       
         TM    EBSCOUT,X'80'                                                    
         BNO   SCALE6                                                           
         SLL   R0,25                                                            
         SRL   R0,25                                                            
         LCR   R0,R1                                                            
         SPACE 1                                                                
SCALE6   SR    R0,R1               ROUNDING FACTOR = OUTPUT SCALE               
         ZIC   R1,EBDECS           - INPUT SCALE - DECIMAL POINTS               
         SR    R0,R1                                                            
         BM    SCALE8                                                           
         STC   R0,EBROUND          THIS IS FOR A DIVIDE                         
         B     XIT                                                              
         SPACE 1                                                                
SCALE8   LCR   R0,R0               CAME OUT NEGATIVE                            
         STC   R0,EBROUND          SO WE'LL MULTIPLY                            
         OI    EBROUND,X'80'                                                    
         B     XIT                                                              
         EJECT                                                                  
*              MASKS FOR EDITOR                                                 
         SPACE 1                                                                
NUMMASKS DS    0C                                                               
         DC    X'184040404040404040202020202020202020202020202120'              
         DC    X'164040404040404020202020202020202020202021204B20'              
         DC    X'1540404040404040202020202020202020202021204B2020'              
         DC    X'14404040404040402020202020202020202021204B202020'              
         DC    X'134040404040404020202020202020202021204B20202020'              
         DC    X'1240404040404040202020202020202021204B2020202020'              
         DC    X'11404040404040402020202020202021204B202020202020'              
         SPACE 1                                                                
EURONUMS DC    X'184040404040404040202020202020202020202020202120'              
         DC    X'164040404040404020202020202020202020202021206B20'              
         DC    X'1540404040404040202020202020202020202021206B2020'              
         DC    X'14404040404040402020202020202020202021206B202020'              
         DC    X'134040404040404020202020202020202021206B20202020'              
         DC    X'1240404040404040202020202020202021206B2020202020'              
         DC    X'11404040404040402020202020202021206B202020202020'              
         SPACE 2                                                                
COMMASKS DS    0C                                                               
         DC    X'18404040402020206B2020206B2020206B2020206B202120'              
         DC    X'1640404020206B2020206B2020206B2020206B2020214B20'              
         DC    X'15404040206B2020206B2020206B2020206B2020214B2020'              
         DC    X'14404040402020206B2020206B2020206B2020214B202020'              
         DC    X'134040404020206B2020206B2020206B2020214B20202020'              
         DC    X'1240404040206B2020206B2020206B2020214B2020202020'              
         DC    X'1140404040402020206B2020206B2020214B202020202020'              
         SPACE 1                                                                
EUROCOMS DC    X'18404040402020204B2020204B2020204B2020204B202021'              
         DC    X'1640404020204B2020204B2020204B2020204B2021206B20'              
         DC    X'15404040204B2020204B2020204B2020204B2021206B2020'              
         DC    X'14404040402020204B2020204B2020204B2021206B202020'              
         DC    X'134040404020204B2020204B2020204B2021206B20202020'              
         DC    X'1240404040204B2020204B2020204B2021206B2020202020'              
         DC    X'1140404040402020204B2020204B2021206B202020202020'              
         SPACE 2                                                                
ICMMASK  DC    AL1(00,01,03,07,15)                                              
SPACES   DC    CL24' '                                                          
         LTORG                                                                  
         EJECT                                                                  
EDITD    DSECT                                                                  
DUB      DS    D                                                                
ADD      DS    PL16                                                             
DIV      DS    PL8                                                              
WORK     DS    CL24                                                             
EDSTART  DS    A                                                                
POINT    DS    CL1                                                              
         SPACE 1                                                                
*DDEBLOCK                                                                       
       ++INCLUDE DDEBLOCK                                                       
*                                                                               
EDITDX   EQU   *                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDEDITOR  05/01/02'                                      
         END                                                                    
