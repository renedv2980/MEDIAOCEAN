*          DATA SET SPEZCOM    AT LEVEL 003 AS OF 11/08/17                      
*CATALP SPEZCOM                                                                 
*                                                                               
***********************************************************************         
* SPEZCOM                                                                       
* ROUTINES, COMMON TO EASI PROGRAMS                                             
***********************************************************************         
*                                                                               
         PRINT NOGEN                                                            
*                                                                               
         REQUS                                                                  
*                                                                               
SPEZCOM  CSECT                                                                  
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XIT1                                                                   
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* GETMED                                                                        
*                                                                               
* ON ENTRY:                                                                     
* R1 HOB = INPUT COLUMN EQUATE                                                  
* R1     = A(INPUT DATA)                                                        
* ON EXIT                                                                       
* CC = EQUAL ON SUCCESSFUL LOOKUP, RF ADDRESSING EZMEDTAB LINE                  
* CC = UNEQUAL OTHERWISE                                                        
***********************************************************************         
         ENTRY GETMED                                                           
GETMED   NTR1  BASE=*,LABEL=*                                                   
         LR    R3,R1               COLUMN EQUATE, A(INPUT)                      
         SRL   R3,24               ISOLATE COLUMN EQUATE IN LOB                 
*                                                                               
         USING EZMEDTBD,RF                                                      
         LA    RF,EZMEDTAB         MEDIA TABLE                                  
         LHI   R0,EZMDTNQ          NUMBER OF ENTRIES IN EZMEDTAB                
*                                                                               
* SET UP EXECUTED CLC LENGTH                                                    
         LHI   RE,1                CLC LEN=1 FOR ALL COLUMNS                    
         CHI   R3,EZMTPMDQ         EXCEPT PRINTABLE MEDIA/BAND COLUMN           
         BNE   *+8                                                              
         LHI   RE,L'EZMTPMED       CLC LEN=2                                    
         BCTR  RE,0                                                             
*                                                                               
GETM10   LA    R2,0(R3,RF)         POINT R2 TO DESIRED COLUMN                   
*                                                                               
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(R1)       MATCHES INPUT DATA?                          
         BE    GETMX               YES - EXIT.  CC==EQ AT THIS POINT            
*                                                                               
         LA    RF,EZMDTLQ(RF)      ADVANCE TO NEXT TABLE ENTRY                  
         BCT   R0,GETM10                                                        
         DROP  RF                                                               
*                                                                               
* ENTRY NOT FOUND IN THE TABLE                                                  
         XR    RF,RF                                                            
         LTR   RB,RB               SET CC NOT EQUAL                             
*                                                                               
GETMX    XIT1  REGS=(RF)                                                        
*                                                                               
* EASI MEDIA TABLE, MUST MATCH EZMEDTBD DSECT (IN SPEZDSCTS)                    
*                                                                               
* COLUMNS:                                                                      
* BAND, MEDIA, BINARY MEDIA NIBBLE, SYSTEM (SPOT/NET),                          
* PRINTABLE 2-CHARACTER BAND/MEDIA, FLAG BYTE                                   
*                                                                               
         ENTRY EZMEDTAB                                                         
EZMEDTAB DC    C'T',C'T',X'01',C'S',C'TV',AL1(0)                                
         DC    C'D',C'T',X'01',C'S',C'DV',AL1(EZMTFDGQ)                         
         DC    C'L',C'T',X'01',C'S',C'L ',AL1(0)                                
         DC    C'A',C'R',X'02',C'S',C'AM',AL1(0)                                
         DC    C'F',C'R',X'02',C'S',C'FM',AL1(0)                                
         DC    C'S',C'R',X'02',C'S',C'SM',AL1(EZMTFDGQ)                         
         DC    C'1',C'R',X'02',C'S',C'CM',AL1(EZMTFDGQ)                         
         DC    C'N',C'N',X'03',C'N',C'N ',AL1(0)                                
         DC    C'C',C'N',X'03',C'N',C'N ',AL1(0)                                
         DC    C'X',C'X',X'04',C'S',C'X ',AL1(0)                                
EZMDTNQ  EQU   (*-EZMEDTAB)/EZMDTLQ                                             
*                                                                               
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* PRTSTA                                                                        
*                                                                               
* ON ENTRY:                                                                     
* P1 = ADDRESS OF 5-CHAR STATION/BAND (E.G. KACLA)                              
* P2 = ADDRESS OF 7-CHAR STATION FIELD (E.G. KACL-AM)                           
* ON EXIT                                                                       
* CC = EQUAL ON SUCCESSFUL LOOKUP                                               
* CC = UNEQUAL OTHERWISE                                                        
***********************************************************************         
         ENTRY PRTSTA                                                           
PRTSTA   NTR1  BASE=*,LABEL=*                                                   
         LM    R1,R2,0(R1)                                                      
*                                                                               
         MVC   0(7,R2),=7C' '      INITIALIZE OUTPUT TO SPACES                  
         MVC   0(4,R2),0(R1)       4 CALL LETTERS                               
*                                                                               
         LA    R2,6(R2)            MOVE PAST THE CALL LETTERS                   
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         LA    R2,1(R2)                                                         
*                                                                               
         MVI   0(R2),C'-'                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         LA    R1,4(R1)            STATION BAND                                 
         ICM   R1,8,=AL1(EZMTBNDQ)                                              
         BRAS  RE,GETMED                                                        
         JNE   NEQXIT                                                           
         MVC   0(L'EZMTPMED,R2),EZMTPMED-EZMEDTBD(RF)                           
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* ++INCLUDES GO HERE                                                            
***********************************************************************         
       ++INCLUDE SPEZDSCTS                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003SPEZCOM   11/08/17'                                      
         END                                                                    
