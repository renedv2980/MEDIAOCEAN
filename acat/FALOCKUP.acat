*          DATA SET FALOCKUP   AT LEVEL 042 AS OF 01/26/99                      
*CATALP LOCKUP                                                                  
         TITLE 'FALOCKUP - BUILD AND MAINTAIN LOCK TABLE'                       
LOCKUP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**LOCKUP,RR=RE,CLEAR=YES                             
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         LR    R6,R1                                                            
         USING LOCKUPD,R6                                                       
         SR    RF,RF               RF=A(LOCKKEY)                                
         ICM   RF,7,LKKEY                                                       
         ST    RF,APKEY                                                         
         SR    RF,RF               RF=A(COMFACS)                                
*                                                                               
LOCK010  TIME                                                                   
         STCM  R0,14,LKTIME        GET DATE/TIME                                
         MVC   LKRTIME,LKTIME                                                   
         XC    LKRTIME,EFFS                                                     
         SRL   R1,4                                                             
         STCM  R1,7,LKDATE                                                      
         EJECT                                                                  
*************************************************************                   
*        EXTRACT SYSTEM INFO                                *                   
*************************************************************                   
         SPACE 1                                                                
GETSSB   MVI   OFFLINE,C'Y'        DEFAULT TO OFFLINE                           
         SR    RF,RF                                                            
         ICM   RF,15,=V(SSB)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE AN SSB                             
         USING SSBD,RF                                                          
*                                                                               
         LAM   R2,R2,SSBALET                                                    
         SAC   512                                                              
         SR    R2,R2                                                            
         L     R2,8(,R2)           GET A(UPDTAB)                                
         ST    R2,AUPDTAB                                                       
         SAC   0                                                                
*                                                                               
         OC    SSBCNTL,SSBCNTL                                                  
         BZ    GETSSBX                                                          
         MVI   OFFLINE,C'N'        MUST BE ONLINE                               
*                                                                               
         ICM   R1,15,SSBTKADR      GET TASKADR IF ACTIVE                        
         BZ    GETSSBX                                                          
         ST    R1,ATASK                                                         
         USING TCBD,R1                                                          
         MVC   AUTL,TCBUTL         GET A(UTL)                                   
         MVC   LKLUID,TCBSYM       GET LUID                                     
*                                                                               
GETSSBX  SR    R1,R1                                                            
         ICM   R1,15,AUTL          UTL IF ONLINE                                
         BNZ   *+8                                                              
         L     R1,=V(UTL)          UTL IF OFFLINE                               
         MVC   LKSENUM,4(R1)                                                    
*                                                                               
         XC    DMCB,DMCB            GET REF ID FROM LOCKSPC                     
         MVI   DMCB+5,4                                                         
         GOTO1 =V(DATAMGR),DMCB,=C'LOCKSP'                                      
         MVC   LKREF,DMCB+8                                                     
*                                                                               
         ICM   RF,15,=V(SSB)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE AN SSB                             
         USING SSBD,RF                                                          
*                                                                               
         LAM   R2,R2,SSBALET                                                    
         SAC   512                                                              
         SR    R2,R2                                                            
         L     R2,8(,R2)           GET A(UPDTAB)                                
         ST    R2,AUPDTAB                                                       
         SAC   0                                                                
         DROP  R1,RF                                                            
         EJECT                                                                  
*************************************************************                   
*        INITIALISE UPDTAB FIRST TIME IN                    *                   
*************************************************************                   
         SPACE 1                                                                
INIT010  L     R2,AUPDTAB                                                       
         SAC   512                                                              
         OC    0(16,R2),0(R2)                                                   
         BNZ   INIT090                                                          
         MVC   10(2,R2),=Y(UPDTABL)                                             
         LH    R1,=Y(UPDTABL)                                                   
         MH    R1,=H'60'           SET FOR 60 ENTRIES                           
         AR    R1,R2                                                            
         LA    R1,16(R1)                                                        
         ST    R1,12(,R2)                                                       
INIT090  SAC   0                                                                
         EJECT                                                                  
*************************************************************                   
*        GOTO ROUTINE BY ACTION                             *                   
*************************************************************                   
         SPACE 1                                                                
         CLI   LKACTN,C'L'         ADD A LOCK ENTRY                             
         BE    LKLOCK                                                           
         CLI   LKACTN,C'T'         TEST A LOCK ENTRY                            
         BE    LKTEST                                                           
         CLI   LKACTN,C'U'         REMOVE A LOCK ENTRY                          
         BE    LKUNLK                                                           
         CLI   LKACTN,C'C'         CLEAR ALL MY LOCK ENTRIES                    
         BE    LKUNLK                                                           
         CLI   LKACTN,C'S'         INHIBIT LOCKING                              
         BE    LKSTOP                                                           
         CLI   LKACTN,C'F'         UNINHIBIT LOCKING                            
         BE    LKFREE                                                           
         CLI   LKACTN,C'A'         GET A(UPDTAB)                                
         BNE   *+6                                                              
         DC    H'0'                NOT SUPPORTED                                
*                                                                               
         CLI   LKACTN,C'B'         BUILD UPDTAB                                 
         BE    EXIT                NO LONGER NEEDED                             
*                                                                               
         DC    H'0'                                                             
         EJECT                                                                  
*************************************************************                   
*        ADD A LOCK ENTRY                                   *                   
*************************************************************                   
         SPACE 1                                                                
LKLOCK   BAS   RE,LKQBIT           TEST FOR INHIBIT                             
         BO    ERR04                                                            
*                                                                               
         BAS   RE,KEYPAD           BUILD WORK KEY                               
*                                                                               
         BAS   RE,TABLOCK          LOCK TABLE                                   
         BNE   ERR02               UNABLE TO LOCK                               
*                                                                               
         BAS   RE,CHKKEY           LOOK FOR KEY                                 
         BE    ERR01               KEY ALREADY LOCKED                           
*                                                                               
         BAS   RE,ADDKEY           ADD THIS KEY                                 
         BNE   ERR03               TABLE FULL                                   
*                                                                               
         CLI   OFFLINE,C'Y'        FLAG LOCK ENTRY ADDED                        
         BE    LKLOCK9                                                          
         L     R1,ATASK                                                         
         USING TCBD,R1                                                          
         OI    TCBFLAG2,TCBLOKUP                                                
         DROP  R1                                                               
*                                                                               
LKLOCK9  BAS   RE,TABFREE                                                       
LKLOCKX  B     EXIT                                                             
         EJECT                                                                  
*************************************************************                   
*        TEST A LOCK ENTRY                                  *                   
*************************************************************                   
         SPACE 1                                                                
LKTEST   BAS   RE,KEYPAD           BUILD WORK KEY                               
         BAS   RE,CHKKEY           LOOK FOR KEY                                 
         BE    ERR01               KEY ALREADY LOCKED                           
LKTESTX  B     EXIT                                                             
         EJECT                                                                  
*************************************************************                   
*        REMOVE A LOCK ENTRY                                *                   
*************************************************************                   
         SPACE 1                                                                
LKUNLK   BAS   RE,KEYPAD           BUILD WORK KEY                               
*                                                                               
LKUN01   BAS   RE,TABLOCK          LOCK TABLE                                   
         BNE   ERR02               UNABLE TO LOCK                               
*                                                                               
         BAS   RE,DELKEY           DELETE THIS KEY                              
         BNE   ERR05               KEY NOT FOUND                                
*                                                                               
         BAS   RE,FLAGCHK          DO WE NEED TCB FLAG                          
         BAS   RE,TABFREE                                                       
*                                                                               
LKUNLKX  B     EXIT                                                             
         EJECT                                                                  
*************************************************************                   
*        INHIBIT LOCKING                                    *                   
*************************************************************                   
         SPACE 1                                                                
LKSTOP   EQU   *                                                                
         SAC   512                                                              
         L     R2,AUPDTAB          GET A(UPDTAB)                                
*                                                                               
         OI    4(R2),X'80'         SET INHIBIT FLAG                             
         SAC   0                                                                
*                                                                               
LKSTOPX  B     EXIT                                                             
         SPACE 2                                                                
*************************************************************                   
*        FREE LOCKING                                       *                   
*************************************************************                   
         SPACE 1                                                                
LKFREE   EQU   *                                                                
         SAC   512                                                              
         L     R2,AUPDTAB          GET A(UPDTAB)                                
*                                                                               
         NI    4(R2),X'FF'-X'80'   CLEAR INHIBIT FLAG                           
         SAC   0                                                                
*                                                                               
LKFREEX  B     EXIT                                                             
         SPACE 2                                                                
*************************************************************                   
*        TEST LOCKING INHIBITOR                             *                   
*************************************************************                   
         SPACE 1                                                                
LKQBIT   EQU   *                                                                
         SAC   512                                                              
         L     R2,AUPDTAB          GET A(UPDTAB)                                
*                                                                               
         TM    4(R2),X'80'         TEST INHIBIT FLAG                            
         SAC   0                                                                
*                                                                               
LKQBITX  BR    RE                                                               
         SPACE 2                                                                
*************************************************************                   
*        CHECK FOR WORK KEY IN TABLE. EXIT CC=EQU IF FOUND  *                   
*************************************************************                   
         SPACE 1                                                                
CHKKEY   ST    RE,SAVERE                                                        
         SAC   512                                                              
CHK000   L     R2,AUPDTAB          SET BXLE                                     
         LA    R2,10(,R2)                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING UPDTABD,R2                                                       
*                                                                               
CHK010   CLI   UPDTABT,UPDTLQ      CHECK IF LOCKUP TYPE                         
         BNE   CHK090                                                           
         CLC   UPDLDATE,UPDKDATE   CHECK DATE                                   
         BNE   CHK090                                                           
         TM    UPDKREF,X'80'       OFFLINE JOBS MUST WAIT                       
         BO    CHK015                                                           
         TM    UPDLREF,X'80'       OFFLINE JOBS ALWAYS COUNT                    
         BO    CHK015                                                           
         B     CHK090              ONLINE/ONLINE IS OK (DMLOCKER)               
                                                                                
CHK015   CLC   UPDLTSE,UPDKTSE     CHECK SE NUMBER                              
         BNE   CHK090                                                           
*                                                                               
CHK020   OC    UPDLTAG,UPDLTAG     AGENCY CHECKS                                
         BZ    CHK030                                                           
         OC    UPDKTAG,UPDKTAG     ZERO AGENCYS MATCH ALL                       
         BZ    CHK030                                                           
         CLC   UPDLTAG,UPDKTAG                                                  
         BNE   CHK090                                                           
*                                                                               
CHK030   OC    UPDLTYP,UPDLTYP     RTYPE CHECKS                                 
         BZ    CHK040                                                           
         OC    UPDKTYP,UPDKTYP     ZERO RTYPES MATCH ALL                        
         BZ    CHK040                                                           
         CLC   UPDLTYP,UPDKTYP                                                  
         BNE   CHK090                                                           
*                                                                               
CHK040   MVC   WORK(L'UPDLKEY),UPDLKEY                                          
         LA    R0,L'UPDLKEY        KEY TEST                                     
         LA    R1,WORK                                                          
         LA    RF,UPDKKEY                                                       
CHK041   CLI   0(R1),0             ZERO BYTES MATCH ALL                         
         BE    CHK042                                                           
         CLI   0(RF),0                                                          
         BE    CHK042                                                           
         CLC   0(1,R1),0(RF)                                                    
         BNE   CHK090                                                           
CHK042   LA    R1,1(R1)            TEST NEXT BYTE                               
         LA    RF,1(RF)                                                         
         BCT   R0,CHK041           UNTIL ALL DONE                               
*                                                                               
         MVC   WORK(64),0(R2)      COPY OWNER                                   
         CR    RB,RB               SET CC=EQU                                   
         B     CHKKEYX                                                          
*                                                                               
CHK090   BXLE  R2,R4,CHK010                                                     
         LTR   RB,RB               SET CC=NEQ                                   
*                                                                               
CHKKEYX  SAC   0                                                                
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        ADD WORK KEY TO TABLE. EXIT CC=NEQ IF FULL         *                   
*************************************************************                   
         SPACE 1                                                                
ADDKEY   ST    RE,SAVERE                                                        
         SAC   512                                                              
         L     R2,AUPDTAB          SET BXLE                                     
         LA    R2,10(,R2)                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING UPDTABD,R2                                                       
*                                                                               
ADD010   CLI   UPDTABT,0           LOOK FOR FREE ENTRY                          
         BNE   ADD090                                                           
         MVC   UPDTABT(UPDTABL),WRKKEY                                          
*                                                                               
         CR    RB,RB               SET CC=EQU                                   
         B     ADDKEYX                                                          
*                                                                               
ADD090   BXLE  R2,R4,ADD010                                                     
         LTR   RB,RB               SET CC=NEQ                                   
*                                                                               
ADDKEYX  SAC   0                                                                
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        DELETE WORK KEY FROM TABLE. CC=NEQ IF NOT FOUND    *                   
*************************************************************                   
         SPACE 1                                                                
DELKEY   ST    RE,SAVERE                                                        
         SAC   512                                                              
DEL000   L     R2,AUPDTAB          SET BXLE                                     
         LA    R2,10(,R2)                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING UPDTABD,R2                                                       
*                                                                               
DEL010   CLI   LKACTN,C'C'         IF CLEAR ACTION                              
         BNE   DEL020                                                           
         CLC   UPDLREF,LKREF       REMOVE ANY LOCKS FROM THIS REF               
         BNE   DEL030                                                           
         BE    DEL090                                                           
*                                                                               
DEL020   CLC   UPDTABT(UPDKEYL),WRKKEY                                          
         BE    DEL090              MUST BE EXACT MATCH FOR DELETE               
*                                                                               
DEL030   BXLE  R2,R4,DEL010                                                     
         CLI   LKACTN,C'C'         IF CLEAR ACTION THEN OK                      
         BE    DELK900                                                          
         LTR   RB,RB               SET CC=NEQ NOT FOUND                         
         B     DELKEYX                                                          
*                                                                               
DEL090   EAR   RE,R2               SET AREG ZERO FOR THIS BIT                   
         SAR   RE,RE                                                            
         LA    R3,1(R5)                                                         
         SR    R3,R2               R3=L'DEST                                    
         LR    RE,R2                                                            
         AR    RE,R4               RE=SOURCE                                    
         LR    RF,R1                                                            
         S     RF,R4               RF=L'SOURCE                                  
*                                                                               
         MVCL  R2,RE               MOVE & PAD LAST ENTRY WITH ZERO              
         LAM   RE,RE,=X'00000000'                                               
         CLI   LKACTN,C'C'         IF CLEAR ACTION                              
         BE    DEL000              GO BACK FOR MORE                             
DELK900  CR    RB,RB                                                            
*                                                                               
DELKEYX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        DELETE WORK KEY FROM TABLE. CC=NEQ IF NOT FOUND    *                   
*************************************************************                   
         SPACE 1                                                                
FLAGCHK  ST    RE,SAVERE                                                        
         CLI   OFFLINE,C'Y'        NO TEST OFFLINE                              
         BE    FLGCHKX                                                          
         SAC   512                                                              
FLG000   L     R2,AUPDTAB          SET BXLE                                     
         LA    R2,10(,R2)                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING UPDTABD,R2                                                       
*                                                                               
FLG010   CLC   UPDLREF,LKREF       TEST FOR THIS REF ID                         
         BE    FLGCHKX                                                          
FLG030   BXLE  R2,R4,FLG010                                                     
*                                                                               
         L     R1,ATASK            NONE FOUND SO REMOVE FLAG                    
         USING TCBD,R1                                                          
         NI    TCBFLAG2,255-TCBLOKUP                                            
         DROP  R1                                                               
*                                                                               
FLGCHKX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        PAD OUT APKEY TO PRODUCE INTERNAL WRKKEY           *                   
*************************************************************                   
         SPACE 1                                                                
KEYPAD   ST    RE,SAVERE                                                        
         L     R5,APKEY                                                         
         USING LKKEYD,R5                                                        
         XC    WRKKEY,WRKKEY                                                    
         MVI   WRKKEY,UPDTLQ       SET LOCKUP ENTRY                             
         MVC   UPDKDATE,LKDATE     TODAYS DATE                                  
*                                                                               
         MVC   UPDKTSE,LKSENUM     SYSTEM SE NUMBER                             
         CLI   LOCKSE,0                                                         
         BE    *+10                                                             
         MVC   UPDKTSE,LOCKSE      CALLER CAN OVERRIDE                          
         CLI   UPDKTSE,0                                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   UPDKTAG,LOCKAGY                                                  
         MVC   UPDKTYP,LOCKRTY                                                  
         MVC   UPDKKEY,LOCKKEY                                                  
         MVC   UPDKTIME,LKTIME                                                  
         MVC   UPDKSTAT,0                                                       
         MVC   UPDKREF,LKREF                                                    
*                                                                               
         CLI   LKXTRA,C'Y'         ANY EXTRA AVAILABLE                          
         BNE   KEYPADX                                                          
         SR    R1,R1                                                            
         ICM   R1,7,LKAXTRA                                                     
         MVC   UPDKXTRA,0(R1)                                                   
*                                                                               
KEYPADX  L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R5                                                               
         EJECT                                                                  
*************************************************************                   
*        LOCK FAUPDTAB IF POSSIBLE. EXIT CC=EQU IF OK       *                   
*************************************************************                   
         SPACE 1                                                                
TABLOCK  ST    RE,SAVERE                                                        
         LA    R4,200              TRY 200 TIMES                                
         SAC   512                                                              
TABL010  L     R2,AUPDTAB                                                       
         L     R0,EFFS             LOCK WORD IS FFFFFFFF                        
         SR    RF,RF                                                            
         CS    RF,R0,0(R2)         LOCK TABLE IF FREE                           
         BE    TABL020                                                          
*                                                                               
         LA    RF,384              WAIT 1 MILLISECOND                           
         GOTO1 =V(TICTOC),DMCB,C'WAIT',(RF)                                     
*                                                                               
         BCT   R4,TABL010          TRY AGAIN                                    
         SAC   0                                                                
         LTR   RB,RB                                                            
         L     RE,SAVERE                                                        
         BR    RE                  GIVE UP EXIT CC NEQ                          
*                                                                               
TABL020  MVI   LKFLAG,C'Y'         FLAG THAT I LOCKUP IT                        
         SAC   0                                                                
         CR    RB,RB                                                            
         L     RE,SAVERE                                                        
         BR    RE                  GOT TABLE EXIT CC EQU                        
         SPACE 2                                                                
*************************************************************                   
*        FREE FAUPDTAB                                      *                   
*************************************************************                   
         SPACE 1                                                                
TABFREE  L     R2,AUPDTAB                                                       
         SAC   512                                                              
         XC    0(4,R2),0(R2)       FREE TABLE                                   
         SAC   0                                                                
         MVI   LKFLAG,C'N'                                                      
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        ERROR EXITS                                        *                   
*************************************************************                   
         SPACE 1                                                                
ERR01    LA    R1,WORK             RETURN OWNERS ENTRY IN P2                    
         ST    R1,LKERR                                                         
         MVI   LKERR,X'01'         KEY IS ALREADY LOCKED                        
         B     ERRX                                                             
ERR02    MVI   LKERR,X'02'         CAN'T LOCK THE TABLE (IS BUSY)               
         B     ERRX                                                             
ERR03    MVI   LKERR,X'03'         LOCK TABLE FULL                              
         B     ERRX                                                             
ERR04    MVI   LKERR,X'04'         LOCKING INHIBITED                            
         B     ERRX                                                             
ERR05    MVI   LKERR,X'05'         KEY NOT FOUND FOR UNLOCK                     
         B     ERRX                                                             
ERR06    MVI   LKERR,X'06'         CONTROL NOT STARTED                          
         B     ERRX                                                             
*                                                                               
ERRX     CLI   LKFLAG,C'Y'         DID I LOCK THE TABLE                         
         BNE   *+8                                                              
         BAS   RE,TABFREE          YES SO FREE IT                               
         B     EXIT                                                             
*                                                                               
ERRCT    CLI   LKFLAG,C'Y'         DID I LOCK THE TABLE                         
         BNE   *+8                                                              
         BAS   RE,TABFREE          YES SO FREE IT                               
         DC    H'0'                                                             
         SPACE 2                                                                
EXIT     XMOD1                                                                  
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS & LTORG                                  *                   
*************************************************************                   
         SPACE 1                                                                
         CNOP  0,4                                                              
EFFS     DC    X'FFFFFFFF'                                                      
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMADD    DC    C'DMADD   '                                                      
CTFILE   DC    C'CTFILE  '                                                      
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE DSECT                              *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
RELO     DS    A                                                                
SAVERE   DS    A                                                                
LKDATE   DS    XL3                 DATE NOW JULIAN                              
LKTIME   DS    XL3                 TIME NOW HH:MM:SS                            
LKRTIME  DS    XL3                 TIME NOW HH:MM:SS (COMPLIMENTED)             
LKSENUM  DS    X                   SENUM ONLINE OR OFF                          
LKLUID   DS    CL8                 LUID IF ONLINE                               
LKREF    DS    CL4                                                              
*                                                                               
KEY      DS    CL32                USED FOR RDHI                                
SAVEKEY  DS    CL25                                                             
*                                                                               
WRKKEY   DS    0CL32               WORK KEY                                     
         DS    CL1                                                              
UPDKDATE DS    CL3                                                              
UPDKTSE  DS    CL1                                                              
UPDKTAG  DS    CL2                                                              
UPDKTYP  DS    CL2                                                              
UPDKKEY  DS    CL10                                                             
UPDKEYL  EQU   *-WRKKEY            LEN FOR COMPARE                              
UPDKTIME DS    CL3                                                              
UPDKSTAT DS    CL1                                                              
         DS    CL5                                                              
UPDKREF  DS    CL4                                                              
*                                                                               
UPDKXTRA DS    CL32                                                             
*                                                                               
ATASK    DS    V                                                                
AUTL     DS    V                                                                
APKEY    DS    A                                                                
AUPDTAB  DS    A                                                                
*                                                                               
OFFLINE  DS    C                   SET TO Y IF OFFLINE                          
LKFLAG   DS    X                   SET TO Y IF TABLE WAS LOCKED                 
SAVESE   DS    X                   SAVED SENUM BEFORE SWITCH                    
*                                                                               
IOAREA   DS    1024C               I/O AREA FOR CTFILE                          
WORKX    EQU   *                                                                
         EJECT                                                                  
* CTGENFILE                                                                     
* DDCOMFACS                                                                     
* FALOCKUPD                                                                     
* FAD                                                                           
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FALOCKUPD                                                      
       ++INCLUDE FAUPDTABA                                                      
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE FASSB                                                          
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'042FALOCKUP  01/26/99'                                      
         END                                                                    
