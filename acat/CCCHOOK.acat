*          DATA SET CCCHOOK    AT LEVEL 003 AS OF 09/25/00                      
*          DATA SET CCCASM     AT LEVEL 040 AS OF 01/05/87                      
*                                                                               
*        THIS MODULE SERVES AS AN INTERFACE BETWEEN THE WATERLOO-C              
*        COMPILER AND THE DDS LIBRARY OF ASSEMBLER ROUTINES.                    
*                                                                               
*        IT IS CALLED FROM THE C-ROUTINE, CALLASM, WHICH PASSES IT TWO          
*        ARGUMENTS, THE ADDRESS OF A STRING CONTAINING THE NAME OF THE          
*        ROUTINE, AND THE ADDRESS OF A DMCB.                                    
*                                                                               
*        THE STRING CONTAINING THE NAME OF THE ROUTINE MUST BE PADDED           
*        ON THE RIGHT WITH BLANKS UP TO THE EIGHTH CHARACTER.                   
*                                                                               
*        AN IMPORTANT NOTE ON WATERLOO-C: WATERLOO-C CALLS SUBROUTINES          
*        BY DOING A                                                             
*                   BALR    RD,RB .                                             
*                                                                               
*        THUS, THIS MODULE MUST DO A                                            
*                   BR      RD                                                  
*        TO RETURN BACK TO THE CALLING ROUTINE.                                 
*                                                                               
*        THIS ALSO MEANS THAT RB CAN CONVENIENTLY BE USED AS A BASE             
*        REGISTER FOR THIS ROUTINE.                                             
*                                                                               
*        THIS ROUTINE WILL LOAD RB WITH THE RETURN VALUE JUST BEFORE            
*        RETURNING TO THE CALLING ROUTINE.                                      
*                                                                               
*        POSSIBLE RETURN VALUES ARE:                                            
*        0 - ROUTINE WAS SUCCESSFULLY CALLED                                    
*        1 - ROUTINE NAME WAS NOT FOUND IN TABLE OF ROUTINE NAMES               
*        2 - CORRESPONDING V-TYPE ADDRESS CONSTANT WAS NOT RESOLVED             
*            BY THE LINKAGE EDITOR (CHECK INCLUDE STATEMENT IN JCL)             
*********************************************************************           
*CATALP CCCHOOK                                                                 
*                                                                               
CHOOK    CSECT                     CALLS ASSEMBLER ROUTINE FROM C               
         ENTRY UTL                                                              
         ENTRY SSB                                                              
         REQUS                                                                  
         USING CHOOK,RB            RB ALREADY POINTS HERE                       
         STM   RC,R2,12(RC)        SAVES REGISTERS                              
         LA    RD,WORK             SETS UP SAVE AREA FOR ASM ROUTINE            
         XCEF  WORK,8000                                                        
         ST    RC,4(RD)            STORES BACKWARD POINTER                      
*                                                                               
         CLI   FIRSTIME,0          CHECKS FIRST TIME THROUGH                    
         BNE   DOCALL                                                           
         MVI   FIRSTIME,1                                                       
         GOTO1 =V(STXITER),STXTAB  ALLOW FOR FORMATTED DUMPS                    
*                                                                               
DOCALL   L     R1,4(RC)            LOADS DMCB INTO R1 FROM C-STACK              
         L     R3,0(RC)            R3 HOLDS ADDRESS OF COMMAND STRING           
         LA    R4,CALLTAB                                                       
         LA    R5,VTAB                                                          
*                                                                               
LOOP     CLC   0(8,R4),0(R3)       COMPARE STRING WITH TABLE ENTRY              
         BE    FOUND                                                            
         CLC   0(8,R4),=C'X       '     END OF TABLE MARKER                     
         BE    NOTFOUND                                                         
         LA    R4,8(R4)            POINT R4 AND R5 TO NEXT TABLE ENTRY          
         LA    R5,4(R5)                                                         
         B     LOOP                                                             
FOUND    L     RF,0(R5)            MAKE SURE REFERENCE HAS BEEN                 
         LTR   RF,RF               RESOLVED BY LINKAGE EDITOR                   
         BE    UNRESOLV                                                         
         BASR  RE,RF                                                            
         LA    R7,0                SUCCESSFUL COMPLETION                        
         B     RESTORE                                                          
NOTFOUND LA    R7,1                RETURN CODE FOR COMMAND NOT FOUND            
         B     RESTORE                                                          
UNRESOLV LA    R7,2                RETURN CODE FOR UNRESOLVED REFERENCE         
RESTORE  LM    RC,R2,12(RC)        RESTORES REGISTERS                           
         LR    RB,R7               LOAD RETURN CODE IN RETURN REGISTER          
         BR    RD                                                               
********************************************************************            
*        COMMAND <-> ADDRESS TABLES. COMMAND STRINGS CORRESPOND TO              
*        V-TYPE ADDRESS CONSTANTS.                                              
********************************************************************            
         DS    0D                                                               
CALLTAB  DC    CL8'DATAMGR '       TABLE OF ROUTINE STRINGS                     
         DC    CL8'HEXOUT  '                                                    
         DC    CL8'HEXIN   '                                                    
         DC    CL8'DECODE  '                                                    
         DC    CL8'DATCON  '                                                    
         DC    CL8'PRINTER '                                                    
         DC    CL8'LOADER  '                                                    
         DC    CL8'HELLO   '                                                    
         DC    CL8'X       '                                                    
VTAB     DC    V(DATAMGR)          TABLE OF V-TYPE ADDRESS CONSTANTS            
         DC    V(HEXOUT)                                                        
         DC    V(HEXIN)                                                         
         DC    V(DECODE)                                                        
         DC    V(DATCON)                                                        
         DC    V(PRINTER)                                                       
         DC    V(LOADER)                                                        
         DC    V(HELLO)                                                         
*                                                                               
UTL      DC    F'0',X'00'          USED BY DATAMGR                              
SSB      DC    F'0'                                                             
*                                                                               
FIRSTIME DC    X'00'               FIRST TIME ROUTINE IS CALLED                 
*                                                                               
STXTAB   DC    A(*+4)              STXITER PARAMETER BLOCK                      
         DC    A(WORK)                                                          
         DC    A(WORKX)                                                         
         ORG   *-4                                                              
         DC    X'80'               END OF LIST MARKER                           
         ORG                                                                    
*                                                                               
         LTORG                                                                  
WORK     DS    0D                  WORK AREA PASSED TO ASSEMBLER                
         DC    8192X'00'           ROUTINE TO BE USED AS THE REGISTER           
WORKX    EQU   *-1                 SAVE AREA                                    
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003CCCHOOK   09/25/00'                                      
         END                                                                    
