*          DATA SET REUNUPGR   AT LEVEL 019 AS OF 05/01/02                      
*CATALP UNUPGR                                                                  
         TITLE 'UNSCAN - REP UPGRADE ELEMENT'                                   
*              PARA 1         BYTE 0    FOR NEW UPGRADES ONLY                   
*                                        IF 0, DISPLAY ALL UPGRADES             
*                                        IF N, DISPLAY NTH UPGRADE ONLY         
*                             BYTE 1-3  A(FIELD HEADER)                         
*              PARA 2         BYTE 1-3  A(RECORD)                               
*              PARA 3         BYTE 1-3  A(UNSCAN)                               
*              PARA 4         BYTE 1-3  A(DEMCON)                               
*              PARA 5         BYTE 1-3  A(DBLOCK)                               
         SPACE 1                                                                
UNUPGR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 UNUPDX-UNUPD,**UNUPGR,CLEAR=YES                                  
         USING UNUPD,RC                                                         
         MVC   SAVEP(8),0(R1)                                                   
         MVC   UNSCAN,8(R1)                                                     
         MVC   DEMCON,12(R1)                                                    
         MVC   DBLOCK,16(R1)                                                    
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
         EJECT                                                                  
         LA    R3,34(R3)           FIRST ELEMENT                                
         XR    R4,R4                                                            
UNUP1    CLI   0(R3),4             UPGRADE ELEMENT                              
         BE    UNUP2                                                            
         CLI   0(R3),5                                                          
         BE    NEWUP               NEW UPGRADE                                  
         CLI   0(R3),0                                                          
         BE    EXIT                                                             
         IC    R4,1(R3)                                                         
         AR    R3,R4                                                            
         B     UNUP1                                                            
         SPACE 1                                                                
UNUP2    MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(99),BLOCK                                                
         LA    R6,BLOCK                                                         
         LA    R7,TYPE                                                          
         SPACE 1                                                                
         CLC   2(1,R3),0(R7)                                                    
         BE    UNUP5               MATCH TYPE                                   
         LA    R7,11(R7)                                                        
         CLI   0(R7),X'FF'                                                      
         BNE   *-18                                                             
         MVI   FULL,0                                                           
         MVC   FULL+1(2),2(R3)     TYPE AND NUMBER                              
         SPACE 1                                                                
***********NOTE - DEMCON IS REALLY DEMOCON **************                       
         SPACE 1                                                                
         GOTO1 DEMCON,DMCB,(0,FULL),(6,(R6)),DBLOCK                             
         B     UNUP6                                                            
         SPACE 1                                                                
UNUP5    MVC   0(10,R6),1(R7)                                                   
UNUP6    LA    R9,1                COUNT OUTPUT LINES                           
         LA    R6,20(R6)                                                        
         XR    R4,R4                                                            
         IC    R4,1(R3)            ELEMENT LENGTH                               
         SH    R4,=H'4'                                                         
         SRL   R4,1                NUMBER OF 2 BYTE OPERANDS                    
         LA    R7,4(R3)            OPERANDS                                     
         SPACE 1                                                                
         CLI   2(R3),5             TYPE 5                                       
         BNE   UNUP8                                                            
         MVC   HALF,0(R7)                                                       
         LH    RE,HALF                                                          
         BCTR  RE,0                                                             
         MH    RE,=H'3'                                                         
         LA    RE,MONTH(RE)                                                     
         MVC   0(3,R6),0(RE)                                                    
         B     UNUP9                                                            
         SPACE 1                                                                
UNUP8    LTR   R4,R4                                                            
         BZ    UNUP12                                                           
         MVC   HALF,0(R7)                                                       
         LH    RE,HALF                                                          
         EDIT  (RE),(5,(R6)),ALIGN=LEFT                                         
         CLI   0(R6),C' '                                                       
         BNE   UNUP9                                                            
         MVI   0(R6),C'0'                                                       
         SPACE 1                                                                
UNUP9    LA    R9,1(R9)                                                         
         LA    R7,2(R7)                                                         
         LA    R6,20(R6)                                                        
         BCTR  R4,0                                                             
         B     UNUP8                                                            
         SPACE 1                                                                
UNUP12   GOTO1 UNSCAN,DMCB,((R9),BLOCK),(R2),0                                  
         CLC   8(5,R2),=C'HUT,0'                                                
         BNE   *+10                                                             
         MVC   8(5,R2),=C'SHARE'                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
*              NEW UPGRADE EXPRESSION                                           
         SPACE 1                                                                
NEWUP    MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(99),BLOCK                                                
         LA    R2,BLOCK            OUTPUT AREA                                  
         SPACE 1                                                                
         CLI   SAVEP,0             ARE ALL UPGRADES TO BE DISPLAYED?            
         BE    NEWUP1              YES                                          
         ZIC   RF,SAVEP            UPGRADE NUMBER                               
         B     NEWCHECK                                                         
         SPACE 1                                                                
         LA    R3,14(R3)           BUMP TO NEXT ELEMENT                         
         CLC   =X'050E',0(R3)      IS IT A NEW UPGRADE ELEMENT?                 
         BNE   EXIT                NO -- NOTHING TO DISPLAY                     
NEWCHECK BCT   RF,*-14                                                          
         SPACE 1                                                                
NEWUP1   LA    R3,2(R3)            BOOK FIELD                                   
         CLI   0(R3),0                                                          
         BE    NEWTYP                                                           
         SPACE 1                                                                
         SR    R7,R7                                                            
         LA    R4,241                                                           
         IC    R7,0(R3)                                                         
         SLL   R7,24                                                            
         SPACE 1                                                                
NEWBK1   SR    R6,R6                                                            
         SLDL  R6,1                IF BIT ON                                    
         LTR   R6,R6               MOVE BOOK NUMBER TO OUTPUT(BLOCK)            
         BZ    *+20                                                             
         MVI   0(R2),C'B'                                                       
         STC   R4,1(R2)                                                         
         MVI   2(R2),C','                                                       
         LA    R2,3(R2)                                                         
         SPACE 1                                                                
         LA    R4,1(R4)                                                         
         CH    R4,=H'249'                                                       
         BL    NEWBK1                                                           
         SPACE 1                                                                
NEWTYP   MVC   BOOKTYPE,1(R3)      SAVE BOOK TYPE                               
         LA    R3,2(R3)            TYPE FIELD                                   
         LA    R7,NTYPE            TYPE TABLE                                   
         CLC   0(1,R7),0(R3)                                                    
         BE    NEWTYP1             MATCH TYPE TO ELEMENT                        
         LA    R7,5(R7)                                                         
         CLI   0(R7),X'FF'                                                      
         BNE   *-18                                                             
         B     DEMTYP              TYPE IS DEMO                                 
         SPACE 1                                                                
NEWTYP1  MVC   0(4,R2),1(R7)       TYPE TO BLOCK                                
         CLI   1(R3),C'P'                                                       
         BNE   *+8                                                              
         MVI   0(R2),C'P'          PUT                                          
         SPACE 1                                                                
         LA    R2,1(R2)            FOLLOWED BY COMMA                            
         CLI   0(R2),C' '                                                       
         BNE   *-8                                                              
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
         LA    R5,4                MAX OPERANDS                                 
         LA    R3,2(R3)            FIRST OPERAND                                
         ST    R3,AFRSTOP          SAVE A POINTER TO IT                         
         OC    0(2,R3),0(R3)                                                    
         BNZ   NEWTYP2                                                          
         SH    R2,=H'4'            MUST BE A SHARE,0                            
         MVC   0(4,R2),=C'SH, '    REPLACE HUT,                                 
         LA    R2,3(R2)                                                         
         LA    R3,2(R3)                                                         
         BCTR  R5,0                                                             
         SPACE 1                                                                
NEWTYP2  CLI   0(R3),0                                                          
         BE    *+12                                                             
         BAS   RE,BOOK             EDIT A BOOK FIELD                            
         B     *+8                                                              
         BAS   RE,VALUE            PUT OUT THE VALUE                            
         LA    R3,2(R3)                                                         
         BCT   R5,*+8                                                           
         B     NEXTEL                                                           
         OC    0(2,R3),0(R3)       NO DATA                                      
         BZ    *-18                                                             
         B     NEWTYP2                                                          
         SPACE 1                                                                
NEXTEL   CLI   SAVEP,0             ARE ALL UPGRADES TO BE DISPLAYED?            
         BNE   *+14                NO -- WE'RE DONE                             
         CLC   0(2,R3),=X'050E'                                                 
         BE    NEWUP1              PROCESS NEXT                                 
         BCTR  R2,0                                                             
         CLI   0(R2),C','                                                       
         BNE   *+8                                                              
         MVI   0(R2),C' '                                                       
         SPACE 1                                                                
         XR    R3,R3                                                            
         L     R4,SAVEP            A(FIELD HEADER)                              
         IC    R3,0(R4)                                                         
         TM    1(R3),X'02'         EXTENDED?                                    
         BZ    *+8                 NO                                           
         SH    R3,=H'8'                                                         
         SH    R3,=H'9'            GET DATA LENGTH                              
         BP    *+6                                                              
         DC    H'0'                                                             
         EX    R3,*+8                                                           
         B     EXIT                                                             
         MVC   8(0,R4),BLOCK       OUTPUT DATA                                  
         SPACE 1                                                                
DEMTYP   MVI   FULL,0                                                           
         MVC   FULL+1(2),0(R3)     TYPE AND NUMBER                              
         SPACE 1                                                                
***********NOTE - DEMCON IS REALLY DEMOCON **************                       
         SPACE 1                                                                
         GOTO1 DEMCON,DMCB,(0,FULL),(6,(R2)),DBLOCK                             
         SPACE 1                                                                
         LA    R2,1(R2)                                                         
         CLI   0(R2),C' '                                                       
         BNE   *-8                                                              
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
         LA    R3,2(R3)                                                         
         BAS   RE,VALUE            VALUE                                        
         LA    R3,8(R3)                                                         
         B     NEXTEL                                                           
         SPACE 1                                                                
BOOK     XR    R8,R8               GET A MONTH AND YEAR                         
         IC    R8,1(R3)            MONTH                                        
         MH    R8,=H'3'                                                         
         LA    R8,MONTH(R8)                                                     
         MVC   0(3,R2),0(R8)                                                    
         SPACE 1                                                                
         LA    R2,3(R2)                                                         
         XR    R8,R8                                                            
         IC    R8,0(R3)            YEAR                                         
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(3),DUB+6(2)                                                 
         MVC   0(2,R2),WORK+1                                                   
         LA    R2,2(R2)            POINT PAST YEAR                              
         C     R3,AFRSTOP          TEST FOR FIRST OPERAND                       
         BNE   BOOKX               NO                                           
         CLI   BOOKTYPE,0          TEST FOR ANY BOOK TYPE                       
         BE    BOOKX               NO                                           
         MVI   0(R2),LPAREN        YES-TACK IT ON TO YEAR                       
         MVC   1(1,R2),BOOKTYPE                                                 
         MVI   2(R2),RPAREN                                                     
         LA    R2,3(R2)                                                         
         SPACE 1                                                                
BOOKX    MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         BR    RE                                                               
         SPACE 1                                                                
VALUE    MVC   HALF,0(R3)                                                       
         LH    R8,HALF                                                          
         EDIT  (R8),(5,(R2)),ALIGN=LEFT                                         
         AR    R2,R0                                                            
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         BR    RE                                                               
         EJECT                                                                  
TYPE     DC    X'01',C'HOM       '                                              
         DC    X'02',C'RTG       '                                              
         DC    X'03',C'HUT       '                                              
         DC    X'04',C'INDEX     '                                              
         DC    X'05',C'SVI       '                                              
         DC    X'FF'                                                            
         SPACE 1                                                                
NTYPE    DC    X'01',C'HOM '       HOMES                                        
         DC    X'02',C'R   '       RATING                                       
         DC    X'03',C'HUT '       HUT  -  SHARE                                
         DC    X'04',C'I   '       INDEX                                        
         DC    X'05',C'SVI '       SVI                                          
         DC    X'06',C'HPT '       HPT                                          
         DC    X'FF'                                                            
         SPACE 1                                                                
MONTH    DC    C'ESTJANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                       
         SPACE 2                                                                
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
UNUPD    DSECT                                                                  
SAVEP    DS    F                                                                
SAVEP2   DS    F                                                                
UNSCAN   DS    A                                                                
DEMCON   DS    A                                                                
DBLOCK   DS    A                                                                
AFRSTOP  DS    A                   POINTER TO FIRST UPGRADE OPERAND             
         SPACE 1                                                                
BLOCK    DS    5CL20                                                            
DMCB     DS    6F                                                               
FULL     DS    F                                                                
HALF     DS    F                                                                
WORK     DS    CL17                                                             
DUB      DS    D                                                                
BOOKTYPE DS    X                   SAVED BOOK TYPE FROM UPGRADE                 
UNUPDX   EQU   *                                                                
         SPACE 2                                                                
* EQUATES                                                                       
*                                                                               
LPAREN   EQU   C'('                                                             
RPAREN   EQU   C')'                                                             
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019REUNUPGR  05/01/02'                                      
         END                                                                    
