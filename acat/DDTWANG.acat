*          DATA SET DDTWANG    AT LEVEL 002 AS OF 10/14/13                      
*CATALP TWANG                                                                   
***********************************************************************         
* P1=A(TWA)                                                           *         
* P2=A(1920 BYTE OUTPUT AREA)                                         *         
* P3=OPTIONAL FILL CHARACTERS                                         *         
***********************************************************************         
         TITLE 'FORMAT TWA INTO 24*80 PRINTABLE BLOCK'                          
TWANG    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TWRKX-TWRK,**TWANG                                               
         USING TWRK,RC             RC=A(W/S)                                    
         LM    R2,R3,0(R1)         R2=A(TWA),R3=A(OUTPUT BLOCK)                 
         XC    FILLS,FILLS         SET FILL CHRS                                
         CLC   8(2,R1),=C'PU'                                                   
         BNE   *+10                                                             
         MVC   FILLS,10(R1)                                                     
         LA    RE,0(R3)            CLEAR OUTPUT BLOCK                           
         LA    RF,24*80                                                         
         XCEFL                                                                  
         AHI   R4,6144             R4=A(END OF TWA+A BIT)                       
         AR    R4,R2                                                            
         SR    R5,R5                                                            
         LA    R2,64(R2)           R2=A(FIRST TWA FIELD)                        
*                                                                               
TWA2     CR    R2,R4               TEST WITHIN TWA LIMIT                        
         BL    *+6                                                              
         DC    H'0'                NO - DIE                                     
         CLI   0(R2),0             TEST END OF TWA                              
         BE    TWAX                                                             
         CLI   1(R2),X'FF'         IGNORE NOP FIELDS                            
         BE    TWA6                                                             
         LLC   R5,0(R2)            GET L'FIELD+HDR                              
         AHI   R5,-9               L'FIELD-1                                    
         TM    1(R2),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R5,-8               YES - SUBTRACT EXTENSION LENGTH              
         LTR   R5,R5                                                            
         BNM   *+6                                                              
         DC    H'0'                INVALID FIELD LENGTH                         
         MVC   DUB(2),2(R2)        GET A(OUTPUT FIELD)                          
         LH    R6,DUB                                                           
         LA    R6,0(R3,R6)                                                      
         MVC   DUB(1),FILLS        SET FILLS                                    
         TM    1(R2),X'20'                                                      
         BO    *+10                                                             
         MVC   DUB(1),FILLS+1                                                   
         CLI   DUB,0                                                            
         BNE   TWA4                                                             
         TM    1(R2),X'0C'         BYPASS ZERO INTENSITY FIELDS                 
         BO    TWA6                                                             
         EX    R5,*+8              MOVE FIELD TO OUTPUT BLOCK                   
         B     *+10                                                             
         MVC   0(0,R6),8(R2)                                                    
         B     TWA6                                                             
*                                                                               
TWA4     MVC   0(1,R6),DUB         FILL WITH CHARACTER                          
         AHI   R5,-1                                                            
         BM    TWA6                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R6),0(R6)                                                    
*                                                                               
TWA6     IC    R5,0(R2)            BUMP TO NEXT TWA FIELD                       
         AR    R2,R5                                                            
         B     TWA2                                                             
*                                                                               
TWAX     LA    R4,24               ENSURE NO FUNNY CHARACTERS                   
         TR    0(80,R3),OUTALLC                                                 
         LA    R3,80(R3)                                                        
         BCT   R4,*-10                                                          
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* ALL CHARACTERS ABOVE X'40' ARE DEFINED AS VALID                    *          
* BOX CHARACTERS ARE 8F,AB,AC,AD,BB,BC,BD,BF,CB,CC,EB,EC,FA AND 42   *          
**********************************************************************          
OUTALLC  DC    XL16'40404040404040404040404040404040'  00-0F                    
         DC    XL16'40404040404040404040404040404040'  10-1F                    
         DC    XL16'40404040404040404040404040404040'  20-2F                    
         DC    XL16'40404040404040404040404040404040'  30-3F                    
         DC    XL16'404142434445464748494A4B4C4D4E4F'  40-4F                    
         DC    XL16'505152535455565758595A5B5C5D5E5F'  50-5F                    
         DC    XL16'606162636465666768696A6B6C6D6E6F'  60-6F                    
         DC    XL16'707172737475767778797A7B7C7D7E7F'  70-7F                    
         DC    XL16'808182838485868788898A8B8C8D8E8F'  80-8F                    
         DC    XL16'909192939495969798999A9B9C9D9E9F'  90-9F                    
         DC    XL16'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'  A0-AF                    
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'  B0-BF                    
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'  C0-CF                    
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'  D0-DF                    
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'  E0-EF                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'  F0-FF                    
*                                                                               
         LTORG                                                                  
*                                                                               
TWRK     DSECT                                                                  
DUB      DS    D                                                                
FILLS    DS    CL2                                                              
TWRKX    EQU   *                                                                
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDTWANG   10/14/13'                                      
         END                                                                    
