*          DATA SET ACCATCALL  AT LEVEL 004 AS OF 07/21/09                      
*CATALP CATCALL                                                                 
CATCALL  TITLE ' GET EXPENSE CATEGORY FROM SE ACCOUNT'                          
CATCALL  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CATC**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL WORKLNG STORAGE)                  
         LR    R9,R1                                                            
         USING CATD,R9             R9=A(VATICAN CONTROL BLOCK)                  
         MVI   CATRTN,C' '         CLEAR THE RETURN FIELDS                      
         MVC   CATRTN+1(CATRTNL-1),CATRTN                                       
         MVI   CATERR,0                                                         
         SPACE 1                                                                
***********************************************************************         
*        MVC   DIR,ACCFIL                                             *         
*        GOTO1 CATDMGR,DMCB,DMDTFA,DIR                                *         
*        L     RE,12(R1)           RE=A(ACCFIL DTF)                   *         
*        MVC   DATADISP,OLDDISP                                       *         
*        TM    DTFTYPE-DTFPHD(RE),DTFTEMU                             *         
*        BZ    CAT2                                                   *         
*        MVC   DIR,ACCDIR                                             *         
*        MVC   DATADISP,NEWDISP                                       *         
*        OI    INDS,INDSEMU        SET ACCFIL EMULATED FLAG           *         
*        BASR  RE,RF                                                  *         
*        L     RE,12(R1)           RE=A(ACCDIR DTF)                   *         
*                                                                     *         
*AT2     ST    RE,ADTF             SAVE A(DIRECTORY DTF)              *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   SAVKEY,0(RE)        SAVE LAST KEY                      *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 CATDMGR,DMCB,DMKEY,ACCDIR,SAVKEY                                 
         EJECT                                                                  
***********************************************************************         
* GET LEDGER RECORD AND ESTABLISH HEIRARCHY                           *         
***********************************************************************         
         SPACE 1                                                                
         ICM   R2,15,CATLDG        GET ADDRESS OF LEDGER RECORD                 
         BNZ   CAT7                                                             
         MVC   KEY,SPACES          OR GO FIND IT                                
         MVC   KEY(LDGKEND),CATSEAC                                             
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   KEY(L'LDGKEY),IO                                                 
         BNE   ERR1                RECORD NOT FOUND                             
*        TM    INDS,INDSEMU        TEST NEW FILE                                
*        BZ    CAT7                                                             
         MVC   DA,IO+(LDGKDA-LDGRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
CAT7     LR    R4,R2                                                            
         MVI   ELCODE,ACLELQ       GET HEIRARCHY ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   ERR3                ELEMENT NOT FOUND                            
         LR    R5,R4                                                            
         USING ACLELD,R5                                                        
         SR    R1,R1               SAVE LENGTHS ELEMENT                         
         IC    R1,ACLLN                                                         
         BCTR  R1,0                                                             
         MVC   SAVACL(0),ACLELD                                                 
         EX    R1,*-6                                                           
*                                                                               
         LR    R4,R2               GET LEDGER ELEMENT                           
         MVI   ELCODE,LDGELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   ERR3                ELEMENT NOT FOUND                            
         USING LDGELD,R4                                                        
         MVI   CATPSTL,C'N'        DEFAULT IS NO                                
         TM    LDGSTAT,LDGSCOST    TEST COST POSTING                            
         BNO   *+8                                                              
         MVI   CATPSTL,C'Y'        COST POSTINGS                                
         SR    RF,RF                                                            
         ICM   RF,3,LDGXMSK        EXPENSE CATEGORY MASK                        
         SLL   RF,16                                                            
         LA    R0,12                                                            
         SR    R3,R3               R3 = NUMBER OF CATEGORY BYTES                
         LA    RE,CATSEAC          RE = EXPENSE ACCOUNT                         
         LA    RE,3(RE)                                                         
         LA    R1,CATACCL          R1 = LEDGER DEFAULT                          
*                                                                               
CAT10    LTR   RF,RF               TEST BIT ON                                  
         BNM   CAT12               BRANCH IF OFF                                
         MVC   0(1,R1),0(RE)       SE ACCOUNT BYTE TO CATEGORY                  
         LA    R1,1(R1)            NEXT CATEGORY FIELD                          
CAT12    LA    RE,1(RE)            NEXT SE ACCOUNT                              
         SLL   RF,1                SETUP THE NEXT BIT                           
         BCT   R0,CAT10            AND TEST IT                                  
         EJECT                                                                  
***********************************************************************         
* GET CATEGORY CODES FOR EACH ACCOUNT LEVEL                           *         
***********************************************************************         
         SPACE 1                                                                
         LA    R7,CATACCA          R7 = RETURN CATEGORY CODE                    
         LA    R6,CATLVA           R6 = A(INPUT LEVEL RECORD)                   
         LA    R5,SAVACL                                                        
         USING ACLELD,R5                                                        
         LA    R5,ACLVALS          R5 = LENGTH OF ACCOUNT                       
         LA    R3,4                R3 = LOOP                                    
*                                                                               
CAT20    ICM   R2,15,0(R6)         GET ADDRESS OF ACCOUNT RECORD                
         BNZ   CAT23                                                            
         MVC   KEY,SPACES          OR GO FIND IT                                
         SR    R1,R1                                                            
         IC    R1,0(R5)            LENGTH OF LEVEL                              
         LA    R1,2(R1)            PLUS C/U/L                                   
         MVC   KEY(0),CATSEAC      SE ACCOUNT KEY                               
         EX    R1,*-6                                                           
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   KEY(L'ACTKEY),IO                                                 
         BNE   ERR1                RECORD NOT FOUND                             
*        TM    INDS,INDSEMU        TEST NEW FILE                                
*        BZ    CAT23                                                            
         MVC   DA,IO+(ACTKDA-ACTRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
CAT23    LR    R4,R2                                                            
         MVI   ELCODE,SPAELQ       GET SPECIAL POSTING ELEMENT                  
CAT27    BAS   RE,GETEL                                                         
         BNE   CAT30                                                            
         USING SPAELD,R4                                                        
         CLI   SPATYPE,SPATANAL    MUST BE ANALYSIS                             
         BNE   CAT27                                                            
         MVC   0(L'CATACCA,R7),SPAAANAL SAVE CATEGORY FOR THIS LEVEL            
*                                                                               
CAT30    LR    R4,R2                                                            
         MVI   ELCODE,RSTELQ       GET STATUS ELEMENT                           
         BAS   RE,GETEL                                                         
         BNE   CAT35                                                            
         USING RSTELD,R4                                                        
         CLI   0(R7),C' '          TEST ANALYSIS CODE PRESENT                   
         BH    CAT33                                                            
         CLI   RSTCOSTG,C' '       TEST THE ONE BYTE CODE                       
         BNH   CAT33                                                            
         MVC   0(L'RSTCOSTG,R7),RSTCOSTG                                        
*                                                                               
CAT33    TM    RSTSTAT2,RSTSYCST   TEST COST WANTED                             
         BNO   *+8                                                              
         MVI   CATPSTA-CATACCA(R7),C'Y'                                         
         TM    RSTSTAT2,RSTSNCST   TEST NO COSTING                              
         BNO   *+8                                                              
         MVI   CATPSTA-CATACCA(R7),C'N'                                         
         CLI   0(R5),12            IS IT LOWEST LEVEL                           
         BNE   CAT35                                                            
         TM    RSTSTAT1,RSTSEADD   FLAGGED FOR DEPT                             
         BO    CAT35                                                            
         CLI   CATOFF,C' '         OFFICE PASSED                                
         BH    *+10                                                             
         MVC   CATOFF,NINES        IF NO OFFICE, USE 99                         
*                                                                               
         CLI   CATDPT,C' '         DEPARTMENT  PASSED                           
         BH    *+10                                                             
         MVC   CATDPT,NINES        IF NO DEPT USE 999 IF NOT ANALYZED           
*                                                                               
CAT35    CLI   0(R5),12            TEST LAST LEVEL JUST PROCESSED               
         BE    CAT40                                                            
         LA    R6,4(R6)            A(NEXT LEVEL RECORD)                         
         LA    R7,CATACCB-CATACCA(R7)                                           
         LA    R5,L'ACLVALS(R5)                                                 
         BCT   R3,CAT20                                                         
         EJECT                                                                  
***********************************************************************         
* MERGER LEDGER AND ACCOUNT LEVEL SETTINGS                            *         
***********************************************************************         
         SPACE 1                                                                
CAT40    MVC   CATCDE(CATACCA-CATACCL),CATACCL LEDGER DEFAULT                   
         LA    R7,CATACCA                                                       
         LA    R3,4                                                             
*                                                                               
CAT43    CLI   0(R7),C' '          TEST ANY OVERRIDE                            
         BNH   *+10                                                             
         MVC   CATCDE,0(R7)        OVERRIDE ACCOUNT                             
         CLI   CATPSTA-CATACCA(R7),C' '                                         
         BNH   *+10                                                             
         MVC   CATPST,CATPSTA-CATACCA(R7)                                       
         LA    R7,CATACCA-CATACCL(R7)                                           
         BCT   R3,CAT43                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD THE 13 ACCOUNT                                                *         
***********************************************************************         
         SPACE 1                                                                
CAT50    CLI   CATPST,C'N'         NO POSTINGS                                  
         BE    XITMOD                                                           
         CLI   CATCDE,C' '         ANY CATEGORY                                 
         BH    *+12                                                             
         MVI   CATPST,C'N'         NO POSTINGS                                  
         B     CATXNO              ERROR                                        
         MVC   CATACC3(1),CATSEAC  COMPANY                                      
         MVC   CATACC3+1(2),=C'13'                                              
         MVC   CATACC3+3(L'CATCDE),CATCDE CATEGORY CODE                         
         MVC   KEY,SPACES          VALIDATE HIGH LEVEL OF 13                    
         MVC   KEY(L'CATACC3),CATACC3                                           
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   KEY(L'ACTKEY),IO                                                 
         BNE   CATXNO              INVALID CATEGORY                             
*                                                                               
         ICM   R2,15,CATLDG3       GET ADDRESS OF LEDGER RECORD                 
         BNZ   CAT53                                                            
         MVC   KEY,SPACES          OR GO FIND IT                                
         MVC   KEY(LDGKEND),CATACC3                                             
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   KEY(L'ACTKEY),IO                                                 
         BNE   ERR1                RECORD NOT FOUND                             
*        TM    INDS,INDSEMU        TEST NEW FILE                                
*        BZ    CAT53                                                            
         MVC   DA,IO+(LDGKDA-LDGRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
CAT53    LR    R4,R2                                                            
         MVI   ELCODE,ACLELQ       GET HEIRARCHY ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   ERR3                ELEMENT NOT FOUND                            
         LR    R5,R4                                                            
         USING ACLELD,R5                                                        
         LA    RF,ACLVALS                                                       
         USING ACLVALS,RF                                                       
         CLI   ACLVLEN,12          ONE LEVEL                                    
         BE    XITMOD              ALL DONE                                     
         LA    R0,3                                                             
         LA    RF,L'ACLVALS(RF)    FIND LAST LEVEL                              
         CLI   ACLVLEN,12                                                       
         BE    *+8                                                              
         BCT   R0,*-12                                                          
         SH    RF,=Y(L'ACLVALS)    BACK UP ONE LEVEL                            
         CH    R0,=H'3'            TEST FOR TWO LEVELS                          
         BE    *+8                                                              
         SH    RF,=Y(L'ACLVALS)    ASSUME LAST 2 ARE OFFICE/DEPT                
         SR    R1,R1                                                            
         IC    R1,ACLVLEN                                                       
         LA    RE,CATACC3+3(R1)    RE = NEXT LEVEL OF 13                        
         MVC   0(L'CATOFF,RE),CATOFF SHOULD BE OFFICE                           
         LA    RF,L'ACLVALS(RF)                                                 
         CLI   ACLVLEN,12          IF 2 LEVELS OFFICE BUT NO DEPT               
         BE    CAT60                                                            
         SR    R1,R1                                                            
         IC    R1,ACLVLEN                                                       
         LA    RE,CATACC3+3(R1)    RE = NEXT LEVEL OF 13                        
         LA    R3,CATDPT           WAS DEPARTMENT PAST                          
         CLI   0(R3),C' '                                                       
         BH    *+8                                                              
         LA    R3,=C'999'          USE DEFAULT                                  
         MVC   0(L'CATDPT,RE),0(R3)  SHOULD BE DEPT.                            
*                                                                               
CAT60    CLI   ACLVLEN,12          GET TO LAST LEVEL                            
         BE    *+12                                                             
         LA    RF,L'ACLVALS(RF)                                                 
         B     CAT60                                                            
         SH    RF,=Y(L'ACLVALS)    BACKUP ONE                                   
         SR    R1,R1                                                            
         IC    R1,ACLVLEN                                                       
         LA    RE,CATACC3+3(R1)    RE = LAST LEVEL IN ACCOUNT                   
         CLI   0(RE),C' '                                                       
         BH    XITMOD                                                           
*                                                                               
CATXNO   MVI   CATERR,CATINV        CATEGORY (ANALYSIS IS INVALID)              
         B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER INTERFACE                                              *         
***********************************************************************         
         SPACE 1                                                                
HIGH     LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 CATDMGR,DMCB,DMRDHI,ACCDIR,KEY,IO                                
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
GET      LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 CATDMGR,DMCB,GETREC,ACCMST,DA,IO,WORK                            
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
* RESTORE LAST KEY READ FROM DIRECTORY FOR APPLICATION IF THIS        *         
* MODULE CHANGED THE READ SEQUENCE                                    *         
***********************************************************************         
         SPACE 1                                                                
***********************************************************************         
*ITMOD   L     RE,ADTF                                                *         
*        USING ISDTF,RE                                               *         
*        LH    R1,ISKEYLN1                                            *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   WORK,0(RE)          EXTRACT CURRENT KEY                *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
*        EX    R1,*+8                                                 *         
*        BZ    XITMOD2                                                *         
*        OC    SAVKEY(0),SAVKEY    TEST ANY PREVIOUS DIRECTORY I/O    *         
*        EX    R1,*+8                                                 *         
*        BE    XITMOD2                                                *         
*        CLC   SAVKEY(0),WORK      TEST DIRECTORY I/O EXECUTED        *         
*        GOTO1 CATDMGR,DMCB,DMREAD,ACCDIR,SAVKEY,IO                   *         
*        BNE   ERR2                                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
XITMOD   OC    SAVKEY(L'ACCKEY),SAVKEY                                          
         BZ    XITMOD2                                                          
         GOTO1 CATDMGR,DMCB,DMKEY,ACCDIR,WORK                                   
         CLC   SAVKEY(L'ACCKEY),WORK                                            
         BE    XITMOD2                                                          
         GOTO1 (RF),(R1),(X'08',DMREAD),ACCDIR,SAVKEY,SAVKEY                    
*                                                                               
XITMOD2  DS    0H                                                               
         SPACE 2                                                                
XIT      XIT1  ,                                                                
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS                                                      *         
***********************************************************************         
         SPACE 1                                                                
ERR1     DC    H'0',C'RECORD NOT FOUND'                                         
ERR2     DC    H'0',C'DISK ERROR'                                               
ERR3     DC    H'0',C'ELEMENT NOT FOUND'                                        
         SPACE 2                                                                
*MDTFA   DC    C'DTFAD  '                                                       
DMKEY    DC    C'DMKEY  '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
GETREC   DC    C'GETREC '                                                       
*CCFIL                                                                          
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
EFFS     DC    4X'FF'                                                           
SPACES   DC    CL64' '                                                          
NINES    DC    CL9'999999999'                                                   
         SPACE 2                                                                
DATADISP DC    Y(ACCRFST-ACCRECD)                                               
*LDDISP  DC    Y(ACCORFST)                                                      
*EWDISP  DC    Y(ACTRFST-ACTRECD)                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR LOCAL WORKING STORAGE                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                     ** WORKING STORAGE DSECT **                  
*DTF     DS    A                   A(ACCFIL/ACCDIR DTF)                         
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
ELCODE   DS    XL1                                                              
*ATADISP DS    H                   DISPLACEMENT TO FIRST ELEMENT                
*IR      DS    CL(L'ACCFIL)        DIR NAME (ACCFIL/ACCDIR)                     
SAVKEY   DS    XL64                SAVED DIRECTORY KEY AREA                     
SAVACL   DS    XL100                                                            
*NDS     DS    XL1                 INDICATOR BYTE                               
*NDSEMU  EQU   X'80'               EMULATED ACCFIL (NEW FILES)                  
*                                                                               
*                                                                               
DA       DS    XL4                 DISK ADDRESS                                 
KEY      DS    XL64                KEY AREA                                     
IO       DS    2000X               I/O AREA                                     
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE ACCATCALLD                                                     
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
*********PRINT OFF                                                              
*********INCLUDE DMDTFIS                                                        
*********PRINT ON                                                               
         SPACE 1                                                                
* DMDTFPH                                                                       
*********PRINT OFF                                                              
*********INCLUDE DMDTFPH                                                        
*********PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004ACCATCALL 07/21/09'                                      
         END                                                                    
