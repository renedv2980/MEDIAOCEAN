*          DATA SET ACPAYSPT   AT LEVEL 030 AS OF 09/23/14                      
*CATALP ACPAYSPT                                                                
         TITLE 'ACPAYSPT - TRANSFER SPOT CLEARANCES ON-LINE'                    
ACPAYSPT CSECT                                                                  
         PRINT NOGEN                                                            
***********************************************************************         
* R1 = A(PAYMENT INTERFACE BLOCK)  SEE PAYD                           *         
***********************************************************************         
         NMOD1 LWSEND-LWSD,ACPAYSPT,R9,CLEAR=YES,RR=R2                          
         USING LWSD,RC                                                          
         ST    R1,ADPAY            SAVE ADDRESS OF PAY BLOCK                    
         ST    R2,RELO                                                          
*                                                                               
         USING REQD,R3                                                          
         USING PAYD,R7                                                          
         USING COMFACSD,R8                                                      
         LA    R7,0(,R1)                                                        
         L     R8,PAYFACS          A(COMFACS)                                   
         L     R3,PAYREQ           A(PAY REQUEST)                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVI   DMINBTS,0                                                        
         MVI   DMOUTBTS,X'7D'                                                   
         XC    DMCB(12),DMCB                                                    
         MVC   DMCB+4(3),=X'D9000A'                                             
         MVI   DMCB+7,QADDTRN                                                   
         GOTO1 CCALLOV,DMCB                                                     
         MVC   ADDTRN,0(R1)                                                     
*                                                                               
         MVI   DMCB+7,QSQUASH                                                   
         GOTO1 CCALLOV,DMCB                                                     
         MVC   SQUASHER,0(R1)                                                   
*                                                                               
         MVI   DMCB+7,QGETBRD      GETBROAD                                     
         GOTO1 CCALLOV,DMCB                                                     
         MVC   GETBROAD,0(R1)                                                   
*                                                                               
         CLI   PAYMODE,C'I'        INIT MODE                                    
         BE    VCPY                                                             
*                                                                               
         XC    PAYERROR,PAYERROR   CLEAR THE ERROR MESSAGE                      
         MVI   STATYPE,STATION        STATION PAYMENT                           
         OC    REQREP,REQREP                                                    
         BZ    PAY03                                                            
         CLC   REQREP,=3C'0'                                                    
         BE    PAY03                                                            
         MVI   STATYPE,REP            OR REP                                    
*                                                                               
PAY03    GOTO1 CDATCON,DMCB,(5,0),(2,TODAY2)  COMPRESSED                        
         GOTO1 CDATCON,DMCB,(5,0),(1,TODAY1)  PACKED                            
         GOTO1 CDATCON,DMCB,(5,0),(0,TODAY0)  CHARACTER                         
         GOTO1 CDATCON,DMCB,(0,REQEND),(1,PSTDATE)                              
         MVC   MOS(1),TODAY0+1     BUILD MOS                                    
         MVC   MOS+1(1),TODAY0+3                                                
         CLI   TODAY0+2,C'1'                                                    
         BNE   PAY05                                                            
         MVI   MOS+1,C'A'                                                       
         CLI   TODAY0+3,C'0'                                                    
         BE    PAY05                                                            
         MVI   MOS+1,C'B'                                                       
         CLI   TODAY0+3,C'1'                                                    
         BE    PAY05                                                            
         MVI   MOS+1,C'C'                                                       
*                                                                               
PAY05    LA    R2,IO               INIT ADDTRN CONTROL BLOCK                    
         ST    R2,TRNREC           TRANSACTION RECORD                           
         MVC   TRNCOMF,PAYFACS     A(COMFACS)                                   
         LA    RE,LEDGTAB          POINT AT LEDGER TABLE                        
         ST    RE,TRNLDG                                                        
         MVI   TRN#LDGS,3          SET NUMBER OF LEDGER TABLE ENTRIES           
         MVC   TRNPUSER,PAYUSRID   SET USER ID NUMBER                           
         MVC   TRNCACNM,SPACES                                                  
         MVC   TRNCACNM(L'PAYCLINM),PAYCLINM                                    
*                                                                               
         LH    R1,=Y(IO2-LWSD)                                                  
         LA    R1,LWSD(R1)         RELOCATE DISP TO IO AREA                     
         ST    R1,AACCOUNT                                                      
         LH    R1,=Y(IO3-LWSD)                                                  
         LA    R1,LWSD(R1)                                                      
         ST    R1,ACONTRA                                                       
         LH    R1,=Y(IO4-LWSD)                                                  
         LA    R1,LWSD(R1)                                                      
         ST    R1,ABUCKET                                                       
*                                                                               
         MVC   TRNACC,AACCOUNT     PASS IO AREA TO ADDTRN                       
         MVC   TRNCAC,ACONTRA      TO OPTIMIZE IOS                              
         MVC   TRNBUK,ABUCKET                                                   
*                                                                               
         GOTO1 CGETFACT,DMCB,(X'80',0),F#UTLD                                   
*                                                                               
         USING F@UTLD,R1                                                        
         L     R1,0(,R1)                                                        
         TM    F@TTEST,X'80'       TEST UPD=NO                                  
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
*                                                                               
         GOTO1 CGETFACT,DMCB,(X'80',0),F#SELISD                                 
*                                                                               
         USING F@SELISD,R1                                                      
         L     R1,0(,R1)                                                        
         TM    F@SEIND,X'04'       TEST READ ONLY                               
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
         DROP  R1                                                               
*                                                                               
         ZAP   NET,=P'0'                                                        
         ZAP   CSH,=P'0'                                                        
*                                                                               
         OC    PAYNME,SPACES       PAYEES NAME                                  
         CLC   PAYNME,SPACES                                                    
         BNE   *+14                                                             
         MVC   ERRMSG(18),=C'MISSING PAYEE NAME'                                
         B     ERRXIT                                                           
*                                                                               
         OC    PAYSTADD(4*26),SPACES                                            
         CLC   PAYSTADD,SPACES                                                  
         BNE   *+14                                                             
         MVC   ERRMSG(21),=C'MISSING PAYEE ADDRESS'                             
         B     ERRXIT                                                           
         EJECT                                                                  
***********************************************************************         
* VALIDATE COMPANY RECORD                                             *         
***********************************************************************         
                                                                                
         USING CPYRECD,R2                                                       
VCPY     LA    R2,KEY                                                           
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,PAYCOMP      COMPANY CODE                                
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   CPYKEY,IO            CHECK COMPANY RECORD                        
         BE    VCPY3                                                            
         MVC   ERRMSG(24),=C'ACCPAK COMPANY NOT FOUND'                          
         B     ERRXIT                                                           
*                                                                               
VCPY3    SR    R1,R1                                                            
         LA    R2,IO                                                            
         LA    R4,ACCORFST(R2)     GET COMPANY ELEMENT                          
VCPY5    CLI   0(R4),CPYELQ                                                     
         BE    VCPY7                                                            
         IC    R1,1(,R4)                                                        
         AR    R4,R1                                                            
         CLI   0(R4),0                                                          
         BNE   VCPY5                                                            
         MVC   ERRMSG(29),=C'INVALID ACCPAK COMPANY RECORD'                     
         B     ERRXIT                                                           
*                                                                               
         USING CPYELD,R4                                                        
VCPY7    CLC   CPYALPHA,PAYALPHA   DOES ALPHA CODE MATCH                        
         BE    VCPY9                                                            
         MVC   ERRMSG(32),=C'ACCPAK ALPHA CODE DOES NOT MATCH'                  
         B     ERRXIT                                                           
*                                                                               
VCPY9    CLI   PAYMODE,C'I'                                                     
         BE    XIT                                                              
         MVC   TRNCPYS1,CPYSTAT1                                                
         MVC   TRNCPYS2,CPYSTAT2                                                
         MVC   TRNCPYS3,CPYSTAT3                                                
         MVC   TRNCPYS4,CPYSTAT4                                                
         MVC   TRNCPYS5,CPYSTAT5                                                
         MVC   TRNCPYS6,CPYSTAT6                                                
         MVC   TRNCPYS7,CPYSTAT7                                                
         MVC   TRNCPYS8,CPYSTAT8                                                
         CLI   CPYLN,CPYLN3Q                                                    
         BL    VCPY10                                                           
         MVC   TRNCPYS9,CPYSTAT9                                                
         MVC   TRNCPYSA,CPYSTATA                                                
*                                                                               
VCPY10   LA    R0,PALAREA                                                       
         STCM  R0,15,TRNPAL                                                     
*                                                                               
         TM    CPYSTAT4,CPYSOFF2   TEST NEW OFFICES                             
         BNO   VCPY11                                                           
         CLI   REQOFF+1,C' '                                                    
         BH    VCPY11                                                           
         MVC   ERRMSG(34),=C'ACCPAK REQUIRES 2 CHARACTER OFFICE'                
         B     ERRXIT                                                           
*                                                                               
VCPY11   MVI   COMREP,C'N'         IS IT A COMMISSION ONLY REP                  
         OC    CPYREPC,CPYREPC                                                  
         BZ    GPROF                                                            
         SR    R5,R5                                                            
         ICM   R5,3,CPYREPC        REP CODE FOR COMMISSION ONLY                 
         CVD   R5,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(5),DUB+5(3)                                                 
         CLC   REQREP,WORK+2                                                    
         BNE   GPROF                                                            
         MVI   COMREP,C'Y'         COMMISSION ONLY REP                          
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* GET THE AC54 PROFILES                                               *         
***********************************************************************         
                                                                                
GPROF    XC    WORK,WORK           BUILD KEY FOR GETPROF                        
         MVC   WORK(4),=C'A054'                                                 
         MVC   WORK+4(1),PAYCOMP   COMPANY CODE                                 
         MVC   WORK+12(2),REQAGY   ALPHA ID                                     
         GOTO1 CGETPROF,DMCB,WORK,PROFILE,CDATAMGR                              
*                                                                               
         USING UNTRECD,R2                                                       
         LA    R2,KEY                                                           
         MVI   UNTKUNT,C'S'        VERIFY UNIT "S"                              
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   KEY,IO                                                           
         BE    LDGR                                                             
         MVC   ERRMSG(23),=C'ACCPAK UNIT S NOT FOUND'                           
         B     ERRXIT                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE LEDGER FOR PAYEE ACCOUNT                                   *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
LDGR     L     R3,PAYREQ                                                        
         MVC   ACTKLDG(13),SPACES                                               
         MVI   ACTKLDG,C'S'        VERIFY THE LEDGER                            
         CLI   PAYCNTRY,C'C'       CANADIAN                                     
         BNE   *+8                                                              
         MVI   ACTKLDG,C'T'        LEDGER IS T                                  
         CLI   REQMED,C'N'         IS THE MEDIA NETWORK                         
         BNE   LDGR3                                                            
         CLI   PROFILE+5,C'Y'      OPTION TO USE LEDGER U                       
         BNE   LDGR3                                                            
         MVI   ACTKLDG,C'U'        FOR NETWORK                                  
*                                                                               
LDGR3    MVC   PAYLED,ACTKLDG      PAYEE LEDGER                                 
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKEY,IO                                                        
         BE    LDGR5               FOUND LEDGER RECORD                          
         MVC   ERRMSG(26),=C'ACCPAK LEDGER XX NOT FOUND'                        
         MVC   ERRMSG+14(2),ACTKUNT                                             
         B     ERRXIT                                                           
*                                                                               
LDGR5    MVC   ACTKACT(1),REQMED CHECK THE MEDIA LEVEL                          
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKEY,IO                                                        
         BE    MEDI                FOUND UNIT RECORD                            
         MVC   ERRMSG(28),=C'ACCPAK ACCOUNT XXX NOT FOUND'                      
         MVC   ERRMSG+15(3),ACTKUNT                                             
         B     ERRXIT                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* VALID MEDIA INTERFACE RECORD                                        *         
***********************************************************************         
                                                                                
         USING MINRECD,R2               GET MI RECORDS                          
MEDI     TM    TRNCPYS4,CPYSMINT        TEST COMPANY USES MI RECORDS            
         BNO   MEDIX                                                            
         LA    R2,KEY                                                           
         MVC   MINKEY,SPACES                                                    
         MVI   MINKTYP,MINKTYPQ         X'08' MEDIA INTERFACE RECORD            
         MVC   MINKCPY,PAYCOMP          COMPANY                                 
         MVC   MINKMED(1),PAYLED        SYSTEM - PAYEE LEDGER                   
         MVC   MINKMED+1(1),REQMED      MEDIA                                   
         MVC   MINKOFF,REQOFF           OFFICE                                  
*                                                                               
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   MINKEY,IO                IGNORE - IF NOT SETUP                   
         BE    MEDI2                                                            
         MVC   MINKOFF,SPACES           CLEAR OUT OFFICE                        
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         BNE   CSHR                                                             
*                                                                               
MEDI2    LA    R4,IO                                                            
         LA    R4,ACCORFST(R4)          POINT TO FIRST ELEMENT                  
*                                                                               
MEDI3    CLI   0(R4),0                  TEST FOR EOR                            
         BE    MEDIX                                                            
         CLI   0(R4),MDIELQ             TEST FOR MEDIA INTERFACE X'19'          
         BE    MEDI5                     YES                                    
         SR    R0,R0                    BUMP TO NEXT ELEMENT                    
         IC    R0,1(,R4)                                                        
         AR    R4,R0                                                            
         B     MEDI3                                                            
*                                                                               
         USING MDIELD,R4                                                        
MEDI5    SR    R1,R1                                                            
         IC    R1,MDILN                                                         
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   SVMDI(0),MDIELD         SAVE MEDIA INTERFACE ELEMENT             
         LA    R4,SVMDI                                                         
*                                                                               
         LA    RF,MDICSHR               CASH RECEIPT ACCOUNT                    
         CLI   PAYCNTRY,C'C'            CANADIAN?                               
         BNE   *+8                                                              
         LA    RF,MDICCSR               CANADIAN CASH RECEIPT ACCOUNT           
         CLI   0(RF),C' '               HAVE THEY SPECIFIED AN ACC?             
         BNH   MEDI7                    NO                                      
*                                                                               
         USING ACTRECD,R2                                                       
         MVC   ACTKEY,SPACES            CHECK MI CASH RECEIPT ACCOUNT           
         MVC   ACTKCPY,PAYCOMP          COMPANY                                 
         MVC   ACTKULA,0(RF)            CASH RECEIPT ACCOUNT                    
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKEY,IO                DOES THIS ACCOUNT EXIST                 
         BNE   *+10                      NO                                     
         MVC   CASHACC,ACTKCULA         SAVE CASH ACCOUNT                       
*                                                                               
MEDI7    CLI   MDICNTL,C' '             ANY  MEDIA CONTROL ACCOUNT ?            
         BNH   MEDI9                                                            
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,PAYCOMP          COMPANY                                 
         MVC   ACTKULA,MDICNTL          MEDIA CONTROL ACCOUNT                   
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKEY,IO                DOES THIS ACCOUNT EXIST                 
         BNE   *+10                      NO                                     
         MVC   SZACC,ACTKCULA           SAVE MEDIA CONTROL                      
*                                                                               
MEDI9    CLI   MDICONL,C' '             ANY  MEDIA CONTROL ACCOUNT ?            
         BNH   MEDIX                                                            
         MVC   ACTKEY,SPACES            MI MEDIA CONTROL(COM ONLY)              
         MVC   ACTKCPY,PAYCOMP          COMPANY                                 
         MVC   ACTKULA,MDICONL          MEDIA CONTROL ACCOUNT                   
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         CLC   ACTKEY,IO                DOES THIS ACCOUNT EXIST                 
         BNE   *+10                      NO                                     
         MVC   SZCOMREP,ACTKCULA        SAVE COMMISSION ONLY ACCOUNT            
*                                                                               
MEDIX    DS    0H                                                               
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE CASH RECEIPT ACCOUNT                                       *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
CSHR     CLI   REQTYPE,C'3'        IS THERE A CASH RECEIPT                      
         BNE   CSHR5                                                            
         CLI   CASHACC,0           TEST CASH ACCOUNT FROM MI.                   
         BNE   PACC                ALREADY READY HAVE ACCOUNT                   
         LA    R2,KEY                                                           
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,PAYCOMP          COMPANY                                 
         MVI   ACTKUNT,C'S'             UNIT                                    
*                                                                               
CSHR2    MVI   ACTKLDG,C'C'                                                     
         MVC   ACTKACT(4),=C'C003' HARD CASH RECEIPT ACCOUNT                    
         CLI   PAYCNTRY,C'C'       CANADA                                       
         BNE   *+8                                                              
         MVI   ACTKACT+1,C'C'                                                   
         CLI   PROFILE+2,C'Y'      CASH REC. BY MEDIA                           
         BNE   *+10                                                             
         MVC   ACTKACT+4(1),REQMED                                              
*                                                                               
CSHR3    GOTO1 HIGH,DMCB,KEY,IO                                                 
         MVC   CASHACC,ACTKEY                                                   
         CLC   ACTKEY,IO                                                        
         BE    PACC                                                             
         CLI   ACTKACT+4,C' '      DOES IT HAVE MEDIA                           
         BE    *+12                IF NOT MUST BE AN ERROR                      
         MVI   ACTKACT+4,C' '      TRY ACCOUNT WITHOUT MEDIA                    
         B     CSHR3                                                            
         MVC   ERRMSG(29),=C'ACCPAK CASH ACCOUNT NOT FOUND'                     
         B     ERRXIT                                                           
*                                                                               
CSHR5    SR    R1,R1                                                            
         ICM   R1,3,REQLEN                                                      
         AR    R3,R1               R3 TO NEXT REQUEST                           
         OC    REQLEN,REQLEN                                                    
         BNZ   CSHR                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE PAYEE ACCOUNT                                              *         
***********************************************************************         
                                                                                
*                                                                               
         USING ACTRECD,R2                                                       
PACC     L     R3,PAYREQ                                                        
         LA    R2,KEY                                                           
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,PAYCOMP          COMPANY                                 
         MVI   ACTKUNT,C'S'             UNIT                                    
         MVC   ACTKLDG,PAYLED           LEDGER                                  
         MVC   ACTKACT(1),REQMED        MEDIA                                   
*                                                                               
         CLI   COMREP,C'Y'             COMMISSION ONLY REP ?                    
         BE    MECA                                                             
         MVC   ACTKACT+1(4),REQREP     REP                                      
         CLI   STATYPE,REP                                                      
         BE    *+10                                                             
         MVC   ACTKACT+1(5),REQSTA    OR STATION                                
         MVC   PAYEE,ACTKEY        SAVE PAYEE ACCOUNT                           
         OI    DMINBTS,X'88'       READ FOR UPDATE-  PASS DELETED               
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         MVI   DMINBTS,0                                                        
         MVI   RSTAT,0                                                          
         CLC   ACTKEY,IO           IS THE PAYEE ON FILE                         
         BNE   PACC3               NO, ADD A ACCOUNT RECORD                     
         LA    R2,IO                                                            
         TM    ACCOSTAT(R2),X'80'  IS IT DELETED                                
         BNO   PACC7               IF NOT, OK TO UPDATE                         
         OI    RSTAT,RDEL          RECORD IS DELETED                            
*                                                                               
PACC3    LA    R2,IO                                                            
         MVC   ACTKEY,SPACES IF NOT BUILD PAYEE RECORD                          
         MVC   ACTKCULA,PAYEE                                                   
         XC    ACCORLEN(20,R2),ACCORLEN(R2)                                     
         LA    RE,PAYNME+19        GET LENGTH OF PAYEE NAME                     
         LA    RF,20                                                            
         CLI   0(RE),X'40'                                                      
         BH    PACC5                                                            
         BCTR  RE,0                                                             
         BCT   RF,*-10                                                          
*                                                                               
         MVC   ERRMSG(20),=CL20'MISSING PAYEE NAME'                             
         B     ERRXIT                                                           
*                                                                               
PACC5    LA    R4,ELEMENT                                                       
         USING NAMELD,R4                                                        
         MVI   NAMEL,NAMELQ     BUILD NAME ELEMENT                              
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
*                                                                               
         MVC   NAMEREC(0),PAYNME                                                
         LA    RF,NAMEREC-NAMELD(RF)  ADJUST FOR ELEMENT LENGTH                 
         STC   RF,NAMLN                                                         
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
         BAS   RE,BLDADDR          BUILD ADDRESSS                               
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
         USING RSTELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   RSTEL,RSTELQ      STATUS ELEMENT                                 
         MVI   RSTLN,RSTLN2Q    LENGTH                                          
         MVC   RSTANAL,STATYPE                                                  
         CLI   PAYREPST,C'Y'       UNWIRED NETWORK                              
         BNE   *+8                                                              
         MVI   RSTANAL,UNWNET                                                   
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
         USING ABLELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   ABLELD,ABLELQ      BALANCE ELEMENT                               
         MVI   ABLLN,ABLLN3Q     LENGTH                                         
         ZAP   ABLFRWD,=P'0'                                                    
         ZAP   ABLDR,=P'0'                                                      
         ZAP   ABLCR,=P'0'                                                      
         ZAP   ABLURG,=P'0'                                                     
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
         USING APOELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   APOEL,X'33'        PEEL ELEMENT                                  
         MVI   APOLN,X'14'       LENGTH                                         
         ZAP   APODR,=P'0'                                                      
         ZAP   APOCR,=P'0'                                                      
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
         TM    RSTAT,RDEL          TEST OLD RECORD DELETED                      
         BO    PACC9                                                            
         GOTO1 ADD,DMCB,(R2),(R2) ADD THE PAYEE RECORD                          
         B     PACC11                                                           
*                                                                               
PACC7    BAS   RE,CHKACT           CHECK ACCOUNT FOR NAME/ADDR PROFILE          
         TM    ACTFLAG,ACT22       DO WE WANT TO UPDATE THE ADDR?               
         BO    *+8                 NO - SKIP FIXADDR                            
         BAS   RE,FIXADDR          FIX ADDRESS                                  
         TM    ACTFLAG,ACT20       DO WE WANT TO UPDATE THE NAME?               
         BO    *+8                 NO - SKIP FIXNAME                            
         BAS   RE,FIXNAME          FIX NAME                                     
         TM    RSTAT,RRPL                                                       
         BNO   PACC11                                                           
*                                                                               
PACC9    GOTO1 WRITE,DMCB,(R2),(R2) WRITE BACK THE PAYEE RECORD                 
*                                                                               
PACC11   MVC   KEY,SPACES          CHECK IN CASE IT'S THE TEST SYSTEM           
         MVC   KEY(15),PAYEE                                                    
         GOTO1 HIGH,DMCB,KEY,(R2)                                               
         CLC   KEY,IO                                                           
         BE    MECA                                                             
         MVC   ERRMSG(10),=C'CAN''T ADD '                                       
         MVC   ERRMSG+10(13),KEY+1                                              
         B     ERRXIT                                                           
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE MEDIA CONTROL (SZ) ACCOUNT                                 *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
MECA     LA    R2,SZDFLT           BUILD SZ DEFAULT ACCOUNT                     
         MVC   SZDFLT,SPACES                                                    
         MVC   ACTKCPY,PAYCOMP                                                  
         MVC   ACTKUNT(3),=C'SZS'                                               
         MVC   ACTKACT+1(1),REQMED                                              
         CLI   REQMED,C'N'                                                      
         BNE   MECA2               NOT NETWORK                                  
         CLI   PROFILE+6,C'N'      NO OVERRIDE PROFILE                          
         BE    MECA2                                                            
         CLI   PROFILE,0                                                        
         BE    MECA2                                                            
         MVC   ACTKLDG(3),=C'ZUN' NETWORK - USE SZ?N                            
         MVC   ACTKACT(1),PROFILE+6                                             
*                                                                               
MECA2    LA    R2,KEY                                                           
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCULA,SZACC      TEST HAVE SZ ACCOUNT FROM MI                 
         CLI   ACTKCULA,0                                                       
         BNE   MECA3                                                            
         MVC   ACTKCULA,SZDFLT                                                  
*                                                                               
MECA3    MVC   SZACC,ACTKEY        SAVE CONTROL ACCOUNT                         
         GOTO1 HIGH,DMCB,ACTKEY,IO                                              
         CLC   ACTKEY,IO                                                        
         BE    MECA5                                                            
         MVC   ERRMSG(29),=C'ACCPAK ACCOUNT XXXX NOT FOUND'                     
         MVC   ERRMSG+15(4),ACTKUNT                                             
         B     ERRXIT                                                           
*                                                                               
MECA5    CLI   COMREP,C'Y'         COMMISSION ONLY REP                          
         BNE   BLDG                                                             
         CLI   SZCOMREP,0          TEST HAVE SZ ACCOUNT FROM MI RECORD          
         BE    *+14                                                             
         MVC   ACTKCULA,SZCOMREP                                                
         B     MECA7                                                            
*                                                                               
         MVC   ACTKCULA,SZDFLT                                                  
         MVC   WORK(5),ACTKACT     CHANGE SZST TO SZCST                         
         MVI   ACTKACT,C'C'                                                     
         MVC   ACTKACT+1(5),WORK                                                
         MVC   SZCOMREP,ACTKEY     COMMISSION ONLY ACCOUNT                      
*                                                                               
MECA7    GOTO1 HIGH,DMCB,ACTKEY,IO                                              
         CLC   ACTKEY,IO                                                        
         BE    BLDG                                                             
         MVC   ERRMSG(30),=C'ACCPAK ACCOUNT       NOT FOUND'                    
         MVC   ERRMSG+15(5),ACTKUNT                                             
         B     ERRXIT                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD LEDGER TABLE ENTRIES FOR MEDIA CONTROL, PAYEE                 *         
* AND CASH LEDGERS                                                    *         
***********************************************************************         
                                                                                
BLDG     LA    R6,LEDGTAB                                                       
         GOTO1 GETLED,DMCB,SZACC,(R6)                                           
         LA    R6,LDGTABL(R6)      BUMP TO NEXT POSITION                        
*                                                                               
         OC    PAYEE,PAYEE         TEST FOR PAYEE ACCOUNT                       
         BZ    BLDG3               NO                                           
         GOTO1 GETLED,DMCB,PAYEE,(R6)                                           
         LA    R6,LDGTABL(R6)                                                   
*                                                                               
BLDG3    OC    CASHACC,CASHACC     TEST CASH RECEIPT ACCOUNT                    
         BZ    PPAY                                                             
         GOTO1 GETLED,DMCB,CASHACC,(R6)                                         
         EJECT                                                                  
***********************************************************************         
* BUILD CREDIT POSTING TO PAYEE ACCOUNT(REGULAR PAYMENT)              *         
*                                                                     *         
* FOR COMMISSION ONLY REPS, ACCUMULATE THE REGULAR PAYMENTS,          *         
*  BUT DO NOT POST FOR THEM                                           *         
***********************************************************************         
                                                                                
PPAY     L     R3,PAYREQ                                                        
*                                                                               
PPAY3    MVC   PAYTYPE,REQTYPE     PAYMENT TYPE                                 
         NI    PAYTYPE,X'0F'                                                    
         OC    REQOFF,SPACES                                                    
         CLI   PAYTYPE,PAYCHK      IS IT A CASH RECEIPT                         
         BE    PPAY24              SKIP OVER ITEM                               
*                                                                               
         USING TRNRECD,R2                                                       
PPAY5    LA    R2,IO                                                            
         MVC   TRNKEY,SPACES                                                    
         XC    TRNKEY+ACCORLEN(20),TRNKEY+ACCORLEN                              
         MVC   TRNKCULA,PAYEE       STATION                                     
         MVC   TRNKREF(3),REQPROD   1ST 3 OF REF IS PRODUCT CODE                
         MVC   TRNKREF+3(3),REQINV                                              
         MVC   TRNKDATE,PSTDATE     TODAY(PACKED)                               
         MVI   TRNKSBR,0                                                        
         CLI   STATYPE,STATION                                                  
         BE    PPAY7                                                            
         MVC   TRNKCACT(1),REQMED                                               
         MVC   TRNKCACT+1(5),REQSTA   REP                                       
         OC    TRNKCACT+1(5),SPACES                                             
*                                                                               
         USING TRNELD,R4                                                        
PPAY7    MVC   TRNKCACT+9(3),REQCLI                                             
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q+1   L'44 ELEMENT IF NO NARRATIVE                   
         MVI   TRNSUB,0                                                         
         MVC   TRNREF,TRNKREF                                                   
         MVC   TRNDATE,PSTDATE                                                  
         MVI   TRNSTAT,0                                                        
         MVC   TRNTYPE,STATYPE                                                  
         MVC   TRNBTCH,SPACES                                                   
         MVC   TRNBTCH(2),MOS                                                   
         MVC   TRNOFFC,REQOFF                                                   
         OC    TRNOFFC,SPACES                                                   
         MVI   TRNNARR,C' '                                                     
*                                                                               
PPAY9    TM    REQNET,X'F0'        DECIMAL FORMAT                               
         BO    PPAY11                                                           
         ZAP   TRNAMNT,REQPNET    OR PACKED                                     
         B     PPAY13                                                           
*                                                                               
PPAY11   PACK  TRNAMNT,REQNET     NET AMOUNT FOR TRANSACTIONS                   
*                                                                               
PPAY13   AP    NET,TRNAMNT                                                      
         CLI   COMREP,C'Y'         TEST COMMISSION ONLY REP                     
         BE    PPAY24              YES-GO TO NEXT REQUEST RECORD                
*                                                                               
         LA    RF,TRNLN1Q+1        L'44 ELEMENT IF NO NARRATIVE                 
         SR    R1,R1                                                            
         ICM   R1,3,REQLEN                                                      
         CLI   REQCOMM,C' '        ANYTHING < BLANK IS NEW STYLE                
         BL    *+8                                                              
         AHI   R1,L'REQEXT                                                      
         SHI   R1,(REQNARR+1-REQLEN) GET LENGTH OF NARRATIVE                    
         BM    PPAY15                                                           
*                                                                               
         LA    RE,REQNARR          ASSUME NEW STYLE (HAS REQEXT FIELD)          
         CLI   REQCOMM,C' '        ANYTHING < BLANK IS NEW STYLE                
         BL    *+8                 OTHERWISE NARRATIVE STARTS 80 BYTES          
         SHI   RE,L'REQEXT         BACK.                                        
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   TRNNARR(0),0(RE)                                                 
         AR    RF,R1                                                            
*                                                                               
PPAY15   STC   RF,TRNLN                                                         
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
*              ADD THE EXTRA PAY INFORMATION                                    
*                                                                               
         USING XPYELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   XPYEL,XPYELQ       X'46' - EXTRA PAY ELEMENT                     
         MVI   XPYLN,XPYLN3Q      EXPANDED LENGTH FOR BINARY MKT                
         MVC   XPYSEQ,REQSEQN                                                   
         MVC   XPYAGY,REQAGY                                                    
         MVC   XPYBMKT,REQBMKT     BINARY MARKET NUMBER                         
         ZAP   XPYCD,=P'0'        CASH DISCOUNT                                 
         MVC   XPYCLI,PAYCLINM                                                  
         MVC   XPYPRO,PAYPRDNM                                                  
         MVC   XPYPRON,PAYPRDNM    ALWAYS PRODUCT NAME                          
         CLI   PROFILE+4,C'Y'      USE PRODUCT NAME NOT CODE                    
         BE    PPAY17                                                           
         MVC   XPYPRO,SPACES                                                    
         MVC   XPYPRO(3),REQPROD                                                
         CLC   REQPROD2,SPACES                                                  
         BE    PPAY17                                                           
         MVI   XPYPRO+3,C'/'                                                    
         MVC   XPYPRO+4(3),REQPROD2                                             
         CLC   REQPROD3,SPACES                                                  
         BE    PPAY17                                                           
         CLC   REQPROD3,=C'REG'                                                 
         BE    PPAY17                                                           
         CLC   REQPROD3,=C'PIG'                                                 
         BE    PPAY17                                                           
         MVI   XPYPRO+7,C'/'                                                    
         MVC   XPYPRO+8(3),REQPROD3                                             
*                                                                               
PPAY17   MVC   XPYINV,SPACES                                                    
         MVC   XPYINV(11),REQINV                                                
         XC    XPYPER,XPYPER                                                    
*                                  CONVERT TO CHARACTER FORMAT DATE             
         GOTO1 CDATCON,DMCB,(0,REQSTRT),(X'20',XPYPER)                          
         CLI   REQEND+2,X'40'                                                   
         BE    PPAY19                                                           
         GOTO1 CDATCON,DMCB,(0,REQEND),(X'20',XPYPER+6)                         
*                                                                               
PPAY19   MVC   XPYTYPE,REQTYPE                                                  
         LA    R0,3                VALIDAITE THE ESTIMATE NUMBER                
         LA    R1,REQESTN          MUST BE 3 NUMERIC                            
         CLI   0(R1),C'0'                                                       
         BL    PPAY20                                                           
         LA    R1,1(,R1)                                                        
         BCT   R0,*-12                                                          
*                                                                               
         PACK  DUB,REQESTN                                                      
         CVB   R1,DUB                                                           
         STCM  R1,3,XPYEST                                                      
*                                                                               
PPAY20   CLC   REQPROD2,SPACES                                                  
         BE    PPAY20A                                                          
         MVC   XPYPRO2,REQPROD2                                                 
*                                                                               
PPAY20A  GOTO1 ADDL,DMCB,(R2),(R4)               ADD XPYELQ X'46'               
*                                                                               
         USING GDAELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   GDAEL,GDAELQ        X'E5' - MMOS PAYABLE DATE                    
         MVI   GDALN,GDALNQ        ELEMENT LENGTH                               
         MVI   GDATYPE,GDAMMOS     MEDIA MONTH OF SERVICE                       
         LA    RF,REQSTRT                                                       
         CLI   REQEND+2,X'40'                                                   
         BE    *+8                                                              
         LA    RF,REQEND                                                        
         CLI   REQMED,C'N'         IS THE MEDIA NETWORK ?                       
         BE    PPAY21              YES                                          
         GOTO1 GETBROAD,DMCB,(1,(RF)),WORK,CGETDAY,CADDAY                       
         LA    RF,WORK+6                                                        
*                                                                               
PPAY21   GOTO1 CDATCON,DMCB,(0,(RF)),(1,GDAYYMM)                                
         MVI   GDAYYMM+L'GDAYYMM,0                                              
*                                                                               
         GOTO1 ADDL,DMCB,(R2),(R4)               ADD GDAELQ X'E5'               
*                                                                               
         TM    REQPGRS+5,X'0F'     IS THIS FIELD PACKED?                        
         BZ    PPAY22              NO, SKIP THIS                                
         USING SCIELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   SCIEL,SCIELQ        X'50' ELEMENT                                
         MVI   SCILN,SCILN3Q                                                    
         MVI   SCITYPE,SCITGRSS    GROSS                                        
         ZAP   SCIAMNT,REQPGRS                                                  
         ZAP   SCINET,=P'0'                                                     
         TM    REQGIND,X'80'         TURN X'80' ON IF NEEDED                    
         BZ    *+8                                                              
         OI    SCIINDR,SCIICAL                                                  
         GOTO1 ADDL,DMCB,(R2),(R4)   ADD SCIELD X'50'                           
*                                                                               
PPAY22   OC    REQPID,REQPID                                                    
         BZ    PPAY23                                                           
         CLC   REQPID,SPACES                                                    
         BE    PPAY23                                                           
         USING PIDEL,R4                                                         
         XC    ELEMENT,ELEMENT                                                  
         MVI   PIDEL,PIDELQ                                                     
         MVI   PIDLN,PIDLNQ                                                     
         MVC   PIDNO,REQPID                                                     
         MVC   PIDAGY,REQAGY                                                    
         GOTO1 ADDL,DMCB,(R2),(R4)   ADD PIDEL X'D8'                            
*                                                                               
PPAY23   MVC   TRNBMOS,TODAY1                                                   
         SR    RE,RE                                                            
         ICM   RE,3,TRNBSEQN                                                    
         AHI   RE,1                                                             
         STCM  RE,3,TRNBSEQN                                                    
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PPAY24   SR    R1,R1                                                            
         ICM   R1,3,REQLEN                                                      
         AR    R3,R1                                                            
         OC    REQLEN,REQLEN       TEST FOR EOT                                 
         BNZ   PPAY3               NO-PROCESS NEXT ONE                          
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD TRANSACTION FOR CASH RECEIPT                                  *         
***********************************************************************         
                                                                                
PCSR     L     R3,PAYREQ           POINT TO START OF REQUEST CHAIN              
*                                                                               
PCSR3    MVC   PAYTYPE,REQTYPE                                                  
         NI    PAYTYPE,X'0F'                                                    
         OC    REQOFF,SPACES                                                    
         CLI   PAYTYPE,PAYCHK      TEST FOR CASH RECEIPT                        
         BNE   PCSR17              NO-NEXT REQUEST                              
*                                                                               
         LA    R2,IO                                                            
         USING TRNRECD,R2                                                       
         MVC   TRNKEY,SPACES                                                    
         XC    TRNKEY+ACCORLEN(20),TRNKEY+ACCORLEN                              
         MVC   TRNKCULA,CASHACC      CASH ACCOUNT                               
         MVC   TRNKREF(3),REQPROD    1ST 3 OF REF IS PRODUCT CODE               
         MVC   TRNKREF+3(3),REQINV                                              
         MVC   TRNKDATE,PSTDATE      TODAY(PACKED)                              
         MVI   TRNKSBR,0                                                        
         MVC   TRNKCUNT(1),REQMED                                               
         MVI   TRNKCLDG,C'/'                                                    
         MVC   TRNKCACT(3),REQCLI                                               
         MVI   TRNKCACT+3,C'/'                                                  
         MVC   TRNKCACT+4(3),REQPROD                                            
         CLC   REQPROD2,SPACES                                                  
         BE    PCSR5                                                            
         MVI   TRNKCACT+7,C'/'                                                  
         MVC   TRNKCACT+8(3),REQPROD2                                           
*                                                                               
PCSR5    LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         USING TRNELD,R4                                                        
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q+1                                                  
         MVI   TRNSUB,0                                                         
         MVC   TRNREF,TRNKREF                                                   
         MVC   TRNDATE,PSTDATE                                                  
         MVI   TRNSTAT,0                                                        
         MVC   TRNTYPE,STATYPE                                                  
         MVC   TRNBTCH,SPACES                                                   
         MVC   TRNBTCH(2),MOS                                                   
         MVC   TRNOFFC,REQOFF                                                   
         OC    TRNOFFC,SPACES                                                   
         MVI   TRNNARR,C' '                                                     
*                                                                               
PCSR7    TM    REQNET,X'F0'        DECIMAL FORMAT                               
         BO    PCSR9                                                            
         ZAP   TRNAMNT,REQPNET    OR PACKED                                     
         B     PCSR11                                                           
*                                                                               
PCSR9    PACK  TRNAMNT,REQNET     NET AMOUNT FOR TRANSACTIONS                   
*                                                                               
PCSR11   MP    TRNAMNT,=P'-1'                                                   
         AP    CSH,TRNAMNT        TOTAL CASH RECEIPTS                           
         SR    RF,RF                                                            
         IC    RF,TRNLN           RF=LENGTH OF NARRATIVE                        
         SR    R1,R1                                                            
         ICM   R1,3,REQLEN                                                      
         CLI   REQCOMM,C' '        ANYTHING < BLANK IS NEW STYLE                
         BL    *+8                                                              
         AH    R1,=Y(L'REQEXT)                                                  
         SH    R1,=Y(REQNARR+1-REQLEN) GET LENGTH OF NARRATIVE                  
         BM    PCSR12                                                           
*                                                                               
         LA    RE,REQNARR          ASSUME NEW STYLE (HAS REQEXT FIELD)          
         CLI   REQCOMM,C' '        ANYTHING < BLANK IS NEW STYLE                
         BL    *+8                 OTHERWISE NARRATIVE STARTS 80 BYTES          
         SH    RE,=Y(L'REQEXT)     BACK.                                        
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   TRNNARR(0),0(RE)                                                 
         AR    RF,R1                                                            
*                                                                               
PCSR12   STC   RF,TRNLN                                                         
         MVC   WORK,SPACES                                                      
         MVC   WORK(1),REQMED                                                   
         MVC   WORK+1(5),REQSTA                                                 
         CLI   STATYPE,STATION                                                  
         BE    PCSR13                                                           
         MVC   WORK+1(5),SPACES                                                 
         MVC   WORK+1(4),REQREP                                                 
*                                                                               
PCSR13   GOTO1 CDATCON,DMCB,(1,TODAY1),(8,WORK+13)                              
         GOTO1 SQUASHER,DMCB,WORK,25                                            
         L     R5,DMCB+4                                                        
         SR    RF,RF                                                            
         IC    RF,TRNLN                                                         
         LA    R1,0(RF,R4)                                                      
         MVI   0(R1),C' '          END OF NARRATIVE                             
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),WORK        STATION AND DATE TO NARRATIVE                
         LA    RF,2(R5,RF)         GET NEW LENGTH                               
         STC   RF,TRNLN                                                         
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
PCSR15   MVC   TRNBMOS,TODAY1                                                   
         SR    RE,RE                                                            
         ICM   RE,3,TRNBSEQN                                                    
         AH    RE,=H'1'                                                         
         STCM  RE,3,TRNBSEQN                                                    
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    PCSR17                                                           
         DC    H'0'                                                             
*                                                                               
PCSR17   SR    R1,R1                                                            
         ICM   R1,3,REQLEN                                                      
         AR    R3,R1               R3 TO NEXT REQUEST                           
         OC    REQLEN,REQLEN                                                    
         BNZ   PCSR3                                                            
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* DEBIT POSTING TO SZ CONTROL ACCOUNT                                 *         
***********************************************************************         
                                                                                
PSZC     L     R3,PAYREQ           RESTORE POINTER TO FIRST REQUEST             
         ZAP   DUB,=P'0'           GET POSTING AMOUNT                           
         CLI   COMREP,C'Y'         FOR COMMISSION ONLY REP                      
         BE    *+10                DON'T ADD REGULAR POSTINGS                   
         AP    DUB,NET                                                          
         AP    DUB,CSH                                                          
*                                                                               
         LA    R2,KEY                                                           
         USING TRNRECD,R2                                                       
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCULA,SZACC       SZACC ACCOUNT                               
         MVC   TRNKCCPY(1),TRNKCPY  COMPANY CODE                                
         MVC   TRNKCUNT(2),=C'SJ'                                               
         MVC   TRNKCACT(3),REQCLI   ONE RECORD PER CLIENY                       
         MVC   TRNKDATE,TODAY1      PER DAY                                     
         MVI   TRNKSBR,0                                                        
         OI    DMINBTS,X'80'                                                    
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         MVI   DMINBTS,0                                                        
         CLC   KEY,IO                                                           
         BNE   PSZC3                 NOT ON FILE                                
*                                                                               
         LA    R2,IO                                                            
         LA    R4,ACCORFST(R2)                                                  
         USING TRNELD,R4                                                        
         AP    TRNAMNT,DUB                                                      
         CP    DUB,=P'0'                                                        
         BE    PSZC5                                                            
         GOTO1 WRITE,DMCB,(R2),(R2)                                             
         B     PSZC5                                                            
         DROP  R2                                                               
*                                                                               
PSZC3    LA    R2,IO                 ADD SZ POSTING                             
         USING TRNRECD,R2                                                       
         MVC   TRNKEY,KEY                                                       
         XC    TRNKEY+ACCORLEN(20),TRNKEY+ACCORLEN                              
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         USING TRNELD,R4                                                        
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q+1                                                  
         MVC   TRNDATE,TODAY1                                                   
         MVC   TRNREF,SPACES                                                    
         MVI   TRNREF,C'1'                                                      
         XC    TRNSUB(2),TRNSUB                                                 
         MVI   TRNSTAT,X'80'                                                    
         MVC   TRNBTCH,SPACES                                                   
         MVC   TRNBTCH(2),MOS                                                   
         MVC   TRNOFFC,REQOFF                                                   
         OC    TRNOFFC,SPACES                                                   
         MVI   TRNNARR,C' '                                                     
         ZAP   TRNAMNT,DUB                                                      
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
         USING TRSELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   TRSEL,X'60'        ADD A SHELL TRANS STATUS ELEM                 
         MVI   TRSLN,TRSLNQ                                                     
         MVC   TRSDATE,TODAY2     DATE ADDED TO FILE                            
         GOTO1 ADDL,DMCB,(R2),(R4)                                              
*                                                                               
PSZC5    MVI   TRNKREF,C'1'        ADDTRN REQUIRES A REFERENCE                  
         LA    R4,ACCORFST(R2)      RE-ADDRESS TRANS ELEMENT                    
         USING TRNELD,R4                                                        
         ZAP   TRNAMNT,DUB        USE CURRENT TRANSACTION AMOUNT                
         CP    DUB,=P'0'                                                        
         BE    COMO                                                             
*                                                                               
         MVI   TRNINDS1,0          CLEAR INDICATORS                             
         MVC   TRNBMOS,TODAY1      TODAY Y/M (PWO)                              
         GOTO1 ADDTRN,TRNBLK       ADD AND UPDATE BUCKETS & ACCOUNT             
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   TRNINDS1,0          CLEAR INDICATORS                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* SZ POSTINGS FOR COMMISSION ONLY REP                                 *         
***********************************************************************         
                                                                                
COMO     CLI   COMREP,C'Y'                                                      
         BNE   COMO3                                                            
         CP    NET,=P'0'                                                        
         BE    COMO3                                                            
*                                                                               
         L     R2,TRNREC                                                        
         USING TRNRECD,R2                                                       
         MVC   TRNKEY,SPACES                                                    
         XC    TRNKEY+ACCORLEN(20),TRNKEY+ACCORLEN                              
         MVC   TRNKCULA,SZCOMREP     SZACC ACCOUNT                              
         MVC   TRNKCCPY(1),TRNKCPY   COMPANY CODE                               
         MVC   TRNKCUNT(2),=C'SJ'                                               
         MVC   TRNKCACT(3),REQCLI                                               
         MVI   TRNKREF,C'1'                                                     
         MVC   TRNKDATE,TODAY1                                                  
         MVI   TRNKSBR,0                                                        
*                                                                               
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         USING TRNELD,R4                                                        
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q+1                                                  
         MVC   TRNDATE,TODAY1                                                   
         MVC   TRNREF,TRNKREF                                                   
         XC    TRNSUB(2),TRNSUB                                                 
         MVI   TRNSTAT,X'80'                                                    
         MVC   TRNBTCH,SPACES                                                   
         MVC   TRNBTCH(2),MOS                                                   
         MVC   TRNOFFC,REQOFF                                                   
         OC    TRNOFFC,SPACES                                                   
         MVI   TRNNARR,C' '                                                     
         ZAP   TRNAMNT,NET                                                      
         GOTO1 ADDL,DMCB,(R2),(R4) ADD CREDIT AND DEBIT                         
*                                                                               
         MVC   TRNBMOS,TODAY1      TODAY Y/M (PWO)                              
         SR    RE,RE                                                            
         ICM   RE,3,TRNBSEQN       UPDATE BATCH SEQUENCE NUMBER                 
         AH    RE,=H'1'                                                         
         STCM  RE,3,TRNBSEQN                                                    
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,ACCORFST(R2)                                                  
         CLI   TRNEL,TRNELQ      MAKE SURE I'M POINTING AT TRANS                
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   TRNSTAT,0          NOW ADD DEBIT                                 
         SR    RE,RE                                                            
         ICM   RE,3,TRNBSEQN       UPDATE BATCH SEQUENCE NUMBER                 
         AH    RE,=H'1'                                                         
         STCM  RE,3,TRNBSEQN                                                    
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
COMO3    OI    TRNINDS,TRNILAST    FORCE LAST TIME CALL TO ADDTRN               
         GOTO1 ADDTRN,TRNBLK                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         B     XIT                                                              
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD ADDRESS ELEMENT                                               *         
***********************************************************************         
*                                                                               
         USING ADRELD,R4                                                        
BLDADDR  NTR1  ,                                                                
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   ADREL,ADRELQ      ELEMENT CODE                                   
         MVI   ADRLN,ADRADD1-ADRELD  MINIMUM LENGTH                             
         MVI   ADRNUM,0          NUMBER OF LINES                                
         LA    R5,ADRADD1                                                       
         LA    R1,PAYSTADD         GET NUMBER OF VALID ADDRESS LINES            
         SR    R6,R6                                                            
*                                                                               
BLDADDR3 MVC   0(26,R5),0(R1)                                                   
         OC    0(26,R5),SPACES                                                  
         CLC   0(26,R5),SPACES                                                  
         BE    BLDADDR5                                                         
         IC    R6,ADRLN          ADJUST ELEMENT LENGTH                          
         LA    R6,26(R6)                                                        
         STC   R6,ADRLN                                                         
         IC    R6,ADRNUM         AND NUMBER OF LINES                            
         LA    R6,1(R6)                                                         
         STC   R6,ADRNUM                                                        
         CLI   ADRNUM,4          MAX IS 4 LINES                                 
         BE    XIT                                                              
         LA    R1,26(R1)                                                        
         LA    R5,L'ADRADD1(R5)                                                 
         B     BLDADDR3                                                         
*                                                                               
BLDADDR5 CLI   ADRNUM,0                                                         
         BNE   XIT                                                              
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK ACCOUNT STATUS ELEMENT ON WHAT ELEMENTS TO ADD                *         
*       INITIALIZE ACTFLAG TO X'00' AS DEFAULT SO ALL ELMS ARE ADDED  *         
*       READ ACCOUNTS RSTELD (30) ELEMENT AND TURN ON BITS ACCORDINGLY*         
*       OF WHICH ELMS TO NOT ADD                                      *         
*       R2 - A(ACCOUNT RECORD)                                        *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
CHKACT   NTR1                                                                   
         MVC   SVCULA,ACTKEY                                                    
         MVC   KEY,SPACES                                                       
         MVC   KEY(3),SVCULA       READ LEDGER                                  
         GOTO1 READ,DMCB,KEY,IO                                                 
         BNE   CHKAX                                                            
         DROP  R2                                                               
*                                                                               
         USING LDGRECD,R2                                                       
         LA    R2,IO                                                            
         LA    R3,ACCORFST(R2)     POINT TO FIRST ELEMENT                       
CHKA10   CLI   0(R3),0             END OF REC?                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R3),ACLELQ        X'16' - ACCOUNTS LENGTHS ELEM                
         BE    CHKA20                                                           
         SR    R1,R1                                                            
         IC    R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     CHKA10                                                           
*                                                                               
         USING ACLELD,R3                                                        
CHKA20   LA    R0,4                ASSUME 4 LEVELS                              
         SR    R1,R1                                                            
         LA    RE,LEVELS                                                        
         LA    RF,ACLVALS                                                       
CHKA30   ICM   R1,1,0(RF)                                                       
         BZ    CHKA40                                                           
         STC   R1,0(RE)                                                         
         LA    RF,L'ACLVALS(RF)                                                 
         LA    RE,1(RE)                                                         
         BCT   R0,CHKA30                                                        
         DROP  R3                                                               
*                                                                               
CHKA40   SR    R1,R1                                                            
         LA    R0,4                ASSUME 4 LEVELS                              
         LA    RE,LEVELS                                                        
         ST    RE,FULL                                                          
CHKA50   L     RE,FULL                                                          
         ICM   R1,1,0(RE)                                                       
         BZ    CHKAX                                                            
         AHI   R1,2                ADD C/U/L AND THEN -1 FOR EXMVC              
         MVC   KEY,SPACES                                                       
         MVC   KEY(0),SVCULA       READ LEDGER                                  
         EX    R1,*-6                                                           
         GOTO1 READ,DMCB,KEY,IO                                                 
         BNE   CHKAX                                                            
*                                                                               
         USING ACTRECD,R2                                                       
         LA    R2,IO                                                            
         LA    R3,ACCORFST(R2)     POINT TO FIRST ELEMENT                       
CHKA60   CLI   0(R3),0             END OF REC?                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R3),RSTELQ        X'30' - RECORD STATUS ELEM                   
         BE    CHKA70                                                           
         SR    R1,R1                                                            
         IC    R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     CHKA60                                                           
*                                                                               
         USING RSTELD,R3                                                        
CHKA70   CLI   RSTLN,RSTLN3Q                                                    
         BL    CHKA80                                                           
         TM    RSTSTAT6,RSTLNAM+RSTLADDR     SOMETHING TO PREVENT?              
         BZ    CHKA80                                                           
         MVI   ACTFLAG,0                                                        
         TM    RSTSTAT6,RSTLNAM    SHOULD WE ADD THE NAME ELM                   
         BNO   *+8                                                              
         OI    ACTFLAG,ACT20                                                    
         TM    RSTSTAT6,RSTLADDR   SHOULD WE ADD THE ADDR ELM                   
         BNO   *+8                                                              
         OI    ACTFLAG,ACT22                                                    
         DROP  R3                                                               
*                                                                               
CHKA80   L     RE,FULL                                                          
         LA    RE,1(RE)                                                         
         ST    RE,FULL                                                          
         BCT   R0,CHKA50                                                        
*                                                                               
CHKAX    B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* FIX OLD ADDRESS                                                     *         
***********************************************************************         
                                                                                
FIXADDR  NTR1  ,                                                                
         LA    R4,ELEMENT                                                       
         BAS   RE,BLDADDR          BUILD NEW ADDRESS                            
         SR    R0,R0                                                            
         LA    R1,ACCORFST(R2)                                                  
*                                                                               
FIXADDR3 CLI   0(R1),X'22'         FIND OLD ADDRESS                             
         BE    FIXADDR5                                                         
         CLI   0(R1),0                                                          
         BE    FIXADDR9                                                         
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     FIXADDR3                                                         
*                                                                               
         USING ADRELD,R4                                                        
FIXADDR5 SR    R3,R3               TEST ADDRESS CHANGED                         
         IC    R3,ADRLN                                                         
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         CLC   ADRELD,0(R1)                                                     
         BE    XIT                                                              
         MVI   0(R1),X'FF'         DELETE OLD                                   
         GOTO1 DELL,DMCB,(X'FF',(R2)),0                                         
*                                                                               
FIXADDR9 GOTO1 ADDL,DMCB,(R2),(R4)  ADD NEW                                     
         OI    RSTAT,RRPL                                                       
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* FIX OLD NAME                                                        *         
***********************************************************************         
                                                                                
FIXNAME  NTR1  ,                                                                
         LA    R4,ELEMENT          BUILD NEW ELEM                               
         USING NAMELD,R4                                                        
         XC    ELEMENT,ELEMENT                                                  
         MVI   NAMEL,NAMELQ      X'20'                                          
         LA    R1,L'PAYNME+2                                                    
         STC   R1,NAMLN          LENGTH                                         
         MVC   NAMEREC(L'PAYNME),PAYNME                                         
         SR    R0,R0                                                            
         LA    R1,ACCORFST(R2)                                                  
*                                                                               
FIXNAME3 CLI   0(R1),NAMELQ       FIND OLD X'20' NAME                           
         BE    FIXNAME5                                                         
         CLI   0(R1),0                                                          
         BE    FIXNAME9                                                         
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     FIXNAME3                                                         
*                                                                               
FIXNAME5 SR    R3,R3               TEST NAME CHANGED                            
         IC    R3,NAMLN                                                         
         SH    R3,=H'1'                                                         
         BM    XIT                                                              
         EX    R3,*+8                                                           
         B     *+10                                                             
         CLC   NAMEL(0),0(R1)                                                   
         BE    XIT                                                              
         MVI   0(R1),X'FF'         DELETE OLD                                   
         GOTO1 DELL,DMCB,(X'FF',(R2)),0                                         
*                                                                               
FIXNAME9 GOTO1 ADDL,DMCB,(R2),(R4) ADD NEW                                      
         OI    RSTAT,RRPL                                                       
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO GET A LEDGER RECORD AND BUILD A LEDGER TABLE ENTRY   *         
*  AT ENTRY, P1=A(KEY)                                                *         
*            P2=A(LEDGER TABLE ENTRY)                                 *         
***********************************************************************         
                                                                                
GETLED   NTR1  ,                                                                
         LM    R5,R6,0(R1)                                                      
         MVC   KEY,SPACES                                                       
         LA    R2,KEY                                                           
         USING LDGRECD,R2                                                       
         MVC   LDGKEY(3),0(R5)      GET COMPANY/UNIT/LEDGER                     
         GOTO1 HIGH,DMCB,KEY,IO                                                 
         LA    R2,IO                                                            
         CLC   LDGKEY,IO                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING LDGTABD,R6                                                       
GETLED2  XC    LDGTABD(LDGTABL),LDGTABD                                         
         MVC   LDGTUL,LDGKUNT                                                   
         LA    R4,ACCORFST(R2)                                                  
         SR    R0,R0                                                            
*                                                                               
GETLED4  CLI   0(R4),0             TEST FOR EOR                                 
         BE    XIT                                                              
         CLI   0(R4),LDGELQ       TEST FOR LEDGER ELEMENT                       
         BNE   GETLED5             NO                                           
         USING LDGELD,R4                                                        
         MVC   LDGTTYPE,LDGTYPE                                                 
         MVC   LDGTLIKE,LDGLIKE                                                 
         MVC   LDGTOFFP,LDGOPOS                                                 
         B     GETLED8                                                          
*                                                                               
GETLED5  CLI   0(R4),ACLELQ                                                     
         BNE   GETLED6                                                          
         USING ACLELD,R4                                                        
         MVC   LDGTLVA,ACLVALS                                                  
         MVC   LDGTLVB,ACLVALS+(L'ACLVALS)                                      
         MVC   LDGTLVC,ACLVALS+(L'ACLVALS*2)                                    
         MVC   LDGTLVD,ACLVALS+(L'ACLVALS*3)                                    
         B     GETLED8                                                          
*                                                                               
GETLED6  CLI   0(R4),RSTELQ                                                     
         BNE   GETLED8                                                          
         USING RSTELD,R4                                                        
         MVC   LDGTSEC,RSTSECY+1                                                
*                                                                               
GETLED8  IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     GETLED4                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT TO A RECORD                               *         
*  P1 A(RECORD)                                                       *         
*  P2 A(ELEMENT)                                                      *         
***********************************************************************         
                                                                                
ADDL     NTR1  ,                                                                
         LM    R2,R3,0(R1)                                                      
         MVC   HALF,ACTKEY+ACCORLEN-ACTRECD(R2)                                 
         SR    R4,R4                                                            
         IC    R4,1(R3)                                                         
         AH    R4,HALF                                                          
         CH    R4,=H'1000'                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 CHELLO,ELIST,(C'P',=C'ACCOUNT '),(R2),(R3)                       
         CLI   ELERR,0                                                          
         BE    XIT                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT FROM A RECORD                          *         
*  P1   BYTE 0    ELEMENT CODE                                        *         
*       BYTE 1-3  A(RECORD)                                           *         
*  P2   BYTE 0    LENGTH OF SEARCH ARGUMENT                           *         
*       BYTE 1-3  A(SEARCH ARGUMENT)                                  *         
***********************************************************************         
                                                                                
DELL     NTR1  ,                                                                
         LM    R2,R3,0(R1)                                                      
         SR    R4,R4                                                            
         IC    R4,0(R1)                                                         
         SR    R5,R5                                                            
         IC    R5,4(R1)                                                         
         GOTO1 CHELLO,ELIST,(C'G',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         CLI   ELERR,0                                                          
         BNE   XIT                                                              
         GOTO1 CHELLO,ELIST,(C'D',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET AN ELEMENT FROM A RECORD                             *         
*  P1   BYTE 0    ELEMENT CODE                                        *         
*       BYTE 1-3  A(RECORD)                                           *         
*  P2   BYTE 0    LENGTH OF SEARCH ARGUMENT                           *         
*       BYTE 1-3  A(SEARCH ARGUMENT)                                  *         
***********************************************************************         
                                                                                
GETL     NTR1  ,                                                                
         LM    R2,R3,0(R1)                                                      
         SR    R4,R4                                                            
         IC    R4,0(R1)                                                         
         SR    R5,R5                                                            
         IC    R5,4(R1)                                                         
         GOTO1 CHELLO,ELIST,(C'G',=C'ACCOUNT '),((R4),(R2)),((R5),(R3))         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* DATAMGR ROUTINES                                                    *         
*  PARAM 1    A(KEY)                                                  *         
*  PARAM 2    A(IOAREA)                                               *         
***********************************************************************         
                                                                                
HIGH     MVC   COMMAND,=C'DMRDHI'                                               
         B     DMRALL                                                           
*                                                                               
SEQ      MVC   COMMAND,=C'DMRSEQ'                                               
         B     DMRALL                                                           
*                                                                               
READ     MVC   COMMAND,=C'DMREAD'                                               
         B     DMRALL                                                           
*                                                                               
WRITE    MVC   COMMAND,=C'DMWRT '                                               
         B     DMRALL                                                           
*                                                                               
ADD      MVC   COMMAND,=C'DMADD '                                               
         B     DMRALL                                                           
*                                                                               
DMRALL   NTR1  ,                                                                
         LM    R2,R3,0(R1)                                                      
         GOTO1 CDATAMGR,DMWK,(DMINBTS,COMMAND),=C'ACCOUNT',(R2),(R3)            
         MVC   DMBYTE,DMCB+8                                                    
         NC    DMBYTE,DMOUTBTS                                                  
         BZ    XIT                                                              
         DC    H'0'                                                             
*                                                                               
ERRXIT   LA    R1,ERRMSG                                                        
         ST    R1,PAYERROR                                                      
XIT      XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR STORAGE                                                   *         
***********************************************************************         
                                                                                
LWSD     DSECT                                                                  
RELO     DS    F                                                                
ADPAY    DS    F                   A(PAY CONTROL BLOCK)                         
ADDTRN   DS    A                   A(ADDTRN)                                    
SQUASHER DS    A                   A(SQUASHER)                                  
GETBROAD DS    A                   A(GETBROAD)                                  
*                                                                               
AACCOUNT DS    A                   A(ACCOUNT RECORD BUFFER)                     
ACONTRA  DS    A                   A(CONTRA-ACCOUNT BUFFER)                     
ABUCKET  DS    A                   A(BUCKET ELEMENT BUFFER)                     
*                                                                               
SVMDI    DS    XL(MDILNQ)                                                       
*                                                                               
NET      DS    PL6                 NET PAYMENT                                  
CSH      DS    PL6                 CASH RECEIPT                                 
*                                                                               
TODAY0   DS    CL6                 TODAY'S DATE(CHARACTER)                      
TODAY1   DS    CL3                 TODAY'S DATE(PACKED)                         
TODAY2   DS    CL2                 TODAY'S DATE(COMPRESSED)                     
MOS      DS    CL2                 MONTH OF SERVICE                             
*                                                                               
RSTAT    DS    XL1                 RECORD STATUS                                
RDEL     EQU   X'80'               OLD RECORD WAS DELETED                       
RRPL     EQU   X'40'               REPLACE ADDRESS                              
*                                                                               
DMCB     DS    6F                                                               
DMWK     DS    6F                                                               
COMMAND  DS    CL8                                                              
DMINBTS  DS    CL1                                                              
DMOUTBTS DS    CL1                                                              
DMBYTE   DS    CL1                                                              
WORK     DS    CL64                                                             
ERRMSG   DS    CL60                                                             
SPACES   DS    CL132                                                            
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    CL1                                                              
*                                                                               
ELIST    DS    3F                  FOR GETL, ADDEL, DELEL                       
ELERR    DS    CL1                                                              
         ORG   ELERR               ERROR CODE FROM HELLO                        
ELADDR   DS    F                   ADDRESS OF ELEMENT FROM HELLO                
         DS    2F                                                               
ELEMENT  DS    CL255                                                            
*                                                                               
PAYLED   DS    CL1                 PAYEE LEDGER(SYSTEM IN MEDIA CODE)           
PAYEE    DS    CL15                PAYEE ACCOUNT(STA/REP)                       
CASHACC  DS    CL15                CASH ACCOUNT(SCCXXX)                         
SZACC    DS    CL15                SZ CONTROL ACCOUNT                           
SZCOMREP DS    CL15                SZ CONTROL FOR COMMISSION ONLY REP           
SZDFLT   DS    CL15                SZ DEFAULT ACCOUNT                           
PSTDATE  DS    CL3                 PACKED POSTING DATE                          
COMREP   DS    CL1                 Y=COMMISSION ONLY REP                        
STATYPE  DS    XL1                 PAYEE TYPE                                   
STATION  EQU   X'21'               STATION                                      
REP      EQU   X'22'               REP                                          
UNWNET   EQU   X'23'               UNWIRED NETWORK                              
PAYTYPE  DS    XL1                 PAYMENT TYPE                                 
PAYREG   EQU   1                   REGULAR PAYMENT                              
PAYCRD   EQU   2                   CREDIT MEMO(CR)                              
PAYCHK   EQU   3                   CASH RECEIPT(CK)                             
PROFILE  DS    CL16                A054 PROFILES                                
*                                                                               
LEVELS   DS    0XL1                                                             
LEVA     DS    XL1                 LEVEL A LENGTH                               
LEVB     DS    XL1                 LEVEL B LENGTH (A+B)                         
LEVC     DS    XL1                 LEVEL C LENGTH (A+B+C)                       
LEVD     DS    XL1                 LEVEL D LENGTH (A+B+C+D)                     
LEVLNQ   EQU   *-LEVELS                                                         
*                                                                               
SVCULA   DS    CL(L'ACTKCULA)      COMPANY/UNIT/LEDGER/ACCOUNT                  
*                                                                               
ACTFLAG  DS    XL1                 ACCOUNT FLAG                                 
ACT20    EQU   X'80'               DO NOT ADD NAMELD                            
ACT22    EQU   X'40'               DO NOT ADD ADRELD                            
*                                                                               
LEDGTAB  DS    3XL(LDGTABL)        LEDGER TABLE                                 
         DS    XL1                                                              
*                                                                               
PALAREA  DS    XL20                P&L BUCKET AREA                              
       ++INCLUDE ACADDTRND                                                      
*                                                                               
KEY      DS    CL(L'ACCKEY)                                                     
KEYSAVE  DS    CL(L'ACCKEY)                                                     
IO       DS    CL2048                                                           
IO2      DS    CL2048                                                           
IO3      DS    CL2048                                                           
IO4      DS    CL2048                                                           
*                                                                               
LWSEND   EQU   *                                                                
         EJECT                                                                  
PAYD     DSECT                                                                  
       ++INCLUDE ACPAYSPTD                                                      
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER PAYMENT REQUEST                                      *         
***********************************************************************         
                                                                                
REQD     DSECT                                                                  
REQLEN   DS    XL2                 LENGTH OF REQUEST                            
REQPROG  DS    CL2                 C'30' FOR CHECK ELEMENTS                     
REQAGY   DS    CL2                                                              
REQMED   DS    CL1                                                              
REQCLI   DS    CL3                                                              
REQESTN  DS    CL3                                                              
REQPROD  DS    CL3                                                              
REQSTAT  DS    CL1                                                              
         DS    XL1                                                              
REQBMKT  DS    XL2                 BINARY MARKET NUMBER FOR MGRPID              
REQSTA   DS    CL5                                                              
REQTODAY DS    CL6                                                              
REQREP   DS    CL3                                                              
REQREPT  DS    CL1                                                              
REQOAGY  DS    CL2                     OVERRIDE AGENCY                          
REQCOMM  DS    CL1                                                              
REQSEQN  DS    CL1                 CLEARANCE SEQUENCE NUMBER                    
REQSTRT  DS    CL6                                                              
REQEND   DS    CL6                                                              
REQPROD2 DS    CL3                                                              
REQPROD3 DS    CL3                                                              
REQTYPE  DS    CL1                                                              
REQNET   DS    CL10                                                             
         ORG   REQNET                                                           
REQPNET  DS    PL5                                                              
REQGSTYP DS    CL1                                                              
REQGST   DS    PL4                                                              
REQOFF   DS    CL2                                                              
REQINV   DS    CL12                                                             
REQEXT   DS    0CL80                                                            
         DS    CL50                                                             
REQPGRS  DS    PL6                 GROSS AMOUNT                                 
REQPID   DS    XL2                 PID NUMBER                                   
REQNET2  DS    PL6                                                              
REQGIND  DS    XL1                 X'80' GROSS IS A CALCULATION                 
         DS    CL15                SPARE                                        
REQNARR  DS    CL200                                                            
REQLNQ   EQU   *-REQD                                                           
         EJECT                                                                  
       ++INCLUDE ACLDGTABD                                                      
         EJECT                                                                  
*ACGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*DDCOREQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'030ACPAYSPT  09/23/14'                                      
         END                                                                    
