*          DATA SET ACOFFAL    AT LEVEL 025 AS OF 06/26/19                      
*CATALP ACOFFAL                                                                 
OFFAL    TITLE '- OFFICE ACCESS LIMIT TESTING (OLD AND NEW)'                    
***********************************************************************         
*GHOA 024 27FEB19  <SPEC-27727> READ MORE THAN 1 OFFICE LIST ELEMENTS           
*GHOA 025 26JUN19  <SPEC-27727> GIVE SEC ERROR WHEN OFFICE NOT IN LIST          
***********************************************************************         
OFFAL    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**ACOF**,RA,CLEAR=YES                                
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         LR    R9,R1                                                            
         USING OFFALD,R9           R9=A(CONTROL BLOCK)                          
         MVC   DATADISP,=Y(ACRECORD-ACKEYD)                                     
         TM    OFFACTRL,OFFACCNV   TEST NEW RECORDS PASSED                      
         BZ    *+10                                                             
         MVC   DATADISP,=Y(ACCRFST-ACCRECD)                                     
         LA    R8,OFFAWORK                                                      
         USING TSARD,R8            R8=A(BUFFER CONTROL BLOCK)                   
         MVI   OFFAERR,0                                                        
         MVI   OFFASTAT,0                                                       
         CLI   OFFAOLDA,C' '       TEST USER HAS OLD LIMIT ACCESS               
         BH    *+10                                                             
         XC    OFFAOLDA,OFFAOLDA   NO - SET OLD ACCESS TO BINARY ZEROES         
         CLI   OFFANEWA,C' '       TEST USER HAS NEW LIMIT ACCESS               
         BH    *+10                                                             
         XC    OFFANEWA,OFFANEWA   NO - SET NEW ACCESS TO BINARY ZEROES         
*                                                                               
         ICM   RF,15,DATAMGR       DATAMGR RESOLVED IF OFFLINE                  
         BZ    OFF4                                                             
         OI    OFFAINDS,OFFAIOFL   SET OFFLINE                                  
         STCM  RF,15,VDATAMGR                                                   
         B     OFF6                                                             
*                                                                               
OFF4     TM    OFFAINDS,OFFAIOFS   TEST IF CALLER SAID OFFLINE/SPOOF            
         BO    *+8                 YES                                          
         OI    OFFAINDS,OFFAIONL   NO-SET ONLINE                                
         L     RF,OFFACOMF         EXTRACT COMFACS VALUES                       
         USING COMFACSD,RF         RF=A(COMFACS)                                
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VCALLOV,CCALLOV                                                  
         DROP  RF                                                               
*                                                                               
OFF6     OI    OFFACTRL,OFFACNEW   SET NEW FILES IN USE                         
*                                                                               
*FF6     TM    OFFACTRL,OFFACOLD+OFFACNEW                                       
*        BNZ   OFF8                                                             
*        GOTO1 VDATAMGR,PARM,DMDTFA,ACCFIL                                      
*        L     R1,12(R1)                                                        
*        TM    DTFTYPE-DTFPHD(R1),DTFTEMU                                       
*        BZ    *+12                                                             
*        OI    OFFACTRL,OFFACNEW   SET NEW FILES IN USE                         
*        B     *+8                                                              
*        OI    OFFACTRL,OFFACOLD   SET OLD FILES IN USE                         
*                                                                               
*FF8     TM    OFFACTRL,OFFACOLD   TEST OLD FILES IN USE                        
*        BZ    OFF10                                                            
OFF8     LA    RE,OFFOLD-4                                                      
         TM    OFFACST4,CPYSOFF2   TEST NEW OFFICE SYSTEM IN USE                
         BZ    OFF16                                                            
         LA    RE,OFFINT-4                                                      
         B     OFF16                                                            
*                                                                               
OFF10    LA    RE,OFFOLD-4                                                      
         TM    OFFACST4,CPYSOFF2   TEST NEW OFFICE SYSTEM IN USE                
         BZ    OFF16                                                            
         TM    OFFAINDS,OFFAIONL   TEST ONLINE                                  
         BZ    OFF14                                                            
         OC    ABUF,OFFABUF        TEST A(BUFFER PASSED)                        
         BNZ   OFF12                                                            
         L     RE,4(RD)            RE=OLD BWRD LINK                             
         ST    RD,ABUF             RD=A(BUFFER)                                 
         XC    0(256,RD),0(RD)     CLEAR START OF BUFFER                        
         AH    RD,=Y(6*1024)       ACQUIRE 6K OF W/S FOR BUFFER                 
         ST    RD,8(RE)            SET NEW FRWD LINK                            
         ST    RE,4(RD)            SET NEW BWRD LINK                            
*                                                                               
OFF12    OC    OFFATSAR,OFFATSAR   SET A(TSAR) IF NOT RESOLVED                  
         BNZ   OFF14                                                            
         GOTO1 VCALLOV,PARM,0,X'D9000A5D'                                       
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                DIE IF CAN'T GET TSAR                        
         MVC   OFFATSAR,0(R1)      SET A(TSAR)                                  
OFF14    LA    RE,OFFNEW-4                                                      
*                                                                               
OFF16    SR    RF,RF                                                            
         ICM   RF,1,OFFAACT        RF=ACTION NUMBER                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     0(RE,RF)            GO TO ACTION ROUTINE                         
*                                                                               
OFFOLD   B     OLDINI              OLD OFFICE SYSTEM                            
         B     OLDRES                                                           
         B     OLDTST                                                           
         B     OLDPST                                                           
         B     OFFSET              GET NOT SUPPORTED FOR OLD OFFICES            
         B     OFFSET              REQ NOT SUPPORTED FOR OLD OFFICES            
         B     OLDVAL                                                           
*                                                                               
OFFINT   B     INTINI              INTERMEDIATE OFFICE SYSTEM                   
         B     INTRES                                                           
         B     INTTST                                                           
         B     INTPST                                                           
         B     OFFSET              GET NOT SUPPORTED FOR OLD OFFICES            
         B     INTREQ                                                           
         B     INTVAL                                                           
*                                                                               
OFFNEW   B     OFFINI                                                           
         B     OFFRES                                                           
         B     OFFTST                                                           
         B     OFFPST                                                           
         B     OFFGET                                                           
         B     OFFSET              REQ NOT SUPPORTED FOR NEW OFFICES            
         DC    AL4(0)              VAL NOT SUPPORTED FOR NEW YET!!!!            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE OFFICE BUFFER                                 *         
*                                                                     *         
* IF USER HAS LIMIT ACCESS SET - THE OFFICE (OR OFFICE LIST) RECORD   *         
* THAT S/HE IS LIMITED TO IS READ AND A LIST OF LIMIT ACCESS OFFICES  *         
* IS BUILT (LIMLIST=LIMIT ACCESS OFFICE LIST, LIMLISTN=N'OFFICES).    *         
* ALL OFFICE (AND OFFICE LIST) RECORDS ARE THEN READ. FOR OFFICES     *         
* THAT THE USER HAS ACCESS TO TSAR RECORDS ARE BUILT AND ADDED. FOR   *         
* OFFICE LISTS ONLY OFFICES THAT THE USER HAS ACCESS TO ARE ADDED TO  *         
* THE TSAR RECORD OFFICE LIST (OFFBCODE), IF FOR ANY OFFICE LIST THE  *         
* USER HAS NO ACCESS TO ANY OFFICE NO RECORD IS PUT TO TSAR. THE TSAR *         
* BUFFER THEREFORE CONTAINS A SUBSET OF THE LISTS THAT THE USER HAS   *         
* ACCESS TO OR ALL OFFICES (AND LISTS) IF S/HE HAS NO LIMIT ACCESS.   *         
***********************************************************************         
         SPACE 1                                                                
OFFINI   XC    OFFASAV(OFFASAVL),OFFASAV                                        
         XC    TSARD(TSPTAB-TSARD),TSARD                                        
         MVC   TSABUF,ABUF         INITIALISE TSARD                             
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         MVC   TSACOM,OFFACOMF                                                  
         MVI   TSKEYL,L'OFFBCODE                                                
         MVI   TSRECI,TSRVAR       SET VARIABLE RECORD & MAX LENGTH             
         MVC   TSRECL,=Y(OFFBMAXL)                                              
         MVI   TSINDS,TSIALLOC     SET TO ALLOCATE FROM TEMPEST                 
         MVI   TSNBUF,1            SET ONE CORE BUFFER                          
         MVI   TSPAGN,TSPAGNCI+1   SET ONE TEMPEST C/I                          
         MVI   TSACTN,TSAINI       SET TO INITIALISE TSAR                       
         BAS   RE,BUFFER                                                        
         BNE   ERREOF                                                           
         MVI   TSACTN,TSAADD       SET TSAR ACTION TO ADD                       
         MVC   OFFALOWP,TSPAGL     SAVE LOW PAGE ALLOCATED                      
         MVC   OFFANUMP,TSPAGN     SAVE NUMBER OF PAGES ALLOCATED               
*                                                                               
         MVI   REQLISTN,0          SET NO OFFICE FILTER REQUESTED               
         CLI   OFFAREQO,C' '       TEST OFFICE FILTER PRESENT                   
         BNH   OFFINI12                                                         
         LA    R2,IO                                                            
         USING OFFRECD,R2          BUILD KEY OF OFFICE RECORD                   
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         MVC   OFFKOFF,OFFAREQO    READ USER REQUEST OFFICE RECORD              
         GOTO1 VDATAMGR,PARM,DMREAD,ACCFIL,OFFRECD,OFFRECD                      
         BNE   ERROFF                                                           
         MVI   REQLISTN,1          SET SINGLE OFFICE REQUEST                    
         MVC   REQLIST,OFFAREQO                                                 
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BZ    OFFINI12                                                         
         MVI   REQLISTN,0                                                       
*                                                                               
         LA    R1,OFFRECD+(ACRECORD-ACKEYD)                                     
         USING OFLELD,R1           R1=A(FIRST ELEMENT)                          
         SR    RF,RF                                                            
OFFINI04 CLI   OFLEL,0             TEST END-OF-RECORD                           
         BE    OFFINI12                                                         
         CLI   OFLEL,OFLELQ        TEST OFFICE LIST ELEMENT                     
         BNE   OFFINI08                                                         
         SR    RE,RE                                                            
         IC    RE,REQLISTN                                                      
         SLL   RE,1                                                             
         LA    RE,REQLIST(RE)      RE=A(END OF LIST)                            
         IC    RF,OFLLN                                                         
         SH    RF,=Y(OFLLN1Q)                                                   
         MVC   0(0,RE),OFLNTRY     MOVE LIST TO W/S                             
         EX    RF,*-6                                                           
         SRL   RF,1                RF=NUMBER OF ENTRIES JUST ADDED              
         SR    RE,RE                                                            
         IC    RE,REQLISTN         RE=NUMBER OF ENTRIES SO FAR                  
         AR    RE,RF                                                            
         STC   RE,REQLISTN                                                      
OFFINI08 IC    RF,OFLLN            BUMP TO NEXT ELEMENT                         
         AR    R1,RF                                                            
         B     OFFINI04                                                         
*                                                                               
OFFINI12 OC    OFFANEWA,OFFANEWA   TEST USER LIMIT ACCESS SET                   
         BNZ   OFFINI14                                                         
         CLI   REQLISTN,0          TEST REQUEST FILTER SET                      
         BE    OFFINI30                                                         
         MVC   LIMLISTN,REQLISTN   YES - SET AS LIMIT ACCESS LIST               
         LA    R0,LIMLIST                                                       
         LA    RE,REQLIST                                                       
         LA    R1,LIMLISTL                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     OFFINI30                                                         
*                                                                               
OFFINI14 LA    R2,IO                                                            
         USING OFFRECD,R2          BUILD KEY OF OFFICE RECORD                   
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         MVC   OFFKOFF,OFFANEWA    READ USER LIMIT ACCESS OFFICE RECORD         
         GOTO1 VDATAMGR,PARM,DMREAD,ACCFIL,OFFRECD,OFFRECD                      
         BNE   ERROFF                                                           
         MVI   LIMLISTN,1          SET LIMITED TO ONE OFFICE                    
         MVC   LIMLIST,OFFANEWA    (EVEN IF REQUEST FOR ANOTHER OFFICE)         
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BZ    OFFINI30                                                         
         MVI   LIMLISTN,0          IF LIMITED TO A LIST BUILD ONE               
*                                                                               
         LA    R1,OFFRECD+(ACRECORD-ACKEYD)                                     
         USING OFLELD,R1           R1=A(FIRST ELEMENT)                          
         SR    RF,RF                                                            
OFFINI16 CLI   OFLEL,0             TEST END-OF-RECORD                           
         BE    OFFINI26                                                         
         CLI   OFLEL,OFLELQ        TEST OFFICE LIST ELEMENT                     
         BNE   OFFINI24                                                         
         LA    RE,OFLNTRY                                                       
         USING OFLNTRY,RE          RE=A(OFFICE LIST ENTRY)                      
         SR    R0,R0                                                            
         ICM   R0,1,OFLLN                                                       
         SH    R0,=Y(OFLLN1Q)                                                   
         SRL   R0,1                R0=NUMBER OF ENTRIES IN ELEMENT              
*                                                                               
OFFINI18 SR    R2,R2               TEST OFFICE IN REQUEST LIST                  
         ICM   R2,1,REQLISTN       R2=N'ENTRIES IN REQUEST LIST                 
         BZ    OFFINI20                                                         
         LA    R3,REQLIST          R3=A(REQUEST LIST)                           
         CLC   OFLNTRY,0(R3)       MATCH LIMIT ACCESS TO REQUEST LIST           
         BE    OFFINI20                                                         
         LA    R3,L'REQLIST(R3)                                                 
         BCT   R2,*-14                                                          
         B     OFFINI22                                                         
*                                                                               
OFFINI20 IC    R2,LIMLISTN         ADD ENTRY TO LIMIT ACCESS LIST               
         LA    R0,1(R2)                                                         
         STC   R0,LIMLISTN                                                      
         SLL   R2,1                                                             
         LA    R2,LIMLIST(R2)                                                   
         MVC   0(L'LIMLIST,R2),OFLNTRY                                          
*                                                                               
OFFINI22 LA    RE,L'OFLNTRY(RE)    BUMP TO NEXT LIST ENTRY                      
         BCT   R0,OFFINI18         DO FOR NUMBER OF OFFICES                     
         DROP  RE                                                               
*                                                                               
OFFINI24 IC    RF,OFLLN            BUMP TO NEXT ELEMENT                         
         AR    R1,RF                                                            
         B     OFFINI16                                                         
*                                                                               
OFFINI26 CLI   LIMLISTN,0          TEST LIST IS EMPTY                           
         BE    ERROFF              YES - EXIT WITH ERROR                        
*                                                                               
OFFINI30 LA    R2,KEY              BUILD KEY FOR FIRST OFFICE RECORD            
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         GOTO1 VDATAMGR,PARM,DMRDHI,ACCFIL,OFFKEY,IO                            
         BNE   ERRDSK                                                           
         LA    R2,IO                                                            
         GOTO1 ,PARM,DMRSEQ,ACCFIL,OFFKEY,OFFKEY                                
*                                                                               
OFFINI40 CLC   OFFKEY(OFFKOFF-OFFKEY),KEY                                       
         BNE   OFFINI82                                                         
         XC    OFFBREC(OFFBLIST-OFFBREC),OFFBREC                                
         MVC   OFFBCODE,OFFKOFF    SET TSAR RECORD KEY                          
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BZ    *+8                                                              
         MVI   OFFBINDS,OFFBILST   SET THIS IS AN OFFICE LIST                   
*                                                                               
         LA    R1,OFFRECD+(ACRECORD-ACKEYD)                                     
         SR    RF,RF                                                            
OFFINI42 CLI   0(R1),0             TEST END-OF-RECORD                           
         BE    OFFINI72                                                         
*                                                                               
         USING NAMELD,R1                                                        
         CLI   NAMEL,NAMELQ        TEST LONG NAME ELEMENT                       
         BNE   OFFINI46                                                         
         MVC   OFFBNAME,SPACES                                                  
         IC    RF,NAMLN                                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         MVC   OFFBNAME(0),NAMEREC                                              
         EX    RF,*-6                                                           
         B     OFFINI60                                                         
*                                                                               
         USING SNMELD,R1                                                        
OFFINI46 CLI   SNMEL,SNMELQ        TEST SHORT NAME ELEMENT                      
         BNE   OFFINI48                                                         
         MVC   OFFBSHRT,SNMNAME                                                 
         B     OFFINI60                                                         
*                                                                               
         USING OFLELD,R1                                                        
OFFINI48 CLI   OFLEL,OFLELQ        TEST OFFICE LIST ELEMENT                     
         BNE   OFFINI58                                                         
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BNZ   *+6                                                              
         DC    H'0'                CAN'T HAVE LIST IF NOT A LIST RECORD         
         SR    R0,R0                                                            
         IC    R0,OFLLN                                                         
         SH    R0,=Y(OFLLN1Q)                                                   
         SRL   R0,1                R0=NUMBER OF ENTRIES IN LIST ELEMENT         
         LA    R3,OFLNTRY          R3=A(FIRST ENTRY)                            
         USING OFLNTRY,R3                                                       
OFFINI50 LA    RE,LIMLIST          RE=A(LIMIT ACCESS LIST)                      
         ICM   RF,1,LIMLISTN       RF=NUMBER OF LIMIT ACCESS MEMBERS            
         BZ    OFFINI54            (ZERO MEANS UNLIMITED ACCESS)                
OFFINI52 CLC   OFLNTRY,0(RE)       MATCH OFFICE TO LIMIT ACCESS LIST            
         BE    OFFINI54                                                         
         LA    RE,L'LIMLIST(RE)                                                 
         BCT   RF,OFFINI52                                                      
         B     OFFINI56            NOT IN LIMIT ACCESS LIST - IGNORE            
OFFINI54 IC    RF,OFFBNUM                                                       
         LA    RF,1(RF)                                                         
         STC   RF,OFFBNUM          SET NUMBER OF ENTRIES IN LIST                
         SLL   RF,1                                                             
         LA    RF,OFFBLIST-2(RF)                                                
         MVC   0(2,RF),OFLNTRY     ADD OFFICE ENTRY TO LIST                     
OFFINI56 LA    R3,L'OFLNTRY(R3)    BUMP TO NEXT OFFICE LIST ENTRY               
         BCT   R0,OFFINI50                                                      
         B     OFFINI60                                                         
         DROP  R3                                                               
*                                                                               
OFFINI58 DS    0H                                                               
OFFINI60 IC    RF,1(R1)            BUMP TO NEXT RECORD ELEMENT                  
         AR    R1,RF                                                            
         B     OFFINI42                                                         
*                                                                               
OFFINI72 TM    OFFBINDS,OFFBILST   TEST THIS IS AN OFFICE LIST                  
         BZ    OFFINI74                                                         
         CLI   OFFBNUM,0           YES - TEST NUMBER OF ENTRIES IS ZERO         
         BNE   OFFINI76                                                         
         B     OFFINI80                                                         
*                                                                               
OFFINI74 ICM   RF,1,LIMLISTN       TEST OFFICE IS IN LIMIT ACCESS LIST          
         BZ    OFFINI76                                                         
         LA    RE,LIMLIST                                                       
         CLC   OFFBCODE,0(RE)      MATCH OFFICE TO LIST ENTRY                   
         BE    OFFINI76                                                         
         LA    RE,2(RE)                                                         
         BCT   RF,*-14             DO FOR NUMBER OF ENTRIES                     
         B     OFFINI80                                                         
*                                                                               
OFFINI76 LA    RE,OFFBLENQ                                                      
         TM    OFFBINDS,OFFBILST   TEST OFFICE LIST                             
         BZ    OFFINI78                                                         
         SR    RE,RE               YES - CALCULATE TSAR RECORD LENGTH           
         IC    RE,OFFBNUM                                                       
         SLL   RE,1                                                             
         LA    RE,OFFBLIST-OFFBREC(RE)                                          
*                                                                               
OFFINI78 STCM  RE,3,OFFBLEN        SET LENGTH OF TSAR RECORD                    
         BAS   RE,BUFFER                                                        
         BE    *+6                                                              
         DC    H'0'                DIE ON ERROR (NOT ENOUGH DISK)               
*                                                                               
OFFINI80 GOTO1 VDATAMGR,PARM       GET NEXT OFFICE RECORD                       
         B     OFFINI40                                                         
*                                                                               
OFFINI82 TM    TSINDS,TSIANYAD     TEST ANY RECORDS ADDED TO TSAR               
         BZ    ERRSEC                                                           
         MVI   TSACTN,TSASAV       SAVE BUFFER (TO FORCE WRITE)                 
         BAS   RE,BUFFER                                                        
         BE    *+6                                                              
         DC    H'0'                DIE ON ERROR                                 
*                                                                               
OFFINIX  OI    OFFAINDS,OFFAIINI   SET BLOCK INITIALISED                        
         LA    R0,LIMLISTN                                                      
         ST    R0,OFFAREC                                                       
         B     OFFSET                                                           
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO RESTORE VALUES                                           *         
*                                                                     *         
* THIS ACTION WILL RESTORE OFFALD VALUES ON TRANSACTIONS SUBSEQUENT   *         
* TO THE FIRST FOR SESSION.                                           *         
***********************************************************************         
         SPACE 1                                                                
OFFRES   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         XC    TSARD(TSPTAB-TSARD),TSARD                                        
         MVC   TSABUF,ABUF                                                      
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         MVC   TSACOM,OFFACOMF                                                  
         MVI   TSKEYL,L'OFFBCODE                                                
         MVI   TSRECI,TSRVAR       SET VARIABLE RECORD & MAX LENGTH             
         MVC   TSRECL,=Y(OFFBMAXL)                                              
         MVI   TSINDS,TSIALLOC+TSIREUSE                                         
         MVI   TSNBUF,1            SET ONE CORE BUFFER                          
         OC    TSPAGL,OFFALOWP     SET LOW PAGE ALLOCATED                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         OC    TSPAGN,OFFANUMP     SET NUMBER OF PAGES                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVI   TSACTN,TSARES       SET TO INITIALISE TSAR                       
         BAS   RE,BUFFER                                                        
         BE    *+6                                                              
         DC    H'0'                CAN'T RESTORE TSAR VALUES                    
*                                                                               
OFFRESX  B     OFFSET                                                           
         EJECT                                                                  
***********************************************************************         
* TEST OFFICE SECURITY FOR ACCOUNTS, OFFICE ACCOUNTS AND TRANSACTIONS *         
* IF THE USER HAS A LIMIT ACCESS SPECIFIED                            *         
*                                                                     *         
* ACCOUNT RECORDS                                                     *         
* ---------------                                                     *         
* IF THE ACCOUNT RECORD DOES NOT HAVE AN OFFICE ACCESS ELEMENT - ANY  *         
* USER REGARDLESS OF LIMIT ACCESS MAY HAVE FULL ACCESS TO THE RECORD. *         
* IF THE ACCOUNT RECORD IS FLAGGED FOR ALL OFFICES (OFAOFFC=ZEROES),  *         
* AND THERE ARE NO OFFICE OVERRIDES - FULL ACCESS IS GIVEN. IF THE    *         
* ACCOUNT IS FLAGGED FOR ALL OFFICES AND THERE ARE OVERRIDES - THE    *         
* USER LIMIT ACCESS RECORD IS READ AND OFFICE EXCLUSIONS ARE MATCHED  *         
* TO IT, IF ALL OFFICES IN THE USER RECORD ARE EXCLUDED NO ACCESS IS  *         
* GIVEN. IF THE ACCOUNT RECORD IS ATTACHED TO A SPECIFIC OFFICE OR    *         
* OFFICE LIST THE OFFICE (OR LIST) RECORD IS READ AND OFFICE EXCLU-   *         
* SIONS (IF ANY) ARE MATCHED TO IT, IF ALL OFFICES IN THE RECORD ARE  *         
* EXCLUDED NO ACCESS IS GIVEN.                                        *         
***********************************************************************         
         SPACE 1                                                                
OFFTST   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         OC    OFFANEWA,OFFANEWA   TEST USER HAS FULL ACCESS                    
         BZ    OFFTSTX                                                          
*                                                                               
         ICM   R2,15,OFFAREC                                                    
         USING ACTRECD,R2          R2=A(CALLER'S RECORD)                        
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BNE   OFFTST30                                                         
*                                                                               
         SR    R0,R0                                                            
         LA    R4,ACTRECD                                                       
         AH    R4,DATADISP         R4=A(FIRST ELEMENT ON RECORD)                
         USING RSTELD,R4           LOCATE ACCOUNT STATUS ELEMENT                
OFFTST2  IC    R0,RSTLN                                                         
         AR    R4,R0                                                            
         CLI   RSTEL,0             TEST END-OF-RECORD                           
         BNE   *+6                                                              
         DC    H'0'                ACCOUNT DOESN'T HAVE ELEMENT                 
         CLI   RSTEL,RSTELQ        TEST STATUS ELEMENT                          
         BNE   OFFTST2                                                          
         MVC   OFFASTA1,RSTSTAT    SAVE ACCOUNT STATUS BYTES                    
         MVC   OFFASTA3,RSTSTAT3                                                
*                                                                               
         XC    OFFACMOS,OFFACMOS   CLEAR CLOSED MOS VALUE                       
         LA    R1,RSTELD                                                        
         USING APOELD,R1                                                        
OFFTST3  IC    R0,APOLN            LOCATE PEEL-OFF ELEMENT ON ACCOUNT           
         AR    R1,R0                                                            
         CLI   APOEL,0             TEST E-O-R                                   
         BE    OFFTST4                                                          
         CLI   APOEL,APOELQ        TEST PEEL-OFF ELEMENT                        
         BNE   OFFTST3                                                          
         MVC   OFFACMOS,APOCMOS    EXTRACT LATEST CLOSED MONTH                  
         DROP  R1                                                               
*                                                                               
OFFTST4  TM    OFFAINDS,OFFAISEC   TEST SECURITY NUMBER CHECK                   
         BZ    OFFTST5                                                          
         OC    OFFAAUTH,OFFAAUTH   TEST AUTHORISATION SET                       
         BZ    OFFTST5                                                          
         CLC   OFFAASEC,RSTSECY+1  TEST USER IS AUTHORISED                      
         BL    ERRSEC                                                           
*                                                                               
OFFTST5  CLC   LDGPROD,ACTKUNT     TEST PRODUCTION LEDGER                       
         BNE   OFFTST6                                                          
         XC    OFFBCODE,OFFBCODE                                                
         OC    OFFBCODE,OFFAOFFC                                                
         BZ    OFFTSTX             NO OFFICE - EXIT OK                          
         B     OFFTST10            ELSE CHECK OFFICE                            
*                                                                               
OFFTST6  SR    R0,R0               HANDLE ACCOUNT RECORDS                       
         LA    R3,ACTRECD                                                       
         AH    R3,DATADISP         R3=A(FIRST ELEMENT ON RECORD)                
         USING OFAELD,R3                                                        
OFFTST8  CLI   OFAEL,0             TEST END-OF-RECORD                           
         BE    OFFTSTX             YES - NO SECURITY ON THIS ACCOUNT            
         CLI   OFAEL,OFAELQ        TEST OFFICE LIST ENTRY                       
         BE    *+14                                                             
         IC    R0,OFALN            NO - BUMP TO NEXT ELEMENT                    
         AR    R3,R0                                                            
         B     OFFTST8                                                          
*                                                                               
         XC    OFFBCODE,OFFBCODE                                                
         OC    OFFBCODE,OFAOFFC    SET KEY FOR TSAR READ                        
         BNZ   *+10                                                             
         MVC   OFFBCODE,OFFANEWA   READ USER LIMIT ACCESS                       
*                                                                               
OFFTST10 MVC   TSABUF,ABUF         INITIALISE TSAR BLOCK FOR READ               
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         MVI   TSACTN,TSARDH                                                    
         BAS   RE,BUFFER                                                        
         BE    OFFTST12                                                         
         CLC   LDGPROD,ACTKUNT     TEST PRODUCTION LEDGER                       
         BE    ERRSEC                                                           
         OC    OFAOFFC,OFAOFFC     IF READ FOR USER RECORD                      
         BNZ   ERRSEC                                                           
         DC    H'0'                IT MUST BE FOUND                             
*                                                                               
OFFTST12 CLC   LDGPROD,ACTKUNT     TEST PRODUCTION LEDGER                       
         BE    OFFTSTX                                                          
         CLC   OFFANEWA,OFAOFFC    TEST ACCESS (LIST) IS OFFICE (LIST)          
         BE    OFFTSTX                                                          
         CLI   OFALN,OFALN1Q       TEST ANY OFFICE OVERRIDES                    
         BE    OFFTSTX                                                          
*                                                                               
         SR    RE,RE               PROCESS OFFICE OVERRIDE LIST                 
         IC    RE,OFALN                                                         
         SH    RE,=Y(OFALN1Q)                                                   
         LA    RF,OFAOVER          RF=A(FIRST OVERRIDE ENTRY)                   
         USING OFAOVER,RF                                                       
         SR    R3,R3               R3=COUNT OF MATCHING ENTRIES                 
*                                                                               
OFFTST14 TM    OFAOQUAL,OFAOQENQ   TEST EXCLUDE THIS OFFICE                     
         BNZ   OFFTST20                                                         
         TM    OFFBINDS,OFFBILST   TEST THIS IS A LIST                          
         BNZ   OFFTST16                                                         
         CLC   OFAOOFFC,OFFBCODE   NO - MATCH OFFICE TO OFFICE KEY              
         BE    ERRSEC              OFFICE IS SPECIFICALLY EXCLUDED              
         B     OFFTST20                                                         
*                                                                               
OFFTST16 SR    R0,R0                                                            
         IC    R0,OFFBNUM          R0=NUMBER OF OFFICES IN OFFICE LIST          
         LA    R1,OFFBLIST         R1=A(OFFICE LIST)                            
OFFTST18 CLC   OFAOOFFC,0(R1)      MATCH EXCLUDED OFFICE TO LIST                
         BNE   *+12                                                             
         LA    R3,1(R3)            FOUND - INCREMENT EXCLUDED COUNT             
         B     *+12                                                             
         LA    R1,2(R1)            BUMP TO NEXT LIST ENTRY                      
         BCT   R0,OFFTST18                                                      
OFFTST20 LA    RF,L'OFAOVER(RF)    BUMP TO NEXT OVERRIDE ENTRY                  
         SH    RE,=Y(L'OFAOVER)                                                 
         BP    OFFTST14                                                         
         TM    OFFBINDS,OFFBILST   IF NOT A LIST IT MUST BE OK                  
         BZ    OFFTSTX                                                          
         CLM   R3,1,OFFBNUM        TEST ALL LIST ENTRIES ARE EXCLUDED           
         BL    OFFTSTX                                                          
         B     ERRSEC                                                           
         DROP  RF                                                               
*                                                                               
         USING OFARECD,R2                                                       
OFFTST30 CLC   OFAKEY+OFAKEND(L'OFAKEY-OFAKEND),SPACES                          
         BNE   OFFTST40                                                         
         MVC   OFFACMOS,OFARCMOS   SAVE OFFICE/ACCOUNT CLOSED MOS               
         MVC   TSABUF,ABUF         INITIALISE TSAR BLOCK FOR READ               
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         MVI   TSACTN,TSARDH                                                    
         MVC   OFFBCODE,OFAKOFF    READ FOR OFFICE ACCOUNT OFFICE CODE          
         BAS   RE,BUFFER                                                        
         BE    OFFTSTX                                                          
         B     ERROFF                                                           
*                                                                               
         USING TRNRECD,R2                                                       
OFFTST40 OC    TRNKDATE,TRNKDATE                                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   TRNKDATE,SPACES                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    RF,RF                                                            
         IC    RF,OFFALDGT         RF=LEDGER CLOSE OUT TYPE                     
         CH    RF,=Y(OFFTBRSM)                                                  
         BNH   *+6                                                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     OFFTBRS(RF)                                                      
OFFTBRS  DS    0XL4                                                             
         B     OFFTBBF             BALANCE BROUGHT FORWARD (SQ)                 
         B     OFFTPBL             PAYABLE LEDGERS (SV, SX)                     
         B     OFFTBBF             ZERO BALANCE FORWARD (SE, SI)                
         B     OFFTRCV             RECEIVABLES LEDGER (SR)                      
         B     OFFTPRD             PRODUCTION LEDGER (SJ)                       
         B     OFFTCSH             CASH LEDGERS (SC)                            
         B     OFFTMBF             MATCHED BALANCE FORWARD (SK, SZ)             
OFFTBRSM EQU   (*-OFFTBRS)/L'OFFTBRS                                            
*                                                                               
OFFTBBF  OC    OFFACMOS,OFFACMOS   TEST OFFICE/ACCOUNT CLOSED                   
         BZ    OFFTSTX                                                          
         CLC   TRNRSMOS,OFFACMOS   TEST TRANSACTION IN CLOSED PERIOD            
         BH    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTPBL  OC    OFFACMOS,OFFACMOS   TEST OFFICE/ACCOUNT CLOSED                   
         BZ    OFFTSTX                                                          
         TM    TRNRSTA2,TRNSUSED   TEST TRANSACTION USED                        
         BZ    OFFTSTX                                                          
         CLC   TRNRSMOS,OFFACMOS   TEST TRANSACTION IN CLOSED PERIOD            
         BH    OFFTSTX                                                          
         CLC   TRNRSUSE,OFFACMOS   TEST USED IN CLOSED PERIOD                   
         BH    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTRCV  TM    TRNRSTA2,TRNSPEEL   TEST TRANSACTION PEELED                      
         BZ    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTPRD  TM    OFFASTA1,RSTSACIC   TEST JOB IS CLOSED                           
         BZ    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTCSH  OC    OFFACMOS,OFFACMOS   TEST OFFICE/ACCOUNT CLOSED                   
         BZ    OFFTSTX                                                          
         CLC   TRNRSMOS,OFFACMOS   TEST TRANSACTION IN CLOSED PERIOD            
         BH    OFFTSTX                                                          
         LA    R3,TRNRECD                                                       
         AH    R3,DATADISP                                                      
         USING TRNELD,R3                                                        
         CLI   TRNEL,TRNELQ                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    TRNSTAT,TRNSDR      TEST DEBIT OR CREDIT                         
         BO    *+16                                                             
         TM    OFFASTA3,RSTSPRCR   TEST CLOSE RECONCILED CREDITS                
         BZ    OFFTCLS             CREDITS ARE NOT TESTED (ERGO CLOSED)         
         B     OFFTCSH2            CREDITS ARE TESTED                           
*                                                                               
         TM    OFFASTA3,RSTSPRDR   TEST CLOSE RECONCILED DEBITS                 
         BZ    OFFTCLS             DEBITS ARE NOT TESTED (ERGO CLOSED)          
*                                                                               
OFFTCSH2 TM    TRNSTAT,TRNSBREC    TEST RECONCILED ITEM                         
         BZ    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTMBF  OC    OFFACMOS,OFFACMOS   TEST OFFICE/ACCOUNT CLOSED                   
         BZ    OFFTSTX                                                          
         CLC   TRNRSMOS,OFFACMOS   TEST TRANSACTION IN CLOSED PERIOD            
         BH    OFFTSTX                                                          
         LA    R3,TRNRECD                                                       
         AH    R3,DATADISP                                                      
         USING TRNELD,R3                                                        
         CLI   TRNEL,TRNELQ                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    TRNSTAT,TRNSMTCH    TEST MATCHED ITEM                            
         BZ    OFFTSTX                                                          
         B     OFFTCLS                                                          
*                                                                               
OFFTCLS  OI    OFFASTAT,OFFASCLS   SET TRANSACTION CLOSED                       
*                                                                               
OFFTSTX  B     OFFSET                                                           
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* TEST IF OFFICE CODE AT OFFAOFFC MAY BE POSTED TO ACCOUNT ADDRESSED  *         
* BY OFFAREC (THIS DOES NOT CHECK MONTH OF SERVICE LOCK FOR OFFICE)   *         
*                                                                     *         
* OFFAOFFC MUST BE A VALID OFFICE FOR THE USER (IE S/HE MUST HAVE     *         
* ACCESS) THIS IS VERIFIED BY READING THE OFFICE RECORD FROM TSAR,    *         
* THIS OFFICE MUST NOT BE A LIST. IF OFFAOFFC IS THE SAME AS THE      *         
* ACCOUNT OFFICE WE ALLOW THE POSTING TO TAKE PLACE. OFFAOFFC IS THEN *         
* MATCHED TO THE ACCOUNT OVERRIDE LIST TO SEE IF IT IS SPECIFICALLY   *         
* INCLUDED OR EXCLUDED.                                               *         
***********************************************************************         
         SPACE 1                                                                
OFFPST   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
*                                                                               
         MVC   TSABUF,ABUF         INITIALISE TSAR BLOCK FOR READ               
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         MVI   TSACTN,TSARDH                                                    
         MVC   OFFBCODE,OFFAOFFC   SET OFFICE CODE                              
         BAS   RE,BUFFER                                                        
         BNE   ERROFF                                                           
         TM    OFFBINDS,OFFBILST   EXIT WITH ERROR IF OFFICE IS A LIST          
         BNZ   ERROFF                                                           
*                                                                               
         ICM   R2,15,OFFAREC                                                    
         USING ACTRECD,R2          R2=A(CALLER'S RECORD)                        
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BE    *+6                                                              
         DC    H'0'                DIE IF NOT AN ACCOUNT RECORD                 
*                                                                               
         SR    R0,R0               LOCATE OFFICE ACCESS ELEMENT                 
         LA    R3,ACTRECD                                                       
         AH    R3,DATADISP         R3=A(FIRST ELEMENT ON RECORD)                
         USING OFAELD,R3                                                        
OFFPST2  CLI   OFAEL,0             TEST END-OF-RECORD                           
         BE    OFFPSTX             YES - ANY OFFICE MAY POST TO ACCOUNT         
         CLI   OFAEL,OFAELQ        TEST OFFICE LIST ENTRY                       
         BE    *+14                                                             
         IC    R0,OFALN            NO - BUMP TO NEXT ELEMENT                    
         AR    R3,R0                                                            
         B     OFFPST2                                                          
*                                                                               
         CLC   OFAOFFC,OFFAOFFC    TEST OFFICE IS THE ACCOUNT OFFICE            
         BE    OFFPSTX                                                          
         CLI   OFALN,OFALN1Q       TEST ANY OFFICE OVERRIDES                    
         BE    OFFPST12                                                         
         SR    RE,RE               PROCESS OFFICE OVERRIDE LIST                 
         IC    RE,OFALN                                                         
         SH    RE,=Y(OFALN1Q)                                                   
         LA    RF,OFAOVER          RF=A(FIRST OVERRIDE ENTRY)                   
         USING OFAOVER,RF                                                       
*                                                                               
OFFPST6  TM    OFAOQUAL,OFAOQPST   TEST POSTING ALLOWED TO OFFICE               
         BNZ   *+14                                                             
         CLC   OFAOOFFC,OFFAOFFC   MATCH OFFICE CODE                            
         BE    ERROFF                                                           
         LA    RF,L'OFAOVER(RF)    BUMP TO NEXT OVERRIDE ENTRY                  
         SH    RE,=Y(L'OFAOVER)                                                 
         BP    OFFPST6                                                          
         DROP  RF                                                               
*                                                                               
OFFPST12 OC    OFAOFFC,OFAOFFC     TEST ALL OFFICES ARE VALID                   
         BE    OFFPSTX                                                          
*                                                                               
         MVC   OFFBCODE,OFAOFFC    READ OFFICE (OR LIST) RECORD                 
         BAS   RE,BUFFER                                                        
         BNE   ERROFF                                                           
         TM    OFFBINDS,OFFBILST   IF NOT A LIST - MUST BE OK                   
         BZ    OFFPSTX                                                          
*                                                                               
         SR    RE,RE               TEST OFFICE IS IN OFFICE LIST                
         IC    RE,OFFBNUM                                                       
         LA    RF,OFFBLIST                                                      
         CLC   OFFAOFFC,0(RF)                                                   
         BE    OFFPSTX                                                          
         LA    RF,2(RF)                                                         
         BCT   RE,*-14                                                          
         B     ERROFF              ERROR IF NOT IN LIST                         
*                                                                               
OFFPSTX  B     OFFSET                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET OFFICE RECORD FOR OFFICE CODE AT OFFAOFFC            *         
*                                                                     *         
* EXIT - OFFAREC POINTS TO OFFICE RECORD IF FOUND                     *         
***********************************************************************         
         SPACE 1                                                                
OFFGET   MVC   TSABUF,ABUF         INITIALISE TSAR BLOCK FOR READ               
         LA    R0,OFFBREC                                                       
         ST    R0,TSAREC                                                        
         ST    R0,OFFAREC                                                       
         MVI   TSACTN,TSARDH                                                    
         MVC   OFFBCODE,OFFAOFFC   SET OFFICE CODE                              
         BAS   RE,BUFFER                                                        
         BNE   ERROFF                                                           
*                                                                               
OFFGETX  B     OFFSET                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH OLD OFFICE LIST                                *         
***********************************************************************         
         SPACE 1                                                                
OLDRES   DS    0H                                                               
OLDINI   XC    OFFAWORK(L'CTPVALUE*2),OFFAWORK                                  
         CLI   OFFAOLDA,C'*'       TEST LIMITED TO ONE OFFICE                   
         BNE   *+14                                                             
         MVC   OFFAWORK(1),OFFAOLDA+1                                           
         B     OLDINIX                                                          
         CLI   OFFAOLDA,C'$'       TEST OFFICE LIST                             
         BNE   OLDINI1                                                          
         XC    DUB(3),DUB                                                       
         MVC   DUB+1(2),OFFAOLDA                                                
         LA    R3,OFFAWORK         POINT TO OFFICE LIST                         
         B     OLDINI2                                                          
*                                                                               
OLDINI1  XC    OFFAOLDA,OFFAOLDA   GIVE ACCESS TO EVERYTHING                    
         B     OLDINIX             IF I CANNOT RECOGNIZE IT                     
*                                                                               
OLDINI2  LA    R2,IO               BUILD KEY OF FIELD RECORD                    
         USING CTUREC,R2                                                        
         XC    CTUKEY,CTUKEY                                                    
         MVI   CTUKTYP,C'U'                                                     
         MVI   CTUKSYS,C'A'                                                     
         MVC   CTUKPROG,DUB                                                     
         MVC   CTUKAGY,OFFAALPH                                                 
         GOTO1 VDATAMGR,PARM,DMREAD,CTFILE,CTUREC,CTUREC                        
         CLI   8(R1),0             TEST FOR ERRORS                              
         BNE   OLDINIX                                                          
*                                                                               
         SR    R0,R0                                                            
         LA    R1,CTUDATA                                                       
         USING CTPVD,R1            LOCATE FIELD VALUES ELEMENT                  
OLDINI4  CLI   CTPVEL,0                                                         
         BE    OLDINI6                                                          
         CLI   CTPVEL,X'72'                                                     
         BE    *+14                                                             
         IC    R0,CTPVLEN                                                       
         AR    R1,R0                                                            
         B     OLDINI4                                                          
         MVC   0(L'CTPVALUE,R3),CTPVALUE                                        
*                                                                               
OLDINI6  LA    R3,L'CTPVALUE(R3)   BUMP TO NEXT LIST BLOCK                      
         CLI   DUB,0               TEST SECOND LIST PROCESSED                   
         BNE   OLDINIX                                                          
         MVC   DUB(2),DUB+1        NO - BUILD SECOND KEY & READ                 
         MVI   DUB+2,C'A'                                                       
         B     OLDINI2                                                          
*                                                                               
OLDINIX  OI    OFFAINDS,OFFAIINI   SET BLOCK INITIALISED                        
         B     OFFSET                                                           
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST OFFICE SECURITY FOR ACCOUNTS ON OLD OFFICE SYSTEM   *         
*                                                                     *         
* ENTRY - OFFAOFFC SET FOR PRODUCTION LEDGER, OFFICE IN FILTER LEDGER *         
* EXIT - OFFAOFFC CONTAINS OFFICE CODE FOR ACCOUNT IF VALID ACCESS    *         
***********************************************************************         
         SPACE 1                                                                
OLDTST   TM    OFFACST1,CPYSOROE   EXIT IF NOT ON OFFICES                       
         BZ    OLDTSTX                                                          
         TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
*                                                                               
         ICM   R2,15,OFFAREC                                                    
         USING ACTRECD,R2          R2=A(CALLER'S RECORD)                        
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BNE   OLDTST20                                                         
*                                                                               
         SR    R0,R0                                                            
         LA    R4,ACTRECD                                                       
         AH    R4,DATADISP         R4=A(FIRST ELEMENT ON RECORD)                
         USING RSTELD,R4           LOCATE ACCOUNT STATUS ELEMENT                
OLDTST2  CLI   RSTEL,0             TEST END-OF-RECORD                           
         BNE   *+6                                                              
         DC    H'0'                ACCOUNT DOESN'T HAVE ELEMENT                 
         CLI   RSTEL,RSTELQ        TEST STATUS ELEMENT                          
         BE    *+14                                                             
         IC    R0,RSTLN                                                         
         AR    R4,R0                                                            
         B     OLDTST2                                                          
*                                                                               
         TM    OFFAINDS,OFFAISEC   TEST SECURITY NUMBER CHECK                   
         BZ    OLDTST4                                                          
         OC    OFFAAUTH,OFFAAUTH   TEST AUTHORISATION SET                       
         BZ    OLDTST4                                                          
         CLC   OFFAASEC,RSTSECY+1  TEST USER IS AUTHORISED                      
         BL    ERRSEC                                                           
*                                                                               
OLDTST4  CLC   LDGPROD,ACTKUNT     TEST PRODUCTION LEDGER                       
         BNE   OLDTST6                                                          
         CLI   OFFAOPOS,LDGOPROF   TEST IN PROFILE                              
         BE    OLDTST30                                                         
*&&UK                                                                           
         CLI   OFFAOPOS,0          UNDEFINED MEANS CHECK OFFICE                 
         BE    OLDTST30                                                         
*&&                                                                             
         B     OLDTSTX                                                          
*                                                                               
OLDTST6  CLI   OFFAOPOS,LDGOKEY    TEST OFFICE IN ACCOUNT KEY                   
         BH    OLDTST8                                                          
         MVC   OFFAOFFC,SPACES                                                  
         SR    RF,RF                                                            
         ICM   RF,1,OFFAOPOS       RF=DISPLACEMENT TO OFFICE IN KEY             
         BZ    OLDTSTX             (LDGOPOS=0 MEANS NO OFFICES)                 
         LA    RF,ACTKLDG(RF)                                                   
         MVC   OFFAOFFC(1),0(RF)   SET OFFICE CODE FROM KEY                     
         CLI   OFFAOFFC,C' '       TEST FOR BLANK                               
         BE    OLDTSTX             YES-LET A HIGH LEVEL ACCOUNT PASS            
         B     OLDTST30                                                         
*                                                                               
OLDTST8  CLI   OFFAOPOS,LDGOTRAN   TEST OFFICE WITHIN TRANSACTIONS              
         BE    *+12                                                             
         CLI   OFFAOPOS,LDGOFLT1   LESS THAN FILTER ONE IS UNKNOWN              
         BNL   OLDTST10                                                         
         MVC   OFFAOFFC,SPACES     SET OFFICE TO SPACES                         
         B     OLDTSTX                                                          
*                                                                               
OLDTST10 CLI   OFFALDGL,L'ACTKACT  TEST ONE LEVEL LEDGER                        
         BNE   OLDTST30                                                         
         MVC   WORK(1),OFFAOPOS    YES - EXTRACT FILTER VALUE                   
         NI    WORK,X'07'                                                       
         SR    RE,RE                                                            
         IC    RE,WORK                                                          
         IC    RE,FILDISP-1(RE)                                                 
         LA    RE,RSTELD(RE)                                                    
         MVC   OFFAOFFC(1),0(RE)   EXTRACT OFFICE CODE                          
         MVI   OFFAOFFC+1,C' '                                                  
         B     OLDTST30            OFFICE HAS BEEN PASSED BY CALLER             
*                                                                               
         USING TRNRECD,R2                                                       
OLDTST20 CLC   TRNKCULC,SPACES     ENSURE TRANSACTION RECORD PASSED             
         BNE   *+6                                                              
         DC    H'0'                                                             
         OC    TRNKDATE,TRNKDATE                                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   TRNKDATE,SPACES                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
         TM    OFFACTRL,OFFACCNV   TEST NEW RECORD PASSED                       
         BZ    OLDTST24                                                         
         TM    TRNRSTA2,TRNSPEEL   YES - TEST NEW STATUS BIT                    
         BZ    *+8                                                              
         OI    OFFASTAT,OFFASCLS                                                
         B     OLDTST26                                                         
*                                                                               
         USING ACKEYD,R2                                                        
OLDTST24 OC    ACDTPEEL,ACDTPEEL   TEST OLD PEELED DATE                         
         BZ    *+8                                                              
         OI    OFFASTAT,OFFASCLS                                                
*                                                                               
OLDTST26 MVC   OFFAOFFC,SPACES                                                  
         USING TRNRECD,R2                                                       
         CLI   OFFAOPOS,LDGOTRAN   TEST OFFICE AT TRANSACTION LEVEL             
         BNE   OLDTSTX                                                          
         LA    R4,TRNRECD                                                       
         AH    R4,DATADISP                                                      
         USING TRNELD,R4                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION ELEMENT                 
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   OFFAOFFC(1),TRNANAL                                              
*                                                                               
OLDTST30 OC    OFFAOLDA,OFFAOLDA   TEST USER HAS FULL ACCESS                    
         BZ    OLDTSTX                                                          
         LA    R1,OFFAWORK         TEST OFFICE IS IN USER ACCESS LIST           
         LA    R0,L'CTPVALUE*2                                                  
OLDTST32 CLI   0(R1),0             TEST END-OF-LIST                             
         BE    ERRSEC              YES - ERROR                                  
         CLI   0(R1),C'0'          IGNORE OFFICE ZERO                           
         BE    *+14                                                             
         CLC   0(1,R1),OFFAOFFC    MATCH OFFICE TO LIMIT ACCESS LIST            
         BE    OLDTSTX                                                          
         LA    R1,1(R1)            BUMP TO NEXT OFFICE                          
         BCT   R0,OLDTST32         DO FOR NUMBER OF LIST ENTRIES                
         B     ERRSEC                                                           
*                                                                               
OLDTSTX  B     OFFSET                                                           
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST IF AN OFFICE CODE MAY BE POSTED TO AN ACCOUNT       *         
***********************************************************************         
         SPACE 1                                                                
OLDVAL   DS    0H                                                               
OLDPST   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         OC    OFFAOLDA,OFFAOLDA   TEST USER HAS FULL ACCESS                    
         BZ    OLDPSTX                                                          
         LA    R1,OFFAWORK         TEST OFFICE IS IN USER ACCESS LIST           
         LA    R0,L'CTPVALUE*2                                                  
OLDPST2  CLI   0(R1),0             TEST END-OF-LIST                             
         BE    ERROFF              YES - ERROR                                  
         CLI   0(R1),C'0'          IGNORE OFFICE ZERO                           
         BE    *+14                                                             
         CLC   0(1,R1),OFFAOFFC    MATCH OFFICE TO LIMIT ACCESS LIST            
         BE    OLDPSTX                                                          
         LA    R1,1(R1)            BUMP TO NEXT OFFICE                          
         BCT   R0,OLDPST2          DO FOR NUMBER OF LIST ENTRIES                
         B     ERROFF                                                           
*                                                                               
OLDPSTX  B     OFFSET                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH OFFICE LIST FOR INTERMEDIATE OFFICE SYSTEM     *         
***********************************************************************         
         SPACE 1                                                                
INTRES   DS    0H                                                               
INTINI   DS    0H                                                               
         LA    RE,OFFAWORK                                                      
         LHI   RF,L'OFFAWORK                                                    
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR OFFICE LIST AREA                       
*                                                                               
         LA    R3,OFFAWORK+2       R3=A(LIST OF OFFICES)                        
         SR    R4,R4               R4=NUMBER OF OFFICES IN LIST                 
         CLI   OFFANEWA,C' '       TEST NEW LIMIT ACCESS SET                    
         BH    INTINI10                                                         
         CLI   OFFAOLDA,C'$'       TEST OLD ACCESS WAS A LIST                   
         BNE   *+14                                                             
         MVC   OFFANEWA,OFFAOLDA   YES - COPY INTO NEW ACCESS                   
         B     INTINI10                                                         
         CLI   OFFAOLDA,C'*'       TEST OLD OFFICE LOCKIN                       
         BE    *+14                                                             
         XC    OFFANEWA,OFFANEWA   NO - ASSUME NO LOCKOUTS                      
         B     INTINI10                                                         
         MVC   OFFANEWA,OFFAOLDA   YES - SET NEW OFFICE LOCKIN                  
         MVI   OFFANEWA+1,C' '                                                  
*                                                                               
INTINI10 OC    OFFANEWA,OFFANEWA   TEST LIMIT ACCESS IS SET                     
         BNZ   INTINI50                                                         
         LA    R2,KEY              BUILD KEY FOR FIRST OFFICE RECORD            
         USING OFFRECD,R2                                                       
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         GOTO1 VDATAMGR,PARM,DMRDHI,ACCFIL,OFFRECD,IO                           
         BNE   ERRDSK                                                           
         LA    R2,IO                                                            
         GOTO1 ,PARM,DMRSEQ,ACCFIL,OFFRECD,OFFRECD                              
INTINI30 CLC   OFFKEY(OFFKOFF-OFFKEY),KEY                                       
         BNE   INTINI90                                                         
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BNZ   INTINI40                                                         
         MVC   0(L'OFFKOFF,R3),OFFKOFF                                          
         LA    R3,L'OFFKOFF(R3)                                                 
         LA    R4,1(R4)                                                         
         CH    R4,=Y((L'OFFAWORK-2)/L'OFFKOFF)                                  
         BNH   INTINI40                                                         
         DC    H'0'                                                             
INTINI40 GOTO1 VDATAMGR,PARM       GET NEXT RECORD                              
         B     INTINI30                                                         
*                                                                               
INTINI50 LA    R2,IO               READ LIMIT ACCESS OFFICE RECORD              
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         MVC   OFFKOFF,OFFANEWA                                                 
         GOTO1 VDATAMGR,PARM,DMREAD,ACCFIL,OFFRECD,OFFRECD                      
         BNE   ERROFF                                                           
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BNZ   INTINI60                                                         
         MVC   0(L'OFFKOFF,R3),OFFKOFF                                          
         LA    R4,1                                                             
         B     INTINI90                                                         
*                                                                               
INTINI60 LA    R1,OFFRECD+(ACRECORD-ACKEYD)                                     
         USING OFLELD,R1                                                        
INTINI70 CLI   OFLEL,0                                                          
         BE    INTINI90                                                         
         CLI   OFLEL,OFLELQ        TEST OFFICE LIST ELEMENT                     
         BNE   INTINI80                                                         
*                                                                               
         LLC   R0,OFLLN            GET LENGTH OF ELEMENT                        
         SHI   R0,OFLLN1Q          SUBTRACT OVERHEAD                            
         LR    R5,R0               SAVE OFF FOR EX MOVE                         
         BCTR  R5,0                SUBTRACT 1 FOR EX MVC                        
         SRL   R0,1                R0=NUMBER OF ENTRIES IN LIST ELEMENT         
*                                                                               
         ZICM  R4,OFFAWORK,2      GET CURRENT NUMBER OF OFFICES                 
         SLL   R4,1               MULTIPLY BY 2 TO GET DISPLACEMENT             
         LA    RE,OFFAWORK+2                                                    
         AR    RE,R4              DISPLACEMENT TO NEXT AVAILABLE SLOT           
         MVC   0(0,RE),OFLNTRY                                                  
         EX    R5,*-6             R5 CONTAINS LENGTH OF D2 MINUS OVHEAD         
         AHI   R5,1               ADJUST EX MVC LENGTH BACK                     
         AR    R4,R5              UPDATE NUMBER OF OFFICES                      
         SRL   R4,1               DIVIDE BY 2 TO GET # OF OFFICES               
         STCM  R4,3,OFFAWORK      SAVE NUMBER OF OFFICES                        
*                                                                               
INTINI80 LLC   RF,1(R1)                                                         
         AR    R1,RF               BUMP TO NEXT ELEMENT                         
         B     INTINI70                                                         
*                                                                               
INTINI90 STCM  R4,3,OFFAWORK       SET NUMBER OF OFFICES FOUND                  
         LTR   R4,R4               TEST ANY OFFICES IN LIST                     
         BNZ   *+8                                                              
         MVI   OFFAERR,OFFAESEC    NO - SET SECURITY LOCKOUT & EXIT             
         OI    OFFAINDS,OFFAIINI   SET BLOCK INITIALISED                        
         B     OFFSET                                                           
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST IF AN OFFICE CODE MAY BE POSTED TO AN ACCOUNT       *         
*                                                                     *         
* THIS CODE ENSURES THAT THE USER HAS ACCESS TO THE OFFICE THAT IS    *         
* PASSED IN OFFAOFFC. NO CHECK IS MADE AS TO WHETHER OR NOT THE       *         
* OFFICE CODE EXISTS.                                                 *         
***********************************************************************         
         SPACE 1                                                                
INTPST   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         OC    OFFANEWA,OFFANEWA   TEST FULL ACCESS ALLOWED                     
         BZ    INTPSTX             YES                                          
         SR    R0,R0                                                            
         ICM   R0,3,OFFAWORK                                                    
         BZ    ERROFF                                                           
         LA    R1,OFFAWORK+2       TEST OFFICE IS IN USER ACCESS LIST           
INTPST2  CLC   OFFAOFFC,0(R1)      MATCH OFFICE TO LIMIT ACCESS LIST            
         BE    INTPSTX                                                          
         LA    R1,L'OFFKOFF(R1)    BUMP TO NEXT OFFICE                          
         BCT   R0,INTPST2          DO FOR NUMBER OF LIST ENTRIES                
         B     ERROFF                                                           
*                                                                               
INTPSTX  B     OFFSET                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST IF AN INPUT OFFICE CODE IS VALID                    *         
*                                                                     *         
* THIS CODE ENSURES THAT THE USER HAS ACCESS TO THE OFFICE THAT IS    *         
* PASSED IN OFFAOFFC. NO CHECK IS MADE AS TO WHETHER OR NOT THE       *         
* OFFICE CODE EXISTS.                                                 *         
***********************************************************************         
         SPACE 1                                                                
INTVAL   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         SR    R0,R0                                                            
         ICM   R0,3,OFFAWORK                                                    
         BZ    ERROFF                                                           
         LA    R1,OFFAWORK+2       TEST OFFICE IS IN USER ACCESS LIST           
INTVAL2  CLC   OFFAOFFC,0(R1)      MATCH OFFICE TO LIMIT ACCESS LIST            
         BE    INTVALX                                                          
         LA    R1,L'OFFKOFF(R1)    BUMP TO NEXT OFFICE                          
         BCT   R0,INTVAL2          DO FOR NUMBER OF LIST ENTRIES                
         B     ERROFF                                                           
*                                                                               
INTVALX  B     OFFSET                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST OFFICE LOCKOUT AT TRANSACTION LEVEL                 *         
***********************************************************************         
         SPACE 1                                                                
INTTST   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         ICM   R2,15,OFFAREC                                                    
         USING ACTRECD,R2                                                       
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BNE   INTTST2                                                          
*                                                                               
         CLC   LDGPROD,ACTKUNT     TEST PRODUCTION LEDGER                       
         BE    INTPST              YES-APPLY CHECK AT ACCOUNT LEVEL             
         CLI   OFFAOPOS,LDGONKLO   TEST NEW OFFICE IN KEY LEDGER                
         BL    OFFSET              NO                                           
         CLI   OFFAOPOS,LDGONKHI                                                
         BH    OFFSET                                                           
         IC    R1,OFFAOPOS                                                      
         N     R1,=F'15'           ISOLATE INDEX INTO KEY                       
         LA    R1,ACTKLDG(R1)      INDEX TO OFFICE POSITION                     
         MVC   OFFAOFFC,0(R1)      EXTRACT OFFICE CODE                          
         CLC   OFFAOFFC,SPACES     TEST HIGH LEVEL ACCOUNT                      
         BE    OFFSET              YES-SKIP CHECK                               
         B     INTPST              NO-APPLY CHECK                               
*                                                                               
         USING OFARECD,R2                                                       
INTTST2  CLC   OFAKEY+OFAKEND(L'OFAKEY-OFAKEND),SPACES                          
         BNE   INTTST4             NOT AN OFFICE-ACCOUNT RECORD                 
*                                                                               
         CLI   OFFAOPOS,LDGONKLO   TEST NEW OFFICE IN KEY LEDGER                
         BL    *+12                                                             
         CLI   OFFAOPOS,LDGONKHI                                                
         BNH   OFFSET              YES-CHECK IS AT ACCOUNT LEVEL                
         MVC   OFFAOFFC,OFAKOFF    EXTRACT OFFICE FROM KEY                      
         B     INTPST              PERFORM CHECK                                
*                                                                               
         USING TRNRECD,R2                                                       
INTTST4  OC    TRNKDATE,TRNKDATE                                                
         BZ    OFFSET                                                           
         CLC   TRNKDATE,SPACES                                                  
         BE    OFFSET                                                           
         CLC   LDGPROD,TRNKUNT     NO OFFICE TEST FOR PRODUCTION TRANS          
         BE    OFFSET                                                           
         CLI   OFFAOPOS,LDGONKLO   OR NEW OFFICE IN KEY LEDGER                  
         BL    *+12                                                             
         CLI   OFFAOPOS,LDGONKHI                                                
         BNH   OFFSET                                                           
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R4,TRNRECD                                                       
         AH    R4,DATADISP                                                      
         USING TRNELD,R4                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION ELEMENT                 
         BNE   OFFSET                                                           
         MVC   OFFAOFFC,TRNANAL    EXTRACT OFFICE CODE                          
         TM    OFFACTRL,OFFACCNV   TEST NEW RECORD PASSED                       
         BZ    INTTST6                                                          
         TM    TRNRSTA2,TRNSPEEL   YES - TEST NEW STATUS BIT                    
         BZ    *+8                                                              
         OI    OFFASTAT,OFFASCLS                                                
         B     INTTSTX                                                          
*                                                                               
         USING ACKEYD,R2                                                        
INTTST6  OC    ACDTPEEL,ACDTPEEL   TEST OLD PEELED DATE                         
         BZ    *+8                                                              
         OI    OFFASTAT,OFFASCLS                                                
*                                                                               
INTTSTX  B     INTPST              GO TO POSTING TEST ROUTINE                   
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* TEST REQUESTED OFFICE INTERSECTS WITH LIMIT ACCESS OFFICE (LIST)    *         
***********************************************************************         
         SPACE 1                                                                
INTREQ   TM    OFFAINDS,OFFAIINI   TEST BLOCK INITIALISED                       
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION SEQUENCE                      
         CLC   OFFAOFFC,SPACES     TEST NO OFFICE FILTER SUPPLIED               
         BE    INTREQ20                                                         
         TM    OFFAINDS,OFFAIUOL   USER OFFICE LIST SUPPLIED IN                 
         BZ    INTREQ1                                                          
         ICM   R1,15,OFFAUSRL      USER PASSED ELEMENT OFFICE LIST              
         BZ    ERROFF                                                           
         SR    R0,R0                                                            
         B     INTREQ4                                                          
*                                                                               
INTREQ1  LA    R2,IO               READ LIMIT ACCESS OFFICE RECORD              
         USING OFFRECD,R2                                                       
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,OFFACPY                                                  
         MVC   OFFKOFF,OFFAOFFC    REQUESTED OFFICE PASSED IN OFFAOFFC          
         GOTO1 VDATAMGR,PARM,DMREAD,ACCFIL,OFFRECD,OFFRECD                      
         BNE   ERROFF                                                           
         SR    RF,RF               RF=N'ENTRIES IN LIST                         
         SR    R7,R7               R7=ZERO OR ADDRESS OF FIRST D2               
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BNZ   INTREQ2                                                          
         LA    R0,1                                                             
         LA    R1,OFFKOFF                                                       
         B     INTREQ6                                                          
*                                                                               
INTREQ2  LA    R1,OFFRECD+(ACRECORD-ACKEYD)                                     
*                                                                               
         USING OFLELD,R1                                                        
         SR    R0,R0                                                            
INTREQ4  CLI   OFLEL,0             TEST E-O-R                                   
         BNE   INTREQ4C                                                         
         LTR   RF,RF               NO ENTRY?                                    
         BNZ   INTREQ14            AT LEAST ONE ENTRY                           
         LTR   R7,R7               FOUND A D2 ELEMENT?                          
         BNZ   ERRSEC              YES, SEC ERROR                               
         DC    H'0'                BAD RECORD                                   
INTREQ4C CLI   OFLEL,OFLELQ        TEST OFFICE LIST ELEMENT                     
         BE    INTREQ5                                                          
INTREQ4X IC    R0,OFLLN                                                         
         AR    R1,R0                                                            
         B     INTREQ4                                                          
*                                                                               
INTREQ5  LR    R5,R1               SAVE FOR LATER                               
         LR    R7,R1                                                            
         IC    R0,OFLLN                                                         
         SH    R0,=Y(OFLLN1Q)                                                   
         SRL   R0,1                                                             
         LA    R1,OFLNTRY                                                       
*                                                                               
INTREQ6  ICM   RE,15,OFFAREQL                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RE,2(,RE)           RE=A(OUTPUT OFFICE LIST)                     
         LR    R4,RF               FIND POSITION                                
         SLL   R4,1                                                             
         AR    RE,R4                                                            
         STM   R0,R1,DUB                                                        
         TM    OFFAINDS,OFFAIXOL   TEST 'EXCLUDE' OFFICE                        
         BO    INTREQ15                                                         
*                                                                               
INTREQ8  LA    R3,OFFAWORK+2       BUILD A LIST OF OFFICES THAT MATCH           
         SR    R4,R4               BETWEEN THE LIMIT ACCESS LIST AND            
         ICM   R4,3,OFFAWORK       THE REQUESTED OFFICE LIST                    
INTREQ10 CLC   0(L'OFFKOFF,R3),0(R1)                                            
         BNE   INTREQ12                                                         
         MVC   0(L'OFFKOFF,RE),0(R3)                                            
         LA    RE,L'OFFKOFF(,RE)                                                
         LA    RF,1(,RF)                                                        
INTREQ12 LA    R3,L'OFFKOFF(,R3)   BUMP TO NEXT LIMIT ACCESS OFFICE             
         BCT   R4,INTREQ10                                                      
         LA    R1,L'OFFKOFF(,R1)   BUMP TO NEXT REQUESTED OFFICE                
         BCT   R0,INTREQ8                                                       
*                                                                               
         TM    OFFRECD+(ACSTATUS-ACKEYD),OFFSLIST                               
         BZ    INTREQ14                                                         
         LR    R1,R5                                                            
         B     INTREQ4X            FIND MORE OFLEL'S                            
*                                                                               
INTREQ14 LTR   RF,RF               ERROR IF NO MATCHING OFFICES                 
         BZ    ERRSEC                                                           
         L     RE,OFFAREQL                                                      
         STCM  RF,3,0(RE)          SET N'OFFICES AT START OF LIST               
         B     OFFSET                                                           
*                                                                               
*                                  EXCLUDE LIST                                 
INTREQ15 LA    R3,OFFAWORK+2       BUILD A LIST OF OFFICES THAT ARE             
         SR    R4,R4               IN LIMITED ACCESS LIST AND NOT IN            
         ICM   R4,3,OFFAWORK       EXLCUDE OFFICE LIST                          
INTREQ16 CLC   0(L'OFFKOFF,R3),0(R1)                                            
         BE    INTREQ17                                                         
         LA    R1,L'OFFKOFF(,R1)   BUMP TO NEXT EXCLUDE OFFICE                  
         BCT   R0,INTREQ16                                                      
         MVC   0(L'OFFKOFF,RE),0(R3)  ADD TO 'OK' LIST                          
         LA    RE,L'OFFKOFF(,RE)                                                
         LA    RF,1(,RF)                                                        
*                                                                               
INTREQ17 LA    R3,L'OFFKOFF(,R3)   BUMP TO NEXT LIMIT ACCESS OFFICE             
         LM    R0,R1,DUB           R0=NUMBER IN LIST, R1=(LIST)                 
         BCT   R4,INTREQ16                                                      
         LTR   RF,RF               ERROR IF ALL EXCLUDED                        
         BZ    ERRSEC                                                           
         L     RE,OFFAREQL                                                      
         STCM  RF,3,0(RE)          SET N'OFFICES AT START OF LIST               
         B     OFFSET                                                           
*                                                                               
INTREQ20 SR    R1,R1                                                            
         ICM   R1,3,OFFAWORK       R1=N'LIMIT ACCESS LIST ENTRIES               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,1(,R1)                                                        
         SLL   R1,1                R1=L'ACCESS LIST+N'ENTRIES                   
         ICM   R0,15,OFFAREQL      R0=A(REQUEST LIST)                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RE,OFFAWORK                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY LIMIT ACCESS TO REQUEST LIST            
         B     OFFSET                                                           
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MANAGE CORE BUFFER (OFFLINE) OR CALLS TO TSAR (ONLINE)   *         
***********************************************************************         
         SPACE 1                                                                
BUFFER   TM    OFFAINDS,OFFAIONL   TEST RUNNING ONLINE                          
         BZ    BUFFOFL                                                          
*                                                                               
BUFFONL  GOTO1 OFFATSAR,TSARD      ONLINE - CALL TSAR                           
         B     OFFXIT              EXIT WITH TSAR CC INTACT                     
*                                                                               
BUFFOFL  MVI   TSERRS,0            SET NO ERRORS                                
         CLI   TSACTN,TSAINI       TEST INITIALISE                              
         BE    BUFFOF10                                                         
         CLI   TSACTN,TSAADD       TEST ADD A RECORD                            
         BE    BUFFOF20                                                         
         CLI   TSACTN,TSARDH       TEST READ HIGH                               
         BE    BUFFOF30                                                         
         CLI   TSACTN,TSASAV       TEST SAVE                                    
         BE    OFFXIT                                                           
         CLI   TSACTN,TSARES       TEST RESTORE                                 
         BE    OFFXIT                                                           
         DC    H'0'                INVALID ACTION CALL                          
*                                                                               
BUFFOF10 ICM   R0,15,OFFABUF       ** INITIALISE BUFFER **                      
         BNZ   *+6                                                              
         DC    H'0'                NO OFFLINE BUFFER PASSED                     
         ST    R0,OFFABUFX         SET A(ACTUAL END OF BUFFER)                  
         ICM   R1,15,OFFABUFL                                                   
         BNZ   *+6                                                              
         DC    H'0'                NO OFFLINE BUFFER LENGTH PASSED              
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR BUFFER TO BINARY ZEROES                
         B     BUFFOFLX                                                         
*                                                                               
BUFFOF20 L     RF,OFFABUF          ** ADD A BUFFER ENTRY **                     
         A     RF,OFFABUFL         RF=A(END OF BUFFER)                          
         L     R0,OFFABUFX                                                      
         SR    R1,R1                                                            
         ICM   R1,3,OFFBLEN                                                     
         BNZ   *+6                                                              
         DC    H'0'                R1=RECORD LENGTH                             
         LR    RE,R0                                                            
         AR    RE,R1               RE=A(NEW BUFFER END)                         
         CR    RE,RF                                                            
         BNH   *+12                                                             
         MVI   TSERRS,TSEEOF                                                    
         B     BUFFOFLX            OFFLINE BUFFER IS FULL                       
         ST    RE,OFFABUFX                                                      
         LA    RE,OFFBREC                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD FROM W/S TO BUFFER               
         B     BUFFOFLX                                                         
*                                                                               
BUFFOF30 L     R1,OFFABUF          ** LOCATE A BUFFER ENTRY **                  
         SR    RE,RE                                                            
         L     RF,OFFABUFX                                                      
         BCTR  RF,0                RF=A(END OF BUFFER-1)                        
         CLC   OFFBCODE,OFFBCODE-OFFBREC(R1)                                    
         BNH   BUFFOF32                                                         
         ICM   RE,3,0(R1)          RE=LENGTH OF THIS RECORD                     
         BXLE  R1,RE,*-14                                                       
         MVI   TSERRS,TSEEOF       AT E-O-B SET E-O-F                           
         B     BUFFOFLX                                                         
*                                                                               
BUFFOF32 BE    *+8                 EQUAL MEANS RECORD FOUND IN BUFFER           
         MVI   TSERRS,TSERNF       ELSE SET RECORD NOT FOUND                    
         LR    RE,R1               RE=A(RECORD IN BUFFER)                       
         SR    RF,RF                                                            
         ICM   RF,3,0(R1)          RF=RECORD LENGTH                             
         LA    R0,OFFBREC          R0=A(WORKING STORAGE RECORD)                 
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE RECORD TO W/S FROM BUFFER               
*                                                                               
BUFFOFLX CLI   TSERRS,0            SET CC FOR CALLER                            
         B     OFFXIT                                                           
         EJECT                                                                  
***********************************************************************         
* EXITS FROM MODULE                                                   *         
***********************************************************************         
         SPACE 1                                                                
ERREOF   MVI   OFFAERR,OFFAEEOF    END OF FILE/BUFFER                           
         B     OFFSET                                                           
ERRDSK   MVI   OFFAERR,OFFAEDSK    DISK ERROR                                   
         B     OFFSET                                                           
ERRSEC   MVI   OFFAERR,OFFAESEC    SECURITY LOCKOUT                             
         B     OFFSET                                                           
ERROFF   MVI   OFFAERR,OFFAEOFF    INVALID OFFICE                               
         B     OFFSET                                                           
         SPACE 2                                                                
OFFSET   CLI   OFFAERR,0           SET CC FOR CALLER                            
OFFXIT   XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
DATAMGR  DC    V(DATAMGR)          RESOLVED IF OFFLINE                          
         SPACE 1                                                                
LDGPROD  DC    C'SJ'               PRODUCTION UNIT/LEDGER                       
         SPACE 1                                                                
ACCFIL   DC    C'ACCOUNT '                                                      
CTFILE   DC    C'CTFILE  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMDTFA   DC    C'DTFAD   '                                                      
         SPACE 1                                                                
FILDISP  DS    0AL1                DISPLACEMENTS TO ACCOUNT FILTERS             
         DC    AL1(RSTFILT1-RSTELD)                                             
         DC    AL1(RSTFILT2-RSTELD)                                             
         DC    AL1(RSTFILT3-RSTELD)                                             
         DC    AL1(RSTFILT4-RSTELD)                                             
         DC    AL1(RSTFILT5-RSTELD)                                             
         EJECT                                                                  
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
DUB      DS    D                                                                
PARM     DS    6F                                                               
WORK     DS    XL8                                                              
SPACES   DS    XL64                                                             
VDATAMGR DS    V                                                                
VCALLOV  DS    V                                                                
ABUF     DS    A                   A(6K BUFFER)                                 
DATADISP DS    H                   DISPLACEMENT TO FIRST ELEMENT                
REQLISTN DS    XL1                 NUMBER OF ENTRIES IN REQLIST                 
REQLIST  DS    256CL2              REQUEST LIST                                 
REQLISTL EQU   *-REQLIST           LENGTH OF REQUEST LIST                       
LIMLISTN DS    XL1                 NUMBER OF ENTRIES IN LIMLIST                 
LIMLIST  DS    256CL2              LIMIT ACCESS LIST                            
LIMLISTL EQU   *-LIMLIST           LENGTH OF LIMIT ACCESS LIST                  
OFFBREC  DS    0X                  ** OFFICE BUFFER RECORD/ENTRY **             
       ++INCLUDE ACOFFBUFF                                                      
KEY      DS    XL64                                                             
IO       DS    XL2048                                                           
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DDTSARD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDTSARD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
         EJECT                                                                  
       ++INCLUDE ACOFFALD                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025ACOFFAL   06/26/19'                                      
         END                                                                    
