*          DATA SET DDUNSCAN   AT LEVEL 003 AS OF 04/23/02                      
*CATALP UNSCAN                                                                  
         TITLE 'MODULE TO DISPLAY STRING OF DATA FROM BLOCK'                    
UNSCAN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 UNSCANX-UNSCAND,**UNSCAN                                         
         USING UNSCAND,RC                                                       
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
         LM    R2,R3,0(R1)         P1 AL1(ITEMS) AL3(BLOCK)                     
         LR    R9,R1               P2 CL1'C'     AL3(CARD)  OR                  
*                                                AL4(SCREEN HEADER)             
         MVI   LLEFT,10                                                         
         LA    R4,12(R9)           P4=OPTIONAL LEFT-HAND SIDE LENGTH            
         CLC   1(3,R4),=C'$LT'     MUST CONTAIN SPECIAL CHARACTERS              
         BNE   UNB                                                              
         CLI   0(R4),10                                                         
         BL    UNB                                                              
         CLI   0(R4),60                                                         
         BH    UNB                                                              
         MVC   LLEFT,0(R4)                                                      
         SPACE 2                                                                
UNB      CTRY  ,                   R0=X'OOAACCLL'                               
         STCM  R0,15,AGCOPT                                                     
         MVC   COMMA(2),COMAEQLS   SET DEFAULT TO ,=                            
         CLC   8(2,R9),COMAEQLS                                                 
         BNE   UN2                                                              
         MVC   COMMA(2),10(R9)     OVERRIDE SPECIAL CHARACTERS                  
         CLC   COMMA(2),=C'  '     TEST IF CALLER WANTS LANG DEFAULTS           
         BNE   UN2                 NO                                           
         SR    RF,RF               YES GET LANGUAGE CODE                        
         IC    RF,LANG                                                          
         CH    RF,=H'7'                                                         
         BNH   *+6                                                              
         SR    RF,RF                                                            
         SLL   RF,1                                                             
         LA    RF,COMAEQLS(RF)     INDEX INTO COMMA EQUALS TABLE                
         MVC   COMMA(2),0(RF)                                                   
         SPACE 2                                                                
UN2      LA    R4,80               SET MAXIMUM LENGTH                           
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVI   LRIGHT,10                                                        
         CLI   4(R9),C'C'          TO 80 FOR CARDS                              
         BE    UN4                                                              
         CLI   4(R9),10                                                         
         BL    UN1                                                              
         CLI   4(R9),60                                                         
         BH    UN1                                                              
         MVC   LRIGHT,4(R9)                                                     
         SPACE 2                                                                
UN1      IC    R4,0(R3)            OTHERWISE DIG OUT FIELD LENGTH               
         SH    R4,=H'8'                                                         
         TM    1(R3),X'02'         TEST FOR EXTENDED HEADER                     
         BZ    *+8                                                              
         SH    R4,=H'8'                                                         
         LA    R3,8(R3)            AND BUMP POINTER TO START OF DATA            
         EJECT                                                                  
*NOW OUTPUT THE DATA                                                            
*                                                                               
UN4      LR    R5,R4               FIRST CLEAR THE FIELD                        
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),SPACES                                                   
         MVI   FIRSTSW,C'Y'                                                     
         SPACE 2                                                                
UN6      CLI   0(R9),0             TEST ANY ENTRIES                             
         BE    XIT                 NONE                                         
         LA    R5,AREA                                                          
         MVC   AREA,SPACES         OUTPUT A FIELD INTO AREA                     
         CLI   FIRSTSW,C'Y'                                                     
         BE    *+14                                                             
         MVC   AREA(1),COMMA       PRECEDE IT BY COMMA IF NOT FIRST             
         LA    R5,1(R5)                                                         
         ZIC   RF,LLEFT                                                         
         BCTR  RF,0                                                             
         EXMVC RF,0(R5),0(R2)                                                   
         MVI   FIRSTSW,C'N'                                                     
         EXCLC RF,0(R2),SPACES                                                  
         BE    UN16                                                             
         AR    R5,RF                                                            
         SPACE 2                                                                
UN8      CLI   0(R5),C' '          FIND END OF STRING                           
         BH    UN10                                                             
         BCT   R5,UN8                                                           
         SPACE 2                                                                
UN10     LA    R6,1(RF,R2)         R6=SECOND CHUNK                              
         CLC   0(10,R6),SPACES     IS IT A DOUBLE OPERAND                       
         BE    UN14                                                             
         MVC   1(1,R5),EQUALS      YES - SPLIT WITH =                           
         ZIC   RF,LRIGHT                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   2(0,R5),0(R6)       OUTPUT SECOND CHUNK                          
         LA    R5,2(RF,R5)                                                      
         SPACE 2                                                                
UN12     CLI   0(R5),C' '          FIND END OF THIS STRING                      
         BH    UN14                                                             
         BCT   R5,UN12                                                          
         SPACE 2                                                                
UN14     LA    R6,AREA             WORK OUT LENGTH OF FIELD                     
         SLL   R6,8                                                             
         SRL   R6,8                                                             
         SR    R5,R6                                                            
         CR    R5,R4               WILL IT FIT                                  
         BNL   XIT                 NO - WE'RE DONE                              
         SR    R4,R5               YES - ADJUST REMAINING SPACE                 
         BCTR  R4,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),AREA        MOVE IN THIS FIELD                           
         LA    R3,1(R5,R3)                                                      
         SPACE 2                                                                
UN16     ZIC   RF,LLEFT                                                         
         AR    R2,RF                                                            
         ZIC   RF,LRIGHT                                                        
         AR    R2,RF                                                            
         ZIC   R5,0(R9)                                                         
         ST    R2,0(R9)            ADJUST PARAMETER LIST - A(NEXT)              
         BCTR  R5,0                                                             
         STC   R5,0(R9)            N(REMAINING LINES)                           
         B     UN6                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLERS ADDRESSING MODE              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
COMAEQLS DC    C',=,=,=#=;=;=;=;='                                              
         SPACE 2                                                                
UNSCAND  DSECT                                                                  
AREA     DS    CL80                                                             
SPACES   DS    CL80                                                             
FIRSTSW  DS    CL1                                                              
COMMA    DS    CL1                                                              
EQUALS   DS    CL1                                                              
LRIGHT   DS    CL1                                                              
LLEFT    DS    CL1                                                              
*                                                                               
AGCOPT   DS    CL1                                                              
AGCTRY   DS    CL1                                                              
CTRY     DS    CL1                                                              
LANG     DS    CL1                                                              
*                                                                               
UNSCANX  EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDUNSCAN  04/23/02'                                      
         END                                                                    
