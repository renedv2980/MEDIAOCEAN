*          DATA SET CXSUPCXU   AT LEVEL 003 AS OF 05/01/02                      
*CATALP CXSUPX                                                                  
         TITLE 'CXSUPX - CONTROL SEC. EXTRACT- SQL USERID PROGRAM'              
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSUPX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSUPX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING CXSUPD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   CXSUPACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
UXTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSUPTYP,CXSUPTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSUPACT,CXSUPACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'CXSUPDAT+L'CXSUPTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),CXSUPDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXNUM    NI    XFLAG,REQUIRED      CONVERT USER ID NUMBER - REQUIRED            
         GOTO1 XMXCNVX,DMCB,(L'CXSUPNUM,CXSUPNUM),(XFLAG,(R3)),(1,INT)          
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXSYS    NI    XFLAG,REQUIRED      CONVERT SYSTEM CODE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSUPSYS,CXSUPSYS),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXPGM    NI    XFLAG,REQUIRED      CONVERT PROGRAM CODE - REQUIRED              
         GOTO1 XMXCNVX,DMCB,(L'CXSUPPGM,CXSUPPGM),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXLAC    NI    XFLAG,REQUIRED      CONVERT LIMIT ACCESS CODE - REQUIRED         
         GOTO1 XMXCNVX,DMCB,(L'CXSUPLAC,CXSUPLAC),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
UXAUT    NI    XFLAG,REQUIRED      CONVERT AUTH. CODE - REQUIRED                
         GOTO1 XMXCNVX,DMCB,(L'CXSUPAUT,CXSUPAUT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
DXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
SMAL     DC    CL8'SMALLINT'                                                    
INT      DC    CL8'INT     '                                                    
REAL     DC    CL8'REAL    '                                                    
FLOT     DC    CL8'FLOAT   '                                                    
BIT      DC    CL8'BIT     '                                                    
MONY     DC    CL8'MONEY   '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C';'                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSUPD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003CXSUPCXU  05/01/02'                                      
         END                                                                    
