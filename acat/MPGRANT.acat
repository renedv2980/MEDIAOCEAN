*          DATA SET MPGRANT    AT LEVEL 011 AS OF 11/25/85                      
*CATALP MPGRANT                                                                 
         TITLE 'MPGRANT - CREATE DUMMY VALUES IN DATA VECTOR'                   
         PRINT NOGEN                                                            
MPGRANT  CSECT                                                                  
         SPACE 2                                                                
**********************************************************************          
*                                                                    *          
* MPGRANT :- CREATES VALUES IN DATA VECTORS                          *          
*            IT ONLY GIVES VALUES FOR 'RAW DATA' ENTRIES IN THE      *          
*            PROCESSING TABLE, AND ONLY SOME OF THOSE.               *          
*            IT STARTS AT A CERTAIN VALUE, WITH A CERTAIN INCREMENT  *          
*        PARAM 1 - DATA VECTOR TABLE ADDRESS                         *          
*        PARAM 2 - PROCESSING COMMAND DSECT ADDRESS                  *          
*                                                                    *          
**********************************************************************          
         SPACE 3                                                                
         NMOD1 0,MPGRANT                                                        
         SPACE 3                                                                
         EJECT                                                                  
         L     R3,0(,R1)           DATA VECTOR ADDRESS                          
         USING VTABD,R3                                                         
         NC    VTLEN,VTLEN         ANY VECTORS AT ALL ?                         
         BZ    A110                NO !!!!!!!                                   
         L     R2,4(,R1)           PROCESSING COMMAND DSECT ADDRESS             
         USING PCMDD,R2                                                         
         LA    R5,500              BASE VALUE FOR IMPRESSIONS                   
         LA    R6,800              BASE VALUE FOR REACH                         
         LA    R8,400              BASE VALUE FOR VRDR                          
         LA    R9,750              BASE VALUE FOR AUD                           
A010     DS    0H                                                               
         CLI   PCTYP,PCTYPG        GLOBAL VALUE ?                               
         BE    A100                YES - IGNORE                                 
         CLI   PCQUAL,1            RAW NUMBER ?                                 
         BNE   A100                NO - GET NEXT PROCTAB ENTRY                  
         CLI   PCFHOW,PCOSUBR      A SUBR TO INVOKE ???                         
         BE    A100                                                             
A020     DS    0H                                                               
         CLI   PCBASE,1            IMPRESSIONS ?                                
         BE    A050                YES - CALC IMP VALUE                         
         CLI   PCBASE,3            REACH ?                                      
         BE    A040                YES - CALC RCH VALUE                         
         CLI   PCBASE,13           VRDR   ?                                     
         BE    A060                YES - CALC VRDR VALUE                        
         CLI   PCBASE,14           TARGET ?                                     
         BE    A030                YES - CALC TARG VALUE                        
         CLI   PCBASE,15           AUD    ?                                     
         BE    A070                YES - GO                                     
         CLI   PCBASE,18           SUM    ?                                     
         BE    A072                YES - GO                                     
         CLI   PCBASE,19           NET    ?                                     
         BE    A074                YES - GO                                     
         CLI   PCBASE,20           DUP    ?                                     
         BE    A076                YES - GO                                     
         B     A100                                                             
A030     DS    0H                                                               
         LA    R4,1000             SET TARG VALUE                               
         B     A080                STUFF IN VECTOR                              
A040     DS    0H                                                               
         LR    R4,R6               SET RCH VALUE                                
         SR    R0,R0                                                            
         ICM   R0,3,PCSFX                                                       
         AR    R4,R0               STICK SUFFIX ON THE REACH                    
         LA    R6,10(,R6)          UPDATE FOR NEXT TIME                         
         B     A080                STUFF IN VECTOR                              
A050     DS    0H                                                               
         LR    R4,R5               SET IMP VALUE                                
         LA    R5,25(,R5)          UPDATE FOR NEXT TIME                         
         B     A080                                                             
A060     DS    0H                                                               
         LR    R4,R8               SET VRDR VALUE                               
         LA    R8,40(,R8)          UPDATE FOR NEXT TIME                         
         B     A080                                                             
A070     DS    0H                                                               
         LR    R4,R9               SET AUD VALUE                                
         LA    R9,20(,R9)          UPDATE FOR NEXT TIME                         
         B     A080                                                             
A072     DS    0H                                                               
         LA    R6,10(,R6)                                                       
         LR    R4,R6               SUM = RCH * 2                                
         SLL   R4,1                                                             
         B     A080                                                             
A074     DS    0H                                                               
         LA    R6,10(,R6)                                                       
         LR    R4,R6               NET = RCH * 1 1/2                            
         SLL   R4,1                                                             
         AR    R4,R6                                                            
         SRL   R4,1                                                             
         B     A080                                                             
A076     DS    0H                                                               
         LA    R6,10(,R6)                                                       
         LR    R4,R6               DUP = RCH * 1/2                              
         SRL   R4,1                                                             
         B     A080                                                             
A080     DS    0H                                                               
         ICM   R7,15,PCWHERE       GET OUR DISPLACEMENT                         
         AR    R7,R3               POINT TO SPOT IN VECTOR                      
*   W   W H   H IIIII PPPP   SSS                                                
*   W   W H   H   I   P   P SS     PREPARE FOR FLAGGELATION                     
*   W   W HHHHH   I   PPPP   SSS                                                
*   W W W H   H   I   P        SS  PREPARE FOR FLAGGELATION                     
*    W W  H   H IIIII P      SSS                                                
         ST    R4,0(,R7)           STUFF VALUE IN VECTOR                        
         AH    R3,VTLEN            POINT TO NEXT VECTOR                         
         NC    VTLEN,VTLEN         ANY MORE ?                                   
         BNZ   A020                YES - DO NEXT                                
A100     DS    0H                                                               
         L     R3,0(,R1)           START AT BEGINNING OF VECTOR AGAIN           
         LA    R2,PCLEN(,R2)       POINT TO NEXT PROC TABLE ENTRY               
         CLI   PCLNTH,0            ANY MORE ?                                   
         BE    A110                NO - THAT'S IT THEN CHUMMY                   
         B     A010                YES - TRY NEXT PROCTAB ENTRY                 
A110     DS    0H                                                               
         XMOD1 1                                                                
DUB      DS    D                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
       ++INCLUDE MPVTABD                                                        
         SPACE 2                                                                
       ++INCLUDE MPPCMDD                                                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011MPGRANT   11/25/85'                                      
         END                                                                    
