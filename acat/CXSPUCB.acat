*          DATA SET CXSPUCB    AT LEVEL 019 AS OF 05/01/02                      
*CATALP CXSPUCB                                                                 
         TITLE 'CXSPUC - CONTROL SECURITY - PERSONID USERID XREF'               
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSPUC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSPUC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSPUD,R2           R2=A(EXTRACT RECORD)                         
         USING SA0REC,R3           R3=A(PERSON PASSWORD RECORD)                 
*                                                                               
CXSLEN   XC    CXSPULEN,CXSPULEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSPULEN(2),=AL2(CXSPUDL)                                        
         MVC   CXSPUTYP,CXSPUDQ                                                 
         MVI   CXSPUDX,MXTRTQ                                                   
         MVI   CXSPUDX+1,MXTRTQ                                                 
*                                                                               
CXSAID   MVI   CXSPUAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSPUAID,SA0KAGY                                                 
*                                                                               
CXSPEN   MVI   CXSPUPEN-1,MXTRTQ   PERSON ID NUMBER                             
         SR    RF,RF                                                            
         ICM   RF,3,SA0KNUM                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSPUPEN,DUB+5(3)                                                
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   CXSPUACT,C'A'       GOTO ADD PROCESS                             
         B     CXSAGU                                                           
CXSACT1  CLI   CXSPUACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPUACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSPUACT,C'A'       IF ADD DO 'K' DELETE ALL                     
         BE    CXSACT2                                                          
         B     CXSAGU              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSPUACT   SAVE THIS ACTION                             
         MVI   CXSPUACT,C'K'       OVERIDE WITH 'K' DELETE ALL TVRS             
         MVI   CXSPUUIN-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         MVI   CXSPUACV-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         CLI   SAVEACTN,C'D'                                                    
         BNE   RETEXIT             IF NOYT DELETE RETURN FOR ADDS               
         B     EOFEXIT             ELSE EXIT WITH JUST DELETE                   
*                                                                               
*                                                                               
         USING AGUTABD,R4                                                       
CXSAGU   ICM   R4,15,AAGUUID       R4=A(LAST USERID IN AGUTAB)                  
         BNZ   CAGU020               UNLESS FIRST TIME?                         
         ICM   R4,15,=V(CXSAGUT)   R4=A(AGENCY USERID TABLE)                    
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CAGU010  CLI   AGUAGYID,0          GET ADDRESS SPATAB ENTRY FOR                 
         BNE   *+6                   SYSTEM PROGRAM CODE IN RECORD              
         DC    H'00'                                                            
         CLC   AGUAGYID,SA0KAGY                                                 
         BE    *+12                                                             
         LA    R4,AGUTLEN(R4)                                                   
         B     CAGU010                                                          
         BAS   RE,MERGPAGU                                                      
         LA    R4,AGUBUFF                                                       
         LA    R4,AGUUID                                                        
         ST    R4,AAGUUID                                                       
         B     CAGU030                                                          
*                                                                               
         USING AGUUID,R4                                                        
CAGU020  LA    R4,AGUULEN(R4)                                                   
         ST    R4,AAGUUID                                                       
*                                                                               
CAGU030  EQU   *                                                                
         CLI   AGUUID,0                                                         
         BE    EOFEXIT                                                          
         CLI   AGUVAL,X'FF'        ACCESS VALUE = REMOVED USER ID               
         BE    CAGU020                                                          
         CLI   AGUVAL,C'N'         TEMP FIX TO IGNORE 'N' ACCESS                
         BE    CAGU020                                                          
*                                                                               
CXSUIN   MVI   CXSPUUIN-1,MXTRTQ   USER ID NUMBER                               
         SR    RF,RF                                                            
         ICM   RF,3,AGUUIN                                                      
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSPUUIN,DUB+5(3)                                                
*                                                                               
CXSVAL   MVI   CXSPUACV-1,MXTRTQ                                                
         MVC   CXSPUACV,AGUVAL     SAVE ACCESS VALUE                            
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    AAGUUID,AAGUUID                                                  
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         OI    8(R1),X'01'         SET READ SEQUENCE BROKEN                     
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MERGE PERSON RECORD USERIDS WITH AGENCY USERID BLOCK     *         
* R3=A(PASSWORD RECORD)                                               *         
* R4=A(AGENCY USER ID ACCESS TABLE ENTRY)                             *         
***********************************************************************         
         SPACE 1                                                                
MERGPAGU NTR1  ,                                                                
         LA    RE,AGUBUFF          INITIALISE AGENCY USERID BLOCK               
         LA    RF,AGUTLEN                                                       
         LR    R0,R4                                                            
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
*                                                                               
         GOTO1 XGETIDS,DMCB,(C'C',(R3)),0,XDATAMGR                              
         CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'                                                            
         CLI   0(R1),0                                                          
         BE    MPAGALL                                                          
         L     R5,4(R1)            R5=A(COMPATIBLE ID LIST)                     
*                                                                               
MPAG010  CLI   0(R5),X'FF'         TEST END OF LIST                             
         BE    MPAGX                                                            
         CLC   0(10,R5),=C'ALL'                                                 
         BE    MPAGALL                                                          
         MVC   USERID,0(R5)                                                     
         BAS   RE,MERGUID                                                       
*                                                                               
MPAG020  LA    R5,12(R5)           BUMP R5 TO NEXT ENTRY                        
         B     MPAG010                                                          
*                                                                               
MPAGALL  MVC   USERID,=C'ALL'                                                   
         BAS   RE,MERGUID                                                       
         B     MPAGX                                                            
*                                                                               
MPAGX    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MERGE USERID INTO PERSON AGENCY USERID BLOCK                       
***********************************************************************         
         SPACE 1                                                                
MERGUID  NTR1  ,                                                                
         USING AGUTABD,R4                                                       
         LA    R4,AGUBUFF          R4=A(AGENCY USERID CONTROL BLOCK)            
         LA    R6,AGUUID                                                        
*                                                                               
         USING AGUUID,R6                                                        
MUID010  CLI   AGUUID,0                                                         
         BE    MUID100                                                          
         CLC   USERID,=C'ALL'                                                   
         BNE   MUID020                                                          
         CLI   AGUVAL,X'FF'                                                     
         BE    MUID030                                                          
         MVI   AGUVAL,C'Y'                                                      
         B     MUID030                                                          
MUID020  CLC   AGUUID,USERID                                                    
         BNE   MUID030                                                          
         CLI   AGUVAL,X'FF'                                                     
         BE    MUIDOK                                                           
         MVI   AGUVAL,C'Y'                                                      
         B     MUIDOK                                                           
MUID030  LA    R6,AGUULEN(R6)                                                   
         B     MUID010                                                          
*                                                                               
MUID100  CLC   USERID,=C'ALL'                                                   
         BE    MUIDOK                                                           
         B     MUIDNO           ?? NON AGENCY IDS ??                            
*                                                                               
MUIDOK   SR    RC,RC                                                            
MUIDNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4,R6                                                            
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
AAGUUID  DC    A(0)                                                             
XHEXOUT  DC    V(HEXOUT)                                                        
XDATAMGR DC    V(DATAMGR)                                                       
XGETIDS  DC    V(GETIDS)                                                        
DMREAD   DC    C'DMREAD '                                                       
CTFILE   DC    C'CTFILE '                                                       
SAVEACTN DC    CL1' '                                                           
         SPACE 1                                                                
*                                                                               
         DS    0D                                                               
AGUBUFF  DC    (AGUTLEN)X'00'                                                   
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                  SUBROUTINE PARAMETER LIST                    
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
USERID   DS    XL(L'SAIKID)                                                     
IOKEY    DS    CL25                                                             
IOL      DS    F                                                                
IO       DS    2048X                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSPUD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019CXSPUCB   05/01/02'                                      
         END                                                                    
