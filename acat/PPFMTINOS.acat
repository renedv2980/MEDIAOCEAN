*          DATA SET PPFMTINOS  AT LEVEL 005 AS OF 09/10/97                      
*CATALP PPFMTINO                                                                
         TITLE 'PPFMTINO - FORMAT PRINT INVOICE NUMBERS'                        
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*                                                                               
*   INPUT                                                                       
*   -----                                                                       
*                                                                               
*   P1 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(DATE)      YYMMDD (TODAY OR DATE OF BILL)                  
*                                                                               
*   P2 BYTE   0    2 MEANS INV NO. IS XL2                                       
*      BYTES  1-3  A(INVOICE NUBMER)  CL4 OR XL2                                
*                                                                               
*   P3 BYTE   0    CL1 - MEDIA CODE                                             
*      BYTES  1-3  A(B1 PROFILE)                                                
*                                                                               
*   P4 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(B1X PROFILE)                                               
*                                                                               
*   OUTPUT   NOTE- PPFMTINO SETS THE ADDRESSES                                  
*   ------                                                                      
*                                                                               
*   P1 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(FULL INVOICE NUMBER) - ME-MN-NNNN OR MN-ME-NNNN             
*                                                                               
*   P2 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(SHORT INV NO.) - MN-NNNN                                    
*                                                                               
*   P3 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MONTH OR YEAR/MONTH) - 2 CHARACTERS                         
*                                                                               
*   P4 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MEDIA) -2 CHARACTERS                                        
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PPFMTINO CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**FMTI**                                                  
*                                                                               
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         MVC   PARMS(24),0(R1)                                                  
*                                                                               
*                                  FORMAT INVOICE NUMBER                        
FI00     DS    0H                                                               
         MVC   ILONG,=CL10' '      SET OUTPUT IN ILONG                          
         L     RF,PARM3            A(B1 PROF)                                   
         MVC   ILONG(2),2(RF)      MEDIA EXPANSION                              
*                                  *=BLANK, **=USE MEDIA CODE                   
         CLI   ILONG+0,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+0,C' '                                                     
         CLI   ILONG+1,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+1,C' '                                                     
         CLC   ILONG+0(2),=C'  '                                                
         BNE   *+10                                                             
         MVC   ILONG+0(1),PARM3                                                 
         MVC   INOMED,ILONG        SAVE MEDIA EXP                               
         MVI   ILONG+2,C'-'                                                     
*                                                                               
         L     R2,PARM1            A(DATE)                                      
         MVC   ILONG+3(2),2(R2)    MONTH                                        
*                                                                               
         L     R3,PARM4            A(B1X PROF)                                  
         CLI   5(R3),0             SEE IF INCREMENTING MONTH                    
         BE    FI03                                                             
         PACK  DUB,2(2,R2)                                                      
         CVB   R0,DUB                                                           
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
         CH    R0,=H'12'          SEE IF OVER 12                                
         BNH   FI02                                                             
         SH    R0,=H'12'                                                        
*                                                                               
FI02     DS    0H                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+3(2),DUB                                                   
*                                                                               
FI03     DS    0H                                                               
         OC    14(2,R3),14(R3)     YM MODE START YEAR/MONTH                     
         BZ    FI05                NO                                           
*                                                                               
         PACK  DUB,0(2,R2)         YEAR OF BILL DATE                            
         CVB   R0,DUB                                                           
         CLM   R0,1,14(R3)                                                      
         BL    FI05                                                             
         BH    FI04                                                             
         PACK  DUB,2(2,R2)         MONTH OF BILL DATE                           
         CVB   R0,DUB                                                           
         CLM   R0,1,15(R3)                                                      
         BL    FI05                                                             
*                                                                               
         CLI   5(R3),0             SEE IF INCREMENTING MONTH                    
         BE    FI04                                                             
         PACK  DUB,2(2,R2)                                                      
         CVB   R0,DUB                                                           
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
         CH    R0,=H'12'          SEE IF OVER 12                                
         BNH   FI03D                                                            
*                                                                               
         SH    R0,=H'12'                                                        
         PACK  DUB,0(2,R2)         BUMP YEAR                                    
         AP    DUB,=P'1'                                                        
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R2),DUB        SET ADJUSTED YEAR                             
*                                                                               
FI03D    CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  2(2,R2),DUB         SET ADJUSTED MTH                             
*                                                                               
*                                                                               
FI04     DS    0H                  ELSE YEAR AS 1-9 AND MONTH AS 1-C            
         MVC   ILONG+3(1),1(R2)    2ND DIGIT OF YEAR                            
         PACK  DUB,2(2,R2)         MONTH                                        
         CVB   RE,DUB                                                           
         LA    RE,MTHTAB-1(RE)                                                  
         MVC   ILONG+4(1),0(RE)                                                 
         B     FI08                                                             
*                                                                               
FI05     DS    0H                                                               
         SR    RF,RF                                                            
         ICM   RF,1,4(R3)          INVOICE NUMBER BASE YEAR                     
         BZ    FI08                NONE                                         
*                                                                               
         PACK  DUB,0(2,R2)         YEAR                                         
         CVB   RE,DUB                                                           
         SR    RE,RF               UNLESS THIS YEAR HIGHER THAN BASE            
         BNP   FI08                IGNORE                                       
         MH    RE,=H'12'                                                        
         PACK  DUB,2(2,R2)         MONTH                                        
         CVB   R0,DUB                                                           
         AR    R0,RE           MONTH =  MONTH + (DIFF IN YEARS * 12)            
*                                                                               
         CLI   5(R3),0           SEE IF INCREMENTING MONTH                      
         BE    FIO6                                                             
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
*                                                                               
FIO6     DS    0H                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+3(2),DUB                                                   
*                                                                               
FI08     DS    0H                                                               
         MVC   INOMTH,ILONG+3     SAVE MONTH (OR YEAR/MONTH)                    
         MVI   ILONG+5,C'-'                                                     
         L     RF,PARM2                                                         
         MVC   ILONG+6(4),0(RF)    INVOICE NUMBER AS CL4                        
         CLI   PARM2,2             BUT IS IT BINARY INSTEAD                     
         BNE   FI08D                                                            
         LH    R0,0(RF)                                                         
         N     R0,=X'00003FFF'     STRIP REVERSAL BITS                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+6(4),DUB                                                   
*                                                                               
FI08D    DS    0H                                                               
         L     RF,PARM3            A(B1 PROF)                                   
         CLI   4(RF),C'1'          MODE (ME-MN OR MN=ME)                        
         BE    FI10                                                             
         MVC   ILONG(2),ILONG+3                                                 
         MVC   ILONG+3(2),INOMED                                                
*                                                                               
FI10     DS    0H                                                               
         MVC   ISHORT(2),INOMTH    SHORT IS MM-NNNN                             
         MVI   ISHORT+2,C'-'                                                    
         MVC   ISHORT+3(4),ILONG+6                                              
*                                  SQUEEZE BLANKS OUT OF ILONG                  
         LA    RF,9                                                             
         LA    R5,ILONG                                                         
*                                                                               
FI12     DS    0H                                                               
         CLI   0(R5),C' '                                                       
         BH    *+14                                                             
         MVC   0(1,R5),1(R5)                                                    
         MVI   1(R5),C' '                                                       
         LA    R5,1(R5)                                                         
         BCT   RF,FI12                                                          
*                                  SET OUTPUT                                   
         L     R1,SAVR1                                                         
         LA    RF,ILONG                                                         
         ST    RF,0(R1)            A(LONG FORMAT)                               
         LA    RF,ISHORT                                                        
         ST    RF,4(R1)            A(SHORT FORMAT)                              
         LA    RF,INOMTH                                                        
         ST    RF,8(R1)            A(MONTH OR YEAR/MONTH)                       
         LA    RF,INOMED                                                        
         ST    RF,12(R1)           A(MEDIA EXPANSION)                           
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
MTHTAB   DC    C'123456789ABC'                                                  
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVR1    DS    F                                                                
PARMS    DS    0F                                                               
PARM1    DS    A                                                                
PARM2    DS    A                                                                
PARM3    DS    A                                                                
PARM4    DS    A                                                                
PARM5    DS    A                                                                
PARM6    DS    A                                                                
*                                                                               
ILONG    DS    CL10                                                             
ISHORT   DS    CL7                                                              
INOMED   DS    CL2                                                              
INOMTH   DS    CL2                                                              
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005PPFMTINOS 09/10/97'                                      
         END                                                                    
