*          DATA SET DDPANLBLS  AT LEVEL 015 AS OF 05/01/02                      
*CATALP PANLBLS                                                                 
         TITLE 'PANLBLS - PRINT LABELS FROM PAN FILE'                           
PANLBLS  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,PANLBLS,=V(REGSAVE),RA                                         
*                                                                               
*                                                                               
*                                                                               
*                                  GET PAN BOOK NAME                            
PANBK    GOTO1 =V(CARDS),P1,CARD,=C'RE00'                                       
         CLC   =C'/*',CARD                                                      
         BE    LBLEX                                                            
         MVC   PANBOOK,CARD                                                     
         BAS   R9,CLR                                                           
*                                                                               
*                                  READ SPEC CARDS                              
*                                       L = NO OF LINES FROM TOP OF ONE         
*                                           LABEL TO THE TOP OF THE             
*                                           NEXT LABEL (MAX = 20)               
*                                       C = NO OF COLUMNS FROM START OF         
*                                           ONE LABEL TO THE START OF           
*                                           THE NEXT LABEL (MIN = 32)           
*                                       U = NO OF LABELS ACROSS                 
*                                       F = PRINT FILTER                        
*                                                                               
         MVC   FILTS,SPACES                                                     
*                                  DEFAULTS - 6 LINES, 36 COLUMNS               
*                                       2 LABELS ACROSS                         
         MVC   LN,=H'8'                                                         
         MVC   COL,=H'36'                                                       
         MVC   UP,=H'2'                                                         
*                                                                               
SPEC     GOTO1 (RF),(R1),CARD                                                   
         CLC   =C'/*',CARD                                                      
         BE    GETPAN                                                           
         CLC   =C'F=',CARD                                                      
         BNE   SPECL                                                            
         MVC   FILTS,CARD+2                                                     
         B     SPEC                                                             
*                                                                               
SPECL    CLC   =C'L=',CARD                                                      
         BNE   SPECC                                                            
         BAS   R9,CHKNUM                                                        
         STH   R0,LN                                                            
         B     SPEC                                                             
*                                                                               
SPECC    CLC   =C'C=',CARD                                                      
         BNE   SPECU                                                            
         BAS   R9,CHKNUM                                                        
         STH   R0,COL                                                           
         B     SPEC                                                             
*                                                                               
SPECU    CLC   =C'U=',CARD                                                      
         BNE   SPECERR                                                          
         BAS   R9,CHKNUM                                                        
         STH   R0,UP                                                            
         B     SPEC                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
*                                  GET PANBOOK                                  
GETPAN   GOTO1 =V(PANIC),P1,=C'READ',=C'DIRECTORY',PANBOOK,CARD                 
         TM    P1+8,X'10'                                                       
         BO    BKERR                                                            
*                                                                               
*                                                                               
*                                  PRINT ALIGNMENT                              
         LA    R2,WORK                                                          
         LH    R4,LN                                                            
         LH    R5,COL                                                           
         BCTR  R5,0                                                             
         LH    R3,UP                                                            
         SH    R4,=H'1'                                                         
LNEUP    EX    R5,LUMOVE                                                        
         MVC   0(32,R2),=32C'*'                                                 
         MVC   10(12,R2),PANBOOK                                                
         LA    R2,1(R5,R2)                                                      
         BCT   R3,LNEUP                                                         
         LH    R3,UP                                                            
         BCT   R4,LNEUP                                                         
         BAS   R9,WRTE                                                          
         BAS   R9,CLR                                                           
         B     LBLRD                                                            
*                                                                               
*                                                                               
LUMOVE   MVC   0(0,R2),SPACES                                                   
         EJECT                                                                  
*                                  READ PAN FILE                                
*                                                                               
LBLRD    GOTO1 =V(PANIC),P1,(X'80',=C'READ'),=C'PAN',PANBOOK,CARD               
*                                                                               
         CLC   CARD(2),=C'/*'                                                   
         BE    CLEANUP                                                          
*                                                                               
         CLI   SKPSW,C'Y'                                                       
         BNE   PROC                                                             
         CLI   CARD,C'*'                                                        
         BNE   LBLRD                                                            
         MVI   SKPSW,C'N'                                                       
         B     FLTRTST                                                          
*                                                                               
*                                                                               
*                                  COMPARE FILTER SPECS TO PAN                  
*                                       FILE - IF THEY MATCH, PRINT             
*                                       THE PAN RECORD (UNTIL NEXT *).          
FLTRTST  LA    R7,CARD+2                                                        
         LA    R8,FILTS                                                         
         LA    R0,50                                                            
FT02     CLI   0(R8),C' '                                                       
         BE    FT04                                                             
         CLC   0(1,R8),0(R7)                                                    
         BE    LBLRD                                                            
FT04     LA    R7,1(R7)                                                         
         LA    R8,1(R8)                                                         
         BCT   R0,FT02                                                          
         MVI   SKPSW,C'Y'                                                       
         B     LBLRD                                                            
*                                                                               
*                                  MOVE PAN FILE TO THE LABEL WORK AREA         
*                                       WHEN THE AREA IS FULL - PRINT.          
PROC     CLI   CRDSW,1                                                          
         BE    PROC02                                                           
         MVI   CRDSW,1                                                          
*                                  R3 = A(WORK)                                 
*                                  R2 = UP                                      
*                                  R4 = UP X COL                                
*                                  R5 = A(WORK)                                 
         LA    R3,WORK                                                          
         LR    R5,R3                                                            
         LH    R2,UP                                                            
         LH    R4,COL                                                           
         MH    R4,UP                                                            
*                                                                               
*                                                                               
PROC02   CLI   CARD,C'*'                                                        
         BNE   PROC04                                                           
         AH    R3,COL                                                           
         LR    R5,R3                                                            
         SH    R2,=H'1'                                                         
         BNZ   FLTRTST                                                          
         BAS   R9,WRTE                                                          
         BAS   R9,CLR                                                           
         MVI   CRDSW,0                                                          
         B     FLTRTST                                                          
*                                                                               
PROC04   MVC   1(32,R5),CARD                                                    
         LA    R5,0(R4,R5)                                                      
         B     LBLRD                                                            
         EJECT                                                                  
*                                                                               
*                                                                               
*                                  VALIDATE NUMERIC ENTRIES                     
CHKNUM   LA    R4,CARD+2                                                        
         SR    R5,R5                                                            
*                                                                               
CN2      CLI   0(R4),C'0'                                                       
         BL    SPECERR                                                          
         CLI   0(R4),C'9'                                                       
         BH    SPECERR                                                          
         LA    R4,1(R4)                                                         
         CLI   0(R4),C' '                                                       
         BE    CN4                                                              
         BCT   R5,CN2                                                           
CN4      LPR   R5,R5                                                            
         CH    R5,=H'2'                                                         
         BH    SPECERR                                                          
         EX    R5,CNEXC                                                         
         CVB   R0,DUB                                                           
         BR    R9                                                               
*                                                                               
CNEXC    PACK  DUB,CARD+2(0)                                                    
         EJECT                                                                  
*                                                                               
*                                  MOVE FROM WORK AREA TO THE PRINT             
*                                       LINE AND WRITE                          
         DS    F                                                                
WRTE     ST    R9,*-4                                                           
WR01     SR    R3,R3                                                            
         LA    R2,WORK                                                          
         LH    R3,LN                                                            
         LH    R4,COL                                                           
         MH    R4,UP                                                            
         BCTR  R4,0                                                             
         MVI   PL,X'41'                                                         
*                                                                               
WR02     EX    R4,WRMOVE                                                        
         EX    R4,WRXC                                                          
         LA    R2,1(R4,R2)                                                      
         BAS   R9,PRT                                                           
         BCT   R3,WR02                                                          
*                                                                               
         L     R9,WRTE-4                                                        
         BR    R9                                                               
*                                                                               
*                                                                               
WRMOVE   MVC   PL+1(0),0(R2)                                                    
WRXC     XC    0(0,R2),0(R2)                                                    
*                                                                               
*                                                                               
*                                  CLEAR WORK AREA                              
CLR      LA    R2,WORK                                                          
         LA    R3,10                                                            
C01      XC    0(256,R2),0(R2)                                                  
         LA    R2,256(R2)                                                       
         BCT   R3,C01                                                           
         XC    WORK+2560(100),WORK+2560                                         
         BR    R9                                                               
         EJECT                                                                  
CLEANUP  BAS   R9,WRTE                                                          
         B     LBLEX                                                            
*                                                                               
*                                                                               
*                                  BOOK ERROR                                   
BKERR    MVC   PL+1(27),=C'BOOK              NOT FOUND'                         
         MVC   PL+6(12),PANBOOK                                                 
         B     ERREX                                                            
*                                                                               
*                                                                               
*                                  SPECIFICATIONS ERROR                         
SPECERR  MVC   PL+1(19),=C'INVALID SPEC OPTION'                                 
         MVC   PL+25(80),CARD                                                   
         B     ERREX                                                            
*                                                                               
ERREX    BAS   R9,PRT                                                           
         GOTO1 =V(PRINT),P1,=C'CLOSE'                                           
LBLEX    XBASE                                                                  
*                                                                               
*                                                                               
*                                  PRINT A LINE                                 
PRT      GOTO1 =V(PRINT),P1,PL,=C'BL01'                                         
         MVC   PL,SPACES                                                        
         BR    R9                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
FILTS    DS    CL80                                                             
LN       DS    H                                                                
COL      DS    H                                                                
UP       DS    H                                                                
DUB      DS    D                                                                
P1       DS    6F                                                               
*                                                                               
PL       DC    CL133' '                                                         
SPACES   DC    CL133' '                                                         
*                                                                               
PANBOOK  DC    CL12' '                                                          
*                                                                               
SKPSW    DC    C'Y'                                                             
CRDSW    DC    X'00'                                                            
*                                                                               
CARD     DS    CL120                                                            
*                                                                               
WORK     DS    20CL133                                                          
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDPANLBLS 05/01/02'                                      
         END                                                                    
