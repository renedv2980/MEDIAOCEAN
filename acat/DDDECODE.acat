*          DATA SET DDDECODE   AT LEVEL 005 AS OF 04/22/05                      
*CATALP DECODE                                                                  
         TITLE 'MODULE TO DECODE MIXED DATA STRINGS'                            
         SPACE 1                                                                
*AL1     LENGTH OF OUTPUT KEY                                                   
*AL3     A(INPUT TEXT) DELIMITED BY A SPACE                                     
*XL1     FILL CHARACTER                                                         
*AL3     A(OUTPUT AREA)                                                         
*XL1     RETURN ERROR CODE                                                      
*AL3     A(ERROR MESSAGE)                                                       
*XL1     ON OUTPUT: ACTUAL INPUT DATA LENGTH (BINARY)                           
*AL3     UNUSED (FOR NOW)                                                       
         SPACE 1                                                                
DECODE   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**DECODE,CLEAR=YES                                   
         USING WORKD,RC                                                         
         L     R2,0(R1)                                                         
         LA    R2,0(R2)            R2=A(INPUT EXPRESSION)                       
         L     R5,4(R1)                                                         
         LA    R5,0(R5)            R5=A(OUTPUT KEY)                             
*                                                                               
         CLI   0(R1),0             TEST OUTPUT LENGTH SPECIFIED                 
         BE    DECOT                                                            
         SR    RE,RE                                                            
         IC    RE,0(R1)                                                         
         ST    RE,LENGT                                                         
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         MVC   0(1,R5),4(R1)       YES - PREFILL KEY WITH PAD CHARACTER         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R5),0(R5)                                                    
         SPACE 1                                                                
DECOT    LR    R3,R2               LOOK FOR DELIMITER                           
         LA    R4,12                                                            
DECOT2   CLI   0(R3),C'/'                                                       
         BE    DECTRN                                                           
         CLI   0(R3),C'('                                                       
         BE    DECTRN                                                           
         CLI   0(R3),C')'                                                       
         BE    DECTRN                                                           
         CLI   0(R3),C''''                                                      
         BE    DECOTA                                                           
         LA    R3,1(R3)                                                         
         BCT   R4,DECOT2                                                        
         B     DECTRN                                                           
         SPACE 1                                                                
DECOTA   LA    R3,ACTAB                                                         
         MVI   ERROR,1                                                          
         MVI   COUNT,0                                                          
         TM    0(R2),X'F0'                                                      
         BNO   DECOCM                                                           
         PACK  DUB,0(1,R2)                                                      
         TM    1(R2),X'F0'                                                      
         BNO   *+14                                                             
         PACK  DUB,0(2,R2)                                                      
         LA    R2,1(R2)                                                         
         LA    R2,1(R2)                                                         
         CVB   R4,DUB                                                           
         MVI   ERROR,9                                                          
         LTR   R4,R4                                                            
         BZ    DECERR                                                           
         BCTR  R4,R0                                                            
         LTR   R4,R4                                                            
         BZ    DECOCM                                                           
         STC   R4,COUNT                                                         
         MVI   ERROR,1                                                          
         SPACE 1                                                                
DECOCM   CLI   0(R3),0                                                          
         BE    DECERR                                                           
         CLC   0(1,R2),0(R3)                                                    
         BE    DECFND                                                           
         LA    R3,L'ACTAB(R3)                                                   
         B     DECOCM                                                           
         SPACE 1                                                                
DECFND   BAS   RE,DECLEN                                                        
         CLI   ERROR,0                                                          
         BNE   DECERR                                                           
         ST    R5,LAST                                                          
         LH    R3,2(R3)                                                         
         B     DECTAB(R3)                                                       
         SPACE 1                                                                
DECTAB   DS    0H                                                               
         B     DECBIN                                                           
         B     DECHEX                                                           
         B     DECCHR                                                           
         B     DECPCK                                                           
         B     DECFUL                                                           
         B     DECHLF                                                           
         B     DECEND                                                           
         B     DECSPL                                                           
         B     DECALI                                                           
         SPACE 1                                                                
DECNXT   L     R2,NEXT                                                          
         CLI   COUNT,0                                                          
         BE    DECOTA                                                           
         LR    R6,R5                                                            
         S     R6,LAST                                                          
         L     R7,LAST                                                          
         BCTR  R6,0                                                             
         SR    R3,R3                                                            
         IC    R3,COUNT                                                         
         SPACE 1                                                                
DECNX2   EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R7)                                                    
         LA    R5,1(R6,R5)                                                      
         BCT   R3,DECNX2                                                        
         B     DECOTA                                                           
         EJECT                                                                  
*              BINARY                                                           
         SPACE 1                                                                
DECBIN   L     R6,LENGTH                                                        
         SR    R7,R7                                                            
         LA    R4,2(R2)                                                         
         MVI   ERROR,6                                                          
         SPACE 1                                                                
DECBI2   SLL   R7,1                                                             
         CLI   0(R4),C'0'                                                       
         BE    *+16                                                             
         CLI   0(R4),C'1'                                                       
         BE    *+8                                                              
         B     DECERR                                                           
         CLI   0(R4),C'1'                                                       
         BNE   *+8                                                              
         LA    R7,1(R7)                                                         
         LA    R4,1(R4)                                                         
         BCT   R6,DECBI2                                                        
         ST    R7,WORD                                                          
         L     R6,LENGTH                                                        
         SRDA  R6,32                                                            
         D     R6,=F'8'                                                         
         LA    R8,WORD+4                                                        
         SR    R8,R7                                                            
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R8)                                                    
         LA    R5,1(R5,R7)                                                      
         B     DECNXT                                                           
         EJECT                                                                  
*              HEXADECIMAL                                                      
         SPACE 1                                                                
DECHEX   MVI   SWITCH,0                                                         
         CLI   MODIFY,0                                                         
         BE    DECHE0                                                           
         MVI   FILL,0                                                           
         BAS   RE,DECFILL                                                       
         L     R3,LENGM                                                         
         SLL   R3,1                                                             
         L     R4,LENGTH                                                        
         SR    R3,R4                                                            
         BP    DECHEM                                                           
         LPR   R3,R3                                                            
         LA    R4,1(R2)                                                         
         AR    R4,R3                                                            
         L     R6,LENGM                                                         
         SLL   R6,1                                                             
         B     DECHE1                                                           
         SPACE 1                                                                
DECHEM   SRL   R3,1                                                             
         AR    R5,R3                                                            
         L     R6,LENGTH                                                        
         LA    R4,1(R2)                                                         
         B     DECHE1                                                           
         SPACE 1                                                                
DECHE0   L     R6,LENGTH                                                        
         LA    R4,1(R2)                                                         
         SPACE 1                                                                
DECHE1   MVI   ERROR,7                                                          
         SPACE 1                                                                
DECHE2   LA    R4,1(R4)                                                         
         LA    R7,HEXTAB                                                        
         SR    R8,R8                                                            
         SPACE 1                                                                
DECHE4   CLI   0(R7),0                                                          
         BE    DECERR                                                           
         CLC   0(1,R7),0(R4)                                                    
         BE    *+16                                                             
         LA    R7,1(R7)                                                         
         LA    R8,1(R8)                                                         
         B     DECHE4                                                           
         CLI   SWITCH,1                                                         
         BE    *+12                                                             
         MVI   0(R5),0                                                          
         SLL   R8,4                                                             
         STC   R8,BYTE                                                          
         OC    0(1,R5),BYTE                                                     
         CLI   SWITCH,0                                                         
         BNE   *+12                                                             
         MVI   SWITCH,1                                                         
         B     *+12                                                             
         LA    R5,1(R5)                                                         
         MVI   SWITCH,0                                                         
         BCT   R6,DECHE2                                                        
         B     DECNXT                                                           
         EJECT                                                                  
*              CHARACTER                                                        
         SPACE 1                                                                
DECCHR   CLI   MODIFY,0                                                         
         BE    DECCHR2                                                          
         MVI   FILL,C' '                                                        
         BAS   RE,DECFILL                                                       
         L     R3,LENGTH                                                        
         CLC   LENGTH,LENGM                                                     
         BL    *+8                                                              
         L     R3,LENGM                                                         
         BCTR  R3,0                                                             
         LA    R4,2(R2)                                                         
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R4)                                                    
         A     R5,LENGM                                                         
         B     DECNXT                                                           
         SPACE 1                                                                
DECCHR2  L     R6,LENGTH                                                        
         LA    R4,2(R2)                                                         
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R4)                                                    
         LA    R5,1(R5,R6)                                                      
         B     DECNXT                                                           
         SPACE 1                                                                
*              PACKED                                                           
         SPACE 1                                                                
DECPCK   BAS   RE,DECNUM                                                        
         CLI   ERROR,0                                                          
         BNE   DECERR                                                           
         L     R6,LENGTH                                                        
         LA    R6,2(R6)                                                         
         SRDA  R6,32                                                            
         D     R6,=F'2'                                                         
         LA    R8,DUB+8                                                         
         SR    R8,R7                                                            
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R8)                                                    
         LA    R5,1(R5,R7)                                                      
         B     DECNXT                                                           
         EJECT                                                                  
*              FULLWORD                                                         
         SPACE 1                                                                
DECFUL   BAS   RE,DECNUM                                                        
         CLI   ERROR,0                                                          
         BNE   DECERR                                                           
         CVB   R4,DUB                                                           
         ST    R4,0(R5)                                                         
         LA    R5,4(R5)                                                         
         B     DECNXT                                                           
         SPACE 1                                                                
*              HALFWORD                                                         
         SPACE 1                                                                
DECHLF   BAS   RE,DECNUM                                                        
         CLI   ERROR,0                                                          
         BNE   DECERR                                                           
         CVB   R4,DUB                                                           
         STH   R4,0(R5)                                                         
         LA    R5,2(R5)                                                         
         B     DECNXT                                                           
         SPACE 1                                                                
*              CHARACTER                                                        
         SPACE 1                                                                
DECALI   BAS   RE,DECNUM                                                        
         CLI   ERROR,0                                                          
         BNE   DECERR                                                           
         CVB   R4,DUB                                                           
         STC   R4,0(R5)                                                         
         LA    R5,1(R5)                                                         
         B     DECNXT                                                           
         EJECT                                                                  
*              SET UP FOR SPLIT KEY                                             
         SPACE 1                                                                
DECSPL   MVI   ERROR,10                                                         
         CLI   SPLIT,0                                                          
         BNE   DECERR                                                           
         MVI   ERROR,11                                                         
         CLI   0(R1),0                                                          
         BE    DECERR                                                           
         L     R4,4(R1)                                                         
         LA    R4,0(R4)                                                         
         SR    R5,R4                                                            
         ST    R5,LENGL                                                         
         LA    R5,RIGHTWK                                                       
         OI    SPLIT,X'01'                                                      
         B     DECNXT                                                           
         EJECT                                                                  
*              GET SUB-STRING LENGTH AND VERIFY                                 
         SPACE 1                                                                
DECLEN   NTR1                                                                   
         CLI   0(R2),C' '                                                       
         BNE   *+12                                                             
         MVI   ERROR,0                                                          
         B     DECXIT                                                           
         CLI   0(R2),C'/'                                                       
         BNE   *+20                                                             
         MVI   ERROR,0                                                          
         LA    R2,1(R2)                                                         
         ST    R2,NEXT                                                          
         B     DECXIT                                                           
         MVI   ERROR,0                                                          
         BAS   RE,DECMOD                                                        
         CLI   ERROR,0                                                          
         BNE   DECXIT                                                           
         SR    R6,R6                                                            
         MVI   ERROR,2                                                          
         CLI   1(R2),C''''                                                      
         BNE   DECXIT                                                           
         LA    R9,2(R2)                                                         
         LR    R7,R9                                                            
         LH    R8,4(R3)                                                         
         LA    R8,1(R8)                                                         
         SPACE 1                                                                
DECL2    CLI   0(R7),C''''                                                      
         BE    DECL4                                                            
         LA    R7,1(R7)                                                         
         BCT   R8,DECL2                                                         
         MVI   ERROR,3                                                          
         B     DECXIT                                                           
         SPACE 1                                                                
DECL4    MVI   ERROR,4                                                          
         SR    R7,R9                                                            
         BZ    DECXIT                                                           
         ST    R7,LENGTH                                                        
         IC    R6,1(R3)                                                         
         L     R8,LENGTH                                                        
         SRDA  R8,32                                                            
         DR    R8,R6                                                            
         MVI   ERROR,5                                                          
         LTR   R8,R8                                                            
         BNZ   DECXIT                                                           
         IC    R6,COUNT                                                         
         LA    R6,1(R6)                                                         
         OC    LENGM,LENGM                                                      
         BZ    *+8                                                              
         L     R9,LENGM                                                         
         MR    R8,R6                                                            
         A     R9,LENGA                                                         
         ST    R9,LENGA                                                         
         CLI   0(R1),0                                                          
         BE    *+18                                                             
         MVI   ERROR,12                                                         
         CLC   LENGA,LENGT                                                      
         BH    DECXIT                                                           
         MVI   ERROR,0                                                          
         L     R7,LENGTH                                                        
         LA    R7,3(R7,R2)                                                      
         ST    R7,NEXT                                                          
         CLI   MODIFY,0                                                         
         BNE   DECMODX                                                          
         B     DECXIT                                                           
         EJECT                                                                  
*              ROUTINE TO FILL OUTPUT WITH CHARACTER                            
         SPACE 1                                                                
DECFILL  DS    0H                                                               
         L     R3,LENGM                                                         
         MVC   0(1,R5),FILL                                                     
         BCTR  R3,0                                                             
         LTR   R3,R3                                                            
         BZR   RE                                                               
         BCTR  R3,0                                                             
         EX    R3,*+6                                                           
         BR    RE                                                               
         MVC   1(0,R5),0(R5)                                                    
         EJECT                                                                  
*              MODIFY CHECK                                                     
         SPACE 1                                                                
DECMOD   NTR1                                                                   
         MVI   MODIFY,0                                                         
         XC    LENGM,LENGM                                                      
         CLI   1(R2),C'L'                                                       
         BNE   DECXIT                                                           
         MVI   ERROR,13                                                         
         CLI   0(R2),C'C'                                                       
         BE    *+16                                                             
         CLI   0(R2),C'X'                                                       
         BE    *+8                                                              
         B     DECXIT                                                           
         LA    R2,1(R2)                                                         
         MVI   ERROR,14                                                         
         TM    1(R2),X'F0'                                                      
         BNO   DECXIT                                                           
         PACK  DUB,1(1,R2)                                                      
         TM    2(R2),X'F0'                                                      
         BNO   *+14                                                             
         PACK  DUB,1(2,R2)                                                      
         LA    R2,1(R2)                                                         
         LA    R2,1(R2)                                                         
         CVB   R4,DUB                                                           
         LTR   R4,R4                                                            
         BZ    DECXIT                                                           
         ST    R4,LENGM                                                         
         OI    MODIFY,X'01'                                                     
         MVI   ERROR,0                                                          
         SPACE 1                                                                
DECMODX  XIT1 REGS=(R2)                                                         
         EJECT                                                                  
*              VALIDATE NUMERIC                                                 
         SPACE 1                                                                
DECNUM   DS    0H                                                               
         L     R6,LENGTH                                                        
         BCTR  R6,0                                                             
         MVC   WORK(12),=12X'F0'                                                
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVZ   WORK(0),2(R2)                                                    
         MVI   ERROR,8                                                          
         CLC   WORK(12),=12X'F0'                                                
         BNER  RE                                                               
         MVI   ERROR,0                                                          
         EX    R6,*+8                                                           
         B     *+10                                                             
         PACK  DUB,2(0,R2)                                                      
         CVB   R6,DUB                                                           
         CVD   R6,DUB                                                           
         BR    RE                                                               
         EJECT                                                                  
*              TRANSLATE OLD INPUT TYPE TO NEW                                  
         SPACE 1                                                                
DECTRN   MVI   NEWSTRNG,C' '                                                    
         MVC   NEWSTRNG+1(L'NEWSTRNG-1),NEWSTRNG                                
         LA    R3,NEWSTRNG                                                      
         LA    R4,87               MAX KEY 42 (2*42+3)                          
         MVI   OPSW,0                                                           
         MVI   FIRST,0                                                          
         SPACE 1                                                                
DECTRN2  BCT   R4,*+12                                                          
         MVI   ERROR,2                                                          
         B     DECERR                                                           
         CLI   0(R2),C'('                                                       
         BE    DECTRN4                                                          
         CLI   0(R2),C')'                                                       
         BE    DECTRN6                                                          
         CLI   0(R2),C'/'                                                       
         BE    DECTRN8                                                          
         CLI   0(R2),C' '                                                       
         BE    DECTRN10                                                         
         CLI   FIRST,0                                                          
         BNE   DECMVE                                                           
         MVC   0(2,R3),=C'X'''                                                  
         LA    R3,2(R3)                                                         
         MVI   FIRST,1                                                          
         B     DECMVE                                                           
         SPACE 1                                                                
DECTRN4  CLI   OPSW,0                                                           
         BNE   DECMVE                                                           
         CLI   FIRST,0                                                          
         BE    *+12                                                             
         MVI   0(R3),C''''                                                      
         LA    R3,1(R3)                                                         
         MVC   0(2,R3),=C'C'''                                                  
         LA    R2,1(R2)                                                         
         MVI   FIRST,1                                                          
         MVI   OPSW,1                                                           
         LA    R3,2(R3)                                                         
         B     DECTRN2                                                          
         SPACE 1                                                                
DECTRN6  CLI   OPSW,1                                                           
         BE    *+12                                                             
         MVI   ERROR,7                                                          
         B     DECERR                                                           
         MVI   OPSW,0                                                           
         MVI   0(R3),C''''                                                      
         LA    R3,1(R3)                                                         
         LA    R2,1(R2)                                                         
         MVI   FIRST,0                                                          
         B     DECTRN2                                                          
         SPACE 1                                                                
DECTRN8  CLI   OPSW,1                                                           
         BE    DECMVE                                                           
         CLI   FIRST,0                                                          
         BE    *+16                                                             
         MVI   0(R3),C''''                                                      
         LA    R3,1(R3)                                                         
         MVI   FIRST,0                                                          
         B     DECMVE                                                           
         SPACE 1                                                                
DECTRN10 CLI   OPSW,1                                                           
         BE    DECMVE                                                           
         CLI   FIRST,0                                                          
         BE    *+16                                                             
         MVI   0(R3),C''''                                                      
         LA    R3,1(R3)                                                         
         MVI   FIRST,0                                                          
         MVI   0(R3),C' '                                                       
         LA    R2,NEWSTRNG                                                      
         B     DECOTA                                                           
         SPACE 1                                                                
DECMVE   MVC   0(1,R3),0(R2)                                                    
         LA    R3,1(R3)                                                         
         LA    R2,1(R2)                                                         
         B     DECTRN2                                                          
         EJECT                                                                  
*              ERRORS AND EXITS                                                 
         SPACE 1                                                                
DECERR   SR    R3,R3                                                            
         IC    R3,ERROR                                                         
         BCTR  R3,0                                                             
         LA    R3,ERRTAB(R3)                                                    
         SR    R4,R4                                                            
         IC    R4,0(R3)                                                         
         LR    R3,R4                                                            
         MH    R3,=H'30'                                                        
         LA    R3,ERRORS(R3)                                                    
         ST    R3,08(R1)                                                        
         MVI   08(R1),X'FF'                                                     
         B     DECEXT                                                           
         SPACE 1                                                                
DECEND   CLI   SPLIT,0                                                          
         BE    DECEND2                                                          
         MVC   08(4,R1),LENGT                                                   
         LA    R4,RIGHTWK                                                       
         SR    R5,R4                                                            
         BZ    DECEXT                                                           
         ST    R5,LENGR                                                         
         A     R5,LENGL                                                         
         MVI   ERROR,12                                                         
         C     R5,LENGT                                                         
         BH    DECERR                                                           
         L     R5,4(R1)                                                         
         LA    R5,0(R5)                                                         
         A     R5,LENGT                                                         
         S     R5,LENGR                                                         
         L     R4,LENGR                                                         
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     DECEXT                                                           
         MVC   0(0,R5),RIGHTWK                                                  
         SPACE 1                                                                
DECEND2  DS    0H                                                               
         MVC   12(1,R1),LENGA+3    ACTUAL INPUT DATA LENGTH (BINARY)            
         OC    LENGT,LENGT                                                      
         BZ    *+14                                                             
         MVC   8(4,R1),LENGT                                                    
         B     DECEXT                                                           
         L     R4,4(R1)                                                         
         LA    R4,0(R4)                                                         
         SR    R5,R4                                                            
         ST    R5,08(R1)                                                        
         SPACE 1                                                                
DECXIT   DS    0H                                                               
DECEXT   XMOD1 1                                                                
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              ACTION TABLE                                                     
         SPACE 1                                                                
         DS    0H                                                               
ACTAB    DS    0CL6                                                             
         DC    C'B',AL1(8),AL2(00,32)                                           
         DC    C'X',AL1(2),AL2(04,87)                                           
         DC    C'C',AL1(1),AL2(08,80)                                           
         DC    C'P',AL1(1),AL2(12,12)                                           
         DC    C'F',AL1(1),AL2(16,08)                                           
         DC    C'H',AL1(1),AL2(20,06)                                           
         DC    C' ',AL1(0),AL2(24,00)                                           
         DC    C'/',AL1(0),AL2(28,00)                                           
         DC    C'A',AL1(1),AL2(32,03)                                           
         DC    C'b',AL1(8),AL2(00,32)                                           
         DC    C'x',AL1(2),AL2(04,87)                                           
         DC    C'c',AL1(1),AL2(08,80)                                           
         DC    C'p',AL1(1),AL2(12,12)                                           
         DC    C'f',AL1(1),AL2(16,08)                                           
         DC    C'h',AL1(1),AL2(20,06)                                           
         DC    C'a',AL1(1),AL2(32,03)                                           
         DC    AL1(0)                                                           
         SPACE 1                                                                
*              VALID HEX CODES                                                  
         SPACE 1                                                                
HEXTAB   DC    C'0123456789ABCDEF',AL1(0)                                       
         SPACE 1                                                                
*              ERROR CONVERTER LIST                                             
         SPACE 1                                                                
ERRTAB   DC    X'0001010101010101000100020000'                                  
         SPACE 1                                                                
*              ERROR MESSAGES                                                   
         SPACE 1                                                                
ERRORS   DS    0CL30                                                            
         DC    CL30'INVALID STRING DEFINITION'                                  
         DC    CL30'INVALID OR MISSING DATA STRING'                             
         DC    CL30'KEY TOO LONG'                                               
         EJECT                                                                  
*              W/S DSECT                                                        
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WORD     DS    F                                                                
LENGTH   DS    F                                                                
LENGA    DS    F                                                                
LENGL    DS    F                                                                
LENGR    DS    F                                                                
LENGT    DS    F                                                                
LENGM    DS    F                                                                
NEXT     DS    F                                                                
LAST     DS    F                                                                
COUNT    DS    C                                                                
BYTE     DS    C                                                                
ERROR    DS    C                                                                
SWITCH   DS    C                                                                
SPLIT    DS    C                                                                
FILL     DS    C                                                                
MODIFY   DS    C                                                                
WORK     DS    CL12                                                             
OPSW     DS    C                                                                
FIRST    DS    C                                                                
NEWSTRNG DS    CL102                                                            
RIGHTWK  DS    200C                                                             
WORKX    EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDDECODE  04/22/05'                                      
         END                                                                    
