*          DATA SET DDPERSON   AT LEVEL 002 AS OF 08/11/04                      
*CATALP PERSON                                                                  
         TITLE 'PERSON - EXTRACT PERSONAL ID DATA'                              
**********************************************************************          
* PERSON - EXTRACT PERSONAL ID DATA FROM CONTROL FILE                *          
* RETURN DATA IN BLOCK PERSOND DEFINED IN DDPERSOND                  *          
* PARAMETERS:                                                        *          
*                                                                    *          
* AL1 INPUT INDICATOR BYTE.                                          *          
*     VALUE=0 - GET PERSON NUMBER FROM UTL                           *          
*     VALUE=1 - A(PERSON NUMBER IN PARAMETER)                        *          
*     VALUE=2 - A(PERSONAL ID IN PARAMETER)                          *          
* AL3 ADR CALLERS DATA AREA WHICH CAN TAKE VALUES:                   *          
*     A(0)                                                           *          
*     A(PERSON NUMBER)                                               *          
*     A(PERSON ID)                                                   *          
*                                                                    *          
* AL1 RETURN CONTROL BYTE                                            *          
*     VALUE=0 - RETURN A(0)                                          *          
*     VALUE=0 - RETURN A(PERSOND)                                    *          
* AL3 ADR RETURN PERSONS DATA AREA WHICH CAN TAKE VALUES:            *          
*     AL3(0) - RETURN ADDRESS OF PERSOND LOCAL AREA                  *          
*     AL3(PERSONC) - ADDRESS OF PERSOND AREA IN CALLING PROGRAM      *          
*                                                                    *          
* AL1 RETURN ERROR CODE                                              *          
*     VALUES:  0 - OK, ELSE IO ERROR CODE                            *          
* AL3 ADR COMFACS                                                    *          
*                                                                    *          
* AL1 N/D                                                            *          
* AL3 ADR SYSFACS                                                    *          
*                                                                    *          
**********************************************************************          
                                                                                
         PRINT NOGEN                                                            
PERSON   CSECT                                                                  
         NMOD1 WORKX-WORKD,*PERSON*,CLEAR=YES,RR=RA                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         B     INIT                                                             
                                                                                
**********************************************************************          
* INITIALISATION                                                     *          
**********************************************************************          
                                                                                
INIT     EQU   *                                                                
         MVC   PARM,0(R1)                                                       
         MVI   RETCODE,0                                                        
*                                                                               
INIT010  ICM   RF,7,ASYSFACS                                                    
         BZ    INIT020                                                          
         LA    RF,0(RF)                                                         
         ICM   RF,15,VSSB-SYSFACD(RF)                                           
         BZ    INIT020                                                          
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         BZ    INIT020                                                          
         ICM   R5,15,TCBUTL-TCBD(RF)                                            
         BNZ   INIT030                                                          
INIT020  ICM   R5,15,ASSB                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ICM   R5,15,TCBUTL-TCBD(RF)                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING UTLD,R5                                                          
INIT030  CLI   INCON,0                                                          
         BE    INIT040                                                          
         CLI   INCON,1                                                          
         BE    INIT050                                                          
         CLI   INCON,2                                                          
         BE    INIT060                                                          
         DC    H'0'                                                             
INIT040  OC    TPERSON,TPERSON                                                  
         BZ    INITERR                                                          
         MVC   PERNUM,TPERSON                                                   
         B     INIT100                                                          
INIT050  ICM   RF,7,APERNUM                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RF,0(RF)                                                         
         MVC   PERNUM,0(RF)                                                     
         B     INIT100                                                          
INIT060  ICM   RF,7,APERPID                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RF,0(RF)                                                         
         MVC   PERSONID,0(RF)                                                   
         B     INIT100                                                          
*                                                                               
INIT100  CLI   RETCON,0                                                         
         BE    INIT110                                                          
         CLI   RETCON,1                                                         
         BE    INIT120                                                          
         DC    H'0'                                                             
INIT110  LA    R4,PERSONC                                                       
         STCM  R4,7,APERSON                                                     
         B     INIT200                                                          
INIT120  ICM   R4,7,APERSON                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R4,0(R4)                                                         
         B     INIT200                                                          
         USING PERSOND,R4                                                       
*                                                                               
INIT200  ICM   RF,7,ACOMFACS                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RF,0(RF)                                                         
         USING COMFACSD,RF                                                      
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         MVC   GETDAY,CGETDAY                                                   
         DROP  RF                                                               
*                                  SET SECURITY AGENCY                          
         OC    TAGYPER,TAGYPER                                                  
         BZ    *+14                                                             
         MVC   SECAGY,TAGYPER                                                   
         B     GETPER                                                           
         OC    TAGYSEC,TAGYSEC                                                  
         BZ    *+14                                                             
         MVC   SECAGY,TAGYSEC                                                   
         B     GETPER                                                           
         MVC   SECAGY,TAGY                                                      
         B     GETPER                                                           
*                                                                               
IOERR    XC    PARM(L'PARM),PARM   IO ERROR                                     
         MVC   RETCODE,IOERR                                                    
         B     EXIT                                                             
*                                                                               
INITERR  XC    PARM(L'PARM),PARM   INPUT PARAMETER ERROD                        
         MVI   RETCODE,X'FF'                                                    
         B     EXIT                                                             
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         CLI   RETCODE,0           RETURN CC .NE. IF ERROR                      
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* GET PERSON SECURITY DATA                                           *          
**********************************************************************          
                                                                                
GETPER   EQU   *                                                                
         TM    TSTAT,TSTATDDS      TEST DDS TEST TERMINAL                       
         BZ    GPER010                                                          
         LA    R2,IOKEY            READ SECURITY AGENCY RECROD                  
         USING CT5REC,R2                                                        
         XC    CT5KEY,CT5KEY                                                    
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,SECAGY                                                  
         BAS   RE,READSA                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 FINDEL,CTAADELQ     TEST FOR PERSONID REQD FLAG                  
         USING CTAADD,R3                                                        
         TM    CTAADFLG,CTAADPRQ                                                
         BNZ   GPER010                                                          
*&&UK*&& MVC   SECAGY,=CL2'D1'     USE DEFAULT DDS PERSONID UNLESS              
*&&US*&& MVC   SECAGY,=CL2'SJ'     PERSON ID REQUIRD FOR AGENCY                 
         DROP  R2,R3                                                            
*                                                                               
GPER010  EQU   *                                                                
         LA    R2,IOKEY            READ PASSWORD RECORD                         
         CLI   INCON,2             CHECK IF PASSED PERSON ID ON INPUT           
         BE    GPER030                                                          
         USING SA0REC,R2                                                        
         XC    SA0KEY,SA0KEY                                                    
         MVI   SA0KTYP,SA0KTYPQ                                                 
         MVC   SA0KAGY,SECAGY                                                   
         MVC   SA0KNUM,PERNUM                                                   
         BAS   RE,READSA                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 FINDEL,SAPASELQ     TEST FOR 'DDS' PASSWORD                      
         USING SAPASD,R3                                                        
         CLI   SAPASLEN,X'0C'      ENSURE ELEMENT HAS                           
         BE    *+12                  CORRECT LENGTH FOR PASSWORD                
         BAS   RE,NEXTEL                                                        
         B     *-12                                                             
         CLC   DDSPWD,SAPASDTA                                                  
         BNE   GPER020                                                          
         DROP  R3                                                               
         OI    PERINDS,PERIDDS                                                  
         MVC   PEROID,DDSOID       SET UP DUMMY PERSONAL ID ETC.                
         MVC   PERDID,DDSDID                                                    
         MVC   PERSONID,DDSPID                                                  
         MVC   PERPID,DDSPID                                                    
         MVC   PERNAMES,DDSNAMES                                                
         B     GETOFFN                                                          
*                                                                               
GPER020  GOTO1 FINDEL,SAPALELQ     EXTRACT PERSONAL ID                          
         MVC   PERSONID,SAPALPID-SAPALD(R3)                                     
*                                                                               
         USING SAPEREC,R2          READ PERSON RECORD                           
GPER030  LA    R2,IOKEY            READ PASSWORD RECORD                         
         MVC   PERPID,PERSONID                                                  
         XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,SECAGY                                                   
         MVC   SAPEPID,PERPID                                                   
         GOTO1 DATCON,DMCB,(5,(0)),(2,SAPEDEF)                                  
         XC    SAPEDEF,EFFS        COMPLEMENT DATE                              
         BAS   RE,RDHISA                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SAPEKEY(SAPEDEF-SAPEKEY),IOAREA                                  
         BE    *+6                                                              
         DC    H'0'                NO RECORD FOUND FOR THIS PERSON              
*                                                                               
         GOTO1 FINDEL,SANAMELQ     FIND NAME ELEMENT                            
         USING SANAMD,R3                                                        
         XR    RE,RE                                                            
         LA    RF,SANAMES                                                       
         USING SANAMES,RF          RF=A(NAME SUB ELEMENT)                       
         MVC   PERNAMES,SPACES                                                  
*                                                                               
         IC    RE,SANAMELN         COPY FIRST NAME                              
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   PERFNAME(0),SANAME                                               
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
                                                                                
         TM    SANAMIND,SANAMIMN   TEST MIDDLE NAME PRESENT                     
         BZ    GPER040                                                          
         IC    RE,SANAMELN         COPY MIDDLE NAME                             
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   PERMNAME(0),SANAME                                               
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
*                                                                               
GPER040  IC    RE,SANAMELN         COPY LAST NAME                               
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   PERLNAME(0),SANAME                                               
         DROP  R3,RF                                                            
*                                                                               
         GOTO1 FINDEL,SAPERELQ     EXTRACT OFFICE/DEPARTMENT IDS                
         MVC   PEROID,SAPEROFF-SAPERD(R3)                                       
         MVC   PERDID,SAPERDID-SAPERD(R3)                                       
*                                                                               
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
* GET OFFICE NAME                                                     *         
***********************************************************************         
                                                                                
GETOFFN  TM    PERINDS,PERIOFLN    TEST OFF-LINE                                
         BZ    GOFFN2                                                           
         LA    R1,SPACES           IF OFF-LINE OUTPUT SPACES                    
         B     GOFFN10                                                          
*                                                                               
GOFFN2   TM    PERINDS,PERIDDS     TEST DDS PASSWORD                            
         BZ    GOFFN4                                                           
         LA    R1,DDSONAME         OUTPUT DUMMY DDS OFFICE NAME                 
         B     GOFFN10                                                          
*                                                                               
GOFFN4   LA    R2,IOKEY            INITIALIZE OFFICE RECORD KEY                 
         USING SAOFREC,R2                                                       
         XC    SAOFKEY,SAOFKEY                                                  
         MVI   SAOFTYP,SAOFTYPQ                                                 
         MVI   SAOFSUB,SAOFSUBQ                                                 
         MVC   SAOFAGY,SECAGY                                                   
         MVC   SAOFOID,PEROID                                                   
*                                                                               
         BAS   RE,READSA           READ RECORD                                  
         BNE   IOERR                                                            
*                                                                               
         GOTO1 FINDEL,SAOFFELQ     FIND OFFICE ELEMENT                          
         AR    RF,R3               APPEND SPACES TO NAME                        
         MVC   0(L'SPACES,RF),SPACES                                            
         LA    R1,SAOFFNAM-SAOFFD(R3)                                           
*                                                                               
GOFFN10  EQU   *                                                                
*                                                                               
         LA    RE,PEROFFN          COPY NAME INTO OUTPUT                        
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   0(0,RE),0(R1)                                                    
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* GET DEPARTMENT NAME                                                 *         
***********************************************************************         
                                                                                
GETDPTN  TM    PERINDS,PERIOFLN    TEST OFF-LINE                                
         BZ    GDPTN2                                                           
         LA    R1,SPACES           IF OFF-LINE OUTPUT SPACES                    
         B     GDPTN10                                                          
*                                                                               
GDPTN2   TM    PERINDS,PERIDDS     TEST DDS PASSWORD                            
         BZ    GDPTN4                                                           
         LA    R1,DDSONAME         OUTPUT DUMMY DDS OFFICE NAME                 
         B     GDPTN10                                                          
*                                                                               
GDPTN4   LA    R2,IOKEY            INITIALIZE DEPARTMENT RECORD KEY             
         USING SADPREC,R2                                                       
         XC    SADPKEY,SADPKEY                                                  
         MVI   SADPTYP,SADPTYPQ                                                 
         MVI   SADPSUB,SADPSUBQ                                                 
         MVC   SADPAGY,SECAGY                                                   
         MVC   SADPOID,PEROID                                                   
         MVC   SADPDID,PERDID                                                   
*                                                                               
         BAS   RE,READSA           READ RECORD                                  
         BNE   IOERR                                                            
*                                                                               
         GOTO1 FINDEL,SADPTELQ     FIND DEPARTMENT ELEMENT                      
         AR    RF,R3               APEND SPACES TO NAME                         
         MVC   0(L'SPACES,RF),SPACES                                            
         LA    R1,SADPTNAM-SADPTD(R3)                                           
*                                                                               
GDPTN10  EQU   *                                                                
*                                                                               
         LA    RE,PERDEPN          MOVE DEPARTMENT NAME TO OUTPUT               
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   0(0,RE),0(R1)                                                    
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIND AN ELEMENT IN IOAREA                                *         
*                                                                     *         
* NTRY: R1=ELEMENT CODE                                               *         
* EXIT: R3=A(ELEMENT), RF=L(ELEMENT)                                  *         
***********************************************************************         
                                                                                
FINDEL   LA    R3,IOAREA+(SAASDATA-SAASREC)                                     
         XR    RF,RF                                                            
                                                                                
FEL2     CLI   0(R3),0             TEST E-O-R                                   
         BE    FELOVER                                                          
                                                                                
         IC    RF,1(R3)            RF=L(ELEMENT)                                
         CLM   R1,1,0(R3)          MATCH ON ELEMENT CODE                        
         BER   RE                                                               
                                                                                
NEXTEL   BXH   R3,RF,FEL2          BUMP R3 TO NEXT ELEMENT                      
                                                                                
FELOVER  SR    RE,RB                                                            
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* ROUTINE TO READ SECURITY ACCESS RECORD                              *         
***********************************************************************         
                                                                                
READSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTO1 DATAMGR,DMCB,DMREAD,CTFILE,IOAREA,IOAREA                         
         XIT1                                                                   
                                                                                
***********************************************************************         
* ROUTINE TO READ HIGH FOR A SECURITY ACCESS RECORD                   *         
***********************************************************************         
                                                                                
RDHISA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTO1 DATAMGR,DMCB,DMRDHI,CTFILE,IOAREA,IOAREA                         
         XIT1                                                                   
                                                                                
***********************************************************************         
* ROUTINE TO READ SEQUENTIAL FOR A SECURITY ACCESS RECORD             *         
***********************************************************************         
                                                                                
RDSQSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTO1 DATAMGR,DMCB,DMRDSQ,CTFILE,IOAREA,IOAREA                         
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
                                                                                
         LTORG                                                                  
                                                                                
ASSB     DC    V(SSB)              A(SYSTEM STATUS BLOCK)                       
                                                                                
SPACES   DC    CL132' '                                                         
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
                                                                                
DMREAD   DC    C'DMREAD '          FOR DATA MANAGER CALLS                       
DMRDHI   DC    C'DMRDHI '                                                       
DMRDSQ   DC    C'DMRSEQ '                                                       
CTFILE   DC    C'CTFILE '                                                       
                                                                                
DDSPWD   DC    C'DDS       '       DDS PASSWORD                                 
DDSOID   DC    C'* '               DDS OFFICE ID                                
DDSDID   DC    C'DDS'              DDS DEPARTMENT ID                            
DDSPID   DC    C'* DDS * '         DDS PERSONAL ID                              
DDSNAMES DS    0CL(L'PERNAMES)     DDS NAME                                     
DDSFNAME DC    CL(L'PERFNAME)'DONOVAN'                                          
DDSMNAME DC    CL(L'PERMNAME)'DATA'                                             
DDSLNAME DC    CL(L'PERLNAME)'SYSTEMS'                                          
DDSONAME DC    CL30'* DONOVAN DATA SYSTEMS *'                                   
*                                                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
RELO     DS    A                                                                
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL16               PARAMETER LIST                               
INCON    DS    XL1                 INPUT PARAMETER CONTROL CODE                 
APERNUM  DS    AL3                 A(PERSON PASSWORD NUMBER)                    
         ORG   *-3                                                              
APERPID  DS    AL3                 A(PERSONAL ID)                               
*                                                                               
RETCON   DS    XL1                 RETURN PARAMETER CONTROL CODE                
APERSON  DS    AL3                 A(PERSOND) DATA BLOCK                        
*                                                                               
RETCODE  DS    XL1                 ROUTINE RETURN CODE                          
ACOMFACS DS    AL3                 A(COMFACS)                                   
*                                                                               
         DS    XL1                 N/D                                          
ASYSFACS DS    AL3                 A(SYSFACS)                                   
*                                                                               
PERSONID DS    CL8                 PERSONAL ID                                  
PERNUM   DS    XL2                 PERSON PASSWORD NUMBER                       
SECAGY   DS    CL2                 SECURITY AGENCY ALPHA ID                     
*                                                                               
DATAMGR  DS    V                   COMFACS ADDRESSES                            
DATCON   DS    V                                                                
GETDAY   DS    V                                                                
*                                                                               
PERSONC  DS    CL(PERLENQ)         LOCAL PERSOND AREA                           
*                                                                               
IOKEY    DS    XL(L'SAPEKEY)       CONTROL FILE KEY                             
IOAREA   DS    2048C               IO AREA                                      
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
         EJECT                                                                  
* SEACSFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE SEACSFILE                                                      
         PRINT ON                                                               
                                                                                
* FADSECTS                                                                      
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
         PRINT ON                                                               
                                                                                
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDPERSOND                                                      
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDPERSON  08/11/04'                                      
         END                                                                    
