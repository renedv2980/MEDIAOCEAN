*          DATA SET DDMQIOA    AT LEVEL 032 AS OF 10/24/00                      
*CATALP MQIOB                                                                   
         TITLE 'DDMQIO - INTERFACE TO MQ SERIES SCHEDULER'                      
***********************************************************************         
* MQIO - FACPAK ONLINE MQ SERIES UTILITY                              *         
*        INTERFACE TO MQ SERIES SHEDULER/SUBMITTER SUBTASK FAMQIO     *         
*                                                                     *         
* PARAMETER LIST SEE DDMQIOPD                                         *         
*                                                                     *         
* COMMANDS INCLUDE:                                                   *         
*                                                                     *         
* 'INIT    ': INITIALISE MQIO (IF NOT CALLED DONE FIRST TIME RESET)   *         
*                                                                     *         
* 'PUT     ': PUT DATA FROM TASK AREA TO FACPAK OUTPUT MQ QUEUE       *         
*                                                                     *         
* 'PUTQ    ': PUT DATA FROM TASK AREA TO SPECIFIC OUTPUT MQ QUEUE     *         
*                                                                     *         
* 'GET     ': GET DATA INTO TASK AREA FROM FACPAK INPUT MQ QUEUE      *         
*                                                                     *         
* 'REPLY   ': PUT REPLY DATA AT END OF FACPAK TRANSACTION FROM TASK   *         
*             DATA AREA TO FACPAK OUTPUT MQ QUEUE                     *         
*                                                                     *         
* 'COMMIT  ': COMMIT MQ TRANSACTIONS FOR FACPAK TASK                  *         
*                                                                     *         
* 'RESET   ': FACPAK CONTROL COMMAND (FROM TASKER) TO RESET STATE     *         
*             OF FACPAK INPUT MQ QUEUES.                              *         
*                                                                     *         
* 'INPUT   ': FACPAK CONTROL COMMAND (FROM TASKER) TO GET DATA INTO   *         
*             TASK AREA AT START OF TRANSACTION FROM FACPAK INPUT     *         
*             MQ QUEUES                                               *         
*             **SET THE "DARMSGFL" TO INFORM FATASKER                 *         
*               PUT THE DATA INTO TASK AREA ROUNTIUE "INDAREM1" IS    *         
*               MOVED TO FATASKER AND RUN AS A TASK.                  *         
*                                                                     *         
* 'END     ': CLEAR OR END CURRENT MQ REQUESTS FOR FACPAK TASKS       *         
*             AFTER MQIO ERROR EVENT                                  *         
*                                                                     *         
* 'JOB     ': BUILD REMOTE JOB REQUEST. OPEN PQ FILE AND PUT MESSAGE  *         
*             TO FACPAK OUTPUT QUEUE                                  *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         PRINT NOGEN                                                            
MQIO     CSECT                                                                  
         ENTRY MQIOECB                                                          
         ENTRY DARMSGFL                                                         
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**MQIO**,RA,R9,CLEAR=YES                             
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          SAVE INPUT PARAMETERS                        
         LA    R6,MQIOC                                                         
         USING MQIOD,R6            R6=A(MQIO LOCAL CONTROL BLOCK)               
         L     RF,=V(SYSFAC)       INTIALISE FACPAK ADDRESSES                   
         ST    RF,ASYSFAC                                                       
         L     RE,VSSB-SYSFACD(RF)                                              
         ST    RE,ASSB                                                          
         L     RE,VTCB-SYSFACD(RF)                                              
         ST    RE,ATCB                                                          
         L     RE,VUTL-SYSFACD(RF)                                              
         ST    RE,AUTL                                                          
         L     RE,VMQIPARM-SYSFACD(RF)                                          
         ST    RE,AMQIOLST                                                      
         MVC   ALCM,VLCM-SYSFACD(RF)                                            
         L     RF,=V(MSGQIN)                                                    
         ST    RF,AMSGQIN                                                       
         L     RE,ASSB                                                          
         MVC   SVMQION,SSBMQION-SSBD(RE)                                        
         L     RE,SSBTKADR-SSBD(RE)                                             
         ST    RE,ATCBTASK                                                      
         L     RF,TCBUTL-TCBD(RE)                                               
         ST    RF,AUTLTASK                                                      
         L     RF,TCBTWA-TCBD(RE)                                               
         ST    RF,ATWA                                                          
         L     RF,TCBCOMF-TCBD(RE)                                              
         ST    RF,ACOMFACS                                                      
         L     RF,TCBTIA-TCBD(RE)                                               
         ST    RF,ATIA                                                          
*                                                                               
         PROT  OFF                 DISABLE CALLER'S STORAGE PROTECTION          
         B     PROCCMD             GO PROCESS COMMAND                           
         EJECT                                                                  
EXIT     EQU   *                                                                
         L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(1,R1),PMQRET      RETURN CONTROL CODE                          
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
         XMOD1 1                                                                
*                                                                               
* HANDLE EXIT                                                                   
*                                                                               
EXITOK   EQU   *                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND TO MQIO FROM CALLER                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCCMD  EQU   *                   PROCESS CALLER COMMAND                       
         SR    RF,RF                                                            
         ICM   RF,7,PMQCMD         GET COMMAND FROM INPUT PARAMETER             
         XC    PMQRET,PMQRET       CLEAR RETURN STATUS CODE                     
         CLC   =CL8'INIT',0(RF)                                                 
         BE    PROCINIT                                                         
         CLC   =CL8'PUT',0(RF)                                                  
         BE    PROCPUT                                                          
         CLC   =CL8'PUTQ',0(RF)                                                 
         BE    PROCPUTQ                                                         
         CLC   =CL8'GET',0(RF)                                                  
         BE    PROCGET                                                          
         CLC   =CL8'RESET',0(RF)                                                
         BE    PROCSET                                                          
         CLC   =CL8'INPUT',0(RF)                                                
         BE    PROCINP                                                          
         CLC   =CL8'REPLY',0(RF)                                                
         BE    PROCRPY                                                          
         CLC   =CL8'COMMIT',0(RF)                                               
         BE    PROCCMT                                                          
         CLC   =CL8'END',0(RF)                                                  
         BE    PROCEND                                                          
         CLC   =CL8'JOB',0(RF)                                                  
         BE    PROCJOB                                                          
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'INIT' TO INITILAISE MQIO                           *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCINIT EQU   *                                                                
         CLI   MQLINIT,0           CHECK ALREADY INITIALISED                    
         BE    EXITOK                                                           
         BAS   RE,INITMQL          CALL MQIO INITIALISATION ROUTINE             
         MVI   MQLINIT,0           SET INITIALISED STATUS FLAG                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'PUT' TO PUT DATA FROM TASK AREA TO FACPAK OUTPUT   *         
* QUEUE                                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCPUT  EQU   *                                                                
         MVI   MQIOACT,MQIOAPUT                                                 
         L     RF,PMQDATA                                                       
         STCM  RF,15,MQIOABUF      A(DATA AREA)                                 
*                                                                               
         LA    R0,MQIOMSGD         CLEAR MQ MESAGE DESCRIPTOR                   
         L     RE,=A(MSGDESC)                                                   
         ICM   R1,15,=AL4(MSGDESC_LENGTH)                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                  INITIALISE VALUES IN MQ MESSAGE              
*                                  DESCRIPTOR                                   
         XC    MQIOMSGD_MSGID,MQIOMSGD_MSGID  CLEAR MQ MESSAGE ID               
         L     RF,PMQDLEN                                                       
         STCM  RF,15,MQIOLBUF                                                   
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'PUTQ' TO PUT DATA FROM TASK AREA TO SPECIFIC       *         
* OUTPUT QUEUE                                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCPUTQ EQU   *                                                                
         MVI   MQIOACT,MQIOAPTQ                                                 
         L     RF,PMQDATA                                                       
         STCM  RF,15,MQIOABUF      A(DATA AREA)                                 
         L     RF,PMQAQNM                                                       
         STCM  RF,15,MQIOAQNM      A(QUEUNAME))                                 
*                                                                               
         LA    R0,MQIOMSGD         CLEAR MQ MESAGE DESCRIPTOR                   
         L     RE,=A(MSGDESC)                                                   
         ICM   R1,15,=AL4(MSGDESC_LENGTH)                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                  INITIALISE VALUES IN MQ MESSAGE              
*                                  DESCRIPTOR                                   
         XC    MQIOMSGD_MSGID,MQIOMSGD_MSGID  CLEAR MQ MESSAGE ID               
         L     RF,PMQDLEN                                                       
         STCM  RF,15,MQIOLBUF                                                   
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'REPLY' TO PUT REPLY DATA AT END OF FACPAK          *         
*         TRANSACTION FROM TASK DATA AREA TO FACPAK OUTPUT MQ QUEUE   *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCRPY  EQU   *                   PROCESS REPLY DATA TO FAMQIO                 
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB1                                                          
         WTO   '*FACPAK* DDMQIO PROCRPY',MCSFLAG=HRDCPY                         
WTOLAB1  EQU   *                                                                
*                                                                               
         LA    R0,MQIOMSGD                                                      
         L     RE,PMQDATA                                                       
         LA    R1,MSGDESC_LENGTH                                                
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         XC    MQIOMSGD_MSGID,MQIOMSGD_MSGID  CLEAR MQ MESSAGE ID               
         L     RF,PMQDATA                                                       
         LA    RF,MSGDESC_LENGTH(RF)                                            
         STCM  RF,15,MQIOABUF                                                   
         ICM   RF,15,PMQDLEN                                                    
         BNZ   PRPY100                                                          
         LA    RE,2024                                                          
         STCM  RE,15,MQIOLBUF                                                   
         MVI   MQIOACT,MQIOACMT                                                 
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB2                                                          
         WTO   '*FACPAK* DDMQIO COMMIT',MCSFLAG=HRDCPY                          
WTOLAB2  EQU   *                                                                
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
*                                                                               
PRPY100  EQU   *                                                                
         STCM  RF,15,MQIOLBUF                                                   
         MVI   MQIOACT,MQIOARPY                                                 
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB3                                                          
         WTO   '*FACPAK* DDMQIO REPLY',MCSFLAG=HRDCPY                           
WTOLAB3  EQU   *                                                                
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
         EJECT                                                                  
         SPACE 1                                                                
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'COMMIT' TO COMMIT MQ TRANSACTIONS FOR FACPAK TASK  *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCCMT  EQU   *                                                                
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB4                                                          
         WTO   '*FACPAK* DDMQIO PROCCMT',MCSFLAG=HRDCPY                         
WTOLAB4  EQU   *                                                                
*                                                                               
         LA    R0,MQIOMSGD                                                      
         L     RE,PMQDATA                                                       
         LA    R1,MSGDESC_LENGTH                                                
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         XC    MQIOMSGD_MSGID,MQIOMSGD_MSGID  CLEAR MQ MESSAGE ID               
         L     RF,PMQDATA                                                       
         LA    RF,MSGDESC_LENGTH(RF)                                            
         STCM  RF,15,MQIOABUF                                                   
         ICM   RF,15,PMQDLEN                                                    
         BNZ   PCMT100                                                          
         LA    RE,2024                                                          
         STCM  RE,15,MQIOLBUF                                                   
         MVI   MQIOACT,MQIOACMT                                                 
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB5                                                          
         WTO   '*FACPAK* DDMQIO COMMIT',MCSFLAG=HRDCPY                          
WTOLAB5  EQU   *                                                                
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
*                                                                               
PCMT100  EQU   *                                                                
         STCM  RF,15,MQIOLBUF                                                   
         MVI   MQIOACT,MQIOARPY                                                 
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB6                                                          
         WTO   '*FACPAK* DDMQIO REPLY',MCSFLAG=HRDCPY                           
WTOLAB6  EQU   *                                                                
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'GET' TO GET DATA INTO TASK AREA FROM FACPAK INPUT  *         
* QUEUE                                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
PROCGET  EQU   *                   PROCESS GET DATA REQUEST TO FAMQIO           
         MVI   MQIOACT,MQIOAGET                                                 
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
         EJECT                                                                  
*                                  HERE TO POST MQIO SCHEDULER WITH             
REQUEST  EQU   *                   REQUEST FROM FACPAK TASK                     
         BAS   RE,GETATC                                                        
         BE    *+6                 ?? NEED THIS IN CASE ABEND SUSPENDED         
         DC    H'0'                                                             
*                                                                               
         L     R8,ATCBTASK                                                      
         USING TCBD,R8                                                          
         STCM  R6,15,TCBMQDAT      SAVE A(MQIOD DATA BLOCK)                     
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB7                                                          
         WTO   '*FACPAK* DDMQIO BEFORE MQWAIT',MCSFLAG=HRDCPY                   
WTOLAB7  EQU   *                                                                
*                                                                               
         TBIN  MILI                                                             
         ST    R1,TCBMQTIM         SET POST TIME                                
         XC    MQRETECB,MQRETECB                                                
         LA    R1,MQRETECB                                                      
         STCM  R1,15,TCBMQERT      SAVE A(MQIO RETURN ECB)                      
         LA    R1,TCBMQERQ                                                      
         POST  (1)                                                              
         LA    R1,MQRETECB                                                      
         O     R1,=X'FF000000'     SET 'DO NOT WAIT ON THIS ECB'                
         GOTO1 =V(MQWAIT)          WAIT FOR MQIO SCHEDULER TO COMPLETE          
*                                                                               
         XC    TCBMQTIM,TCBMQTIM   CLEAR POST TIME                              
         XC    TCBMQDAT,TCBMQDAT                                                
         ICM   R1,15,TCBMQERT                                                   
         XC    TCBMQERT,TCBMQERT                                                
         MVI   0(R1),0                                                          
         OC    0(4,R1),0(R1)                                                    
         BZ    REQU100                                                          
         MVI   PMQRET,MQPCRERQ                                                  
         ICM   RF,15,PMQAERB                                                    
         BZ    REQU100                                                          
         MVC   0(4,RF),MQIOCC                                                   
         MVC   4(8,RF),MQIORC                                                   
*                                                                               
REQU100  EQU   *                                                                
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB8                                                          
         WTO   '*FACPAK* DDMQIO AFTER MQWAIT',MCSFLAG=HRDCPY                    
WTOLAB8  EQU   *                                                                
         B     EXITOK                                                           
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS CONTROL COMMAND 'RESET' FROM FACPAK TASKER TO RESET STATE   *         
*         OF FACPAK INPUT MQ QUEUES                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCSET  EQU   *                                                                
         CLI   MQLINIT,0           TEST INITLIASED STATUS FLAG                  
         BE    PSET010                                                          
         BAS   RE,INITMQL          CALL MQIO INITIALISATION ROUTINE             
         MVI   MQLINIT,0           SET INITIALISATION STATUS FLAG               
*                                                                               
*                                  PROCESS MQIO CONTROL PARAMETER LIST          
*                                  FOR INPUT QUEUES AWAITING RESET              
PSET010  EQU   *                                                                
         L     R4,AMQIOLST         R4=A(MQIO CONTROL PARAMETER LIST)            
         USING MQIOLD,R4                                                        
PSET020  TM    MQIOLFLG,MQIOLCTL   TEST FACPAK CONTROL QUEUE                    
         BO    PSETCT                                                           
         TM    MQIOLFLG,MQIOLRD    TEST FACPAK INPUT READ QUEUE                 
         BO    PSETRD                                                           
PSETNXT  ICM   R4,15,MQIOLNXT      GET NEXT ENTRY IN MQIOLST                    
         BZ    EXITOK                                                           
         B     PSET020                                                          
*                                                                               
PSETCT   EQU   *                   FACPAK CONTROL Q RESET                       
         TM    MQIOLFLG,MQIOLBSY   IGNORE IF CURRENTLY BUSY                     
         BO    PSETNXT                                                          
         BAS   RE,GETATC           GET A(FAATC ENTRY)                           
         BNE   EXITOK                                                           
         BAS   RE,SETCT            RESET FACPAK CONTROL QUEUE                   
         BNE   EXITOK                                                           
         B     PSETNXT                                                          
*                                                                               
PSETRD   EQU   *                   FACPAK INPUT Q RESET                         
         BAS   RE,GETATC           GET A(FAATC ENTRY)                           
         BNE   EXITOK                                                           
         BAS   RE,SETRD            RESET FACPKA INPUT QUEUE                     
         BNE   EXITOK                                                           
         B     PSETNXT                                                          
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS CONTROL COMMAND 'INPUT' FROM FACPAK TASKER TO GET DATA      *         
*         INTO TASK AREA AT START OF TRANSACTION FROM FACPAK INPUT    *         
*         MQ QUEUES                                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCINP  EQU   *                   PROCESS Q DATA RETURNED FROM FAMQIO          
         XC    MQIOECB,MQIOECB                                                  
*                                  PROCESS MQIO CONTROL PARAMETER LIST          
*                                  FOR INPUT QUEUES DATA                        
         L     R4,AMQIOLST                                                      
         USING MQIOLD,R4                                                        
PINP010  EQU   *                                                                
         TM    MQIOLECB,X'40'      TEST INPUT ECB FLAGGED BY FAMQIO             
         BZ    PINPNXT                                                          
         XC    MQIOLECB,MQIOLECB   CLEAR ECB FOR NEXT TIME                      
         TM    MQIOLFLG,MQIOLCTL   TEST FACPAK CONTROL QUEUE                    
         BO    PINPCT                                                           
         TM    MQIOLFLG,MQIOLRD    TEST FACPAK INPUT READ QUEUE                 
         BO    PINPRD                                                           
PINPNXT  ICM   R4,15,MQIOLNXT      GET NEXT ENTRY IN MQIOLST                    
         BZ    EXITOK                                                           
         B     PINP010                                                          
*                                                                               
PINPCT   EQU   *                   FACPAK CONTROL Q DATA FROM FAMQIO            
         BAS   RE,INPCT            GET INPUT DATA FROM CONTROL QUEUE            
         BNE   EXITOK                                                           
         NI    MQIOLFLG,X'FF'-MQIOLBSY                                          
         B     PINPNXT                                                          
*                                                                               
PINPRD   EQU   *                   FACPAK INPUT Q DATA FROM FAMQIO              
         BAS   RE,INPRD            GET INPUT DATA FROM INPUT QUEUE              
         BNE   EXITOK                                                           
         B     PINPNXT                                                          
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'END' TO END OR CLEAR CURRENT OUTSTANDING MQ        *         
*         REQUESTS FACPAK TASKS AFTER MQIO ERROR EVENT                *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCEND  EQU   *                                                                
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLAB9                                                          
         WTO   '*FACPAK* DDMQIO END CALLED',MCSFLAG=HRDCPY                      
WTOLAB9  EQU   *                                                                
*                                                                               
         L     R3,ATCB             SEARCH TCB FOR MQIO REQUESTS PENDING         
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING TCBD,R3                                                          
*                                                                               
PEND010  TM    TCBFLAG3,TCBMQWT                                                 
         BZ    PEND020                                                          
         ICM   R1,15,TCBMQERT                                                   
         BZ    PEND020                                                          
         LA    R2,999                                                           
         POST  (1),(2)                                                          
*                                                                               
PEND020  BXLE  R3,R4,PEND010                                                    
         DROP  R3                                                               
*                                                                               
         XC    MQIOECB,MQIOECB                                                  
*                                  SEARCH MQIO CONTROL PARAMETER LIST           
*                                  FOR PENDING EVENTS                           
         L     R4,AMQIOLST                                                      
         USING MQIOLD,R4                                                        
PEND100  EQU   *                                                                
         TM    MQIOLFLG,MQIOLRD                                                 
         BO    PEND120                                                          
PEND110  ICM   R4,15,MQIOLNXT                                                   
         BZ    PEND200                                                          
         B     PEND100                                                          
*                                                                               
PEND120  EQU   *                                                                
         ICM   R6,15,MQIOLC                                                     
         USING UTLD,R7                                                          
         ICM   R7,15,MQIOLUTL                                                   
         BZ    PEND110                                                          
         NI    TSTAT2,X'FF'-TSTATTIP                                            
         B     PEND110                                                          
*                                                                               
PEND200  EQU   *                                                                
         B     EXITOK                                                           
         DROP  R4,R7                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* PROCESS COMMAND 'JOB' TO BUILD REMOTE JOB. OPEN PQ FILE AND PUT     *         
*         MESSAGE TO FACPAK OUTPUT MQ QUEUE.                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PROCJOB  EQU   *                                                                
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABA                                                          
         WTO   '*FACPAK* DDMQIO JOB CALLED',MCSFLAG=HRDCPY                      
WTOLABA  EQU   *                                                                
*                                                                               
         LA    R5,SPOKAREA                                                      
         USING SPOOK,R5                                                         
         XC    SPOOK(SPOOKXL),SPOOK                                             
         MVC   SPOOKSYS,=CL2'C '                                                
         MVC   SPOOKDID,=CL3'MQB'                                               
*                                                                               
         L     R2,ATWA                                                          
*                                                                               
         MVI   REQCON,0            NO REQUEST CARDS                             
         MVI   SPOOKWEN,8          SET FOR MQ REMOTE SOON JOB                   
*                                                                               
         MVC   JCLBOOK,SPACES      SET UP JCL BOOK FOR SOON                     
         MVC   JCLBOOK(2),SPOOKSYS                                              
         MVI   JCLBOOK+2,C'0'                                                   
*        MVC   SPOOKJCL,QHDR+26                                                 
*        MVC   SPOOKDES,QHDR+11                                                 
         MVC   JCLBOOK+3(2),SPOOKJCL                                            
         ICM   R3,15,ACOMFACS       FIND CALLOV                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING COMFACSD,R3                                                      
         L     RF,CCALLOV                                                       
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A25' SPOON CORE RESIDENT PHASE                 
         GOTO1 (RF),DMCB                                                        
         L     RF,DMCB                                                          
         ST    R2,DMCB                                                          
         MVI   DMCB,X'FF'          REQUEST A(PRTQUE KEY) IN P6                  
         GOTO1 (RF),DMCB,,(REQCON,WORK),(R3),JCLBOOK,(R5)                       
         L     RE,DMCB+20          RETURNED JESKEY                              
         OC    0(7,RE),0(RE)       TEST VALID KEY RETURNED (JCL ERROR)          
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   REPSUBID,2(RE)                                                   
         MVC   REPNUM,6(RE)                                                     
         L     RF,ATCBTASK                                                      
         MVC   REPCIA,TCBSJCIA-TCBD(RF)                                         
*                                                                               
*        MVC   PHEADER+RQHOSID-RQHHDR(L'RQHOSID),2(RE) SET REP ID               
*        MVC   PHEADER+RQHOSNO-RQHHDR(L'RQHOSNO),6(RE) SET REP NO               
*        GOTO1 CDATAMGR,DMCB,(X'20',DMADD),=C'REQUEST',PHEADER,PHEADER          
*                                                                               
*        GOTO1 GETID,CUUSER                                                     
*                                                                               
         DROP  R3                                                               
         MVC   USERID,=CL10'DDS1'                                               
         MVC   ROUTECOD,=CL8'DAPREQMQ'                                          
*                                                                               
         L     R4,PMQDATA                                                       
         USING DDMQBHD,R4                                                       
         MVC   DDMQBHD_MQB_MID,DDMQBHC_MQB_MID                                  
         MVC   DDMQBHD_STRUC_ID,=CL5'00001'                                     
         MVC   DDMQBHD_VERSION,=CL3'001'                                        
         MVC   DDMQBHD_PROC_ID,DDMQBHC_PROCESS_EXREP                            
         MVC   DDMQBHD_SOURCE_CODE,USERID                                       
         MVC   DDMQBHD_ROUTE_CODE,ROUTECOD                                      
         MVC   DDMQBHD_MSG_CODE,=CL5'00001001'                                  
         MVC   DDMQBHD_STATUS_CODE,=CL5'00000'                                  
         LA    R3,DDMQBHD_STRUC_1_DATA                                          
         USING DDMQEXRD,R3                                                      
         MVC   DDMQEXR_REPORT_SUB_ID,REPSUBID                                   
         EDIT  REPNUM,(5,DDMQEXR_REPORT_NUM),ZERO=NOBLANK,FILL=0                
         EDIT  REPCIA,(5,DDMQEXR_REPORT_CIA),ZERO=NOBLANK,FILL=0                
         LA    RF,DDMQBHD_STRUC_1_LENQ                                          
         LA    RE,DDMQEXR_MSG_1_LENQ                                            
         AR    RF,RE                                                            
*                                                                               
         MVI   MQIOACT,MQIOAPUT    PREPARE TO PUT DATA TO OUTPUT QUEUE          
         STCM  R4,15,MQIOABUF      A(DATA AREA)                                 
         STCM  RF,15,MQIOLBUF                                                   
         DROP  R3,R4                                                            
*                                                                               
         LA    R0,MQIOMSGD         CLEAR MQ MESAGE DESCRIPTOR                   
         L     RE,=A(MSGDESC)                                                   
         ICM   R1,15,=AL4(MSGDESC_LENGTH)                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                  INITIALISE VALUES IN MQ MESSAGE              
*                                  DESCRIPTOR                                   
         XC    MQIOMSGD_MSGID,MQIOMSGD_MSGID  CLEAR MQ MESSAGE ID               
*                                                                               
         B     REQUEST             GO SCHEDULE REQUEST FROM FACPAK TASK         
*                                                                               
PJOBX    EQU   *                                                                
         B     EXITOK                                                           
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* ROUTINE TO SET FACPAK INPUT QUEUE READY FOR NEXT INCOMING MESSAGE   *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING MQIOLD,R4                                                        
SETRD    NTR1                                                                   
         USING UTLD,R7                                                          
         ICM   R7,15,MQIOLUTL                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    TSTAT2,TSTATTIP                                                  
         BO    SERDOK                                                           
*                                                                               
         TM    MQIOLFLG,MQIOLBSY                                                
         BZ    SERD010                                                          
*                                                                               
         TM    MQIOLFLG,MQIOLWTG                                                
         BZ    SERDOK                                                           
         NI    MQIOLFLG,X'FF'-(MQIOLWTG+MQIOLBSY)                               
*                                                                               
SERD010  EQU   *                                                                
         ICM   R3,15,ATCENTRY      GET A(ATC ENTRY)                             
         USING ATCD,R3             R3=A(ATC)                                    
*                                                                               
         LA    R1,ATCECB                                                        
         POST  (1)                                                              
*                                                                               
         B     SERDOK                                                           
*                                                                               
SERDOK   SR    RC,RC                                                            
SERDNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* ROUTINE TO SET FACPAK CONTROL QUEUE READY FOR NEXT INCOMING MESSAGE *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING MQIOLD,R4                                                        
SETCT    NTR1                                                                   
         ICM   R3,15,ATCENTRY      GET A(ATC ENTRY)                             
         USING ATCD,R3             R3=A(ATC)                                    
*                                                                               
         LA    R1,ATCECB                                                        
         POST  (1)                                                              
*                                                                               
         B     SECTOK                                                           
*                                                                               
SECTOK   SR    RC,RC                                                            
SECTNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* ROUTINE TO GET INPUT DATA FROM FACPAK INPUT READ QUEUE AND SCHEDULE *         
* FACPAK TRANSACTION TO PROCESS THAT DATA                             *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING MQIOLD,R4                                                        
INPRD    NTR1                                                                   
         ICM   R6,15,MQIOLC        R6=A(MQIO CONTROL BLOCK)                     
*                                                                               
         USING UTLD,R7                                                          
         ICM   R7,15,MQIOLUTL      R7=A(DUMMY MQ UTL)                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OI    TSTAT2,TSTATTIP     SET UTL ENTRY 'IN PROGRESS'                  
         MVI   TSYS,0              SET UP SRMQP00                               
         MVI   TSVCREQ,X'01'                                                    
         MVI   TSVCREQ+1,X'6B'                                                  
         XC    TSVDATA1,TSVDATA1   CLEAR THIS FOR SAFETY                        
*                                                                               
         ICM   R3,15,TBUFF         BUILD MESSAGE INTO UTL BUFFER                
         SH    R3,=H'8'                                                         
         USING TBHD,R3                                                          
         STCM  R7,15,TBHAUTL                                                    
         MVI   TBHINDS,0                                                        
         ICM   RF,15,MQIOLDAT                                                   
         LA    RE,MSGDESC_LENGTH                                                
         AR    RF,RE                                                            
         STCM  RF,3,TBHMSGL                                                     
*                                                                               
         ICM   R0,15,TBUFF                                                      
         LA    RE,MQIOMSGD                                                      
         LA    R1,MSGDESC_LENGTH                                                
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                  QUEUE SRMQP00 THRU MSGQIN                    
         GOTO1 AMSGQIN,DMCB,ASYSFAC,TBUFF,(R7)                                  
*                                                                               
         OI    MQIOLFLG,MQIOLWTG   FLAG THIS INPUT QUEUE WAITING                
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABD                                                          
         WTO   '*FACPAK* DDMQIO AFTER INTRD',MCSFLAG=HRDCPY                     
WTOLABD  EQU   *                                                                
*                                                                               
         B     INRDOK                                                           
*                                                                               
INRDOK   SR    RC,RC                                                            
INRDNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R3,R4,R7                                                         
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* ROUTINE TO GET INPUT DATA FROM FACPAK CONTROL QUEUE AND CALL THE    *         
* ROUTINE TO PROCESS THAT DATA                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING MQIOLD,R4                                                        
INPCT    NTR1                                                                   
         ICM   R6,15,MQIOLC                                                     
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   INCT010                                                          
         LA    RF,80                                                            
         STCM  RF,3,WORK                                                        
         LA    RF,WORK+2                                                        
         MVC   0(40,RF),=CL40'*FACMQ1* DDMQIO DATA AFTER INTCT: '               
         ICM   RE,15,MQIOABUF                                                   
         MVC   34(46,RF),0(RE)                                                  
         LA    R3,WORK                                                          
         WTO   TEXT=(R3),MCSFLAG=HRDCPY                                         
INCT010  EQU   *                                                                
*                                                                               
*                                  FIND ENTRY IN CQIMTAB                        
         ICM   R2,15,MQIOABUF                                                   
         LA    R5,CQIMTAB                                                       
INCT100  EQU   *                                                                
         CLI   0(R5),X'FF'                                                      
         BE    INCT200                                                          
         CLC   4(8,R5),0(R2)                                                    
         BE    INCT110                                                          
         LA    R5,12(R5)                                                        
         B     INCT100                                                          
*                                                                               
INCT110  EQU   *                                                                
         ICM   RF,15,0(R5)                                                      
         ICM   R3,15,MQIOLBUF                                                   
         GOTO1 (RF),DMCB,(R2),(R3)                                              
         B     INCTOK                                                           
*                                                                               
INCT200  EQU   *                                                                
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABE                                                          
         WTO   '*FACPAK* DDMQIO UNDEFINED CQ INPUT',MCSFLAG=HRDCPY              
WTOLABE  EQU   *                                                                
         B     INCTOK                                                           
*                                                                               
INCTOK   SR    RC,RC                                                            
INCTNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         EJECT                                                                  
GETATC   NTR1                      LOCATE A FREE FAMQIO SCHEDULER               
         L     RE,ASSB                                                          
         L     R3,SSBAATC-SSBD(RE)                                              
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING ATCD,R3             R3=A(ATC)                                    
*                                                                               
GATC010  CLI   ATCTYPE,ATCTMQIO    TEST MQIO SUB TASK                           
         BNE   GATC020                                                          
         TM    ATCSTAT1,ATCSATCH   TEST TASK IS ATTACHED                        
         BZ    GATC020                                                          
         TM    ATCSTAT1,ATCSBUSY   TEST TASK IS BUSY                            
         BNZ   GATC020                                                          
         OC    ATCATCB,ATCATCB     TEST ALLOCATED TO ANOTHER TASK               
         BNZ   GATC020                                                          
         TM    ATCSTAT1,ATCSERRS   TEST TASK IS IN ERROR                        
         BNZ   GATC020                                                          
         B     GATC040                                                          
*                                                                               
GATC020  BXLE  R3,R4,GATC010                                                    
         CLI   ATCFLAG,C'N'                                                     
         BE    GATCNO                                                           
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABF                                                          
         WTO   '*FACPAK* DDMQIO ATC NOT FOUND',MCSFLAG=HRDCPY                   
WTOLABF  EQU   *                                                                
         MVI   ATCFLAG,C'N'                                                     
         B     GATCNO                                                           
*                                                                               
GATC040  EQU   *                                                                
         STCM  R3,15,ATCENTRY                                                   
         CLI   ATCFLAG,C'Y'                                                     
         BE    GATCOK                                                           
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABG                                                          
         WTO   '*FACPAK* DDMQIO ATC FOUND',MCSFLAG=HRDCPY                       
WTOLABG  EQU   *                                                                
         MVI   ATCFLAG,C'Y'                                                     
         B     GATCOK                                                           
*                                                                               
GATCOK   SR    RC,RC                                                            
GATCNO   LTR   RC,RC                                                            
         XIT1                                                                   
         EJECT                                                                  
INITMQL  NTR1                      INITIALISE MQIOLST                           
         USING MQIOLD,R4                                                        
         L     R4,AMQIOLST                                                      
IMQL010  TM    MQIOLFLG,MQIOLRD                                                 
         BO    IMQL100                                                          
IMQL020  ICM   R4,15,MQIOLNXT                                                   
         BZ    IMQLOK                                                           
         B     IMQL010                                                          
*                                                                               
         USING UTLD,R7                                                          
IMQL100  EQU   *                                                                
         GOTO1 ALCM,DMCB,4                                                      
         LTR   R3,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         GOTO1 ALCM,DMCB,VTGETUTL                                               
         LTR   R7,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R7,MQIOLUTL                                                      
         ST    R3,TBUFF                                                         
         OI    TSTAT1,TSTATDDS     SET DDS/3270/DUMMY TERMINAL                  
         OI    TTYPE2,TTYPEMQ                                                   
         OI    TTYPE2,TTYPEDUM                                                  
         OI    TFLAG,TFLAGIRB      SET INHIBIT RECEIVING BROADCASTS             
         MVC   TSYM,=C'DUMMYMQT'                                                
*                                                                               
         L     RE,ASSB                                                          
         USING SSBD,RE                                                          
         LH    RF,SSBDTRMS                                                      
         LA    RF,1(RF)                                                         
         STH   RF,SSBDTRMS                                                      
         LH    RF,SSBTRMS                                                       
         LA    RF,1(RF)                                                         
         STH   RF,SSBTRMS                                                       
         DROP  RE                                                               
*                                                                               
         ICM   R6,15,MQIOLC                                                     
*                                                                               
         LA    RF,MSGDESC_LENGTH                                                
         AR    R3,RF                                                            
         STCM  R3,15,MQIOABUF                                                   
         LA    RE,2024                                                          
         SR    RE,RF                                                            
         STCM  RE,15,MQIOLBUF                                                   
*                                                                               
         CLC   SVMQION,=F'2'                                                    
         BNE   WTOLABH                                                          
         WTO   '*FACPAK* DDMQIO ATC FOUND',MCSFLAG=HRDCPY                       
WTOLABH  EQU   *                                                                
*                                                                               
         B     IMQL020                                                          
*                                                                               
IMQLOK   SR    RC,RC                                                            
IMQLNO   LTR   RC,RC                                                            
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
*                                                                    *          
* ROUTINE TO PROCESS CONTROL Q INPUT DATA FOR 'DAREMSG1'             *          
*                                                                    *          
* PARAMETERS:                                                        *          
* P1 = A(DATA BUFFER)                                                *          
* P2 = LENGTH DATA BUFFER                                            *          
*                                                                    *          
**********************************************************************          
         SPACE 1                                                                
INDAREM1 NTR1                    ROUTINE TO PROCESS CONTROL Q DATA              
         LM    R2,R3,0(R1)       R2=A(DATA BUFFER), R3=LENGTH DATA              
*                                                                               
         CLC   SVMQION,=F'2'     WRITE OUT MESSAGE AND DATA TO JESMSG           
         BNE   IDM120                                                           
         LR    RE,R3                                                            
         CLM   RE,15,=AL4(30)                                                   
         BH    *+8                                                              
         LA    RE,30                                                            
         STCM  RE,3,WORK                                                        
         LA    RF,WORK+2                                                        
         MVC   0(34,RF),=CL34'*FACMQ1* DDMQIO CQI DAREMSG1:'                    
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   34(0,RF),0(R2)                                                   
         LA    R5,WORK                                                          
         WTO   TEXT=(R5),MCSFLAG=HRDCPY                                         
*                                                                               
* SET THE DARE MSG FLAG "DARMSGFL" TO TELL FATASKER TO PROCESS THE              
* MESSAGE AS A TASK                                                             
*                                                                               
         MVI   DARMSGFL,X'80'                                                   
*                                                                               
IDM120   XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
MQIOECB  DC    F'0'                MQIO INTERFACE ECB                           
         DS    0D                                                               
*                                                                               
DARMSGFL DC    F'0'                DARE MSG FLAG, FATASKER CHECK IT             
         DS    0D                                                               
*                                                                               
MQLINIT  DC    X'FF'               MQIO LIST INITIALISED FLAG                   
ATCFLAG  DC    C' '                ATC FOUND FLAG                               
         EJECT                                                                  
***********************************************************************         
* MQIO SHCEDULER CONTROL Q INPUT MESSAGE TABLE                        *         
***********************************************************************         
         SPACE 1                                                                
CQIMTAB  DS    0D                                                               
         DC    AL4(INDAREM1)                                                    
         DC    CL8'DAREMSG1'                                                    
*                                                                               
CQIMTABX DC    X'FF'                                                            
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    80C' '                                                           
FFS      DC    80X'FF'                                                          
DMOPEN   DC    C'OPEN   '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMADD    DC    C'DMADD  '                                                       
DMWRT    DC    C'DMWRT  '                                                       
DMCLSE   DC    C'DMCLSE '                                                       
DMFAST   DC    C'DMFAST '                                                       
GFILE    DC    C'GFILE  '                                                       
PRTQUE   DC    C'PRTQUE '                                                       
SYSFLES  DC    C'SYSFLES'                                                       
GETREC   DC    C'GETREC '                                                       
PUTREC   DC    C'PUTREC '                                                       
ADDREC   DC    C'ADDREC '                                                       
DMRFIL   DC    C'RECOVER'                                                       
CONTROL  DC    C'CONTROL'                                                       
CTFILE   DC    C'CTFILE '                                                       
GENDIR   DC    C'GENDIR '                                                       
GENFIL   DC    C'GENFIL '                                                       
         EJECT                                                                  
***********************************************************************         
*  MQ API CONSTANTS                                                   *         
***********************************************************************         
         CMQA LIST=NO                                                           
*                                                                               
OBJDESC    CMQODA  DSECT=NO,LIST=YES        OBJECT DESCRIPTOR                   
MSGDESC    CMQMDA  DSECT=NO,LIST=YES        MESSAGE DESCRIPTOR                  
PUTMSGOPTS CMQPMOA DSECT=NO,LIST=YES        PUT MESSAGE OPTIONS                 
GETMSGOPTS CMQGMOA DSECT=NO,LIST=YES        GET MESSAGE OPTIONS                 
*                                                                               
*                                                                               
         EJECT                                                                  
* DDMQBMC                                                                       
       ++INCLUDE DDMQBMC                                                        
         SPACE 1                                                                
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
PLIST    DS    8F                                                               
RELO     DS    A                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL24               PARAMETER LIST                               
PMQRET   DS    AL1                 RETURN CODE                                  
PMQCMD   DS    AL3                 A(MQIO COMMAND)                              
PMQDATA  DS    AL4                 A(DATA)                                      
PMQDLEN  DS    AL4                 DATA LENGTH                                  
PMQAQNM  DS    AL4                 A(MQ QUEUE NAME)                             
PMQAIOB  DS    AL4                 A(IO BUFFER)                                 
PMQAERB  DS    AL4                 A(ERROR INFO BLOCK)                          
*                                                                               
AMQIOLST DS    A                                                                
AMQIT    DS    A                                                                
*                                                                               
ATCENTRY DS    A                                                                
*                                                                               
MQRETECB DS    F                                                                
*                                                                               
ASYSFAC  DS    A                                                                
AMSGQIN  DS    A                                                                
ASSB     DS    A                                                                
ATCB     DS    A                                                                
ATCBTASK DS    A                                                                
AUTL     DS    A                                                                
AUTLTASK DS    A                                                                
ALCM     DS    A                                                                
ATWA     DS    A                                                                
ATIA     DS    A                                                                
ACOMFACS DS    A                                                                
*                                                                               
SVMQION  DS    F                                                                
USERID   DS    CL10                                                             
ROUTECOD DS    CL8                                                              
REPSUBID DS    CL3                                                              
REPNUM   DS    XL2                                                              
REPCIA   DS    XL2                                                              
PHEADER  DS    XL54                                                             
*                                                                               
WORK     DS    CL255                                                            
MQIOC    DS    XL(MQIODLQ)                                                      
DATALEN  DS    F                                                                
*                                                                               
JCLBOOK  DS    CL10                                                             
REQCON   DS    CL1                                                              
SPOKAREA DS    CL(SPOOKXL)         SPOOK AREA                                   
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* DDMQBMD                                                                       
       ++INCLUDE DDMQBMD                                                        
         SPACE 1                                                                
* FATBHD                                                                        
       ++INCLUDE FATBHD                                                         
         SPACE 1                                                                
* FADSECTS                                                                      
       ++INCLUDE FADSECTS                                                       
         SPACE 1                                                                
* DDMQIOD                                                                       
       ++INCLUDE DDMQIOD                                                        
* DDMQIOPD                                                                      
       ++INCLUDE DDMQIOPD                                                       
         SPACE 1                                                                
* DDSPOOK                                                                       
       ++INCLUDE DDSPOOK                                                        
         SPACE 1                                                                
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
         EJECT                                                                  
* DDBSPARA                                                                      
       ++INCLUDE DDBSPARA                                                       
         EJECT                                                                  
* DMREQHDRA                                                                     
       ++INCLUDE DMREQHDRA                                                      
         EJECT                                                                  
* FATABSDEQU                                                                    
       ++INCLUDE FATABSDEQU                                                     
         EJECT                                                                  
* FATABSDAR                                                                     
       ++INCLUDE FATABSDAR                                                      
         EJECT                                                                  
* DMSPACED                                                                      
       ++INCLUDE DMSPACED                                                       
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032DDMQIOA   10/24/00'                                      
         END                                                                    
