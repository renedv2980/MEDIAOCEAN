*          DATA SET TIMPK      AT LEVEL 006 AS OF 08/12/91                      
*CATALP TIMPK                                                                   
         TITLE 'TIMPK  - CONVERT TO MILITARY TIME'                              
         SPACE 3                                                                
*        PARAM1     BYTE    0   NOT USED ON INPUT                               
*                               ON OUTPUT CONTAINS LENGTH OF VALID              
*                               FIELD OR X'00' IF INVALID                       
*                   BYTES 1-3   A(INPUT)                                        
         SPACE 2                                                                
*        PARAM2     BYTE    0   NOT USED                                        
*                   BYTES 1-3   A(OUTPUT)                                       
*                               OUTPUT IS 2 BYTE BINARY 0 - 2400 TIME           
         SPACE 3                                                                
         PRINT NOGEN                                                            
TIMPK    CSECT                                                                  
         NMOD1 2,TIMPK                                                          
         USING WORKD,RC                                                         
         SPACE 3                                                                
         L     R4,0(1)                                                          
         LR    R5,R4                                                            
         LR    R6,R4                                                            
         LA    R7,5                MAX OF 5 CHARACTERS                          
*                                                                               
TIMPK2   CLI   0(R4),C'0'                                                       
         BL    TIMPK2A                                                          
         CLI   0(R4),C'9'                                                       
         BH    TIMPKERR                                                         
         LA    R4,1(R4)                                                         
         BCT   R7,TIMPK2                                                        
         B     TIMPKERR                                                         
*                                                                               
TIMPK2A  SR    R5,R4                                                            
         BNM   TIMPKERR                                                         
         LCR   R5,R5                                                            
         BCTR  R5,R0                                                            
         EX    R5,TIMPKPAK                                                      
         CVB   R0,DUB                                                           
         CH    R5,=H'1'                                                         
         BH    *+8                                                              
         MH    R0,=H'100'          IF 1 OR 2 NUMERICS ASSUME INPUT IS           
*                                  HOURS ONLY                                   
TIMPK3   CH    R0,=H'1200'                                                      
         BH    TIMPK4                                                           
         BL    TIMPK5                                                           
*                                       = '1200'                                
         CLI   0(R4),C'N'          NOON                                         
         BE    TIMPK6                                                           
         CLI   0(R4),C' '          NO SUFFIX = NOON                             
         BNH   TIMPK6                                                           
         CLI   0(R4),C'M'          MIDNIGHT                                     
         BNE   TIMPKERR                                                         
         AH    R0,=H'1200'         = 2400                                       
         B     TIMPK6                                                           
*                                       GREATER '1200'                          
TIMPK4   CH    R0,=H'1259'                                                      
         BNH   TIMPK4B                                                          
         CH    R0,=H'2400'                                                      
         BH    TIMPKERR                                                         
         CLI   0(R4),C' '          NO SUFFIX FOR 1300+ TIMES                    
         BH    TIMPKERR                                                         
         B     TIMPK6              NO ADJUSTMENT                                
*                                                                               
TIMPK4B  DS    0H                                                               
         CLI   0(R4),C' '          IF NO SUFFIX- NO ADJUST                      
         BNH   TIMPK6                                                           
         SH    R0,=H'1200'                                                      
         B     TIMPK5A                                                          
*                                       LESS '1200'                             
TIMPK5   DS    0H                                                               
         CLI   0(R4),C' '          IF NO SUFFIX                                 
         BNH   TIMPK6              TIME IS AM                                   
*                                                                               
TIMPK5A  CLI   0(R4),C'A'               AM                                      
         BE    TIMPK6                                                           
         CLI   0(R4),C'P'               PM                                      
         BNE   TIMPKERR                                                         
         AH    R0,=H'1200'                                                      
*                                                                               
TIMPK6   LR    R7,R0                                                            
         SR    R6,R6                                                            
         D     R6,=F'100'                                                       
         CH    R6,=H'59'                MINUTES NOT GREATER 59                  
         BH    TIMPKERR                                                         
         STH   R0,DUB                                                           
         L     R2,4(R1)                                                         
         MVC   0(2,R2),DUB                                                      
         LA    R0,2(R5)                 R0 = LENGTH                             
*                                                                               
TIMPKEXT STC   R0,0(R1)                 STORE LENGTH                            
         XIT1                                                                   
*                                                                               
TIMPKERR SR    R0,R0                                                            
         B     TIMPKEXT                                                         
*                                                                               
TIMPKPAK PACK  DUB,0(0,R6)                                                      
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006TIMPK     08/12/91'                                      
         END                                                                    
