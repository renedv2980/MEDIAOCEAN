*          DATA SET MPOPVAL    AT LEVEL 004 AS OF 01/27/86                      
*CATALP MPOPVAL                                                                 
         TITLE 'MPOPVAL - OPTIONS SCANNER'                                      
         PRINT NOGEN                                                            
MPOPVAL  CSECT                                                                  
         SPACE 2                                                                
*                                                                               
*        INPUT IS IN 4 FULLWORDS :-                                             
*        PROGRAM NO, A(SCREEN HDR), A(PAIRED TABLE), A(MPDITAB)                 
*                                                                               
*        OUTPUT IS UPDATED PAIRED TABLE AND RETURN CODE                         
*                                                                               
*        THE INPUT STRING MAY BE IN SEVERAL FORMATS :-                          
*         KEYWORD                                                               
*         KEYWORD=XXX (FROM FIXED LIST)                                         
*         KEYWORD=XXX (ANYTHING E.G. SPACE=PG4C)                                
*         KEYWORD=NUMERIC                                                       
*                                                                               
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPOPVAL                                              
         SPACE 2                                                                
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         SH    RC,=H'4'                                                         
         L     RC,0(RC)            RESTORE A(GENCONS GLOBAL STORAGE)            
         USING GEND,RC                                                          
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        GENERAL SCANNER ROUTINE                                      *         
*                                                                     *         
*        BUILDS A TABLE OF KEYWORD=QUALIFIER PAIRS BY SCANNING        *         
*        OPTKEYW FOR KEYWORDS AND OPTQUAL FOR QUALIFIERS              *         
*        THE PAIRING IS THEN VALIDATED AGAINST OPTVAL TABLE           *         
*        NOTE - OPTVAL  CONTAINS THE FOLLOWING SPECIAL ENTRIES        *         
*        QUAL OF '00' MEANS THAN KEYW WITH NO QUAL IS O.K.            *         
*        QUAL OF 'FE' MEANS THAN KEYW WITH NUMERIC QUAL IS O.K.       *         
*        QUAL OF 'FF' MEANS THAN KEYW MUST HAVE A QUAL BUT ANY VALUE  *         
*                     IS O.K.                                         *         
***********************************************************************         
*                                                                               
         SPACE 2                                                                
GENSCAN  DS    0H                                                               
         L     R2,4(R1)            ADDRESS SCREEN HEADER                        
         L     R3,8(R1)            ADDRESS PAIRED TABLE(OPTTAB)                 
         USING OPTTABD,R3                                                       
         L     R4,12(R1)           ADDRESS MPDITAB                              
         USING MPDIKD,R4                                                        
         A     R4,12(R4)           ADDRESS OPTION KEYWORD TABLE                 
         ST    R4,AKEYTAB                                                       
         ST    R1,SAVER1           SAVE PARM REG                                
         XC    WORKFLD,WORKFLD     CLEAR SCANNER'S WORK AREA                    
         SPACE                                                                  
         GOTO1 SCANNER,DMCBA,(R2),(0,WORKFLD)                                   
         SPACE                                                                  
         LA    R5,WORKFLD                                                       
         CLI   DMCBA+4,0                                                        
         MVI   ERROR,INVOPTS       INVALID OPTIONS TABLE                        
         BE    TRAPERR                                                          
GENSC1   DS    0H                                                               
         L     R4,AKEYTAB                                                       
         CLI   0(R5),0             END OF SCANNER ENTRIES ?                     
         BE    GENSC7                                                           
GENSC2   DS    0H                                                               
         CLI   MPKTYP,C'K'         CHECK STILL IN KEYWORD TABLE                 
         MVI   ERROR,INVOPKW       INVALID OPTION KEYWORD                       
         BNE   TRAPERR                                                          
         ZIC   R1,0(R5)            DATA LENGTH                                  
         BCTR  R1,R0               LESS 1 FOR EX                                
         EX    R1,*+8              COMPARE WITH KEYWORD                         
         B     *+10                                                             
         CLC   12(0,R5),MPKENAM    MATCH SCANNER : KEYWORD NAME                 
         BE    GENSC2B             YES - SEE IF LONG ENOUGH                     
GENSC2A  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,MPKLEN           GET ENTRY LENGTH                             
         AR    R4,R0               BUMP TOP NEXT ENTRY                          
         B     GENSC2              CHECK NEXT ENTRY                             
GENSC2B  DS    0H                                                               
         CLC   0(1,R5),MPKMINC     LONG ENOUGH FOR UNIQUE MATCH ?               
         BL    GENSC2A             NO - TRY NEXT ENTRY IN TABLE                 
*                                                                               
GENSC3   DS    0H                                                               
         MVC   OPKEYC,MPKCOD       SET UP CODE IN OPTTAB                        
         CLI   1(R5),0             ANY R.H.S. ?                                 
         MVI   OPLEN,3             SET LENGTH IF NO TEXT                        
         BE    GENSC6              NO - LEAVE AS X'00'                          
         TM    3(R5),X'80'         NUMERIC R.H.S. ?                             
         BZ    GENSC3A             BRANCH NO                                    
         MVI   OPLEN,7             SET LENGTH FOR NUMERIC                       
         MVC   OPNUM,8(R5)         INSERT NUMBER INTO OPTTAB TABLE              
         MVI   OPANSC,NUMERIC      INDICATE NUMERIC                             
         B     GENSC6                                                           
GENSC3A  DS    0H                                                               
         L     R1,SAVER1                                                        
         L     R4,12(R1)           @ MPDITAB                                    
         A     R4,16(R4)           ADDR MPDITAB ANSWER TABLE                    
         USING MPDIAD,R4                                                        
GENSC4   DS    0H                                                               
         CLI   MPATYP,C'A'         STILL AN ANSWER ENTRY ?                      
         BNE   GENSC4B             FLAG AS UNKNOWN                              
         ZIC   R1,1(R5)            SCANNER DATA LENGTH                          
         BCTR  R1,0                LESS 1 FOR EX COMPARE                        
         EX    R1,*+8              COMPARE WITH ANSWEER TABLE                   
         B     *+10                                                             
         CLC   22(0,R5),MPAENAM    MATCH SCANNER : MPDITAB ANSWER               
         BE    GENSC4C             YES - CHECK LONG ENOUGH FOR MATCH            
GENSC4A  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,MPALEN           GET ENTRY LENGTH                             
         AR    R4,R0               BUMP TO NEXT ENTRY                           
         B     GENSC4                                                           
GENSC4B  DS    0H                                                               
         MVI   OPANSC,UNKNOWN      FLAG 'UNKNOWN R.H.S.'                        
         IC    R1,1(R5)            GET TEXT LENGTH                              
         LA    R1,3(R1)            ADD 3 FOR OTHER DATA                         
         STC   R1,OPLEN            STORE IN TABLE ENTRY                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     GENSC6                                                           
         MVC   OPTEXT(0),22(R5)    MOVE TEXT INTO TABLE                         
GENSC4C  DS    0H                                                               
         CLC   1(1,R5),MPAMINC     LONG ENOUGH TO BE UNIQUE ?                   
         BL    GENSC4A             NO -TRY NEXT ENTRY IN TABLE                  
GENSC5   DS    0H                                                               
         MVI   OPLEN,3                                                          
         MVC   OPANSC,MPACOD       SET UP CODE FROM TABLE                       
GENSC6   DS    0H                                                               
         SR    R1,R1                                                            
         IC    R1,OPLEN                                                         
         AR    R3,R1               BUMP TO NEXT ENTRY POSITION                  
         LA    R5,32(R5)           NEXT SCANNER ENTRY                           
         B     GENSC1                                                           
GENSC7   DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        GENERAL PAIRING ROUTINE FOR OPTIONS                          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
GENOPR   DS    0H                                                               
         L     R1,SAVER1                                                        
         L     R5,0(R1)            PROGRAM NUMBER                               
         L     R3,8(R1)            ADDRESS OPTTAB TABLE                         
         L     R4,12(R1)           ADDRESS MPDITAB                              
         A     R4,20(R4)           ADDRESS VALID OPTIONS PAIRINGS               
         USING MPDICD,R4                                                        
         LR    R6,R4               SAVE START ADDRESS                           
GENOP1   DS    0H                                                               
         LR    R4,R6               RESTORE START ADDRESS                        
GENOP2   DS    0H                                                               
         CLI   MPCTYP,C'C'         STILL A COMBINATION ENTRY                    
         BNE   GENERR                                                           
         CLC   OPKEYC(2),MPCKCOD   MATCH OPTTAB : DITAB COMBINATION             
         BNE   GENOP2A             NO -TRY NEXT ENTRY                           
         SR    R1,R1                                                            
         LH    R1,MPCVALF          GET VALIDITY FLAGS                           
         NR    R1,R5               VALID FOR THIS PROG ?                        
         BNZ   GENOP3              YES - GET NEXT OPTTAB PAIR                   
GENOP2A  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,MPCLEN           GET LENGTH                                   
         AR    R4,R0               POINT TO NEXT COMBINATION ENTRY              
         B     GENOP2                                                           
*                                                                               
GENOP3   DS    0H                                                               
         SR    R1,R1                                                            
         IC    R1,OPLEN                                                         
         AR    R3,R1               NEXT OPTTAB ENTRY                            
         CLI   OPLEN,X'00'         LAST ENTRY                                   
         BE    GENOP4              YES - EXIT                                   
         B     GENOP1                                                           
GENOP4   DS    0H                                                               
         L     R1,SAVER1                                                        
         ST    R3,8(R1)                                                         
         MVI   ERROR,0                                                          
         XIT1                                                                   
GENERR   CLI   OPANSC,NOANSWER                                                  
         MVI   ERROR,OPREQANS      OPTION REQUIRES AN ANSWER/ARGUMENT           
         BE    TRAPERR                                                          
         CLI   OPANSC,UNKNOWN                                                   
         MVI   ERROR,INVOPANS      INV ANSWER/ARGUMENT (NOT IN TABLE)           
         BE    TRAPERR                                                          
         MVI   ERROR,INVOPPR       INV KW=ARG PAIR FOR REPORT                   
         BE    TRAPERR                                                          
         SPACE 3                                                                
TRAPERR  DS    0H                                                               
         GOTO1 ERREX                                                            
         EJECT                                                                  
         LTORG                                                                  
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*DDSPLWORKD,MPDITABD,MPEQUATES                                                  
         SPACE 2                                                                
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPDITABD                                                       
       ++INCLUDE MPEQUATES                                                      
         PRINT ON                                                               
         SPACE                                                                  
WORKD    DSECT                                                                  
SAVER1   DS    F                                                                
AKEYTAB  DS    F                                                                
DMCBA    DS    3F                                                               
WORKFLD  DS    CL224               7 32-BYTE SCANNER BLOCKS                     
WORKX    DS    0H                                                               
         SPACE 2                                                                
OPTTABD  DSECT                                                                  
OPLEN    DS    X                   LENGTH OF ENTRY                              
OPKEYC   DS    C                   KEYWORD CODE                                 
OPANSC   DS    C                   ANSWER CODE, OR                              
NOANSWER EQU   X'00'               '00' FOR NOTHING                             
NUMERIC  EQU   X'FE'               'FE' FOR NUMERIC                             
UNKNOWN  EQU   X'FF'               'FF' FOR UNKNOWN                             
OPNUM    DS    0FL4                FULLWORD NUMBER                              
OPTEXT   DS    CL10                TEXT (MAXIMUM LENGTH)                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004MPOPVAL   01/27/86'                                      
         END                                                                    
