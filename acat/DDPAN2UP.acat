*          DATA SET DDPAN2UP   AT LEVEL 004 AS OF 05/01/02                      
*CATALP PAN2UP                                                                  
         TITLE 'PAN2UP - PRINT SYSTEMS MANUALS 2-UP'                            
PAN2UP   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,**PAN2UP,=V(REGSAVE),R9,RR=R2                                  
         ST    R2,RELO                                                          
         SPACE 1                                                                
         GOTO1 =V(DATCON),DMCB,(5,0),(8,TODAY)                                  
         LA    R2,PAGEPOOL                                                      
         ST    R2,APOOL                                                         
         L     R8,=V(BOXAREA)                                                   
         USING BOXD,R4                                                          
         BAS   RE,SETBOX                                                        
         EJECT                                                                  
*              HANDLE THE CARD DECK                                             
         SPACE 3                                                                
CARD     GOTO1 =V(CARDS),DMCB,C,=C'RE00'                                        
         CLC   C(2),=C'/*'                                                      
         BNE   CARD2                                                            
         BAS   RE,DOPAGE                                                        
         GOTO1 =V(PRINT),DMCB,=C'CLOSE'                                         
         GOTO1 =V(PANIC),DMCB,=C'CLOSE'                                         
         XBASE                                                                  
         SPACE 1                                                                
CARD2    CLC   C(9),=C'<DOCNAME>'  CHECK FOR SPECIAL CONTROL CARDS              
         BE    DOCCARD                                                          
         CLC   C(6),=C'<CHAP>'                                                  
         BE    CHAPCARD                                                         
         CLC   C(4),=C'<PA>'                                                    
         BE    SKIPCARD                                                         
         CLC   C(3),=C'<X>'                                                     
         BE    INDXCARD                                                         
         BAS   RE,DOPAN            OTHERWISE ITS A PAN NAME                     
         B     CARD                                                             
         EJECT                                                                  
*              HANDLE SPECIAL CONTROL CARDS                                     
         SPACE 3                                                                
DOCCARD  MVC   DOCNAME,C+9                                                      
         BAS   RE,DOCPAGE                                                       
         B     CARD                                                             
         SPACE 1                                                                
CHAPCARD OC    CHAP,CHAP           UNLESS THIS IS FIRST CHAPTER                 
         BZ    *+8                                                              
         BAS   RE,DOPAGE           FINISH OFF END OF CHAPTER                    
         MVC   CHAPNAME,C+6                                                     
         LH    R1,CHAP                                                          
         LA    R1,1(R1)                                                         
         ST    R1,CHAP                                                          
         MVC   PAGE,=H'1'                                                       
         B     CARD                                                             
         SPACE 1                                                                
SKIPCARD BAS   RE,NEWPAGE                                                       
         B     CARD                                                             
         SPACE 1                                                                
INDXCARD DS    0H                                                               
         B     CARD                                                             
         EJECT                                                                  
*              HANDLE THE PAN BOOKS                                             
         SPACE 3                                                                
DOPAN    NTR1                                                                   
         MVC   PANNAME,C                                                        
         MVC   THISTIT,SPACES      TITLE DEFAULTS TO PAN-DC                     
         CLC   PANNAME(2),=C'DC'                                                
         BNE   *+10                                                             
         MVC   THISTIT(8),PANNAME+2                                             
         BAS   RE,POSTTIT                                                       
         SPACE 1                                                                
DOPAN2   GOTO1 =V(PANIC),DMCB,(INCOPT,=C'READ'),=C'PAN',PANNAME,C               
         TM    DMCB+8,X'80'                                                     
         BO    DOPANEND                                                         
         TM    DMCB+4,X'40'                                                     
         BO    DOPANERR                                                         
         CLC   C+9(5),=C'TITLE'                                                 
         BE    DOPANTIT                                                         
         CLC   C+9(5),=C'EJECT'                                                 
         BE    DOPANEJE                                                         
         CLC   C+9(5),=C'SPACE'                                                 
         BE    DOPANSPA                                                         
         GOTO1 POOLPOST,DMCB,C                                                  
         B     DOPAN2                                                           
         SPACE 1                                                                
DOPANTIT BAS   RE,SETTIT                                                        
         BAS   RE,POSTTIT                                                       
         MVC   C,SPACES                                                         
         MVC   C(32),THISTIT                                                    
         GOTO1 POOLPOST,DMCB,C                                                  
         GOTO1 =V(UNDERLIN),DMCB,(32,THISTIT),(X'BF',C)                         
         GOTO1 POOLPOST,DMCB,C                                                  
         GOTO1 POOLPOST,DMCB,SPACES                                             
         B     DOPAN2                                                           
         SPACE 1                                                                
DOPANEJE BAS   RE,NEWPAGE                                                       
         B     DOPAN2                                                           
         SPACE 1                                                                
DOPANSPA GOTO1 POOLPOST,DMCB,SPACES                                             
         CLI   C+15,C'2'                                                        
         BL    DOPAN2                                                           
         BASR  RE,RF                                                            
         B     DOPAN2                                                           
         SPACE 1                                                                
DOPANEND GOTO1 POOLPOST,DMCB,SPACES                                             
         B     CARD                                                             
         SPACE 1                                                                
DOPANERR MVC   C,SPACES                                                         
         MVC   C(10),PANNAME                                                    
         MVC   C+12(20),=C'** BOOK NOT FOUND **'                                
         GOTO1 POOLPOST,DMCB,C                                                  
         GOTO1 POOLPOST,DMCB,SPACES                                             
         B     CARD                                                             
         EJECT                                                                  
*              ROUTINE TO POST INTO POOL                                        
         SPACE 3                                                                
POOLPOST NTR1                                                                   
         L     R2,0(R1)            P1=A(DATA)                                   
         L     R3,APOOL                                                         
         MVC   0(80,R3),0(R2)                                                   
         LA    R3,80(R3)                                                        
         ST    R3,APOOL                                                         
         LH    R1,LINE                                                          
         CH    R1,=H'52'                                                        
         BE    POOLP2                                                           
         LA    R1,1(R1)                                                         
         STH   R1,LINE                                                          
         B     XIT                                                              
         SPACE 1                                                                
POOLP2   BAS   RE,NEWPAGE                                                       
         B     XIT                                                              
         EJECT                                                                  
*              TITLE ROUTINES                                                   
         SPACE 3                                                                
SETTIT   NTR1                                                                   
         MVC   THISTIT,SPACES                                                   
         LA    R2,C+16                                                          
         LA    R3,THISTIT                                                       
         LA    R4,32                                                            
         SPACE 1                                                                
SETTIT2  CLI   0(R2),C''''                                                      
         BE    XIT                                                              
         MVC   0(1,R3),0(R2)                                                    
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,SETTITS                                                       
         B     XIT                                                              
         SPACE 1                                                                
POSTTIT  NTR1                                                                   
         TM    PAGE+1,X'01'                                                     
         BO    POSTLEFT                                                         
         CLC   FIRSTR,SPACES                                                    
         BNE   *+10                                                             
         MVC   FIRSTR,THISTIT                                                   
         MVC   LASTR,THISTIT                                                    
         B     XIT                                                              
         SPACE 1                                                                
POSTLEFT CLC   FIRSTL,SPACES                                                    
         BNE   *+10                                                             
         MVC   FIRSTL,THISTIT                                                   
         MVC   LASTL,THISTIT                                                    
         B     XIT                                                              
         EJECT                                                                  
*              PAGE ROUTINES                                                    
         SPACE 3                                                                
DOCPAGE  NTR1                                                                   
         B     XIT                                                              
         SPACE 1                                                                
NEWPAGE  NTR1                                                                   
         TM    PAGE+1,X'01'        ARE WE ON THE ODD PAGE?                      
         BO    NEWPAGE2                                                         
         BAS   RE,DOPAGE           NO - SO PRINT IT ALL OUT                     
         B     XIT                                                              
         SPACE 1                                                                
NEWPAGE2 LA    R2,PAGEPOOL                                                      
         LA    R1,52                                                            
         MH    R1,=H'80'                                                        
         AR    R2,R1                                                            
         ST    R2,APOOL                                                         
         LH    R1,PAGE                                                          
         LA    R1,1(R1)                                                         
         STH   R1,PAGE                                                          
         LH    R1,PAGE                                                          
         LA    R1,1(R1)                                                         
         STH   R1,PAGE                                                          
         MVC   LINE,=H'1'                                                       
         B     XIT                                                              
         SPACE 1                                                                
DOPAGE   NTR1                                                                   
         BAS   RE,SPACE                                                         
         MVC   P+1(32),DOCNAME                                                  
         MVC   P+36(9),TODAY                                                    
         MVC   P+48(32),CHAPNAME                                                
         GOTO1 =V(RIGHT),DMCB,P+48,32                                           
         MVC   P+84(80),P+1                                                     
         MVI   BOXFONT,1                                                        
         BAS   RE,SPACE2                                                        
         MVI   BOXFONT,0                                                        
         LA    R2,PAGEPOOL                                                      
         LA    R3,52                                                            
         MH    R3,=H'80'                                                        
         AR    R3,R2                                                            
         LA    R4,52                                                            
         SPACE 1                                                                
DOPAGE2  MVC   P+1(80),0(R2)                                                    
         MVC   0(80,R2),SPACES                                                  
         MVC   P+84(80),0(R3)                                                   
         MVC   0(80,R3),SPACES                                                  
         LA    R2,80(R2)                                                        
         LA    R3,80(R3)                                                        
         BCT   R4,DOPAGE2                                                       
         SPACE 1                                                                
         BAS   RE,SPACE2                                                        
         MVC   P+1(32),FIRSTL                                                   
         LH    R1,PAGE                                                          
         BCTR  R1,0                                                             
         STH   R1,PAGE                                                          
         LA    R2,P+36                                                          
         BAS   RE,EDITPAGE                                                      
         MVC   P+48(32),LASTL                                                   
         GOTO1 =V(RIGHT),DMCB,P+48,32                                           
         MVC   P+83+1(32),FIRSTR                                                
         LH    R1,PAGE                                                          
         LA    R1,1(R1)                                                         
         STH   R1,PAGE                                                          
         LA    R2,P+83+36                                                       
         BAS   RE,EDITPAGE                                                      
         MVC   P+83+48(32),LASTR                                                
         GOTO1 =V(RIGHT),DMCB,P+83+48,32                                        
         MVI   BOXFONT,1                                                        
         BAS   RE,SKIP                                                          
         MVI   BOXFONT,0                                                        
         LH    R1,PAGE                                                          
         LA    R1,1(R1)                                                         
         STH   R1,PAGE                                                          
         MVC   LINE,=H'1'                                                       
         LA    R2,PAGEPOOL                                                      
         ST    R2,APOOL                                                         
         MVC   FIRSTL,SPACES                                                    
         MVC   LASTL,SPACES                                                     
         MVC   FIRSTR,SPACES                                                    
         MVC   LASTR,SPACES                                                     
         B     XIT                                                              
         SPACE 1                                                                
EDITPAGE NTR1                                                                   
         MVC   0(4,R2),=C'PAGE'                                                 
         LA    R2,5(R2)                                                         
         EDIT  (2,CHAP),(3,0(R2)),ALIGN=LEFT                                    
         SPACE 1                                                                
EP2      CLI   0(R2),C' '                                                       
         BE    EP4                                                              
         LA    R2,1(R2)                                                         
         B     EP2                                                              
         SPACE 1                                                                
EP4      MVI   0(R2),C'.'                                                       
         LA    R2,1(R2)                                                         
         EDIT  (2,PAGE),(3,0(R2)),ALIGN=LEFT                                    
         B     XIT                                                              
         EJECT                                                                  
*              UTILITIES                                                        
         SPACE 3                                                                
SETBOX   NTR1                                                                   
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXWT,1                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXOFF,0                                                         
         MVC   BOXCOLS,SPACES                                                   
         MVC   BOXCOLSR,SPACES                                                  
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXCOLS,C'L'                                                     
         MVI   BOXCOLS+81,C'R'                                                  
         MVI   BOXCOLS+83,C'L'                                                  
         MVI   BOXCOLS+164,C'R'                                                 
         MVI   BOXROWS,C'T'                                                     
         MVI   BOXROWS+2,C'M'                                                   
         MVI   BOXROWS+57,C'M'                                                  
         MVI   BOXROWS+59,C'B'                                                  
         MVC   BOXWIDTH,=F'165'                                                 
         MVI   BOXOPTCD,C'J'                                                    
         B     XIT                                                              
         SPACE 1                                                                
SPACE    NTR1                                                                   
         GOTO1 =V(PRINT),DMCB,P-1,=C'BL01'                                      
         MVC   P,SPACES                                                         
         B     XIT                                                              
         SPACE 1                                                                
SPACE2   NTR1                                                                   
         GOTO1 =V(PRINT),DMCB,P-1,=C'BL02'                                      
         MVC   P,SPACES                                                         
         B     XIT                                                              
         SPACE 1                                                                
SKIP     NTR1                                                                   
         GOTO1 =V(PRINT),DMCB,P-1,=C'BC01'                                      
         MVC   P,SPACES                                                         
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              STORAGE FOR PROGRAM                                              
         SPACE 3                                                                
DUB      DS    D                                                                
WORK     DS    CL64                                                             
DMCB     DS    6F                                                               
RELO     DS    F                                                                
APOOL    DS    A                                                                
C        DS    CL80                                                             
PFILL    DS    CL1                                                              
P        DC    CL165' '                                                         
SPACES   DC    CL165' '                                                         
DOCNAME  DC    CL32' '                                                          
CHAPNAME DC    CL32' '                                                          
FIRSTL   DC    CL32' '                                                          
LASTL    DC    CL32' '                                                          
FIRSTR   DC    CL32' '                                                          
LASTR    DC    CL32' '                                                          
THISTIT  DC    CL32' '                                                          
TODAY    DC    CL9' '                                                           
PANNAME  DC    CL10' '                                                          
CHAP     DC    H'1'                                                             
PAGE     DC    H'1'                                                             
LINE     DC    H'1'                                                             
         SPACE 1                                                                
         LTORG                                                                  
PAGEPOOL DC    104CL80' '                                                       
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDPAN2UP  05/01/02'                                      
         END                                                                    
