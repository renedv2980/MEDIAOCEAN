*          DATA SET MPDVEC     AT LEVEL 003 AS OF 05/01/02                      
*CATALP MPDVEC                                                                  
         TITLE 'MPDVEC  - CREATE DATA VECTORS'                                  
         PRINT NOGEN                                                            
MPDVEC   CSECT                                                                  
         SPACE 2                                                                
**********************************************************************          
*                                                                    *          
* MPDVEC  :- CREATES FIXED INFORMATION PART OF THE VECTOR,           *          
*            READING THE SCHEDULE RECORD AND IT'S ASSOCIATED         *          
*            VEHICLE RECORDS TO OBTAIN IT.                           *          
*                                                                    *          
*        PARAM 1 - DATA VECTOR TABLE ADDRESS                         *          
*        PARAM 2 - SCHEDULE SCREEN HEADER ADDRESS                    *          
*        PARAM 3 - AGENCY CODE ADDRESS                               *          
*        PARAM 4 - INS=SEP OR ALL INDIC, OR NUMBER OF VECTORS        *          
*        PARAM 5 - LENGTH OF ONE DATA VECTOR                         *          
*                                                                    *          
**********************************************************************          
         SPACE 3                                                                
         NMOD1 WEND-WSTART,MPDVEC                                               
         SPACE 3                                                                
         LR    R7,RC                                                            
         USING WORKDS,R7                                                        
         SH    RC,=H'4'            RESTORE REGISTER C ...                       
         L     RC,0(,RC)           ... THE GEND REG                             
         USING GEND,RC                                                          
         EJECT                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R3,0(,R1)           DATA VECTOR TABLE                            
         LR    R2,R3                                                            
         SH    R2,=H'4'            END ADDR OF VECTOR TAB STORED HERE           
         L     R2,0(R2)            GET END ADDRESS                              
         S     R2,16(R1)           LEAVE ROOM FOR A TOTALS VECTOR               
         STCM  R2,15,VTABEND       SAVE TO PREVENT TABLE OVERFLOW               
         USING VTABD,R3                                                         
         L     R2,4(,R1)           SCREEN HEADER                                
*                                                                               
*                                  GET SCHEDULE RECORD                          
         GOTO1 ANY                                                              
         XC    WKEY,WKEY                                                        
         MVC   WKEY(4),SKEYCON     SET UP SCHEDULE KEY STRUCTURE                
         L     R1,8(,R8)           GET TWAAGY ADDRESS                           
         MVC   WKEY+1(2),0(R1)     MOVE IN TWAAGY                               
         MVC   WKEY+4(8),WORK                                                   
         MVC   COMMAND,=C'DMREAD'                                               
         MVC   DMFILE,SYSDIR                                                    
         L     R6,AIO3                                                          
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,WKEY,(R6),0                
         TM    DMCB+8,X'10'        CHECK THAT DIRECTORY RECORD FOUND            
         BZ    *+6                                                              
         DC    H'0'                DIE - SCHEDULE NOT FOUND                     
         MVC   COMMAND,=C'GETREC'                                               
         MVC   DMFILE,SYSFIL       * READ ...FIL *                              
         LA    R9,36(R6)                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,(R9),(R6),DMWK2            
         TM    DMCB+8,X'10'        CHECK ...FIL RECORD FOUND                    
         BZ    *+6                                                              
         DC    H'0'                DIE - NO ...FIL RECORD                       
         MVI   ELCODE,X'20'        ELEMENT CODE FOR VEHICLES                    
         BAS   RE,GETEL            POINT TO VEHICLE ELEMENTS                    
         USING SCDVDEL,R6                                                       
         MVI   ERROR,NOVEH         ERROR CODE, IN CASE                          
         BNE   TRAPERR             NO VEHICLES IN SCHEDULE                      
         XC    CURRVEC,CURRVEC     SET INITIAL VALUE                            
         XC    ENTRY,ENTRY         INITIAL VALUE FOR ENTRY NUMBER               
*                                                                               
DV010    DS    0H                                                               
*                                                                               
*        WE MUST DECIDE HERE WHETHER TO CREATE AN(OTHER) ENTRY FOR              
*        THIS VEHICLE(WITH A DIFFERENT NUMBER OF INSERTS PERHAPS).              
*                                                                               
         SPACE                                                                  
         L     R1,16(,R8)          GET VECTOR LENGTH - PASSED TO ME             
         SH    R1,=H'3'            LESS 3(2 FOR LENGTH,1 FOR EX)                
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    VTTYPE(0),VTTYPE    CLEAR VECTOR                                 
         SPACE                                                                  
         L     R1,12(,R8)          GET INS PARAMETER                            
         CLI   0(R1),C'0'          IF '0', CREATE A VECTOR                      
         BE    DV020                                                            
         CLI   0(R1),C'N'          IF 'N'                                       
         BE    *+12                                                             
         CLI   0(R1),C'Y'          OR IF 'Y'                                    
         BNE   DV012                                                            
         CLC   CURRVEC,SCDVDINS    AND ALL INSERTS DONE ?                       
         BE    DV040               ... GET NEXT VEHICLE ELEMENT                 
         B     DV015               ELSE CREATE VECTOR                           
DV012    DS    0H                                                               
*        CLC   CURRVEC,0(R1)       IF NUMBER AND ALL ENTRIES DONE               
*        BE    DV040               ... GET NEXT VEHICLE ELEMENT                 
         B     DV020               ??? CREATE VECTOR                            
         SPACE                                                                  
DV015    DS    0H                                                               
         CLI   0(R1),C'Y'          ON ?                                         
         BNE   DV020               NO - GO TO READ NEXT VEHICLE                 
         LH    R1,CURRVEC                                                       
         LA    R1,1(,R1)           UPDATE CURR COUNT                            
         STH   R1,CURRVEC                                                       
         CLC   CURRVEC,=XL2'1'     FIRST PASS FOR THIS VEHICLE ?                
         BNE   DV030               OMIT VEHICLE READ                            
         B     DV025                                                            
DV020    DS    0H                                                               
         MVC   CURRVEC,SCDVDINS                                                 
DV025    DS    0H                                                               
         XC    WKEY,WKEY                                                        
         MVC   WKEY(4),VKEYCON     SET UP VEHICLE KEY STRUCTURE                 
         MVC   WKEY+4(8),SCDVDVEH  MOVE IN VEHICLE CODE                         
         MVC   COMMAND,=C'DMREAD'                                               
         MVC   DMFILE,SYSDIR                                                    
         L     R4,AIO2                                                          
DV026    DS    0H                                                               
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,WKEY,(R4),0                
         TM    DMCB+8,X'10'        FOUND ?                                      
         BZ    DV027               YES - GOODY                                  
         CLC   WKEY+3(1),RKEYCON+3     RDRSHP RECORD NOT FOUND ?                
         BE    TRAPERR                 YES - THAT WAS OUR LAST HOPE             
         CLC   WKEY+3(1),PKEYCON+3     WAS IT PROTO RECORD ?                    
         BE    *+14                    YES - SET UP RDRSHP FOR NEXT TRY         
         MVC   WKEY(4),PKEYCON         VEHICLE, SET UP PROTO FOR NEXT           
         B     *+10                                                             
         MVC   WKEY(4),RKEYCON                                                  
         L     R1,8(,R8)                                                        
         MVC   WKEY+1(2),0(R1)       MOVE IN TWAAGY                             
         MVC   WKEY+4(8),SCDVDVEH+1  MOVE IN VEHICLE CODE(WITHOUT *)            
         DROP  R6                                                               
         B     DV026                                                            
DV027    DS    0H                                                               
         CLC   WKEY(4),VKEYCON     VEHICLE TYPE RECORD ?                        
         BNE   DV035               NO - NO ELEMENT DETAILS TO SET UP            
         MVC   COMMAND,=C'GETREC'                                               
         MVC   DMFILE,SYSFIL       * READ ...FIL *                              
         LA    R9,36(R4)                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,(R9),(R4),DMWK2            
         TM    DMCB+8,X'10'        RECORD FOUND ?                               
         BZ    DV030                                                            
         DC    H'0'                DIE - ...FIL RECORD NOT FOUND                
DV030    DS    0H                                                               
         CLC   WKEY(4),VKEYCON     VEHICLE TYPE RECORD ?                        
         BNE   DV035               NO - NO ELEMENT DETAILS TO SET UP            
         USING VHCKEY,R4                                                        
         L     R4,AIO2             RESET TO AIO AREA                            
         MVI   VTTYPE,C'D'                                                      
         MVC   VTTCOD,VHCCTYPF     MOVE TYPE CODE                               
         MVC   VTICOD,VHCCINTF     MOVE INTEREST CODE                           
         MVC   VTFCOD,VHCCFRQF     MOVE FREQUENCY CODE                          
         SPACE                                                                  
         L     R1,12(,R8)          GET INS= PARM ADDRESS                        
         CLI   0(R1),C'Y'          INS=SEP ?                                    
         BNE   *+14                NO GET REQUIRED INSERT NUMBER                
         MVC   VTINS,=H'1'         YES - SET TO 1                               
         B     *+10                                                             
         MVC   VTINS,CURRVEC       SET UP INSERT NUMBER                         
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        NOW FIND THE ELEMENTS THAT WILL SUPPLY OUR DATA              *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         MVI   ELCODE,VHCFNELC     FULLNAME ELEMENT CODE                        
         BAS   RE,GETELA           GET ELEMENT                                  
         MVC   VTLNAM,SPACES                                                    
         USING VHCFNEL,R4                                                       
         SR    R1,R1                                                            
         IC    R1,VHCFNLEN                                                      
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   VTLNAM(0),VHCFNAM   MOVE FULL NAME TO DATA VECTOR                
*                                                                               
         MVI   ELCODE,VHCSNELC     SHORT NAME ELEMENT CODE                      
         L     R4,AIO2                                                          
         BAS   RE,GETELA           GET ELEMENT                                  
         MVC   VTSNAM,SPACES                                                    
         USING VHCSNEL,R4                                                       
         SR    R1,R1                                                            
         IC    R1,VHCSNLEN                                                      
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   VTSNAM(0),VHCSNAM   MOVE SHORTNAME TO DATA VECTOR                
         SPACE                                                                  
         MVI   ELCODE,VHCSHELC     SHORT HEADER ELEMENT CODE                    
         L     R4,AIO2             IO AREA                                      
         BAS   RE,GETELA           GET ELEMENT                                  
         MVC   VTSHEAD1,SPACES                                                  
         MVC   VTSHEAD2,SPACES                                                  
         USING VHCSHEL,R4                                                       
         SR    R1,R1                                                            
         IC    R1,VHCSHWID                                                      
         BCTR  R1,0                                                             
         EX    R1,EXMVC1                                                        
         EX    R1,EXMVC2                                                        
         B     *+16                                                             
EXMVC1   MVC   VTSHEAD1(0),VHCSHDR    MOVE IN ..                                
EXMVC2   MVC   VTSHEAD2(0),VHCSHDR+10 .. SHORT HEAD                             
*                                                                               
         MVI   ELCODE,VHCRCELC     CIRCULATION ELEMENT CODE                     
         L     R4,AIO2                                                          
         XC    VTCIRC,VTCIRC                                                    
         XC    VTCOST,VTCOST                                                    
         BAS   RE,GETELA           GET ELEMENT                                  
         USING VHCRCEL,R4                                                       
         BNE   *+16                                                             
         MVC   VTCIRC,VHCRCCIR     MOVE CIRC TO VECTOR                          
         DROP  R4                  *        MAKE SCHEDULE                       
         USING SCDVDEL,R6          *      RECORD ADDRESSABLE                    
         NC    SCDVDCOS,SCDVDCOS   ANY COST IN SCHEDULE ?                       
         BZ    *+14                NO - USE VEHICLE COST                        
         MVC   VTCOST,SCDVDCOS     USE SCHEDULE COST                            
         DROP  R6                  *        MAKE VEHICLE                        
         USING VHCRCEL,R4          *      RECORD ADDRESSABLE                    
         B     *+10                                                             
         MVC   VTCOST,VHCRCRCC     MOVE RCC TO VECTOR                           
*                                                                               
         MVI   ELCODE,X'41'        NRS LINK ELEMENT CODE                        
         L     R4,AIO2                                                          
         XC    VTQID,VTQID                                                      
         BAS   RE,GETELA           GET ELEMENT                                  
         BNE   DV035                                                            
         USING VHCNLEL,R4                                                       
         SR    R1,R1                                                            
         IC    R1,VHCNLLEN         GET LENGTH OF DATA                           
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   VTQID(0),VHCNLNK  MOVE NRS LINK TO DATA VECTOR                   
         B     DV035                                                            
*                                                                               
*        FIXED PART OF DATA VECTOR DONE, NOW FOR THE NEXT                       
*                                                                               
DV035    DS    0H                                                               
         LH    R1,ENTRY                                                         
         LA    R1,1(,R1)                                                        
         STH   R1,ENTRY            CALCULATE NEW ENTRY NUMBER...                
         STH   R1,VTENTNO          ...AND PUT IN VECTOR                         
         L     R1,16(,R8)          GET VECTOR LENGTH - PASSED TO ME             
         STH   R1,VTLEN            PUT LENGTH IN VECTOR                         
         AR    R3,R1               ADDRESS NEXT VECTOR                          
         LR    RF,R3               CHECK THAT THERE IS ROOM FOR ANOTHER         
         AR    RF,R1               POINT TO END OF NEW VECTOR                   
         C     RF,VTABEND          CHECK NOT BEYOND END OF TABLE AREA           
         BNH   DV036                                                            
         MVI   ERROR,CANTDO        VECTOR TABLE TOO SMALL                       
         L     R2,4(R8)            A(SCHEDULE SCR HEADER)                       
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR             ONLINE - INFORM USER                         
         DC    H'0'                OFFLINE - DIE - VECTAB TOO SMALL             
*                                                                               
DV036    L     R1,12(,R8)                                                       
         CLI   0(R1),C'0'          IF INS=INCLUDE 0,                            
         BE    DV040               ... GET NEXT VEHICLE                         
         CLI   0(R1),C'N'          IF INS=NUMBER OF PUBS IN SCHEDULE ..         
         BL    DV040               ..(RCH+VXV REPORTS)GET NEXT VEHICLE          
         B     DV010                                                            
DV040    DS    0H                                                               
         XC    CURRVEC,CURRVEC     SET INITIAL VALUE                            
         MVI   ELCODE,X'20'                                                     
         BAS   RE,NEXTEL           NEXT VEHICLE ELEMENT OF SCHEDULE REC         
         BE    DV010               MORE - GO TO PROCESS                         
         XC    VTLEN,VTLEN         END - SET LENGTH OF VECTOR TO ZERO           
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
***********************************************************************         
XIT      DS    0H                                                               
         XC    0(4,R8),0(R8)       .. RETURN NUMBER OF ..                       
         MVC   2(2,R8),ENTRY       .. DATA VECTORS                              
         SPACE                                                                  
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         GOTO1 ERREX                                                            
         SPACE 2                                                                
         GETEL R6,DATADISP,ELCODE                                               
         SPACE 2                                                                
*        THIS IS A COPY OF GETEL USING A DIFFERENT REG BASE                     
         SPACE 2                                                                
GETELA   AH    R4,DATADISP                                                      
         SPACE                                                                  
FRSTELA  CLI   0(R4),0                                                          
         BNE   *+10                                                             
         CLI   0(R4),1             FORCE BAD RETURN                             
         BR    RE                                                               
         CLI   ELCODE,0                                                         
         BER   RE                                                               
         CLC   ELCODE,0(R4)                                                     
         BER   RE                                                               
NEXTELA  SR    RF,RF                                                            
         IC    RF,1(,R4)                                                        
         AR    R4,RF                                                            
         B     FRSTELA                                                          
         SPACE 2                                                                
SKEYCON  DC    C'S',X'0000',C'P'                                                
VKEYCON  DC    C'V',X'0000',C'P'                                                
PKEYCON  DC    C'V',X'0000',C'Q'                                                
RKEYCON  DC    C'V',X'0000',C'R'                                                
SPACES   DC    CL40' '                                                          
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
       ++INCLUDE DDSPLWORKD                                                     
         SPACE 2                                                                
       ++INCLUDE MPVTABD                                                        
         SPACE 2                                                                
       ++INCLUDE MPSVHFIL                                                       
         SPACE 2                                                                
       ++INCLUDE MPEQUATES                                                      
         SPACE 2                                                                
WORKDS   DSECT                                                                  
WSTART   DS    0C                                                               
DMWK2    DS    12D                                                              
WKEY     DS    CL48                                                             
VTABEND  DS    AL4                                                              
CURRVEC  DS    XL2                                                              
ENTRY    DS    XL2                                                              
WEND     DS    0C                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003MPDVEC    05/01/02'                                      
         END                                                                    
