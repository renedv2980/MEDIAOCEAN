*          DATA SET ACPERFACT  AT LEVEL 043 AS OF 03/12/07                      
*CATALP PERFACT                                                                 
*                                                                               
***********************************************************************         
*  NEW COST/TMS                                                       *         
*      A) STANDARD HOURS FOR A PERSON'S 1R ACCOUNT USING CALANDERS    *         
*      B) EDIT HOURS FOR A PERSON'S 1R ACCOUNT USING CALANDERS        *         
***********************************************************************         
         TITLE 'ACPERFACT - PERSON FACTS FOR NEW COST'                          
PERFACT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PRWRKLNQ,**PRFT**,R9,RR=R5                                       
         USING COSTWKD,RA          INTERFACE BLOCK FROM CALLER                  
         USING PRWORKD,RC                                                       
PERFACTS L     RA,0(R1)            INPUT BLOCK                                  
         ST    R5,PRRELO                                                        
         MVI   PRSPACE,C' '                                                     
         MVC   PRSPACE+1(L'PRSPACE-1),PRSPACE                                   
         CLI   CSTMODE,CSTINT      INITIALIZE                                   
         BE    COSTINIT                                                         
         CLI   CSTMODE,CSTSTD      GET STANDARD HOURS                           
         BE    PROCHRS                                                          
         CLI   CSTMODE,CSTEDT      GET EDIT HOURS                               
         BE    PROCHRS                                                          
         DC    H'00'                                                            
         SPACE 1                                                                
PERFACTX XMOD1                                                                  
         EJECT                                                                  
**********************************************************************          
*        INITIALIZE BLOCKS                                           *          
**********************************************************************          
COSTINIT XC    MONTH#,MONTH#                                                    
         BAS   RE,GETCAL                                                        
*                                                                               
         TM    CSTIND,CSTCALOF                                                  
         BZ    COSTIN20                                                         
         LA    RF,0                                                             
         L     R2,=A(OFFKYS)                                                    
COSTIN10 CLI   0(R2),EOT           END OF TABLE                                 
         BNE   COSTIN12                                                         
         GOTO1 CSTXSRT,PRPARM,A(OFFKYS),(RF),OFFLNQ,OFFLNQ,0                    
         B     COSTIN20                                                         
COSTIN12 LA    RF,1(RF)            COUNT NUMBER OF ENTRIES                      
         LA    R2,OFFLNQ(R2)                                                    
         B     COSTIN10                                                         
*                                                                               
         USING MOND,R5                                                          
COSTIN20 SR    R1,R1                                                            
         L     R5,=A(MONTAB)       SET MOA RANGE                                
         MVC   CALSMOA,MONMOA      STARTING MOA                                 
         MVC   CALEMOA,MONMOA                                                   
COSTIN22 CLI   0(R5),EOT                                                        
         BE    COSTIN25                                                         
         MVC   CALEMOA,MONMOA      ENDDING MOA                                  
         AHI   R1,1                                                             
         LA    R5,MONLNQ(R5)                                                    
         B     COSTIN22                                                         
*                                                                               
COSTIN25 STC   R1,MONTH#           SAVE NUMBER OF MONTHS                        
         CLC   CSTMTHD,PRSPACE     SAME METHOD TYPE                             
         BH    *+8                                                              
         MVI   CSTMTHD,C'1'        SET DEFAULT                                  
         MVC   LASTSTD,PRSPACE                                                  
         MVC   LASTEDT,PRSPACE                                                  
         MVC   LASTKEY,PRSPACE                                                  
*                                                                               
COSTIN90 B     PERFACTX                                                         
         DROP  R5                                                               
         EJECT                                                                  
**********************************************************************          
*  PROCESS STANDARD OR EDIT HOURS FOR PERSON                         *          
**********************************************************************          
         SPACE 1                                                                
         USING MOND,R5                                                          
         USING CALD,R6                                                          
PROCHRS  CLI   LEV1RLEN,0          DO WE HAVE LEVEL LENGTHS?                    
         BNE   *+8                                                              
         BAS   RE,GET1RLDG                                                      
         SR    RE,RE                                                            
         IC    RE,LEV1RLEN                                                      
         LA    RE,CSTKEY1R+(ACTKACT-ACTRECD)(RE)                                
         CLC   =C'99',0(RE)        TEST OVERHEAD ACCOUNT                        
         BNE   PROCHR02                                                         
         L     RE,CSTIO            YES - CLEAR BUFFER AND EXIT                  
         XC    0(64,RE),0(RE)                                                   
         B     XIT                                                              
*                                                                               
PROCHR02 BAS   RE,BREAKLV          FILL IN STD HOURS KEY                        
         MVC   CSTOFFCD,STDHROFC   MOVE IN OFFICE                               
         MVC   CSTCALDR,=A(OFFCAL)                                              
         OI    CSTIND,CSTCCOF                                                   
         BAS   RE,GETCAL           TRY GETING OFFICE CALANDER                   
         BE    PROCHR05                                                         
         MVC   CSTCALDR,=A(CPYCAL)                                              
         OI    CSTIND,CSTCCCP                                                   
         NI    CSTIND,TURNOFF-CSTCCOF                                           
*                                                                               
PROCHR05 L     R6,CSTCALDR         CLEAR CALANDER HRS                           
         L     R5,=A(MONTAB)                                                    
         CLC   LASTKEY,STDHRKEY                                                 
         BE    PROCHR15                                                         
         MVC   LASTKEY,STDHRKEY    MOVE IN NEW KEY                              
         BAS   RE,SETLOCAL         GET AND SET LOCATION DATES                   
PROCHR10 CLI   0(R6),EOT                                                        
         BE    PROCHR15                                                         
         ZAP   CALSHRS,=P'0'                                                    
         ZAP   CALEHRS,=P'0'                                                    
         LA    R6,CALLNQ(R6)                                                    
         B     PROCHR10                                                         
*                                                                               
PROCHR15 CLI   0(R5),EOT           END OF TABLE                                 
         BE    PROCHR20                                                         
         MVC   MONHOURS,=PL5'0'                                                 
         LA    R5,MONLNQ(R5)                                                    
         B     PROCHR15                                                         
*                                                                               
PROCHR20 CLI   CSTMODE,CSTSTD      STANDARD HOURS?                              
         BNE   *+8                                                              
         BAS   RE,GETSTD                                                        
         CLI   CSTMODE,CSTEDT      EDIT HOURS?                                  
         BNE   *+8                                                              
         BAS   RE,GETEDT                                                        
         B     XIT                                                              
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* READ CALANDER RECORDS AND SET UP TABLES                             *         
***********************************************************************         
         SPACE 1                                                                
         USING CASRECD,R3                                                       
         USING TMPELD,R4                                                        
         USING MOND,R5                                                          
         USING CALD,R6                                                          
GETCAL   NTR1                                                                   
         L     R6,=A(CPYCAL)       COMPANY CALANDER                             
         TM    CSTIND,CSTCALCP     ALREADY HAVE?                                
         BNZ   GETCAL10                                                         
         XC    CALSTR,CALSTR       CALANDER START DATE YYMMDD                   
         XC    CALEND,CALEND                END   DATE YYMMDD                   
         XC    CALSMOA,CALSMOA     CALANDER START MOA YYMM                      
         XC    CALEMOA,CALEMOA              END   MOA YYMM                      
         OI    CSTIND,CSTCALCP     ABOUT TO GET, SO MARK GOT                    
         MVC   CSTOFFCD,PRSPACE                                                 
         B     GETCAL15                                                         
*                                                                               
GETCAL10 TM    CSTIND,CSTCALOF     WERE THERE ANY OFFICE CALANDERS?             
         BZ    GETCALNO            NO SO USE COMPANY CAL                        
         CLC   CSTOFFCD,CSTCCOFF   CURRENT CALLANDER OFFICE                     
         BE    GETCALOK            HAVE IT SO OK                                
         B     GETCAL50            GET COMPOSITE OFFICE CALANDER                
*                                                                               
GETCAL15 LA    R3,PRKEY                                                         
         XC    CASPAS,CASPAS                                                    
         MVI   CASPTYP,CASPTYPQ    X'3E' CALANDER PASSIVE POINTER               
         MVI   CASPSUB,CASPSUBQ    X'0C'                                        
         MVC   CASPCPY,CSTCPY      COMPANY CODE                                 
         MVC   CASPEDTE,CSTRQSTR   GET FIRST TO APPLY                           
         MVC   CASPOFC,CSTOFFCD                                                 
         L     R2,=A(OFFKYS)                                                    
         L     R5,=A(MONTAB)                                                    
         BAS   RE,READHI                                                        
*                                                                               
         USING OFFD,R2                                                          
GETCAL20 LA    R3,PRDIR                                                         
         CLI   CASPTYP,CASPTYPQ    X'3E'                                        
         BNE   GETCALOK                                                         
         CLI   CASPSUB,CASPSUBQ    X'0C'                                        
         BNE   GETCALOK                                                         
         CLC   CASPCPY,CSTCPY                                                   
         BNE   GETCALOK                                                         
         CLC   CSTRQEND,CASPSDTE   IN RANGE                                     
         BL    GETCALOK                                                         
         CLC   CSTRQSTR,CASPEDTE                                                
         BH    GETCALOK                                                         
         CLC   CASPOFC,PRSPACE     ONLY COMPANY CALANDER FIRST                  
         BE    GETCAL22                                                         
         OI    CSTIND,CSTCALOF     INDICATE THERE ARE OFFICE CALDRS             
         MVC   OFFCODE,CASPOFC     SAVE OFFICE KEYS                             
         MVC   OFFSDTE,CASPSDTE                                                 
         MVC   OFFEDTE,CASPEDTE                                                 
         LA    R2,OFFLNQ(R2)                                                    
         XC    OFFCODE,OFFCODE                                                  
         BAS   RE,READSEQ          GET NEXT CALANDER RECORD                     
         B     GETCAL20                                                         
*                                                                               
GETCAL22 OC    CALSTR,CALSTR       SAVE START OF FIRST CALANDER REC             
         BNE   *+10                                                             
         MVC   CALSTR,CASPSDTE     SET START DATE                               
         CLC   CASPEDTE,CSTRQEND                                                
         BL    *+10                                                             
         MVC   CALEND,CASPEDTE     SET END DATE                                 
         BAS   RE,GETREC                                                        
*                                                                               
         L     R3,CSTIO                                                         
         LA    R4,CASRFST          FIRST ELEMENT                                
         XC    MOND(MONLNQ),MOND                                                
         MVC   MONMOA,CASKSMOA     SET FIRST MOA                                
         MVC   MONHOURS,=PL5'0'                                                 
         ZIC   R0,MONTH#                                                        
         AHI   R0,1                                                             
         STC   R0,MONTH#                                                        
GETCAL30 CLI   0(R4),EOR           EOR                                          
         BE    GETCAL40                                                         
         CLI   0(R4),TMPELQ        X'88' TIMESHEET PERIODS                      
         BNE   GETCAL38                                                         
         CLC   MONMOA,TMPMTH       SAME MONTH?                                  
         BE    GETCAL35                                                         
         LA    R5,MONLNQ(R5)                                                    
         XC    MOND(MONLNQ),MOND                                                
         MVC   MONMOA,TMPMTH       SAVE MOA DATE                                
         MVC   MONHOURS,=PL5'0'                                                 
         AHI   R0,1                COUNT MONTHS                                 
         STC   R0,MONTH#                                                        
GETCAL35 STC   R0,CALBUCK          MONTH BUCKET NUMBER                          
         MVC   CALSDTE,TMPSTART    SAVE RANGE                                   
         MVC   CALEDTE,TMPEND                                                   
         MVC   CALMOA,TMPMTH                                                    
         MVC   CALPID#,TMPNUMB     PERIOD NUMBER                                
         LA    R6,CALLNQ(R6)       BUMP TO NEXT ENTRY                           
         XC    CALD(CALLNQ),CALD   CLEAR NEXT ENTRY                             
GETCAL38 SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     GETCAL30                                                         
*                                                                               
GETCAL40 BAS   RE,READSEQ                                                       
         LA    R5,MONLNQ(R5)                                                    
         XC    MOND(MONLNQ),MOND                                                
         B     GETCAL20                                                         
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*  GET OFFICE CALANDER, USED SAVED CAL/OFF KEYS                       *         
***********************************************************************         
GETCAL50 L     R2,=A(OFFKYS)                                                    
         OC    OFFCODE,OFFCODE                                                  
         BZ    GETCALNO            THIS OFFICE AIN'T HERE                       
         CLC   OFFCODE,CSTOFFCD                                                 
         BE    GETCAL55                                                         
         LA    R2,OFFLNQ(R2)                                                    
         B     *-24                                                             
*                                                                               
CPY      USING CALD,R6                                                          
OFF      USING CALD,R7                                                          
GETCAL55 LA    R0,1                                                             
         MVC   CSTCCOFF,CSTOFFCD   SAVE CURRENT OFFICE                          
         L     R5,=A(MONTAB)                                                    
         L     R6,=A(CPYCAL)       GET COMPANY CALANDER                         
         L     R7,=A(OFFCAL)       BUILD OFFICE CALANDER                        
*                                                                               
GETCAL58 LA    R3,PRKEY                                                         
         XC    CASPAS,CASPAS                                                    
         MVI   CASPTYP,CASPTYPQ    X'3E' CALANDER PASSIVE POINTER               
         MVI   CASPSUB,CASPSUBQ    X'0C'                                        
         MVC   CASPCPY,CSTCPY      COMPANY CODE                                 
         MVC   CASPSDTE,OFFSDTE                                                 
         MVC   CASPEDTE,OFFEDTE                                                 
         MVC   CASPOFC,OFFCODE                                                  
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'00'                                                            
         BAS   RE,GETREC                                                        
*                                                                               
         USING CASRECD,R3                                                       
         L     R3,CSTIO                                                         
         LA    R4,CASRFST          FIRST ELEMENT                                
         CLC   MONMOA,CASKSMOA                                                  
         BE    GETCAL62                                                         
         BL    *+6                                                              
         DC    H'00'                                                            
*                                                                               
         LA    R5,MONLNQ*12(R5)    BUMP UP 12 MONTHS                            
         AHI   R0,12               AND 12 BUCKET NUMBERS                        
GETCAL60 CLI   CPY.CALD,EOT                                                     
         BE    GETCAL70            FINISHED ALL                                 
         CLM   R0,1,CPY.CALBUCK                                                 
         BE    GETCAL70            FINISHED THIS RECORD                         
         MVC   OFF.CALD(CALLNQ),CPY.CALD                                        
         LA    R6,CALLNQ(R6)                                                    
         LA    R7,CALLNQ(R7)                                                    
         B     GETCAL60                                                         
*                                                                               
GETCAL62 CLI   0(R4),EOR           EOR                                          
         BE    GETCAL68                                                         
         CLI   0(R4),TMPELQ        X'88' TIMESHEET PERIODS                      
         BNE   GETCAL64                                                         
         CLC   MONMOA,TMPMTH                                                    
         BE    *+12                                                             
         AHI   R0,1                                                             
         LA    R5,MONLNQ(R5)                                                    
         STC   R0,OFF.CALBUCK                                                   
         MVC   OFF.CALSDTE,TMPSTART                                             
         MVC   OFF.CALEDTE,TMPEND                                               
         MVC   OFF.CALMOA,TMPMTH                                                
         MVC   OFF.CALPID#,TMPNUMB     PERIOD NUMBER                            
         LA    R7,CALLNQ(R7)                                                    
GETCAL64 SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     GETCAL62                                                         
*                                                                               
GETCAL68 AHI   R0,1                                                             
GETCAL69 CLI   CPY.CALD,EOT                                                     
         BE    GETCAL75                                                         
         CLM   R0,1,CPY.CALBUCK                                                 
         BE    GETCAL70                                                         
         LA    R6,CALLNQ(R6)           BUMP TO PAST THIS GROUP                  
         B     GETCAL69                                                         
*                                                                               
GETCAL70 LA    R2,OFFLNQ(R2)       BUMP TO NEXT OFF CAL KEY                     
         CLC   OFFCODE,CSTOFFCD    SAME OFFICE?                                 
         BE    GETCAL58            READ NEXT RECORD                             
GETCAL75 CLI   CPY.CALD,EOT        END OF COMPANY CALANDER?                     
         BNE   *+14                                                             
         XC    OFF.CALD(CALLNQ),OFF.CALD                                        
         B     GETCALOK                                                         
         MVC   OFF.CALD(CALLNQ),CPY.CALD                                        
         LA    R6,CALLNQ(R6)                                                    
         LA    R7,CALLNQ(R7)                                                    
         B     GETCAL75                                                         
*                                                                               
GETCALOK SR    RE,RE                                                            
GETCALNO LTR   RE,RE                                                            
         B     XIT                                                              
         DROP  CPY,OFF                                                          
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
*  GET STANDARD HOURS RECORD                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING STDRECD,R3                                                       
         USING MOND,R5                                                          
GETSTD   NTR1                                                                   
         MVI   STATUS,0                                                         
         LA    R3,PRKEY                                                         
         MVC   STDKEY,PRSPACE                                                   
         MVI   STDKTYP,STDKTYPQ          X'3E' STANDARD HOURS                   
         MVI   STDKSUB,STDKSUBQ          X'0D'                                  
         MVC   STDKCPY,CSTCPY                                                   
         MVC   STDKOFC(16),STDHRKEY      FOURTH LEVEL                           
         MVI   STDKYR,0                  OLD WAY                                
                                                                                
GETSTD08 BAS   RE,READ                                                          
         BE    GETSTD09                                                         
         TM    STATUS,NEWSTD         READ THE NEW WAY ?                         
         BO    GETSTD10              YES, SO ALREADY PROCESSED                  
         MVC   STDKOFC(16),STDHRKEY  NO, LOOK FOR NEW STD RECS                  
         MVC   STDKYR,CSTRQEND       GET YEAR FROM END DATE                     
         MVI   STDKSEQ,0                                                        
         OI    STATUS,NEWSTD         TURN ON NEW BIT                            
         B     GETSTD08              YES, SO TRY NEXT LEVEL                     
                                                                                
GETSTD09 BAS   RE,VALHRS                                                        
         BNE   GETSTD10            NOT IN RANGE CHECK NEXT LEVEL                
         BAS   RE,GETREC                                                        
         L     R2,CSTIO                                                         
         LA    R2,STDRFST-STDRECD(R2)  R2=A(FIRST ELEMENT)                      
         BAS   RE,FILLCAL                                                       
         BE    GETSTD60                                                         
*                                                                               
GETSTD10 L     R2,=A(STDHR3)                                                    
         NI    STATUS,TURNOFF-NEWSTD     RESET                                  
         CLC   STDHRKEY(8),LASTSTD                                              
         BNE   GETSTD12                                                         
         TM    CSTIND3,CSTSTD3     TEST SAVED STD HOURS AT THIS LVL             
         BO    GETSTD16                                                         
*                                                                               
GETSTD12 MVI   0(R2),EOR           MARK EOR                                     
         MVC   STDKPER,PRSPACE     THIRD LEVEL                                  
         MVI   STDKYR,0            OLD WAY                                      
         MVI   STDKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTSTD3                                          
                                                                                
GETSTD14 BAS   RE,READ                                                          
         BE    GETSTD15                                                         
         TM    STATUS,NEWSTD       DID WE PROCESS NEW WAY ?                     
         BO    GETSTD20            YES                                          
         MVC   STDKYR,CSTRQEND     NO, GET YEAR FROM END DATE                   
         MVI   STDKSEQ,0                                                        
         OI    STATUS,NEWSTD       TURN ON NEW BIT                              
         B     GETSTD14                                                         
                                                                                
GETSTD15 BAS   RE,VALHRS                                                        
         BNE   GETSTD20            STD CALANDER NOT OK                          
         OI    CSTIND3,CSTSTD3     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETSTD16 BAS   RE,FILLCAL                                                       
         BE    GETSTD60                                                         
*                                                                               
GETSTD20 L     R2,=A(STDHR2)                                                    
         NI    STATUS,TURNOFF-NEWSTD     RESET                                  
         CLC   STDHRKEY(5),LASTSTD                                              
         BNE   GETSTD22                                                         
         TM    CSTIND3,CSTSTD2     TEST SAVED STD HOURS AT THIS LVL             
         BO    GETSTD26                                                         
*                                                                               
GETSTD22 MVI   0(R2),EOR           MARK EOR                                     
         MVC   STDKSBD,PRSPACE     SECOND LEVEL                                 
         MVI   STDKYR,0            OLD WAY                                      
         MVI   STDKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTSTD2                                          
                                                                                
GETSTD24 BAS   RE,READ                                                          
         BE    GETSTD25            FOUND SO PROCESS                             
         TM    STATUS,NEWSTD       DID WE PROCESS NEW WAY ?                     
         BO    GETSTD30            YES                                          
         MVC   STDKYR,CSTRQEND     NO, GET YEAR FROM END DATE                   
         MVI   STDKSEQ,0                                                        
         OI    STATUS,NEWSTD       TURN ON NEW BIT                              
         B     GETSTD24                                                         
                                                                                
GETSTD25 BAS   RE,VALHRS                                                        
         BNE   GETSTD30            STD CALANDER NOT OK                          
         OI    CSTIND3,CSTSTD2     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETSTD26 BAS   RE,FILLCAL                                                       
         BE    GETSTD60                                                         
*                                                                               
GETSTD30 L     R2,=A(STDHR1)                                                    
         NI    STATUS,TURNOFF-NEWSTD     RESET                                  
         CLC   STDHRKEY(2),LASTSTD                                              
         BNE   GETSTD32                                                         
         TM    CSTIND3,CSTSTD1     TEST SAVED STD HOURS AT THIS LVL             
         BO    GETSTD36                                                         
*                                                                               
GETSTD32 MVI   0(R2),EOR           MARK EOR                                     
         MVC   STDKDPT,PRSPACE     FIRST LEVEL                                  
         MVI   STDKYR,0            OLD WAY                                      
         MVI   STDKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTSTD1                                          
                                                                                
GETSTD34 BAS   RE,READ                                                          
         BE    GETSTD35                                                         
         TM    STATUS,NEWSTD       DID WE PROCESS NEW WAY ?                     
         BO    GETSTD40            YES                                          
         MVC   STDKYR,CSTRQEND     NO, GET YEAR FROM END DATE                   
         MVI   STDKSEQ,0                                                        
         OI    STATUS,NEWSTD       TURN ON NEW BIT                              
         B     GETSTD34                                                         
                                                                                
GETSTD35 BAS   RE,VALHRS                                                        
         BNE   GETSTD40            STD CALANDER NOT OK                          
         OI    CSTIND3,CSTSTD1     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETSTD36 BAS   RE,FILLCAL                                                       
         BE    GETSTD60                                                         
*                                                                               
GETSTD40 L     R2,=A(STDHR0)                                                    
         NI    STATUS,TURNOFF-NEWSTD     RESET                                  
         TM    CSTIND3,CSTSTD0     DO I HAVE COMPANY ONE?                       
         BO    GETSTD46                                                         
         MVC   STDKOFC,PRSPACE     COMPANY WIDE                                 
         MVI   STDKYR,0            OLD WAY                                      
         MVI   STDKSEQ,X'40'                                                    
                                                                                
GETSTD42 BAS   RE,READ                                                          
         BE    GETSTD44                                                         
         TM    STATUS,NEWSTD       HAVE WE LOOKED FOR NEW STD RECS?             
         BO    GETSTD60            YES                                          
         MVC   STDKYR,CSTRQEND     NO, GET YEAR FROM END DATE                   
         MVI   STDKSEQ,0                                                        
         OI    STATUS,NEWSTD       TURN ON NEW BIT                              
         B     GETSTD42            NO STANDARD RECORD                           
                                                                                
GETSTD44 BAS   RE,VALHRS                                                        
         BNE   GETSTD60            NOT A VALID STD CALANDER FOR REQ             
         OI    CSTIND3,CSTSTD0     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETSTD46 BAS   RE,FILLCAL                                                       
*                                                                               
GETSTD60 BAS   RE,BUCKHRS                                                       
         MVC   LASTSTD,STDHRKEY                                                 
         TM    CSTIND2,CSTHRT      TEST RATES REQUIRED TOO                      
         BZ    XIT                                                              
         BAS   RE,BUCKRAT          SET RATES IN BUCKTAB                         
         B     XIT                                                              
         DROP  R3,R5                                                            
         EJECT                                                                  
***********************************************************************         
*  GET EDIT HOURS RECORD                                              *         
***********************************************************************         
         SPACE 1                                                                
         USING EDTRECD,R3                                                       
         USING MOND,R5                                                          
GETEDT   NTR1                                                                   
         MVI   STATUS,0                                                         
         LA    R3,PRKEY                                                         
         MVC   EDTKEY,PRSPACE                                                   
         MVI   EDTKTYP,EDTKTYPQ    X'3E' EDIT HOURS                             
         MVI   EDTKSUB,EDTKSUBQ    X'10'                                        
         MVC   EDTKCPY,CSTCPY                                                   
         MVC   EDTKOFC(16),EDTHRKEY      FOURTH LEVEL                           
         MVI   EDTKYR,0            OLD WAY                                      
                                                                                
GETEDT05 BAS   RE,READ                                                          
         BE    GETEDT08                                                         
         TM    STATUS,NEWEDT                                                    
         BO    GETEDT10                                                         
         MVC   EDTKYR,CSTRQEND       GET YEAR FROM END DATE                     
         MVI   EDTKSEQ,0                                                        
         OI    STATUS,NEWEDT         TURN ON NEW BIT                            
         B     GETEDT05                                                         
                                                                                
GETEDT08 BAS   RE,VALHRS                                                        
         BNE   GETEDT10            NOT IN RANGE CHECK NEXT LEVEL                
         BAS   RE,GETREC                                                        
         L     R2,CSTIO                                                         
         LA    R2,EDTRFST-EDTRECD(R2)  R2=A(FIRST ELEMENT)                      
         BAS   RE,FILLCAL                                                       
         BE    GETEDT60                                                         
*                                                                               
GETEDT10 L     R2,=A(EDTHR3)                                                    
         NI    STATUS,TURNOFF-NEWEDT                                            
         CLC   EDTHRKEY(8),LASTEDT                                              
         BNE   GETEDT12                                                         
         TM    CSTIND3,CSTEDT3     TEST SAVED EDIT HOURS AT THIS LVL            
         BO    GETEDT16            YES                                          
*                                                                               
GETEDT12 MVI   0(R2),EOR           MARK EOR                                     
         MVC   EDTKPER,PRSPACE     THIRD LEVEL                                  
         MVI   EDTKYR,0            OLD WAY                                      
         MVI   EDTKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTEDT3                                          
                                                                                
GETEDT14 BAS   RE,READ                                                          
         BE    GETEDT15                                                         
         TM    STATUS,NEWEDT                                                    
         BO    GETEDT20                                                         
         MVC   EDTKYR,CSTRQEND       GET YEAR FROM END DATE                     
         MVI   EDTKSEQ,0                                                        
         OI    STATUS,NEWEDT         TURN ON NEW BIT                            
         B     GETEDT14                                                         
                                                                                
GETEDT15 BAS   RE,VALHRS                                                        
         BNE   GETEDT20            EDIT CALANDER NOT OK                         
         OI    CSTIND3,CSTEDT3     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETEDT16 BAS   RE,FILLCAL                                                       
         BE    GETEDT60                                                         
*                                                                               
GETEDT20 L     R2,=A(EDTHR2)                                                    
         NI    STATUS,TURNOFF-NEWEDT                                            
         CLC   EDTHRKEY(5),LASTEDT                                              
         BNE   GETEDT22                                                         
         TM    CSTIND3,CSTEDT2     TEST SAVED EDIT HOURS AT THIS LVL            
         BO    GETEDT26                                                         
*                                                                               
GETEDT22 MVI   0(R2),EOR           MARK EOR                                     
         MVC   EDTKSBD,PRSPACE     SECOND LEVEL                                 
         MVI   EDTKYR,0            OLD WAY                                      
         MVI   EDTKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTEDT2                                          
                                                                                
GETEDT24 BAS   RE,READ                                                          
         BE    GETEDT25                                                         
         TM    STATUS,NEWEDT                                                    
         BO    GETEDT30                                                         
         MVC   EDTKYR,CSTRQEND       GET YEAR FROM END DATE                     
         MVI   EDTKSEQ,0                                                        
         OI    STATUS,NEWEDT         TURN ON NEW BIT                            
         B     GETEDT24                                                         
                                                                                
GETEDT25 BAS   RE,VALHRS                                                        
         BNE   GETEDT30            EDT CALANDER NOT OK                          
         OI    CSTIND3,CSTEDT2     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETEDT26 BAS   RE,FILLCAL                                                       
         BE    GETEDT60                                                         
*                                                                               
GETEDT30 L     R2,=A(EDTHR1)                                                    
         NI    STATUS,TURNOFF-NEWEDT                                            
         CLC   EDTHRKEY(2),LASTEDT                                              
         BNE   GETEDT32                                                         
         TM    CSTIND3,CSTEDT1     TEST SAVED EDIT HOURS AT THIS LVL            
         BO    GETEDT36                                                         
*                                                                               
GETEDT32 MVI   0(R2),EOR           MARK EOR                                     
         MVC   EDTKDPT,PRSPACE     FIRST LEVEL                                  
         MVI   EDTKYR,0            OLD WAY                                      
         MVI   EDTKSEQ,X'40'                                                    
         NI    CSTIND3,TURNOFF-CSTEDT1                                          
                                                                                
GETEDT34 BAS   RE,READ                                                          
         BE    GETEDT35                                                         
         TM    STATUS,NEWEDT                                                    
         BO    GETEDT40                                                         
         MVC   EDTKYR,CSTRQEND       GET YEAR FROM END DATE                     
         MVI   EDTKSEQ,0                                                        
         OI    STATUS,NEWEDT         TURN ON NEW BIT                            
         B     GETEDT34                                                         
                                                                                
GETEDT35 BAS   RE,VALHRS                                                        
         BNE   GETEDT40            EDT CALANDER NOT OK                          
         OI    CSTIND3,CSTEDT1     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETEDT36 BAS   RE,FILLCAL                                                       
         BE    GETEDT60                                                         
*                                                                               
GETEDT40 L     R2,=A(EDTHR0)                                                    
         NI    STATUS,TURNOFF-NEWEDT                                            
         TM    CSTIND3,CSTEDT0     DO I HAVE COMPANY ONE?                       
         BO    GETEDT46                                                         
         MVC   EDTKOFC,PRSPACE     COMPANY WIDE                                 
         MVI   EDTKYR,0            OLD WAY                                      
         MVI   EDTKSEQ,X'40'                                                    
                                                                                
GETEDT44 BAS   RE,READ                                                          
         BE    GETEDT45                                                         
         TM    STATUS,NEWEDT       HAVE WE LOOKED FOR NEW EDT RECS?             
         BO    GETEDT60            NO STANDARD RECORD                           
         MVC   EDTKYR,CSTRQEND     GET YEAR FROM END DATE                       
         MVI   EDTKSEQ,0                                                        
         OI    STATUS,NEWEDT       TURN ON NEW BIT                              
         B     GETEDT44                                                         
                                                                                
GETEDT45 BAS   RE,VALHRS                                                        
         BNE   GETEDT60            NOT A VALID EDT CALANDER FOR REQ             
         OI    CSTIND3,CSTEDT0     HAVE LEVEL                                   
         BAS   RE,GETREC                                                        
         BAS   RE,SAVESHR          SAVE OFF ELEMENTS                            
                                                                                
GETEDT46 BAS   RE,FILLCAL                                                       
*                                                                               
GETEDT60 BAS   RE,BUCKHRS                                                       
         MVC   LASTEDT,EDTHRKEY                                                 
         B     XIT                                                              
         DROP  R3,R5                                                            
         EJECT                                                                  
***********************************************************************         
*  BUILD BUCKETS IN IO AREA FOR HOURS (STANDARD/EDIT)                 *         
***********************************************************************         
         USING BUKELD,R2                                                        
         USING MOND,R5                                                          
BUCKHRS  NTR1                                                                   
         SR    R0,R0                                                            
         IC    R0,MONTH#                                                        
         L     R2,CSTIO                                                         
         L     R5,=A(MONTAB)       BUILD BUCKETS                                
BUCKHR10 MVI   BUKEL,BUKELQ        X'45'ELEMENT                                 
         MVI   BUKLN,BUKLNQ        LENGTH                                       
         MVC   BUKMOS,MONMOA                                                    
         ZAP   BUKCR,=P'0'                                                      
         ZAP   BUKDR,MONHOURS                                                   
         LA    R2,BUKLNQ(R2)       NEXT BUCKET                                  
         LA    R5,MONLNQ(R5)       NEXT MONTH                                   
         BCT   R0,BUCKHR10                                                      
         MVI   0(R2),EOR                                                        
BUCKHRX  B     XIT                                                              
         DROP  R2,R5                                                            
         EJECT                                                                  
***********************************************************************         
*  PLUG BUCKTAB WITH HOURLY RATES (STANDARD/EDIT)                     *         
***********************************************************************         
BUCKRAT  NTR1                                                                   
         MVC   SCSTIO,CSTIO        SAVE A(HOURS TABLE)                          
         L     R0,=A(LOCIO)                                                     
         ST    R0,CSTIO            REPLACE WITH A(LOCAL I/O AREA)               
         SPACE 1                                                                
         SR    R0,R0                                                            
         ICM   R0,3,CSTRQSTR       COMPLEMENT REQUESTED START/END DATES         
         L     RF,=A(TWOCOMP)                                                   
         SR    RF,R0                                                            
         STH   RF,STMOAC                                                        
         ICM   R0,3,CSTRQEND                                                    
         L     RF,=A(TWOCOMP)                                                   
         SR    RF,R0                                                            
         STH   RF,NDMOAC                                                        
         SPACE 1                                                                
         LA    R3,PRKEY            READ PAYROLL HISTORY RECORDS                 
         USING PHIRECD,R3          FOR PASSED 1R ACCOUNT                        
         XC    PHIKEY,PHIKEY                                                    
         MVI   PHIKTYP,PHIKTYPQ                                                 
         MVI   PHIKSUB,PHIKSUBQ                                                 
         MVC   PHIKCPY,CSTCPY                                                   
         MVC   PHIKOFC(L'STDHRKEY),STDHRKEY                                     
         XC    PHIKMOA,PHIKMOA                                                  
         MVC   SPHIKEY,PHIKEY                                                   
         BAS   RE,READHI                                                        
         B     *+8                                                              
BUCKR02  BAS   RE,READSEQ                                                       
         BNE   BUCKRX                                                           
         LA    R3,PRDIR                                                         
         CLC   SPHIKEY,PHIKEY      TEST SAME ACCOUNT                            
         BNE   BUCKRX                                                           
         CLC   PHIKMOA,NDMOAC      COMPARE COMPLEMENTED MONTHS                  
         BL    BUCKR02                                                          
         CLC   PHIKMOA,STMOAC                                                   
         BH    BUCKRX                                                           
         BAS   RE,GETREC                                                        
         L     R3,CSTIO                                                         
         LA    R4,PHIRFST                                                       
         USING PDEELD,R4                                                        
         SR    R0,R0                                                            
BUCKR04  CLI   PDEEL,0                                                          
         BE    BUCKR02                                                          
         CLI   PDEEL,PDEELQ        PAYROLL DETAIL ELEMENT CONTAINS RATE         
         BE    *+14                                                             
         IC    R0,PDELN                                                         
         AR    R4,R0                                                            
         B     BUCKR04                                                          
         L     RF,SCSTIO           FIND ENTRY IN HOURS TABLE                    
         USING BUKELD,RF                                                        
BUCKR06  CLI   BUKMOS,0                                                         
         BE    BUCKR02                                                          
         CLC   BUKMOS,PDEDTE                                                    
         BE    *+12                                                             
         LA    RF,BUKLNQ(RF)                                                    
         B     BUCKR06                                                          
         ZAP   BUKCR,PDEAMT        PLUG RATE INTO CR FIELD                      
         B     BUCKR02                                                          
         DROP  R3,R4,RF                                                         
         SPACE 1                                                                
BUCKRX   MVC   CSTIO,SCSTIO        RESTORE A(HOURS TABLE)                       
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*  VALIDATE THAT STD/EDIT HOUR CALANDER IS WITH IN DATE RANGE         *         
***********************************************************************         
         SPACE 1                                                                
         USING STDRECD,R3                                                       
VALHRS   LA    R3,PRDIR                                                         
         MVI   PRBYTE,1            SET CC TO NOT OK                             
         CLC   STDKENDT,CALSTR     IS IT IN RANGE?                              
         BNH   VALHRSX                                                          
         CLC   STDKSTDT,CALEND                                                  
         BNL   VALHRSX                                                          
         MVI   PRBYTE,0            SET CC TO OK                                 
VALHRSX  LA    R3,PRKEY            RESET TO KEY                                 
         CLI   PRBYTE,0            SET CC                                       
         BR    RE                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*  FILL IN STANDARD OR EDIT HOURS ACCOURDINGLY (R2=FIRST ELEMENT)     *         
***********************************************************************         
         USING SHRELD,R2                                                        
         USING MOND,R5                                                          
         USING CALD,R6                                                          
FILLCAL  NTR1                                                                   
         SR    R1,R1                                                            
FILCAL10 CLI   0(R2),EOR           END OF RECORD                                
         BE    FILCAL80                                                         
         L     R6,CSTCALDR                                                      
         CLI   0(R2),SHRELQ        X'89' STD HOURS ELEM                         
         BNE   FILCAL30            NEXT ELEMENT                                 
         CLC   SHREND,CALSTR                                                    
         BL    FILCAL30            NEXT ELEMENT                                 
         CLC   SHRSTART,CALEND                                                  
         BH    FILCAL30            NEXT ELEMENT                                 
*                                                                               
FILCAL12 CLI   0(R6),EOT           END OF CALANDER?                             
         BE    FILCAL30            FINISHED                                     
         TM    CALIND,CALLOCY      IS LOCATION OK?                              
         BZ    FILCAL20                                                         
         CLI   CSTMODE,CSTSTD      STANDARD HOURS?                              
         BNE   FILCAL14            NO, SO MUST BE EDIT HOURS                    
         TM    CALIND,CALSTDY      DID WE PROCESS THIS ONE ALREADY              
         BO    FILCAL20            YES                                          
         B     *+12                                                             
FILCAL14 TM    CALIND,CALEDTY      DID WE PROCESS THIS ONE ALREADY              
         BO    FILCAL20            YES                                          
         CLC   SHREND,CALSDTE                                                   
         BL    FILCAL20                                                         
         CLC   SHRSTART,CALEDTE                                                 
         BH    FILCAL20                                                         
*                                                                               
         ZAP   PRHOURS,SHRHOURS    SAVE STD HOURS                               
         TM    CALIND,CALLOCS      IS THIS A SPLIT LOCATION?                    
         BZ    FILCAL17                                                         
         GOTO1 CSTDATC,PRPARM,(1,CALSDTE),(0,PRWORK)                            
         GOTO1 CSTDATC,PRPARM,(1,CALEDTE),(0,PRWORK+6)                          
         GOTO1 CSTPERV,PRPARM,PRWORK,PRWORK+6                                   
         SR    R3,R3                                                            
         ICM   R3,3,8(R1)          NUMBER OF DAYS IN PERIOD                     
         CVD   R3,PRDUB                                                         
         ZAP   PRPACK16,CALDAYS                                                 
         MP    PRPACK16,=P'10000'                                               
         DP    PRPACK16,PRDUB      CALCULATE PERCENTAGE OF HOURS                
         ZAP   PRPACK,PRPACK16(8)  DROP REMAINDER                               
         MP    PRPACK,PRHOURS      MULTIPLY BY HOURS IN PERIOD                  
         SRP   PRPACK,64-6,5       ROUND TO NEAREST HOUR                        
         MP    PRPACK,=P'100'                                                   
         ZAP   PRHOURS,PRPACK      PUT BACK NEW HOURS                           
*                                                                               
FILCAL17 SR    R5,R5                                                            
         IC    R5,CALBUCK          GET BUCKET NUMBER                            
         SHI   R5,1                                                             
         MH    R5,=Y(MONLNQ)       NUMBER OF ENTRIES INTO TAB                   
         A     R5,=A(MONTAB)       ADD BASE                                     
         AP    MONHOURS,PRHOURS    ADD IN STD/EDIT HOURS BY MONTH               
         CLI   CSTMODE,CSTSTD      STANDARD HOURS?                              
         BNE   *+14                                                             
         ZAP   CALSHRS,PRHOURS     PUT STD HOURS IN CALANDER                    
         OI    CALIND,CALSTDY                                                   
         CLI   CSTMODE,CSTEDT      EDIT HOURS?                                  
         BNE   *+14                                                             
         ZAP   CALEHRS,PRHOURS     PUT EDIT HOURS IN CALANDER                   
         OI    CALIND,CALEDTY                                                   
*                                                                               
FILCAL20 LA    R6,CALLNQ(,R6)      NEXT CALANDER                                
         B     FILCAL12                                                         
*                                                                               
FILCAL30 SR    R1,R1                                                            
         IC    R1,1(,R2)                                                        
         AR    R2,R1                                                            
         B     FILCAL10                                                         
*                                                                               
FILCAL80 L     R6,CSTCALDR                                                      
*                                                                               
FILCAL82 CLI   0(R6),EOT           EOR                                          
         BE    FILCALOK            ALL WE MARKED DONE                           
         TM    CALIND,CALLOCY                                                   
         BZ    FILCAL84                                                         
         LA    RF,CALSTDY          STANDARD HOURS INDICATOR                     
         CLI   CSTMODE,CSTSTD                                                   
         BE    *+8                                                              
         LA    RF,CALEDTY          EDIT     HOURS INDICATOR                     
         EX    RF,*+8                                                           
         BZ    FILCALNO                                                         
         TM    CALIND,0            SOMETHING WAS NOT USED IF ZERO               
*                                                                               
FILCAL84 LA    R6,CALLNQ(,R6)                                                   
         B     FILCAL82                                                         
*                                                                               
FILCALOK SR    RE,RE                                                            
FILCALNO LTR   RE,RE                                                            
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*  SAVE OFF LEVEL OF STANDARD ELEMENTS AT R2                          *         
***********************************************************************         
         USING STDRECD,R3                                                       
OLD      USING SHRELD,R4                                                        
NEW      USING SHRELD,R2                                                        
SAVESHR  NTR1                                                                   
         SR    R1,R1                                                            
         L     R3,CSTIO                                                         
         LA    R4,STDRFST                                                       
SAVSHR10 CLI   OLD.SHREL,0         END OF RECORD?                               
         BE    SAVSHRX                                                          
         CLI   OLD.SHREL,SHRELQ                                                 
         BNE   SAVSHRX                                                          
         CLC   OLD.SHREND,CALSTR                                                
         BL    SAVSHR20            NEXT STD HRS ELEMENT                         
         CLC   OLD.SHRSTART,CALEND                                              
         BH    SAVSHR20            NEXT STD HRS ELEMENT                         
         MVC   NEW.SHREL(SHRLNQ),OLD.SHREL                                      
         LA    R2,SHRLNQ(R2)                                                    
         MVI   NEW.SHREL,EOR       MARK END OF RECORD                           
SAVSHR20 IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     SAVSHR10                                                         
*                                                                               
SAVSHRX  B     XIT                                                              
         DROP  OLD,NEW                                                          
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*  GET 1R LEDGER LENGTHS                                              *         
***********************************************************************         
         SPACE 1                                                                
         USING LDGRECD,R3                                                       
GET1RLDG NTR1                                                                   
         LA    R3,PRKEY                                                         
         XC    LEV1RLEN(4),LEV1RLEN                                             
         MVC   LDGKEY,PRSPACE                                                   
         MVC   LDGKCPY,CSTCPY                                                   
         MVC   LDGKUNT(2),=C'1R'                                                
         BAS   RE,READ             READ 1R LEDGER RECORD                        
         BE    *+6                                                              
         DC    H'00'                                                            
         BAS   RE,GETREC                                                        
         L     R3,CSTIO                                                         
         LA    R4,LDGRFST          POINT TO FIRST ELEMENT                       
*                                                                               
         SR    R1,R1                                                            
GET1RL10 CLI   0(R4),EOR                                                        
         BNE   *+6                 GOT TO HAVE X'16' ELEMENT                    
         DC    H'00'                                                            
         CLI   0(R4),ACLELQ        X'16' LEDGER LENGTHS                         
         BE    GET1RL20                                                         
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     GET1RL10                                                         
*                                                                               
         USING ACLELD,R4                                                        
GET1RL20 IC    R1,ACLLN            GET LENGTH                                   
         SHI   R1,ACLLN1Q                                                       
         LA    RE,LEV1RLEN                                                      
         LA    RF,ACLVALS                                                       
         SR    R2,R2                                                            
GET1RL22 MVC   0(1,RE),0(RF)       SAVE LENGTH                                  
         SHI   R1,L'ACLVALS                                                     
         LA    RE,1(RE)            NEXT LEV 1R LENGTH AREA                      
         LA    RF,L'ACLVALS(RF)    NEXT LEVEL                                   
         BP    GET1RL22            FINISHED                                     
         LA    R0,3                                                             
         SR    R1,R1                                                            
         SR    RF,RF                                                            
         LA    R3,LEV1RDSP                                                      
         LA    RE,LEV1RLEN                                                      
         MVC   LEV1RDSP(4),LEV1RLEN                                             
*                                                                               
GET1RL30 IC    R1,0(,R3)                                                        
         IC    RF,1(,R3)                                                        
         SR    RF,R1               LENGTH OF LEVEL                              
         STC   RF,1(,RE)                                                        
         LA    R3,1(,R3)                                                        
         LA    RE,1(,RE)                                                        
         BCT   R0,GET1RL30                                                      
*                                                                               
GET1RLX  B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
*  GET 1R PERSON LOCATION DATES                                                 
***********************************************************************         
         SPACE 1                                                                
         USING PERRECD,R3                                                       
         USING TIMRECD,R4                                                       
         USING CALD,R6                                                          
SETLOCAL NTR1                                                                   
         L     R6,CSTCALDR                                                      
         CLI   0(R6),EOT           END OF CALANDER?                             
         BE    SETLOC05                                                         
         MVI   CALIND,0                                                         
         LA    R6,CALLNQ(,R6)                                                   
         B     *-16                                                             
*                                                                               
SETLOC05 SR    R5,R5                                                            
         ICM   R5,15,CSTELM83      DID USER SUPPLY ELEMENTS?                    
         BNZ   SETLOC10                                                         
         LA    R3,PRKEY                                                         
         MVC   PERKEY,PRSPACE                                                   
         MVI   PERKTYP,PERKTYPQ    X'0F' PERSON RECORD                          
         MVC   PERKCPY,CSTCPY                                                   
         SR    RF,RF                                                            
         IC    RF,LEV4RLEN         GET LENGTH OF PERSON CODE                    
         SR    RE,RE                                                            
         IC    RE,LEV3RDSP                                                      
         LA    R4,CSTKEY1R         CURRENT KEY                                  
         LA    RE,TIMKACT(RE)      POINT TO PERSON CODE                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   PERKCODE(0),0(RE)   MOVE IN CODE                                 
         BAS   RE,READ             READ 1R LEDGER RECORD                        
         BNE   SETLOCX             PROBABLY AN OVERHEAD ACCOUNT                 
         BAS   RE,GETREC                                                        
*                                                                               
         SR    R1,R1                                                            
         L     R3,CSTIO                                                         
         LA    R5,PERRFST          POINT TO FIRST ELEMENT                       
SETLOC10 CLI   0(R5),EOR           END OF RECORD?                               
         BE    SETLOCX                                                          
         CLI   0(R5),LOCELQ        X'83' LOCATION DATES                         
         BE    SETLOC20                                                         
SETLOC15 IC    R1,1(R5)                                                         
         AR    R5,R1                                                            
         B     SETLOC10                                                         
*                                                                               
         USING LOCELD,R5                                                        
SETLOC20 IC    R1,LEV1RLEN         FIRST LEVEL                                  
         BCTR  R1,0                                                             
         EXCLC R1,LOCOFF,STDHROFC                                               
         BNE   SETLOC15            NOT CORRECT OFFICE                           
         IC    R1,LEV2RLEN         SECOND LEVEL                                 
         BCTR  R1,0                                                             
         EXCLC R1,LOCDEPT,STDHRDPT                                              
         BNE   SETLOC15            NOT CORRECT DEPARTMENT                       
         IC    R1,LEV3RLEN         THIRD LEVEL                                  
         BCTR  R1,0                                                             
         EXCLC R1,LOCSUB,STDHRSDP                                               
         BNE   SETLOC15            NOT CORRECT SUB DEPARTMENT                   
*                                                                               
         USING CALD,R6                                                          
         L     R6,CSTCALDR         LOAD CALANDER                                
         OC    LOCEND,LOCEND                                                    
         BNZ   *+10                                                             
         MVC   LOCEND,=X'FFFFFF'                                                
SETLOC30 CLI   0(R6),EOR                                                        
         BE    SETLOC15            TRY NEXT ELEMENT                             
         CLC   CALSDTE,LOCEND                                                   
         BH    SETLOC40                                                         
         CLC   CALEDTE,LOCSTART                                                 
         BL    SETLOC40                                                         
         CLC   CALSDTE,LOCSTART    CHECK FOR SPLIT                              
         BNL   SETLOC35                                                         
         OI    CALIND,CALSTRT      SPLIT LOCATION                               
         BAS   RE,GETDAYS                                                       
         B     SETLOC38                                                         
SETLOC35 CLC   CALEDTE,LOCEND      CHECK FOR SPLIT                              
         BNH   SETLOC38                                                         
         BAS   RE,GETDAYS          SPLIT LOCATION                               
SETLOC38 OI    CALIND,CALLOCY      LOCACTION OK                                 
SETLOC40 LA    R6,CALLNQ(R6)                                                    
         B     SETLOC30            GET NEXT                                     
         DROP  R6                                                               
*                                                                               
SETLOCX  B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*  BREAK UP LEVELS ON 1R INTO STD HOURS KEY                                     
***********************************************************************         
         USING TIMRECD,R4                                                       
BREAKLV  NTR1                                                                   
         SR    R1,R1                                                            
         LA    R4,CSTKEY1R                                                      
         MVC   STDHRKEY,PRSPACE                                                 
         IC    R1,LEV1RLEN         FIRST LEVEL                                  
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   STDHROFC(0),TIMKACT   MOVE IN OFFICE                             
*                                                                               
         LA    RE,TIMKACT+1(R1)    POINT TO DEPARTMENT CODE                     
         IC    R1,LEV2RLEN         SECOND LEVEL                                 
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   STDHRDPT(0),0(RE)   MOVE IN DEPARTMENT                           
*                                                                               
         LA    RE,1(R1,RE)         POINT TO SUB DEPT CODE                       
         IC    R1,LEV3RLEN         THIRD LEVEL                                  
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   STDHRSDP(0),0(RE)   MOVE IN SUB DEPARTMENT                       
*                                                                               
         LA    RE,1(R1,RE)         POINT TO PERSON CODE                         
         IC    R1,LEV4RLEN         THIRD LEVEL                                  
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   STDHRPER(0),0(RE)   MOVE IN PERSON                               
         MVC   EDTHRKEY,STDHRKEY                                                
BREAKLVX B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*  DETERMINE NUMBER OF DAYS IN PERIOD FOR THIS LOCATION                         
***********************************************************************         
         USING LOCELD,R5                                                        
         USING CALD,R6                                                          
GETDAYS  NTR1                                                                   
         TM    CALIND,CALSTRT      USE LOC START?                               
         BZ    GETDAY10            NO, USE LOC END DATE                         
         GOTO1 CSTDATC,PRPARM,(1,LOCSTART),(0,PRWORK)                           
         GOTO1 CSTDATC,PRPARM,(1,CALEDTE),(0,PRWORK+6)                          
         B     GETDAY20                                                         
*                                                                               
GETDAY10 GOTO1 CSTDATC,PRPARM,(1,CALSDTE),(0,PRWORK)                            
         GOTO1 CSTDATC,PRPARM,(1,LOCEND),(0,PRWORK+6)                           
*                                                                               
GETDAY20 GOTO1 CSTPERV,PRPARM,PRWORK,PRWORK+6                                   
         SR    R2,R2                                                            
         ICM   R2,3,8(R1)          NUMBER OF DAYS                               
         CVD   R2,PRDUB                                                         
         ZAP   CALDAYS,PRDUB                                                    
*                                                                               
GETDAYSX OI    CALIND,CALLOCS      MARK AS SPLIT                                
         B     XIT                                                              
         DROP  R5,R6                                                            
         EJECT                                                                  
***********************************************************************         
*  DATAMGR INTERFACE                                                            
***********************************************************************         
         SPACE 1                                                                
READ     MVC   COMMAND,=CL8'DMREAD'                                             
         MVC   FILE,=CL8'ACCDIR'                                                
         MVI   PRBYTE,0                                                         
         B     DMCALL                                                           
*                                                                               
READHI   MVC   COMMAND,=CL8'DMRDHI'                                             
         MVC   FILE,=CL8'ACCDIR'                                                
         MVI   PRBYTE,0                                                         
         B     DMCALL                                                           
*                                                                               
READSEQ  MVC   COMMAND,=CL8'DMRSEQ'                                             
         MVC   FILE,=CL8'ACCDIR'                                                
         MVI   PRBYTE,0                                                         
         B     DMCALL                                                           
*                                                                               
GETREC   MVC   COMMAND,=CL8'GETREC'                                             
         MVC   FILE,=CL8'ACCMST'                                                
         MVI   PRBYTE,1                                                         
         B     DMCALL                                                           
*                                                                               
         USING ACTRECD,R4                                                       
DMCALL   NTR1                                                                   
         LA    R3,PRKEY                                                         
         MVC   SAVEKEY,PRKEY                                                    
         LA    R4,PRDIR                                                         
         SR    R5,R5                                                            
         CLI   PRBYTE,1            GETREC FLAG                                  
         BNE   DMGO                                                             
         LA    R3,ACTKDA           A(ADDRESS OF REC)                            
         L     R4,CSTIO                                                         
         LA    R5,PRWRK                                                         
DMGO     GOTO1 CSTDMGR,PRPARM,COMMAND,FILE,(R3),(R4),(R5)                       
         TM    CSTDEBUG,CSTDUMP                                                 
         BZ    DMGO14                                                           
         MVC   PRWORK,PRSPACE                                                   
         MVC   PRWORK(8),=C'GETREC  '                                           
         L     RE,4(,RD)                                                        
         L     RE,4(,RE)               GET TO ORIGINAL CALLER                   
         MVC   PRWORK+10(4),0(RE)      MOVE IN CALL ROUTINE LABLE               
         CLI   PRBYTE,1                                                         
         BE    DMGO10                  JUST DUMP RECORD                         
         MVC   PRWORK(8),=C'READ KEY'                                           
         GOTO1 CSTPRTBL,PRPARM2,(15,PRWORK),(R3),C'DUMP',56,=C'1D'              
         MVC   PRWORK(8),=C'GOT KEY '                                           
         LA    R0,56                                                            
         B     DMGO12                                                           
*                                                                               
DMGO10   SR    R0,R0                                                            
         ICM   R0,3,ACTRLEN                                                     
*                                                                               
DMGO12   GOTO1 CSTPRTBL,PRPARM2,(15,PRWORK),(R4),C'DUMP',(R0),=C'1D'            
*                                                                               
DMGO14   CLI   PRPARM+8,0          SET CONDITION CODE FROM READ                 
         B     XIT                                                              
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0F                                                               
         DC    C'**OFKY**'                                                      
OFFKYS   DS    200CL(OFFLNQ)                                                    
         DS    0F                                                               
         DC    C'**MMOA**'                                                      
MONTAB   DS    ((12*15)+1)CL(MONLNQ)                                            
         DS    0F                                                               
         DC    C'**CCAL**'                                                      
CPYCAL   DS    XL(20*53*CALLNQ)                                                 
         DS    0F                                                               
         DC    C'**OCAL**'                                                      
OFFCAL   DS    XL(20*53*CALLNQ)                                                 
         DS    0F                                                               
         DC    C'**STD0**'                                                      
STDHR0   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**STD1**'                                                      
STDHR1   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**STD2**'                                                      
STDHR2   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**STD3**'                                                      
STDHR3   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**EDT0**'                                                      
EDTHR0   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**EDT1**'                                                      
EDTHR1   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**EDT2**'                                                      
EDTHR2   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**EDT3**'                                                      
EDTHR3   DS    XL1500                                                           
         DS    0F                                                               
         DC    C'**LOCIO*'                                                      
LOCIO    DS    XL2048                                                           
         DS    0F                                                               
         EJECT                                                                  
PRWORKD  DSECT                                                                  
PACK16   DS    PL16                                                             
BUCKET   DS    XL1                 BUCKET TO PROCESS                            
CUROFC   DS    CL2                                                              
BINKEY   DS    CL20                                                             
PRKEY    DS    CL(ACCKLEN)                                                      
SAVEKEY  DS    CL(ACCKLEN)                                                      
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
SCSTIO   DS    A                   SAVED CSTIO                                  
STMOAC   DS    H                   COMPLEMENTED START YYMM                      
NDMOAC   DS    H                        "       END   YYMM                      
SPHIKEY  DS    CL(PHIKMOA-PHIKEY)                                               
                                                                                
STATUS   DS    X                   STATUS                                       
NEWSTD   EQU   X'80'               .  TRIED NEW STANDARD   RECORD               
NEWEDT   EQU   X'40'               .  TRIED NEW EDIT HOURS RECORD               
*                                                                               
PRSPACE  DS    CL132                                                            
PRDIR    DS    CL60                                                             
PRWRK    DS    12D                                                              
PRPARM   DS    6F                                                               
PRPARM2  DS    6F                                                               
PRDUB    DS    D                                                                
PRFULL   DS    F                                                                
PRRELO   DS    A                                                                
PRPACK   DS    PL8                                                              
PRPACK16 DS    PL16                                                             
PRHOURS  DS    PL4                                                              
PRSVHR   DS    PL4                                                              
PRWORK   DS    XL64                                                             
PRBYTE   DS    X                                                                
PRDISP   DS    H                                                                
PRHALF   DS    H                                                                
PRDATE   DS    CL3                                                              
PRSTRT   DS    CL3                                                              
PREND    DS    CL3                                                              
PRSTAT   DS    CL1                                                              
PRWRKLNQ EQU   *-PRWORKD                                                        
*                                                                               
EOT      EQU   0                                                                
EOR      EQU   0                                                                
TURNOFF  EQU   X'FF'                                                            
TWOCOMP  EQU   X'10000'                                                         
         EJECT                                                                  
MOND     DSECT                                                                  
MONMOA   DS    PL2                 MOA BUCKET YYMM                              
MONHOURS DS    PL5                 STANARD OR EDIT HOURS                        
MONLNQ   EQU   *-MOND                                                           
         SPACE 2                                                                
*&&DO                                                                           
CALD     DSECT                                                                  
CALSDTE  DS    PL3                 START PERIOD RANGE YYMMDD                    
CALEDTE  DS    PL3                 END   PERIOD RANGE YYMMDD                    
CALBUCK  DS    XL1                 1-36  BUCKET NUBMER                          
CALIND   DS    XL1                                                              
CALLOCY  EQU   X'80'               LOCATION IS OK                               
CALSTDY  EQU   X'40'               MARK STANDARD PERIOD DONE                    
CALEDTY  EQU   X'20'               MARKED EDIT PERIOD DONE                      
CALPID#  DS    XL1                 PERIOD NUMBER                                
CALHRS   DS    PL3                 PERSONS STD OR EDIT HOURS BY PERIOD          
CALLNQ   EQU   *-CALD                                                           
*&&                                                                             
         SPACE 2                                                                
OFFD     DSECT                                                                  
OFFCODE  DS    CL2                                                              
OFFSDTE  DS    PL3                                                              
OFFEDTE  DS    PL3                                                              
OFFLNQ   EQU   *-OFFD                                                           
         EJECT                                                                  
       ++INCLUDE ACPERFACTC                                                     
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* ACREPWORKD                                                                    
*        PRINT OFF                                                              
*        INCLUDE ACREPWORKD                                                     
*        PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'043ACPERFACT 03/12/07'                                      
         END                                                                    
