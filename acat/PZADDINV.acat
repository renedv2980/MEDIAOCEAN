*          DATA SET PZADDINV   AT LEVEL 097 AS OF 05/01/02                      
*CATALP PZADDINV                                                                
*        TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE'                            
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - CHANGE LOG'               
***********************************************************************         
*                                                                     *         
*        USED WITH PPZ502 - LINK WITH LKPPZ502                        *         
*                                                                     *         
***********************************************************************         
*                                                                     *         
*        CHANGE LOG                                                   *         
*                                                                     *         
*  BOBY NOV/94    CREATION                                            *         
*                                                                     *         
***********************************************************************         
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - INIT'                     
***********************************************************************         
*                                                                     *         
*        INITIALIZATION                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
PZADDINV CSECT                                                                  
         NMOD1 PZADIDL,**PZADI                                                  
*                                                                               
         LA    RA,COMSUBS          ESTABLISH COMMON SUBROUTINES                 
         USING COMSUBS,RA                                                       
*                                                                               
         USING PZADID,RC           ESTABLISH WORKING STORAGE                    
*                                                                               
         L     R7,0(R1)            A(EZBLOCK)                                   
         USING EZBLOCKD,R7         ESTABLISH INCOMING EZBLOCK                   
*                                                                               
         L     R8,4(R1)            A(PPWORKD)                                   
         USING PPWORKD,R8          ESTABLISH REPORT WORKING STORAGE             
         L     R9,PPWORK2C         POINT TO SECOND PART OF STORAGE              
         USING PPWORK2D,R9         ESTABLISH SECOND REPORT WORKING STG          
*                                                                               
         MVI   EZERRCD,0           CLEAR ERROR                                  
*                                                                               
         CLI   ADSCTL,ADS1STQ      SKIP IF NOT FIRST TIME                       
         BNE   *+12                                                             
         BAS   RE,MININIT             INIT STORAGE AREAS                        
         MVI   ADSCTL,0               CLEAR FIRST TIME SWITCH                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - MODE'                     
***********************************************************************         
*                                                                     *         
*        DETERMINE MODE                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         CLI   EZMODE,EZINVP       PROC INV                                     
         BE    PRINV                                                            
*                                                                               
         CLI   EZMODE,EZSPTP       PROCESS PRINT DETAIL                         
         BE    PRANN                                                            
*                                                                               
         CLI   EZMODE,EZINVL       LAST FOR INVOICE                             
         BE    LINV                                                             
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRINV'                    
***********************************************************************         
*                                                                     *         
*        PROCESS INVOICE HEADER                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRINV    DS    0H                                                               
*                                                                               
         MVC   SVRECSEQ,EZRECSEQ    SAVE RECORD SEQUENCE NUMBER                 
*                                                                               
         L     RF,=A(DETBUFF)      RESET DETAIL BUFFER                          
         MVI   0(RF),X'FF'         SET END                                      
         ST    RF,ANXTDET                                                       
         XC    LASTPRD,LASTPRD     CLEAR PRODUCT                                
         XC    LASTEST,LASTEST           EST                                    
         XC    LASTZED,LASTZED           ZONE/EDIT                              
         MVI   INSSTAT,0                 AND INSERT STATUS                      
         MVI   MPRSTAT,0                 AND MISSING PRD SWITCH                 
*                                                                               
PRINVX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
***********************************************************************         
*                                                                     *         
*        PROCESS ANNOUNCEMENT                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRANN    DS    0H                                                               
*                                  SAVE IN DETAIL BUFFER                        
         L     RF,ANXTDET          SPOT FOR NEXT DETAIL                         
         C     RF,=A(DETBUFFX)     TEST AT END OF BUFFER                        
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   0(256,RF),EZSPFRST                                               
         MVC   256(256,RF),EZSPFRST+256                                         
         MVC   512(EZSPDLEN-512,RF),EZSPFRST+512                                
         MVI   EZSPDLEN(RF),X'FF'                                               
*                                                                               
         LA    RF,EZSPDLEN(RF)                                                  
         ST    RF,ANXTDET                                                       
*                                                                               
         CLI   EZIHCVPR,C' '       IF HAVE CLIENT OVERRIDE                      
         BNH   *+10                                                             
         MVC   EZIHLPRC,EZIHCVPR   USE IT                                       
*                                                                               
         CLI   EZSPCVPR,C' '       USE OVERIDE PROD IF PRESENT                  
         BNH   *+10                                                             
         MVC   EZSPLPRC,EZSPCVPR                                                
         CLI   EZSPLPRC,C'A'       IF NO PRODUCT AT DETAIL                      
         BNL   *+16                                                             
         MVC   EZSPLPRC,EZIHLPRC   USE INVOICE PRODUCT CODE                     
         MVC   EZSPLPRN,EZIHLPRN   AND NAME                                     
*                                                                               
         CLI   EZIHLPRN,C' '                                                    
         BH    *+10                                                             
         MVC   EZIHLPRN,EZSPLPRN   SET INV PRD NAME TO DETAIL                   
*                                                                               
         CLI   EZMPROPT,C'Y'       OPTION TO NOT CVT IF MISSING PRD             
         BNE   PRANN04                                                          
         CLI   EZSPLPRC,C'A'                                                    
         BNL   *+8                                                              
         MVI   MPRSTAT,C'Y'        SET HAVE MISSING PRODUCT                     
*                                                                               
PRANN04  DS    0H                                                               
         CLI   INSSTAT,0            TEST FIRST TIME                             
         BNE   PRANN06                                                          
         MVC   LASTPRD,EZSPLPRC                                                 
         MVC   LASTEST,EZSPBEST                                                 
         MVC   LASTZED,EZSPLZON                                                 
         MVI   INSSTAT,X'80'       SET HAVE INSERT                              
         B     PRANN08                                                          
*                                                                               
PRANN06  DS    0H                                                               
         CLC   LASTPRD,EZSPLPRC    IS PRODUCT THE SAME                          
         BE    PRANN07                                                          
         OI    INSSTAT,X'40'       NO, SET HAVE MULTI PRD                       
         CLI   EZSPLPRC,C' '       AND USE ANY PRESENT CODE                     
         BNH   *+10                                                             
         MVC   LASTPRD,EZSPLPRC    TO SET LASTPRD                               
*                                                                               
PRANN07  DS    0H                                                               
         CLC   LASTEST,EZSPBEST    IS ESTIMATE THE SAME                         
         BE    *+8                                                              
         OI    INSSTAT,X'20'       NO, SET HAVE MULTI EST                       
*                                                                               
         CLC   LASTZED,EZSPLZON    IS ZONE/EDIT THE SAME                        
         BE    *+8                                                              
         OI    INSSTAT,X'10'       NO, SET HAVE MULTI ZONE/EDIT                 
*                                                                               
PRANN08  DS    0H                                                               
PRANNX   DS    0H                                                               
         XIT1                                                                   
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - LINV'                     
***********************************************************************         
*                                                                     *         
*        LAST FOR INVOICE                                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
LINV     DS    0H                                                               
*                                                                               
         BAS   RE,DOINV            INVOICE PROCEDURE                            
         CLI   EZERRCD,0           ANY ERROR?                                   
         BNE   LINV96              YES, SKIP WORKER FILE MARKING                
*                                                                               
LINV90   DS    0H                  MARK INVOICE AS CONVERTED                    
*                                                                               
         CLI   EZTEST,C'Y'         TEST TO SUPPRESS MARKING                     
         BE    LINV96                                                           
*                                                                               
         L     RF,EZWKRREC         PREPARE FOR 'RANDOM' READ                    
         XC    0(12,RF),0(RF)      OF INVOICE HEADER                            
         MVC   2(2,RF),SVRECSEQ                                                 
         MVC   4(4,RF),=C'REC '                                                 
         GOTO1 DATAMGR,DMCB,=C'RANDOM',EZWKRFIL,EZWKRIND,EZWKRREC,     X        
               EZWKRBUF                                                         
*                                                                               
*                               SET CONVERSION DATA IN INVOICE HEADER           
         L     RF,EZWKRREC                                                      
         LA    R3,7(RF)            SKIP RECLEN(4),RECCODE(2),DELIM(1)           
         OI    0(R3),EZIHCVQ       INDICATE A CONVERTED INVOICE                 
         NI    0(R3),X'FF'-EZIHRCVQ  TURN OFF ANY RECONVERTED INDICATOR         
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(1,1(R3)) TODAY PWOS                           
*                                                                               
         MVC   4(3,R3),EZIHLADC    CLIENT                                       
*                                                                               
         MVC   7(3,R3),=C'***'     SET MULTI-PROD                               
*                                                                               
         CLI   LASTPRD,C'A'        IF NO INSERT HAD PRODUCT                     
         BNH   LINV94                                                           
         TM    INSSTAT,X'40'       IS IT MULTI PRODUCT?                         
         BNZ   LINV94                                                           
*                                                                               
         MVC   7(3,R3),LASTPRD     NO, USE SINGLE PROD                          
*                                                                               
LINV94   DS    0H                                                               
         CLI   EZESTOPT,C'Y'       DOING ESTIMATES?                             
         BNE   LINV95                                                           
         TM    INSSTAT,X'20'       IF MULTI DETAIL ESTS                         
         BNZ   LINV95              LEAVE EST AS NULL                            
         OC    10(2,R3),LASTEST    USE ESTIMATE FROM DETAILS                    
         BNZ   *+10                IF PRESENT                                   
         MVC   10(2,R3),EZIHBEST   ELSE HEADER ESTIMATE                         
*                                                                               
LINV95   DS    0H                                                               
         GOTO1 DATAMGR,DMCB,=C'WRITE',EZWKRFIL,EZWKRIND,EZWKRREC                
*                                                                               
*              NOW RE-READ CURRENT RECORD                                       
*                                                                               
         L     RF,EZWKRREC         PREPARE FOR 'RANDOM' READ                    
         XC    0(12,RF),0(RF)      OF INVOICE HEADER                            
         MVC   2(2,RF),EZRECSEQ                                                 
         MVC   4(4,RF),=C'REC '                                                 
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'RANDOM',EZWKRFIL,EZWKRIND,EZWKRREC,     X        
               EZWKRBUF                                                         
*                                                                               
*                                                                               
LINV96   DS    0H                                                               
*                                                                               
LINVX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PZADDINV - INVOICE PROCEDURE - DOINV'                           
***********************************************************************         
*                                                                     *         
*        DOINV - HANDLE INVOICE (CALLED AT EZINVL)                    *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
DOINV    NTR1                                                                   
*                                                                               
         CLI   EZIHLADC,C' '       TEST HAVE ADVERTISER                         
         BNH   PRERRANF                                                         
*                                                                               
         CLI   LASTPRD,C' '        TEST HAVE ANY PRODUCT                        
         BNH   PRERRPNF                                                         
*                                                                               
         CLI   MPRSTAT,C'Y'        TEST MISSING PROD ON ANY INSERT              
         BE    PRERRMPR                                                         
*                                                                               
         MVI   HDRSQ,0             INIT HEADER SEQUENCE NUMBER                  
         MVI   HDRSQOLD,0          INIT OLD HEADER SEQUENCE NUMBER              
*                                                                               
*        BUILD MASTER INVOICE KEY                                               
*                                                                               
         LA    R5,MINBLK                                                        
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         MVI   MINOPEN,C'N'        FORCE NEW RECORD                             
*                                                                               
         XC    MINMKEY,MINMKEY                                                  
         LA    R4,MINMKEY          ESTABLISH NEW INVOICE RECORD KEY             
         USING PINVKEY,R4                                                       
*                                                                               
         MVC   PINVAGY,EZAGY       AGENCY ALPHA CODE                            
         MVC   PINVMED,EZMED       MEDIA                                        
         MVI   PINVTYPE,PINVTYPQ   RECORD TYPE                                  
*                                                                               
         MVC   PINVCLT,EZIHLADC    THIS WILL ALWAYS BE RIGHT CLIENT             
*                                                                               
         MVC   PINVPRD,=C'***'     SET MULTI-PROD                               
         CLI   LASTPRD,C'A'        IF NO INSERT HAD PRODUCT                     
         BNH   DV04                                                             
         TM    INSSTAT,X'40'       IS IT MULTI PRODUCT?                         
         BNZ   DV04                                                             
         MVC   PINVPRD,LASTPRD     NO, USE SINGLE PROD                          
*                                                                               
DV04     DS    0H                                                               
         CLC   PINVPRD,=C'***'                                                  
         BNE   *+10                                                             
         MVC   EZIHLPRN,=CL20'VARIOUS'                                          
*                                                                               
         MVC   PINVPUB,EZSNPUB     PUB/ZONE/EDN                                 
         OC    EZSNPUB+4(2),EZSNPUB+4  IF ZONE/ED PRESENT HERE                  
         BNZ   DV06                    SKIP AT DETAIL LEVEL???                  
*                                                                               
         MVC   PINVPUB+4(2),=X'FFFF'                                            
         TM    INSSTAT,X'10'       MULTI ZONE/ED?                               
         BNZ   DV06                                                             
         MVC   PINVPUB+4(2),LASTZED     NO, USE ONLY ONE                        
*                                                                               
DV06     DS    0H                                                               
         MVC   PINVYS,EZIHDMOS+5   YEAR OF SERVICE                              
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,PINVKEY             DUMP KEY                                  
         BAS   RE,DMPREC                                                        
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINOPN',MINBLKD)    OPEN FILE MINIO BLCK         
*                                                                               
         CLI   MINERR,MINESNF      SKIP IF RECORD NOT FOUND                     
         BE    PRHDRDN                                                          
*                                                                               
         CLI   MINERR,0            NO OTHER ERRORS TOLERTED                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    MINSTAT,MINDELQ     IF RECORD SET IS DELETED                     
         BNO   PRANOLDX                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINRSF',MINBLKD)  RESTORE MINIO SET              
*                                                                               
PRANOLDX DS    0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        FIND NEXT AVAILABLE HEADER SEQUENCE NUMBER                             
*        OR CURRENT SEQUENCE NUMBER                                             
*                                                                               
PRHDRSQ  DS    0H                                                               
*                                                                               
         XC    DTLSQ,DTLSQ         INIT DETAIL SEQUENCE NUMBER                  
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          ESTABLISH ELEMENT WORKAREA                   
         USING PIMHDREL,R2         AS INVOICE HEADER ELEMENT                    
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET ELEMENT IDENTIFIER                       
         MVI   PIMHDRLN,PIMHDRSQ-PIMHDREL  LOOK AT ALL HEADER ELMS              
*                                                                               
PRHDRLP  DS    0H                                                               
*                                                                               
         GOTO1 GETEL,DMCB,MINBLKD,PINVELM,0 FIND HEADER ELM                     
*                                                                               
         ICM   R2,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    PRHDRDN             EOF - NEW INVOICE                            
*                                                                               
         CLI   HDRSQ,0             IF NO HEADER SEQUENCE FOUND YET              
         BNE   *+10                                                             
         MVC   HDRSQ,PIMHDRSQ         SAVE THE ONE FROM FOUND ELEMENT           
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRHDRESX                                                         
*                                                                               
         OC    EZIHCVES,EZIHCVES   IF ESTIMATE OVERRIDE PRESENT                 
         BZ    PRHDRES4                                                         
         CLC   PIMEST,EZIHCVES      MATCH TO OVRD ESTIMATE                      
         BE    PRHDRESX                                                         
         B     PRHDRCN                ELSE NO MATCH                             
*                                                                               
PRHDRES4 DS    0H                                                               
         OC    EZIHBEST,EZIHBEST   IF HEADER ESTIMATE AVAILABLE                 
         BZ    PRHDRESX                                                         
         CLC   PIMEST,EZIHBEST        MATCH TO HEADER ESTIMATE                  
         BE    PRHDRESX                                                         
         B     PRHDRCN                ELSE NO MATCH                             
*                                                                               
PRHDRESX DS    0H                                                               
         MVC   WKINVNO,PIMINVNO       SPACE FILL INVOICE NUMBER                 
         OC    WKINVNO,SPACES                                                   
*                                                                               
         CLC   WKINVNO(10),EZIHINV    MATCH ON INVOICE NUMBER                   
         BE    PRHDRFD                                                          
*                                                                               
PRHDRCN  DS    0H                                                               
*                                                                               
         SR    RF,RF               BUMP SEQUENCE NUMBER                         
         IC    RF,PIMHDRSQ                                                      
         LA    RF,1(RF)                                                         
         STC   RF,PIMHDRSQ-PIMHDREL+PINVELM                                     
*                                                                               
         CLI   PIMHDRSQ-PIMHDREL+PINVELM,0 IF ZERO THEN ONLY 1 HDR (FF)         
         BE    PRHDRDN                                                          
*                                                                               
         B     PRHDRLP                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
*        HEADER ALREADY EXISTS                                                  
*           ALLOWED IF REPLACING INVOICES                                       
*                                                                               
PRHDRFD  DS    0H                                                               
*                                                                               
         MVC   HDRELMSV,PIMHDREL   SAVE HEADER ELEMENT FOR LATER                
*                                                                               
         MVC   HDRSQOLD,PIMHDRSQ      SAVE SEQUENCE NUMBER                      
*                                                                               
         CLI   EZRPLOPT,C'Y'       ERROR IF NOT ALLOWED TO REPLACE INV          
         BE    PRANDEL                                                          
         TM    EZIHCVST,EZIHRCVQ     OR TO RECONVERT                            
         BZ    PRERRDUP                                                         
*                                                                               
*        DELETE INVOICE FROM FILE                                               
*                                                                               
PRANDEL  DS    0H                                                               
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,MINMKEY             DUMP KEY                                  
         BAS   RE,DMPREC                                                        
*                                                                               
         LA    R2,PINVELM          ESTABLISH AS HEADER ELEMENT                  
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET KEY FOR THIS INVOICE                     
         MVC   PIMHDRSQ,HDRSQOLD   SET SEQUENCE NUMBER                          
*                                                                               
         GOTO1 GETEL,DMCB,MINBLKD,PINVELM,0 READ FOR HEADER                     
*                                                                               
         ICM   RF,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    PRANDELX            NONE FOUND                                   
*                                                                               
         GOTO1 DELEL,DMCB,MINBLKD,PINVELM,0   DELETE HEADER                     
*                                                                               
*        DELETE ALL DETAILS AND COMMENTS FOR INVOICE                            
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY                             
*                                                                               
         MVI   MINEKEY,PIMDTLEQ       SET DETAIL ELEMENT ID                     
         MVC   MINEKEY+1(1),HDRSQOLD  SET HEADER SEQUENCE NUMBER                
*                                                                               
         MVI   MINFILTL,PIMDTLS2-PIMDTLEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
PRANDTLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   PRANDTDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
PRANDTCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     PRANDTLP                                                         
*                                                                               
PRANDTDN DS    0H                                                               
*                                                                               
*        DELETE COMMENTS                                                        
*                                                                               
         MVI   MINEKEY,PIMCOMEQ       SET COMMENT ELEMENT ID                    
         MVC   MINEKEY+1(1),HDRSQOLD  SET HEADER SEQUENCE NUMBER                
*                                                                               
         MVI   MINFILTL,PIMCOMS2-PIMCOMEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
PRANCMLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   PRANCMDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
PRANCMCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     PRANCMLP                                                         
*                                                                               
PRANCMDN DS    0H                                                               
*                                                                               
PRANDELX DS    0H                                                               
*                                                                               
*        NEW INVOICE GROUP                                                      
*                                                                               
PRHDRDN  DS    0H                                                               
*                                                                               
         OC    HDRSQOLD,HDRSQOLD   IF INVOICE ALREADY ON FILE                   
         BZ    *+14                                                             
         MVC   HDRSQ,HDRSQOLD         USE ITS SEQUENCE NUMBER                   
         B     PRHDRSQX                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,HDRSQ            ELSE DECREMENT FIRST ON FILE                 
         BCTR  RF,0                                                             
         STC   RF,HDRSQ                                                         
*                                                                               
PRHDRSQX DS    0H                                                               
*                                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRPIMHDR'                 
***********************************************************************         
*                                                                     *         
*        ADD INVOICE HEADER ELEMENT                                   *         
*        ADD ACTIVITY       ELEMENT                                   *         
*        ADD DETAIL         ELEMENTS                                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRPIMHDR DS    0H                                                               
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD INVOICE HEADER ELEMENT                 
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   ELEMENT ID                                   
         MVI   PIMHDRLN,PIMHDRLQ   ELEMENT LENGTH                               
         MVC   PIMHDRSQ,HDRSQ      HEADER SEQUENCE NUMBER                       
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRPIM6                                                           
*                                                                               
         TM    INSSTAT,X'20'       IF MULTI DETAIL ESTS                         
         BNZ   PRPIM4              LEAVE EST AS NULL                            
         OC    PIMEST,LASTEST      USE ESTIMATE FROM DETAILS                    
         BNZ   *+10                IF PRESENT                                   
         MVC   PIMEST,EZIHBEST     ELSE HEADER ESTIMATE                         
*                                                                               
PRPIM4   DS    0H                                                               
PRPIM6   DS    0H                                                               
         OI    PIMSTAT,X'40'       ASSUME GROSS, NOT NET                        
         OC    EZITBCD,EZITBCD     IF NO CD IN INV TOTALS                       
         BNZ   *+8                 SAY SO IN HEADER ELEM                        
         OI    PIMSTAT,X'20'   **  NOTE- SEE ALSO PIMDSTAT                      
*                                                                               
         MVC   PIMINVDT,EZIHBIDT   INVOICE DATE                                 
*                                                                               
         MVC   PIMINVNO(10),EZIHINV  INVOICE NUMBER                             
*                                                                               
*        NULL FILL END OF INVOICE NUMBER                                        
*                                                                               
         LA    R0,L'PIMINVNO       NUMBER OF BYTES IN INVOICE NUMBER            
         LA    RF,PIMINVNO+L'PIMINVNO-1  LAST BYTE OF INVOICE NO.               
*                                                                               
         CLI   0(RF),C' '          IF NOT A SPACE                               
         BH    *+14                   DONE                                      
         MVI   0(RF),0             ELSE MAKE NULLS                              
         BCTR  RF,0                BACK UP A BYTE                               
         BCT   R0,*-14                                                          
*                                                                               
         CLC   EZIHDISD,SPACES     IF EITHER INVOICE START                      
         BNH   *+10                                                             
         CLC   EZIHDIED,SPACES     OR END DATE MISSING                          
         BH    PRHDRIDT                                                         
*                                                                               
*        USE MONTH OF SERVICE START AND END AS INVOICE DATES                    
*                                                                               
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING PERVALD,R4          ESTABLISH PERVAL WORKAREA                    
*                                                                               
         GOTO1 VPERVAL,DMCB,(6,EZIHDMOS),(R4) VALIDATE PERIOD                   
*                                                                               
         CLI   4(R1),0             CHECK FOR ERRORS                             
         BNE   PRHDRIDX                                                         
*                                                                               
         MVC   PIMSTDT,PVALBSTA   SAVE START AND END DATES                      
         MVC   PIMENDDT,PVALBEND                                                
*                                                                               
         B     PRHDRIDX                                                         
*                                                                               
PRHDRIDT DS    0H                                                               
*                                                                               
         GOTO1 DATCON,DMCB,(0,EZIHISDT),(3,PIMSTDT)  INVOICE START DATE         
*                                                                               
         GOTO1 DATCON,DMCB,(0,EZIHIEDT),(3,PIMENDDT) INVOICE END   DATE         
*                                                                               
PRHDRIDX DS    0H                                                               
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(3,PIMCREDT) DATE INVOICE ADDED                
*                                                                               
         MVC   PIMACTDT,PIMCREDT   DATE OF LAST ACTIVITY                        
         ZAP   PIMAMT,=P'0'        INIT INVOICE AMOUNT                          
*                                                                               
         MVC   HDRELMSV,PIMHDREL   SAVE HEADER ELEMENT FOR LATER                
*                                                                               
         DROP  R2                                                               
*                                  SET ACTIVITY ELEM                            
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD ACTIVITY ELEMENT                       
         USING ACTVD,R2                                                         
*                                                                               
         B     PRHDROLD            NOOP ACIVITY ELEM CREATE                     
         MVI   ACTVEL,X'F1'                                                     
         MVI   ACTVLEN,20                                                       
         GOTO1 DATCON,DMCB,(5,0),(3,ACTVADDT)   TODAY'S DATE                    
         MVC   ACTVADID,RCORIGID   USER ID                                      
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
*                                                                               
*        CREATE AND ADD DETAIL ELEMENTS                                         
*                                                                               
PRHDROLD DS    0H                                                               
*                                                                               
         L     R6,=A(DETBUFF)                                                   
         MVI   FRSTDET,C'Y'                                                     
*                                                                               
PRADET   DS    0H                                                               
         CLI   0(R6),X'FF'         END                                          
         BE    PRADET9                                                          
*                                                                               
         MVC   EZSPFRST(256),0(R6)    MOVE FIELDS BACK TO EZBLOCK               
         MVC   EZSPFRST+256(256),256(R6)                                        
         MVC   EZSPFRST+512(EZSPDLEN-512),512(R6)                               
*                                     (NOT ELEGANT, BUT SHOULD BE OK)           
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM                                                       
         USING PIMDTLEL,R2         ESTABLISH DETAIL ELEMENT                     
*                                                                               
*        BUILD DETAIL ELEMENT KEY                                               
*                                                                               
         MVI   PIMDTLEL,PIMDTLEQ   SET DETAIL ELEMENT CODE                      
         MVI   PIMDTLLN,PIMDTLLQ   SET DETAIL ELEMENT LENGTH                    
         MVC   PIMDTLS1,HDRSQ      SET HEADER SEQUENCE NUMBER                   
         SR    RF,RF                                                            
         ICM   RF,3,DTLSQ          BUMP TO NEXT SEQUENCE NUMBER                 
         LA    RF,1(RF)                                                         
         STCM  RF,3,PIMDTLS2                                                    
         STCM  RF,3,DTLSQ                                                       
*                                                                               
*        BUILD DETAIL ELEMENT                                                   
*                                                                               
         GOTO1 DATCON,DMCB,(2,EZSPCDT),(3,PIMIDATE)                             
         MVC   PIMIEST,EZSPBEST    INVOICE ESTIMATE                             
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRANESX                                                          
*                                                                               
         MVC   PIMIEST,EZIHBEST    INVOICE ESTIMATE NUMBER                      
*                                                                               
PRANESX  DS    0H                                                               
*                                                                               
         MVC   PIMSPACE,EZSPSPCE   SPACE DESCRIPTION                            
*                                                                               
         ICM   RF,15,EZSPBLNS      IF LINES PRESENT                             
         BZ    *+12                                                             
         MVI   PIMUIND,C'L'           SET UNITS AS LINES                        
         B     PRANUNT                GO FORMAT UNITS                           
*                                                                               
         ICM   RF,15,EZSPBICS      IF INCHES PRESENT                            
         BNE   PRANUNTX                 (LOWERCASE I)                           
         MVI   PIMUIND,X'89'          SET UNITS AS INCHES                       
*                                                                               
PRANUNT  DS    0H                  SPACE DESCRIPTION IN UNITS                   
*                                  NOTE- TOTAL UNITS, NOT PER COLUMN            
         CVD   RF,DUB              RF HAS TOTAL UNITS                           
         ZAP   PIMUNITS,DUB        SET UNITS TOTAL                              
*                                                                               
PRANUNTX DS    0H                                                               
*              IF HAVE UNIT RATE AND NO SPECIAL PREMS, USE UNIT RATE            
*              ELSE USE TOTAL COST AND CUME PREMS                               
*                                                                               
         OC    EZSPBRAT,EZSPBRAT   UNIT RATE?                                   
         BZ    PRADET5                                                          
         OC    EZSPBPM1(12),EZSPBPM1  ANY PREMS?                                
         BNZ   PRADET5                                                          
*                                  USING UNIT COST                              
         OI    PIMDSTAT,X'80'                                                   
         ICM   R0,15,EZSPBRAT                                                   
         CVD   R0,DUB                                                           
         ZAP   PIMCOST,DUB                                                      
*                                                                               
         ICM   RF,15,EZSPBGRS                                                   
         CVD   RF,DUB                                                           
         AP    PIMAMT-PIMHDREL+HDRELMSV,DUB  UPDATE INV TOTAL                   
         B     PRADET6B                                                         
*                                                                               
PRADET5  DS    0H                                                               
         ICM   RF,15,EZSPBGRS      SET COST (NOTE- USING TOTAL COST,            
         CVD   RF,DUB              NOT UNIT RATE)                               
         ZAP   PIMCOST,DUB                                                      
*                                                                               
*        ACCUMULATE PREMIUM CHARGES                                             
*                                                                               
         B     PRADET6           **NOOP PREMIUM ADD BECAUSE AMOUNTS             
*                                  INCLUDED IN GROSS                            
         LA    R0,3                MAX THREE PREMIUM CHARGES                    
         LA    R1,EZSPBPM1         FIRST PREMIUM                                
         SR    RF,RF               INIT ACCUMULATOR                             
*                                                                               
         ICM   RE,15,0(R1)         NEXT PREMIUM                                 
         AR    RF,RE               ACCUMULATE                                   
         LA    R1,4(R1)            NEXT CHARGE                                  
         BCT   R0,*-10                                                          
*                                                                               
         CVD   RF,DUB                                                           
         AP    PIMCOST,DUB         SET PREMIUM CHARGE                           
*                                                                               
PRADET6  DS    0H                                                               
         AP    PIMAMT-PIMHDREL+HDRELMSV,PIMCOST    UPDATE INV TOTAL             
*                                                                               
PRADET6B DS    0H                                                               
         MVI   PIMCSIND,C' '       COST IS GROSS                                
*                                                                               
         ICM   RF,15,EZSPBCLS                                                   
         CVD   RF,DUB                                                           
         ZAP   PIMCLMS,DUB         SET NUMBER OF COLUMNS                        
*                                                                               
         ICM   RF,15,EZSPBCLA      COLOR COST                                   
         CVD   RF,DUB                                                           
         ZAP   PIMPREM,DUB         SET NUMBER OF COLUMNS                        
*                                                                               
         MVC   PIMBZONE(2),EZSPLZON   ZONE/EDITION                              
         MVC   PIMSPRD,EZSPLPRC       PRODUCT                                   
*                                                                               
*        A NOTE ABOUT CD HANDLING. CD STATUS OF INV IS DETERMINED               
*        ONLY BY A NON-ZERO VALUE IN EZITBCD. IT IS ASSUMED THAT                
*        EITHER ALL INSERTS HAVE CD OR NONE DO. THE % IS NOT                    
*        CARRIED HERE AND DOESN'T SEEM TO BE INVOLVED IN                        
*        THE MATCHING PROCESS.                                                  
*                                                                               
         TM    HDRELMSV+PIMSTAT-PIMHDREL,X'20'  IF HEADER IS NO CD              
         BZ    *+8                                                              
         OI    PIMDSTAT,X'40'      SAY SO IN DETAIL ALSO                        
*                                                                               
         CLI   EZWRITE,C'Y'        IF NOT WRITING, SKIP                         
         BNE   PRADET8                                                          
         GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0   ADD ELEMENT TO RECORD             
         CLI   FRSTDET,C'Y'        ON FIRST DETAIL                              
         BNE   PRADET8                                                          
         MVI   FRSTDET,C'N'                                                     
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINCLS',MINBLK)  CLOSE AND OPEN TO               
         GOTO1 (RF),(R1),('MINOPN',MINBLK)  PREVENT MINIO BUG WITH              
*                                           SPLITS OF NEW RECS                  
PRADET8 DS     0H                                                               
         LA    R6,EZSPDLEN(R6)      NEXT DETAIL ENTRY                           
         B     PRADET                                                           
*                                                                               
PRADET9  DS    0H                                                               
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,HDRELMSV,0 UPDATE HEADER ELEMENT              
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINCLS',MINBLK)    CLOSE FILE   RECORD           
*                                                                               
PRAX     DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
*                                                                               
PRERRANF DS    0H                  CLIENT NOT FOUND                             
         MVI   EZERRCD,EZERRANF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRPNF DS    0H                  PRODUCT NOT FOUND                            
         MVI   EZERRCD,EZERRPNF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRMPR DS    0H                  NOT ALL INSERTS HAVE PRODUCT                 
         MVI   EZERRCD,EZERRMPR                                                 
         B     PRERRX                                                           
*                                                                               
PRERRDUP DS    0H                  DUPLICATE INVOICE                            
         MVI   EZERRCD,EZERRDUP                                                 
         B     PRERRX                                                           
*                                                                               
PRERRX   DS    0H                                                               
         BAS   RE,GETERRM                                                       
*                                                                               
         B     EXIT                                                             
*                                                                               
*                                                                               
DOINVX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PZADDINV - PROGRAM INITIALIZATION - MININIT'                    
***********************************************************************         
*                                                                     *         
*        MININIT - PROGRAM INITIALIZATION                             *         
*              INIT PRINT INVOICE RECORD MINIO BLOCK                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
MININIT  NTR1                                                                   
*                                                                               
         GOTO1 LOADER,DMCB,=CL8'T00A74',0,0                                     
         MVC   VMINIO,DMCB+4       SET MINIO ADDRESS                            
*                                                                               
         L     RF,=V(PERVERT)                                                   
         ST    RF,VPERVERT                                                      
*                                                                               
*        INITIALIZE MINIO VALUES                                                
*                                                                               
         LA    R0,MINBLK           CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R5,MINBLK           ESTABLISH MINIO BLOCK                        
         USING MINBLKD,R5                                                       
*                                                                               
         MVC   MINRECUP,=V(RECUP)  A(RECUP)                                     
         MVC   MINCOMF,EZCOMFCS    A(COMFACS)                                   
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,INVFILE      FILE NAME                                    
         MVC   MINDIR,INVDIR       DIR NAME                                     
         MVI   MINFKLEN,L'PINVKEY  KEY LENGTH                                   
         MVI   MINEKLEN,L'PINVMINI ELEMENT KEY LENGTH                           
         MVI   MINEKDSP,L'PINVMAST DISPLACEMENT TO ELEMENT KEY                  
         MVI   MINNCTL,L'PINVSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,MAXRECSZ   MAXIMUM RECORD LENGTH                        
*                                                                               
         L     RF,=A(PINVMBF1)     POINT TO START OF MINIO BUFFERS              
         ST    RF,MINBUFF          A(FIRST BUFFER)                              
*                                                                               
         MVI   MINNBUF,2           USE TWO BUFFERS                              
*                                                                               
         L     RF,=A(PINVMRTB)                                                  
         ST    RF,MINRTAB          A(AREA FOR RECORD TABLE)                     
*                                                                               
         MVC   MINRTABL,=Y(L'PINVMRTB) LENGTH OF RECORD TABLE                   
*                                                                               
         L     RF,=A(PINVMELM)      A(AREA FOR ELEM OR CLUSTER)                 
         ST    RF,MINELEM                                                       
*                                                                               
         XC    0(L'PINVMELM,RF),0(RF)  CLEAR MINELEM AREA                       
*                                                                               
         MVC   MINMAXEL,=Y(L'PINVMELM)   MAX LENGTH OF ELEM OR CLUSTER          
*                                                                               
         MVI   MINDELSW,C'Y'       PROCESS DELETED RECORDS                      
*                                                                               
         CLI   EZWRITE,C'Y'        IF WRITES NOT ALLOWED                        
         BE    *+8                                                              
         MVI   MINWRITE,C'N'          DISABLE WRITES                            
*                                                                               
MININITX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMELM'                   
***********************************************************************         
*                                                                     *         
*        COMELM - FIND/SET COMMENT ELEMENTS IN BUFFER                           
*                                                                     *         
*              **NOT CURRENTLY USED **                                          
*                                                                               
*NTRY    R5 ==>  A(MINBLK)                                            *         
*                                                                     *         
*EXIT                                                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
COMELM   NTR1                                                                   
*                                                                               
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         CLC   EZICCOM,=CL256' '   SKIP IF NO COMMENT AVAILABLE                 
         BNH   COMELMX                                                          
*                                                                               
         XC    PINVCELM,PINVCELM                                                
         LA    R2,PINVCELM         ESTABLISH COMMENT ELEMENT                    
         USING PIMCOMEL,R2                                                      
*                                                                               
         MVI   PIMCOMEL,PIMCOMEQ   SET ELEMENT ID                               
         MVI   PIMCOMLN,PIMCOMOV   SET DEFAULT MINIMUM LENGTH                   
****     MVC   PIMCOMS1,           SET HEADER SEQUENCE NUMBER                   
****     MVC   PIMCOMS2,           DETAIL SEQUENCE NUMBER                       
*                                                                               
         LA    R0,PIMCOMEL         MINIMUM COMMENT ELM LENGTH                   
         LA    R1,PIMCOML1         POINT TO FIRST COMMENT LENGTH BYTE           
         LA    R4,PIMCOMTX         POINT TO START OF COMMENTS IN ELM            
         LA    R5,3                MAX 3 COMMENT SEGMENTS                       
         LA    RE,EZICCOM          POINT TO START OF COMMENT                    
*                                                                               
COMELMLP DS    0H                                                               
*                                                                               
         CLC   0(39,RE),=CL256' '  SKIP IF SEGMENT NOT PRESENT                  
         BNH   COMELMCN                                                         
*                                                                               
         LA    RF,40               MAX POSITIONS TO CHECK                       
         LA    R3,39(RE)           POINT TO END+1 OF COMMENT SEGMENT            
*                                                                               
         CLI   0(R3),C' '          FIND LAST SPACE                              
         BNH   *+18                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         LA    R3,39(RE)           NO SPACES IN COMMENT - RESET POINTER         
         B     COMELML1                                                         
*                                                                               
         BCTR  R3,0                BACK UP A POSITION                           
         BCTR  RF,0                RECTIFY THE COUNT                            
*                                                                               
         CLI   0(R3),C' '          FIND PREVIOUS NON-SPACE                      
         BH    *+12                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
COMELML1 DS    0H                                                               
*                                                                               
         SR    R3,RE               LENGTH OF COMMENT SEGMENT                    
*                                                                               
         STC   R3,0(R1)            SET COMMENT LENGTH                           
         LA    R1,1(R1)            BUMP COMMENT LENGTH POINTER                  
*                                                                               
         AR    R0,R3               INCREMENT ELEMENT LENGTH COUNTER             
*                                                                               
         BCTR  R3,0                DECREMENT FOR EXECUTE                        
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(RE)       MOVE COMMENT SEGMENT TO ELEMENT              
*                                                                               
COMELMCN DS    0H                                                               
*                                                                               
         LA    R4,1(R3,RE)         BUMP TO NEXT SEGMENT IN ELEMENT              
         LA    RE,1(R3,RE)         BUMP TO NEXT INCOMING SEGMENT                
*                                                                               
         CLI   0(RE),C' '          IF SEGMENT STARTS WITH SPACE                 
         BH    *+8                                                              
         LA    RE,1(RE)               BUMP PAST IT                              
*                                                                               
         BCT   R5,COMELMLP                                                      
*                                                                               
COMELMDN DS    0H                                                               
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,PINVCELM,0  ADD TO RECORD                     
*                                                                               
COMELMX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
HDRSQ    DC    X'00'               HEADER SEQUENCE NUMBER                       
HDRSQOLD DC    X'00'               HEADER SEQUENCE NUMBER FROM FILE             
DTLSQ    DC    XL2'00'             DETAIL SEQUENCE NUMBER                       
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMSUBS'                  
***********************************************************************         
*                                                                     *         
*        COMSUBS - CODE AND DATA ADDRESSABLE FROM ALL CSECTS          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
COMSUBS  DS    0H                                                               
*                                                                               
*        DMPREC - HEX DUMP OF ELEMENT WORK AREA                                 
*                                                                               
*NTRY    R1 ==>  ELEMENT TO BE ADDED                                            
*                                                                               
         DS    0D                                                               
DMPREC   NTR1                                                                   
*                                                                               
         LR    R5,R1               POINT TO ELEMENT WORKAREA                    
         SR    R2,R2                                                            
         ICM   R2,1,1(R5)          ELEMENT LENGTH                               
*                                                                               
         CLI   3(R5),PINVTYPQ      IF DUMPING KEY                               
         BNE   *+8                                                              
         LA    R2,L'PINVKEY           RESET DUMP LENGTH                         
*                                                                               
         LA    R3,0(R5,R2)         EOR                                          
*                                                                               
DMPREC2  DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R5                                                            
         BNP   DMPRECX                                                          
         CH    R4,=H'32'                                                        
         BNH   *+8                                                              
         LA    R4,32                                                            
         XC    WORK,WORK                                                        
         MVC   P,SPACES                                                         
         GOTO1 HEXOUT,DMCB,(R5),WORK,(R4),=C'N'                                 
*                                                                               
         MVC   P+01(8),WORK+00                                                  
         MVC   P+10(8),WORK+08                                                  
         MVC   P+19(8),WORK+16                                                  
         MVC   P+28(8),WORK+24                                                  
         MVC   P+37(8),WORK+32                                                  
         MVC   P+46(8),WORK+40                                                  
         MVC   P+55(8),WORK+48                                                  
         MVC   P+64(8),WORK+56                                                  
*                                                                               
         MVC   WORK(32),0(R5)                                                   
         TR    WORK(32),TRTAB                                                   
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+75(0),WORK                                                     
         LA    R4,1(R4)                                                         
         GOTO1 EZPRINT,DMCB,P,=C'BL01'  PRINT LINE                              
         LA    R5,0(R5,R4)                                                      
         B     DMPREC2                                                          
*                                                                               
DMPRECX  DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
TRTAB    DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     00-0F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     10-1F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     20-2F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     30-3F                    
         DC    X'404B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     40-4F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B5B5C5D4B4B'     50-5F                    
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D4B6F'     60-6F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B7B4B7D7E4B'     70-7F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     80-8F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     90-9F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     A0-AF                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     B0-BF                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'     C0-CF                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'     D0-DF                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'     E0-EF                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'     F0-FF                    
         EJECT                                                                  
*        GETERRM - GET ERROR MESSAGE AND LEVEL                                  
         SPACE 2                                                                
         DS    0D                                                               
GETERRM  NTR1                                                                   
         L     R3,=A(ERRTAB)                                                    
         MVC   EZERRMSG,SPACES                                                  
         MVI   EZERRLEV,0                                                       
*                                                                               
GERM2    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BE    GERMX                                                            
*                                                                               
         CLC   EZERRCD,0(R3)                                                    
         BE    GERM4                                                            
         LA    R3,ERRTABL(R3)                                                   
         B     GERM2                                                            
*                                                                               
GERM4    DS    0H                                                               
         MVC   EZERRLEV,1(R3)                                                   
         MVC   EZERRMSG,2(R3)                                                   
         OC    EZERRLVR,1(R3)      'OR' UP ERROR - RECORD                       
         OC    EZERRLVI,1(R3)                    - INVOICE                      
         OC    EZERRLVB,1(R3)                    - BATCH                        
*                                                                               
GERMX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         SPACE 2                                                                
*                                  ERROR TABLE                                  
*                                  CODE(1),LEVEL(1),MESSAGE(40)                 
*                                                                               
ERRTAB   DS    0C                                                               
         DC    AL1(EZERRDUP)       DUPLICATE INVOICE                            
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVOICE ALREADY ON FILE'                                    
*                                                                               
         DC    AL1(EZERROVF)       OVERFLOW                                     
         DC    AL1(0)              LEVEL                                        
         DC    CL40'TOO MANY PRINTS IN INVOICE'                                 
*                                                                               
         DC    AL1(EZERRANF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'CLIENT ERROR'                                               
*                                                                               
         DC    AL1(EZERRPNF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRODUCT ERROR'                                              
*                                                                               
         DC    AL1(EZERRSPL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRINT DETAIL PRINT LENGTH ZERO'                             
*                                                                               
         DC    AL1(EZERRMUL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'COMBINED INVOICES, NO RECONVERT'                            
*                                                                               
         DC    AL1(EZERRMPR)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'SOME INSERTIONS WITH NO PRODUCT'                            
*                                                                               
         DC    X'FFFF'                                                          
*                                                                               
INVDIR   DC    CL8'PRTDIR'         INVOICE DIRECTORY NAME                       
INVFILE  DC    CL8'PRTFILE'        INVOICE FILE NAME                            
MAXRECSZ DC    H'2900'                                                          
ERRTABL  EQU   42                                                               
*                                                                               
VMINIO   DS    A                   A(MINIO)                                     
VPERVAL  DC    V(PERVAL)           A(PERVAL)                                    
VPERVERT DS    A                   A(PERVERT)                                   
*                                                                               
ADSCTL   DC    C'1'                CONTROL SWITCH                               
ADS1STQ  EQU   C'1'                FIRST TIME TO PROGRAM                        
*                                                                               
         SPACE 2                                                                
*                                                                               
         TITLE 'ROUTINE TO ADD ELEMENT TO A MINIO BLOCK - ADDEL'                
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT TO A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
ADDEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         MVC   MINELEM,4(R1)       POINT BLOCK TO NEW ELEMENT                   
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         L     R1,4(R1)               POINT TO ELEMENT TO BE ADDED              
         BAS   RE,DMPREC              GO DUMP ELEMENT                           
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINADD',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
ADDELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO DELETE AN ELEMENT IN A MINIO BLOCK - ADDEL'          
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT FROM A RECORD                          *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    NOT USED                                            *         
*        PARM3    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
DELEL    NTR1  0H                                                               
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         L     R2,8(R1)            POINT TO ELEMENT TO BE DELETED               
*                                                                               
         ST    R2,MINELEM          POINT BLOCK TO ELEMENT                       
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET KEY LENGTH                               
*                                                                               
         CLM   RF,1,MINEKLEN       DEFAULT TO MINIO KEY LENGTH                  
         BZ    DELELX              NO ELEMENT                                   
         BNH   *+8                                                              
         IC    RF,MINEKLEN                                                      
*                                                                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
*                                                                               
         EX    RF,DELELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     DELELX                                                           
*                                                                               
DELELMV  MVC   MINEKEY+1(0),2(R2)   SETS REST OF ELEMENT KEY                    
*                                                                               
*                                                                               
DELELX   XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO GET AN ELEMENT IN A MINIO BLOCK - GETEL'             
***********************************************************************         
* ROUTINE TO GET AN ELEMENT IN A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
GETEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT O ELEMENT KEY                          
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         LA    R0,MINRD            DEFAULT TO DIRECT READ                       
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    GETEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   GETEL10                                                          
*                                                                               
         LA    R0,MINHI            SET FOR READ HI/EQUAL                        
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
GETEL10  DS    0H                                                               
*                                                                               
         ZIC   RF,MINEKLEN         GET KEY LENGTH                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
         EX    RF,GETELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   GETELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     GETELX                                                           
*                                                                               
GETELMV  MVC   MINEKEY+1(0),2(R2)  SETS REST OF ELEMENT KEY                     
*                                                                               
GETELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO GET NEXT ELEMENT IN A MINIO BLOCK - SEQEL'           
***********************************************************************         
* ROUTINE TO GET NEXT SEQUENTIAL ELEMENT IN A MINIO RECORD            *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
*                 ASSUMES LAST ACTION WAS A GETEL AND MINIO BLOCK     *         
*                  IS ALREADY SET UP FOR SEQUENTIAL READ              *         
*                                                                     *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
SEQEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT TO ELEMENT KEY                         
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    SEQEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   SEQEL10                                                          
*                                                                               
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
SEQEL10  DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           SET FOR READ READ SEQUENTIAL                 
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   SEQELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     SEQELX                                                           
*                                                                               
SEQELX   XIT1                                                                   
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO PUT AN ELEMENT TO A MINIO BLOCK - PUTEL'             
***********************************************************************         
* ROUTINE TO PUT AN ELEMENT IN A RECORD                               *         
*       DELETES AND ADDS NEW ELEMENT WILL NOT GET UPSET IF THERE      *         
*         IS NO PRIOR ELEMENT                                         *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
PUTEL    NTR1  0H                                                               
*                                                                               
         GOTO1 GETEL,(R1)          FIND ELEMENT                                 
*                                                                               
         OC    8(4,R1),8(R1)       ADD ELEMENT IF NONE ALREADY                  
         BZ    PUTELADD            ELSE DELETE OLD ELEMENT                      
*                                                                               
         GOTO1 DELEL,(R1)          DELETE ELEMENT                               
*                                                                               
PUTELADD DS    0H                                                               
*                                                                               
         GOTO1 ADDEL,(R1)          ADD ELEMENT TO RECORD                        
*                                                                               
PUTELX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*                                                                               
SVRECSEQ DS    XL2                                                              
ANXTDET  DS    A                                                                
LASTPRD  DS    CL3                                                              
LASTEST  DS    XL2                                                              
LASTZED  DS    XL2                                                              
INSSTAT  DS    XL1                                                              
MPRSTAT  DS    CL1        Y=MISSING PRODUCT ON AT LEAST ONE INSERT              
*                                                                               
         DS    0D                                                               
MINBLK   DS    XL(MINBLKL)         MINIO BLOCK FOR INVOICE RECORD               
*                                                                               
         DS    0D                                                               
HDRELMSV DS    XL128               HEADER ELEMENT SAVEAREA                      
PINVMELM DS    XL128               ELEMENT RETRIEVAL AREA                       
PINVMRTB DS    XL(200*(6+L'PINVMINI))  RECORD TABLE                             
*                                                                               
         DS    0D                                                               
PINVMBF1 DS    XL4096              FIRST  MINIO BUFFER                          
PINVMBF2 DS    XL4096              SECOND MINIO BUFFER                          
         DS    0D                                                               
         DC    CL8'*DETBUF*'                                                    
DETMAX   EQU   500                                                              
DETBUFF  DS    (DETMAX*EZSPDLEN)X    DETAILS BUFFER                             
DETBUFFX EQU   *                                                                
*                                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        WORKAREAS                                                    *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PZADID   DSECT                                                                  
PINVELM  DS    XL256               ELEMENT WORKAREA                             
PINVCELM DS    XL256               COMMENT ELM WORKAREA                         
ERR      DS    XL1                                                              
FRSTDET  DS    CL1                                                              
         DS    0D                                                               
*                                                                               
*        MINIO ROUTINES WORKAREAS                                               
*                                                                               
MNPRMS   DS    6A                  PARAMETER BLOCK                              
*                                                                               
WKINVNO  DS    CL(L'PIMINVNO)      INVOICE NUMBER WORKAREA                      
INVSAVE  DS    XL(L'EZIHINV)       INVOICE NUMBER SAVEAREA                      
*                                                                               
PZADIDL  EQU   *-PZADID                                                         
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        OTHER INCLUDED DSECTS                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE PZBLOCK                                                        
       ++INCLUDE PINVREC                                                        
*                                                                               
* DDPERVALD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
* DDACTIVD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDACTIVD                                                       
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DDMINBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDMINBLK                                                       
         PRINT ON                                                               
* DMWRKRD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRD                                                        
         PRINT ON                                                               
* PPREPWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
* PPREPWORK2                                                                    
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK2                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'097PZADDINV  05/01/02'                                      
         END                                                                    
