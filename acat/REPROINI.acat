*          DATA SET REPROINI   AT LEVEL 018 AS OF 04/20/99                      
*CATALP REPROINI                                                                
         TITLE '- SYSTEM INITIALIZATION - OBJECT CONTROLLER'                    
***********************************************************************         
* NTRY: R9=A(GLOBAL W/S)                                              *         
*       P1=A(CONTROLLER ADDRESSES 2)                                  *         
*       P2=DISPLACEMENT TO MIXED CASE DICTIONARY OUTPUT FROM BSDICT   *         
*       P3=3 CHARACTER PROGRAM NAME TO GET PROFILES                   *         
* EXIT: CC=LOW IF ERROR                                               *         
*       CC=HIGH IF FIRST TIME CALL                                    *         
*       CC=EQUAL IF NOT FIRST TIME AND NO ERROR                       *         
***********************************************************************         
REPROINI CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 IWORKL,**REGI**,RR=RE                                            
         USING IWORKD,RC           RC=A(LOCAL WORKING STORAGE)                  
         USING WORKD,R9            R9=A(GLOBAL WORKING STORAGE)                 
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         L     RA,ATWA                                                          
         USING TWAD,RA                                                          
         MVC   IPARMS,0(R1)                                                     
***********************************                                             
* SET UP ADDRESSES FOR CORE-RESIDENT PHASES                                     
***********************************                                             
         L     R2,=A(PHASES)       R2=A(PHASE LIST)                             
         A     R2,BCRELO                                                        
         LA    R3,APHASES          R3=A(ADDRESS LIST)                           
         LA    R4,PHASESN          R4=MAX NUMBER OF PHASES (CORERES)            
         SR    R0,R0                                                            
         ICM   R0,14,=X'D9000A'                                                 
         LA    R1,BCPARM                                                        
         L     RF,VCOLY                                                         
*                                                                               
         GOTO1 (RF),(R1),0,(R0)    <==  SPECIAL FOR BOOKVAL BECAUSE             
         MVC   VBOOKVAL,0(R1)             QBOOKVAL IS EQUATED TO 0              
*                                                                               
INITAD10 ICM   R0,1,0(R2)          ANY ENTRY HERE?                              
         BZ    INITAD15            NONE, SKIP TO THE NEXT ENTRY                 
*                                                                               
         GOTO1 (RF),(R1),0,(R0)                                                 
         MVC   0(4,R3),0(R1)                                                    
*                                                                               
INITAD15 LA    R2,1(R2)            BUMP TO THE NEXT ENTRY                       
         LA    R3,4(R3)                                                         
         BCT   R4,INITAD10                                                      
***********************************                                             
* LOAD UP THE MINIO RECORD TABLE                                                
***********************************                                             
         GOTO1 (RF),(R1),(X'02',0),0                                            
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   AREPRO02,0(R1)                                                   
***********************************                                             
* SET UP ADDRESSES FOR CORE-RESIDENT OVERLAYS                                   
***********************************                                             
INITAD20 L     R2,=A(OVLAYS)       R2=A(OVERLAYS LIST)                          
         A     R2,BCRELO                                                        
         LA    R3,AOVLAYS          R3=A(ADDRESS LIST)                           
         LA    R4,OVLAYSN          R4=MAXIMUM NUMBER OF OVERLAYS                
         ICM   R0,14,=X'D9080A'    PHASE SIGNATURE - T80A??                     
         LA    R1,BCPARM                                                        
         L     RF,VCOLY                                                         
*                                                                               
INITAD22 ICM   R0,1,0(R2)          ANY ENTRY HERE?                              
         BZ    INITAD25            NONE, SKIP TO THE NEXT ENTRY                 
*                                                                               
         GOTO1 (RF),(R1),0,(R0)    FIND THE A(OVERLAY) IN CORE                  
         MVC   0(4,R3),0(R1)                                                    
*                                                                               
INITAD25 LA    R2,1(R2)            BUMP TO THE NEXT ENTRY                       
         LA    R3,4(R3)                                                         
         BCT   R4,INITAD22                                                      
*                                                                               
INITAD30 LA    R0,ADDRS2N          SET CONTROLLER ADDRESSES 2                   
         SR    RE,RE               RE=INDEX TO ADDRESS VALUE                    
         L     RF,IPADDRS2                                                      
         L     R1,0(RF,RE)                                                      
         A     R1,BCRELO           RELOCATE AND STORE IN W/S                    
         ST    R1,AADDRS2(RE)                                                   
         LA    RE,4(RE)                                                         
         BCT   R0,*-16                                                          
*                                                                               
INITSAVE TM    TWAINDS1,TW1INIT                                                 
         BO    INITGV90                                                         
*                                                                               
         OI    IINDS,IIFRST        INDICATE FIRST TIME                          
*                                                                               
         OI    TWAINDS1,TW1INIT    INITIALISE DICTIONARY                        
         LH    R2,=Y(TWDICT-TWAD)                                               
         AR    R2,RA                                                            
         GOTO1 VDICTAT,BCPARM,C'LU  ',ADICUPR,(R2)                              
         L     R0,IPDICMIX                                                      
         AR    R2,R0                                                            
         GOTO1 (RF),(R1),C'LL  ',ADICMIX,(R2)                                   
***********************************************************************         
* INITIALISE COMMON SAVED DATA                                                  
***********************************************************************         
INITGV90 TM    IINDS,IIFRST        TEST WAS FIRST TIME                          
         BZ    INITGVE                                                          
INITGVH  CLI   *,0                 SET CC HIGH FOR FIRST TIME                   
         B     INITGVX                                                          
INITGVL  CLI   *,FF                SET CC LOW FOR ERROR                         
         B     INITGVX                                                          
INITGVE  CR    RB,RB               SET CC EQUAL                                 
INITGVX  XMOD1 ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
PHASES   DS    0X                  ** LOADED PHASE LIST **                      
         DC    AL1(0)     <=====   RESERVED BY REPRO01                          
         DC    AL1(0)     <=====   USED BY REPRO02 FOR MINIO (NON CORE)         
         DC    AL1(QREPFACS)                                                    
         DC    AL1(QREPORT)        REQUIRED BY GEFIL                            
         DC    AL1(QREQTWA)        REQUIRED BY GEFIL                            
         DC    AL1(QDEMOCON)                                                    
         DC    AL1(QDEMOVAL)                                                    
         DC    AL1(QUPVAL)                                                      
         DC    AL1(QBOOKVAL)  <=== BUT THIS EQUATE IS ALSO 0                    
         DC    AL1(QDAYVAL)                                                     
         DC    AL1(QDAYUNPK)                                                    
         DC    AL1(QTIMVAL)                                                     
         DC    AL1(QUNTIME)                                                     
         DC    AL1(QREFETCH)                                                    
         DC    AL1(QGETBROD)                                                    
         DC    AL1(QGETIDS)        REQUIRED BY GEFIL                            
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
PHASESN  EQU   *-PHASES                                                         
*                                                                               
OVLAYS   DS    0X                  ** LOADED OVERLAYS LIST **                   
         DC    AL1(REPRO01Q)       REPRO01 - COMMON ROUTINES                    
         DC    AL1(0)     <=====   USED BY REPRO02                              
         DC    AL1(0)                                                           
         DC    AL1(0)     <=====   RESERVED BY GEFIL FOR REPORT                 
         DC    AL1(0)     <=====        "    "  "    "    "                     
         DC    AL1(0)     <=====   USED BY DEMOCON                              
         DC    AL1(0)     <=====   USED BY DEMOVAL                              
         DC    AL1(0)     <=====   USED BY UPVAL                                
         DC    AL1(0)     <=====   USED BY BOOKVAL                              
         DC    AL1(0)     <=====   USED BY DAYVAL                               
         DC    AL1(0)     <=====   USED BY DAYUNPK                              
         DC    AL1(0)     <=====   USED BY TIMVAL                               
         DC    AL1(0)     <=====   USED BY UNTIME                               
         DC    AL1(0)     <=====   USED BY FETCH                                
         DC    AL1(0)     <=====   USED BY GETBROAD                             
         DC    AL1(0)     <=====   RESERVED BY GEFIL FOR GETIDS                 
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
OVLAYSN  EQU   *-OVLAYS                                                         
*                                                                               
FF       EQU   X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION LOCAL W/S                                            *         
***********************************************************************         
SWORKD   DSECT                     SUBROUTINE WORK AREA                         
SAPARM   DS    A                                                                
SPARMS   DS    0XL(SPARMSL)        * INPUT PARAMETERS *                         
SPARM1   DS    A                                                                
SPARM2   DS    A                                                                
SPARM3   DS    A                                                                
SPARM4   DS    A                                                                
SPARM5   DS    A                                                                
SPARM6   DS    A                                                                
SPARMSL  EQU   *-SPARM1                                                         
*                                                                               
SPARMO   DS    6A                                                               
SDUB     DS    D                                                                
SBYTE1   DS    X                                                                
SWORK    DS    XL80                                                             
*                                                                               
SWORKL   EQU   *-SWORKD                                                         
*                                                                               
IWORKD   DSECT                     GENINI WORK AREA                             
IPARMS   DS    0XL(IPARMSL)        * INPUT PARAMETERS *                         
IPADDRS2 DS    A                   A(CONTROLLER ADDRESSES 2)                    
IPDICMIX DS    A                   DISP. TO MIXED CASE DICTIONARY LIST          
IPNAME   DS    CL3                 3 CHARACTER PROGRAM NAME FOR PROFILE         
         DS    CL1                 N/D                                          
IPARMSL  EQU   *-IPADDRS2                                                       
*                                                                               
IINDS    DS    XL1                 * INDICATORS *                               
IIFRST   EQU   X'80'               FIRST TIME                                   
IWORKL   EQU   *-IWORKD                                                         
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
* DDGETHELPD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDGETHELPD                                                     
         PRINT ON                                                               
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
* REPROWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE REPROWORK                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018REPROINI  04/20/99'                                      
         END                                                                    
