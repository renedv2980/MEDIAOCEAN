*          DATA SET DEINMKT    AT LEVEL 002 AS OF 09/29/98                      
*CATALP DEINMKT                                                                 
         SPACE 2                                                                
***********************************************************************         
* INPUT:       P1 - A(DBLOCK)                                         *         
*              P2 - A(AFFIL LIST)                                     *         
*              P3 - A(OUTPUT AREA) WHERE TO BUILT                               
*                    5 CHAR STATION LIST ENDING WITH X'00'            *         
*                                                                     *         
* OUTPUT:      P1 - NON ZERO IF ERROR OCCURED                         *         
*              P3 - OUPUT LIST FILLED WITH STATIONS                   *         
*                                                                               
***********************************************************************         
         TITLE 'DEINMKT- IN MKT SHARE STATION LIST BUILDING MODULE'             
DEINMKT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,DEINMKT,RA,CLEAR=YES,RR=R8                           
         USING WORKD,RC            RC=A(W/S)                                    
         ST    R1,APARM                                                         
         ST    RD,SAVERD           SAVE RD FOR ERROR EXIT                       
         ICM   R5,15,0(R1)         R5=USERS DBLOCK                              
         BNZ   *+6                 A(DBLOCK) NOT PASSED                         
         DC    H'0'                                                             
         MVC   ADBLK(12),0(R1)                                                  
         L     RE,AOUTLST          INIT OUTPUT STATION LIST                     
         XC    0(10,RE),0(RE)                                                   
         MVI   0(R1),0             RESET ERROR BYTE                             
USRDB    USING DBLOCKD,R5                                                       
*                                                                               
         L     R1,USRDB.DBCOMFCS   SET ADDR OF  DEMAND                          
         MVC   VDEMAND,CDEMAND-COMFACSD(R1)                                     
         L     RF,CCALLOV-COMFACSD(R1)                                          
         ICM   R0,15,=X'D9000A26'                                               
         LA    R1,DMCB             SET ADDR OF DEFINE                           
         GOTO1 (RF),(R1),0,(R0)                                                 
         MVC   VDEFINE,0(R1)                                                    
*                                                                               
         LA    R7,DBLOCK1          R7=LOCAL STORAGE DBLOCK                      
         USING DBLOCKD,R7                                                       
         XC    DBLOCK1,DBLOCK1                                                  
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBVLSTBK    VALIDATE STN TO GET MKT                      
         MVC   DBAREC,USRDB.DBAREC                                              
         MVC   DBCOMFCS,USRDB.DBCOMFCS                                          
         MVC   DBSELMED,USRDB.DBSELMED                                          
         MVC   DBSELSRC,USRDB.DBSELSRC                                          
         MVC   DBSELAGY,USRDB.DBSELAGY                                          
         MVC   DBSELSTA,USRDB.DBSELSTA                                          
         MVC   DBSELBK,USRDB.DBSELBK                                            
         GOTO1 VDEMAND,DMCB,DBLOCK,0                                            
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BNE   ERRXIT              NO - EXTRACT MARKET NUMBER                   
         MVC   DBSELRMK,DBKEY+(BSRMKT-BSKEY)                                    
         CLI   DBSELSRC,C'N'       FIX NSI FUCK UPS FOR ATL/COLOSPR             
         BNE   DEIN20                                                           
         CLI   DBBTYPE,C' '        LEAVE IT ALONE IF BOOK TYPE                  
         BNE   DEIN20                                                           
         CLC   DBSELRMK,=H'124'    ATLANTA                                      
         BNE   *+10                                                             
         MVC   DBSELRMK,=H'168'                                                 
         CLC   DBSELRMK,=H'299'    COLO SPRINGS TA                              
         BNE   *+10                                                             
         MVC   DBSELRMK,=H'352'                                                 
*                                                                               
DEIN20   XC    STNLST(5),STNLST    CLEAR TABLE                                  
         MVI   DBFUNCT,DBGETMS     GET STATIONS FOR MARKET                      
         GOTO1 VDEMAND,DMCB,DBLOCKD,DEMHKMS,0                                   
                                                                                
         XC    DBLOCK,DBLOCK       SET FIXED VALUES INTO DBLOCK                 
         LA    R0,IO1                                                           
         ST    R0,DBAREC                 A(I/O AREA),                           
         MVC   DBFILE,=C'TP '            FILE,                                  
         MVI   DBFUNCT,DBGETDEM          FUNCTION,                              
         MVC   DBCOMFCS,USRDB.DBCOMFCS                                          
         MVC   DBSELMED,USRDB.DBSELMED                                          
         MVC   DBSELSRC,USRDB.DBSELSRC                                          
         MVC   DBSELAGY,USRDB.DBSELAGY                                          
         MVC   DBSELBK,USRDB.DBSELBK                                            
         MVI   DBSELDAY,X'40'            ARBITRARY DAY                          
         MVC   DBSELTIM,=AL2(0800,0815)     AND TIMES,                          
*                                                                               
         LA    R6,STNLST                                                        
DEIN30   OC    0(5,R6),0(R6)                                                    
         BZ    EXIT                                                             
         MVC   DBSELSTA,0(R6)                                                   
         XC    DBACTUAL,DBACTUAL                                                
         MVC   DBBTYPE,USRDB.DBBTYPE                                            
         GOTO1 VDEMAND,DMCB,DBLOCKD,AFFHK,0                                     
         LA    R6,5(R6)            BUMP TO NEXT STATION                         
         B     DEIN30                                                           
*                                                                               
ERRXIT   DS    0H                  ERROR OCCURED                                
         L     R1,APARM                                                         
         MVC   0(1,R1),DBERROR                                                  
*                                                                               
EXIT     XIT1                      EXIT MODULE                                  
                                                                                
*******************************************************************             
*DEMHKMS - HOOK TO PICK OFF ALL STATIONS HOME TO MARKET.                        
*******************************************************************             
DEMHKMS  ST    RE,SAVERE           SAVE RETURN ADDRESS                          
         LA    R1,DBKEY                                                         
         USING MLKEY,R1                                                         
         CLI   DBRECTYP,DBRECMS                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    MLSTAT,X'F0'        NOT INTERESTED IN MARKET SPILLS              
         BO    DEMHKMSX                                                         
         OC    MLKMKT,MLKMKT                                                    
         BNZ   DEMHKMSX            IGNORE SPILL STATIONS                        
*                                                                               
         LH    RF,STNCNT           BUMP STATION COUNTER                         
         LA    RE,1(RF)                                                         
         CHI   RE,400              MAX # STATIONS TABLE CAN HANDLE              
         BNH   *+6                                                              
         DC    H'0'                STATION TABLE OVERFLOW                       
         STH   RE,STNCNT                                                        
         MHI   RF,5                MULTIPLY COUNT BY # STA IN TABLE             
         LA    RF,STNLST(RF)                                                    
         MVC   0(5,RF),MLSTAT      SAVE AWAY STATION IN TABLE                   
         DROP  R1                                                               
*                                                                               
DEMHKMSX L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
         EJECT                                                                  
*******************************************************************             
*AFFHK   - FILTER OUT STNS  ON AFFIL LIST PASSED                                
*******************************************************************             
AFFHK    ST    RE,SAVERE           SAVE RETURN ADDRESS                          
         XC    WORK,WORK                                                        
         GOTO1 VDEFINE,MYDMCB,=C'AFFL',DBLOCK,WORK                              
         L     R1,AAFFLST          MATCH ON AFFL PASSED TO PRGM                 
AFFHK10  OC    0(5,R1),0(R1)                                                    
         BZ    AFFHKX              NO MATCH, BYPASS THIS STATION                
         CLC   WORK(5),0(R1)       DOES AFFL OF STATION MATCH RQST?             
         BE    AFFHK20                                                          
         LA    R1,5(R1)                                                         
         B     AFFHK10                                                          
*                                                                               
AFFHK20  L     R1,AOUTLST          SAVE STATION IN OUTPUT LIST                  
         OC    0(5,R1),0(R1)                                                    
         BZ    *+12                                                             
         LA    R1,5(R1)            BUMP TO AN OPEN BUCKET                       
         B     *-14                                                             
         MVC   0(5,R1),DBACTSTA    ADD STATION TO OUTPUT LIST                   
         XC    5(5,R1),5(R1)                                                    
                                                                                
AFFHKX   L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
         EJECT                                                                  
*******************************************************************             
*CONSTANTS AND WORKING STORAGE                                                  
*******************************************************************             
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
WORK     DS    XL64                                                             
SAVERD   DS    F                                                                
SAVERE   DS    F                                                                
APARM    DS    A                                                                
ADBLK    DS    A                   A(USER'S DBLOCK)                             
AAFFLST  DS    A                   AFFIL LIST PASSED FROM USER                  
AOUTLST  DS    A                   A(STATION) LIST TO PASS TO USER              
VDEMAND  DS    A                                                                
VDEFINE  DS    A                                                                
MYDMCB   DS    6F                                                               
DBLOCK1  DS    XL(L'DBLOCK)                                                     
STNCNT   DS    H                                                                
STNLST   DS    2000X                                                            
IO1      DS    2000X                                                            
*                                                                               
WORKX    EQU   *                                                                
                                                                                
         EJECT                                                                  
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 2                                                                
* DEDBEXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DEDBEXTRAD                                                     
         PRINT ON                                                               
         SPACE 2                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DEINMKT   09/29/98'                                      
         END                                                                    
