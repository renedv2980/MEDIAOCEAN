*          DATA SET CXSFBC     AT LEVEL 022 AS OF 05/21/98                      
*CATALP CXSFBC                                                                  
         TITLE 'CXSFBC - FCONTROL USERID/GROUP LEVELS'                          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSFBC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSFBC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSFBD,R2           R2=A(EXTRACT RECORD)                         
         USING SAFCREC,R3          R3=A(FCONTROL GROUP RECORD)                  
*                                                                               
CXSLEN   XC    CXSFBLEN,CXSFBLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSFBLEN(2),=AL2(CXSFBDL)                                        
         MVC   CXSFBTYP,CXSFBDQ                                                 
         MVI   CXSFBDX,MXTRTQ                                                   
         MVI   CXSFBDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSAID   MVI   CXSFBAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSFBAID,SAFCAGY                                                 
*                                                                               
CXSUIN   MVI   CXSFBUIN-1,MXTRTQ   USER ID NUMBER                               
         SR    RF,RF                                                            
         ICM   RF,3,SAFCUID                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSFBUIN,DUB+5(3)                                                
*                                                                               
CXSAGC   MVI   CXSFBAGC-1,MXTRTQ   ACCESS GROUP CODE ??                         
         GOTO1 XHEXOUT,DMCB,SAFCAGN,CXSFBAGC,2,=C'TOG'                          
*                                                                               
CXSSYS   MVI   CXSFBSYS-1,MXTRTQ   SYSTEM CODE                                  
         GOTO1 XHEXOUT,DMCB,SAFCOVS,CXSFBSYS,1,=C'TOG'                          
*                                                                               
CXSPGM   MVI   CXSFBPGM-1,MXTRTQ   PROGRAM CODE                                 
         GOTO1 XHEXOUT,DMCB,SAFCPGM,CXSFBPGM,1,=C'TOG'                          
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   CXSFBACT,C'A'       GOTO ADD PROCESS                             
         B     CXSREC                                                           
CXSACT1  CLI   CXSFBACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSFBACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         B     CXSREC              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSFBACT   SAVE THIS ACTION                             
         MVI   CXSFBACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
         MVI   CXSFBFLD-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         MVI   CXSFBCOD-1,MXTRTQ                                                
         CLI   SAVEACTN,C'D'                                                    
         BNE   RETEXIT             IF NOT DELETE THEN RETURN FOR ADDS           
         B     DELEXIT             ELSE EXIT WITH JUST DELETE                   
*                                                                               
CXSREC   MVI   CXSFBFLD-1,MXTRTQ                                                
         MVI   CXSFBCOD-1,MXTRTQ                                                
*                                                                               
         USING SPATABD,R4                                                       
         ICM   R4,15,ASPATAB       GET ADDRESS SPATAB ENTRY                     
         BNZ   CXSFLD              FIRST TIME?                                  
         ICM   R4,15,=V(CXSSPAT)   R4=A(SYSTEM PROGRAM ACCESS TABLE)            
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CXSPAT   CLI   SPATOVS,0           GET ADDRESS SPATAB ENTRY                     
         BE    EOFEXIT                                                          
*        BNE   *+6                                                              
*        DC    H'00'                                                            
         CLC   SPATOVPG,SAFCOVPG                                                
         BE    *+12                                                             
         LA    R4,SPATLEN(R4)                                                   
         B     CXSPAT                                                           
         ST    R4,ASPATAB                                                       
         MVI   FLDNUM,0                                                         
         B     CXSF010                                                          
*                                                                               
CXSFLD   CLI   FLDNUM,X'FF'                                                     
         BE    EOFEXIT                                                          
         SR    R1,R1                                                            
         IC    R1,FLDNUM           R6=A(LAST FIELD NUMBER PROCESSED)            
         LA    R1,1(R1)                                                         
         STC   R1,FLDNUM                                                        
*                                                                               
CXSF010  SR    R0,R0                                                            
         IC    R0,FLDNUM                                                        
         GOTO1 TESTBIT,SPATFCL                                                  
         BZ    CXSFLD                                                           
*                                                                               
         GOTO1 XHEXOUT,DMCB,FLDNUM,CXSFBFLD,1,=C'TOG'                           
         LA    R5,SAFCDATA                                                      
CXSELM   CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXSFCN                                                           
         CLI   0(R5),SAFCWELQ      GET FCONTROL WRITE ELEMENT                   
         BE    CXSFCW                                                           
         CLI   0(R5),SAFCRELQ      GET FCONTROL READ ELEMENT                    
         BE    CXSFCR                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         B     CXSELM                                                           
*                                                                               
         USING SAFCWD,R5                                                        
CXSFCW   EQU   *                                                                
         SR    R0,R0                                                            
         IC    R0,FLDNUM                                                        
         XC    BITTABLX,BITTABLX   SET UP BIT TABLE                             
         CLI   SAFCWLN,SAFCWLNQ                                                 
         BE    CXSFCWA                                                          
         SR    RE,RE                                                            
         IC    RE,SAFCWLN          R2=L(BIT TABLE)-1                            
         SH    RE,=Y(SAFCWLNQ+1)                                                
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLX(0),SAFCWTAB                                             
CXSFCWA  GOTO1 TESTBIT,BITTABLX                                                 
         BZ    CXSNXT                                                           
         MVI   CXSFBCOD,C'W'       CONTROL CODE = WRITE                         
         B     RETEXIT                                                          
*                                                                               
         USING SAFCRD,R5                                                        
CXSFCR   EQU   *                                                                
         SR    R0,R0                                                            
         IC    R0,FLDNUM                                                        
         XC    BITTABLX,BITTABLX   SET UP BIT TABLE                             
         CLI   SAFCRLN,SAFCRLNQ                                                 
         BE    CXSFCRA                                                          
         SR    RE,RE                                                            
         IC    RE,SAFCRLN          R2=L(BIT TABLE)-1                            
         SH    RE,=Y(SAFCRLNQ+1)                                                
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLX(0),SAFCRTAB                                             
CXSFCRA  GOTO1 TESTBIT,BITTABLX                                                 
         BZ    CXSFCN                                                           
         MVI   CXSFBCOD,C'R'       CONTROL CODE = READ                          
         B     RETEXIT                                                          
*                                                                               
CXSFCN   MVI   CXSFBCOD,C'N'       CONTROL CODE = NO                            
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    ASPATAB,ASPATAB                                                  
         MVI   FLDNUM,0                                                         
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         B     EXIT                                                             
*                                                                               
DELEXIT  XC    ASPATAB,ASPATAB     EXIT FOR DELETED RECORD                      
         MVI   FLDNUM,0                                                         
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),0             SET EOF RETURN AND WRITE RECORD              
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* NTRY: R1=A(BIT TABLE), R0=CODE                                      *         
***********************************************************************         
         SPACE 1                                                                
TESTBIT  LR    RF,R0               R1=A(BYTE OF BIT)                            
         SRL   RF,3                                                             
         AR    R1,RF                                                            
         SPACE 1                                                                
         LA    RF,X'07'            RF=MASK                                      
         NR    RF,R0                                                            
         IC    RF,MASKS(RF)                                                     
         SPACE 1                                                                
         EX    RF,TESTBITM         EX A TM                                      
         BR    RE                                                               
         SPACE 1                                                                
TESTBITM TM    0(R1),0             A TM                                         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
ASPATAB  DC    A(0)                                                             
FLDNUM   DC    AL1(0)                                                           
XHEXOUT  DC    V(HEXOUT)                                                        
         SPACE 1                                                                
MASKS    DC    X'8040201008040201'                                              
SAVEACTN DC    CL1' '                                                           
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                  SUBROUTINE PARAMETER LIST                    
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
BITTABLE DS    0XL8                8 BYTE BIT TABLE                             
BITTABLX DS    XL32                32 BYTE BIT TABLE                            
*                                                                               
FACTBITT DS    XL8                 BIT TABLE RETURNED BY FINDACTS               
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSFBD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022CXSFBC    05/21/98'                                      
         END                                                                    
