*          DATA SET REUNDEM    AT LEVEL 003 AS OF 09/09/83                      
*CATALP UNDEM                                                                   
         TITLE 'UNSCAN A STRING OF DEMO FIELDS'                                 
         SPACE 1                                                                
*              PARA 1              BYTE 0    MAX. NUMBER OF FIELDS              
*                                  BYTE 1-3  A(3-BYTE DEMO FIELDS)              
*              PARA 2              A(OUTPUT FIELD HEADER)                       
*              PARA 3              A(UNSCAN)                                    
         SPACE 2                                                                
UNDEM    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 30,UNDEM                                                         
         USING UNDEMD,RC                                                        
         LR    R2,R1               ADDRESS OF PARAMETER LIST                    
         SPACE 1                                                                
         SR    R3,R3                                                            
         IC    R3,0(R2)            NUMBER OF FIELDS                             
         L     R4,0(R2)            ADDRESS OF INPUT FIELDS                      
         MVC   UNSCAN,8(R2)                                                     
         MVI   WORK,C' '                                                        
         MVC   WORK+1(199),WORK                                                 
         LA    R5,WORK                                                          
         XR    R9,R9                                                            
         EJECT                                                                  
UNDEM2   LR    R6,R5                                                            
         TM    0(R4),X'80'                                                      
         BNO   *+12                                                             
         MVI   0(R6),C'-'          SUPPRESS CPP/CPM                             
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         MVC   0(1,R6),1(R4)       TYPE                                         
         CLI   0(R6),C'T'                                                       
         BE    *+8                 T IS DEFAULT                                 
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         XR    RE,RE               DEMO NUMBER                                  
         IC    RE,2(R4)                                                         
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R6),DUB+6(2)                                                 
         LR    R7,R6                                                            
         LA    R6,3(R6)                                                         
         CLI   0(R7),C'0'                                                       
         BNE   *+20                                                             
         MVC   0(2,R7),1(R7)       REMOVE LEADING ZEROS                         
         BCTR  R6,0                                                             
         MVI   0(R6),C' '                                                       
         B     *-20                                                             
         SPACE 1                                                                
         TM    0(R4),X'40'         PRIMARY                                      
         BNO   *+8                                                              
         MVI   0(R6),C'P'                                                       
         SPACE 1                                                                
         LA    R5,20(R5)                                                        
         LA    R9,1(R9)                                                         
         LA    R4,3(R4)                                                         
         OC    0(3,R4),0(R4)       END OF LIST                                  
         BZ    *+8                                                              
         BCT   R3,UNDEM2                                                        
         SPACE 1                                                                
         L     R4,4(R2)            FIELD HEADER                                 
         GOTO1 UNSCAN,DMCB,((R9),WORK),(R4),0                                   
         XMOD1 1                                                                
         EJECT                                                                  
UNDEMD   DSECT                                                                  
WORK     DS    10CL20                                                           
UNSCAN   DS    A                                                                
         SPACE 1                                                                
DMCB     DS    4F                                                               
DUB      DS    D                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003REUNDEM   09/09/83'                                      
         END                                                                    
