*          DATA SET ACAPGXREF  AT LEVEL 040 AS OF 03/23/15                      
*PROCESS USING(WARN(15))                                                        
*CATALP APGXREF                                                                 
APGXREF  TITLE 'PAN TITLE AND SCAN REPORT FROM TAPE'                            
APGXREF  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE LWSX-LWD,*APGX**,=V(REGSAVE),RA,CLEAR=YES                        
         USING LWD,RC                                                           
         L     R9,=V(CPRINT)                                                    
         USING DPRINT,R9                                                        
         ST    RD,SAVRD                                                         
         GOTO1 STXITER,DMCB,DUMPLIST                                            
         MVI   BOOKLST,X'FF'       MARK END OF BOOK TABLE                       
         MVI   SCANLST,X'FF'       AND SCAN TABLE                               
*                                                                               
VAL03    GOTO1 CARDS,DMCB,CARD,=C'RE00'                                         
         CLC   =C'/*',CARD         CHECK FOR END OF SELECTION FILE              
         BE    OPEN                OPEN FILES                                   
         MVC   P+1(80),CARD                                                     
         GOTO1 PRINTER                                                          
*                                                                               
VAL09    LA    R2,OPTTAB           LIST OF VALID INPUT FIELDS                   
         SR    R1,R1                                                            
*                                                                               
VAL11    IC    R1,0(R2)            LENGTH FOR COMPARE                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   CARD(0),1(R2)       MATCH CARD FIELD TO TABLE                    
         BE    VAL13                                                            
         LA    R2,L'OPTTAB(R2)                                                  
         CLI   0(R2),X'FF'                                                      
         BNE   VAL11                                                            
         B     ERR1                INVALID INPUT OPTION                         
*                                                                               
VAL13    SR    RF,RF               GET VALIDATION ROUTINE                       
         ICM   RF,3,11(R2)                                                      
         AR    RF,RB                                                            
         LA    R7,CARD+1(R1)       R7 TO CARD DATA                              
         BASR  RE,RF               VALIDATE INPUT OPTION                        
         B     VAL03               GET NEXT CARD                                
         EJECT                                                                  
***********************************************************************         
* VALIDATE THE REPORT SELECTION CARD                                            
***********************************************************************         
         SPACE 1                                                                
VALRPT   LR    R0,RE                                                            
         TM    OPTSW,OPTITL+OPTSCN TEST ALREADY INPUT                           
         BNZ   ERR2                                                             
         CLC   =C'TITLE',0(R7)                                                  
         BNE   VALRPT10                                                         
         OI    OPTSW,OPTITL                                                     
         B     VALRPTX                                                          
*                                                                               
VALRPT10 CLC   =C'SCAN',0(R7)                                                   
         BNE   ERR1                                                             
         OI    OPTSW,OPTSCN                                                     
*                                                                               
VALRPTX  LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE THE INPUT=DISK                                                       
***********************************************************************         
         SPACE 1                                                                
VALINP   CLI   0(R7),C'D'                                                       
         BNER  RE                                                               
         OI    OPTSW,OPTDSK                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE A BOOK SELECTION CARD                                                
***********************************************************************         
         SPACE 1                                                                
VALBOK   NTR1  ,                                                                
         LA    R2,BOOKLST          LIST OF BOOKS TO INCLUDE                     
         LA    R3,BOOKMX           MAXIMUM NUMBER                               
         CLI   0(R2),X'FF'         FIND CURRENT END                             
         BE    *+16                                                             
         LA    R2,L'BOOKLST(R2)                                                 
         BCT   R3,*-12                                                          
         B     ERR3                TOO MANY BOOK= CARDS                         
         MVC   0(L'BOOKLST,R2),SPACES                                           
         LA    RE,CARD+L'CARD-1    GET LENGTH OF DATA                           
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8              RE TO LAST CHARACTER                         
         CLI   0(RE),C'?'          DOES IT END WITH ? ?                         
         BNE   *+12                NO                                           
         BCTR  RE,0                DON'T COUNT ?                                
         XC    0(L'BOOKLST,R2),0(R2) PRFILL WITH ZEROS                          
         SR    RE,R7               LENGTH(LESS ONE) OF DATA                     
         BM    ERR4                INVALID BOOK CARD                            
         CH    RE,=Y(L'BOOKLST-1)  MAX LENGTH                                   
         BH    ERR4                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R7)       BOOK TO LIST                                 
         LA    R0,L'BOOKLST        CHANGE * TO ZEROS                            
         CLI   0(R2),C'*'                                                       
         BNE   *+8                                                              
         MVI   0(R2),0                                                          
         LA    R2,1(R2)                                                         
         BCT   R0,*-16                                                          
         MVI   0(R2),X'FF'         MARK NEW END OF TABLE                        
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE A SCAN CARD                                                          
***********************************************************************         
         SPACE 1                                                                
VALSCN   NTR1  ,                                                                
         LA    R2,SCANLST          LIST OF SCAN FIELDS                          
         USING SCAND,R2                                                         
         LA    R3,SCANMX           MAXIMUM NUMBER                               
         CLI   0(R2),X'FF'         FIND CURRENT END                             
         BE    *+16                                                             
         LA    R2,L'SCANLST(R2)                                                 
         BCT   R3,*-12                                                          
         B     ERR5                TOO MANY SCAN= CARDS                         
         MVI   SCANBGN,0           DEFAULT COL 1 THRU 72                        
         MVI   SCANEND,72                                                       
         ZAP   SCANCNT,=P'0'                                                    
         LA    RF,CARD+L'CARD-1    SEARCH FOR COL=NN NN                         
         LR    R6,RF               R6=LAST BYTE OF CARD                         
         SR    RF,R7               RF=NUMBER OF BYTES AFTER SCAN=               
         LR    R0,R7               R0 & R7=START OF DATA AFTER SCAN=            
*                                                                               
VALSCN03 CLC   =C' COL=',0(R7)     IS A COLUMN START/END SPECIFIED?             
         BE    VALSCN07            YES, VALIDATED COLUMN NUMBERS                
         LA    R7,1(R7)            LOOP THRU CARD DATA                          
         BCT   RF,VALSCN03                                                      
         B     VALSCN09            NO COLUMN SPECIFICATION                      
*                                                                               
VALSCN07 LR    R6,R7               R6=LAST BYTE OF SCAN DATA                    
         BAS   RE,VALCOL           SET COLUMN START END                         
         BNE   ERR7                INVALID COLUMN NUMBER                        
*                                                                               
VALSCN09 LR    R7,R0               R7=START OF SCAN DATA                        
         CLI   0(R6),C' '          R6=TO LAST SIGNIFICANT DATA                  
         BH    *+8                                                              
         BCT   R6,*-8              RE TO LAST CHARACTER                         
         SR    R6,R7               R6=LENGTH OF DATA                            
         BM    ERR6                INVALID SCAN DATA                            
         CH    R6,=Y(L'SCANDATA-1) MAX LENGTH                                   
         BH    ERR6                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   SCANDATA(0),0(R7)    DATA TO LIST                                
         STC   R6,SCANLEN                                                       
         LA    R2,L'SCANLST(R2)                                                 
         MVI   0(R2),X'FF'                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE OPTIONAL COLUMN NUMBERS                                              
***********************************************************************         
         SPACE 1                                                                
VALCOL   NTR1  ,                                                                
         LA    RF,5(R7)            RF TO START COLUMN NUMBER                    
         BAS   RE,VALCNUM          COL=N(N) N(N)                                
         STC   R3,SCANEND          ASSUME ONE COLUMN SEARCH                     
         BCTR  R3,0                                                             
         STC   R3,SCANBGN          START COLUMN                                 
         LA    RF,1(R7)            RF TO END COLUMN NUMBER                      
         LA    RE,CARD+L'CARD-1    TEST ANYMORE ON CARD                         
         SR    RE,RF                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES                                                   
         BE    XIT                                                              
         BAS   RE,VALCNUM          VALIDATE END COLUMN                          
         STC   R3,SCANEND                                                       
         CR    R3,R3                                                            
         B     XIT                                                              
*                                                                               
VALCNUM  SR    R1,R1               VALIDATE NUMERICS                            
         TM    0(RF),X'F0'                                                      
         BNO   XIT                                                              
         CLI   1(RF),C' '          ONE DIGIT NUMBER                             
         BE    *+16                                                             
         TM    1(RF),X'F0'                                                      
         BNO   XIT                                                              
         LA    R1,1(R1)            TWO DIGIT NUMBER                             
*                                                                               
         LA    R7,1(R1,RF)         TEST MORE THAN 2 CHARACTERS                  
         CLI   0(R7),C' '                                                       
         BNE   XIT                                                              
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,RF)                                                      
         CVB   R3,DUB                                                           
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SET TO SUPPRESS SCREENS AND DSECTS                                            
***********************************************************************         
         SPACE 1                                                                
VALNDS   OI    OPTSW,OPTNDS                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* OPEN AND PUT PAN DIRECTORIES TO OUTPUT FILE                         *         
***********************************************************************         
         SPACE 1                                                                
OPEN     TM    OPTSW,OPTITL+OPTSCN REPORT SELECTED?                             
         BNZ   *+8                 YES                                          
         OI    OPTSW,OPTITL        DEFAULT IS TITLE                             
         LA    R2,SRTKLNQ          INITIALIZE SORT                              
         LA    R4,SORTCARD+15                                                   
         EDIT  (R2),(3,0(R4)),FILL=0                                            
         LA    R2,SRTLNQ                                                        
         LA    R4,RECCARD+21                                                    
         EDIT  (R2),(3,0(R4)),FILL=0                                            
         GOTO1 SORTER,DMCB,SORTCARD,RECCARD                                     
*                                                                               
GETDIR   DS    0H                  FIRST READ ALL DIR ENTRIES                   
         OPEN  (OUTFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         MVC   PANBK,=CL10'A'      READ DIR->SAVE IN SEQ FILE                   
*                                                                               
GETDIR3  XC    CARD,CARD                                                        
         LA    R2,=C'T'                                                         
         TM    OPTSW,OPTDSK                                                     
         BNO   *+8                                                              
         LA    R2,=C'D'                                                         
         GOTO1 PANIC,DMCB,(X'40',READ),((R2),DIR),PANBK,CARD                    
         CLC   =C'/*',CARD         END OF DIRECTORY?                            
         BE    GETDIR4             YES                                          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BAS   RE,FLTBK            FILTER THE BOOK                              
         BNE   GETDIR3             BOOK NOT IN LIST                             
         BAS   RE,SETGRP           SET BOOK GROUP CODE                          
         CLI   GRPC,0              TEST BOOK IS EXCLUDED                        
         BE    GETDIR3                                                          
         PUT   OUTFILE,CARD                                                     
         B     GETDIR3                                                          
*                                                                               
GETDIR4  CLOSE OUTFILE                                                          
         GOTO1 PANIC,DMCB,CLOSE,0,0,0                                           
*                                                                               
***********************************************************************         
* READ TEMP DIRECTORIES AND GET PANFILE                               *         
***********************************************************************         
                                                                                
         OPEN  (OUTFILE,INPUT)                                                  
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GETDIR5  XC    CARD,CARD                                                        
         GET   OUTFILE,CARD        READ IN DIR ENTRY                            
         MVC   PANBK,CARD          SET PANBK TO NAME OF BOOK READ               
         BAS   RE,SETGRP           SET BOOK GROUP CODE                          
         MVC   SRTDIR,CARD         SAVE DIRECTORY INFO                          
         MVC   SRTKCODE,SRTBOOK    DEFAULT SORT IS BOOK                         
         MVC   SRTPHSE,SPACES      CLEAR PHASE                                  
         MVC   SRTTITL,SPACES      TITLE                                        
         MVI   SRTFLAG,0           STATUS                                       
         MVC   SRTKGRPC,GRPC       SET GROUP CODE                               
         MVI   SRTKTYPE,0          TYPE                                         
         MVC   SYSNAME,SPACES      SYSTEM NAME FOR APG                          
         OI    FLAG,PENDING        SET PENDING FLAG                             
*                                                                               
GETTAPE  XC    CARD,CARD                                                        
         LA    R2,=C'T'                                                         
         TM    OPTSW,OPTDSK                                                     
         BNO   *+8                                                              
         LA    R2,=C'D'                                                         
         GOTO1 PANIC,DMCB,READ,((R2),PAN),PANBK,CARD                            
         CLC   CARD(2),=C'/*'        END OF BOOK?                               
         BNE   GETTAPE3              NO, CONTINUE TO PROCESS                    
         BAS   RE,PUT                PUT IT TO SORT                             
         B     GETDIR5               GET ANOTHER DIRECTORY                      
*                                                                               
GETTAPE3 CLI   8(R1),0             FILE ERROR                                   
         BNE   GETDIR5             SKIP IT                                      
*                                                                               
         CLC   =C'*          DATA SET',CARD                                     
         BE    GETTAPE               IGNORE DATA SETS                           
         CLC   =C'000',SRTBOOK+10                                               
         BE    GETTAPE               IGNORE SUPERSETS                           
*                                                                               
         BAS   RE,GETTIT           GET TITLE                                    
         BAS   RE,GETTYP           GET TYPE PHASE/CATALP                        
         BAS   RE,GETSYN           GET SYSNAME(APG)                             
         OI    SRTFLAG,SRTFNXT     SET SUBSEQUENT CARD                          
*                                                                               
         TM    OPTSW,OPTSCN        IS IT A SCAN REPORT?                         
         BNO   *+8                 NO, SKIP SCAN ROUTINE                        
         BAS   RE,SCAN             SEARCH FOR SCAN ARGUMENTS                    
         B     GETTAPE                                                          
         EJECT                                                                  
***********************************************************************         
* FILTER THE DIRECTORY ENTRIES                                        *         
***********************************************************************         
         SPACE 1                                                                
FLTBK    LA    R2,BOOKLST                                                       
         CLI   0(R2),X'FF'         ANY BOOK FILTERS?                            
         BER   RE                  NO, SELECT BOOK                              
*                                                                               
FLTBK3   LA    R0,L'BOOKLST        R0=LENGTH OF BOOK                            
         LA    RF,CARD             RF=CURRENT BOOK                              
         LR    R1,R2               R1=BOOK SELECTION ENTRY                      
*                                                                               
FLTBK5   CLI   0(R1),0             DON'T MATCH ZERO'S                           
         BE    *+14                                                             
         CLC   0(1,R1),0(RF)                                                    
         BNE   FLTBK7              NO MATCH                                     
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,FLTBK5                                                        
         CR    RB,RB               SET EQ                                       
         BR    RE                                                               
*                                                                               
FLTBK7   LA    R2,L'BOOKLST(R2)                                                 
         CLI   0(R2),X'FF'                                                      
         BNE   FLTBK3                                                           
         LTR   RB,RB               SET NOT EQ                                   
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SET GROUP CODE                                                      *         
***********************************************************************         
         SPACE 1                                                                
SETGRP   MVI   GRPC,X'FF'          SET DEFAULT GROUP                            
         TM    OPTSW,OPTITL        TEST TITLE REPORT                            
         BNOR  RE                                                               
         SR    R1,R1                                                            
         LA    R2,GROUP            GROUP LIST                                   
*                                                                               
SETGRP3  CLI   0(R2),X'FF'         EOT                                          
         BER   RE                                                               
         IC    R1,5(R2)            LENGTH FOR COMPARE                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),CARD                                                     
         BE    SETGRP5                                                          
         LA    R2,L'GROUP(R2)                                                   
         B     SETGRP3                                                          
SETGRP5  MVC   GRPC,6(R2)          SET GROUP CODE                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET A TITLE CARD                                                   *          
***********************************************************************         
         SPACE 1                                                                
GETTIT   CLC   SRTTITL,SPACES      ALREADY HAVE A TITLE?                        
         BNER  RE                  YES, DON'T WANT THIS ONE                     
         LA    R3,CARD+3                                                        
         LA    0,16                                                             
         CLC   =C' TITLE ',0(R3)   IS IT A TITLE CARD?                          
         BE    *+14                                                             
         LA    R3,1(R3)                                                         
         BCT   R0,*-14                                                          
         BR    RE                                                               
*                                                                               
         LA    R3,1(R3)            FIND FIRST '                                 
         LA    R0,15                                                            
         CLI   0(R3),C''''                                                      
         BE    *+14                                                             
         LA    R3,1(R3)                                                         
         BCT   R0,*-12                                                          
         BR    RE                                                               
         LA    R3,1(R3)            R3 = FIRST BYTE OF TITLE                     
*                                                                               
         LA    R1,CARD+70          FIND TRAILING '                              
         LR    R0,R1                                                            
         SR    R0,R3                                                            
         CLI   0(R1),C''''                                                      
         BE    *+12                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         BR    RE                                                               
*                                                                               
         SR    R1,R3               R1=LENGTH OF TITLE                           
         BCTR  R1,0                                                             
         LA    R4,L'PLTITL-1       R4=MAXIMUM LENGTH OF TITLE                   
         CR    R1,R4                                                            
         BL    *+6                                                              
         LR    R1,R4                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   SRTTITL(0),0(R3)  SAVE TITLE                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET A PHASE/CATALP CARD                                            *          
***********************************************************************         
         SPACE 1                                                                
GETTYP   CLI   SRTKTYPE,0                                                       
         BNER  RE                                                               
         CLC   =C'*PHASE ',CARD    IS THIS A PHASE CARD?                        
         BNE   GETTYP3                                                          
         MVC   SRTPHSE,CARD+7      SAVE PHASE                                   
         MVI   SRTKTYPE,SRTKTREP   REPORT                                       
         CLC   SRTBOOK+2(3),=C'REP'                                             
         BER   RE                                                               
         MVI   SRTKTYPE,SRTKTPHS   OTHER PHASE                                  
         CLC   SRTPHSE(2),=C'T6'                                                
         BNER  RE                                                               
         MVI   SRTKTYPE,SRTKTONL   ONLINE MODULE                                
         TM    SRTKCODE+5,X'F0'    FORCE NUMERICS FIRST                         
         BNOR  RE                                                               
         NI    SRTKCODE+5,X'0F'                                                 
         TM    SRTKCODE+6,X'F0'                                                 
         BNOR  RE                                                               
         NI    SRTKCODE+6,X'0F'                                                 
         BR    RE                                                               
                                                                                
GETTYP3  CLC   =C'*CATALP',CARD    CATALP CARD?                                 
         BNE   GETTYP5                                                          
         MVC   SRTPHSE,CARD+8      SAVE CATALP                                  
         MVI   SRTKTYPE,SRTKTREL                                                
         BR    RE                                                               
                                                                                
GETTYP5  CLC   =C'APG',SRTBOOK                                                  
         BNE   GETTYP7                                                          
         CLC   =C'PHASE',CARD                                                   
         BNE   GETTYP7                                                          
         MVC   SRTPHSE(2),=C'AC'                                                
         MVC   SRTPHSE+2(6),CARD+9                                              
         MVI   SRTKTYPE,SRTKTAPG                                                
         BR    RE                                                               
                                                                                
GETTYP7  CLC   =C'DSECT ',CARD+15  TEST DSECT FOR SCREEN                        
         BNE   GETTYP9                                                          
         CLC   =C'T6',CARD+37                                                   
         BNE   GETTYP9                                                          
         MVI   SRTKTYPE,SRTKTDSC   DSECT FOR SCREEN                             
         MVC   SRTPHSE,CARD+37     SAVE PHASE                                   
         BR    RE                                                               
                                                                                
GETTYP9  LA    R0,18                                                            
         LA    R1,CARD+1                                                        
         CLC   =C'DSECT ',0(R1)                                                 
         BE    GETTYP11                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,*-14                                                          
         B     GETTYP13                                                         
GETTYP11 MVC   SRTPHSE,=CL12'DSECT'                                             
         MVI   SRTKTYPE,SRTKTDSC                                                
         BR    RE                                                               
                                                                                
GETTYP13 TM    SRTFLAG,SRTFNXT     TEST FIRST CARD                              
         BNZ   GETTYP15            SECOND/THIRD....                             
         CLI   CARD,C'S'           TEST SCREEN CARD                             
         BNE   GETTYP15                                                         
         CLI   CARD+4,C'T'                                                      
         BNE   GETTYP15                                                         
         MVC   SRTPHSE(4),CARD+4   BUILD SCREEN PHASE                           
         MVC   SRTPHSE+4(2),CARD+1                                              
         MVI   SRTKTYPE,SRTKTSCR                                                
         BR    RE                                                               
*                                                                               
GETTYP15 DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET A SYSNAME CARD                                                 *          
***********************************************************************         
         SPACE 1                                                                
GETSYN   CLC   SYSNAME,SPACES      ALREADY HAVE A TITLE?                        
         BNER  RE                  YES, DON'T WANT THIS ONE                     
         CLC   =C'APG',SRTBOOK                                                  
         BNER  RE                                                               
         CLC   =C'SYSNAME',CARD                                                 
         BNER  RE                                                               
         MVC   SYSNAME,CARD+15                                                  
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SCAN FOR SELECTED DATA                                              *         
***********************************************************************         
         SPACE 1                                                                
         USING SCAND,R2                                                         
SCAN     NTR1  ,                                                                
         LA    R2,SCANLST          LIST OF SCAN ENTRIES                         
*                                                                               
SCAN3    SR    R1,R1                                                            
         IC    R1,SCANBGN          START COLUMN LESS ONE                        
         LA    RE,CARD(R1)         RE = START OF DATA SEARCH                    
         SR    R0,R0                                                            
         IC    R0,SCANEND                                                       
         SR    R0,R1               R0 = NUMBER OF COLUMNS TO SEARCH             
         SR    RF,RF                                                            
         IC    RF,SCANLEN          LENGTH OF DATA                               
*                                                                               
SCAN5    EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),SCANDATA    DOES DATA MATCH SCAN ENTRY                   
         BNE   *+10                NO, DON'T COUNT IT                           
         AP    SCANCNT,=P'1'       COUNT IT                                     
         LA    RE,1(RE)                                                         
         BCT   R0,SCAN5                                                         
*                                                                               
         LA    R2,SCANLNQ(R2)      NEXT ENTRY                                   
         CLI   0(R2),X'FF'         IS IT END OF TABLE?                          
         BNE   SCAN3               NO SEARCH FOR NEXT ITEM                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* PUT RECORDS TO SORTER                                               *         
***********************************************************************         
         SPACE 1                                                                
PUT      NTR1  ,                                                                
         TM    FLAG,PENDING                                                     
         BNO   XIT                                                              
         NI    FLAG,X'FF'-(PENDING)                                             
         CLC   SRTTITL,SPACES      IS THERE A TITLE CARD?                       
         BNE   *+10                YES, USE IT                                  
         MVC   SRTTITL,SYSNAME     ELSE USE SYSNAME (APG)                       
         MVC   SRTDATA,SPACES                                                   
         ZAP   SRTCNT,=P'0'                                                     
         TM    OPTSW,OPTSCN        IS IT A SCAN REPORT?                         
         BO    PUT09               YES, PUT RECORD FOR ITEMS FOUND              
         CLI   SRTKGRPC,X'FF'      ANY TYPE SET                                 
         BNE   PUT03                                                            
         MVI   SRTKTYPE,SRTKTOTH   SET OTHER                                    
                                                                                
PUT03    TM    OPTSW,OPTNDS        TEST SUPPRESS DSECTS AND SCREENS             
         BNO   PUT04                                                            
         CLI   SRTKTYPE,SRTKTDSC                                                
         BE    XIT                                                              
         CLI   SRTKTYPE,SRTKTSCR                                                
         BE    XIT                                                              
PUT04    GOTO1 SORTER,DMCB,=C'PUT',SRTKEY                                       
         B     XIT                                                              
*                                                                               
         USING SCAND,R2                                                         
PUT09    LA    R2,SCANLST          LIST OF SCAN ENTRIES                         
*                                                                               
PUT11    CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         CP    SCANCNT,=P'0'       NO HITS                                      
         BE    PUT13                                                            
         ZAP   SRTCNT,SCANCNT      NUMBER OF OCCURANCES                         
         ZAP   SCANCNT,=P'0'       CLEAR FOR NEXT TIME                          
         MVC   SRTDATA,SCANDATA    SCAN DATA TO SORT                            
         GOTO1 SORTER,DMCB,=C'PUT',SRTKEY                                       
*                                                                               
PUT13    LA    R2,SCANLNQ(R2)      NEXT ENTRY                                   
         B     PUT11                                                            
         EJECT                                                                  
***********************************************************************         
* PRODUCE A REPORT                                                   *          
***********************************************************************         
         SPACE 1                                                                
TAPEEOF  MVC   MID1+4(4),=C'BOOK'                                               
         MVC   MID2+1(10),UNDRLNE                                               
         MVC   MID1+16(10),=C'PHASE/RELO'                                       
         MVC   MID2+16(10),UNDRLNE                                              
         MVC   MID1+51(5),=C'TITLE'                                             
         MVC   MID2+31(45),UNDRLNE                                              
         TM    OPTSW,OPTITL                                                     
         BNO   HEADSCAN                                                         
         MVC   TITLE(20),=CL20' PAN TITLE REPORT'                               
         MVC   MID1+80(5),=C'LEVEL'                                             
         MVC   MID2+80(5),UNDRLNE                                               
         MVC   MID1+87(4),=C'USER'                                              
         MVC   MID2+87(4),UNDRLNE                                               
         MVC   MID1+94(7),=C'UPDATED'                                           
         MVC   MID2+94(7),UNDRLNE                                               
         B     HEADALL                                                          
*                                                                               
HEADSCAN MVC   TITLE(20),=CL20' PAN SCAN REPORT'                                
         MVC   MID1+84(9),=C'SCAN DATA'                                         
         MVC   MID2+78(20),UNDRLNE                                              
         MVC   MID1+100(10),=C'OCCURENCES'                                      
         MVC   MID2+100(10),UNDRLNE                                             
*                                                                               
HEADALL  ZAP   PAGE,=P'1'                                                       
         ZAP   LINE,=P'75'                                                      
         XC    PANBK,PANBK                                                      
*                                                                               
GETSORT  GOTO1 SORTER,DMCB,=C'GET'                                              
         ICM   R2,15,4(R1)                                                      
         BZ    EOJ                 NO MORE ITEMS                                
         LA    R5,P                                                             
         USING PLD,R5                                                           
         MVC   SRTKEY(SRTLNQ),0(R2)                                             
         CLC   PANBK,SRTBOOK       SAME BOOK?                                   
         BE    GETS10              YES, MUST BE SCAN REPORT                     
         CLC   SVGRPC,SRTKGRPC     GROUP CODE                                   
         BE    *+10                                                             
         ZAP   LINE,=P'75'         NEW PAGE                                     
*                                                                               
GETS03   MVC   SVGRPC,SRTKGRPC                                                  
         MVC   SVBOOK,SRTBOOK                                                   
         MVC   PANBK,SRTBOOK       SAVE BOOK NAME                               
         MVC   PLBOOK,SRTBOOK      PRINT BOOK                                   
         MVC   PLPHSE,SRTPHSE      PHASE                                        
         MVC   PLTITL,SRTTITL      TITLE                                        
         TM    OPTSW,OPTSCN        IS IT A SCAN REPORT?                         
         BO    GETS10              YES, PUT OUT SCAN DATA                       
         MVC   PLLEVL,SRTLEVL      LEVEL                                        
         MVC   PLUSER,SRTUSER      USER                                         
         MVC   PLMAIN,SRTMAIN      MAINTAINED                                   
         B     GETS20                                                           
*                                                                               
GETS10   MVC   PLSCAN,SRTDATA      SCAN DATA                                    
         EDIT  SRTCNT,(4,PLCNT)    OCCURRANCES                                  
*                                                                               
GETS20   L     RF,PRINTER          PRINT A LINE                                 
         BASR  RE,RF                                                            
         B     GETSORT                                                          
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*        ERROR ROUTINES                                               *         
***********************************************************************         
*                                                                               
ERR1     MVC   P+1(L'ERRM1),ERRM1  INVALID OPTION                               
         B     ERRX                                                             
*                                                                               
ERR2     MVC   P+1(L'ERRM2),ERRM2  DUPLICATE TITLE CARD                         
         B     ERRX                                                             
*                                                                               
ERR3     MVC   P+1(L'ERRM3),ERRM3  TOO MANY BOOK= CARDS                         
         B     ERRX                                                             
*                                                                               
ERR4     MVC   P+1(L'ERRM4),ERRM4  INVALID BOOK CARD                            
         B     ERRX                                                             
*                                                                               
ERR5     MVC   P+1(L'ERRM5),ERRM5  TOO MANY SCAN= CARDS                         
         B     ERRX                                                             
*                                                                               
ERR6     MVC   P+1(L'ERRM6),ERRM6  INVALID SCAN  CARD                           
         B     ERRX                                                             
*                                                                               
ERR7     MVC   P+1(L'ERRM7),ERRM7  INVALID COLUMN SPECIFICATION                 
         B     ERRX                                                             
*                                                                               
ERRX     GOTO1 PRINTER                                                          
         L     RD,SAVRD                                                         
         B     EOJ                                                              
*                                                                               
XIT      XIT1                                                                   
EOJ      XBASE                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS                                                      *         
***********************************************************************         
         SPACE 1                                                                
SORTER   DC    V(SORTER)                                                        
STXITER  DC    V(STXITER)                                                       
CARDS    DC    V(CARDS)                                                         
PRINTER  DC    V(PRINTER)                                                       
PANIC    DC    V(PANIC)                                                         
*                                                                               
READ     DC    C'READ'                                                          
CLOSE    DC    C'CLOSE'                                                         
DIR      DC    C'DIR'                                                           
PAN      DC    C'PAN'                                                           
*                                                                               
OPTSW    DC    X'00'               OPTIONS SWITCH                               
OPTITL   EQU   X'80'               TITLE REPORT                                 
OPTSCN   EQU   X'40'               SCAN REPORT                                  
OPTDSK   EQU   X'08'               INPUT IS DISK                                
OPTNDS   EQU   X'01'               SUPPRESS SCREENS AND DSECTS                  
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,NNN,A),FORMAT=BI'                            
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=NNN'                                   
*                                                                               
UNDRLNE  DC    50C'-'                                                           
*                                                                               
GROUP    DS    XL7                 CODE/LENGTH/SEQUENCE                         
         DC    CL5'AC58 ',AL1(4,000)                                            
         DC    CL5'AC59 ',AL1(4,000)                                            
         DC    CL5'AC67 ',AL1(4,000)                                            
         DC    CL5'AC97 ',AL1(4,000)                                            
         DC    CL5'AC98 ',AL1(4,000)                                            
         DC    CL5'ACREP',AL1(5,001)                                            
         DC    CL5'ACBAT',AL1(5,005)                                            
         DC    CL5'ACBIL',AL1(5,010)                                            
         DC    CL5'ACBTN',AL1(5,015)                                            
         DC    CL5'ACBUD',AL1(5,020)                                            
         DC    CL5'ACCAP',AL1(5,025)                                            
         DC    CL5'ACCLB',AL1(5,030)                                            
         DC    CL5'ACCRD',AL1(5,035)                                            
         DC    CL5'ACCSH',AL1(5,040)                                            
         DC    CL5'ACCTA',AL1(5,045)                                            
         DC    CL5'ACENQ',AL1(5,050)                                            
         DC    CL5'ACEXP',AL1(5,055)                                            
         DC    CL5'ACINF',AL1(5,060)                                            
         DC    CL5'ACINQ',AL1(5,065)                                            
         DC    CL5'ACINT',AL1(5,070)                                            
         DC    CL5'ACINV',AL1(5,075)                                            
         DC    CL5'ACLFM',AL1(5,080)                                            
         DC    CL5'ACMAC',AL1(5,085)                                            
         DC    CL5'ACMRK',AL1(5,090)                                            
         DC    CL5'ACORD',AL1(5,095)                                            
         DC    CL5'ACPRO',AL1(5,100)                                            
         DC    CL5'ACREC',AL1(5,105)                                            
         DC    CL5'ACREG',AL1(5,110)                                            
         DC    CL5'ACREQ',AL1(5,115)                                            
         DC    CL5'ACSCR',AL1(5,120)                                            
         DC    CL5'ACTRA',AL1(5,125)                                            
         DC    CL5'ACTRN',AL1(5,130)                                            
         DC    CL5'ACVOI',AL1(5,135)                                            
         DC    CL5'ACWFM',AL1(5,140)                                            
         DC    CL5'ACWRI',AL1(5,145)                                            
         DC    CL5'ACWRK',AL1(5,150)                                            
         DC    CL5'ACLD ',AL1(4,155)                                            
         DC    CL5'ACACC',AL1(5,160)                                            
         DC    CL5'ACGEN',AL1(5,165)                                            
         DC    X'FF'                                                            
OUTFILE  DCB   DDNAME=OUTFILE,DSORG=PS,RECFM=FB,MACRF=(GM,PM),         X        
               BLKSIZE=3120,LRECL=80,EODAD=TAPEEOF                              
         EJECT                                                                  
*                                                                               
***********************************************************************         
*        INPUT CARD OPTIONS                                           *         
***********************************************************************         
*                                                                               
OPTTAB   DS    0CL13                                                            
         DC    AL1(6),CL10'REPORT=   ',AL2(VALRPT-APGXREF)                      
         DC    AL1(4),CL10'BOOK=     ',AL2(VALBOK-APGXREF)                      
         DC    AL1(4),CL10'SCAN=     ',AL2(VALSCN-APGXREF)                      
         DC    AL1(6),CL10'NODSECT   ',AL2(VALNDS-APGXREF)                      
         DC    AL1(5),CL10'INPUT=    ',AL2(VALINP-APGXREF)                      
         DC    X'FF'                                                            
*                                                                               
ERRM1    DC    C'INVALID OPTION'                                                
ERRM2    DC    C'ONLY ONE REPORT= CARD IS PERMITTED'                            
ERRM3    DC    C'TOO MANY BOOK= CARDS'                                          
ERRM4    DC    C'INVALID BOOK '                                                 
ERRM5    DC    C'TOO MANY SCAN= CARDS'                                          
ERRM6    DC    C'INVALID SCAN CARD'                                             
ERRM7    DC    C'INVALID COLUMN SPECIFICATION'                                  
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(APGXREF)                                                       
         DC    V(DUMMY)                                                         
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
LWD      DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
SAVRD    DS    F                                                                
SVGRPC   DS    XL1                                                              
SVBOOK   DS    XL10                                                             
GRPC     DS    XL1                 GROUP CODE                                   
*                                                                               
FLAG     DS    XL1                                                              
PENDING  EQU   X'80'               WRITE PENDING                                
*                                                                               
PANBK    DS    CL10                                                             
CARD     DS    CL80                                                             
WORK     DS    CL64                                                             
*                                                                               
BOOKMX   EQU   10                  MAXIMUM NUMBER OF BOOK CARDS                 
BOOKLST  DS    (BOOKMX)CL10        TABLE OF BOOK SELECTION CRITERIA             
BOOKLAST DS    XL1                 MAXIMUM END                                  
*                                                                               
SCANMX   EQU   30                  MAXIMUM NUMBER OF SCAN CARDS                 
SCANLST  DS    (SCANMX)CL(SCANLNQ) TABLE OF SCAN SELECTION CRITERIA             
SCANLAST DS    XL1                 MAXIMUM END                                  
         EJECT                                                                  
*                                                                               
SRTKEY   DS    0C                  SORT KEY                                     
SRTKGRPC DS    XL1                 GROUP                                        
SRTKCODE DS    CL10                SPECIAL SORT CODE                            
SRTKLNQ  EQU   *-SRTKEY            SORT KEY LENGTH                              
SRTKTYPE DS    XL1                 BOOK TYPE                                    
SRTKTREP EQU   1                   REPORT                                       
SRTKTONL EQU   2                   ONLINE MODULE                                
SRTKTAPG EQU   3                   APG MODULE                                   
SRTKTPHS EQU   4                   OTHER PHASE                                  
SRTKTREL EQU   5                   RELO MODULE                                  
SRTKTOTH EQU   6                   OTHER                                        
SRTKTSCR EQU   7                   SCREEN                                       
SRTKTDSC EQU   8                   DSECT                                        
*                                                                               
SRTDIR   DS    CL80                                                             
         ORG   SRTDIR                                                           
SRTBOOK  DS    CL10                BOOK                                         
SRTLEVL  DS    CL3                 LEVEL                                        
SRTUSER  DS    CL4                 USER                                         
SRTFM    DS    CL1                                                              
SRTLANG  DS    CL4                 LANGUAGE                                     
         DS    CL1                                                              
SRTSTAT  DS    CL3                 STATUS                                       
SRTMAIN  DS    CL8                 DATE LAST MAINT.                             
SRTACCS  DS    CL8                 DATE LAST ACCESS                             
SRTSTMT  DS    CL8                 STATEMENTS                                   
         DS    CL4                                                              
SRTAVRG  DS    CL2                 AVERAGE                                      
         DS    (L'SRTDIR-(*-SRTBOOK))C                                          
SRTPHSE  DS    CL(L'PLPHSE)        PHASE/CATALP                                 
SRTTITL  DS    CL(L'PLTITL)        TITLE CARD                                   
*                                                                               
SRTFLAG  DS    XL1                 STATUS                                       
SRTFNXT  EQU   X'01'               NOT FIRST CARD                               
*                                                                               
SRTDATA  DS    CL20                SCAN DATA                                    
SRTCNT   DS    PL3                 OCCURANCES                                   
SRTLNQ   EQU   *-SRTKEY                                                         
*                                                                               
SYSNAME  DS    CL(L'PLTITL)        SYSTEM NAME                                  
LWSX     EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* DSECT FOR A SCAN CARD ENTRY                                         *         
***********************************************************************         
         SPACE 1                                                                
SCAND    DSECT                                                                  
SCANBGN  DS    XL1                 FIRST COLUMN OF SCAN                         
SCANEND  DS    XL1                 LAST COLUMN OF SCAN                          
SCANLEN  DS    XL1                 LENGTH OF SCAN DATA LESS 1                   
SCANDATA DS    CL20                SCAN DATA                                    
SCANCNT  DS    PL3                 OCCURANCES                                   
SCANLNQ  EQU   *-SCAND                                                          
         EJECT                                                                  
***********************************************************************         
* DSECT FOR A PRINT LINE                                              *         
***********************************************************************         
         SPACE 1                                                                
PLD      DSECT                                                                  
         DS    C                                                                
PLBOOK   DS    CL10                BOOK                                         
         DS    5C                                                               
PLPHSE   DS    CL12                PHASE                                        
         DS    3C                                                               
PLTITL   DS    CL45                TITLE                                        
         DS    CL2                                                              
PLRGHT   DS    0C                  RIGHT SIDE OF REPORT                         
         DS    CL3                                                              
PLLEVL   DS    CL3                 LEVEL                                        
         DS    CL3                                                              
PLUSER   DS    CL4                 USER                                         
         DS    CL3                                                              
PLMAIN   DS    CL8                 LAST UPDATE                                  
         ORG   PLRGHT                                                           
PLSCAN   DS    CL20                SCAN DATA                                    
         DS    CL2                                                              
PLCNT    DS    CL4                 OCCURANCES                                   
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'040ACAPGXREF 03/23/15'                                      
         END                                                                    
