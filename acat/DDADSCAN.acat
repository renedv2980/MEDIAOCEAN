*          DATA SET DDADSCAN   AT LEVEL 051 AS OF 01/21/98                      
*CATALP ADSCAN                                                                  
         TITLE 'ADSCAN - YUK''S FREEFORM CITY, STATE, ZIP FORMATTER'            
         PRINT NOGEN                                                            
         SPACE 1                                                                
* PARMS:                                                                        
* 1= L'SENDING FIELD/ADDR OF SENDING FIELD                                      
* 2= L'RECVG CITY FIELD/ADDR OF REVCG CITY FIELD                                
* 3= ADDRESS OF 2 POS. STATE RECVG FIELD                                        
* 4= L'ZIP/ADDR OF RECVG ZIP FIELD                                              
*                                                                               
* ERROR CODE PASSED BACK IN PARM 1:                                             
* 0= CONGRATULATIONS.                                                           
* 1= SERIOUS ERROR, COUDN'T FIGURE ANYTHING VALID.                              
* 2= STATE CODE DID NOT MATCH ZIP CODE, STATE CODE CHANGED TO MATCH             
*    ZIP CODE.                                                                  
* 3= ZIP CODE LENGTH ERROR. NOT 5 OR 9 OR LONGER THAN RECEIVING FIELD.          
         EJECT                                                                  
ADSCAN   CSECT                                                                  
         NMOD1 8,*ADSCAN*                                                       
         ZIC   R3,0(R1)            SEND LENGTH                                  
         L     R5,0(R1)                                                         
         ST    R5,SEND             SEND ADDR                                    
         NI    SEND,0              MAKE SURE IT'S A GOOD ADDRESS                
         MVC   0(4,R1),=F'0'                                                    
         ZIC   R4,4(R1)                                                         
         STH   R4,CYL              CITY LENGTH                                  
         L     R5,4(R1)                                                         
         ST    R5,CITY             CITY ADDR                                    
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPCES                                                    
         L     R4,8(R1)                                                         
         ST    R4,STATE            STATE ADDR                                   
         MVC   0(2,R4),=C'  '                                                   
         L     R5,12(R1)                                                        
         ST    R5,ZIP              ZIP ADDR                                     
         ZIC   R4,12(R1)                                                        
         STH   R4,ZPL              ZIP LENGTH                                   
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPCES                                                    
         L     R4,SEND             GET TO BACK OF SEND                          
         AR    R4,R3                                                            
         ST    R1,PARAM            SAVE PARM LIST ADDR                          
         EJECT                                                                  
ZIP000   SR    R5,R5                                                            
ZIP00    BCTR  R4,0                                                             
         CLI   0(R4),C' '                                                       
         BE    ZIP00                                                            
         SPACE 1                                                                
ZIP0     CLI   0(R4),C'-'          FOR 9 DIGIT ZIP CODES                        
         BE    ZIP1A                                                            
         CLI   0(R4),X'F0'                                                      
         BL    ZIP2                                                             
         CLI   0(R4),X'F9'                                                      
         BNH   ZIP1                                                             
         B     ZIP2                                                             
         SPACE 1                                                                
ERR1     L     R1,PARAM                                                         
         L     R4,=F'1'                                                         
         ST    R4,0(R1)                                                         
         B     XIT                                                              
         SPACE 1                                                                
ZIP1     LA    R5,1(R5)                                                         
ZIP1A    BCTR  R4,0                                                             
         B     ZIP0                                                             
         SPACE 1                                                                
ZIP2     CH    R5,=H'5'                                                         
         BE    ZIP3                                                             
         CH    R5,=H'9'                                                         
         BE    ZIP3                                                             
         SPACE 1                                                                
ERR3     L     R1,PARAM                                                         
         L     R4,=F'3'                                                         
         ST    R4,0(R1)         CC IN PARM 1                                    
         ST    R5,4(R1)         ACTUAL LENGTH IN PARM 2                         
         LH    R5,ZPL           CORRECT LENGTH IN PARM 3                        
         ST    R5,8(R1)                                                         
         B     XIT                                                              
         SPACE 1                                                                
ZIP3     LH    R6,ZPL                                                           
         CR    R5,R6                                                            
         BH    ERR3                                                             
         L     R6,ZIP                                                           
         LR    RF,R4                                                            
         B     ZIP15                                                            
         SPACE                                                                  
ZIP10    LA    R6,1(R6)                                                         
ZIP12    LA    RF,1(RF)                                                         
ZIP15    CLI   1(RF),C'-'          FOR 9 DIGIT ZIP CODE.                        
         BE    ZIP12                                                            
         MVC   0(1,R6),1(RF)                                                    
         BCT   R5,ZIP10                                                         
         SPACE                                                                  
*        BCTR  R5,0                                                             
*        EX    R5,*+8                                                           
*        B     *+10                                                             
*        MVC   0(0,R6),1(R4)                                                    
         SPACE                                                                  
         B     STATE1                                                           
         SPACE 1                                                                
STATE0   BCTR  R4,0                                                             
STATE1   CLI   0(R4),C'A'                                                       
         BL    STATE0                                                           
         CLI   0(R4),C'Z'                                                       
         BH    STATE0                                                           
         L     R5,STATE                                                         
         MVC   1(1,R5),0(R4)                                                    
STATE2   BCTR  R4,0                                                             
         CLI   0(R4),C'A'                                                       
         BL    STATE2                                                           
         CLI   0(R4),C'Z'                                                       
         BH    STATE2                                                           
         MVC   0(1,R5),0(R4)                                                    
         SPACE 1                                                                
STATE3   LA    R1,STATETBL                                                      
         L     R2,ZIP                                                           
STATE4   CLC   3(3,R1),0(R2)                                                    
         BNL   STATE5                                                           
         LA    R1,10(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BE    ERR1                                                             
         B     STATE4                                                           
STATE5   CLC   0(3,R1),0(R2)                                                    
         BH    ERR1                                                             
         CLC   0(2,R5),6(R1)                                                    
         BE    STATE6                                                           
         L     R3,PARAM                                                         
         LH    R6,=H'2'                                                         
         ST    R6,0(R3)                                                         
         MVC   0(2,R5),6(R1)                                                    
*                          IF INPUT IS NOT A STATE, THEN DON'T IGNORE           
         LA    R4,2(R4)    THOSE LAST 2 BYTES.                                  
STATE6   B     CITY1                                                            
         SPACE 1                                                                
CITY1    BCTR  R4,0                                                             
         C     R4,SEND                                                          
         BL    XIT                                                              
         CLI   0(R4),C' '                                                       
         BE    CITY1                                                            
         CLI   0(R4),C','                                                       
         BE    CITY1                                                            
         SR    R6,R6                                                            
         L     R5,SEND                                                          
CITY2    BCTR  R4,0                                                             
         LA    R6,1(R6)                                                         
         CR    R4,R5                                                            
         BNL   CITY2                                                            
         CH    R6,CYL                                                           
         BH    ERR1                                                             
         L     R2,CITY                                                          
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     XIT                                                              
         MVC   0(0,R2),1(R4)                                                    
XIT      XIT1                                                                   
         EJECT                                                                  
SEND     DS    A                                                                
CITY     DS    A                                                                
STATE    DS    A                                                                
ZIP      DS    A                                                                
PARAM    DS    A                                                                
CYL      DS    H                                                                
ZPL      DS    H                                                                
BYTE     DS    C                                                                
WORKA    DS    CL50                                                             
SPCES    DC    CL50' '                                                          
         EJECT                                                                  
         LTORG                                                                  
*   FORMAT OF STATETBL                                                          
*    POS 1-3 FIRST 3 ZIP DIGITS FROM                                            
*        4-6 FIRST 3 ZIP DIGITS TO                                              
*        7-8 STATE NAME ABBR (OFFICIAL US POSTAL CODE)                          
*        9-10 STATE CODE                                                        
STATETBL DS    0H                                                               
         DC    CL10'008008VIVI'                                                 
         DC    CL10'006009PRPR'                                                 
         DC    CL10'010027MA25'                                                 
         DC    CL10'028029RI44'                                                 
         DC    CL10'030038NH33'                                                 
         DC    CL10'039049ME23'                                                 
         DC    CL10'050059VT50'                                                 
         DC    CL10'060069CT09'                                                 
         DC    CL10'070089NJ34'                                                 
         DC    CL10'090149NY36'                                                 
         DC    CL10'150196PA42'                                                 
         DC    CL10'197199DE10'                                                 
         DC    CL10'200200DC11'                                                 
         DC    CL10'201201VA51'                                                 
         DC    CL10'202205DC11'                                                 
         DC    CL10'206219MD24'                                                 
         DC    CL10'220246VA51'                                                 
         DC    CL10'247268WV54'                                                 
         DC    CL10'270289NC37'                                                 
         DC    CL10'290299SC45'                                                 
         DC    CL10'300319GA13'                                                 
         DC    CL10'320349FL12'                                                 
         DC    CL10'350369AL01'                                                 
         DC    CL10'370385TN47'                                                 
         DC    CL10'386397MS28'                                                 
         DC    CL10'400427KY21'                                                 
         DC    CL10'430458OH39'                                                 
         DC    CL10'460479IN18'                                                 
         DC    CL10'480499MI26'                                                 
         DC    CL10'500528IA19'                                                 
         DC    CL10'530549WI55'                                                 
         DC    CL10'550567MN27'                                                 
         DC    CL10'570577SD46'                                                 
         DC    CL10'580588ND38'                                                 
         DC    CL10'590599MT30'                                                 
         DC    CL10'600629IL17'                                                 
         DC    CL10'630658MO29'                                                 
         DC    CL10'660679KS20'                                                 
         DC    CL10'680693NE31'                                                 
         DC    CL10'700714LA22'                                                 
         DC    CL10'716729AR05'                                                 
         DC    CL10'730749OK40'                                                 
         DC    CL10'750799TX48'                                                 
         DC    CL10'800816CO08'                                                 
         DC    CL10'820831WY56'                                                 
         DC    CL10'832838ID16'                                                 
         DC    CL10'840847UT49'                                                 
         DC    CL10'850865AZ04'                                                 
         DC    CL10'870884NM35'                                                 
         DC    CL10'890898NV32'                                                 
         DC    CL10'900966CA06'                                                 
         DC    CL10'967968HI15'                                                 
         DC    CL10'970979OR41'                                                 
         DC    CL10'980994WA53'                                                 
         DC    CL10'995999AK02'                                                 
         DC    X'FF'                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'051DDADSCAN  01/21/98'                                      
         END                                                                    
