*          DATA SET EDREQPEEL  AT LEVEL 009 AS OF 10/01/20                      
*CATALP REQPEEL                                                                 
REQPEEL  TITLE '- PEEL REQUEST FILES AND CREATE EODSORT'                        
REQPEEL  CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY UTL                                                              
         ENTRY SSB                                                              
         NBASE 0,*REQPEEL,WORK=AREQWRK                                          
         LARL  R8,LITERALS                                                      
         USING LITERALS,R8         R8=A(LITERALS & GLOBAL VARIABLES)            
         L     R7,AREQEOD                                                       
         USING EODREQD,R7          R7=A(EOD RECORD)                             
         L     R6,AREQINHD                                                      
         USING REQHDRD,R6          R6=A(REQUEST HEADER)                         
         L     RC,VCPRINT                                                       
         USING DPRINT,RC           RC=A(CPRINT)                                 
         MVC   TITLE+2(12),=C'REQUEST PEEL'                                     
*                                                                               
         LHI   R0,SORTKEY+1-SORTD  SET SORT VARIABLES                           
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SORTKDSP,DUB        SET DISPLACEMENT TO SORT KEY                 
         LHI   R0,SORTKEYL                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SORTKLEN,DUB        SET LENGTH OF SORT KEY                       
         LHI   R0,SORTMAXL                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SORTRLEN,DUB        SET MAXIMUM RECORD LENGTH                    
         GOTOR VSORTER,PARA,SORTPAR1,SORTPAR2                                   
         L     R0,AEODSORT         OPEN OUTPUT FILE                             
         OPEN  ((0),(OUTPUT))                                                   
*                                                                               
REQPEEL2 GOTOR GETNXT              READ A PARAMETER CARD                        
*                                                                               
REQPEEL3 CLC   C(6),=C'DDSIO='     DDSIO=XXXXXXXX TO CHANGE DATAMGR             
         BNE   REQPEEL4                                                         
         L     RF,VDDSIO                                                        
         MVC   0(8,RF),C+6                                                      
         B     REQPEEL2            GET NEXT PARAMETER CARD                      
*                                                                               
REQPEEL4 CLC   C(7),=C'DSPACE='    DSPACE=X TO CHANGE DATA SPACE                
         BNE   REQPEEL6                                                         
         L     RF,ASSB                                                          
         MVC   SSODSPAC-SSOOFF(1,RF),C+7                                        
         B     REQPEEL2                                                         
*                                                                               
REQPEEL6 CLC   C(9),=C'STXITER=N'  STXITER=N TO DISABLE STXIT                   
         BE    *+14                                                             
         CLC   C(7),=C'DUMP=OS'    DUMP=OS DOES THE SAME                        
         BNE   REQPEEL8                                                         
         L     RF,VSTXITER                                                      
         MVC   0(2,RF),NOPROUT                                                  
         B     REQPEEL2            GET NEXT PARAMETER CARD                      
*                                                                               
REQPEEL8 GOTOR VDATAMGR,PARA,DMOPEN,DMSYSTEM,DMFILES                            
         GOTOR VSTXITER,PARA,DUMPLIST                                           
         GOTOR VDATCON,PARA,(5,0),(0,TODAY)                                     
         PACK  DUB(2),TODAY+4(3)                                                
         MVC   THISDAY,DUB                                                      
         GOTOR VGETDAY,PARA,(0,TODAY),(0,DUB)                                   
         MVC   THISDOW,0(R1)                                                    
         LLC   R1,THISDOW                                                       
         AHI   R1,-1                                                            
         LHI   R0,X'80'            R0=MONDAY BIT                                
         SRL   R0,0(R1)            SHIFT BIT INTO POSITION                      
         STC   R0,TODAYM           SET TODAY'S MASK BIT ON                      
         B     INPUT02             PROCESS FIRST PARAMETER CARD                 
*                                                                               
REQPEELX L     R0,AEODSORT         CLOSE OUTPUT FILE                            
         CLOSE ((0))                                                            
*                                                                               
REQPEELA CP    DUPECNT,PZERO       PRINT NUMBER OF DUPLICATES                   
         BE    REQPEELB                                                         
         UNPK  P(6),DUPECNT                                                     
         OI    P+5,X'F0'                                                        
         MVC   P+7(18),=C'DUPLICATE REQUESTS'                                   
         GOTOR VPRINTER                                                         
*                                                                               
REQPEELB CP    DUPEKEPT,PZERO      PRINT NUMBER OF DUPLICATES KEPT              
         BE    REQPEELC                                                         
         UNPK  P(6),DUPEKEPT                                                    
         OI    P+5,X'F0'                                                        
         MVC   P+7(23),=C'DUPLICATE REQUESTS KEPT'                              
         GOTOR VPRINTER                                                         
*                                                                               
REQPEELC CP    DUPEDELT,PZERO      PRINT NUMBER OF DUPLICATES DELETED           
         BE    REQPEELD                                                         
         UNPK  P(6),DUPEDELT                                                    
         OI    P+5,X'F0'                                                        
         MVC   P+7(26),=C'DUPLICATE REQUESTS DELETED'                           
         GOTOR VPRINTER                                                         
*                                                                               
REQPEELD XBASE                     EXIT BACK TO MVS                             
*                                                                               
AREQWRK  DC    A(REQWRK)           A(RD CHAIN)                                  
         EJECT                                                                  
***********************************************************************         
*READ AND PROCESS INPUT CONTROL CARDS                                 *         
***********************************************************************         
INPUT    GOTOR GETNXT              GET NEXT SYSIN CARD                          
*                                                                               
INPUT02  LARL  R1,PARAMS           R1=A(PARAMETER TABLE)                        
         SR    RE,RE                                                            
INPUT04  ICM   RE,1,0(R1)          TEST FOR END OF TABLE                        
         BZ    INPUT08             YES-NOT A CONTROL CARD                       
         EX    RE,*+8              MATCH CARD DATA TO TABLE ENTRY               
         BE    INPUT06                                                          
         CLC   C(0),4(R1)                                                       
         AHI   R1,L'PARAMS         BUMP TO NEXT TABLE ENTRY                     
         B     INPUT04                                                          
*                                                                               
INPUT06  SR    RF,RF               PROCESS PARAMETER CARD                       
         ICM   RF,7,1(R1)          RF=A(PROCESS/VALIDATION ROUTINE)             
         LA    R1,C+1(RE)          R1=A(DATA VALUE)                             
         GOTOR (RF),(R1)           CALL PROCESS/VALIDATE ROUTINE                
         B     INPUT                                                            
*                                                                               
INPUT08  CLI   REQSW,REQSCARD      TEST PROCESSING SYSIN REQUESTS               
         BNE   INPUTERR                                                         
         GOTOR MANREQ              PROCESS MANUAL REQUEST CARD(S)               
         B     INPUT               PROCESS NEXT PARAMETER/REQUEST CARD          
*                                                                               
INPUTX   MVI   REQSW,REQSNONE      RESET REQUEST SWITCH                         
         OC    THISRNGE,THISRNGE   PRINT DATE/TIME RANGE FOUND                  
         BZ    INPUTX2                                                          
         ICM   R1,15,THISRCNT                                                   
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RNGTXTCT,DUB                                                     
         MVC   RNGTXTSY,=C'REQUESTS'                                            
         CLI   THISSYS,C' '                                                     
         BE    *+16                                                             
         MVC   RNGTXTSY+3(2),=C'S '                                             
         MVC   RNGTXTSY+5(3),THISSYS   REQS SXX IF SYSTEM IS KNOWN              
         GOTOR VDATCON,PARA,(0,THISRFDT),(10,RNGTXTFD) FIRST DATE               
         GOTOR (RF),(R1),(0,THISRLDT),(10,RNGTXTLD)    LAST DATE                
         MVC   RNGTXTFT,SPACES                                                  
         MVC   RNGTXTLT,SPACES                                                  
         OC    THISRFTM,THISRFTM   SKIP TIMES IF NOT THERE                      
         BZ    INPUTX1                                                          
         UNPK  WORK+0(3),THISRFTM+0(2) PWOS FIRST TIME                          
         MVI   WORK+2,C':'                                                      
         UNPK  WORK+3(3),THISRFTM+1(2)                                          
         MVI   WORK+5,C':'                                                      
         UNPK  WORK+6(3),THISRFTM+2(2)                                          
         MVC   RNGTXTFT,WORK                                                    
         UNPK  WORK+0(3),THISRLTM+0(2) PWOS LAST TIME                           
         MVI   WORK+2,C':'                                                      
         UNPK  WORK+3(3),THISRLTM+1(2)                                          
         MVI   WORK+5,C':'                                                      
         UNPK  WORK+6(3),THISRLTM+2(2)                                          
         MVC   RNGTXTLT,WORK                                                    
INPUTX1  MVC   P(L'RNGTXT),RNGTXT                                               
         GOTOR VPRINTER                                                         
INPUTX2  DS    0H                                                               
*                                                                               
         CLI   TRACE,YES           CLEAR TITLE TRACE IF NECESSARY               
         BNE   INPUT                                                            
         MVC   P,SPACES                                                         
         MVC   TITLE+21(30),SPACES                                              
         ZAP   LINE,MAXLINES                                                    
         B     INPUT                                                            
*                                                                               
INPUTERR MVC   P(19),=C'CARD NOT RECOGNISED'                                    
         GOTOR ERROR                                                            
         B     INPUT                                                            
         EJECT                                                                  
***********************************************************************         
*INPUT ROUTINES - SYSTEM REQUEST FILES                                *         
***********************************************************************         
SYSREQ   GOTOR CLRREQ              CLEAR REQUEST AREA                           
         GOTOR DSKREQ              GET NEXT SYSTEM REQUEST FILE RECORD          
         BNE   INPUTX                                                           
         GOTOR TSTRDF              APPLY REQUEST DATE FILTERS                   
         BNE   SYSREQ                                                           
*                                                                               
         OC    THISDATE,THISDATE   TEST INCLUDE DATE FILTER SET                 
         BNZ   SYSREQ04            YES-MUST BE OKAY                             
         CLC   DATE,TODAY          IF NOTHING SPECIFIED                         
         BE    SYSREQ04            TODAY'S DATE IS OK                           
*                                                                               
         CLI   IINCSW,YES          TEST INPUT VIA INCSW=Y/N CARD                
         BE    SYSREQ04                                                         
         CLI   IINCSW,NO                                                        
         BE    SYSREQ                                                           
*                                                                               
SYSREQ02 CLI   INCSW,YES           ASK OPERATOR FOR INCLUDE OPTION              
         BE    SYSREQ04                                                         
         CLI   INCSW,NO                                                         
         BE    SYSREQ                                                           
         MVC   LMSGOP,MSGOP        SAVE LAST MESSAGE TO OPERATOR                
         MVC   MSGOP,MSGOP1        INCLUDE SYS=... DATE=......                  
         MVC   MSGOPSY,THISSYS                                                  
         MVC   MSGOPDT,DATE                                                     
         GOTOR VLOGIO,PARA,1,(40,MSGOP)                                         
         GOTOR (RF),(R1),0,(1,INCSW)                                            
         B     SYSREQ02                                                         
*                                                                               
SYSREQ04 CLI   INCOPER,C'<'        TEST IF TIME<HHMM INPUT                      
         BNE   SYSREQ06                                                         
         CLC   RQHPHMS,INCHMS                                                   
         BL    SYSREQ08                                                         
         B     SYSREQ                                                           
*                                                                               
SYSREQ06 CLI   INCOPER,C'>'        TEST IF TIME>HHMM INPUT                      
         BNE   SYSREQ08                                                         
         CLC   RQHPHMS,INCHMS                                                   
         BL    SYSREQ                                                           
*                                                                               
SYSREQ08 CLI   INCREQ,C' '         TEST IF REQ=PP,PP,.. INPUT                   
         BE    SYSREQ20                                                         
         L     RF,AREQIO           POINT TO FIRST CARD                          
         AH    RF,THISRQHL                                                      
         L     RE,=A(INCREQL)      POINT TO REQUEST LIST                        
         LLC   R0,INCREQ#          R0=NUM OF REQ LISTS INPUT                    
         MHI   R0,15               R0=MAX NUMBER OF REQUESTS                    
         LTR   R0,R0                                                            
         BZ    SYSREQ20                                                         
*                                                                               
SYSREQ10 CLC   0(2,RE),0(RF)       SEARCH REQUEST LISTS                         
         BE    SYSREQ14                                                         
         LA    RE,3(RE)                                                         
         BCT   R0,SYSREQ10                                                      
*                                                                               
SYSREQ12 CLI   INCREQ,C'='         REQUEST NOT IN LIST                          
         BE    SYSREQ                                                           
         B     SYSREQ20                                                         
*                                                                               
SYSREQ14 CLI   INCREQ,C'='         REQUEST IN LIST                              
         BNE   SYSREQ                                                           
*                                                                               
SYSREQ20 DS    0H                  PUT REQUEST RECORD                           
         GOTOR RNGSET              SAVE FIRST/LAST TIME RANGE                   
         GOTOR PUTREQ,0            PUT REQUEST RECORD                           
         B     SYSREQ              AND GET NEXT INPUT RECORD                    
         EJECT                                                                  
***********************************************************************         
*INPUT ROUTINES - MERGE TAPE AND OTHER DISK FILES                     *         
***********************************************************************         
OTHOPEN  ST    R0,AMERGE1          OPEN MERGE INPUT FILE                        
         OPEN  ((0),(INPUT))                                                    
*                                                                               
OTHREQ   GOTOR CLRREQ              CLEAR REQUEST AREA                           
         L     R1,AMERGE1          GET FIRST/NEXT INPUT RECORD                  
         L     R0,AREQIO                                                        
         GET   (1),(0)                                                          
         L     R1,AMERGE1                                                       
*                                                                               
         TM    36(R1),X'80'        TEST FIXED LENGTH RECORDS                    
         BZ    OTHREQ02                                                         
         MVC   DUB(2),82(R1)       GET RECORD LENGTH                            
         L     R0,AREQIO                                                        
         ST    R0,DUB+4            GET ADDRESS OF RECORD                        
         B     OTHREQ04                                                         
*                                                                               
OTHREQ02 TM    36(R1),X'40'        TEST VARIABLE LENGTH RECORDS                 
         JZ    *+2                                                              
         L     RE,AREQIO                                                        
         MVC   DUB(2),0(RE)                                                     
         AHI   RE,4                                                             
         ST    RE,DUB+4                                                         
         LH    RE,DUB                                                           
         AHI   RE,-4                                                            
         STH   RE,DUB                                                           
*                                                                               
OTHREQ04 SR    RE,RE               TEST NEW OLD STYLE RECORDS                   
         LH    RF,DUB              GET RECORD LENGTH                            
         LHI   R0,L'QDATA                                                       
         DR    RE,R0                                                            
         L     RF,DUB+4                                                         
         LTR   RE,RE               IF IT'S MODULO 80 THEN                       
         BNZ   OTHREQ06                                                         
         MVC   THISRQHL,NEWRQHL    SET NEW STYLE REQUEST HEADER                 
         MVC   RQHHDR,0(RF)                                                     
         B     OTHREQ08                                                         
*                                                                               
OTHREQ06 MVC   THISRQHL,OLDRQHL    WE HAVE AN OLD HEADER                        
         XC    RQHHDR,RQHHDR       CLEAR NEW HEADER                             
         MVC   RQHIUSR,0(RF)       SET OLD STYLE REQUEST HEADER                 
*                                                                               
OTHREQ08 AH    RF,THISRQHL         POINT TO REQUEST CARD                        
         CLC   0(2,RF),SPACES      DROP REQUEST IF NO PROGRAM                   
         BE    OTHREQ                                                           
         SR    R1,R1                                                            
*&&UK*&& IC    R1,RQHFLAG          HIGH-ORDER NIBBLE = NUM EXTRA CARDS          
         SRL   R1,4                                                             
         AHI   R1,1                SO ACTUAL CARD COUNT IS ONE MORE             
         MHI   R1,L'REQINCD1                                                    
         LR    RE,RF                                                            
         LR    RF,R1                                                            
         LA    R0,REQINCD1                                                      
         MVCL  R0,RE               MOVE REQUEST CARD(S) TO BUFFER               
         GOTOR TSTRDF              APPLY REQUEST DATE FILTERS                   
         BNE   OTHREQ              THIS ONE DOESN'T QUALIFY                     
         GOTOR OTHFIX              APPLY ANY LAST TIME FIXES                    
         GOTOR RNGSET              SAVE FIRST/LAST TIME RANGE                   
         GOTOR PUTREQ,0            PUT REQUEST RECORD                           
         B     OTHREQ              AND GET NEXT INPUT RECORD                    
*                                                                               
OTHCLOSE L     R0,AMERGE1          CLOSE THE MERGE FILE                         
         CLOSE ((0))                                                            
         ICM   R0,15,AMERGE2       CHECK IF SECOND FILE TO MERGE                
         BZ    INPUTX                                                           
         XC    AMERGE2,AMERGE2     YES-CLEAR SECOND FILE ADDRESS                
         B     OTHOPEN             GO OPEN AND PROCESS IT                       
         EJECT                                                                  
***********************************************************************         
*INPUT ROUTINES - SPECIAL ACCOUNT REQUESTS AND ODDS FILE REQUESTS     *         
***********************************************************************         
AODREQ   LARL  R2,SPLIST           R2=A(SPECIALS LIST)                          
*                                                                               
AODREQ02 CLI   0(R2),FF            TEST END OF SPECIALS                         
         BE    AODREQ08                                                         
         GOTOR TSTSEN,0(R2)        ARE WE DOING THE RIGHT FILE                  
         BNE   AODREQ06                                                         
*                                                                               
         XC    RQHHDR,RQHHDR                                                    
         MVC   REQORIG,0(R2)                                                    
         MVC   REQDEST,2(R2)                                                    
         MVC   REQINCD1,SPACES                                                  
         MVC   REQINCD1(2),4(R2)                                                
         MVC   REQINCD1+9(3),6(R2)                                              
         MVC   REQINCD1+QSRNMAD(QSRNMAL),=CL12'AUTOMATIC'                       
         CLC   4(2,R2),=C'58'      FOR AUTOMATIC 58'S                           
         BNE   AODREQ04                                                         
         MVI   REQINCD1+59,C'U'    USE UNDISBURSED OPTION                       
         MVI   REQINCD1+60,C'Y'    OPTION 2=Y                                   
         MVC   WORK(6),TODAY                                                    
         MVC   WORK+4(2),=C'01'                                                 
         LHI   RF,-105                                                          
         GOTOR VADDAY,PARA,WORK,WORK+6,(RF)                                     
         MVC   REQINCD1+36(4),WORK+6                                            
*                                                                               
AODREQ04 GOTOR PUTREQ,0            PUT REQUEST RECORD                           
*                                                                               
AODREQ06 AHI   R2,L'SPLIST         BUMP TO NEXT TABLE ENTRY                     
         B     AODREQ02                                                         
*                                                                               
AODREQ08 GOTOR VWORKER,PARA,WKINDEX,AODDBUFF,ODDKEY                             
         CLI   8(R1),0             TEST END OF INDEX                            
         BNE   INPUTX                                                           
         CLI   ODDKTYP,ODDKTYPQ    TEST ODDS FILE                               
         BNE   AODREQ08                                                         
*&&UK*&& CLI   ODDKSYS,ACCLETQ     ACCOUNT SYSTEM                               
*&&UK*&& BNE   AODREQ08                                                         
         CLC   THISDAY,ODDKDAY     ONLY WANT TODAY'S ODDS DATA                  
         BNE   AODREQ08                                                         
         CLC   ODDKPRG(2),=C'54'   DON'T CHECK USER-ID FOR CLEARANCES           
         BE    AODREQ10                                                         
         GOTOR TSTSEN,ODDKUSER     IS THE DDS ID ON THIS SYSTEM                 
         BNE   AODREQ08                                                         
*                                                                               
AODREQ10 GOTOR VWORKER,PARA,WKREAD,AODDBUFF,ODDKEY,ODDIO                        
         CLI   8(R1),0             END OF FILE - GET NEXT INDEX ENTRY           
         BNE   AODREQ08                                                         
         GOTOR TSTSEN,ODDUSER      IS THE ID OF THIS INDEX ON THIS FILE         
         BNE   AODREQ10                                                         
         XC    RQHHDR,RQHHDR                                                    
         MVC   REQORIG,ODDUSER                                                  
         MVC   REQDEST,ODDUSER                                                  
         MVC   REQINCD1,SPACES                                                  
         MVC   REQINCD1(L'ODDKPRG),ODDKPRG                                      
         MVC   REQINCD1+9(L'ODDCOMP),ODDCOMP                                    
*&&UK                                                                           
AODREQ12 MVC   REQINCD1+QSRNMAD(QSRNMAL),=CL12'ACCODDSFILE'                     
         LH    R1,ODDCNT                                                        
         AHI   R1,1                                                             
         STH   R1,ODDCNT                                                        
         GOTOR PUTREQ              PUT & SET QSPLTR VALUE                       
         B     AODREQ10                                                         
*&&                                                                             
*&&US                                                                           
AODREQ12 SR    R1,R1               CLEAR QSPLTR VALUE                           
         CLC   ODDKPRG(2),=C'54'   DIFFERENTIATE SPOT/PRINT CLEARANCES          
         BNE   AODREQ14                                                         
         MVC   REQINCD1+11(L'ODDKSYS),ODDKSYS                                   
         B     AODREQ16                                                         
AODREQ14 MVC   REQINCD1+QSRNMAD(QSRNMAL),=CL12'ACCODDSFILE'                     
         LH    R1,ODDCNT                                                        
         AHI   R1,1                                                             
         STH   R1,ODDCNT           SET QSPLTR TO MAKE SEPARATE JOBS             
AODREQ16 GOTOR PUTREQ              PUT REQUEST RECORD WITH R1=QSPLTR            
         B     AODREQ10            AND GET NEXT INPUT RECORD                    
*&&                                                                             
         EJECT                                                                  
*&&UK                                                                           
***********************************************************************         
*INPUT ROUTINES - CREATE MEDIA ODDS FILE REQUESTS                     *         
***********************************************************************         
MODREQ   GOTOR VWORKER,PARA,WKINDEX,AODDBUFF,ODDKEY                             
         CLI   8(R1),0             TEST END OF INDEX                            
         BNE   INPUTX                                                           
         CLI   ODDKTYP,ODDKTYPQ    TEST ODDS FILE                               
         BNE   MODREQ                                                           
         CLI   ODDKSYS,MEDLETQ     TEST MEDIA SYSTEM                            
         BNE   MODREQ                                                           
         CLC   THISDAY,ODDKDAY     ONLY WANT TODAY'S MEODDS DATA                
         BNE   MODREQ                                                           
         GOTOR TSTSEN,ODDKUSER     TEST USER ON THIS MEDIA SYSTEM               
         BNE   MODREQ                                                           
*                                                                               
MODREQ02 GOTOR VWORKER,PARA,WKREAD,AODDBUFF,ODDKEY,ODDIO                        
         CLI   8(R1),0             END OF FILE - GET NEXT INDEX ENTRY           
         BNE   MODREQ                                                           
         XC    RQHHDR,RQHHDR                                                    
         MVC   REQORIG,ODDUSER                                                  
         MVC   REQINCD1,SPACES                                                  
         MVC   REQINCD1(L'ODDKPRG),ODDKPRG                                      
         MVC   REQINCD1+L'ODDKPRG(L'ODDAGYA),ODDAGYA                            
         GOTOR PUTREQ,0            PUT REQUEST RECORD                           
         B     MODREQ02            AND GET NEXT INPUT RECORD                    
*&&                                                                             
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
*SPECIAL ROUTINE FOR PRINT SYSTEM MERGED REQUESTS                     *         
***********************************************************************         
OTHFIX   NTR1  LABEL=NO                                                         
         CLI   THISLET,PRTLETQ                                                  
         JNE   XIT                                                              
         OC    REQORIG,REQORIG                                                  
         JNZ   XIT                                                              
         XC    RQHHDR,RQHHDR                                                    
*                                                                               
         LA    R2,OTHAGY                                                        
         CLC   THISRQHL,OLDRQHL                                                 
         BE    OTHFIX02                                                         
         GOTOR VDATCON,PARA,TODAY,(19,RQHPJDT)                                  
         B     OTHFIX04                                                         
*                                                                               
OTHFIX02 MVC   REQDATE,TODAY                                                    
*                                                                               
OTHFIX04 CLC   0(2,R2),REQINCD1+2                                               
         BE    OTHFIX06                                                         
         CLI   0(R2),FF                                                         
         BE    OTHFIX06                                                         
         AHI   R2,L'OTHAGY                                                      
         B     OTHFIX02                                                         
*                                                                               
OTHFIX06 MVC   REQORIG,2(R2)                                                    
         J     XIT                                                              
*                                                                               
OTHAGY   DS    0XL4                AGENCY ALPHA/ORIGIN ID                       
         DC    C'CO',AL2(15)                                                    
         DC    C'DF',AL2(11)                                                    
         DC    C'SF',AL2(12)                                                    
         DC    C'TB',AL2(09)                                                    
         DC    C'BD',AL2(34)                                                    
         DC    C'SC',AL2(29)                                                    
         DC    C'MC',AL2(14)                                                    
         DC    X'FFFF',AL2(DDSID#)                                              
*&&                                                                             
         EJECT                                                                  
*&&UK                                                                           
***********************************************************************         
*SPECIAL ROUTINE FOR MEDIA SYSTEM MERGED REQUESTS                     *         
***********************************************************************         
OTHFIX   CLI   THISLET,MEDLETQ     INPUT IS ONE FILE FOR ALL SYSTEMS            
         BNER  RE                                                               
         CLI   THISMRGE,C' '                                                    
         BER   RE                                                               
         MVC   THISSUB,REQLINK+2   TWO CHR SYSTEM IN REQLINK FIELD              
         CLI   THISSUB,C' '                                                     
         BHR   RE                                                               
         MVC   THISSUB,=C'??'                                                   
         BR    RE                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*DEAL WITH RF REQUESTS OR DO REGULAR REQUEST PUT                      *         
***********************************************************************         
PUTREQ   NTR1  LABEL=NO                                                         
*                                                                               
*&&US*&& CLC   REQINCD1(2),=C'RF'  TEST FOR RF REQUEST                          
*&&US*&& JNE   *+12                                                             
*&&US*&& GOTOR RFGREQ              YES-CALL RF HANDLING ROUTINE                 
*&&US*&& B     PUTREQX                                                          
*                                                                               
         GOTOR REQPUT,(R1)         ELSE DO REGULAR REQUEST PUT                  
*                                                                               
PUTREQX  J     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*READ SORTED RECORDS & CREATE END OF DAY RECORDS                      *         
***********************************************************************         
OUTPUT   GOTOR VSORTER,PARA,SORTGET                                             
         ICM   R2,15,4(R1)                                                      
         BZ    REQPEELX                                                         
         USING SORTD,R2            R2=A(SORTED RECORD)                          
TINF     USING QREQINFO,SREQINFO   THIS REQUEST INFO                            
         CLI   TRACESRT,YES        TEST TRACING ACTIVE                          
         BNE   OUTPUT02                                                         
         LH    R0,SRECLEN                                                       
         GOTOR VPRNTBL,PARA,=C'SORTD',SORTD,C'DUMP',(R0),=C'1D'                 
*                                                                               
OUTPUT02 GOTOR CLREOD              CLEAR END OF DAY RECORD                      
*                                                                               
         MVC   QJOBKEY,SJOBKEY     BUILD END OF DAY RECORD                      
         MVC   QINTKEY,SINTKEY                                                  
         CLI   QSYSTEM,ACCLETQ     TEST ACCOUNT REQUEST                         
         BNE   OUTPUT04                                                         
         TM    TINF.QQHFLAG,QQHFLNK+QQHFTWO                                     
         BNO   OUTPUT04                                                         
         MVC   SCARD1+QSRNMAD(QSRNMAL),SREQSTOR                                 
*                                                                               
OUTPUT04 LLC   R1,SCARDS           MOVE JOB INFO AND REQUEST CARDS              
         MHI   R1,L'QDATA          R1=TOTAL LENGTH OF REQUEST CARDS             
         LA    RF,QDATA(R1)        RF=A(JOB INFO)                               
         MVC   0(L'QREQINFO,RF),SREQINFO                                        
         LA    R5,L'QKEY+L'QREQINFO+4(R1)                                       
         LA    RE,EODREQD                                                       
         AHI   RE,-4                                                            
         STCM  R5,3,0(RE)          SET EOD RECORD LENGTH                        
         LA    R0,QDATA            R0=A(FIRST REQUEST CARD)                     
         LA    RE,SCARD1                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE REQUEST CARDS TO EOD RECORD             
*                                                                               
         CLI   TRACESRT,YES                                                     
         BNE   OUTPUT08                                                         
         GOTOR VPRNTBL,PARA,=C'EODREQD',EODREQD,C'DUMP',(R5),=C'1D'             
*                                                                               
OUTPUT08 L     R3,ALASTEOD                                                      
LREQ     USING SORTD,R3            LAST REQUEST                                 
         LA    R4,LREQ.SREQINFO                                                 
LINF     USING QREQINFO,R4         LAST REQUEST INFO                            
         CLC   SCARDS,LREQ.SCARDS  TEST SAME N'CARDS & JOB KEY                  
         BNE   OUTPUT46                                                         
         CLC   SJOBKEY,LREQ.SJOBKEY                                             
         BNE   OUTPUT46                                                         
*                                                                               
         MVI   DUPEFLAG,0          TEST FOR DUPLICATE REQUESTS                  
         LA    RE,LREQ.SCARD1      POINT TO FIRST DATA CARDS                    
         LA    RF,SCARD1                                                        
         SR    R0,R0               INITIALIZE CARD COUNT                        
*                                                                               
OUTPUT10 TM    TINF.QQHFLAG,RQHFTWA                                             
         BO    OUTPUT12                                                         
         CLC   0(L'QDATA,RE),0(RF) COMPARE REGULAR REQUEST                      
         BNE   OUTPUT14                                                         
         B     OUTPUT16                                                         
*                                                                               
OUTPUT12 CLC   0(5,RE),0(RF)       COMPARE REQTWA REQUEST AVOIDING SIN          
         BNE   OUTPUT14                                                         
         CLC   12(L'QDATA-12,RE),12(RF)                                         
         BE    OUTPUT16                                                         
*                                                                               
OUTPUT14 LTR   R0,R0               TEST IF FIRST CARD                           
         BNZ   *+12                                                             
         OI    DUPEFLAG,DUPEFFCQ   SET DIFFERENT FIRST CARD                     
         B     OUTPUT16                                                         
         OI    DUPEFLAG,DUPEFGFQ   SET DIFFERENT > FIRST CARD                   
         B     OUTPUT18                                                         
*                                                                               
OUTPUT16 AHI   RE,L'QDATA          BUMP TO NEXT CARD                            
         AHI   RF,L'QDATA                                                       
         AHI   R0,1                BUMP CARD NUMBER                             
         CLM   R0,1,SCARDS         TEST ALL CARDS PROCESSED                     
         BNE   OUTPUT10                                                         
*                                                                               
OUTPUT18 CLI   DUPEFLAG,0          TEST IF DIFFERENT REQUEST CARD DATA          
         BNE   OUTPUT22            YES                                          
*                                                                               
         CLI   TINF.QQHSECF,QQHFOSN                                             
         BL    OUTPUT19                                                         
         CLI   TINF.QQHSECF,QQHFPID                                             
         BH    OUTPUT19                                                         
         CLC   TINF.QQHSECI(6),LINF.QQHSECI                                     
         BE    OUTPUT36            DUPLICATE IF SAME SECURITY DATA              
*                                                                               
         CLI   TRACE,YES           TRACE REQUESTS WITH SECURITY DATA            
         BNE   OUTPUT46                                                         
         MVC   P,SPACES                                                         
         MVC   P(8),=CL8'QQHSECD='                                              
         MVC   P+08(6),TINF.QQHSECD                                             
         MVC   P+36(80),0(R2)                                                   
         GOTOR VPRINTER                                                         
         B     OUTPUT46            KEEP THIS REQUEST                            
*                                                                               
OUTPUT19 CLI   QSYSTEM,ACCLETQ     SPECIAL ACCOUNT SYSTEM CHECK                 
         BE    OUTPUT20            TO COMPARE LIMIT ACCESS VALUES               
*&&UK*&& CLI   QSYSTEM,MEDLETQ     SPECIAL UK MEDIA SYSTEM CHECK                
*&&UK*&& BE    OUTPUT20            TO COMPARE LIMIT ACCESS VALUES               
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*                                                                               
OUTPUT20 CLC   TINF.QQHACCS,LINF.QQHACCS                                        
         BNE   OUTPUT46            DIFFERENT LIMIT ACCESS - PUT REQUEST         
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*                                                                               
OUTPUT22 CLI   QSYSTEM,ACCLETQ     HERE IF REQUESTS NOT DUPLICATED              
         BE    OUTPUT32                                                         
*&&UK                                                                           
         CLI   QSYSTEM,MEDLETQ     UK MEDIA SYSTEM                              
         BNE   OUTPUT26                                                         
         CLC   SCARD1+QSRNMAD(QSRNMAL),LREQ.SCARD1+QSRNMAD                      
         BE    OUTPUT24            TEST IF SAME VALUE IN QREQSTOR               
         CLI   SCARD1+QSRNMAD,C'*'                                              
         BE    OUTPUT46            PROCESS DIFFERENT VALUE IN QREQSTOR          
         CLI   LREQ.SCARD1+QSRNMAD,C'*'                                         
         BE    OUTPUT46                                                         
*                                                                               
OUTPUT24 CLC   LREQ.SCARD1(QSRNMAD),SCARD1                                      
         BNE   OUTPUT46            TEST IF REST OF FIRST CARD DIFFERENT         
         CLC   LREQ.SCARD1+78(1),SCARD1+78                                      
         BNE   OUTPUT46                                                         
         TM    DUPEFLAG,DUPEFGFQ   TEST SECOND+ CARDS DIFFERENT                 
         BO    OUTPUT46                                                         
         CLC   TINF.QQHACCS,LINF.QQHACCS                                        
         BNE   OUTPUT46            ELSE PROCEED WITH THIS REQUEST               
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*&&                                                                             
OUTPUT26 DS    0H                                                               
*&&US                                                                           
         CLI   QSYSTEM,TALLETQ     IF TALENT THAT'S ENOUGH                      
         BE    OUTPUT46                                                         
         CLI   QSYSTEM,PRTLETQ     PRINT 52 AND 53                              
         BNE   OUTPUT30                                                         
         CLC   SCARD1(2),=C'52'                                                 
         BE    OUTPUT28                                                         
         CLC   SCARD1(2),=C'53'                                                 
         BNE   OUTPUT30                                                         
*                                                                               
OUTPUT28 CLC   LREQ.SCARD1(69),SCARD1                                           
         BNE   OUTPUT46                                                         
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*                                                                               
OUTPUT30 CLI   QSYSTEM,REPLETQ                                                  
         BE    OUTPUT34                                                         
         CLI   NYCSBILL,YES        DONT ALLOW CHILDSPOT 86 IF ANY BILL          
         BNE   *+14                                                             
         CLC   SCARD1(7),=C'K6DFTCO'                                            
         BE    OUTPUT                                                           
         CLC   SCARD1+QSRNOSD(7),=C'PRGTYP='                                    
         BE    OUTPUT46                                                         
         CLC   SCARD1+QSRNOSD(6),=C'GROSS='                                     
         BE    OUTPUT46                                                         
         CLC   LREQ.SCARD1+QSRNOSD(6),=C'GROSS='                                
         BE    OUTPUT46                                                         
         CLC   SCARD1+QSRNOSD(5),=C'SIZE='                                      
         BE    OUTPUT46                                                         
         CLC   LREQ.SCARD1+QSRNOSD(5),=C'SIZE='                                 
         BE    OUTPUT46                                                         
         CLC   SCARD1(2),=C'NS'    DONE FOR NETWORK SPOOL REQUESTS              
         BE    OUTPUT46                                                         
         MVC   WORK(7),SCARD1+QSRNOSD                                           
         MVC   WORK+1(2),SPACES                                                 
         MVC   WORK+4(2),SPACES                                                 
         CLC   WORK(7),=C'+  .   '                                              
         BE    OUTPUT46                                                         
         CLC   WORK(7),=C'-  .   '                                              
         BE    OUTPUT46                                                         
         MVC   WORK(7),LREQ.SCARD1+QSRNOSD                                      
         MVC   WORK+1(2),SPACES                                                 
         MVC   WORK+4(2),SPACES                                                 
         CLC   WORK(7),=C'+  .   '                                              
         BE    OUTPUT46                                                         
         CLC   WORK(7),=C'-  .   '                                              
         BE    OUTPUT46                                                         
*&&                                                                             
         CLC   LREQ.SCARD1(68),SCARD1                                           
         BNE   OUTPUT46                                                         
*                                                                               
         TM    DUPEFLAG,DUPEFGFQ   TEST SECOND+ CARDS DIFFERENT                 
         BO    OUTPUT46                                                         
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*                                                                               
OUTPUT32 CLC   LREQ.SCARD1(QSRNMAD),SCARD1                                      
         BNE   OUTPUT46                                                         
         CLC   LREQ.SCARD1+78(1),SCARD1+78                                      
         BNE   OUTPUT46                                                         
         CLC   LREQ.SCARD2(L'SCARD2*2),SCARD2                                   
         BNE   OUTPUT46                                                         
         B     OUTPUT36            PROCESS DUPLICATE REQUEST                    
*&&US                                                                           
OUTPUT34 CLC   LREQ.SCARD1(4),SCARD1                                            
         BNE   OUTPUT46                                                         
         CLC   LREQ.SCARD1+18(L'SCARD1-18),SCARD1+18                            
         BNE   OUTPUT46                                                         
         TM    DUPEFLAG,DUPEFGFQ   TEST SECOND+ CARDS DIFFERENT                 
         BNZ   OUTPUT46            YES                                          
*&&                                                                             
OUTPUT36 AP    DUPECNT,PONE        BUMP DUPLICATE COUNTER                       
         TM    DUPE,DUPEPRNT                                                    
         BZ    OUTPUT38                                                         
         MVC   P(L'QJOBKEY),SJOBKEY                                             
         UNPK  DUB,DUPECNT         SET JOB KEY AND DUPLICATE COUNTER            
         OI    DUB+7,X'F0'                                                      
         LA    RE,P+L'QJOBKEY                                                   
         MVI   0(RE),C'#'                                                       
         MVC   1(4,RE),DUB+4                                                    
         LA    R5,SCARD1           PRINT REQUEST CARDS                          
         LLC   R0,SCARDS                                                        
         L     RF,VPRINTER                                                      
         MVC   P+L'QJOBKEY+6(L'SCARD1),0(R5)                                    
         BASR  RE,RF                                                            
         AHI   R5,L'SCARD1                                                      
         BCT   R0,*-12                                                          
*                                                                               
OUTPUT38 TM    DUPE,DUPEKEEP       TEST PURGE DUPLICATES (DEFAULT)              
         BZ    OUTPUT44                                                         
         MVC   WORK(1),QSYSTEM     TEST FOR AUTOMATICS                          
         MVC   WORK+1(2),SCARD1                                                 
         LARL  R1,AUTOLST          R1=A(AUTOMATICS LIST)                        
         LHI   R0,AUTOLSTN                                                      
         LTR   R0,R0                                                            
         BZ    OUTPUT42                                                         
OUTPUT40 CLC   0(L'AUTOLST,R1),WORK                                             
         BE    OUTPUT44            DISCARD DUPLICATE AUTOMATICS                 
         CLI   L'AUTOLST-1(R1),C' '                                             
         BNE   *+14                                                             
         CLC   0(L'AUTOLST-1,R1),WORK                                           
         BE    OUTPUT44                                                         
         AHI   R1,L'AUTOLST                                                     
         BCT   R0,OUTPUT40                                                      
OUTPUT42 AP    DUPEKEPT,PONE       BUMP DUPLICATES KEPT COUNTER                 
         B     OUTPUT46                                                         
OUTPUT44 AP    DUPEDELT,PONE       DUMP DUPLICATES DELETED COUNTER              
         B     OUTPUT                                                           
*                                                                               
OUTPUT46 L     R0,ALASTEOD         COPY EOD RECORD TO LAST                      
         LH    R1,SRECLEN                                                       
         LA    RE,SORTD                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         CLI   TRACE,YES           TEST IF TRACE REQUIRED                       
         BNE   OUTPUT50                                                         
         CLC   TITLE+21(6),=C'OUTPUT'                                           
         BE    OUTPUT48                                                         
         MVC   TITLE+21(30),SPACES                                              
         MVC   TITLE+21(6),=C'OUTPUT'                                           
         ZAP   LINE,MAXLINES       FORCE PAGE BREAK                             
*                                                                               
OUTPUT48 MVC   P(L'QJOBKEY),SJOBKEY                                             
         LA    R5,SCARD1           PRINT JOB KEY AND REQUEST CARDS              
         LLC   R0,SCARDS                                                        
         L     RF,VPRINTER                                                      
         MVC   P+L'QJOBKEY+6(L'SCARD1),0(R5)                                    
         BASR  RE,RF                                                            
         AHI   R5,L'SCARD1                                                      
         BCT   R0,*-12                                                          
*                                                                               
OUTPUT50 L     R1,AEODSORT         R1=A(OUTPUT FILE)                            
         LA    R0,EODREQD                                                       
         AHI   R0,-4                                                            
         PUT   (1),(0)             ADD OUTPUT REQUEST                           
         B     OUTPUT                                                           
         DROP  TINF,LREQ,LINF,R2,RB                                             
         EJECT                                                                  
***********************************************************************         
*HANDLE MANUAL CARD REQUESTS FROM SYSIN OR PAN LIBRARY BOOK           *         
***********************************************************************         
MANREQ   NTR1  BASE=*,LABEL=*                                                   
         CLI   THISPAN,YES         TEST READING PAN BOOK                        
         BNE   MANREQ04                                                         
         OC    PANDATE,PANDATE     TEST IF PANDATE OPERATIONAL                  
         BZ    MANREQ04            NO                                           
*                                                                               
         LA    RF,THISDATE         YES DATE MUST MATCH A PANDATE=VALUE          
         OC    THISDATE,THISDATE   USE THISDATE IF SET                          
         BNZ   *+8                                                              
         LA    RF,TODAY            OR TODAY IF NOT FOR COMPARE                  
         LA    RE,PANDATES         RE=A(PAN DATE TABLE)                         
         LHI   R0,PANDATEM         R0=MAXIMUM N'PAN DATE ENTRIES                
MANREQ02 CLC   0(L'PANDATES,RE),0(RF)                                           
         BE    MANREQ04                                                         
         OC    0(L'PANDATES,RE),0(RE)                                           
         BZ    MANREQX                                                          
         AHI   RE,L'PANDATES       BUMP TO NEXT ENTRY                           
         BCT   R0,MANREQ02                                                      
         B     MANREQX                                                          
*                                                                               
MANREQ04 GOTOR CLRREQ              CLEAR REQUEST DATA WORK AREA                 
         XC    RQHHDR,RQHHDR                                                    
         OC    REQOUT,THISOUT      SET OUTPUT=                                  
         BNZ   *+10                                                             
         MVC   REQOUT,SETOUT                                                    
         OC    REQDEST,THISDEST    SET DESTINATION=                             
         BNZ   *+10                                                             
         MVC   REQDEST,SETDEST                                                  
         OC    REQORIG,THISORIG    SET ORIGIN=                                  
         BNZ   *+10                                                             
         OC    REQORIG,SETORIG                                                  
         BNZ   MANREQ06                                                         
*                                                                               
         MVC   P(37),=C'NO ORIGIN SUPPLIED FOR FOLLOWING CARD'                  
         GOTOR ERROR                                                            
         MVC   P(L'C),C                                                         
         GOTOR ERROR                                                            
         LHI   R0,DDSID#                                                        
         STCM  R0,3,REQORIG                                                     
         MVC   P(31),=C'PROCESSING CARD WITH ORIGIN=DDS'                        
         GOTOR ERROR                                                            
*                                                                               
MANREQ06 MVI   CARDVCNT,1          SET CARD COUNT TO 1                          
         MVC   REQINCD1,C          SET CARD REQUEST DATA                        
         XC    THISVALS(THISVALL),THISVALS                                      
*                                                                               
         LA    R2,COLTAB           LOOK UP SYSTEM IN COLUMN TABLE               
         USING COLTABD,R2          R2=A(COLUMN TABLE)                           
         LHI   R0,COLTABN                                                       
MANREQ08 CLC   THISLET,COLTSYS                                                  
         BE    MANREQ12                                                         
         AHI   R2,COLTABL                                                       
         BCT   R0,MANREQ08                                                      
*                                                                               
MANREQ10 CLI   CARDVNUM,0          TEST IF CARDNUM= OVERRIDE                    
         BE    MANREQ20                                                         
         CLC   CARDVCNT,CARDVNUM   TEST IF CARD NUMBER MATCHES COUNT            
         BNL   MANREQ20                                                         
         GOTOR GETNXT              READ NEXT CONT CARD FROM CARDS/PAN           
         BNE   MANREQX             EXIT IF ERROR                                
         LLC   R1,CARDVCNT         BUMP INPUT CARD COUNT                        
         LHI   RF,L'REQINCD1                                                    
         MR    RE,R1                                                            
         LA    RE,REQINCD1(RF)                                                  
         MVC   0(L'REQINCD1,RE),C                                               
         AHI   R1,1                                                             
         STC   R1,CARDVCNT                                                      
         B     MANREQ10            GET NEXT CARD                                
*                                                                               
MANREQ12 CLI   CARDVNUM,0          TEST IF CARDNUM= OVERRIDE                    
         BNE   MANREQ14                                                         
         LLC   RE,COLTCC1D                                                      
         LA    RE,REQINCD1(RE)                                                  
         CLC   COLTCC1V,0(RE)      TEST CONTINUATION FOLLOWS                    
         BNE   MANREQ20            AFTER FIRST CARD                             
         B     MANREQ16                                                         
*                                                                               
MANREQ14 CLC   CARDVCNT,CARDVNUM   TEST IF CARD NUMBER MATCHES COUNT            
         BNL   MANREQ20                                                         
*                                                                               
MANREQ16 GOTOR GETNXT              READ NEXT CONT CARD FROM CARDS/PAN           
         BNE   MANREQX             EXIT IF ERROR                                
*                                                                               
         LLC   R1,CARDVCNT         BUMP INPUT CARD COUNT                        
         LHI   RF,L'REQINCD1                                                    
         MR    RE,R1                                                            
         LA    RE,REQINCD1(RF)                                                  
         MVC   0(L'REQINCD1,RE),C                                               
         AHI   R1,1                                                             
         STC   R1,CARDVCNT                                                      
         CLI   CARDVNUM,0          TEST IF CARDNUM= OVERRIDE                    
         BNE   MANREQ14                                                         
*                                                                               
         LLC   RE,COLTCC2D                                                      
         LA    RE,C(RE)            TEST THIS IS A CONTINUATION CARD             
         CLC   COLTCC2V,0(RE)                                                   
         BE    MANREQ16            YES-GET NEXT                                 
*                                                                               
MANREQ20 CLI   THISPAN,YES         TEST READING PAN BOOK                        
         BNE   MANREQY                                                          
         CLI   SUNDAY,C' '         TEST SUNDAY=Y/N ACTIVE                       
         BNH   MANREQY                                                          
         CLI   THISDOW,7           TEST TODAY IS SUNDAY                         
         BNE   MANREQ22                                                         
         CLI   SUNDAY,YES          SUNDAY=Y PREVIOUSLY INPUT                    
         BE    MANREQY                                                          
         B     MANREQX                                                          
MANREQ22 CLI   SUNDAY,YES          SKIP SUNDAY ONLY REQUESTS                    
         BE    MANREQX                                                          
*                                                                               
MANREQY  GOTOR PUTREQ,0            PUT REQUEST RECORD                           
         XC    CARDVALS,CARDVALS                                                
*                                                                               
MANREQX  J     XIT                                                              
         DROP  R2,RB                                                            
*                                                                               
COLTAB   DS    0XL(COLTABL)        SYSTEM CONTINUATION COLUMNS                  
         DC    AL1(ACCLETQ),AL1(49),C'C',AL1(76),C'C'                           
*&&US*&& DC    AL1(REPLETQ),AL1(79),C'+',AL1(79),C'+'                           
*&&US*&& DC    AL1(SPTLETQ),AL1(30),C'*',AL1(30),C'*'                           
*&&US*&& DC    AL1(PRTLETQ),AL1(60),C'*',AL1(60),C'*'                           
COLTABN  EQU   (*-COLTAB)/L'COLTAB                                              
*                                                                               
COLTABD  DSECT                     COLUMN TABLE LAYOUT                          
COLTSYS  DS    C                   SYSTEM LETTER                                
COLTCC1D DS    AL1                 DISPLACEMENT TO CARD 1 COLUNM                
COLTCC1V DS    C                   CARD 1 CONTINUATION VALUE                    
COLTCC2D DS    AL1                 DISPLACEMENT TO CARD 2-N COLUMN              
COLTCC2V DS    C                   CARD 2-N CONTINUATION VALUE                  
COLTABL  EQU   *-COLTABD                                                        
*                                                                               
REQPEEL  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO GET A RECORD FROM A SYSTEM REQUEST FILE                   *         
***********************************************************************         
DSKREQ   NTR1  BASE=*,LABEL=*                                                   
         L     R4,AREQDTF                                                       
         USING DTFPHD,R4           R4=A(REQUEST FILE DTF)                       
         CLI   THISOPEN,YES        TEST REQUEST FILE OPEN (FIRST TIME)          
         BE    DSKREQ02                                                         
         L     RF,ADUMDTF                                                       
         MVC   DTFPHD(L'REQDTF),0(RF)                                           
         MVC   DTFDD,THISFILE                                                   
         OI    DTFOPEN,X'80'       SET REQUEST FILE READ-ONLY                   
         GOTOR VDADDS,P1,DAOPEN,REQHDRD,0,DTFPHD,0,0                            
         MVI   THISOPEN,YES                                                     
         L     RE,ATRKBUFF                                                      
         XC    0(16,RE),0(RE)      CLEAR TRACK BUFF HDR FOR NEW FILE            
         LHI   RE,1                INITIALIZE DISK ADDRESS                      
         SLL   RE,16                                                            
         STCM  RE,15,P7                                                         
*                                                                               
DSKREQ02 LLC   RF,P7+2                                                          
         AHI   RF,1                BUMP RECORD NUMBER                           
         STC   RF,P7+2                                                          
         GOTOR VDADDS,P1,RDTRK,AREQIO,0,DTFPHD,P7,ATRKBUFF                      
         TM    P3+1,X'08'          TEST RECORD NOT FOUND                        
         BZ    DSKREQ06                                                         
         CLI   P7+2,1              THIS COULD BE END OF FILE                    
         BE    DSKREQN                                                          
*                                                                               
DSKREQ04 LH    RF,P7               NO-SO BUMP TO NEXT TRACK                     
         AHI   RF,1                                                             
         SLL   RF,16                                                            
         ST    RF,P7                                                            
         B     DSKREQ02                                                         
*                                                                               
DSKREQ06 TM    P3+1,X'04'          TEST REAL END OF FILE                        
         BO    DSKREQN                                                          
         OC    P3(2),P3            TEST FOR DISK ERROR                          
         BZ    DSKREQ08                                                         
         MVC   P(40),=C'DISK ERROR XXXX ON FFFFFFF (DA=TTTTBBTT)'               
         GOTOR VHEXOUT,PARA,P3,P+11,2,HEXTOG                                    
         GOTOR (RF),(R1),P7,P+31,4                                              
         MVC   P+19(7),THISFILE                                                 
         GOTOR VLOGIO,PARA,1,(40,P)                                             
         GOTOR VPRINTER                                                         
         MVC   P(28),=C'RUN CONTINUING ON NEXT TRACK'                           
         GOTOR VLOGIO,PARA,1,(28,P)                                             
         GOTOR VPRINTER                                                         
         BASR  RE,RF                                                            
         B     DSKREQ04            GO BACK TO BUMP TO NEXT TRACK                
*                                                                               
DSKREQ08 SR    RE,RE                                                            
         LH    RF,P3+2             GET RECORD LENGTH                            
         LHI   R0,L'QDATA                                                       
         DR    RE,R0                                                            
         MVC   THISRQHL,OLDRQHL                                                 
         LTR   RE,RE               IF ITS MODULO 80 THEN                        
         BNZ   *+10                                                             
         MVC   THISRQHL,NEWRQHL    SET NEW STYLE REQUEST HEADER                 
         L     RE,AREQIO           POINT TO FIRST CARD                          
         AH    RE,THISRQHL                                                      
         CLC   0(2,RE),=C'99'      IGNORE DELETED REQUESTS                      
         BE    DSKREQ02                                                         
         CLI   0(RE),FF            IGNORE END OF CHAINS                         
         BE    DSKREQ02                                                         
*&&US                                                                           
         CLC   0(2,RE),=C'30'      IGNORE CHECK REQUESTS                        
         BNE   DSKREQ10                                                         
         CLI   THISLET,SPTLETQ     FOR SPOT                                     
         BE    DSKREQ02                                                         
         CLI   THISLET,PRTLETQ     AND PRINT                                    
         BE    DSKREQ02                                                         
*&&                                                                             
DSKREQ10 SHI   RE,L'RQHIUSR-(RQHFLAG-RQHIUSR)                                   
         LLC   RF,0(RE)            RF=REQUEST FLAG BYTE                         
         SRL   RF,4                DROP INDICATOR                               
         AHI   RF,1                ACTUAL CARD COUNT IS ONE MORE                
*                                                                               
         L     RE,AREQIO                                                        
         MVC   RQHIUSR,0(RE)       SET OLD REQUEST HEADER                       
         CLC   THISRQHL,NEWRQHL                                                 
         BNE   *+10                                                             
         MVC   RQHHDR,0(RE)        SET NEW REQUEST HEADER                       
         AH    RE,THISRQHL         RE=A(FIRST REQUEST CARD)                     
         MHI   RF,L'QDATA          RF=L'REQUEST CARDS                           
         LA    R0,RQHCARD                                                       
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE REQUEST CARDS TO REQUEST RECORD         
         J     XITY                                                             
*                                                                               
DSKREQN  GOTOR VDADDS,P1,DACLOSE,REQHDRD,0,DTFPHD,0,0                           
         J     XITN                                                             
         DROP  R4,RB                                                            
         EJECT                                                                  
***********************************************************************         
*TEST REQUEST DATE AGAINST REQUEST DATE FILTERS                       *         
***********************************************************************         
TSTRDF   NTR1  LABEL=NO                                                         
         MVC   DATE,TODAY          DEFAULT TO TODAY'S DATE                      
         XC    TIME,TIME           NO TIME YET                                  
         CLC   THISRQHL,OLDRQHL                                                 
         JE    TSTRDF02                                                         
         MVC   TIME,RQHPHMS                                                     
         OC    RQHPJDT,RQHPJDT     TEST FOR JULIAN DATE IN NEW HEADER           
         JZ    TSTRDF04                                                         
         GOTOR VDATCON,PARA,(8,RQHPJDT),(0,DATE)                                
         J     TSTRDF04                                                         
*                                                                               
TSTRDF02 CLI   REQDATE,0           TEST FOR DATE DEFINED IN OLD HEADER          
         JE    TSTRDF04                                                         
         CLI   REQDATE,C' '                                                     
         JE    TSTRDF04                                                         
         MVC   DATE,REQDATE                                                     
         CLI   DATE,C'0'           TEST FOR PACKED DATE                         
         JNL   TSTRDF04                                                         
         GOTOR VDATCON,PARA,(1,REQDATE),(0,DATE)                                
*                                                                               
TSTRDF04 OC    THISNODT,THISNODT   EXCLUDE DATE FILTER                          
         JZ    *+14                                                             
         CLC   THISNODT,DATE                                                    
         JE    XITN                                                             
*                                                                               
         OC    THISDATE,THISDATE   INCLUDE DATE FILTER                          
         JZ    XITY                                                             
         CLC   THISDATE,DATE                                                    
         JNE   XITN                                                             
         J     XITY                                                             
         EJECT                                                                  
***********************************************************************         
*LOG FIRST AND LAST REQUEST DATE AND TIME                             *         
***********************************************************************         
RNGSET   NTR1  LABEL=NO                                                         
         ICM   R1,15,THISRCNT      COUNT REQUESTS                               
         AHI   R1,1                                                             
         STCM  R1,15,THISRCNT                                                   
         OC    THISRFDT,THISRFDT   HAVE WE GOT A FIRST DATE?                    
         JZ    RNGSET20                                                         
         CLC   DATE,THISRFDT       COMPARE DATE TO FIRST                        
         JL    RNGSET20            LOW, NEW FIRST DATE AND TIME                 
         JH    RNGSET30            HIGH, IGNORE                                 
         CLC   TIME,THISRFTM       COMPARE TIME TO FIRST                        
         JL    RNGSET22            LOW, REPLACE TIME                            
         J     RNGSET30            IGNORE                                       
RNGSET20 MVC   THISRFDT,DATE       SET FIRST DATE                               
RNGSET22 MVC   THISRFTM,TIME       SET FIRST TIME                               
RNGSET30 OC    THISRLDT,THISRLDT   HAVE WE GOT A LAST DATE?                     
         JZ    RNGSET40                                                         
         CLC   DATE,THISRLDT       COMPARE DATE TO LAST                         
         JH    RNGSET40            HIGH, NEW FIRST DATE AND TIME                
         JL    XITY                LOW, IGNORE                                  
         CLC   TIME,THISRLTM       COMPARE TIME TO LAST                         
         JH    RNGSET42            HIGH, REPLACE TIME                           
         J     XITY                IGNORE                                       
RNGSET40 MVC   THISRLDT,DATE       SET LAST DATE                                
RNGSET42 MVC   THISRLTM,TIME       SET LAST TIME                                
         J     XITY                                                             
         EJECT                                                                  
***********************************************************************         
*COMPLETE REQUEST HEADER, CALL REQXPLD AND PUT REQUEST(S) TO SORT     *         
***********************************************************************         
REQPUT   NTR1  BASE=*,LABEL=*                                                   
         XC    QJOBKEY(L'QJOBKEY+L'QNONKEY),QJOBKEY                             
         MVC   QSYSTEM(L'QSYSTEM+L'QSUBSYS),THISSYKY                            
         MVC   QOUTTYPE,REQOUT                                                  
         MVC   QORIGIN,REQORIG                                                  
         MVC   QDEST,REQDEST                                                    
         MVC   QAGCTRY,RQHAGCTY                                                 
         MVC   QCTRY,RQHCTRY                                                    
         MVC   QLANG,RQHLANG                                                    
         STCM  R1,1,QSPLTR         SET QSPLTR VALUE                             
         CHI   R1,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  R1,2,QSUB2                                                       
         MVC   QQHISYS,RQHISYS     SAVE INFO FROM REQUEST RECORD HEADER         
         MVC   QQHITRM,RQHITRM                                                  
         MVC   QQHWHY,RQHWHY                                                    
         MVC   QQHAGY,RQHAGY                                                    
         MVC   QQHOFF,RQHOFF                                                    
         MVC   QQHCTRL,RQHCTRL     THIS FIELD IS NOW ACTIVE                     
         MVC   QQHFLAG,RQHFLAG                                                  
         XC    QQHOSID(6),QQHOSID                                               
         CLI   RQHFLG1,QQHFOSN     TEST IF OVERNIGHT SOON INFO DEFINED          
         BNE   REQPUT00                                                         
         MVC   QQHSECF,RQHFLG1     SET FIELD CONTAINS O/N SOON ID               
         MVC   QQHOSID(5),RQHOSID                                               
         B     REQPUT01                                                         
*                                                                               
REQPUT00 CLI   RQHFLG1,QQHFPIN     TEST DATA IS PIN                             
         BE    *+12                                                             
         CLI   RQHFLG1,QQHFPID     OR PID                                       
         BNE   REQPUT01                                                         
         MVC   QQHSECF,RQHFLG1     SET FIELD CONTAINS PIN/PID                   
         MVC   QQHSECD(4),RQHSECD                                               
*                                                                               
REQPUT01 LA    R0,QDATA            MOVE EOD REQUEST CARD DATA                   
         LA    RE,REQINCD1                                                      
         LHI   R1,REQINCDL                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         CLI   QSYSTEM,ACCLETQ     LEAVE COL 80 ALONE FOR ACCOUNTING            
         BNE   *+14                                                             
         MVC   QUSER,REQOFFC       FOR ACC 1ST 2 OF OFFICE IS ALPHA ID          
         B     REQPUT02                                                         
*                                                                               
         OI    QDATA+79,X'40'      X'40' IN LAST BYTE = NON-CARD                
         CLI   REQSW,REQSCARD                                                   
         BNE   *+8                                                              
         NI    QDATA+79,X'BF'                                                   
         CLI   REQOFFC+3,C'*'                                                   
         BNE   *+8                                                              
         NI    QDATA+79,X'BF'                                                   
*                                                                               
REQPUT02 MVC   QPROGRAM,QDATA                                                   
         LARL  R5,STACKTAB         INITIALIZE STACK POINTER                     
STACK    USING EODREQD,R5                                                       
*                                                                               
         LA    R0,STACK.EODREQD    CLEAR THE STACK                              
         LHI   R1,STACKNUM*STACKLEN                                             
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R0,STACK.EODREQD    MOVE EOD REQUEST RECORD TO STACK             
         LHI   R1,STACKLEN                                                      
         LA    RE,EODREQD                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         GOTOR CHKBUF,BUFBEF       CHECK BUFFER CONTENTS                        
*                                                                               
         MVC   SVRDEST,QDEST       SAVE INPUT DEST/OUTTYPE                      
         MVC   SVROUTT,QOUTTYPE                                                 
*                                                                               
         OC    AXPLD,AXPLD         TEST XPLD PHASE LOADED                       
         BNZ   REQPUT04                                                         
         GOTOR GETPHS,PARA,(0,REQXPLD),FXPLD                                    
         LM    R0,R1,0(R1)         NO-LOAD IT NOW                               
         MVI   DMPPEELX,0          UPDATE DUMP LIST                             
         ST    R0,DMPXPSTR         START  OF XPLD                               
         AR    R0,R1               LENGTH OF XPLD PHASE                         
         ST    R0,DMPXPEND         END    OF XPLD                               
         OI    DMPXPEND,X'80'      MARK END OF DUMP LIST                        
         GOTOR VSTXITER,PARA,DUMPLIST                                           
*                                                                               
REQPUT04 OC    STACK.QDATA2,STACK.QDATA2                                        
         BNZ   *+10                                                             
         MVC   STACK.QDATA2,SPACES                                              
         GOTOR AXPLD,PARA,STACK.EODREQD,STACKNUM,AUTL,RQHHDR,VDATAMGR, X        
               XPLXTRAS                                                         
         GOTOR CHKBUF,BUFAFT       CHECK BUFFER CONTENTS                        
*                                                                               
REQPUT06 OC    STACK.EODREQD(L'QKEY+L'QDATA),STACK.EODREQD                      
         BZ    REQPUTX                                                          
*&&US                                                                           
         CLC   STACK.QDATA(7),=C'04DFTCO'                                       
         BNE   *+8                                                              
         MVI   NYCSBILL,YES        SET BILLING FOR CHILDSPOT                    
         MVC   STACK.QPROGRAM,STACK.QDATA                                       
                                                                                
         CLI   REQSW,REQSRLPG      PROCESSING RLP                               
         BE    *+12                YES TRACE IS UNCONDITIONAL                   
*&&                                                                             
         CLI   TRACE,YES                                                        
         BNE   REQPUT14                                                         
         CLI   REQSW,REQSCARD      WE HAVE ALREADY PRINTED CARDS                
         BE    REQPUT14                                                         
         CLI   TRACEHEX,YES                                                     
         BNE   REQPUT08                                                         
         MVC   P,SPACES                                                         
         GOTOR VPRINTER                                                         
*                                                                               
REQPUT08 MVC   P(L'QJOBKEY),STACK.QJOBKEY                                       
         MVC   P+L'QJOBKEY+6(L'QDATA),STACK.QDATA                               
*                                                                               
         USING GRPKEYD,RLPGKEY                                                  
         CLI   REQSW,REQSRLPG      IS THIS FROM AN RLP GROUP RECORD?            
         BNE   REQPUT10                                                         
         EDIT  GRPKUSER,(5,P+119),ALIGN=LEFT                                    
         LA    R1,P+119            PRINT USERID/GROUP CODE                      
         AR    R1,R0                                                            
         MVI   0(R1),C'/'                                                       
         MVC   1(L'GRPKGRP,R1),GRPKGRP                                          
*                                                                               
REQPUT10 GOTOR VPRINTER                                                         
         OC    STACK.QDATA+(L'QDATA*1),STACK.QDATA+(L'QDATA*1)                  
         BZ    REQPUT12                                                         
         MVC   P+L'QJOBKEY+6(L'QDATA),STACK.QDATA+(L'QDATA*1)                   
         GOTOR VPRINTER                                                         
         OC    STACK.QDATA+(L'QDATA*2),STACK.QDATA+(L'QDATA*2)                  
         BZ    REQPUT12                                                         
         MVC   P+L'QJOBKEY+6(L'QDATA),STACK.QDATA+(L'QDATA*2)                   
         GOTOR VPRINTER                                                         
*                                                                               
REQPUT12 CLI   TRACEHEX,YES                                                     
         BNE   REQPUT14                                                         
         MVC   WORK(L'QJOBKEY),STACK.QJOBKEY                                    
         MVC   WORK+L'QJOBKEY(L'QREQINFO),QREQINFO                              
         GOTOR VHEXOUT,PARA,WORK,AHEXWORK,L'HEXWORK,HEXSEP                      
         L     R2,AHEXWORK                                                      
         MVC   P(L'QJOBKEY),0(R2)  HEX EOD JOB HEADER                           
         MVC   P+L'QJOBKEY+6(L'QREQINFO),L'QJOBKEY(R2)                          
         GOTOR VPRINTER                                                         
         AHI   R2,L'HEXWORK                                                     
         MVC   P(L'QJOBKEY),0(R2)  HEX EOD JOB HEADER                           
         MVC   P+L'QJOBKEY+6(L'QREQINFO),L'QJOBKEY(R2)                          
         GOTOR VPRINTER                                                         
*                                                                               
REQPUT14 L     R4,ASORTREC         BUILD SORT RECORD                            
         USING SORTD,R4            R4=A(SORT RECORD)                            
         LA    R0,SORTD            CLEAR SORT RECORD                            
         LHI   R1,SORTMAXL                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   SREQINFO,QREQINFO                                                
         CLC   SVRDEST,STACK.QDEST                                              
         BE    *+8                                                              
         NI    SREQINFO+(QQHFLAG1-QREQINFO),FF-QQHFDEST                         
         CLC   SVROUTT,STACK.QOUTTYPE                                           
         BE    *+8                                                              
         NI    SREQINFO+(QQHFLAG1-QREQINFO),FF-QQHFOUTT                         
*                                                                               
         CLI   STACK.QSYSTEM,ACCLETQ                                            
         BNE   REQPUT16                                                         
         TM    QQHFLAG,QQHFLNK+QQHFTWO                                          
         BNO   REQPUT16                                                         
         MVC   SREQSTOR,STACK.QDATA+QSRNMAD                                     
         XC    STACK.QDATA+QSRNMAD(QSRNMAL),STACK.QDATA+QSRNMAD                 
*                                                                               
REQPUT16 MVC   SJOBKEY,STACK.QJOBKEY                                            
         MVC   SINTKEY,STACK.QINTKEY                                            
*                                                                               
         LA    R1,STACK.QDATA      MOVE REQUEST CARDS TO SORT RECORD            
         LHI   RE,SCARD1-SORTD     RE=INITIAL RECORD LENGTH                     
         LA    RF,SCARD1                                                        
         SR    R2,R2               R2=ACTUAL CARD COUNT                         
         LHI   R0,QDATAMAX         R0=MAXIMUM N'CARDS                           
REQPUT18 OC    0(L'QDATA,R1),0(R1) TEST END OF RECORD DATA                      
         BZ    REQPUT20                                                         
         CLC   0(L'QDATA,R1),SPACES                                             
         BE    REQPUT20                                                         
         MVC   0(L'QDATA,RF),0(R1) MOVE CARD DATA FROM STACK                    
         AHI   RE,L'QDATA                                                       
         AHI   RF,L'QDATA                                                       
         AHI   R1,L'QDATA                                                       
         AHI   R2,1                                                             
         BCT   R0,REQPUT18                                                      
*                                                                               
REQPUT20 LTR   R2,R2               TEST ANY CARDS IN REQUEST                    
         BZ    REQPUTX                                                          
         STC   R2,SCARDS           SET ACTUAL NUMBER OF REQUEST CARDS           
*                                                                               
REQPUT22 CHI   RE,SORTMINL         INCREASE LENGTH TO MINIMUM REQUIRED          
         BNL   *+8                                                              
         LHI   RE,SORTMINL                                                      
         STH   RE,SRECLEN          SET SORT RECORD LENGTH                       
         GOTOR VSORTER,PARA,SORTPUT,SORTD                                       
         AHI   R5,STACKLEN         PROCESS NEXT STACK ENTRY                     
         B     REQPUT06                                                         
*                                                                               
REQPUTX  GOTOR CLREOD              CLEAR EOD RECORD                             
         GOTOR CLRREQ              CLEAR REQUEST BUFFERS                        
         J     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO PRINT OUT BUFFER TO TRAP 'BLANK CARD' REQUESTS            *         
***********************************************************************         
CHKBUF   NTR1  LABEL=NO                                                         
         MVC   P,SPACES                                                         
         MVC   P(9),0(R1)                                                       
         OC    STACK.QDATA+(L'QDATA*1),STACK.QDATA+(L'QDATA*1)                  
         BZ    *+14                                                             
         CLC   STACK.QDATA+(L'QDATA*1),SPACES                                   
         BNE   CHKBUFX                                                          
         OC    STACK.QDATA+(L'QDATA*2),STACK.QDATA+(L'QDATA*2)                  
         BZ    CHKBUFX                                                          
         B     CHKBUF02                                                         
*                                                                               
CHKBUF02 GOTOR VPRINTER                                                         
         MVC   P(22),=C'<<<DATA IN 3RD CARD>>>'                                 
         GOTOR (RF)                                                             
*                                                                               
CHKBUF04 MVC   P(L'QKEY),STACK.QKEY                                             
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),STACK.QDATA                                          
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),STACK.QDATA+(L'QDATA*1)                              
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),STACK.QDATA+(L'QDATA*2)                              
         GOTOR (RF)                                                             
         MVC   P(21),=C'<<<ORIGINAL BUFFER>>>'                                  
         GOTOR (RF)                                                             
         MVC   P(L'QKEY),QKEY                                                   
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),QDATA                                                
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),QDATA+(L'QDATA*1)                                    
         GOTOR (RF)                                                             
         MVC   P(L'QDATA1),QDATA+(L'QDATA*2)                                    
         GOTOR (RF)                                                             
         MVC   P,SPACES                                                         
*                                                                               
CHKBUFX  J     XIT                                                              
         DROP  STACK,RB                                                         
         EJECT                                                                  
***********************************************************************         
*RLP GROUP PROCESSING                                                 *         
***********************************************************************         
RLPREQ   NTR1  BASE=*,LABEL=*                                                   
         GOTOR INIRLP              INITIALIZE RFPIO                             
         USING RFPIOD,R3           R3=A(RFPIO CONTROL BLOCK)                    
         CLI   RFPERROR,RFPNOSYS   SYSTEM KNOWN?                                
         BE    RLPREQX             NO                                           
*                                                                               
         OC    RLPRUN,RLPRUN       TEST RUN DATES ARE RESOLVED                  
         BNZ   *+8                                                              
         GOTOR INIRLD              NO-INITIALIZE THEM                           
*                                                                               
         L     R2,AACCESS          R2=A(AGENCY ALPHA BUFFER)                    
         MVI   0(R2),ACCESSXQ      SET NO ENTRIES IN BUFFER                     
*                                                                               
         L     R4,AIOAREA          BUILD AGENCY ACCESS TABLE                    
         USING CT5REC,R4           R4=A(ACCESS RECORD)                          
         XC    CT5KEY,CT5KEY                                                    
         MVI   CT5KTYP,CT5KTYPQ    ACCESS RECORD TYPE                           
         OC    RLPAGYF,RLPAGYF     TEST AGENCY FILTER PRESENT                   
         BZ    *+10                                                             
         MVC   CT5KALPH,RLPAGYF    YES-SET IN ACCESS KEY                        
         GOTOR VDATAMGR,PARA,DMRDHI,CTFILE,CT5REC,CT5REC                        
*                                                                               
RLPREQ02 CLI   8(R1),0             TEST FOR DATAMGR ERRORS                      
         BNE   RLPREQ08                                                         
         CLI   CT5KTYP,CT5KTYPQ    ACCESS RECORD?                               
         BNE   RLPREQ08                                                         
*                                                                               
         LA    R1,CT5DATA          TEST AGENCY ON THIS SYSTEM                   
         USING CTSYSD,R1                                                        
         SR    R0,R0                                                            
RLPREQ04 CLI   CTSYSEL,0           TEST END OF RECORD                           
         BE    RLPREQ06                                                         
         CLI   CTSYSEL,CTSYSELQ    TEST SYSTEM ELEMENT                          
         BNE   *+14                                                             
         CLC   CTSYSSE,THISSENO    TEST SE NUMBER MATCHES                       
         BE    *+14                                                             
         IC    R0,CTSYSLEN                                                      
         AR    R1,R0                                                            
         B     RLPREQ04                                                         
*                                                                               
         MVC   0(L'ACCESSBF,R2),CT5KALPH                                        
         AHI   R2,L'ACCESSBF                                                    
         MVI   0(R2),ACCESSXQ      SET END OF TABLE INDICATOR                   
*                                                                               
RLPREQ06 OC    RLPAGYF,RLPAGYF     TEST AGENCY FILTER SET                       
         BNZ   RLPREQ08                                                         
         GOTOR VDATAMGR,PARA,DMRSEQ,CTFILE,CT5REC,CT5REC                        
         B     RLPREQ02                                                         
         DROP  R1,R4                                                            
*                                                                               
RLPREQ08 L     RF,AACCESS          AGENCY ALPHA BUFFER                          
         B     RLPREQ12                                                         
*                                                                               
RLPREQ10 L     RF,RLPAAGY          BUMP TO NEXT AGENCY                          
         AHI   RF,L'ACCESSBF                                                    
*                                                                               
RLPREQ12 CLI   0(RF),ACCESSXQ      TEST END OF AGENCY BUFFER                    
         BE    RLPREQX             YES-NO MORE REQUESTS FOR SYSTEM              
         ST    RF,RLPAAGY          SET A(AGENCY BUFFER ENTRY)                   
*                                                                               
         XC    GRPKEY,GRPKEY       READ GROUPS FOR THIS SYSTEM/AGENCY           
         MVI   GRPKSYS,GRPKSYSQ                                                 
         MVI   GRPKSTYP,GRPKSTYQ                                                
         MVC   GRPKSYST,RFPFSYS    SET SYSTEM                                   
         MVC   GRPKAGY,0(RF)       SET AGENCY                                   
         MVC   RLPGKEYS,GRPKEY     SAVE THE BUILT PART OF THE KEY               
*                                                                               
RLPREQ14 GOTOR VDATAMGR,PARA,DMRDHI,GENDIR,GRPKEY,GRPKEY                        
         JNE   *+2                                                              
         CLC   RLPGKEYS,GRPKEY     TEST SAME SYSTEM/AGENCY                      
         BNE   RLPREQ10            NO-BUMP TO NEXT AGENCY                       
*                                                                               
         CLC   GRPKELEM,EFFS       IS THIS THE MASTER MINIO RECORD?             
         BE    *+14                                                             
         MVC   GRPKELEM,EFFS       NO-SET KEY & GO GET IT                       
         B     RLPREQ14                                                         
*                                                                               
         MVC   RFPFUID,GRPKUSER    AGENCY USERID                                
         MVC   RFPFAGY,GRPKAGY     ALPHA AGENCY CODE                            
         MVC   RFPFGRP,GRPKGRP     GROUP CODE                                   
         GOTOR GORFIO,RFPVALGP     VALIDATE THE GROUP                           
         JNE   *+2                                                              
         OC    RFPXFILE,RFPXFILE   GROUP ATTACHED TO AN XFILE RECORD?           
         BNZ   RLPREQ22            YES-IGNORE IT                                
         TM    RFPVHSTA,GRPHSRRS   TEST DESKTOP SAVED REQUEST GROUP             
         BNZ   RLPREQ22            YES-IGNORE IT                                
*                                                                               
         CLI   RFPVFREQ,GRPFRDAY   TEST RUNDAY FREQUENCY                        
         JE    RLPREQ17            YES-NO DATE CHECKING                         
         OC    RFPVNXTR,RFPVNXTR   TEST NEXT/START RUN DATE SET                 
         BZ    RLPREQ22                                                         
         LA    R2,RLPTEST          TEST GROUP SHOULD RUN ON ANY DAY             
         USING RLPTESTD,R2         R2=A(RLP DATE TEST TABLE)                    
         LHI   R0,RLPRUNN          R0=MAXIMUM NUMBER OF DATES TO TEST           
RLPREQ16 OC    RLPTJDAT,RLPTJDAT   TEST END OF TABLE                            
         BZ    RLPREQ22                                                         
*                                                                               
RLPREQ17 GOTOR TSTDAT,RLPTESTD     TEST THE DATE OR RUN DAY                     
         BE    *+16                GOOD EXIT IF CAN RUN TODAY                   
         AHI   R2,RLPTESTL         BUMP TO NEXT TABLE ENTRY                     
         BRCT  R0,RLPREQ16         DO FOR NUMBER OF DATES                       
         B     RLPREQ22                                                         
*                                                                               
         MVI   WORK,WHENOVQ        APPLY 'WHEN' FILTER                          
         TM    RFPVHSTA,GRPHSRAM                                                
         BZ    *+8                                                              
         MVI   WORK,WHENAMQ                                                     
         CLC   WHEN,WORK                                                        
         BNE   RLPREQ22                                                         
*                                                                               
         OI    RFPFFLAG,RFPF1STR   START WITH FIRST REQUEST                     
*                                                                               
RLPREQ18 GOTOR GORFIO,RFPRETRQ     RETURN FIRST/NEXT REQUEST                    
         BH    RLPREQ22            ALL REQUESTS PROCESSED                       
         JNE   *+2                                                              
*                                                                               
         LA    R2,RLPTEST          CREATE REQUEST FOR EACH RUN DATE             
         LHI   R0,RLPRUNN          R0=MAXIMUM NUMBER OF DATES                   
RLPREQ20 OC    RLPTJDAT,RLPTJDAT   TEST END OF RUN DATE LIST                    
         BZ    RLPREQ18                                                         
         GOTOR TSTDAT,RLPTESTD     TEST REQUEST CAN RUN THAT DAY                
         BNE   *+8                                                              
         GOTOR RLPPUT              YES-PUT REQUEST                              
         AHI   R2,RLPTESTL         BUMP TO NEXT DATE TABLE ENTRY                
         BCT   R0,RLPREQ20         DO FOR NUMBER OF RUN DATES                   
         B     RLPREQ18            GET NEXT GROUP REQUEST RECORD                
*                                                                               
RLPREQ22 LLC   R1,GRPKGRP+7        BUMP TO NEXT GROUP                           
         AHI   R1,1                                                             
         STC   R1,GRPKGRP+7                                                     
         MVC   GRPKELEM,EFFS                                                    
         B     RLPREQ14                                                         
*                                                                               
RLPREQX  J     XIT                                                              
         DROP  R2,RB                                                            
*                                                                               
RLPAAGY  DC    A(0)                A(AGENCY BUFFER ENTRY)                       
RLPGKEYS DC    XL(GRPKUSER-GRPKEY)'00'                                          
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
*USE RF REQUESTS TO GENERATE RLP GROUP REQUESTS                       *         
***********************************************************************         
RFGREQ   NTR1  BASE=*,LABEL=*                                                   
         MVC   RFGSAVE,REQHDRD     SAVE RF REQUEST - HEADER & 2 CARDS           
         XC    REQHDRD(L'RFGSAVE),REQHDRD                                       
*                                                                               
         GOTOR INIRLP              INITIALIZE RFPIO                             
         CLI   RFPERROR,RFPNOSYS   SYSTEM KNOWN?                                
         BE    RFGREQX             NO                                           
*                                                                               
         MVC   RFPFAGY,RFGSAVE+(RQHAGY-REQHDRD)                                 
         MVC   RFPFUID,RFGSAVE+(RQHORIG-REQHDRD)                                
*                                                                               
         USING SPQD,RFGSAVE+(REQINCD1-REQINHD)                                  
         MVC   RFPFGRP,QRFPGRP     GROUP CODE                                   
         OC    RFPFGRP,SPACES      MAKE UPPERCASE                               
*                                                                               
         GOTOR GORFIO,RFPVALGP     VALIDATE THE GROUP                           
         BNE   RFGREQ26                                                         
         OI    RFPFFLAG,RFPF1STR   START WITH FIRST REQUEST                     
*                                                                               
RFGREQ02 GOTOR GORFIO,RFPRETRQ     GET FIRST/NEXT REQUEST RECORD                
         BH    RFGREQX             ALL REQUESTS PROCESSED                       
         JNE   *+2                                                              
         TM    RFPVSTAT,RFPVSPOF   TEST SPOOF REQUEST                           
         BZ    RFGREQ24            NO                                           
*                                                                               
         LA    R1,RFGSYSTB                                                      
         USING RFGSYSD,R1          R1=A(RF SYSTEM TABLE)                        
         LHI   R0,RFGSYSTN                                                      
RFGREQ04 CLC   RFGSYSTM,SYSLET     FIND ENTRY FOR SYSTEM                        
         BE    RFGREQ06                                                         
         AHI   R1,RFGSYSL          BUMP TO NEXT ENTRY                           
         BCT   R0,RFGREQ04                                                      
         B     RFGREQ26                                                         
*                                                                               
RFGREQ06 MVC   RFGATAB,RFGSAFLD    SET A(RF FIELD TABLE)                        
         DROP  R1                                                               
*                                                                               
         LLC   R0,RFPVNUMC         REPLACE FIELDS IN RLP GROUP REQUEST          
         AHI   R0,-1               R0=NUMBER OF REQUEST CARDS                   
         LA    R2,RFPVREQC         POINT TO START OF RLP REQUEST                
*                                                                               
RFGREQ08 LA    R9,12(R2)           POINT TO FIRST FIELD ON REQUEST CARD         
         LHI   R5,L'QDATA-12       SET COLUMN COUNTER                           
*                                                                               
RFGREQ10 XC    DUB(2),DUB          TEST END OF REQUEST CARD                     
         MVZ   DUB(2),2(R9)                                                     
         CLC   DUB(2),EZEROES                                                   
         BNE   RFGREQ22                                                         
*                                                                               
         PACK  DUB,2(2,R9)         GET FIELD LENGTH                             
         CVB   RA,DUB                                                           
         AHI   RA,-1               DECREMENT FOR EXECUTE                        
*                                                                               
         CLC   0(2,R9),EZEROES     IGNORE FIELD 0 (SCREEN PHASE)                
         BE    RFGREQ20                                                         
*                                                                               
         ICM   R4,7,RFGATAB                                                     
         USING RFGFLDD,R4          R4=A(FIELD TABLE)                            
RFGREQ12 CLI   RFGFLDD,RFGFEOTQ    DONE AT END OF TABLE                         
         BE    RFGREQ20                                                         
         TM    1(R9),X'F0'         TEST FIELD & INDEX VALUE PRESENT             
         BO    RFGREQ14                                                         
         CLC   RFGFFLDI,0(R9)      YES-MATCH FIELD ID NUMBER                    
         BE    RFGREQ18                                                         
         B     RFGREQ16                                                         
*                                                                               
RFGREQ14 CLC   RFGFFLD#,0(R9)      MATCH RELATIVE FIELD NUMBER                  
         BE    RFGREQ18                                                         
*                                                                               
RFGREQ16 AHI   R4,RFGFLDL          BUMP TO NEXT FIELD IN TABLE                  
         B     RFGREQ12                                                         
*                                                                               
RFGREQ18 CHI   RA,3-1              IF FIELD LENGTH IS 3                         
         BNE   *+14                                                             
         CLC   4(3,R9),=C'ALL'     SKIP IF ENTRY IS 'ALL'                       
         BE    RFGREQ20                                                         
         EX    RA,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R9),SPACES      INITIALIZE REQUEST FIELD                     
*                                                                               
         LLC   R1,RFGFDISP         GET DISPLACEMENT OF DATA IN RF REQ           
         LA    R1,SPQD(R1)         POINT TO DATA IN RF REQUEST                  
*                                                                               
         ICM   RF,7,RFGFEDIT       SET/TEST A(EDIT ROUTINE)                     
         BZ    *+6                                                              
         GOTOR (RF)                GO TO IT IF PRESENT                          
*                                                                               
         LR    RE,RA                                                            
         CLM   RE,1,RFGFXLEN       USE SMALLER OF 2 LENGTHS                     
         BNH   *+8                                                              
         ICM   RE,1,RFGFXLEN                                                    
         EX    RE,*+8                                                           
         B     RFGREQ20                                                         
         MVC   4(0,R9),0(R1)       MOVE RF DATA TO RLP GROUP REQUEST            
*                                                                               
RFGREQ20 LA    R9,5(RA,R9)         R9=END OF CURRENT FIELD                      
         CLI   0(R9),C'*'          TEST END OF REQUEST                          
         BE    RFGREQ24                                                         
*                                                                               
         AHI   R9,1                BUMP TO NEXT DATA COLUMN                     
         SR    R5,RA               DECREMENT COLUMN COUNTER                     
         AHI   R5,-6               ADJUST REMAINING LENGTH                      
         CHI   R5,4                NEXT FIELD IN REQUEST                        
         BH    RFGREQ10                                                         
*                                                                               
RFGREQ22 AHI   R2,L'QDATA          POINT TO NEXT CARD                           
         BCT   R0,RFGREQ08                                                      
*                                                                               
RFGREQ24 GOTOR RLPPUT,0            PUT RLP GROUP REQUEST                        
         B     RFGREQ02            AND GET NEXT ONE                             
*                                                                               
RFGREQ26 MVC   P,SPACES                                                         
         MVC   P(13),=C'BAD RFP GROUP'                                          
         MVC   P+17(L'QDATA),RFGSAVE+L'RQHHDR                                   
         GOTOR VPRINTER                                                         
*                                                                               
RFGREQX  J     XIT                                                              
         DROP  R4,RB                                                            
*                                                                               
RFGATAB  DC    AL3(0)              A(RF FIELD TABLE)                            
RFGSAVE  DC    XL(L'QDATA*3)'00'   REQUEST HEADER & 2 REQUEST CARDS             
*                                                                               
RFGSYSD  DSECT                     RF SYSTEM TABLE                              
RFGSYSTM DS    C                   SYSTEM LETTER                                
RFGSAFLD DS    AL3                 A(FIELD TABLE)                               
RFGSYSL  EQU   *-RFGSYSD                                                        
*                                                                               
RFGFLDD  DSECT                     RF FIELD TABLE                               
RFGFEOTQ EQU   FF                  END OF TABLE INDICATOR                       
RFGFFLD# DS    CL2                 FIELD NUMBER                                 
RFGFFLDI DS    X                   FIELD ID                                     
RFGFDISP DS    AL1                 DISPLACEMENT TO REQUEST FIELD                
RFGFXLEN DS    AL1                 EXECUTE LENGTH                               
RFGFEDIT DS    AL3                 A(FIELD EDIT ROUTINE)                        
RFGFLDL  EQU   *-RFGFLDD                                                        
*                                                                               
REQPEEL  CSECT                                                                  
         EJECT                                                                  
RFGSYSTB DS    0XL(RFGSYSL)        RF SYSTEMS SUPPORTED                         
         DC    AL1(SPTLETQ),AL3(RFGSPTFT)                                       
         DC    AL1(NETLETQ),AL3(RFGNETFT)                                       
         DC    AL1(PRTLETQ),AL3(RFGPRTFT)                                       
RFGSYSTN EQU   (*-RFGSYSTB)/L'RFGSYSTB                                          
*                                                                               
RFGSPTFT DS    0XL(RFGFLDL)        SPOT SYSTEM FIELDS                           
*                                                                               
         DC    C'12',AL1(10)       MEDIA                                        
         DC    AL1(SPQMED-SPQD,L'SPQMED-1),AL3(0)                               
*                                                                               
         DC    C'13',AL1(12)       CLIENT                                       
         DC    AL1(SPQCLT-SPQD,L'SPQCLT-1),AL3(0)                               
*                                                                               
         DC    C'14',AL1(14)       PRODUCT                                      
         DC    AL1(SPQPRD-SPQD,L'SPQPRD-1),AL3(0)                               
*                                                                               
         DC    C'15',AL1(16)       ESTIMATE                                     
         DC    AL1(SPQEST-SPQD,L'SPQEST-1),AL3(0)                               
*                                                                               
         DC    C'16',AL1(18)       MARKET                                       
         DC    AL1(SPQMKT-SPQD,L'SPQMKT-1),AL3(0)                               
*                                                                               
         DC    AL1(RFGFEOTQ)       END OF TABLE                                 
*                                                                               
RFGNETFT DS    0XL(RFGFLDL)        NETWORK SYSTEM FIELDS                        
*                                                                               
         DC    C'12',AL1(11)       CLIENT                                       
         DC    AL1(SPQCLT-SPQD,L'SPQCLT-1),AL3(0)                               
*                                                                               
         DC    C'13',AL1(16)       PRODUCT                                      
         DC    AL1(SPQPRD-SPQD,L'SPQPRD-1),AL3(0)                               
*                                                                               
         DC    C'14',AL1(22)       ESTIMATE                                     
         DC    AL1(SPQEST-SPQD,L'SPQEST-1),AL3(0)                               
*                                                                               
         DC    C'15',AL1(25)       NETWORK                                      
         DC    AL1(SPQSTA-SPQD,L'SPQSTA-1),AL3(0)                               
*                                                                               
         DC    AL1(RFGFEOTQ)       END OF TABLE                                 
*                                                                               
RFGPRTFT DS    0XL(RFGFLDL)        PRINT SYSTEM FIELDS                          
*                                                                               
         DC    C'12',AL1(10)       MEDIA                                        
         DC    AL1(PPQMEDIA-PPQRECORD,L'PPQMEDIA-1),AL3(0)                      
*                                                                               
         DC    C'13',AL1(12)       CLIENT                                       
         DC    AL1(PPQCLIENT-PPQRECORD,L'PPQCLIENT-1),AL3(0)                    
*                                                                               
         DC    C'14',AL1(14)       PRODUCT                                      
         DC    AL1(PPQPRODUCT-PPQRECORD,L'PPQPRODUCT-1),AL3(0)                  
*                                                                               
         DC    C'15',AL1(16)       ESTIMATE                                     
         DC    AL1(PPQEST-PPQRECORD,L'PPQEST-1),AL3(0)                          
*                                                                               
         DC    C'16',AL1(18)       PUBLICATION                                  
         DC    AL1(PPQPUB-PPQRECORD,L'PPQPUB-1),AL3(0)                          
*                                                                               
         DC    AL1(RFGFEOTQ)       END OF TABLE                                 
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO PUT AN RLP GROUP REQUEST                                  *         
***********************************************************************         
RLPPUT   NTR1  BASE=*,LABEL=*                                                   
         OC    TODAY19,TODAY19     TEST TODAY'S JULIAN DATES RESOLVED           
         BNZ   RLPPUT02                                                         
         LR    R0,R1                                                            
         GOTOR VDATCON,PARA,TODAY,(19,TODAY19)                                  
         GOTOR (RF),(R1),TODAY,(15,TODAY15)                                     
         LR    R1,R0                                                            
*                                                                               
RLPPUT02 MVC   REQHDRD(L'RQHHDR),RFPVREQH                                       
         MVC   RQHSYS,THISSENO     SET ORIGINATING SE NUMBER                    
         OC    RQHORIG,RQHORIG     TEST ORIGIN PRESENT IN HEADER                
         JNZ   *+10                                                             
         MVC   RQHORIG,RFPFUID     NO-SET FROM RFP GROUP                        
*                                                                               
         MVC   RLPEDATE,TODAY      SET RLP RUN DATES                            
         MVC   RLPJDATE,TODAY15                                                 
         LTR   R1,R1               TEST A(DATE TEST TABLE ENTRY) PASSED         
         BZ    RLPPUT04            NO                                           
         MVC   RLPEDATE,RLPTEDAT-RLPTESTD(R1)                                   
         MVC   RLPJDATE,RLPTJDAT-RLPTESTD(R1)                                   
         ICM   R1,15,RLPJDATE                                                   
         SRL   R1,4                                                             
         STCM  R1,7,RQHPJDT        SET DATE IN REQUEST HEADER                   
         B     RLPPUT06                                                         
*                                                                               
RLPPUT04 MVC   RQHPJDT,TODAY19     SET RUNNING TODAY                            
*                                                                               
RLPPUT06 TM    RQHFLAG1,RQHFDEST   TEST DESTINATION SET FOR REQUEST             
         BNZ   RLPPUT08                                                         
         MVC   RQHDEST,RFPVDEST    NO-GET OVERRIDE FROM GROUP                   
         OC    RQHDEST,RQHDEST                                                  
         BZ    RLPPUT08                                                         
         OI    RQHFLAG1,RQHFDEST                                                
*                                                                               
RLPPUT08 TM    RQHFLAG1,RQHFOUT    TEST OUTPUT TYPE SET FOR REQUEST             
         BNZ   RLPPUT10                                                         
         MVC   RQHOUT,RFPVOTYP     NO-GET OVERRIDE FROM GROUP                   
         OC    RQHOUT,RQHOUT                                                    
         BZ    RLPPUT10                                                         
         OI    RQHFLAG1,RQHFOUT                                                 
*                                                                               
RLPPUT10 CLC   RQHOUT,SPACES       TEST OUTPUT TYPE IS SPACES                   
         BNE   *+14                                                             
         XC    RQHOUT,RQHOUT       YES-SET TO NULLS & TURN OFF FLAG             
         NI    RQHFLAG1,FF-RQHFOUT                                              
         OI    RQHFLAG1,RQHFRFPG   TURN ON RLP GROUP REQUEST                    
         MVC   RQHRFPG,RFPVGRP     AND SET RLP GROUP CODE                       
*                                                                               
         LLC   R2,RFPVNUMC                                                      
         AHI   R2,-1               R2=NUMBER OF REQUEST CARDS                   
         LR    R1,R2               COPY RLP REQUEST CARDS TO BUFFER             
         MHI   R1,L'QDATA                                                       
         LA    R0,RQHCARD                                                       
         LA    RE,RFPVREQC                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R5,RQHCARD          R5=A(FIRST REQUEST CARD)                     
         TM    RFPVSTAT,RFPVRUNR   TEST RUNNER STYLE REQUEST                    
         BNZ   RLPPUT36                                                         
*                                                                               
RLPPUT12 LA    R0,L'RFPVREQC(R5)                                                
         ST    R0,RLPANEXT         SAVE A(START OF NEXT CARD)                   
*                                                                               
RLPPUT14 CLI   0(R5),X'20'         IS THIS AN ESCAPE CHARACTER?                 
         BL    RLPPUT24                                                         
         CLI   0(R5),X'2D'                                                      
         BH    RLPPUT24                                                         
         TM    RFPVSTAT,RFPVSPOF   TEST SPOOF STYLE REQUEST                     
         BZ    *+12                                                             
         TM    3(R5),X'F0'         YES-TEST GOOD ESCAPE SEQUENCE                
         BO    RLPPUT24                                                         
*                                                                               
         LLC   R1,3(R5)                                                         
         AHI   R1,-1               R1=L'FIELD-1                                 
         LA    RE,RLPNOSYM         RE=A(VALUE) IF NO SYMBOL FOUND               
         SR    R0,R0                                                            
         ICM   R0,1,RFPVNUMS       LOOK UP SYMBOL IN TABLE                      
         BZ    RLPPUT18                                                         
         LHI   RF,RFPVSYMX-RFPIOD                                               
         LA    RF,RFPIOD(RF)                                                    
         USING RFPVSYME,RF         RF=A(SYMBOL TABLE)                           
RLPPUT16 CLC   RFPVSYME(3),0(R5)   MATCH SYMBOL TO TABLE                        
         BE    *+16                                                             
         AHI   RF,RFPVSYML         ELSE BUMP TO NEXT TABLE ENTRY                
         BCT   R0,RLPPUT16         DO FOR NUMBER OF SYMBOLS IN TABLE            
         B     RLPPUT20                                                         
         LA    RE,RFPVCVAL         FOUND - POINT RE TO CURRENT VALUE            
         MVC   RLPVRULE,RFPVVRUL   AND SAVE VALIDATION RULE NUMBER              
         DROP  RF                                                               
*                                                                               
RLPPUT18 EX    R1,*+8              REPLACE ESCAPE SEQUENCE WITH VALUE           
         B     *+10                                                             
         MVC   0(0,R5),0(RE)                                                    
*                                                                               
         CLI   RLPVRULE,RRULBDAT   TEST ADJUSTED BILLING DATE SYMBOL            
         BE    *+12                                                             
         CLI   RLPVRULE,RRULTODY   OR TODAY SYMBOL                              
         BNE   RLPPUT20                                                         
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPACES                                                   
         CHI   R1,L'RLPEDATE-1                                                  
         BNH   *+8                                                              
         LHI   R1,L'RLPEDATE-1                                                  
         EX    R1,*+8              YES-REPLACE WITH RUN DATE                    
         B     *+10                                                             
         MVC   0(0,R5),RLPEDATE                                                 
*                                                                               
         CLI   RLPVRULE,RRULBDAT   TEST ADJUSTED BILLING DATE SYMBOL            
         BNE   RLPPUT22                                                         
         SR    RF,RF               YES-TEST IF ADJUSTED FOR TODAY               
         ICM   RF,3,RLPJDATE+2     GET RUN DAY                                  
         SRL   RF,4                SHIFT OFF SIGN                               
         STCM  RF,3,WORK           AND SAVE FOR COMPARE                         
*                                                                               
         LA    RF,RFPBADJS         RF=A(BILLING DATE ADJUSTMENT LIST)           
         LHI   R0,RFPBADJM         R0=MAXIMUM NUMBER OF ENTRIES                 
         BASR  RE,0                                                             
         OC    0(L'RFPBADJS,RF),0(RF)                                           
         BZ    RLPPUT22                                                         
         MVC   WORK+L'RFPBADJS(L'RFPBADJS),0(RF)                                
         NC    WORK+L'RFPBADJS(L'RFPBADJS),=AL2(GRPBACAL)                       
         CLC   WORK(L'RFPBADJS),WORK+L'RFPBADJS                                 
         BE    *+14                                                             
         AHI   RF,L'RFPBADJS                                                    
         BCTR  R0,RE                                                            
         B     RLPPUT22            NOT IN LIST - NO ADJUSTMENT                  
*                                                                               
         MVC   WORK(L'RFPBADJS),0(RF)                                           
         SR    RF,RF               CALCULATE BILLING DATE                       
         ICM   RF,3,WORK                                                        
         SRL   RF,10               RF=NUMBER OF ADJUSTMENT DAYS                 
         LR    R0,R1               SAVE R1                                      
         GOTOR VADDAY,PARA,RLPEDATE,WORK,(RF)                                   
         LR    R1,R0               RESTORE R1                                   
         EX    R1,*+8              MOVE ADJUSTED BILLING DATE VALUE             
         B     RLPPUT22                                                         
         MVC   0(0,R5),WORK                                                     
*                                                                               
RLPPUT20 CLI   0(R5),C'|'          IS THIS A SOFT DATE EXPRESSION?              
         BE    *+8                                                              
         CLI   0(R5),C'!'                                                       
         BNE   RLPPUT22                                                         
         LR    R0,R1               YES-RESOLVE IT                               
         GOTOR DOSOFT,PARA,(RLPVRULE,(R5)),(R0)                                 
         LR    R1,R0                                                            
*                                                                               
RLPPUT22 EX    R1,*+8              DROP REQUEST IF CAN'T RESOLVE DATE           
         BZ    RLPPUT40                                                         
         OC    0(0,R5),0(R5)                                                    
         AR    R5,R1               BUMP TO LAST CHARACTER IN SEQUENCE           
*                                                                               
RLPPUT24 AHI   R5,1                BUMP TO NEXT CHARACTER IN CARD               
         LA    R0,2(R5)            TEST ENOUGH SPACE FOR ANOTHER                
         C     R0,RLPANEXT         ESCAPE SEQUENCE IN THIS CARD                 
         BL    RLPPUT14            YES                                          
         L     R5,RLPANEXT         POINT TO START OF NEXT CARD                  
         BCT   R2,RLPPUT12         DO FOR NUMBER OF REQUEST CARDS               
*                                                                               
         TM    RFPVSTAT,RFPVSPOF   TEST SPOOF REQUEST                           
         BZ    RLPPUT38            NO                                           
         LA    R2,RQHCARD          R2=A(FIRST REQUEST CARD)                     
         LLC   R0,RFPVNUMC                                                      
         AHI   R0,-1               R0=NUMBER OF REQUEST CARDS                   
*                                                                               
RLPPUT26 LA    R5,12(R2)           R5=A(FIRST DATA COLUMN)                      
         LHI   R4,L'QDATA-12       R4=NUMBER OF DATA COLUMNS                    
*                                                                               
RLPPUT28 XC    DUB(2),DUB          TEST END OF REQUEST CARD                     
         MVZ   DUB(2),2(R5)                                                     
         CLC   DUB(2),EZEROES                                                   
         BNE   RLPPUT34                                                         
*                                                                               
         PACK  DUB,2(2,R5)         GET LENGTH OF FIELD                          
         CVB   R1,DUB                                                           
         CHI   R1,5                TEST COULD BE DESTINATION/OUTPUT             
         BNH   RLPPUT32                                                         
         CHI   R1,12                                                            
         BH    RLPPUT32                                                         
         LR    RF,R1                                                            
         AHI   RF,-1                                                            
*                                                                               
         EX    RF,*+8              TEST DESTINATION FIELD                       
         BNE   RLPPUT30                                                         
         CLC   4(0,R5),DSTLIT                                                   
         EX    RF,*+8              SET DESTINATION FIELD                        
         B     *+10                                                             
         MVC   4(0,R5),SPACES                                                   
         OC    RFPVFXID,RFPVFXID   TEST FAX ID PRESENT                          
         BZ    RLPPUT32                                                         
         MVC   4(3,R5),FAXPFX      SET FAX ID PREFIX                            
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         B     RLPPUT32                                                         
         MVC   7(0,R5),RFPVFXID    SET FAX ID                                   
*                                                                               
RLPPUT30 EX    RF,*+8              TEST OUTPUT TYPE FIELD                       
         BNE   RLPPUT32                                                         
         CLC   4(0,R5),OUTLIT                                                   
         EX    RF,*+8              SET OUTPUT TYPE FIELD                        
         B     *+10                                                             
         MVC   4(0,R5),SPACES                                                   
         OC    RFPVFXID,RFPVFXID   TEST FAX ID PRESENT                          
         BZ    RLPPUT32                                                         
         MVC   4(L'FAXOUT,R5),FAXOUT                                            
         MVC   RQHOUT,FAXOUT       SET FAX OUTPUT TYPE                          
         OI    RQHFLAG1,RQHFOUT                                                 
*                                                                               
RLPPUT32 LA    R5,4(R1,R5)         BUMP TO NEXT INPUT FIELD                     
         CLI   0(R5),C'*'          TEST END OF REQUEST                          
         BE    RLPPUT38                                                         
*                                                                               
         AHI   R5,1                BUMP TO NEXT DATA COLUMN                     
         SR    R4,R1               DECREMENT REMAINING COLUMNS                  
         AHI   R4,-5                                                            
         CHI   R4,4                                                             
         BH    RLPPUT28                                                         
*                                                                               
RLPPUT34 AHI   R2,L'QDATA          BUMP TO NEXT INPUT CARD                      
         BCT   R0,RLPPUT26         DO FOR NUMBER OF CARDS                       
         B     RLPPUT38                                                         
*                                                                               
RLPPUT36 CLI   RFPVGRP,GRPKDTFQ    MUST BE A DESKTOP FOLDER GROUP               
         JNE   *+2                                                              
         USING RUNQD,R5            BUILD RUNNER JOB HEADER                      
         MVC   RUNQPASS(RUNQDATA-RUNQPASS),SPACES                               
         LA    R0,RFPVGRP+(GRPKDTFP-GRPKDTF)                                    
         GOTOR VHEXOUT,PARA,RFPVGRP+(GRPKDTFP-GRPKDTF),RUNQPASS,       *        
               L'GRPKDTFP,HEXTOG                                                
         MVC   WORK(L'GRPKDTFI),RFPVGRP+(GRPKDTFI-GRPKDTF)                      
         XC    WORK(L'GRPKDTFI),EFFS                                            
         SR    R0,R0                                                            
         ICM   R0,7,WORK                                                        
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RUNQFLDR,DUB                                                     
         MVC   RUNQSRVR,RFPVSORT                                                
         GOTOR VHEXOUT,PARA,RFPVRQID,RUNQMAPN,L'RFPVRQID,HEXTOG                 
         LLC   R0,RFPVSEQN                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RUNQSEQN,DUB                                                     
         DROP  R5                                                               
*                                                                               
RLPPUT38 SR    R1,R1                                                            
         CLC   RQHPJDT,TODAY19     SET SPLIT VALUE IF NOT RUNNING TODAY         
         BE    *+8                                                              
         IC    R1,RQHPJDT+L'RQHPJDT-1                                           
         GOTOR REQPUT              PUT REQUEST                                  
         B     RLPPUTX                                                          
*                                                                               
RLPPUT40 GOTOR CLREOD              CLEAR EOD RECORD                             
         GOTOR CLRREQ              CLEAR REQUEST BUFFERS                        
*                                                                               
RLPPUTX  J     XIT                                                              
         DROP  RB                                                               
*                                                                               
RLPANEXT DC    A(0)                A(NEXT RLP CARD TO PROCESS)                  
RLPNOSYM DC    C'*RLP*RLP*RLP*RLP*RLP*RLP'                                      
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO INTERFACE TO RFPIO                                        *         
***********************************************************************         
GORFIO   NTR1  LABEL=NO                                                         
         STC   R1,RFPMODE          SET MODE IN RFPIO BLOCK                      
         OC    ARFPIO,ARFPIO       TEST RFPIO LOADED                            
         JNZ   GORFIO02            YES                                          
         GOTOR GETPHS,PARA,('QRFPIO',RFPIO),FRFPIO                              
*                                                                               
GORFIO02 GOTOR ARFPIO,PARA,RFPIOD                                               
         NI    RFPFFLAG,FF-RFPF1STR                                             
         CLI   RFPERROR,RFPNOERR   TEST RFPIO ERRORS                            
         JNE   XITN                                                             
         CLI   RFPMODE,RFPRETRQ    TEST CALLER WANTS REQUESTS                   
         JNE   XITY                                                             
         CLI   RFPVREQD,RFPVRDIS   IS THIS REQUEST DISABLED?                    
         JE    GORFIO02            YES-SKIP IT                                  
         CLI   RFPRET,RFPEOF       TEST END OF ALL REQUESTS                     
         JE    XITH                YES-EXIT WITH CC HIGH                        
         J     XITY                ELSE EXIT WITH CC EQUAL                      
         EJECT                                                                  
***********************************************************************         
*RESOLVE SOFT DATE EXPRESSIONS                                        *         
***********************************************************************         
DOSOFT   NTR1  BASE=*,LABEL=*,WORK=(R9,DOSWORKL)                                
         USING DOSWORKD,R9         R9=A(LOCAL WORKING STORAGE)                  
         MVC   DOSPARM(DOSPARML),0(R1)                                          
*                                                                               
         LA    R2,DVLTAB                                                        
         USING DVLTABD,R2          R2=A(DATE VALIDATION TABLE)                  
         LHI   R0,DVLTABN          LOOK UP VALIDATION RULE IN TABLE             
         BASR  RE,0                                                             
         CLC   DVLTTYPE,DOSRULE    MATCH ON RULE NUMBER                         
         BE    *+14                                                             
         AHI   R2,DVLTABL          BUMP TO NEXT TABLE ENTRY                     
         BCTR  R0,RE                                                            
         B     DOSOFTER                                                         
*                                                                               
         XC    DOSFLDH,DOSFLDH     BUILD DUMMY TWA FIELD FOR SOFDAT             
         MVI   DOSFLDH,L'DOSFLDH+L'DOSFLD                                       
         MVC   DOSFLD,SPACES                                                    
         ICM   RF,7,DOSAINP                                                     
         ICM   RE,15,DOSLINP                                                    
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   DOSFLD(0),0(RF)                                                  
*                                                                               
         MVC   DOSWORK,SPACES                                                   
         CLI   DVLTSOFT,SOFITYM+SOFITYMD                                        
         BE    DOSOFT02                                                         
         GOTOR GOSOFT,DOSP,(DVLTSOFT,DOSFLDH),(DVLTSOFO,DOSWORK),      *        
               (DVLTSOFI,0)                                                     
         BNE   DOSOFTER                                                         
         B     DOSOFT04                                                         
*                                                                               
DOSOFT02 GOTOR GOSOFT,DOSP,('SOFITYM',DOSFLDH),(DVLTSOFO,DOSWORK),     *        
               (DVLTSOFI,0)                                                     
         BE    DOSOFT04                                                         
*                                                                               
         GOTOR GOSOFT,DOSP,('SOFITYMD',DOSFLDH),(DVLTSOFO,DOSWORK),    *        
               (DVLTSOFI,0)                                                     
         BNE   DOSOFTER                                                         
*                                                                               
DOSOFT04 TM    DVLTINDS,DVLTID01   TEST SET DAYS TO 01                          
         BZ    DOSOFT06                                                         
         MVC   DOSWORK+04(2),=C'01'                                             
         MVC   DOSWORK+10(2),=C'01'                                             
*                                                                               
DOSOFT06 TM    DVLTINDS,DVLTIDSP   TEST SET DAYS TO SPACES                      
*&&US*&& BZ    DOSOFT08                                                         
*&&UK*&& BZ    DOSOFT10                                                         
         MVC   DOSWORK+04(2),SPACES                                             
         MVC   DOSWORK+10(2),SPACES                                             
*&&US                                                                           
DOSOFT08 CLI   SYSLET,REPLETQ      TEST REP SYSTEM                              
         BNE   DOSOFT10                                                         
         CLI   DVLTSOFO,SOFOTSD2   YES-TEST EBCDIC OUTPUT TYPE                  
         BNE   DOSOFT10                                                         
         LLC   RE,DOSWORK+0                                                     
         AHI   RE,-10              RE=Y2K ADJUSTED YEAR (START DATE)            
         CLI   DOSWORK,C'9'        TEST Y2K START YEAR                          
         BNH   *+8                                                              
         STC   RE,DOSWORK+0        YES-SET ADJUSTED VALUE                       
         LLC   RE,DOSWORK+6                                                     
         AHI   RE,-10              RE=Y2K ADJUSTED YEAR (END DATE)              
         CLI   DOSWORK+6,C'9'      TEST Y2K END YEAR                            
         BNH   DOSOFT10                                                         
         STC   RE,DOSWORK+6        YES-SET ADJUSTED VALUE                       
*&&                                                                             
DOSOFT10 ICM   R1,7,DOSAINP        MOVE RESOLVED DATE TO OUTPUT AREA            
         ICM   RF,15,DOSLINP                                                    
         LA    RE,DOSWORK                                                       
         TM    DVLTINDS,DVLTIPRT   TEST OUTPUT IS PRINTABLE                     
         BZ    *+8                                                              
         LA    RE,DOSFLD                                                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),SPACES                                                   
         EX    RF,*+8                                                           
         B     DOSOFTX                                                          
         MVC   0(0,R1),0(RE)                                                    
*                                                                               
DOSOFTER ICM   R1,7,DOSAINP        BRANCH HERE ON ERRORS                        
         ICM   RF,15,DOSLINP                                                    
         EX    RF,*+8                                                           
         B     DOSOFTX                                                          
         XC    0(0,R1),0(R1)       SET INPUT FIELD TO BINARY ZEROES             
*                                                                               
DOSOFTX  J     XIT                                                              
         DROP  R2,R9,RB                                                         
*                                                                               
DVLTAB   DS    0XL(DVLTABL)        DATE VALIDATION TABLE                        
*                                                                               
         DC    AL1(RRULYM)                                                      
         DC    AL1(0)                                                           
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIONE+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD1)                                                    
*                                                                               
         DC    AL1(RRULYMD)                                                     
         DC    AL1(0)                                                           
         DC    AL1(SOFITYMD)                                                    
         DC    AL1(SOFIIANY+SOFIIONE+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULDATE)                                                    
         DC    AL1(0)                                                           
         DC    AL1(SOFITYM+SOFITYMD)                                            
         DC    AL1(SOFIIANY+SOFIIONE+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULYMP)                                                     
         DC    AL1(0)                                                           
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIF1O+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD1)                                                    
*                                                                               
         DC    AL1(RRULYMDP)                                                    
         DC    AL1(0)                                                           
         DC    AL1(SOFITYMD)                                                    
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPERD)                                                    
         DC    AL1(DVLTIPRT)                                                    
         DC    AL1(SOFITYM+SOFITYMD)                                            
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPDSP)                                                    
         DC    AL1(DVLTIDSP)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPD01)                                                    
         DC    AL1(DVLTID01)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPVAL)                                                    
         DC    AL1(DVLTIPRT)                                                    
         DC    AL1(SOFITYMD)                                                    
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPV1D)                                                    
         DC    AL1(DVLTIPRT)                                                    
         DC    AL1(SOFITYMD)                                                    
         DC    AL1(SOFIIANY+SOFIIONE+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPV1M)                                                    
         DC    AL1(DVLTIPRT)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIONE+SOFIIRES+SOFIIOUT)                         
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULPVPM)                                                    
         DC    AL1(DVLTIPRT)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULREP1)                                                    
         DC    AL1(DVLTIDSP)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
         DC    AL1(RRULREPQ)                                                    
         DC    AL1(DVLTIDSP)                                                    
         DC    AL1(SOFITYM)                                                     
         DC    AL1(SOFIIANY+SOFIIRES+SOFIIOUT)                                  
         DC    AL1(SOFOTSD2)                                                    
*                                                                               
DVLTABN  EQU   (*-DVLTAB)/L'DVLTAB                                              
*                                                                               
DVLTABD  DSECT                     DATE VALIDATION/DISPLAY TABLE                
*                                                                               
DVLTTYPE DS    XL(L'RFPVVRUL)      VALIDATION RULE NUMBER                       
DVLTEOTQ EQU   0                   END OF TABLE                                 
*                                                                               
DVLTINDS DS    X                   INDICATOR BYTE                               
DVLTID01 EQU   X'80'               SET DAYS TO 01                               
DVLTIPRT EQU   X'40'               OUTPUT IS IN PRINTABLE FORMAT                
DVLTIDSP EQU   X'20'               SET DAYS TO SPACES                           
*                                                                               
DVLTSOFT DS    XL(L'SOFITYPE)      SOFDAT INPUT TYPE                            
DVLTSOFI DS    XL(L'SOFIINDS)      SOFDAT INPUT INDICATORS                      
DVLTSOFO DS    XL(L'SOFOTYPE)      SOFDAT OUTPUT TYPE                           
*                                                                               
DVLTABL  EQU   *-DVLTABD           LENGTH OF TABLE ENTRY                        
*                                                                               
DOSWORKD DSECT                     DOSOFT S/R LOCAL W/S                         
*                                                                               
DOSPARM  DS    0X                  INPUT PARAMETER LIST                         
DOSRULE  DS    X                   RULE NUMBER                                  
DOSAINP  DS    AL3                 A(INPUT STRING)                              
DOSLINP  DS    AL4                 L'INPUT STRING                               
DOSPARML EQU   *-DOSPARM                                                        
*                                                                               
DOSP     DS    6F                  PARAMETER LIST                               
DOSWORK  DS    CL(L'RFPVCVAL)      WORK AREA                                    
*                                                                               
DOSFLDH  DS    XL8                 DUMMY FIELD HEADER FOR SOFDAT                
DOSFLD   DS    CL(L'RFPVCVAL)      DUMMY FIELD FOR SOFDAT                       
DOSWORKL EQU   *-DOSWORKD                                                       
*                                                                               
REQPEEL  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO INTERFACE TO SOFDAT                                       *         
*                                                                     *         
*NTRY -  R1=A(PARAMETER LIST AS FOLLOWS):-                            *         
*        P1/B0  =SOFDAT INPUT TYPE (SOFITYPE)                         *         
*        P1/B1-3=A(INPUT AREA)                                        *         
*        P2/B0  =SOFDAT OUTPUT TYPE (SOFOTYPE)                        *         
*        P2/B1-3=A(OUTPUT AREA)                                       *         
*        P3/B0  =SOFDAT INPUT INDICATORS (SOFIINDS)                   *         
*EXIT -  CC=SET BY SOFDAT                                             *         
***********************************************************************         
GOSOFT   NTR1  LABEL=NO,WORK=(R9,GOSWORKL)                                      
         USING GOSWORKD,R9         R9=A(LOCAL WORKING STORAGE)                  
         USING SOFDATD,GOSSOFTD                                                 
         XC    SOFDATD(SOFXTNL),SOFDATD                                         
         MVC   SOFITYPE,0(R1)                                                   
         OI    SOFITYPE,SOFITXTN   SET EXTENSION BLOCK PASSED                   
         MVC   SOFAINP+1(L'SOFAINP-1),1(R1)                                     
         MVC   SOFOTYPE,4(R1)                                                   
         MVC   SOFAOUT+1(L'SOFAOUT-1),5(R1)                                     
         MVC   SOFIINDS,8(R1)                                                   
         MVC   SOFTODAY,RLPEDATE   TODAY IS GROUP SUBMIT DATE                   
         MVC   SOFACOM,ACOMFACS                                                 
         MVC   SOFABUFF,ASOFTBUF                                                
         MVC   SOFSYSN,SYSMST      SET MASTER SYSTEM NUMBER                     
         OC    ASOFDAT,ASOFDAT     TEST SOFDAT LOADED                           
         JNZ   GOSOFT04            YES                                          
         GOTOR GETPHS,PARA,('QSOFDAT',SOFDAT),FSOFDAT                           
*                                                                               
GOSOFT04 GOTOR ASOFDAT,SOFDATD     CALL SOFDAT TO RESOLVE DATE(S)               
         J     XIT                 AND RETURN WITH SOFDAT CC INTACT             
         DROP  R9                                                               
*                                                                               
GOSWORKD DSECT                     GOSOFT S/R LOCAL W/S                         
GOSSOFTD DS    XL(SOFXTNL)         SOFDAT CONTROL BLOCK                         
GOSWORKL EQU   *-GOSWORKD                                                       
*                                                                               
REQPEEL  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
*INITIALIZE RFPIO                                                     *         
***********************************************************************         
INIRLP   NTR1  LABEL=NO                                                         
         L     R3,ARFPIOB          R3=A(RFPIOD)                                 
         MVI   RFPINIT,0           SET NOT INITIALIZED                          
         OC    AMINIO,AMINIO       TEST MINIO LOADED                            
         JNZ   INIRLP02                                                         
         GOTOR GETPHS,PARA,('QMINIO',MINIO),FMINIO                              
         L     RF,ACOMFACS                                                      
         MVC   CMINIO-COMFACS(4,RF),AMINIO                                      
*                                                                               
INIRLP02 OI    RFPFLAGS,RFPXSYMS   EXTENDED BLOCK IS PROVIDED                   
         MVC   RFPACOMF,ACOMFACS   SET A(COMFACS)                               
         MVC   RFPAMIN,AMINWRK     AREA FOR MINIO BLOCK AND BUFFERS             
         MVC   RFPAMINR,AMINREC    MINIO RECORD TABLE                           
         LHI   R0,MINIORLN                                                      
         STCM  R0,3,RFPMINRL       LENGTH OF RFPAMINR                           
         XC    RFPFUID,RFPFUID     NO USERID                                    
         XC    RFPFAGY,RFPFAGY     NO AGENCY ALPHA                              
         MVC   RFPFSYS,SYSLET      SET SYSTEM                                   
         GOTOR GORFIO,0                                                         
         JE    INIRLPX                                                          
         CLI   RFPERROR,RFPNOSYS   SYSTEM KNOWN?                                
         JNE   *+2                 NO                                           
INIRLPX  XIT1  REGS=(R3)           EXIT WITH R3 INTACT                          
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO INITIALIZE RLP RUN DATE VALUES                            *         
***********************************************************************         
INIRLD   NTR1  LABEL=NO                                                         
         GOTOR VDATCON,PARA,TODAY,(2,RLPTOD02)                                  
         SR    R0,R0                                                            
         ICM   R0,1,RLPBACK        TEST PROCESSING RLP BACK DAYS                
*&&UK*&& JZ    INIRLD04            NO-BACK DAYS (SUN+SAT RULE NOT UK)           
*&&US                                                                           
         JNZ   INIRLD01                                                         
         CLI   THISDOW,7           NO-TEST TODAY IS SUNDAY                      
         JNE   INIRLD04                                                         
         LHI   R0,1                YES-LOOK FOR SATURDAY GROUPS TOO             
*&&                                                                             
INIRLD01 LHI   R2,-1                                                            
         LA    R4,RLPPD102                                                      
         MVC   WORK(L'TODAY),TODAY                                              
INIRLD02 GOTOR VADDAY,PARA,WORK,WORK+L'TODAY,(R2)                               
         GOTOR VDATCON,PARA,(0,WORK+L'TODAY),(2,0(R4))                          
         MVC   WORK(L'TODAY),WORK+L'TODAY                                       
         AHI   R4,L'RLPPD102                                                    
         BRCT  R0,INIRLD02                                                      
*                                                                               
INIRLD04 LA    R2,RLPRUN           R2=A(LIST OF COMRESSED RUN DATES)            
         LHI   R0,RLPRUNN                                                       
         LA    R4,RLPTEST                                                       
         USING RLPTESTD,R4         R4=A(RLP DATE TEST TABLE)                    
INIRLD06 OC    0(L'RLPRUN,R2),0(R2)                                             
         JZ    INIRLDX                                                          
         GOTOR VDATCON,PARA,(2,(R2)),(15,RLPTJDAT)                              
         GOTOR (RF),(R1),(2,(R2)),(0,RLPTEDAT)                                  
         XC    DUB,DUB                                                          
         MVC   DUB+6(2),RLPTJDAT+2                                              
         CVB   RE,DUB                                                           
         SRDL  RE,3                                                             
         SRL   RF,32-3                                                          
         IC    RF,DAYBITS(RF)                                                   
         STC   RF,RLPTTBIT         SET TEST BIT                                 
         LA    RE,RFPVRNGE(RE)                                                  
         STCM  RE,7,RLPTTADR       SET A(TEST BYTE)                             
         AHI   R2,L'RLPRUN         BUMP TO NEXT INPUT DATE                      
         AHI   R4,RLPTESTL         BUMP TO NEXT TEST TABLE ENTRY                
         BRCT  R0,INIRLD06         DO FOR MAXIMUM NUMBER OF DATES               
*                                                                               
INIRLDX  J     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*TEST IF AN RLP REQUEST SHOULD RUN FOR A GIVEN RUN DATE               *         
***********************************************************************         
TSTDAT   NTR1  LABEL=NO                                                         
         LR    R2,R1                                                            
         USING RLPTESTD,R2         R2=A(RLP DATE TEST ENTRY)                    
         CLI   RFPVFREQ,GRPFRDAY   TEST RUNDAYS FREQUENCY                       
         JE    TSTDAT06            YES-CHECK RUN DAYS MASK                      
         OC    RFPVENDR,RFPVENDR   ANY END DATE PRESENT?                        
         JZ    TSTDAT02            NO-SO DON'T CHECK ARRAY OF BITS              
         IC    RF,RLPTTBIT         RF=TEST BIT                                  
         ICM   R1,7,RLPTTADR       R1=TEST BYTE                                 
         BASR  RE,0                                                             
         EX    RF,8(RE)            TEST GROUP SHOULD RUN TODAY                  
         JZ    XITN                NO                                           
         TM    0(R1),0                                                          
*                                                                               
TSTDAT02 OC    RFPVENDR,RFPVENDR   ANY END DATE PRESENT?                        
         JNZ   TSTDAT04            YES                                          
         CLC   RLPTJDAT,RFPVNXTR   RUN TODAY?                                   
         JE    XITY                YES                                          
         J     XITN                                                             
*                                                                               
TSTDAT04 CLC   RLPTJDAT,RFPVNXTR   TEST WITHIN THE REQUEST PERIOD               
         JL    XITN                                                             
         CLC   RLPTJDAT,RFPVENDR                                                
         JH    XITN                                                             
         J     XITY                                                             
*                                                                               
TSTDAT06 CLI   RFPVDCNV,RFPVDCNY   TEST CONVERTED GROUP                         
         JNE   XITN                                                             
         CLI   RFPVDTYP,RFPVDTDY   TEST LIST OF (RUN) DAYS                      
         JNE   XITN                                                             
         MVC   RLPTDAYS,RFPVDDAY   EXTRACT RUN DAY LIST                         
         NC    RLPTDAYS,TODAYM     AND WITH TODAY'S MASK                        
         JNZ   XITY                TODAY'S MASK BIT IS ON                       
         J     XITN                TODAY'S MASK BIT NOT ON                      
         DROP  R2,R3                                                            
*                                                                               
RLPTESTD DSECT                     RLP TEST BLOCK                               
RLPTJDAT DS    XL4                 FULL JULIAN DATE                             
RLPTEDAT DS    CL6                 EBCDIC DATE                                  
RLPTTBIT DS    X                   TEST BIT IN RFPVRNGE BYTE                    
RLPTTADR DS    AL3                 A(BYTE TO TEST IN RLPVRNGE)                  
RLPTDAYS DS    XL(L'RFPVDDAY)      LIST OF RUN DAYS (X'80'=MON...)              
RLPTESTL EQU   *-RLPTESTD                                                       
*                                                                               
REQPEEL  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO TEST A USER-ID HAS ACCESS TO SE NUMBER                    *         
*NTRY -  R1=A(USER-ID NUMBER)                                         *         
***********************************************************************         
TSTSEN   NTR1  LABEL=NO                                                         
         MVC   THISUSER,0(R1)                                                   
         L     RF,AIOAREA                                                       
         USING CTIREC,RF                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,THISUSER                                                 
         GOTOR VDATAMGR,PARA,DMREAD,CTFILE,CTIREC,CTIREC                        
         JNE   XITN                                                             
*                                                                               
         L     RF,AIOAREA                                                       
         AHI   RF,CTIDATA-CTIREC                                                
         USING CTSYSD,RF                                                        
         SR    RE,RE                                                            
TSTSEN02 CLI   CTSYSEL,0           TEST END OF RECORD                           
         JE    XITN                                                             
         CLI   CTSYSEL,CTSYSELQ    TEST SYSTEM ELEMENT                          
         JNE   *+14                                                             
         CLC   CTSYSSE,THISSENO    MATCH SE NUMBER                              
         JE    XITY                                                             
         IC    RE,CTSYSLEN         BUMP TO NEXT ELEMENT                         
         AR    RF,RE                                                            
         J     TSTSEN02                                                         
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
*PARAMETER CARD VALIDATION ROUTINES                                   *         
***********************************************************************         
PARLOAD  MVC   REQXPLD,0(R1)       LOAD= OR REQXPLD= OR REQXPLOD=XXX...         
         XC    AXPLD,AXPLD                                                      
         BR    RE                                                               
*                                                                               
PARGETI  MVC   GETIDS,0(R1)        GETIDS=XXXXXXXX                              
         XC    AGETIDS,AGETIDS                                                  
         BR    RE                                                               
*                                                                               
PARRNOP  BR    RE                  DUMMY ROUTINE TO NOP PARAMETER CARD          
*                                                                               
PARRFIO  MVC   RFPIO,0(R1)         RFPIO=XXXXXXXX                               
         XC    ARFPIO,ARFPIO                                                    
         BR    RE                                                               
*                                                                               
PARSOFT  MVC   SOFDAT,0(R1)        SOFDAT=XXXXXXXX                              
         XC    ASOFDAT,ASOFDAT                                                  
         BR    RE                                                               
*                                                                               
PARMINI  MVC   MINIO,0(R1)         MINIO=XXXXXXXX                               
         XC    AMINIO,AMINIO                                                    
         BR    RE                                                               
*                                                                               
PARRLPG  ST    RE,SAVERE           RFPG=SSSAA/RLPG=SSSAA                        
         MVI   REQSW,REQSRLPG                                                   
         MVC   TITLE+21(30),C                                                   
         ZAP   LINE,MAXLINES                                                    
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         XC    RLPAGYF,RLPAGYF                                                  
         CLI   3(R1),C' '          TEST AGENCY FILTER                           
         JE    *+10                                                             
         MVC   RLPAGYF,3(R1)       YES-SET IT                                   
         GOTOR RLPREQ              GET RLP GROUP RECORDS                        
         MVC   TITLE+21(30),SPACES                                              
         ZAP   LINE,MAXLINES                                                    
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARRLPB  CLI   0(R1),C'1'          RLPBACK=1-4/RFPBACK=1-4                      
         JL    PARRLPBN                                                         
         CLI   0(R1),C'4'                                                       
         JH    PARRLPBN                                                         
         MVC   RLPBACK,0(R1)                                                    
         NI    RLPBACK,X'0F'       SET RLP BACK DAYS OPTION                     
         BR    RE                                                               
*                                                                               
PARRLPBN MVC   P(20),C                                                          
         MVC   P+20(39),=C'INVALID RLPBACK VALUE - S/B 1 THROUGH 4'             
         GOTOR ERROR                                                            
         J     INPUT                                                            
*                                                                               
PARWHEN  MVC   WHEN,0(R1)          WHEN=O(VERNIGHT)/A(M)                        
         BR    RE                                                               
*                                                                               
PARREQL  ST    RE,SAVERE           REQL=XXX REQUEST LIST                        
         XC    FULL,FULL                                                        
         CLC   CLAST(4),=C'REQL'                                                
         JNE   PARDISK                                                          
         MVC   FULL(3),THISSYS     LAST SYSTEM SHOULD EQUAL THIS SYSTEM         
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         LA    R1,=CL12'SYS NOT SAME'                                           
         CLC   THISSYS,FULL                                                     
         JNE   PARDISKP                                                         
         LA    R1,=CL12'TOO MANY'                                               
         CLI   INCREQ#,10          CANT HAVE MORE THAN 10 REQL=CARDS            
         JE    PARDISKP                                                         
         J     PARDISK0                                                         
*                                                                               
PARDISK  ST    RE,SAVERE           DISK=XXX OR REQL=XXX                         
         XC    FULL,FULL                                                        
         CLC   CLAST(4),=C'REQL'                                                
         JE    PARDISK0                                                         
         MVI   INCSW,C' '          INIT IF FIRST OF SET OF CARDS                
         MVI   INCOPER,C' '                                                     
         MVI   INCREQ,C' '                                                      
         MVI   INCREQ#,0                                                        
         XC    INCHMS,INCHMS                                                    
         L     RE,=A(INCREQL)      CLEAR REQ LISTS                              
         LHI   R0,10                                                            
         MVC   0(45,RE),SPACES     EACH LIST HAS 15 3-BYTE ENTRIES              
         LA    RE,45(RE)                                                        
         BRCT  R0,*-10                                                          
PARDISK0 CLI   TRACE,YES           OUTPUT TITLE IF TRACE AND DISK=CARD          
         JNE   PARDISK1                                                         
         CLC   C(4),=C'REQL'                                                    
         JE    PARDISK1                                                         
         MVC   TITLE+21(30),C                                                   
         ZAP   LINE,MAXLINES                                                    
*                                                                               
PARDISK1 MVC   FULL(3),THISSYS     SAVE LAST AND GET THIS SYSTEM DATA           
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         CLI   INCREQ#,0           LAST SYSTEM MUST EQUAL THIS                  
         JE    PARDISK2                                                         
         LA    R1,=CL12'SYS NOT SAME'                                           
         CLC   THISSYS,FULL                                                     
         JNE   PARDISKP                                                         
*                                                                               
PARDISK2 LA    RF,C+6              SEARCH FOR TIME OPTION ON DISK= CARD         
         LHI   R0,10                                                            
         BASR  RE,0                                                             
         CLC   0(5,RF),=C' TIME'                                                
         JE    *+14                                                             
         AHI   RF,1                                                             
         BCTR  R0,RE                                                            
         J     PARDISK4                                                         
*                                                                               
         CLI   5(RF),C'<'                                                       
         JE    *+12                                                             
         CLI   5(RF),C'>'                                                       
         JNE   PARDISKN                                                         
*                                                                               
         MVC   INCOPER,5(RF)       SAVE TIME OPERAND < OR >                     
         AHI   RF,6                                                             
         XC    DUB(4),DUB          CHECK HHMM VALID (IGNORE SECONDS)            
         MVZ   DUB(4),0(RF)                                                     
         CLC   DUB(4),EZEROES                                                   
         JNE   PARDISKN                                                         
         CLC   0(2,RF),=C'23'                                                   
         JH    PARDISKN                                                         
         CLC   2(2,RF),=C'59'                                                   
         JH    PARDISKN                                                         
         PACK  DUB(4),0(4,RF)                                                   
         L     RE,DUB                                                           
         SRDL  RE,4                                                             
         SLL   RE,8                                                             
         STCM  RE,7,INCHMS         SAVE PWOS P'HHMM00'                          
*                                                                               
PARDISK4 LA    RF,C+6              SEARCH FOR REQ LIST ON DISK= CARD            
         LHI   R0,30                                                            
         BASR  RE,0                                                             
         CLC   0(4,RF),=C' REQ'                                                 
         JE    *+14                                                             
         AHI   RF,1                                                             
         BCTR  R0,RE                                                            
         J     PARDISK8                                                         
*                                                                               
PARDISK5 MVC   FULL(1),INCREQ      SAVE LAST INCREQ CHR                         
         CLI   4(RF),C'='          REQ=RR,RR.. TO INCLUDE REQUESTS              
         JNE   *+12                                                             
         MVI   INCREQ,C'='                                                      
         J     PARDISK6                                                         
         LA    R1,=CL12'NOT = OR /='                                            
         CLC   4(2,RF),=C'/='                                                   
         JNE   PARDISKP                                                         
         MVI   INCREQ,C'/'         REQ/=RR,RR. TO EXCLUDE REQUESTS              
         LA    RF,1(RF)                                                         
*                                                                               
PARDISK6 L     RE,=A(INCREQL)      RE=A(NEXT LIST OF REQUESTS)                  
         LA    R1,=CL12'NOT SAME ='                                             
         CLI   INCREQ#,0                                                        
         JE    *+14                                                             
         CLC   INCREQ,FULL         MUST HAVE SAME OPERATOR                      
         JNE   PARDISKP                                                         
         LLC   R0,INCREQ#          BUMP COUNT OF CARDS                          
         AHI   R0,1                                                             
         STC   R0,INCREQ#                                                       
         AHI   R0,-1                                                            
         MHI   R0,45               15 3-BYTE REQUESTS PER CARD                  
         AR    RE,R0                                                            
         MVC   0(45,RE),5(RF)      SAVE LIST OF MAX 15 REQUESTS                 
*                                                                               
         LA    R0,C+L'C            MAKE SURE NO '16TH' ENTRY                    
         LA    R1,5+45(RF)         REST OF CARD IS BLANK                        
         BASR  RE,0                                                             
         CR    R1,R0                                                            
         JNL   PARDISK7                                                         
         CLI   0(R1),C' '                                                       
         JNE   *+10                                                             
         LA    R1,1(R1)                                                         
         BR    RE                                                               
         LA    R1,=CL12'> 15 ITEMS'                                             
         J     PARDISKP                                                         
*                                                                               
PARDISK7 CLC   C(4),=C'REQL'       LOOK FOR CONTINUATION IF NOT REQL            
         JE    PARDISK8                                                         
         LA    RF,5+45-3(RF)       1ST BYTE OF 15TH TRIO                        
         LA    R0,15                                                            
         BASR  RE,0                                                             
         CLC   0(3,RF),SPACES      LOOK AT LAST NON-BLANK TRIO                  
         JNE   *+14                                                             
         AHI   RF,-3                                                            
         BCTR  R0,RE                                                            
         J     PARDISK8            SHOULDN'T BE ALL BLANK!                      
         CLI   2(RF),C'+'          IS IT CONTINUATION FLAG?                     
         JNE   PARDISK8            NO-GO PROCESS DISK=                          
         LA    R1,=CL12'TOO MANY'                                               
         CLI   INCREQ#,10          CANT CONTINUE BEYOND 10 CARDS                
         JE    PARDISKP            (1*DISK= + 9*CONTINUATION)                   
         GOTOR GETNXT              READ CONT CARD FROM CARDS/PAN                
         JNE   PARDISKX            EXIT IF ERROR                                
         LA    R1,=CL12'EXPECT CONT.'                                           
         CLC   C(7),SPACES         FIRST 7 MUST BE BLANK                        
         JNE   PARDISKP                                                         
         LA    RF,C+6              SEARCH FOR REQ LIST ON CONT CARD             
         LHI   R0,30               SAME LIMITS AS ORIGINAL                      
         BASR  RE,0                                                             
         CLI   1(RF),C' '          MUST BE NOTHING BEFORE LIST                  
         JNE   *+14                                                             
         AHI   RF,1                                                             
         BCTR  R0,RE                                                            
         J     PARDISKP                                                         
         CLC   0(4,RF),=C' REQ'                                                 
         JE    PARDISK5                                                         
         J     PARDISKP                                                         
*                                                                               
PARDISK8 CLC   C(4),=C'REQL'       EXIT IF REQL=XXX CARD                        
         JE    PARDISKX                                                         
         MVI   THISOPEN,NO         SET REQUEST FILE NOT OPEN                    
         XC    THISRNGE,THISRNGE   CLEAR DATE RANGE FOUND                       
         J     SYSREQ              GO PROCESS INPUT DISK FILE                   
*                                                                               
PARDISKX L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARDISKN MVC   P(20),=CL20'INVALID TIME OPTION'                                 
         GOTOR ERROR                                                            
         J     INPUT                                                            
*                                                                               
PARDISKP MVC   P(20),=CL20'INVALID REQ LIST -'                                  
         MVC   P+20(12),0(R1)                                                   
         GOTOR ERROR                                                            
         J     INPUT                                                            
*                                                                               
PARTRCY  MVC   TITLE+15(5),C       TRACE=YES                                    
         MVI   TRACE,YES                                                        
         BR    RE                                                               
*                                                                               
PARTRCH  MVC   TITLE+15(5),C       TRACE=YESHEX                                 
         MVI   TRACE,YES                                                        
         MVI   TRACEHEX,YES                                                     
         BR    RE                                                               
*                                                                               
PARTRCS  MVI   TRACESRT,YES        TRACE=SORT                                   
         BR    RE                                                               
*                                                                               
PARINCS  MVC   IINCSW,0(R1)        INCSW=Y/N                                    
         BR    RE                                                               
*                                                                               
PARCARD  ST    RE,SAVERE           CARD=XXX                                     
         MVC   THISSYS,0(R1)                                                    
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         XC    THISVALS(THISVALL),THISVALS                                      
         MVI   REQSW,REQSCARD                                                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PAROUTP  MVC   THISOUT,0(R1)       OUTPUT=XXXXXX                                
         BR    RE                                                               
*                                                                               
PARSOUT  MVC   SETOUT,0(R1)        SET OUTPUT=XXXXXX                            
         CLC   SETOUT,SPACES                                                    
         BNER  RE                                                               
         XC    SETOUT,SETOUT                                                    
         BR    RE                                                               
*                                                                               
PARDATE  ST    RE,SAVERE           DATE=XXXXXXXX                                
*&&UK                                                                           
         XC    THISDATE,THISDATE                                                
         CLC   0(5,R1),=C'NONE '                                                
         BER   RE                                                               
*&&                                                                             
         LR    R0,R1                                                            
         GOTOR VDATCON,PARA,(4,(R0)),(0,THISDATE)                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARNDAT  ST    RE,SAVERE           NODATE=XXXXXXXX                              
         LR    R0,R1                                                            
         GOTOR VDATCON,PARA,(4,(R0)),(0,THISNODT)                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARTODY  ST    RE,SAVERE           TODAY=XXXXXXXX                               
         LR    R0,R1                                                            
         GOTOR VDATCON,PARA,(4,(R0)),(0,TODAY)                                  
         PACK  DUB(2),TODAY+4(3)                                                
         MVC   THISDAY,DUB                                                      
         GOTOR VGETDAY,PARA,(0,TODAY),(0,DUB)                                   
         MVC   THISDOW,0(R1)                                                    
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARPANI  ST    RE,SAVERE           PAN=XXXXXXXXXX                               
         MVC   THISBOOK,0(R1)                                                   
         MVI   THISPAN,YES         SET CARDS FROM PAN                           
         XC    CARDVALS,CARDVALS                                                
         GOTOR CLRDAT              CLEAR PAN DATES                              
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARSORI  LA    R4,SETORIG          SET ORIGIN=XXXXXXXXXX                        
         CLI   0(R1),C' '                                                       
         JNE   PARORIG2                                                         
         XC    SETORIG,SETORIG                                                  
         BR    RE                                                               
*                                                                               
PARORIG  LA    R4,THISORIG         ORIGIN=XXXXXXXXXX                            
*                                                                               
PARORIG2 ST    RE,SAVERE                                                        
         L     R2,AIOAREA          READ USER-ID RECORD                          
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKID,0(R1)                                                     
         GOTOR VDATAMGR,PARA,DMREAD,CTFILE,CTIKEY,CTIKEY                        
         JNE   PARORIGN                                                         
*                                                                               
         LA    R2,CTIDATA          LOCATE ID NUMBER ELEMENT                     
         USING CTDSCD,R2                                                        
         SR    R0,R0                                                            
PARORIG4 CLI   CTDSCEL,0                                                        
         JE    PARORIGN                                                         
         CLI   CTDSCEL,CTDSCELQ                                                 
         JE    *+14                                                             
         IC    R0,CTDSCLEN                                                      
         AR    R2,R0                                                            
         J     PARORIG4                                                         
         MVC   THISUSER,CTDSC      AND SAVE IT                                  
         MVC   0(L'THISUSER,R4),THISUSER                                        
         J     PARORIGX                                                         
         DROP  R2                                                               
*                                                                               
PARORIGN MVC   P(20),C                                                          
         MVC   P+20(12),=C'ID NOT FOUND'                                        
         GOTOR ERROR                                                            
*                                                                               
PARORIGX L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARSDES  LA    R4,SETDEST          SET DESTINATION=XXXXXXXXXX                   
         CLI   0(R1),C' '                                                       
         JNE   PARDEST2                                                         
         XC    SETDEST,SETDEST                                                  
         BR    RE                                                               
*                                                                               
PARDEST  LA    R4,THISDEST         DESTINATION=XXXXXXXXXX                       
*                                                                               
PARDEST2 L     R2,AIOAREA                                                       
         USING CTIREC,R2                                                        
         CLC   CTIKID,0(R1)        DOES IT MATCH ORIGIN                         
         JNE   *+12                                                             
         LA    R3,THISUSER         YES-POINT TO ORIGIN ID                       
         J     PARDEST6                                                         
*                                                                               
         LR    R0,R1               SAVE A(INPUT) IN R0                          
         ST    RE,SAVERE                                                        
         OC    AGETIDS,AGETIDS     TEST GETIDS LOADED                           
         JNZ   PARDEST4                                                         
         GOTOR GETPHS,PARA,('QGETIDS',GETIDS),FGETIDS                           
*                                                                               
PARDEST4 GOTOR AGETIDS,PARA,(C'D',CTIREC),AIDWORK,VDATAMGR                      
         CLI   0(R1),FF                                                         
         JE    PARDESTN            ERROR                                        
         CLI   0(R1),0                                                          
         JE    PARDESTN            NO VALID DESTS                               
         LR    RF,R0               RF=A(INPUT VALUE)                            
         SR    R0,R0                                                            
         ICM   R0,1,0(R1)                                                       
         ICM   R0,2,8(R1)          R0=N'ENTRIES IN LIST                         
         CHI   R0,IDWORKMX                                                      
         JH    *+2                                                              
         L     R3,AIDWORK          R3=A(DESTINATION ID LIST)                    
*                                                                               
         BASR  RE,0                                                             
         CLC   0(L'CTIKID,R3),0(RF) MATCH INPUT TO LIST                         
         JE    *+14                                                             
         AHI   R3,L'IDWORK         BUMP TO NEXT LIST ENTRY                      
         BCTR  R0,RE                                                            
         J     PARDESTN            DESTINATION ID NOT FOUND                     
         AHI   R3,L'CTIKID                                                      
*                                                                               
PARDEST6 MVC   0(L'THISUSER,R4),0(R3)                                           
         J     PARDESTX                                                         
*                                                                               
PARDESTN MVC   P(20),C                                                          
         MVC   P+20(32),=C'DESTINATION NOT VALID FOR ORIGIN'                    
         MVC   P+53(10),CTIKID                                                  
         GOTOR ERROR                                                            
         MVC   P+20(28),=C'DESTINATION OVERRIDE IGNORED'                        
         GOTOR ERROR                                                            
*                                                                               
PARDESTX L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
PARTAPE  MVC   THISSYS,0(R1)       TAPE=XXXXXXXX                                
         MVC   THISSYKY,THISSYS                                                 
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         L     R1,ATAPEREQ                                                      
         J     PARMRGEX                                                         
*                                                                               
PARMRGE  CLI   TRACE,YES           MERGE=XXX                                    
         JNE   *+16                                                             
         MVC   TITLE+21(30),C                                                   
         ZAP   LINE,MAXLINES                                                    
         MVC   THISSYS,0(R1)                                                    
         MVC   THISMRGE,SPACES                                                  
         CLI   THISSUB,C'*'        MERGE=X*FILENAME                             
         JNE   PARMRGE1                                                         
         MVI   THISSUB+1,C' '                                                   
         MVC   THISMRGE,2(R1)      EXTRACT FILE NAME FOR GENERIC MERGE          
         CLI   2(R1),C'*'                                                       
         JNE   PARMRGE2                                                         
         MVC   THISMRGE,3(R1)      MERGE=X**FILENAME IS VALID AS WELL           
         J     PARMRGE2                                                         
*                                                                               
PARMRGE1 GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
*                                                                               
PARMRGE2 CLI   THISLET,ACCLETQ     TEST ACCOUNT SYSTEM                          
         JNE   PARMRGE3                                                         
         XC    ODDKEY,ODDKEY       CLEAR ODDS KEY                               
         XC    ODDCNT,ODDCNT       CLEAR ODDS COUNT                             
         XC    THISRNGE,THISRNGE   CLEAR DATE RANGE FOUND                       
         J     AODREQ              AND GO TO ACODDS MERGING ROUTINE             
*                                                                               
PARMRGE3 L     R1,AREQDISK         DEFAULT IS REQDISK                           
*&&UK                                                                           
PARMRGE4 CLI   THISLET,MEDLETQ     MEDIA SYSTEM                                 
         JNE   PARMRGEX                                                         
         L     RF,AMEDGEN          SET MEDGEN TO MEDGENXX                       
         LA    R1,L'SVMEDGEN(RF)                                                
         USING IHADCB,R1                                                        
         MVC   0(L'SVMEDGEN,R1),0(RF)  COPY MEDGEN DCB AND USE COPY             
         MVC   DCBDDNAM+6(2),THISSUB                                            
         CLI   THISSUB,C'*'        TEST ALL SYSTEMS IN ONE FILE                 
         JNE   PARMRGEX                                                         
         MVC   DCBDDNAM,THISMRGE   SET MEDGEN FILE NAME AS INPUT                
         J     PARMRGEX                                                         
         DROP  R1                                                               
*&&                                                                             
*&&US                                                                           
PARMRGE6 CLI   THISLET,PRTLETQ     PRINT SYSTEM                                 
         JNE   PARMRGEX                                                         
         MVC   AMERGE2,APP49RQ     PRINT SYSTEM MERGES IN TWO FILES             
         L     R1,APP49RQ                                                       
         USING IHADCB,R1                                                        
         MVC   DCBDDNAM+6(2),THISSUB                                            
         L     R1,APBUYRQ                                                       
         MVC   DCBDDNAM+6(2),THISSUB                                            
         CLI   THISSUB,C' '        TEST IF SET                                  
         JH    PARMRGEX                                                         
         MVC   DCBDDNAM+6(2),=C'1 ' DEFAULT TO PBUYRQ1                          
         DROP  R1                                                               
*&&                                                                             
PARMRGEX LR    R0,R1               POINT R0 TO DCB                              
         XC    THISRNGE,THISRNGE   CLEAR DATE RANGE FOUND                       
         J     OTHOPEN             PROCESS MERGE FILE                           
*&&UK                                                                           
PARXTRA  CLI   0(R1),MEDLETQ       EXTRA=MXX FOR UK MEDIA SYSTEM ONLY           
         JNE   INPUT                                                            
         CLI   TRACE,YES                                                        
         JNE   *+16                                                             
         MVC   TITLE+21(30),C                                                   
         ZAP   LINE,MAXLINES                                                    
         MVC   THISSYS,0(R1)                                                    
         GOTOR GETSYS,(R1)                                                      
         JNE   INPUT                                                            
         XC    ODDKEY,ODDKEY       CLEAR ODDS KEY                               
         XC    THISRNGE,THISRNGE   CLEAR DATE RANGE FOUND                       
         J     MODREQ              GO TO MEODDS MERGING ROUTINE                 
*&&                                                                             
PARPAND  ST    RE,SAVERE           PANDATE=XXXXXXXX                             
*                                                                               
         CLC   0(8,R1),SPACES      TEST PARAMETER VALUE OF SPACES               
         JNE   *+12                                                             
         GOTOR CLRDAT              YES-THIS CLEARS THE PAN DATES                
         J     PARPANDX                                                         
*                                                                               
         LR    RF,R1                                                            
         GOTOR VDATCON,PARA,(4,(RF)),(0,PANDATE)                                
*                                                                               
         LA    RE,PANDATES         ADD ENTRY TO PAN DATE TABLE                  
         LHI   R0,PANDATEM                                                      
PARPAND2 OC    0(L'PANDATES,RE),0(RE)                                           
         JZ    *+16                                                             
         AHI   RE,L'PANDATES                                                    
         BRCT  R0,PARPAND2                                                      
         J     PARPANDX                                                         
         MVC   0(L'PANDATES,RE),PANDATE                                         
*                                                                               
PARPANDX L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARSUND  MVC   SUNDAY,0(R1)        SUNDAY=Y/N                                   
         BR    RE                                                               
*                                                                               
PARDUPE  CLI   0(R1),C'P'          DUPE=P TO PRINT DUPLICATES                   
         JNE   *+8                                                              
         OI    DUPE,DUPEPRNT                                                    
         CLI   0(R1),C'K'          DUPE=K TO KEEP DUPLICATES                    
         JNE   *+8                                                              
         OI    DUPE,DUPEKEEP                                                    
         CLI   0(R1),C'+'          DUPE=+ TO KEEP AND PRINT DUPLICATES          
         JNE   *+8                                                              
         OI    DUPE,DUPEKEEP+DUPEPRNT                                           
         BR    RE                                                               
*                                                                               
PARCNUM  TM    0(R1),X'F0'         CARDNUM=XX                                   
         JNO   PARCNUMN                                                         
         CLI   1(R1),C' '          TEST ONE CHARACTER                           
         JE    PARCNUM2                                                         
         TM    1(R1),X'F0'         TEST SECOND CHARACTER NUMERIC                
         JNO   PARCNUMN                                                         
         PACK  DUB,0(2,R1)                                                      
         J     PARCNUM4                                                         
PARCNUM2 PACK  DUB,0(1,R1)                                                      
PARCNUM4 CVB   R1,DUB                                                           
         STC   R1,CARDVNUM                                                      
         CLI   CARDVNUM,0          CHECK CARD NUMBER > 0                        
         JE    PARCNUMN                                                         
         CLI   CARDVNUM,QDATAMAX   CHECK CARD NUMBER < MAXIMUM                  
         BNHR  RE                                                               
*                                                                               
PARCNUMN MVC   P(20),C                                                          
         MVC   P+22(22),=C'INVALID CARDNUM= VALUE'                              
         GOTOR ERROR                                                            
         J     INPUT                                                            
*                                                                               
PARPTCH  ST    RE,SAVERE           PATCH=DDDD PPP...PPP                         
         LR    R0,R1                                                            
         GOTOR VHEXIN,PARA,(R0),WORK,4                                          
         OC    12(4,R1),12(R1)                                                  
         JZ    PARPTCHN                                                         
         AHI   R0,5                                                             
         LR    RF,R0                                                            
         CLI   0(RF),C' '                                                       
         JE    *+12                                                             
         AHI   RF,1                                                             
         J     *-12                                                             
         SR    RF,R0                                                            
         GOTOR VHEXIN,PARA,(R0),WORK+2,(RF)                                     
         ICM   R1,15,12(R1)                                                     
         JZ    PARPTCHN                                                         
         AHI   R1,-1                                                            
         SR    RF,RF                                                            
         ICM   RF,3,WORK                                                        
         A     RF,AREQPEEL                                                      
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         J     *+10                                                             
         MVC   0(0,RF),WORK+2                                                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PARPTCHN MVC   P(20),C                                                          
         MVC   P+20(18),=C'INVALID PATCH CARD'                                  
         GOTOR ERROR                                                            
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO GET FIRST/NEXT SYSIN RECORD FROM CARDS OR PAN BOOK        *         
***********************************************************************         
GETNXT   NTR1  LABEL=NO                                                         
         MVC   CLAST,C                                                          
         CLI   THISPAN,YES         TEST READING INPUT FROM PAN BOOK             
         JNE   GETNXT06                                                         
         MVC   C,SPACES                                                         
         MVC   C(10),=C'READ ERROR'                                             
         GOTOR VPANIC,PARA,(X'80',PANREAD),PAN,THISBOOK,C                       
         TM    8(R1),X'80'         TEST END OF PAN BOOK                         
         JZ    GETNXT02            END OF PAN BOOK                              
         GOTOR CLRDAT              CLEAR PAN DATES BUFFER                       
         XC    SETVALS(SETVALL),SETVALS                                         
         J     GETNXT04            GO GET NEXT SYSIN RECORD                     
*                                                                               
GETNXT02 TM    8(R1),X'10'         TEST PAN BOOK NOT FOUND                      
         JZ    GETNXT08                                                         
         MVC   P(10),THISBOOK                                                   
         MVC   P+11(18),=C'PAN BOOK NOT FOUND'                                  
         GOTOR ERROR                                                            
*                                                                               
GETNXT04 MVI   THISPAN,C'X'        SET PAN ERROR EXIT                           
*                                                                               
GETNXT06 GOTOR VCARDS,PARA,C,RE00  GET NEXT SYSIN RECORD                        
*                                                                               
GETNXT08 MVC   P(L'C),C            PRINT INPUT CARD                             
         GOTOR VPRINTER                                                         
         CLI   THISPAN,C'X'        TEST WAS READING PAN                         
         JNE   XITY                                                             
         MVI   THISPAN,NO          YES-SET NOT READING PAN                      
         J     XITN                AND RETURN CC NOT EQUAL                      
         EJECT                                                                  
***********************************************************************         
*CLEAR EOD RECORD                                                     *         
***********************************************************************         
CLREOD   STM   RE,R1,12(RD)                                                     
         LA    R0,EODREQD                                                       
         LHI   R1,2000                                                          
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
                                                                                
***********************************************************************         
*CLEAR REQUEST DATA WORK AREA                                         *         
***********************************************************************         
CLRREQ   STM   RE,R1,12(RD)                                                     
         LA    RE,REQHDRD                                                       
         LHI   RF,REQINLN                                                       
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
                                                                                
***********************************************************************         
*CLEAR PAN DATES                                                      *         
***********************************************************************         
CLRDAT   STM   RE,R1,12(RD)                                                     
         LA    R0,PANDATE                                                       
         LHI   R1,L'PANDATE+PANDATEL                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVI   SUNDAY,0                                                         
         LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO LOAD A PHASE INTO STORAGE                                 *         
***********************************************************************         
GETPHS   NTR1  BASE=*,LABEL=*                                                   
         LR    R4,R1                                                            
         L     R2,4(R4)            R2=A(A(PHASE))                               
         OC    0(4,R2),0(R2)       TEST ALREADY LOADED                          
         JNZ   XIT                 YES                                          
         SR    R3,R3                                                            
         ICM   R3,7,1(R4)                                                       
         OC    0(8,R3),0(R3)       TEST SPECIAL PHASE NAME REQUIRED             
         BNZ   GETPHS02            YES                                          
         MVC   0(L'T00A,R3),T00A   BUILD T00A PHASE NAME                        
         SR    RE,RE                                                            
         ICM   RE,1,0(R4)          RE=T00A PHASE NUMBER                         
         JZ    *+2                                                              
         SRDL  RE,4                                                             
         SRL   RF,32-4                                                          
         IC    RE,HEXTAB(RE)                                                    
         IC    RF,HEXTAB(RF)                                                    
         STC   RE,4(R3)                                                         
         STC   RF,5(R3)                                                         
         MVC   6(2,R3),SPACES                                                   
*                                                                               
GETPHS02 LOAD  EPLOC=(3),ERRET=GETPHSER                                         
         LA    R1,0(R1)                                                         
         SLL   R1,3                                                             
         STM   R0,R1,0(R4)         SET A(LOADED PHASE)                          
         ST    R0,0(R2)                                                         
         J     XIT                                                              
*                                                                               
GETPHSER DC    H'0'                CAN'T LOAD PHASE                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
*RESOLVE SYSTEM DETAILS - R1=A(SYSTEM/SUB-SYSTEM ID)                  *         
***********************************************************************         
GETSYS   NTR1  BASE=*,LABEL=*                                                   
         MVC   THISSYS,0(R1)       SET SYSTEM/SUB-SYSTEM                        
         CLI   THISSYS,CONLETQ                                                  
         BE    GETSYS02                                                         
         CLI   THISSUB,C' '        SET TO SYSTEM 1 IF NOT GIVEN                 
         BNE   GETSYS02                                                         
         MVI   THISSUB,C'1'        FOR ALL EXCEPT CONTROL                       
*                                                                               
GETSYS02 MVC   SYSLET(L'SYSLET+L'SYSSUB),THISSYS                                
         MVI   SYSMST,0            SET MASTER SYSTEM UNKNOWN                    
         XC    THISSYSV(THISSYSL),THISSYSV                                      
         LARL  RF,SYSTAB           CONVERT SYSTEM LETTER TO NUMBER              
         LHI   R0,SYSTABN                                                       
         BASR  RE,0                                                             
         CLC   SYSLET,0(RF)        MATCH INPUT TO TABLE                         
         BE    *+14                                                             
         AHI   RF,L'SYSTAB         BUMP TO NEXT TABLE ENTRY                     
         BCTR  R0,RE                                                            
         B     GETSYSN                                                          
*                                                                               
         MVC   THISLET,1(RF)       SET ACTUAL SYSTEM LETTER                     
         MVC   SYSMST,2(RF)        SET MASTER SYSTEM NUMBER                     
*&&US                                                                           
GETSYS04 CLI   SYSLET,SPTLETQ      TEST THIS IS A SPOT SYSTEM                   
         BNE   GETSYS06                                                         
         MVC   DUB(2),=C'S='       SET UP S=SXX TO GET SPOT SYSTEM              
         MVC   DUB+2(3),SYSLET                                                  
         GOTO1 VDDNAME,PARA,=C'DDNAME',DUB                                      
         L     RF,8(R1)                                                         
         CLI   8(R1),0             TEST SPOT SYSTEM NOT FOUND                   
         BNE   GETSYSN                                                          
         CLI   DDNASYN3-DDNAD(RF),C'N'      TEST SPOT IS A NET SYSTEM           
         BNE   GETSYS20                                                         
         MVC   SYSLET(3),DDNASYN3-DDNAD(RF) EXTRACT NET SYSTEM SYNONYM          
         MVI   SYSMST,3                                                         
         B     GETSYS20                                                         
*                                                                               
GETSYS06 CLI   SYSLET,NETLETQ      TEST IF A NETWORK SYSTEM                     
         BNE   GETSYS20                                                         
         MVC   DUB(2),=C'S='       SET UP S=NXX TO GET NET SYSTEM               
         MVC   DUB+2(3),SYSLET                                                  
         GOTO1 VDDNAME,PARA,=C'DDNAME',DUB                                      
         L     RF,8(R1)                                                         
         CLI   8(R1),0             TEST NETWORK SYSTEM NOT FOUND                
         BNE   GETSYSN                                                          
         MVC   THISSUB(2),DDNASEN3+1-DDNAD(RF) EXTRACT SPOT SUB SYSTEM          
*&&                                                                             
GETSYS20 MVC   DUB(2),=C'S='       SET UP S=XXX TO SEARCH FOR SYSTEM            
         MVC   DUB+2(3),THISSYS                                                 
         GOTO1 VDDNAME,PARA,=C'DDNAME',DUB                                      
         L     RF,8(R1)                                                         
         CLI   8(R1),0                                                          
         BNE   GETSYSN                                                          
         MVC   THISSENO,DDNASENO-DDNAD(RF)                                      
         MVC   THISSYKL,THISLET    SET UP SYSTEM USED IN WORKER KEY             
         MVC   THISSYKS,1(RF)                                                   
*                                                                               
         ICM   RF,15,DDNAASFL-DDNAD(RF) GET SYSTEM FILE LIST                    
         BZ    GETSYSN                                                          
         LLC   R0,3(RF)            R0=NUMBER OF FILES THIS SYSTEM               
         AHI   RF,4                RF=A(FILE LIST)                              
         BASR  RE,0                                                             
         TM    0(RF),X'20'         TEST THIS IS THE REQUEST FILE                
         BNZ   *+14                                                             
         AHI   RF,8                NO-BUMP TO NEXT FILE                         
         BCTR  R0,RE               DO FOR NUMBER OF FILES IN SYSTEM             
         B     GETSYSN                                                          
         ICM   RF,7,5(RF)          GET A(REQUEST FILE DTF)                      
         MVC   THISFILE,DTFDD-DTFPHD(RF)                                        
*                                                                               
GETSYSX  J     XITY                                                             
*                                                                               
GETSYSN  MVC   P(20),C                                                          
         MVC   P+20(21),=C'SYSTEM NOT RECOGNISED'                               
         GOTOR ERROR                                                            
         J     XITN                                                             
         DROP  RB                                                               
         EJECT                                                                  
ERROR    NTR1  LABEL=NO            PRINT & SEND ERROR MESSAGE                   
         GOTOR VLOGIO,PARA,1,(70,P)                                             
         GOTOR VPRINTER                                                         
*                                                                               
XITN     SR    R0,R0               SET CC TO NOT EQUAL AND RETURN               
         J     XITCC                                                            
XITH     LHI   R0,2                SET CC TO HIGH AND RETURN                    
         J     XITCC                                                            
XITY     LHI   R0,1                SET CC TO EQUAL AND RETURN                   
XITCC    CHI   R0,1                SET CONDITION CODE                           
XIT      XIT1                      GENERAL ROUTINE EXIT                         
         EJECT                                                                  
FF       EQU   X'FF'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
*                                                                               
QSRNOSD  EQU   68                  DISPLACEMENT TO REQUESTOR                    
QSRNMAD  EQU   66                  DISPLACEMENT TO ACC/MED REQUESTOR            
QSRNMAL  EQU   12                  LENGTH OF ACC/MED REQUESTOR                  
*                                                                               
ACCLETQ  EQU   C'A'                ACCOUNT SYSTEM                               
MBALETQ  EQU   C'B'                MEDIABASE SYSTEM                             
CONLETQ  EQU   C'C'                CONTROL SYSTEM                               
PERLETQ  EQU   C'E'                PERSON SYSTEM                                
MPLLETQ  EQU   C'L'                PLANNING SYSTEM                              
*&&US                                                                           
CPPLETQ  EQU   C'D'                CPP SYSTEM                                   
SPTLETQ  EQU   C'S'                SPOT SYSTEM                                  
NETLETQ  EQU   C'N'                NETWORK SYSTEM                               
PRTLETQ  EQU   C'P'                PRINT SYSTEM                                 
REPLETQ  EQU   C'R'                REP SYSTEM                                   
TALLETQ  EQU   C'T'                TALENT SYSTEM                                
STRLETQ  EQU   C'F'                SPOT TRAFFIC SYSTEM                          
*&&                                                                             
*&&UK                                                                           
MEDLETQ  EQU   C'M'                MEDIA SYSTEM                                 
FEELETQ  EQU   C'F'                ARTIST FEE SYSTEM                            
*&&                                                                             
         EJECT                                                                  
LITERALS DS    0D                  GLOBAL LITERALS AND WORK AREAS               
         LTORG                                                                  
         EJECT                                                                  
FXPLD    DS    0F                                                               
AXPLD    DC    A(0)                A(REQXPLD)                                   
REQXPLD DC     CL8'REQXPLD '       REQXPLD PHASE NAME                           
*                                                                               
FGETIDS  DS    0F                                                               
AGETIDS  DC    A(0)                A(GETIDS)                                    
GETIDS   DC    XL8'00'             GETIDS PHASE NAME                            
*                                                                               
XPLXTRAS DS    0F                  PASSED TO REQXPLD IN PARA6                   
ASSB     DC    A(SSB)                                                           
AUTL     DC    A(UTL)                                                           
VDATAMGR DC    V(DATAMGR)                                                       
VDADDS   DC    V(DADDS)                                                         
VDATCON  DC    V(DATCON)                                                        
VADDAY   DC    V(ADDAY)                                                         
VDDNAME  DC    V(DMDDNAME)                                                      
VGETDAY  DC    V(GETDAY)                                                        
VPRINTER DC    V(PRINTER)                                                       
VCPRINT  DC    V(CPRINT)                                                        
VSORTER  DC    V(SORTER)                                                        
VCARDS   DC    V(CARDS)                                                         
VPANIC   DC    V(PANIC)                                                         
VDDSIO   DC    V(DDSIO)                                                         
VSTXITER DC    V(STXITER)                                                       
VLOGIO   DC    V(LOGIO)                                                         
VWORKER  DC    V(WORKER)                                                        
VHEXIN   DC    V(HEXIN)                                                         
VHEXOUT  DC    V(HEXOUT)                                                        
VPRNTBL  DC    V(PRNTBL)                                                        
*                                                                               
AIOAREA  DC    A(IOAREA)                                                        
AREQIO   DC    A(REQIO)                                                         
AREQINHD DC    A(REQINHD)                                                       
AEODSORT DC    A(EODSORT)                                                       
ALASTEOD DC    A(LASTEOD)                                                       
AREQEOD  DC    A(REQEOD)                                                        
ASORTREC DC    A(SORTREC)                                                       
AHEXWORK DC    A(HEXWORK)                                                       
AREQDTF  DC    A(REQDTF)                                                        
ADUMDTF  DC    A(DUMDTF)                                                        
AREQDISK DC    A(REQDISK)                                                       
AIDWORK  DC    A(IDWORK)                                                        
ATAPEREQ DC    A(TAPEREQ)                                                       
ATRKBUFF DC    A(TRKBUFF)                                                       
AODDBUFF DC    A(ODDBUFF)                                                       
AMERGE1  DC    A(0)                A(MERGE FILE 1 DCB)                          
AMERGE2  DC    A(0)                A(MERGE FILE 2 DCB)                          
*                                                                               
DUMPLIST DS    0F                  DUMP LIST FOR STXITER                        
AREQPEEL DC    A(REQPEEL)                                                       
DMPPEELX DC    X'80',AL3(ENDOFMOD)                                              
DMPXPSTR DC    A(0)                A(START OF REQXPLD PHASE)                    
DMPXPEND DC    A(0)                A(END OF REQXLPOD PHASE)                     
NOPROUT  BR    RE                                                               
*                                                                               
FRFPIO   DS    0F                                                               
ARFPIO   DC    A(0)                A(RFPIO)                                     
RFPIO    DC    XL8'00'             RFPIO PHASE NAME                             
FSOFDAT  DS    0F                                                               
ASOFDAT  DC    A(0)                A(SOFDAT)                                    
SOFDAT   DC    XL8'00'             SOFDAT PHASE NAME                            
FMINIO   DS    0F                                                               
AMINIO   DC    A(0)                A(MINIO)                                     
MINIO    DC    XL8'00'             MINIO PHASE NAME                             
*                                                                               
ARFPIOB  DC    A(RFPIOBLK)                                                      
AMINWRK  DC    A(MINIOWRK)                                                      
AMINREC  DC    A(MINIOREC)                                                      
ACOMFACS DC    A(COMFACS)                                                       
AACCESS  DC    A(ACCESSBF)                                                      
ASOFTBUF DC    A(SOFTBUFF)                                                      
*&&US                                                                           
APP49RQ  DC    A(PP49RQ)                                                        
APBUYRQ  DC    A(PBUYRQ)                                                        
*&&                                                                             
DAYBITS  DC    X'8040201008040201'                                              
EFFS     DC    20X'FF'                                                          
DMRDHI   DC    C'DMRDHI'                                                        
DMRSEQ   DC    C'DMRSEQ'                                                        
DSTLIT   DC    C'&&&&DST&&&&&&&&&&&&&&'                                         
OUTLIT   DC    C'&&&&OUT&&&&&&&&&&&&&&'                                         
FAXPFX   DC    C'FX='              FAX DESTINATION PREFIX                       
FAXOUT   DC    C'FAX   '           FAX OUTPUT TYPE                              
*                                                                               
WHEN     DC    AL1(WHENOVQ)        RLP WHEN FILTER                              
WHENOVQ  EQU   C'O'                OVERNIGHT RLP GROUPS ONLY                    
WHENAMQ  EQU   C'A'                AM RLP GROUPS ONLY                           
*&&US                                                                           
DDSID#   EQU   90                  DDS US USER-ID NUMBER                        
*&&                                                                             
RLPBACK  DC    AL1(0)              RLP BACK DAYS VALUE                          
RLPAGYF  DC    XL(L'CT5KALPH)'00'  RLP AGENCY FILTER                            
RLPEDATE DC    XL6'00'             RLP EBCDIC DATE                              
RLPJDATE DC    XL4'00'             RLP JULIAN DATE                              
RLPVRULE DC    XL(L'RFPVVRUL)'00'  RLP DATE VALIDATION RULE NUMBER              
*                                                                               
RLPRUN   DS    0XL2                COMPRESSED RUN DATES                         
RLPTOD02 DC    XL2'00'             TODAY'S DATE                                 
RLPPD102 DC    XL2'00'             TODAY'S DATE-1                               
RLPPD202 DC    XL2'00'             TODAY'S DATE-2                               
RLPPD302 DC    XL2'00'             TODAY'S DATE-3                               
RLPPD402 DC    XL2'00'             TODAY'S DATE-4                               
RLPRUNN  EQU   (*-RLPRUN)/L'RLPRUN                                              
*                                                                               
RLPTEST  DC    (RLPRUNN)XL(RLPTESTL)'00'                                        
*                                                                               
RLPGKEY  DC    XL40'00'            FOR GENDIR RLP READING                       
*&&UK                                                                           
AMEDGEN  DC    A(MEDGEN)                                                        
DDSID#   EQU   1                   DDS UK USER-ID NUMBER                        
*&&                                                                             
DMSYSTEM DC    C'CONTROL'                                                       
DMOPEN   DC    C'DMOPEN'                                                        
DMREAD   DC    C'DMREAD'                                                        
SYSFLES  DC    C'SYSFLES'                                                       
*                                                                               
DMFILES  DS    0C                  FILE OPEN LIST                               
         DC    C'N'                                                             
CTFILE   DC    C'CTFILE '                                                       
         DC    C'N'                                                             
GENDIR   DC    C'GENDIR '                                                       
         DC    C'N'                                                             
GENFIL   DC    C'GENFIL '                                                       
         DC    C'X'                                                             
*                                                                               
HEXTAB   DC    C'0123456789ABCDEF'                                              
OLDRQHL  DC    Y(L'RQHIUSR)        OLD REQUEST HEADER LENGTH                    
NEWRQHL  DC    Y(L'RQHHDR)         NEW REQUEST HEADER LENGTH                    
RE00     DC    C'RE00'                                                          
MAXLINES DC    P'99'                                                            
T00A     DC    C'T00A'                                                          
SORTGET  DC    C'GET'                                                           
SORTPUT  DC    C'PUT'                                                           
PAN      DC    C'PAN'                                                           
PANREAD  DC    C'READ'                                                          
WKREAD   DC    C'READ'                                                          
WKINDEX  DC    C'INDEX'                                                         
HEXTOG   DC    C'TOG'                                                           
HEXSEP   DC    C'SEP'                                                           
BUFBEF   DC    C'<<<BFX>>>'                                                     
BUFAFT   DC    C'<<<AFX>>>'                                                     
EZEROES  DC    C'000000'                                                        
PZERO    DC    P'0'                                                             
PONE     DC    P'1'                                                             
*                                                                               
SAVERE   DC    A(0)                SAVED RE VALUE                               
*                                                                               
REQSW    DC    AL1(REQSNONE)       REQUEST INPUT SWITCH                         
REQSNONE EQU   C' '                REQUESTS NOT ESTABLISHED                     
REQSCARD EQU   C'C'                REQUESTS FROM CARDS                          
REQSRLPG EQU   C'R'                REQUESTS FROM RLP GROUPS                     
*                                                                               
DUPECNT  DC    PL4'0'              DUPLICATE REQUEST COUNTER                    
DUPEKEPT DC    PL4'0'              DUPLICATE REQUEST KEPT COUNTER               
DUPEDELT DC    PL4'0'              DUPLICATE REQUEST DELETED COUNTER            
DUPE     DC    X'00'               DUPLICATE REQUEST HANDLING                   
DUPEKEEP EQU   X'80'               KEEP DUPLICATES                              
DUPEPRNT EQU   X'40'               PRINT DUPLICATES                             
DUPEFLAG DC    X'00'               DUPLICATE REQUEST DATA FLAGS                 
DUPEFFCQ EQU   X'80'               DIFFERENT 1ST REQUEST DATA                   
DUPEFGFQ EQU   X'40'               DIFFERENT 2ND-NTH REQUEST DATA               
*                                                                               
IINCSW   DC    C' '                                                             
TRACE    DC    C' '                                                             
TRACEHEX DC    C' '                                                             
TRACESRT DC    C' '                                                             
NYCSBILL DC    C' '                                                             
*                                                                               
SETVALS  DS    0X                  SET PARAMETER VALUES                         
SETOUT   DC    XL6'00'             OUTPUT TYPE                                  
SETDEST  DC    AL2(0)              DESTINATION ID NUMBER                        
SETORIG  DC    AL2(0)              ORIGIN ID NUMBER                             
*                                                                               
CARDVALS DS    0XL(L'CARDVCNT+L'CARDVNUM)                                       
CARDVCNT DC    AL1(0)              PAN BOOK INPUT CARD COUNT                    
CARDVNUM DC    AL1(0)              PAN BOOK INPUT CARD NUMBER                   
*                                                                               
SETVALL  EQU   *-SETVALS                                                        
*                                                                               
SVRDEST  DC    XL2'00'             SAVED DESTINATION ID NUMBER                  
SVROUTT  DC    XL6'00'             SAVED OUTPUT TYPE                            
*                                                                               
TODAY    DC    XL6'00'             TODAY'S DATE EBCDIC YYMMDD                   
TODAY15  DC    XL4'00'             TODAY'S DATE FULL JULIAN                     
TODAY19  DC    XL3'00'             TODAY'S DATE SHORT JULIAN                    
TODAYM   DC    X'00'               TODAY'S RUNDAY MASK BIT (X'80'=MON)          
DATE     DC    XL6'00'             DATE WORK AREA                               
TIME     DC    XL3'00'             TIME HHMM00 (PWOS)                           
SUNDAY   DC    X'00'               SUNDAY VALUE                                 
PANDATE  DC    XL6'00'             LAST PANDATE=VALUE PROCESSED                 
PANDATEM EQU   60                  MAXIMUM N'ENTRIES IN PAN DATE TABLE          
PANDATES DC    (PANDATEM)XL(L'PANDATE)'00'                                      
PANDATEL EQU   *-PANDATES                                                       
*                                                                               
INCOPER  DC    C' '                TIME OPERATOR                                
INCHMS   DC    XL3'00'             TIME HHMM00 (PWOS)                           
INCREQ#  DC    AL1(0)              NUMBER OF REQUEST LIST CARDS INPUT           
INCREQ   DC    C' '                REQUEST OPERATOR                             
INCSW    DC    CL08' '             INCLUDE REQ SWITCH OPERATOR INPUT            
*                                                                               
LMSGOP   DC    CL40' '                                                          
MSGOP    DC    CL40' '                                                          
         ORG   MSGOP                                                            
         DS    CL12                                                             
MSGOPSY  DS    CL3                                                              
         DS    CL06                                                             
MSGOPDT  DS    CL6                                                              
         DS    CL13                                                             
*                                                                               
MSGOP1   DC    CL40'INCLUDE SYS=... DATE=...... ? REPLY Y/N '                   
*                                                                               
RNGTXT   DC    C'WRITTEN NNNNNN REQUESTS. EARLIEST: DD/MM/YY HH:MM:SS, X        
               LATEST: DD/MM/YY HH:MM:SS'                                       
         ORG   RNGTXT                                                           
         DS    CL8                                                              
RNGTXTCT DS    CL6                                                              
         DS    C                                                                
RNGTXTSY DS    CL8                                                              
         DS    CL12                                                             
RNGTXTFD DS    CL8                                                              
         DS    C                                                                
RNGTXTFT DS    CL8                                                              
         DS    CL10                                                             
RNGTXTLD DS    CL8                                                              
         DS    C                                                                
RNGTXTLT DS    CL8                                                              
         ORG                                                                    
*                                                                               
PARA     DS    8F                                                               
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
WORK     DS    XL256                                                            
CLAST    DC    CL80' '                                                          
C        DC    CL80' '                                                          
*                                                                               
SYSLET   DC    C' '                ACTUAL SYSTEM                                
SYSSUB   DC    CL2' '              ACTUAL SUB-SYSTEM                            
SYSMST   DC    X'00'               ACTUAL MASTER SYSTEM NUMBER                  
*                                                                               
THISSYS  DS    0CL3                INPUT SYSTEM/SUB-SYSTEM                      
THISLET  DC    C' '                SYSTEM                                       
THISSUB  DC    CL2' '              SUB-SYSTEM                                   
*                                                                               
THISSYSV DS    0X                  SYSTEM VALUES                                
THISSYKY DS    0CL2                SYSTEM/SUBSYSTEM FOR REQUEST REC KEY         
THISSYKL DC    C' '                                                             
THISSYKS DC    C' '                                                             
THISSENO DC    X'00'               SE NUMBER                                    
THISFILE DC    CL8' '              REQUEST FILE NAME                            
THISSYSL EQU   *-THISSYSV                                                       
*                                                                               
THISRQHL DC    H'0'                REQUEST HEADER LENGTH                        
THISOPEN DC    C' '                REQUEST FILE OPEN SWITCH                     
THISMRGE DC    CL8' '              MERGE FILE NAME                              
THISBOOK DC    CL10' '             PAN BOOK NAME                                
THISPAN  DC    C' '                PAN BOOK READING SWITCH                      
THISUSER DC    AL2(0)              CURRENT USER-ID NUMBER                       
THISDATE DC    XL6'00'             INCLUDE DATE FILTER                          
THISNODT DC    XL6'00'             EXCLUDE DATE FILTER                          
THISDAY  DC    X'00'               TODAY'S DAY PWOS                             
THISDOW  DC    X'00'               TODAY'S DAY OF WEEK (MON=1,SUN=7)            
*                                                                               
THISVALS DS    0X                  REQUEST HEADER VALUES                        
THISOUT  DC    XL6'00'             OUTPUT TYPE                                  
THISDEST DC    AL2(0)              DESTINATION ID                               
THISORIG DC    AL2(0)              ORIGIN ID                                    
THISVALL EQU   *-THISVALS                                                       
*                                                                               
THISRNGE DS    0XL22               REQUEST TIME RANGE FOUND                     
THISRCNT DS    XL4                 NUMBER OF REQUESTS                           
THISRFDT DS    XL6                 FIRST DATE EBCDIC YYMMDD (DATCON 0)          
THISRFTM DS    XL3                 FIRST TIME PWOS X'HHMMSS'                    
THISRLDT DS    XL6                 LAST DATE  EBCDIC YYMMDD (DATCON 0)          
THISRLTM DS    XL3                 LAST TIME  PWOS X'HHMMSS'                    
*                                                                               
SORTPAR1 DS    0C                  SORT PARAMETER CARD 1                        
         DC    C'SORT FIELDS=('                                                 
SORTKDSP DS    CL3                 DISPLACEMENT TO START OF KEY                 
         DC    C','                                                             
SORTKLEN DS    CL3                 LENGTH OF SORT KEY                           
         DC    C',A),FORMAT=BI,WORK=1 '                                         
*                                                                               
SORTPAR2 DS    0C                  SORT PARAMETER CARD 2                        
         DC    C'RECORD TYPE=V,LENGTH=('                                        
SORTRLEN DS    CL4                 MAXIMUM RECORD LENGTH                        
         DC    C',,,,) '                                                        
*                                                                               
ODDCNT   DS    H                                                                
ODDKEY   DS    0XL32               ODDS FILE KEY                                
ODDKUSER DS    XL2                 USER-ID NUMBER                               
ODDKSP   DS    0CL(L'ODDKSYS+L'ODDKPRG)                                         
ODDKSYS  DS    C                   SYSTEM                                       
ODDKPRG  DS    CL2                 PROGRAM                                      
         DS    X                                                                
ODDKDAY  DS    PL1                 DAY                                          
ODDKTYP  DS    C                   FILE TYPE                                    
ODDKTYPQ EQU   C'O'                ODDS FILE TYPE                               
         ORG   ODDKEY+L'ODDKEY                                                  
*                                                                               
ODDIO    DS    XL4                 ODDS RECORD                                  
ODDCOMP  DS    X                   COMPANY CODE                                 
ODDUSER  DS    XL2                 USER-ID NUMBER                               
ODDAGYA  DS    CL2                 AGENCY ALPHA ID                              
         DS    XL1000                                                           
*                                                                               
REQINHD  DC    XL(L'RQHHDR)'00'    REQUEST WORK AREA                            
REQINCD1 DC    (QDATAMAX)XL(L'QDATA)'00'                                        
REQINCDL EQU   *-REQINCD1                                                       
REQINLN  EQU   *-REQINHD                                                        
*                                                                               
HEXWORK  DS    2CL256              HEXOUT WORK AREA                             
*                                                                               
LASTEOD  DC    (SORTMAXL)X'00'     LAST SORT RECORD AREA                        
*                                                                               
         DC    C'*INCREQ*'                                                      
INCREQL  DC    CL45' '             REQUEST LIST PP,PP,PP,.....                  
INCREQL1 DC    CL45' '                                                          
INCREQL2 DC    CL45' '                                                          
INCREQL3 DC    CL45' '                                                          
INCREQL4 DC    CL45' '                                                          
INCREQL5 DC    CL45' '                                                          
INCREQL6 DC    CL45' '                                                          
INCREQL7 DC    CL45' '                                                          
INCREQL8 DC    CL45' '                                                          
INCREQL9 DC    CL45' '                                                          
INCREQLX DC    C' '                                                             
*                                                                               
REQEODH  DC    AL4(0)              END OF DAY RECORD AREA                       
REQEOD   DC    2000X'00'                                                        
         EJECT                                                                  
         DS    0F                                                               
PARAMS   DS    0XL24               TABLE OF PARAMETER CARDS                     
*                                                                               
         DC    AL1(06),AL3(INPUT),CL20'ACC=EMU'                                 
         DC    AL1(18),AL3(INPUT),CL20'*          DATA SET'                     
         DC    AL1(01),AL3(OUTPUT),CL20'/*'                                     
*                                                                               
         DC    AL1(05),AL3(PARPTCH),CL20'PATCH='                                
         DC    AL1(09),AL3(PARTRCY),CL20'TRACE=YES'                             
         DC    AL1(11),AL3(PARTRCH),CL20'TRACE=YESHEX'                          
         DC    AL1(09),AL3(PARTRCS),CL20'TRACE=SORT'                            
         DC    AL1(05),AL3(PARINCS),CL20'INCSW='                                
         DC    AL1(04),AL3(PARLOAD),CL20'LOAD='                                 
         DC    AL1(08),AL3(PARLOAD),CL20'REQXPLOD=' OLD 8-CHR NAME              
         DC    AL1(07),AL3(PARLOAD),CL20'REQXPLD='  NEW 7-CHR NAME              
         DC    AL1(04),AL3(PARDISK),CL20'DISK='                                 
         DC    AL1(04),AL3(PARREQL),CL20'REQL='                                 
         DC    AL1(06),AL3(PARGETI),CL20'GETIDS='                               
         DC    AL1(04),AL3(PARCARD),CL20'CARD='                                 
         DC    AL1(06),AL3(PAROUTP),CL20'OUTPUT='                               
         DC    AL1(10),AL3(PARSOUT),CL20'SET OUTPUT='                           
         DC    AL1(06),AL3(PARORIG),CL20'ORIGIN='                               
         DC    AL1(10),AL3(PARSORI),CL20'SET ORIGIN='                           
         DC    AL1(11),AL3(PARDEST),CL20'DESTINATION='                          
         DC    AL1(15),AL3(PARSDES),CL20'SET DESTINATION='                      
         DC    AL1(04),AL3(PARTAPE),CL20'TAPE='                                 
         DC    AL1(05),AL3(PARMRGE),CL20'MERGE='                                
         DC    AL1(04),AL3(PARDATE),CL20'DATE='                                 
         DC    AL1(06),AL3(PARNDAT),CL20'NODATE='                               
         DC    AL1(05),AL3(PARTODY),CL20'TODAY='                                
         DC    AL1(03),AL3(PARPANI),CL20'PAN='                                  
         DC    AL1(07),AL3(PARCNUM),CL20'CARDNUM='                              
         DC    AL1(07),AL3(PARPAND),CL20'PANDATE='                              
         DC    AL1(06),AL3(PARSUND),CL20'SUNDAY='                               
         DC    AL1(04),AL3(PARDUPE),CL20'DUPE='                                 
         DC    AL1(04),AL3(PARRLPG),CL20'RLPG='                                 
         DC    AL1(04),AL3(PARRLPG),CL20'RFPG='                                 
         DC    AL1(07),AL3(PARRLPB),CL20'RLPBACK='                              
         DC    AL1(07),AL3(PARRLPB),CL20'RFPBACK='                              
         DC    AL1(05),AL3(PARRFIO),CL20'RFPIO='                                
         DC    AL1(06),AL3(PARSOFT),CL20'SOFDAT='                               
         DC    AL1(05),AL3(PARMINI),CL20'MINIO='                                
*                                                                               
*&&US*&& DC    AL1(04),AL3(PARWHEN),CL20'WHEN='                                 
*&&US*&& DC    AL1(05),AL3(PARRNOP),CL20'EXTRA='                                
*                                                                               
*&&UK*&& DC    AL1(05),AL3(PARXTRA),CL20'EXTRA='                                
*                                                                               
PARAMSX  DC    AL1(0)                                                           
         EJECT                                                                  
         DS    0F                                                               
SYSTAB   DS    0XL3                SYSTEM CONVERSION TABLE                      
*&&US*&& DC    AL1(SPTLETQ,SPTLETQ,02)                                          
*&&US*&& DC    AL1(NETLETQ,SPTLETQ,03)                                          
*&&US*&& DC    AL1(PRTLETQ,PRTLETQ,04)                                          
*&&UK*&& DC    AL1(MEDLETQ,MEDLETQ,04)                                          
         DC    AL1(MPLLETQ,MPLLETQ,05)                                          
         DC    AL1(ACCLETQ,ACCLETQ,06)                                          
*&&US*&& DC    AL1(TALLETQ,TALLETQ,07)                                          
*&&UK*&& DC    AL1(FEELETQ,FEELETQ,07)                                          
*&&US*&& DC    AL1(REPLETQ,REPLETQ,08)                                          
         DC    AL1(MBALETQ,MBALETQ,09)                                          
         DC    AL1(CONLETQ,CONLETQ,10)                                          
*&&US*&& DC    AL1(CPPLETQ,CPPLETQ,12)                                          
*&&US*&& DC    AL1(STRLETQ,STRLETQ,13)                                          
*&&UK*&& DC    AL1(PERLETQ,PERLETQ,14)                                          
SYSTABN  EQU   (*-SYSTAB)/L'SYSTAB                                              
                                                                                
         DS    0F                                                               
SPLIST   DS    0XL12               ACCPAK AUTO REQUESTS                         
*&&US                                                                           
         DC    AL2(0014,00),C'60BSS   ' INTERPUBLIC                             
         DC    AL2(0014,00),C'60BSP   '                                         
         DC    AL2(0014,00),C'60BSV   '                                         
*  ITMF-49716                           JW DATA REMOVED FROM SYSTEM             
*        DC    AL2(0460,00),C'58TSP   ' JWNYA                                   
*        DC    AL2(0460,00),C'58TSS   '                                         
*        DC    AL2(0460,00),C'58TSV   '                                         
*        DC    AL2(0460,00),C'58TSU   '                                         
*&&                                                                             
         DC    AL1(FF)                                                          
                                                                                
         DS    0F                                                               
AUTOLST  DS    0CL3                LIST OF AUTOMATIC REQUESTS                   
*&&US*&& DC    C'SU '              ALL 'U' REPORTS ARE AUTOS FOR SPOT           
*&&UK                                                                           
         DC    C'M09'              UK AUTOMATICS                                
         DC    C'M0C'                                                           
         DC    C'M12'                                                           
         DC    C'M13'                                                           
         DC    C'M1A'                                                           
         DC    C'M22'                                                           
         DC    C'M2A'                                                           
         DC    C'MA2'                                                           
         DC    C'MAA'                                                           
         DC    C'MAC'                                                           
         DC    C'MBA'                                                           
         DC    C'MCF'                                                           
         DC    C'MCP'                                                           
         DC    C'MFR'                                                           
         DC    C'MMO'                                                           
         DC    C'MTA'                                                           
         DC    C'MTC'                                                           
         DC    C'MTP'                                                           
         DC    C'MTS'                                                           
         DC    C'MTT'                                                           
         DC    C'MVC'                                                           
         DC    C'MVT'                                                           
*&&                                                                             
AUTOLSTN EQU   (*-AUTOLST)/L'AUTOLST                                            
         EJECT                                                                  
         DS    0D                                                               
REQDTF   DC    XL256'00'           FOR SYSTEM REQUEST FILE READING              
*                                                                               
DUMDTF   DMDA  RECSIZE=106,DSKXTNT=16                                           
*                                                                               
EODSORT  DCB   DDNAME=EODSORT,DSORG=PS,RECFM=VB,MACRF=PM,              *        
               LRECL=1366,BLKSIZE=23184                                         
*                                                                               
TAPEREQ  DCB   DDNAME=TAPEREQ,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=OTHCLOSE         
*                                                                               
REQDISK  DCB   DDNAME=REQDISK,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=OTHCLOSE         
*&&US                                                                           
PBUYRQ   DCB   DDNAME=PBUYRQ,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=OTHCLOSE          
*                                                                               
PP49RQ   DCB   DDNAME=PP49RQ,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=OTHCLOSE          
*&&                                                                             
*&&UK                                                                           
*EDGEN   DCB   DDNAME=MEDGEN,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=OTHCLOSE          
MEDGEN   DCB   DDNAME=MEDGEN,DSORG=PS,MACRF=GM,EODAD=OTHCLOSE                   
SVMEDGEN DC    XL(*-MEDGEN)'00'  COPY MEDGEN HERE & USE COPY INSTEAD            
*&&                                                                             
         EJECT                                                                  
         DS    0D                                                               
         DC    C'*COMFACS'                                                      
COMFACS  DS    0A                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSC                                                     
         PRINT ON                                                               
*                                                                               
         DS    0D                                                               
         DC    C'*SSB*SSB'                                                      
SSB      DS    0X                                                               
         PRINT OFF                                                              
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
         ORG   SSB                                                              
         DC    (SSOOFFX-SSOOFF)X'00'                                            
         ORG   SSOXTND                                                          
         DC    X'FF'               SET EXTENDED OFFLINE SSB                     
         DC    AL1(SSOSNRCV)       SET RECOVERY OFF                             
         ORG                                                                    
*                                                                               
         DS    0D                                                               
         DC    C'*UTL*UTL'                                                      
UTL      DC    F'0',X'0A',251X'00'                                              
*                                                                               
         DS    0D                                                               
         DC    C'*SORTREC'                                                      
SORTREC  DC    (SORTMAXL)X'00'     SORT RECORD BUILT HERE                       
*                                                                               
         DS    0D                                                               
         DC    C'*STACK**'                                                      
STACKNUM EQU   10                  MAXIMUM N'REQUESTS                           
STACKLEN EQU   QRECLEN             L'STACK TABLE ENTRY                          
         DC    AL2(STACKNUM)       FOR EDREQXPLD USE                            
         DC    AL2(STACKLEN)       FOR EDREQXPLD USE                            
STACKTAB DC    (STACKNUM*STACKLEN)X'00'                                         
*                                                                               
         DS    0D                                                               
         DC    C'*IOAREA*'                                                      
IOAREA   DC    4096X'00'                                                        
*                                                                               
         DS    0D                                                               
         DC    C'*TRKBUF*'                                                      
TRKBUFF  DC    16X'00'                                                          
         DS    60000X                                                           
*                                                                               
         DS    0D                                                               
         DC    C'*ODDBUF*'                                                      
ODDBUFF  DS    1000D                                                            
*                                                                               
         DS    0D                                                               
         DC    C'*REQIO**'                                                      
REQIO    DC    2048X'00'           MAX REQUEST 16*80=1280                       
*                                                                               
         DS    0D                                                               
         DC    C'*RFPIO**'                                                      
RFPIOBLK DC    (RFPBLTLN)X'00'                                                  
*                                                                               
         DS    0D                                                               
         DC    C'*MINWRK*'                                                      
MINIORLN EQU   5120                                                             
MINIOWRK DC    (MINIORLN)X'00'                                                  
*                                                                               
         DS    0D                                                               
         DC    C'*MINREC*'                                                      
MINIOREC DC    (MINIORLN)X'00'                                                  
*                                                                               
         DS    0D                                                               
         DC    C'*ACCESS*'                                                      
ACCESSMX EQU   2500                MAXIMUM N'AGENCIES PER SYSTEM                
ACCESSXQ EQU   FF                  END OF ACCESS BUFFER INDICATOR               
ACCESSBF DS    (ACCESSMX)XL2       TABLE OF AGENCIES ON ONE SYSTEM              
ACCESSBL EQU   *-ACCESSBF                                                       
*                                                                               
         DS    0D                                                               
         DC    C'*SOFBUF*'                                                      
SOFTBUFF DC    (SOFBUFFL)X'00'     SOFDAT OPTIMIZATION BUFFER                   
*                                                                               
         DS    0D                                                               
         DC    C'*IDWORK*'                                                      
IDWORKMX EQU   2000                                                             
IDWORK   DS    (IDWORKMX)XL12      LIST OF VALID DESTINATION IDS                
*                                                                               
         DS    0D                                                               
         DC    C'*REQWRK*'                                                      
REQWRK   DS    5000D               WORKING STORAGE AREA                         
*                                                                               
ENDOFMOD DS    0X                                                               
         EJECT                                                                  
***********************************************************************         
*SORT RECORD LAYOUT                                                   *         
***********************************************************************         
SORTD    DSECT                     SORT RECORD LAYOUT                           
SRECLEN  DS    2H                  SORT RECORD LENGTH                           
*                                                                               
SCARDS   DS    X                   ACTUAL NUMBER OF REQUEST CARDS               
SREQINFO DS    XL(L'QREQINFO)      REQUEST HEADER                               
SREQSTOR DS    XL(QSRNMAL)         REQUESTOR NAME                               
*                                                                               
SORTKEY  DS    0X                  SORT KEY AREA                                
SJOBKEY  DS    XL(L'QJOBKEY)       JOB KEY                                      
SINTKEY  DS    XL(L'QINTKEY)       SORT KEY WITHIN JOB                          
*                                                                               
SCARD1   DS    CL(L'QDATA)         REQUEST CARD 1                               
SCARD2   DS    CL(L'QDATA)         REQUEST CARD 2                               
SCARD3   DS    CL(L'QDATA)         REQUEST CARD 3                               
*                                                                               
SORTKEYL EQU   *-SORTKEY           LENGTH OF SORT KEY                           
*                                                                               
SORTMINL EQU   *-SORTD             MINIMUM LENGTH OF SORT RECORD                
*                                                                               
         ORG   SCARD1                                                           
         DS    (QDATAMAX)CL(L'QDATA)                                            
*                                                                               
SORTMAXL EQU   *-SORTD             MAXIMUM LENGTH OF SORT RECORD                
         EJECT                                                                  
***********************************************************************         
*INCLUDED SYSTEM DSECTS                                               *         
***********************************************************************         
*EDREQD                                                                         
       ++INCLUDE EDREQD                                                         
         EJECT                                                                  
*CTGENRFP                                                                       
       ++INCLUDE CTGENRFP                                                       
         EJECT                                                                  
*DDCNTRL                                                                        
       ++INCLUDE DDCNTRL                                                        
         EJECT                                                                  
*DDCOREQUS                                                                      
       ++INCLUDE DDCOREQUS                                                      
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
*DDRUNNERD                                                                      
       ++INCLUDE DDRUNNERD                                                      
         EJECT                                                                  
*DDSOFDATD                                                                      
       ++INCLUDE DDSOFDATD                                                      
         EJECT                                                                  
*DDSYSELD                                                                       
       ++INCLUDE DDSYSELD                                                       
         EJECT                                                                  
*DMDDNAMED                                                                      
DDNAD    DSECT                                                                  
       ++INCLUDE DMDDNAMED                                                      
         EJECT                                                                  
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
         EJECT                                                                  
*DMGREQUS                                                                       
       ++INCLUDE DMGREQUS                                                       
         EJECT                                                                  
REQHDRD  DSECT                                                                  
       ++INCLUDE DMREQHDRA                                                      
         EJECT                                                                  
*GERFPIOD                                                                       
RFPIOD   DSECT                                                                  
       ++INCLUDE GERFPIOD                                                       
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         EJECT                                                                  
*IHADCB                                                                         
         DCBD                                                                   
         EJECT                                                                  
*&&US                                                                           
*SPQD                                                                           
       ++INCLUDE SPQD                                                           
QRFPGRP  EQU   SPQBOOK1            RLP GROUP CODE FOR RF REQUEST                
         EJECT                                                                  
*PPREPWORK                                                                      
*PREFIX=PP                                                                      
       ++INCLUDE PPREPWORK                                                      
*PREFIX=                                                                        
*&&                                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009EDREQPEEL 10/01/20'                                      
         END                                                                    
