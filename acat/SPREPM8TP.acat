*          DATA SET SPREPM8TP  AT LEVEL 013 AS OF 05/01/02                      
*CATALP SPM8TP                                                                  
         TITLE 'SPM8TP - TAPE INTERFACE FOR SPM8 REPORT'                        
SPM8TP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPM8TP                                                         
         L     RA,0(R1)            SPREPWORKD ADDRESS                           
         USING SPWORKD,RA,R9                                                    
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
*                                                                               
         L     RC,4(R1)            BUFFALO RECORD ADDRESS                       
         USING BFRECD,RC                                                        
*                                                                               
         MVC   AMT,8(R1)           POINTS/DOLLARS AMOUNT                        
         MVC   PTSDOL,0(R1)        POINTS/DOLLARS INDICATOR                     
         CLI   0(R1),C'X'          LAST CALL INDICATOR                          
         BE    CLOSE                                                            
*                                                                               
         CLI   BFTYP,1             IGNORE TOTAL RECORDS                         
         BNE   EXIT                                                             
         CLI   BFPRD,X'FF'                                                      
         BE    EXIT                                                             
         MVC   JWMED,MED           MEDIA                                        
         MVC   JWCLT,CLT           CLIENT                                       
         CLI   BFPRD,X'FF'         TEST FOR UNRECOGNIZED PRODUCT                
         BNE   *+14                                                             
         MVC   JWPRD,=C'***'                                                    
         B     MT010                                                            
         MVC   JWPRD,=C'POL'       PRODUCTS 219+ ARE POOL                       
         CLI   BFPRD,219                                                        
         BNL   MT010                                                            
         ZIC   RE,BFPRD            FIND PRODUCT FROM CLIENT HDR                 
         BCTR  RE,0                                                             
         SLL   RE,2                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST-CLTHDRD(RE,RF)                                          
         MVC   JWPRD,0(RF)                                                      
*                                                                               
MT010    MVC   JWMKT,MKT           MARKET                                       
         L     R2,ADDPTTAB                                                      
         LA    R3,37                                                            
*                                                                               
MT020    CLI   0(R2),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   BFDPT+1(3),2(R2)    LOOK UP DAY-PART TABLE                       
         BE    MT030                                                            
         LA    R2,5(R2)                                                         
         BCT   R3,MT020                                                         
         DC    H'0'                                                             
*                                                                               
MT030    MVC   JWDPT,0(R2)         DAYPART CODE                                 
         EDIT  (1,BFSLN),(3,JWSLN),FILL=0    SPOT LENGTH                        
         MVC   JWSTART,QSTART      START DATE                                   
         MVC   JWEND,QEND          END DATE                                     
         LA    R2,JWDOL                                                         
         CLI   PTSDOL,C'$'                                                      
         BE    MT040                                                            
         LA    R2,JWPTS                                                         
         CLI   PTSDOL,C'P'                                                      
         BE    MT040                                                            
         DC    H'0'                                                             
*                                                                               
MT040    EDIT  (4,AMT),(8,(R2)),FILL=0     DOLLARS/POINTS                       
         OC    TPREC,TPREC         TEST FOR FIRST TIME                          
         BZ    MT050                                                            
         CLC   TPREC(L'JWKEY),JWREC   TEST FOR CHANGE OF KEY                    
         BE    MT050                                                            
         BAS   RE,PUTTP               YES - PUT THE LAST RECORD                 
*                                                                               
MT050    MVC   TPREC,JWREC         MOVE RECORD TO OUTPUT AREA                   
*                                                                               
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
CLOSE    OC    TPREC,TPREC         LAST CALL                                    
         BZ    *+8                                                              
         BAS   RE,PUTTP            PUT LAST RECORD                              
         LA    R2,JWTAPE                                                        
         CLOSE (R2)                CLOSE TAPE FILE                              
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*        ROUTINE TO PUT RECORD TO TAPE                                          
*                                                                               
PUTTP    NTR1                                                                   
         BC    0,PT010                                                          
         OI    *-3,X'F0'                                                        
         LA    R2,JWTAPE                                                        
         GOTO1 DYNALLOC,DMCB,DDJW,DSNJW                                         
*        GOTO1 DYNALLOC,DMCB,(X'40',DDJW),(X'80',TRACKS)                        
         OPEN  ((2),OUTPUT)                                                     
*                                                                               
PT010    LA    R1,JWTAPE                                                        
         LA    R0,TPREC                                                         
         PUT   (1),(0)                                                          
         B     EXIT                                                             
TRACKS   DC    XL6'000002000002'                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0D                                                               
AMT      DS    F                   POINTS/DOLLARS                               
PTSDOL   DS    C                                                                
DDJW     DC    CL8'JWTAPE'                                                      
DSNJW    DC    CL20'SPTTAPE.SP0M8JW1'                                           
*                                                                               
         DS    0D                                                               
TPREC    DC    XL80'00'            TAPE OUTPUT RECORD                           
*                                                                               
JWREC    DS    CL80                JWT RECORD                                   
         ORG   JWREC                                                            
JWKEY    DS    0CL27                                                            
JWMED    DS    CL1       A           MEDIA CODE                                 
JWCLT    DS    CL3       AAA         CLIENT CODE                                
JWPRD    DS    CL3       AAA         PRODUCT CODE                               
JWMKT    DS    CL4       NNNN        MARKET NUMBER                              
JWDPT    DS    CL1       A           DAYPART CODE                               
JWSLN    DS    CL3       NNN         SPOT LENGTH                                
JWSTART  DS    CL6       NNNNNN      START DATE                                 
JWEND    DS    CL6       NNNNNN      END DATE                                   
JWDOL    DS    CL8       NNNNNNNN    PURCHASED DOLLARS                          
JWPTS    DS    CL8       NNNNNNNN    PURCHASED POINTS                           
         ORG                                                                    
*                                                                               
JWTAPE   DCB   DDNAME=JWTAPE,BLKSIZE=800,LRECL=80,RECFM=FB,MACRF=PM,   X        
               DSORG=PS                                                         
         EJECT                                                                  
BFRECD   DSECT                    SPM802 BUFFALO RECORD DSECT                   
BFREC    DS    0C                                                               
         DS    CL1                 PAGE                                         
         DS    CL1                 DEMO                                         
BFPRD    DS    CL1                 PRODUCT 1                                    
         DS    CL1                 PRODUCT 2                                    
BFDPT    DS    CL4                 DAYPART                                      
BFSLN    DS    CL1                 TOTAL SPOT LENGTH                            
         DS    CL1                 SPOT LENGTH 1                                
         DS    CL1                 SPOT LENGTH 2                                
BFTYP    DS    CL1                 TYPE                                         
         EJECT                                                                  
         PRINT OFF                                                              
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013SPREPM8TP 05/01/02'                                      
         END                                                                    
