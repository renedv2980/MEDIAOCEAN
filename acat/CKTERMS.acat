*          DATA SET CKTERMS    AT LEVEL 062 AS OF 05/01/02                      
*CATALP CKTERMS                                                                 
***********************************************************************         
*  TTL:      CKTERMS                                                  *         
*  PURPOSE:  TEST FOR THE EXISTENCE OF 25 AND 26 ELEMENTS IN          *         
*            TERMINAL RECORDS, BTAM=VTAM, VTAM REC EXISTS, PASSIVE    *         
*            REC EXISTS, P-RECS EXIST, BAD LNE/CU/DV, DUPLICATE 25'S  *         
*  NOTE:     THIS PROGRAM IS EXTREMELY I/O INTENSIVE!!!               *         
***********************************************************************         
CKTERMS  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,*CKTERMS,=V(REGSAVE)                                           
*                                                                               
         ENTRY UTL                                                              
         ENTRY SSB                                                              
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
*                                                                               
         GOTO1 =V(STXITER),DMCB,DUMPLIST                                        
         B     START                                                            
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(CKTERMS,65000)                                                 
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG   *                                                                
*                                                                               
START    GOTO1 =V(DATAMGR),DMCB,(0,=C'DMOPEN'),=C'CONTROL',FLIST,AIO1           
         MVI   DATADISP+1,28                                                    
         MVC   TITLE(40),=C'CKTERMS - CK FOR MATCHING BTAM/VTAM RECS'           
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,C'T'            READ HI TERM REC                             
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,AIO1              
         CLI   DMCB+8,0                                                         
         BE    *+6                 NO ERROR                                     
         DC    H'0'                                                             
*                                                                               
* READ TILL C'T' REC WITH STATUS X'02' OR X'06'                                 
*                                                                               
         L     R7,AIO1                                                          
         USING CTTREC,R7                                                        
*                                                                               
STLP     DS    0H                                                               
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRSEQ'),=C'CTFILE',KEY,AIO1              
         CLI   DMCB+8,0                                                         
         BE    *+6                 NO ERROR                                     
         DC    H'0'                                                             
         TM    CTTSTAT,X'01'       IS THIS A PASSIVE COPY?                      
         BNZ   STLP                YES? IGNORE                                  
*                                                                               
MAIN     DS    0H                                                               
         MVC   KEY,0(R7)           MAKE SURE KEY IS CURRENT                     
         L     R6,AIO1             GET 25 ELEMENT                               
         MVI   ELCODE,X'25'                                                     
         BAS   RE,GETEL                                                         
         BE    M02                                                              
         OC    CTTKPASS(8),CTTKPASS                                             
         BNZ   M10                 NO 25 IN PASSWORD RECS                       
         BAS   RE,BAD25                                                         
         B     M10                                                              
M02      DS    0H                                                               
         USING CTTRMD,R6                                                        
* CK IF LOCAL TERM, IF SO, NO TESTING OF 25 OR 26 OR VTAM.                      
         CLI   CTTRMLTY,C'L'       LOCAL                                        
         BE    NEXT1                                                            
         CLI   CTTRMLTY,C'S'       SDLC                                         
         BE    NEXT1                                                            
* CK THAT LINE, CU AND DV ARE OKAY...                                           
         CLC   CTTRMLNE,CTTKLINE                                                
         BNE   M06                                                              
* CTTKADDR IS 4 BYTE CHAR, CONV & COMP W/CTTRMCU & CTTRMDV                      
         CLI   CTTKADDR+3,C' '                                                  
         BE    M06                                                              
         GOTO1 =V(HEXIN),DMCB,CTTKADDR,CU,2                                     
         GOTO1 =V(HEXIN),DMCB,CTTKADDR+2,DV,2                                   
         CLC   CTTRMCU,CU                                                       
         BNE   M06                                                              
         CLC   CTTRMDV,DV                                                       
         BNE   M06                                                              
         BAS   RE,NEXTEL           IS THERE ANOTHER X'25'???                    
         BNE   M10                 NO, FINE                                     
         BAS   RE,EX25             YES, SHOW ERROR                              
         B     M10                                                              
M06      BAS   RE,BAD25                                                         
         DROP  R6                                                               
*                                                                               
M10      DS    0H                                                               
         TM    CTTSTAT,X'04'       IS THIS A PRINTER?                           
         BNZ   M10A                YES? DIFFERENT PASSIVES...                   
         L     R6,AIO1                                                          
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+12                                                             
         BAS   RE,NO03                                                          
         B     M15                                                              
         XC    KEY,KEY                                                          
         MVI   KEY,C'T'                                                         
         MVC   KEY+23(2),2(R6)                                                  
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,AIO2              
         CLI   DMCB+8,0                                                         
         BE    M15                                                              
         BAS   RE,NOPASS                                                        
         B     M15                                                              
*                                                                               
M10A     DS    0H                                                               
         MVI   KEY+6,C'P'          PRINTER PASSIVE                              
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,AIO2              
         CLI   DMCB+8,0                                                         
         BE    M15                                                              
         BAS   RE,NOPASS                                                        
*                                                                               
* M10 WAS HERE                                                                  
*                                                                               
M15      DS    0H                                                               
         MVC   KEY,0(R7)           RESTORE KEY                                  
         L     R6,AIO1                                                          
         MVI   ELCODE,X'26'        GET VTAM NAME                                
         BAS   RE,GETEL                                                         
         BE    M20                 IF FOUND, GO READ                            
         BAS   RE,NO26                                                          
         B     NEXT                                                             
M20      MVC   KEY+7(8),3(R6)      PUT VTAM NAME IN KEY                         
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,AIO2              
         CLI   DMCB+8,0                                                         
         BE    M30                                                              
         BAS   RE,NOVTAM                                                        
         B     NEXT                                                             
*                                                                               
* SEE IF THIS IS A SHUTTLE, IF SO, IS ATTR X'08' ON?                            
*                                                                               
M30      DS    0H                                                               
         L     R6,AIO2                                                          
         CLI   14(R6),C'S'                                                      
         BNE   M40                                                              
         TM    27(R6),X'08'                                                     
         BNZ   M40                                                              
         BAS   RE,BADSTAT                                                       
*                                                                               
* SWAP BTAM AND VTAM NAMES AND ZERO STATUS BYTE                                 
*                                                                               
M40      DS    0H                                                               
         L     R6,AIO2                                                          
         MVI   CTTSTAT,0           CLEAR STATUS BYTE IN BTAM REC                
         MVI   27(R6),0            CLEAR STATUS BYTE                            
         MVC   TEMP,7(R6)          SAVE OFF VTAM NAME                           
         MVI   ELCODE,X'26'        GET BTAM NAME                                
         BAS   RE,GETEL                                                         
         BE    *+6                 OKAY IF FOUND                                
         DC    H'0'                                                             
         L     R5,AIO2                                                          
         MVI   2(R6),0             CLEAR X'80'                                  
         MVC   7(8,R5),3(R6)       MOVE BTAM TO VTAM                            
         MVC   3(8,R6),TEMP        MOVE VTAM TO BTAM                            
*                                                                               
         XR    R1,R1                                                            
         LR    R0,R7               'FROM' ADDRESS                               
         ICM   R1,3,CTTLEN         'FROM' LENGTH                                
         L     RE,AIO2             'TO' ADDRESS                                 
         LR    RF,R1               'TO' LENGTH = 'FROM' LENGTH                  
         CLCL  R0,RE                                                            
         BE    NEXT                                                             
         BAS   RE,PRNTERR                                                       
*                                                                               
NEXT     DS    0H                                                               
         MVC   KEY,0(R7)           GET BACK BTAM KEY                            
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,AIO1              
         CLI   DMCB+8,0                                                         
         BE    *+6                 NO ERROR                                     
         DC    H'0'                                                             
NEXT1    DS    0H                                                               
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRSEQ'),=C'CTFILE',KEY,AIO1              
         CLI   DMCB+8,0                                                         
         BE    *+6                 NO ERROR                                     
         DC    H'0'                                                             
         CLI   CTTKTYP,C'T'                                                     
         BNE   DONE                                                             
         TM    CTTSTAT,X'01'       IS THIS A PASSIVE COPY?                      
         BNZ   NEXT1                                                            
         B     MAIN                                                             
*                                                                               
DONE     XBASE                                                                  
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* PRNTERR: PRINT OUT BTAM NAME OF BAD RECS                            *         
***********************************************************************         
         SPACE                                                                  
PRNTERR  NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(10),=C'NOT = RECS'                                             
         MVC   P+11(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* NOVTAM: PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
NOVTAM   NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(11),=C'NO VTAM REC'                                            
         MVC   P+12(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* NO26:   PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
NO26     NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(11),=C'NO X26 ELEM'                                            
         MVC   P+12(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BAD25:  PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
BAD25    NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(13),=C'BAD LNE/CU/DV'                                          
         MVC   P+15(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EX25:   PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
EX25     NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(13),=C'EXTRA 25 ELEM'                                          
         MVC   P+15(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* NO03:   PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
NO03     NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(13),=C'NO 03 ELEMENT'                                          
         MVC   P+15(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BADSTAT:PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
BADSTAT  NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(18),=C'BAD SHUTTLE STATUS'                                     
         MVC   P+20(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* NOPASS: PRINT OUT BTAM NAME OF BAD RECS                             *         
***********************************************************************         
         SPACE                                                                  
NOPASS   NTR1                                                                   
         LH    R1,COUNT                                                         
         LA    R1,1(R1)                                                         
         STH   R1,COUNT                                                         
         CH    R1,=H'5000'                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         MVC   P(14),=C'NO PASSIVE REC'                                         
         MVC   P+16(18),7(R7)                                                   
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
SSB      DC    F'0'                                                             
UTL      DC    F'0',X'0A'                                                       
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
FLIST    DS    0H                                                               
         DC    CL8' CTFILE '                                                    
         DC    CL8'X       '                                                    
*                                                                               
AIO1     DC    A(IO1)              ADDR OF IO AREA                              
AIO2     DC    A(IO2)                                                           
DUB      DS    D                                                                
*                                                                               
KEY      DS    XL28                                                             
WORK     DS    CL17                                                             
DMCB     DS    6F                                                               
DATADISP DS    H                                                                
ELCODE   DS    C                                                                
COUNT    DC    H'0'                                                             
TEMP     DS    CL8                                                              
CU       DS    X                                                                
DV       DS    X                                                                
*                                                                               
         DS    0D                                                               
         DC    C'***IO***'                                                      
IO1      DS    XL1000              IO AREA FOR DATAMGR                          
IO2      DS    XL1000                                                           
         EJECT                                                                  
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
       ++INCLUDE DDDPRINT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'062CKTERMS   05/01/02'                                      
         END                                                                    
