*          DATA SET SPMEDGETLK AT LEVEL 035 AS OF 09/19/19                      
*CATALP MEDGETLK                                                                
         TITLE 'MEDIA SUMMARY - EXTRACT LOCKIN DATA'                            
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKRC)                   RA-RC   A(WORKC)                             
*                                  R2      GOAL RECORD                          
*                                  R3      MEDBLOCK                             
*                                  R4      VARIABLE DSECTS                      
*                                  R5      DISPLACEMENT TO NEXT ENTRY           
*                                                                               
VMDGETLK CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,MEDGETLK                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         L     R2,ADGOAL                                                        
         USING GOALREC,R2                                                       
         L     R3,MEDBUFF                                                       
         USING MEDBLOCK,R3                                                      
         L     R5,MEDAFRST         SET OVERALL START AND END DATES              
         MVC   MLSTART,0(R5)                                                    
         L     R5,MEDALAST                                                      
         MVC   MLEND,2(R5)                                                      
         LA    R6,MEDTOTAL                                                      
         L     R5,MEDAFRST                                                      
*                                                                               
CLRBUFF  L     RE,4(R5)            CLEAR EACH ACTIVE ENTRY                      
         LTR   RE,RE                                                            
         BZ    CLRBUFF2                                                         
         L     RF,MEDLCHNK                                                      
         XCEF                                                                   
CLRBUFF2 LA    R5,12(R5)                                                        
         CR    R5,R6                                                            
         BH    CLRBUFF3                                                         
         B     CLRBUFF                                                          
CLRBUFF3 DS    0H                                                               
         EJECT                                                                  
* GET MEDBLOCK KEY INFORMATION                                                  
*                                                                               
         GOTO1 MEDEQUIV,MLPARAM,(RA)                                            
         MVC   MLEQU,MEDEQFAC                                                   
*                                                                               
         CLC   MLEQU,=H'10'        IF NO EQUIVALENCE                            
         BH    *+10                                                             
         MVC   MLEQU,=H'1000'      FORCE TO 1000                                
*                                                                               
         MVI   MEDPRIMY,0                                                       
*                                                                               
GETLKF   DS    0H                                                               
         LA    RE,GDELEM                                                        
         DROP  R2                                                               
         LR    R2,RE               SET R2 FOR ELEMENTS                          
         USING GLKELEM,R2                                                       
         LA    R4,MEDDATA                                                       
         USING MEDGLD,R4                                                        
         L     R6,MEDAFRST                                                      
GETLK1   CLI   GLKCOD,0            END                                          
         BE    DOTOTAL                                                          
         SPACE 2                                                                
* FILTER THE ELEMENTS BASED ON MEDSPILL                                         
         CLI   MEDSPILL,C'O'       ORIGINATING ONLY                             
         BNE   LKFLTR1                                                          
         CLI   GLKCOD,X'30'        ACCEPT MONTHLY                               
         BL    GLKNXT                                                           
         CLI   GLKCOD,X'31'        AND WEEKLY                                   
         BH    GLKNXT                                                           
LKFLTR1  CLI   MEDSPILL,C'S'       SPILL ONLY                                   
         BNE   LKFLTRX                                                          
         CLI   GLKCOD,X'32'        MONTHLY                                      
         BL    GLKNXT                                                           
         CLI   GLKCOD,X'33'        WEEKLY                                       
         BH    GLKNXT                                                           
LKFLTRX  DS    0H                                                               
         SPACE 2                                                                
         OC    MEDNUMWK,MEDNUMWK   WEEKLYS ACTIVE                               
         BZ    GETLK2               NO - ACCEPT WEEKLY OR MONTHLY               
         CLI   GLKCOD,X'31'                                                     
         BE    *+8                                                              
         CLI   GLKCOD,X'33'                                                     
         BNE   GLKNXT                                                           
GETLK2   CLI   GLKCOD,X'31'                                                     
         BE    *+8                                                              
         CLI   GLKCOD,X'33'                                                     
         BNE   GETLK3                                                           
         MVC   MLHALF,=H'10'                                                    
         B     GETLK4                                                           
GETLK3   CLI   GLKCOD,X'30'                                                     
         BE    *+8                                                              
         CLI   GLKCOD,X'32'                                                     
         BNE   GLKNXT                                                           
         MVC   MLHALF,=H'100'                                                   
                                                                                
* CHECK IF WITHIN BEFORE PERIOD                                                 
                                                                                
GETLK4   CLC   GLKDAT,MLSTART                                                   
         BNL   GETLK4A                                                          
         CLI   MEDNYBEF,C'Y'                                                    
         BNE   GLKNXT                                                           
         CLC   GLKDAT,MEDBFORE                                                  
         BL    GLKNXT                                                           
         LA    R6,MEDBFORE                                                      
         B     GETLK6                                                           
*                                                                               
GETLK4A  CLC   GLKDAT,MLEND                                                     
         BNH   GETLK4B                                                          
         CLI   MEDNYAFT,C'Y'                                                    
         BNE   GLKNXT                                                           
         CLC   GLKDAT,MEDAFTER+2                                                
         BH    GLKNXT                                                           
         LA    R6,MEDAFTER                                                      
         B     GETLK6                                                           
*                                                                               
GETLK4B  CLC   GLKDAT,2(R6)        FIND PROPER SLOT                             
         BNH   GETLK6                                                           
*                                                                               
GETLK5   LA    R6,12(R6)                                                        
         CLI   0(R6),0                                                          
         BE    GETLK5                                                           
         B     GETLK4B                                                          
*                                                                               
GETLK6   L     R4,4(R6)            SET PROPER SLOT                              
         XC    MLFULL,MLFULL                                                    
         MVC   MLFULL+2(2),GLKSPT                                               
         L     RF,MEDLKSPT                                                      
         A     RF,MLFULL                                                        
         ST    RF,MEDLKSPT                                                      
*                                                                               
         MVC   MLFULL,GLKDLR       ADD DOLLARS                                  
         L     RF,MLFULL                                                        
         MH    RF,MLHALF                                                        
         LR    RE,RF                                                            
         A     RE,MEDLKD                                                        
         ST    RE,MEDLKD                                                        
         BAS   R9,EQUIVL                                                        
         A     RF,MEDLKDEQ                                                      
         ST    RF,MEDLKDEQ                                                      
*                                                                               
         MVC   MLFULL,GLKDEMVL     MOVE DEMO VALUE                              
         NI    MLFULL,X'7F'        DROP OVERRIDE                                
*                                                                               
         L     RF,MLFULL           GET DEMO VALUE                               
         LA    RE,10               FOR MONTHLY, ADJUST TO 1-DEC                 
         CLI   0(R2),X'30'         TEST MONTHLY ELEM (0 DEC)                    
         JNE   *+6                                                              
         MR    RE,RE               ADJUST PREC TO 1-DEC                         
*                                                                               
         BAS   RE,ISRATING                                                      
         BNE   GETLK10                                                          
*                                                                               
         TM    RQOPTS,RQOPTS_2DEC                                               
         BZ    *+8                                                              
         MHI   RF,10               ADJUST PRECISION FOR 2-DEC                   
         B     GETLK12                                                          
*                                                                               
GETLK10  TM    RQOPTS,RQOPTS_2DECIMP                                            
         JZ    *+8                                                              
         MHI   RF,10                                                            
*                                                                               
GETLK12  LR    RE,RF                                                            
         A     RE,MEDLK1                                                        
         ST    RE,MEDLK1                                                        
*                                                                               
         BAS   R9,EQUIVL2                                                       
         A     RF,MEDLK1EQ                                                      
         ST    RF,MEDLK1EQ                                                      
*                                                                               
         LA    RE,MEDBFORE         SET DATE POINTER                             
         CR    R6,RE                                                            
         BNE   *+8                                                              
         L     R6,MEDAFRST                                                      
         LA    RE,MEDAFTER                                                      
         CR    R6,RE                                                            
         BNE   *+8                                                              
         L     R6,MEDAFRST                                                      
*                                                                               
GLKNXT   SR    RE,RE               GET NEXT ELEMENT                             
         IC    RE,GLKLEN                                                        
         AR    R2,RE                                                            
         B     GETLK1                                                           
         EJECT                                                                  
EQUIVL   SR    RE,RE               EQUIVALENCE DOLLARS                          
         M     RE,=F'1000'                                                      
         SLDA  RE,1                                                             
         LH    R1,MLEQU                                                         
         DR    RE,R1                                                            
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         BR    R9                                                               
         SPACE 2                                                                
EQUIVL2  SR    RE,RE               EQUIVALENCE DOLLARS                          
         MH    RF,MLEQU                                                         
         SLDA  RE,1                                                             
         D     RE,=F'1000'                                                      
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         BR    R9                                                               
         EJECT                                                                  
* DO TOTALS                                                                     
                                                                                
DOTOTAL  L     R6,MEDAFRST                                                      
         LA    RE,MEDTOTAL                                                      
         ST    RE,MLDUB                                                         
DOTOTAL1 C     R6,MLDUB                                                         
         BE    EXIT                                                             
         L     RE,4(R6)                                                         
         L     RF,8(R6)                                                         
         LTR   RF,RF                                                            
         BZ    DOTOTAL5                                                         
         LA    R4,7                                                             
         LA    RE,32(RE)           SET TO START OF DATA                         
         LA    RF,32(RF)                                                        
DOTOTAL3 L     R7,0(RE)                                                         
         A     R7,0(RF)                                                         
         ST    R7,0(RF)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R4,DOTOTAL3                                                      
DOTOTAL5 LA    R6,12(R6)                                                        
         C     R6,MLDUB                                                         
         BNL   EXIT                                                             
         CLI   2(R6),0                                                          
         BE    DOTOTAL5                                                         
         B     DOTOTAL1                                                         
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*==============================================================                 
* SUBROUTINE RETURNS WITH CC EQ IF DEMO AT GLKDEM IS A RATING                   
* ON ENTRY R2 POINTS TO GLKELEM                                                 
*==============================================================                 
                                                                                
ISRATING NTR1  0H                                                               
         TM    RQOPT2,RQOPT2_NONTDEM  NONT DEMOS NORMALIZED?                    
         JZ    ISRTG2                 NO                                        
         CLI   GLKDEM,0               TEST NON-TRAD DEMO                        
         JE    ISRTG2                 NO                                        
         CLI   GLKDEM,C'R'                                                      
         JE    ISRTGYES                                                         
         CLI   GLKDEM,C'E'                                                      
         JE    ISRTGYES                                                         
         J     ISRTGNO                                                          
*                                                                               
ISRTG2   CLI   GLKDEM+2,0          NON-TRAD DEMO CATEGORY?                      
         JE    ISRTG4              YES                                          
*                                                                               
         CLI   GLKDEM+1,C'R'       SEE IF NORMAL DEMO IS A RATING               
         JE    ISRTGYES                                                         
         CLI   GLKDEM+1,C'E'                                                    
         JE    ISRTGYES                                                         
         J     ISRTGNO                                                          
*                                                                               
ISRTG4   LLC   RF,GLKDEM+1         YES, GET THE NON-TRAD INDEX                  
         BCTR  RF,0                                                             
         MHI   RF,9                9 CHARS/DEMO IN 50EL                         
         A     RF,VPOL50EL                                                      
         LA    RF,2(RF)                                                         
         CLI   0(RF),C'R'          SEE IF NON-TRAD CTGY IS RATING               
         JE    ISRTGYES              OR EXTENDED RATING                         
         CLI   0(RF),C'E'                                                       
         JNE   ISRTGNO                                                          
*                                                                               
ISRTGYES MVI   RATING,C'Y'                                                      
         J     *+8                                                              
*                                                                               
ISRTGNO  MVI   RATING,C'N'                                                      
*                                                                               
         CLI   RATING,C'Y'                                                      
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
MLFULL   DS    F                                                                
MLDUB    DS    F                                                                
MLPARAM  DS    6F                                                               
MLSTART  DS    H                                                                
MLEND    DS    H                                                                
MLDADDR  DS    6F                                                               
MLEQU    DS    H                                                                
MLHALF   DS    H                                                                
MLSECOND DS    C                                                                
RATING   DS    C                                                                
         PRINT OFF                                                              
         EJECT                                                                  
* SPMEDBLOCK                                                                    
       ++INCLUDE SPMEDBLOCK                                                     
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
* SPGENGOAL                                                                     
       ++INCLUDE SPGENGOAL                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035SPMEDGETLK09/19/19'                                      
         END                                                                    
