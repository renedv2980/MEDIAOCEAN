*          DATA SET FAWLMLV    AT LEVEL 001 AS OF 04/26/10                      
*CATALP FAWLMLV                                                                 
         TITLE 'WLMLV - INTERFACE TO IBM WLM LEAVE ENCLAVE'                     
********************************************************************            
* FACPACK - INTERFACE TO WORKLOAD MANAGER LEAVE A PREVIOUSLY                    
*         - JOINED ENCLAVE                                                      
* MODULE:   FAWLMLV                                                             
*                                                                               
* PROPERTIES:                                                                   
*    PROBLEM STATE                                                              
*    ADDRESSING MODE= 31                                                        
*    PRIMARY ASC MODE SAC 0                                                     
*    DISPATCHABLE UNIT = TCB                                                    
*                                                                               
* INVOCATION:                                                                   
*        SAM31                                                                  
*        SAC   0                                                                
*        GOTO1 VWLMLV,WLMPARM,C'WLLV'                                           
*                                                                               
* PROCESSING:                                                                   
* - ENCLAVE TOKEN MUST BE PRESERVED                                             
* - ESTABLISH STANDEAD DDS LINKAGE                                              
* - UPON ENTRY REGISTER 1 CONTAINS THE ADDRESS OF THE PARAMETER                 
*   REQUEST BLOCK.                                                              
* - ESTABLISH REGISTER 7 AS THE REQUEST BLOCK BASE REGISTER                     
* - SWITCH TO SUPERVISOR STATE                                                  
* - ISSUE IWMELEAV TO JOIN AN EXISTING ENCLAVE                                  
* - SWITCH BACK TO PROBLEM STATE                                                
* - RETURN TO CALLER                                                            
*                                                                               
*IWMELEAV                                                                       
*THE PURPOSE OF THIS SERVICE IS TO ALLOW THE TASK INVOKING THIS SERVICE         
*TO LEAVE AN ENCLAVE.                                                           
*FOR THE PURPOSE OF PERFORMANCE MANAGEMENT, THE TASK WILL BECOME                
*ASSOCIATED WITH ITS HOME ADDRESS SPACE.                                        
*THE SCOPE OF THIS SERVICE AFFECTS THE CURRENT TASK AT THE TIME THE             
*SERVICE IS INVOKED.                                                            
*                                                                               
*A SUBTASK WHICH INHERITED THE ENCLAVE ATTRIBUTE FROM ITS MOTHER TASK           
*IS NOT ALLOWED TO USE IWMELEAV TO EXPLICITLY LEAVE THE ENCLAVE.                
*THE SUBTASK WOULD ONLY LEAVE THE ENCLAVE UPON ITS OWN (TASK)                   
*TERMINATION OR WHEN THE ENCLAVE IS DELETED (IWM4EDEL).                         
*                                                                               
*UPON SUCCESSFUL COMPLETION OF THIS SERVICE, THE CPU TIME FOR THE               
*TASK (AND ANY SUBSEQUENTLY ATTACHED SUBTASKS) WILL BE ATTRIBUTED               
*TO THE HOME ADDRESS SPACE FOR THE PURPOSE OF SERVICE UNIT                      
*CALCULATIONS AND PERFORMANCE *PERIOD WITCHES,                                  
*RATHER THAN BEING ATTRIBUTED TO THE ENCLAVE.                                   
*                                                                               
********************************************************************            
         USING WLMED,R7           INFORM ASSEMBLER                              
FAWLMLV  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**WLMLV                                                        
         LR    R7,R1               PARAMETER LIST ADDRESS                       
         XR    R0,R0               INITIALIZATION BYTE                          
         ST    R0,ECLVRETC         CLEAR RETURN CODE                            
         ST    R0,ECLVRSNC         CLEAR REASON CODE                            
*                                                                               
         ICM   R0,15,ECLVTOKEN                                                  
         BZ    EXIT                                                             
*                                                                               
         MODESET MODE=SUP         SWITCH TO SUPERVISOR STATE                    
*                                                                               
         IWMELEAV ETOKEN=ECLVTOKEN,                                    +        
               RETCODE=ECLVRETC,                                       +        
               RSNCODE=ECLVRSNC,                                       +        
               MF=(E,LEAVLIST,COMPLETE)                                         
*PVD        ENCLAVESERVER=YES,                                     +            
*                                                                               
         MODESET MODE=PROB         SWITCH BACK TO PROBLEM STATE                 
*                                                                               
         ICM   RF,15,ECLVRETC      Check for errors                             
         JNZ   ERROR10             Return code error                            
         OC    ECLVRSNC,ECLVRSNC   Check Reason code                            
         JZ    EXIT                All okay                                     
*                                                                               
ERROR10  STM   R0,RF,DUMPREGS                                                   
         MVC   ABEND#,=CL3'917'                                                 
         AP    ERRORS,=P'1'                                                     
         LA    R0,8                                                             
         LA    R1,MSGRTC           Format return code                           
ERROR12  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR12                                                       
                                                                                
         L     RF,ECLVRSNC         Reason code                                  
         LA    R0,8                                                             
         LA    R1,MSGRSNC          Format reason code                           
ERROR14  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR14                                                       
                                                                                
         LA    R3,MSG1                                                          
         WTO   TEXT=(R3)                                                        
*                                                                               
EXIT     XMOD1 1                                                                
                                                                                
         LTORG ,                  GENERTE LOCAL LITTERL POOL                    
         EJECT ,                                                                
***********************************************************************         
* Constants                                                                     
***********************************************************************         
         DS    0D                                                               
         DC    CL4'WR#='                                                        
WARNS    DC    PL8'0'                                                           
         DC    CL4'ER#='                                                        
ERRORS   DC    PL8'0'                                                           
         DC    CL6'ABEND='                                                      
ABEND#   DC    CL3' '                                                           
         DC    CL3' '                                                           
         DC    C'DUMPREGS'                                                      
DUMPREGS DC    18A(0)                                                           
HEX2CHR  DC    C'0123456789ABCDEF'                                              
                                                                                
MSG1     DC    AL2(MSG1LNQ)                                                     
MSG1TXT  DC    C'**U917** FAWLMLV LEAVE ENCLAVE'                                
         DC    C' RTC='                                                         
MSGRTC   DC    C'00000000'                                                      
         DC    C' RSN='                                                         
MSGRSNC  DC    C'00000000'                                                      
MSG1LNQ  EQU   *-MSG1TXT                                                        
*                                                                               
         DS   0D                                                                
         IWMELEAV PLISTVER=IMPLIED_VERSION,MF=(L,LEAVLIST)                      
         EJECT                                                                  
**********************************************************************          
* DSECTS                                                                        
**********************************************************************          
* FASSB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FASSB                                                          
*        PRINT ON                                                               
* FATCB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FATCB                                                          
*        PRINT ON                                                               
* FAWLMED                                                                       
       ++INCLUDE FAWLMED                                                        
* CVT                                                                           
         CVT DSECT=YES                                                          
* IWMYCON                                                                       
         IWMYCON                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAWLMLV   04/26/10'                                      
         END                                                                    
