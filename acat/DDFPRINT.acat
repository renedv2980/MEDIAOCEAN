*          DATA SET DDFPRINT   AT LEVEL 003 AS OF 05/01/02                      
*CATALP FPRINT                                                                  
         TITLE 'MFPRINT - V(PRINT) FOR MICRO FICHE TAPE'                        
PRINTF   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY PPFPRINT                                                         
         USING *,RF                                                             
         MVI   WHERE,C'N'                                                       
         B     PRINTB                                                           
         SPACE 2                                                                
PPFPRINT DS    0D                                                               
         USING *,RF                                                             
         MVI   WHERE,C'P'                                                       
         SPACE 2                                                                
PRINTB   NMOD1 0,*MFPRINT                                                       
         ST    R1,ADMCB            SAVE A(CALLER'S DMCB)                        
         LM    R2,R3,0(R1)         R2=A(133 CHR LINE) R3=A(CONTROL)             
         LA    RA,MFTAP            RA=A(DTF) OR A(DCB)                          
         CLC   2(2,R3),=C'00'      IGNORE ANY FUNNY SPACING                     
         BL    PRINTX                                                           
         CLC   2(2,R3),=C'50'                                                   
         BH    PRINTX                                                           
         MVC   CNTRL,0(R3)                                                      
         CLC   0(5,R2),=C'CLOSE'                                                
         BE    PRINTX                                                           
         CLI   WHERE,C'P'                                                       
         BNE   *+6                                                              
         BCTR  R2,0                                                             
         ICM   R9,15,=V(COMC)                                                   
         BZ    PRT                                                              
         USING COMCTRLD,R9         R9=A(FICHE CONTROL CSECT)                    
*                                                                               
         CLI   COMSTART,2          CLOSE                                        
         BNE   COM                                                              
         CLI   COMOPEN,1           TEST IF TAPE OPEN                            
         BNE   PRINTX                                                           
*&&OS*&& CLOSE ((RA))                                                           
*&&DO*&& CLOSER (RA)                                                            
         MVI   COMOPEN,0                                                        
*                                                                               
PRINTX   XMOD1 1                                                                
         EJECT                                                                  
COM      TM    COMIO,1                                                          
         BZ    COMX                                                             
         CLI   COMOPEN,0           TEST IF TAPE OPEN                            
         BNE   COM1                                                             
*&&OS*&& OPEN  ((RA),OUTPUT)                                                    
*&&DO*&& OPENR (RA)                                                             
         MVI   COMOPEN,1                                                        
         MVI   COMSTART,0                                                       
*                                                                               
COM1     CLI   COMSTART,0          TEST IF NEW REPORT                           
         BNE   COM2                                                             
         MVI   COMLINE,C' '                                                     
         MVC   COMLINE+1(L'COMLINE-1),COMLINE                                   
         GOTO1 COMABLD,DUB,(C'T',COMLINE)                                       
         MVC   COMCTRL,=C'&&&&&&'                                               
         MVI   COMFORCE,C'F'                                                    
         MVI   COMSTART,1                                                       
         TM    COMIO,4             TEST IF INDEX REQD FOR P1                    
         BZ    *+12                                                             
         BAS   RE,PUTNDX           PUT TITLE WITH INDEX                         
         B     COM1A                                                            
         LA    R8,COMLINE          PUT TITLE WITH NO INDEX                      
         PUT   (RA),(R8)                                                        
         MVI   BFLAG,1                                                          
*                                                                               
COM1A    MVI   COMFORCE,C' '       RESET NEW FICHE FLAG                         
         CLC   CNTRL,=C'BC01'                                                   
         BE    COMX                                                             
         CLC   CNTRL,=C'AC01'                                                   
         BNE   COM2                                                             
         MVI   CNTRL+1,C'L'        TURN AC01 INTO AL01                          
         SPACE 2                                                                
COM2     MVC   MFREC,0(R2)                                                      
         PACK  DUB,CNTRL+2(2)                                                   
         CVB   R4,DUB                                                           
         SRDA  R4,32                                                            
         CLI   CNTRL+1,C'L'                                                     
         BE    *+20                                                             
         CH    R5,=H'12'           R5=CHANNEL NUMBER FOR C                      
         BNH   *+8                                                              
         LA    R5,1                SET CHANNEL TO 1 IF FUNNY                    
         B     COM3                                                             
*                                                                               
         D     R4,=F'3'                                                         
         LTR   R4,R4                                                            
         BNZ   COM3                                                             
         LTR   R5,R5                                                            
         BZ    COM3                                                             
         LA    R4,3                R4=SPACE 0-3 FOR L                           
         BCTR  R5,0                R5=SPACE 3 EXTRAS REQUIRED FOR L             
*                                                                               
COM3     CLI   CNTRL,C'B'                                                       
         BNE   COMA                                                             
         SPACE 2                                                                
COMB     CLI   CNTRL+1,C'C'        PRINT BEFORE CHANNEL                         
         BNE   COMB1                                                            
         CH    R5,=H'1'                                                         
         BNE   *+8                                                              
         MVI   BFLAG,0             SET INDEX REQUIRED NEXT TIME                 
         LA    R6,BC(R5)                                                        
         MVC   MFCTL,0(R6)                                                      
         PUT   (RA),MFREC                                                       
         B     COMX                                                             
         SPACE 1                                                                
COMB1    CLI   BFLAG,0             PRINT BEFORE LINES                           
         BNE   COMB2                                                            
         BAS   RE,PUTNDX           OUTPUT INDEX RECORD IF REQUIRED              
*                                                                               
COMB2    LA    R6,BL(R4)           PRINT BEFORE SPACING                         
         MVC   MFCTL,0(R6)                                                      
         PUT   (RA),MFREC                                                       
         MVI   MFDATA,C' '                                                      
         MVC   MFDATA+1(L'MFDATA-1),MFDATA                                      
         MVI   MFCTL,X'1B'                                                      
*                                                                               
COMB3    LTR   R5,R5                                                            
         BZ    COMX                                                             
*                                                                               
COMB4    PUT   (RA),MFREC          DO EXTRA SPACE 3                             
         BCT   R5,COMB4                                                         
         B     COMX                                                             
         SPACE 2                                                                
COMA     CLI   CNTRL+1,C'C'        PRINT AFTER CHANNEL                          
         BNE   COMA2                                                            
         MVI   MFDATA,C' '                                                      
         MVC   MFDATA+1(L'MFDATA-1),MFDATA                                      
         LA    R6,AC(R5)           SKIP TO CHANNEL IMMEDIATE                    
         MVC   MFCTL,0(R6)                                                      
         PUT   (RA),MFREC                                                       
         MVC   MFREC,0(R2)                                                      
         CH    R5,=H'1'                                                         
         BNE   COMA1                                                            
         BAS   RE,PUTNDX           PUT INDEX RECORD IF AT CHANNEL 1             
*                                                                               
COMA1    MVC   MFCTL,X'01'         PRINT DATA (NO SPACE)                        
         PUT   (RA),MFREC                                                       
         B     COMX                                                             
         SPACE 1                                                                
COMA2    CLI   BFLAG,0             PRINT AFTER LINE                             
         BNE   *+8                                                              
         BAS   RE,PUTNDX           PUT INDEX RECORD IF REQUIRED                 
         MVI   MFDATA,C' '                                                      
         MVC   MFDATA+1(L'MFDATA-1),MFDATA                                      
         LTR   R5,R5                                                            
         BZ    COMA4                                                            
         MVI   MFCTL,X'1B'         SPACE 3 LINES                                
*                                                                               
COMA3    PUT   (RA),MFREC                                                       
         BCT   R5,COMA3                                                         
*                                                                               
COMA4    LA    R6,AL(R4)           SPACE REMAINING LINES                        
         MVC   MFCTL,0(R6)                                                      
         PUT   (RA),MFREC                                                       
         MVC   MFREC,0(R2)                                                      
         B     COMA1               GO WRITE DATA LINE                           
         SPACE 2                                                                
*                                  PUT INDEX RECORD TO TAPE                     
PUTNDX   NTR1                                                                   
         GOTO1 COMABLD,DUB,(C'I',COMLINE)                                       
         LA    R8,COMLINE                                                       
         PUT   (RA),(R8)                                                        
         MVI   BFLAG,1                                                          
         XIT1                                                                   
         SPACE 2                                                                
COMX     TM    COMIO,2                                                          
         BZ    PRINTX                                                           
         B     PRT                                                              
         EJECT                                                                  
*&&DO                                                                           
PRT      LA    R2,1(R2)                                                         
         ST    R2,DUB                                                           
         MVC   CCWCNTL+1(3),DUB+1                                               
         MVC   CCWPRNT+1(3),DUB+1                                               
*                                                                               
         PACK  DUB,2(2,R3)                                                      
         CVB   R4,DUB                                                           
         SRDA  R4,32                                                            
         CLI   1(R3),C'L'                                                       
         BE    *+20                                                             
         CH    R5,=H'12'           R5=CHANNEL NUM FOR C                         
         BNH   PRT1                                                             
         LA    R5,1                SET CHANNEL TO 1 IF FUNNY                    
         B     PRT1                                                             
*                                                                               
         D     R4,=F'3'                                                         
         LTR   R4,R4                                                            
         BNZ   PRT1                                                             
         LTR   R5,R5                                                            
         BZ    PRT1                                                             
         LA    R4,3                R4=SPACE 0-3 FOR L                           
         BCTR  R5,0                R5=SPACE 3 EXTRAS REQUIRED FOR L             
*                                                                               
PRT1     LA    R7,PRTCCW                                                        
         CLI   0(R3),C'B'                                                       
         BNE   PRTA                                                             
         SPACE 2                                                                
PRTB     CLI   1(R3),C'C'          PRINT BEFORE SPACING                         
         BNE   PRTBL                                                            
*                                                                               
PRTBC    MVC   0(8,R7),CCWPRNT     PRINT BEFORE CHANNEL                         
         LA    R6,BC(R5)                                                        
         MVC   0(1,R7),0(R6)                                                    
         B     PRTIO                                                            
*                                                                               
PRTBL    MVC   0(8,R7),CCWPRNT     PRINT BEFORE LINES                           
         LA    R6,BL(R4)                                                        
         MVC   0(1,R7),0(R6)                                                    
PRTBL1   LTR   R5,R5               CHECK FOR EXTRA CCWS                         
         BZ    PRTIO                                                            
         OI    4(R7),X'40'         CHAIN TO SPACE 3                             
         LA    R7,8(R7)                                                         
         MVC   0(8,R7),CCWCNTL                                                  
         BCTR  R5,0                                                             
         B     PRTBL1                                                           
         SPACE 2                                                                
PRTA     CLI   1(R3),C'C'          PRINT AFTER SPACING                          
         BNE   PRTAL                                                            
*                                                                               
PRTAC    MVC   0(8,R7),CCWCNTL     PRINT AFTER CHANNEL                          
         LA    R6,AC(R5)                                                        
         MVC   0(1,R7),0(R6)                                                    
         OI    4(R7),X'40'         SKIP CHANNEL CHAIN TO PRINT NO SPACE         
         MVC   8(8,R7),CCWPRNT                                                  
         B     PRTIO                                                            
*                                                                               
PRTAL    LTR   R5,R5               PRINT AFTER LINES                            
         BZ    PRTAL1                                                           
         MVC   0(8,R7),CCWCNTL     EXTRA SPACE 3 CCWS                           
         OI    4(R7),X'40'                                                      
         LA    R7,8(R7)                                                         
         BCTR  R5,0                                                             
         B     PRTAL                                                            
PRTAL1   MVC   0(8,R7),CCWCNTL     SKIP LINES CHAIN TO PRINT NO SPACE           
         LA    R6,AL(R4)                                                        
         MVC   0(1,R7),0(R6)                                                    
         OI    4(R7),X'40'                                                      
         MVC   8(8,R7),CCWPRNT                                                  
         B     PRTIO                                                            
         SPACE 2                                                                
PRTIO    LA    R7,PRTCCW           CHANNEL PROGRAM AT PRTCCW                    
         LA    R1,CCB                                                           
         ST    R7,8(R1)                                                         
         EXCP  (1)                                                              
         WAIT  (1)                                                              
         B     PRINTX                                                           
*&&                                                                             
         SPACE 2                                                                
*&&OS                                                                           
PRT      L     R1,ADMCB                                                         
         GOTO1 =V(PRINT)                                                        
         B     PRINTX                                                           
*&&                                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
DUB      DS    D                                                                
ADMCB    DS    A                                                                
*&&DO                                                                           
PRTCCW   DS    20D                 CCW CHAIN FOR 20 COMMANDS                    
CCB      CCB   SYSLST,0,X'0200'    LINE PRINTER CCB                             
CCWCNTL  CCW   X'1B',0,X'20',1     SPACE 3 LINES                                
CCWPRNT  CCW   X'01',0,X'20',132   WRITE WITH NO SPACE                          
*&&                                                                             
BFLAG    DC    X'00'                                                            
CNTRL    DC    C'    '                                                          
ALASA    DC    C'+ 0-'                                                          
AL       DC    X'010B131B'                                                      
AC       DC    X'8B8B939BA3ABB3BBC3CBD3DBE3'                                    
BL       DC    X'01091119'                                                      
BC       DC    X'89899199A1A9B1B9C1C9D1D9E1'                                    
*                                                                               
WHERE    DS    CL1                                                              
         SPACE 1                                                                
MFREC    DS    0CL133                                                           
MFCTL    DS    CL1                                                              
MFDATA   DS    CL132                                                            
         EJECT                                                                  
*&&DO                                                                           
*              DTF FOR FICHE TAPE                                               
*                                                                               
MFTAP    DTFMT DEVADDR=SYS007,TYPEFLE=OUTPUT,FILABL=NO,IOAREA1=TAPIO,  *        
               REWIND=UNLOAD,WORKA=YES,BLKSIZE=3325,RECSIZE=133,       *        
               RECFORM=FIXBLK                                                   
*                                                                               
TAPIO    DS    3325C                                                            
*&&                                                                             
         SPACE 2                                                                
*&&OS                                                                           
MFTAP    DCB   DDNAME=MFTAP,DSORG=PS,MACRF=(PM),RECFM=FB,BLKSIZE=3325, *        
               LRECL=133,BUFNO=2                                                
*&&                                                                             
         SPACE 2                                                                
* DDCOMCTRLD                                                                    
       ++INCLUDE DDCOMCTRLD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDFPRINT  05/01/02'                                      
         END                                                                    
