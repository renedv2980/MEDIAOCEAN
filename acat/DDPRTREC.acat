*          DATA SET DDPRTREC   AT LEVEL 010 AS OF 04/14/16                      
*CATALP PRTREC                                                                  
***********************************************************************         
* ROUTINE TO PRINT OUT A RECORD                                       *         
* NTRY:  P1 B0   TYPE OF FORMATTING (E=ELEM, R=REC) E=DEFAULT         *         
*        P1 B1-3 A(RECORD)                                            *         
*        P2 B0   DISP TO FIRST ELEMENT                                *         
*           B1-3 DISP TO RECORD LENGTH                                *         
*        P3 B0-3 A(PRINT)                                             *         
*        P4 B0-3 A(HEXOUT)                                            *         
*        P5 B0-3 C'DOME'           P5/6 OPTIONAL FOR TITLE PRINTING   *         
*        P6 B0   L'TITLE STRING                                       *         
*        P6 B1-3 A(TITLE STRING)                                      *         
***********************************************************************         
         TITLE 'DUMP OUT A RECORD TO PRINT'                                     
         SPACE 1                                                                
PRTREC   CSECT                                                                  
         NMOD1 WORKL,*PRTREC*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         MVC   IPARMS,0(R1)                                                     
         MVC   PH,SPACES                                                        
         MVC   P,SPACES                                                         
         CLC   =C'DOME',IDOME                                                   
         BNE   PRTR02                                                           
         XR    RF,RF                                                            
         IC    RF,ITITLE                                                        
         L     RE,ITITLE                                                        
         LA    RE,0(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(RE)                                                       
         BRAS  RE,PRINT            AND PRINT IT                                 
*                                                                               
PRTR02   DS    0H                                                               
         MVC   TYPE,ITYPE                                                       
         CLI   TYPE,C'R'           SET DEFAULT TYPE AS 'E'                      
         BE    *+8                                                              
         MVI   TYPE,C'E'                                                        
*                                                                               
* ZERO OUT HI-ORDER BYTE OF P1 (ITYPE)                                          
* IF LEFT AS IS, HEXOUT WILL THINK P1 IS A 31-BIT ADDRESS                       
         MVI   IREC,X'00'                                                       
*                                                                               
         L     RF,IREC             GET RECORD LENGTH                            
         AH    RF,IDSPLN                                                        
         XR    R0,R0                                                            
         ICM   R0,3,0(RF)                                                       
         BNZ   *+6                                                              
         DC    H'0'                CHECK PARAMETERS PASSED                      
         STH   R0,RECLEN                                                        
*                                                                               
         CLI   TYPE,C'R'           JUST DUMP RECORD AS HEX?                     
         BNE   PRTR04              NO                                           
         BRAS  RE,DISHEX                                                        
         B     EXIT                                                             
*                                                                               
PRTR04   BRAS  RE,DISDIR           DISPLAY KEY DETAILS                          
         BRAS  RE,DISELEM          DISPLAY REST OF RECORD                       
         B     EXIT                                                             
*                                                                               
***********************************************************************         
* ROUTINE TO DISPLAY DIRECTORY DETAILS                                *         
***********************************************************************         
         SPACE 1                                                                
DISDIR   NTR1  ,                                                                
         XC    PLIST,PLIST                                                      
         LA    R1,PLIST                                                         
         MVC   0(4,R1),IREC                                                     
         XR    R0,R0                                                            
         IC    R0,IDSP1                                                         
         ST    R0,4(R1)                                                         
         BRAS  RE,DISSTR           OUTPUT ALL DIRECTORY INFO                    
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY RECORD IN ELEMENT FORMAT (WHERE POSSIBLE)                   *         
***********************************************************************         
         SPACE 1                                                                
DISELEM  NTR1  ,                                                                
         L     R2,IREC             CHECK ELEMENTS ARE INTACT                    
         LR    R3,R2                                                            
         XR    RF,RF                                                            
         IC    RF,IDSP1                                                         
         AR    R2,RF               R2=A(START OF DATA)                          
         AH    R3,RECLEN           R3=A(END OF RECORD)                          
*                                                                               
DELM02   CR    R2,R3               PAST END OF RECORD?                          
         BH    DELM08              NO                                           
         CLI   0(R2),0             REACHED END OF RECORD?                       
         BE    DELM04              YES                                          
         ICM   RF,1,1(R2)          NEXT ELEMENT                                 
         BZ    DELM08                                                           
         BXH   R2,RF,DELM02                                                     
*                                                                               
DELM04   CR    R2,R3               RECORD CAN HAVE A 0 ON ITS END               
         BE    DELM06              THAT CAN BE INCUDED IN LENGTH                
         CLI   0(R2),0             OF RECORD - HAVE TO CHECK FOR                
         BNE   DELM08              THIS CONDITION                               
         AHI   R2,1                                                             
         CR    R2,R3                                                            
         BNE   DELM08                                                           
*                                                                               
DELM06   BRAS  RE,DMPELM           ELEMENTS ARE OK - DUMP THEM                  
         B     EXIT                                                             
*                                                                               
DELM08   L     R2,IREC             ELEMENTS ARE BAD - DO ALL YOU CAN            
         XR    R0,R0                                                            
         IC    R0,IDSP1                                                         
         AR    R2,R0               R2=A(FIRST DATA)                             
         XR    R3,R3                                                            
         IC    R3,IDSP1            R3=TOTAL LENGTH OUTPUT SO FAR                
*                                                                               
         LA    R1,PLIST                                                         
DELM10   CLI   0(R2),0             POSSIBLE EOR                                 
         BE    DELM12                                                           
         CH    R3,RECLEN                                                        
         BH    DELM12                                                           
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         BZ    DELM14              ZERO LENGTH ELEMENT                          
*                                                                               
         ST    R2,0(R1)                                                         
         ST    RF,4(R1)                                                         
         ST    R3,8(R1)                                                         
         BRAS  RE,DISSTR                                                        
*                                                                               
         AR    R2,RF                                                            
         AR    R3,RF                                                            
         B     DELM10                                                           
*                                                                               
DELM12   CH    R3,RECLEN           ACCOUNT FOR POSSIBLE ZERO ON END             
         BE    EXIT                                                             
         CLI   0(R2),0             ZERO ON END?                                 
         BNE   DELM14              NO                                           
         LA    RF,1(R3)                                                         
         CH    RF,RECLEN           REALLY THE END?                              
         BE    EXIT                YES                                          
*                                                                               
DELM14   MVC   P(L'ERRORT),ERRORT                                               
         MVC   P+L'ERRORT(L'BADELEM),BADELEM                                    
         BRAS  RE,PRINT                                                         
*                                                                               
         SH    RF,RECLEN                                                        
         BNP   EXIT                                                             
*                                                                               
         LA    R1,PLIST                                                         
         ST    R2,0(R1)                                                         
         ST    RF,4(R1)                                                         
         ST    R3,8(R1)                                                         
         BRAS  RE,DISSTR                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DUMP WHOLE RECORD AS HEX                                 *         
***********************************************************************         
         SPACE 1                                                                
DISHEX   NTR1  ,                                                                
         XC    PLIST,PLIST                                                      
         LA    R1,PLIST                                                         
         MVC   0(4,R1),IREC        A(RECORD)                                    
         LH    R0,RECLEN                                                        
         ST    R0,4(R1)            L'RECORD                                     
         BRAS  RE,DISSTR           OUTPUT ALL RECORD INFO                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HEX DUMP RECORD DATA IN ELEMENT FORMAT                   *         
***********************************************************************         
         SPACE 1                                                                
DMPELM   NTR1  ,                                                                
         L     R2,IREC                                                          
         XR    R0,R0                                                            
         IC    R0,IDSP1                                                         
         AR    R2,R0               R2=A(FIRST DATA)                             
         XR    R3,R3                                                            
         IC    R3,IDSP1            R3=TOTAL LENGTH OUTPUT SO FAR                
*                                                                               
         XR    RF,RF                                                            
         LA    R1,PLIST                                                         
DMPEL02  CLI   0(R2),0                                                          
         BE    DMPEL04                                                          
         CH    R3,RECLEN                                                        
         BH    DMPEL04                                                          
*                                                                               
         IC    RF,1(R2)                                                         
         ST    R2,0(R1)                                                         
         ST    RF,4(R1)                                                         
         ST    R3,8(R1)                                                         
         BRAS  RE,DISSTR                                                        
*                                                                               
         AR    R2,RF                                                            
         AR    R3,RF                                                            
         B     DMPEL02                                                          
*                                                                               
DMPEL04  CH    R3,RECLEN           ACCOUNT FOR POSSIBLE ZERO ON END             
         BNL   EXIT                                                             
         ST    R2,0(R1)                                                         
         LHI   RF,1                                                             
         ST    RF,4(R1)                                                         
         ST    R3,8(R1)                                                         
         BRAS  RE,DISSTR                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT OUT AN AREA OF STORAGE FOR A LENGTH                  *         
* NTRY: P1 = A(STORAGE)                                               *         
*       P2 = L'STORAGE                                                *         
*       P3 = START DISPLACEMENT                                       *         
***********************************************************************         
         SPACE 1                                                                
DISSTR   NTR1  ,                                                                
         LM    R2,R4,0(R1)         SET PARAMETERS                               
         MVI   FLAG,0                                                           
*                                                                               
DSTR02   LTR   R5,R3               ANYTHING LEFT TO OUTPUT?                     
         BNP   EXIT                NO                                           
         CHI   R5,32                                                            
         BL    *+8                                                              
         LHI   R5,32                                                            
*                                                                               
         CVD   R4,DUB              OUTPUT START DISPLACEMENT                    
         OI    DUB+7,X'0F'                                                      
         UNPK  P(4),DUB                                                         
         CLI   FLAG,0                                                           
         BNE   *+8                                                              
         MVI   P+5,C'='                                                         
         MVI   FLAG,255                                                         
*                                  HEXOUT DATA TO PRINT LINE                    
         GOTO1 IHEXOUT,DMCB,(R2),P+6,(R5),0                                     
*                                                                               
         LHI   R1,32               MOVE OUT HEX DATA                            
         SLL   R1,1                                                             
         LA    R1,P+8(R1)                                                       
         LR    RF,R5                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R2)                                                    
*                                                                               
         TR    P,TRTAB             TRANSLATE LINE                               
         BRAS  RE,PRINT            AND PRINT IT                                 
*                                                                               
         AR    R2,R5               NEXT DATA                                    
         SR    R3,R5                                                            
         AR    R4,R5               DISPLACEMENT                                 
         B     DSTR02                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DO PRINTING OF A LINE                                    *         
***********************************************************************         
         SPACE 1                                                                
PRINT    NTR1  ,                                                                
         GOTO1 IPRINT,PCB,PLINE,=C'BL01'                                        
         MVC   P,SPACES                                                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
         SPACE 1                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
SPACES   DC    132C' '                                                          
*                                                                               
ERRORT   DC    CL8'*ERROR* '                                                    
BADELEM  DC    CL60'Element structure invalid - HEX dump follows'               
*                                                                               
TRTAB    DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 00-0F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 10-1F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 20-2F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 30-3F                     
         DC    XL16'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F' 4B-4F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B5A5B5C5D5E5F' 50-5F                     
         DC    XL16'60614B4B4B4B4B4B4B4B4B4B6C6D6E6F' 60-6F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B797A7B7C7D7E7F' 70-7F                     
         DC    XL16'4B8182838485868788894B4B4B4B4B4B' 80-8F                     
         DC    XL16'4B9192939495969798994B4B4B4B4B4B' 90-9F                     
         DC    XL16'4BA1A2A3A4A5A6A7A8A94B4B4B4B4B4B' A0-AF                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' B0-BF                     
         DC    XL16'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B' C0-CF                     
         DC    XL16'D0D1D2D3D4D5D6D7D8D94B4B4B4B4B4B' D0-DF                     
         DC    XL16'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B' E0-EF                     
         DC    XL16'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B' F0-FF                     
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
PLIST    DS    XL24                                                             
*                                                                               
IPARMS   DS    0XL24                                                            
ITYPE    DS    0X                                                               
IREC     DS    A                                                                
IDSP1    DS    X                                                                
         DS    X                                                                
IDSPLN   DS    H                                                                
IPRINT   DS    A                                                                
IHEXOUT  DS    A                                                                
IDOME    DS    F                                                                
ITITLE   DS    F                                                                
*                                                                               
RECLEN   DS    H                                                                
TYPE     DS    X                                                                
FLAG     DS    X                                                                
*                                                                               
PCB      DS    4F                                                               
*                                                                               
PLINE    DS    0CL133                                                           
PH       DS    X                                                                
P        DS    CL132                                                            
*                                                                               
SPARE    DS    512C                                                             
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
         PRINT OFF                                                              
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDPRTREC  04/14/16'                                      
         END                                                                    
