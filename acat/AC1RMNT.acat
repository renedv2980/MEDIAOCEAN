*          DATA SET AC1RMNT    AT LEVEL 007 AS OF 05/01/02                      
*CATALP AC1RMNT                                                                 
*                                                                               
***************************************************************                 
*                                                             *                 
* ACRATMNT - DELETES OR RESTORES 1R RELATED RECORDS           *                 
*                                                             *                 
* ON ENTRY, P1  BYTE  0   = ACTION (D,R)                      *                 
*                     1-3 = A(KEY)                            *                 
*           P2  BYTE  0-3 = A(DATAMGR)                        *                 
*                                                             *                 
* ON EXIT,  P2  BYTE  0   = ERROR CODE                        *                 
*                                                             *                 
* THIS MODULE IS INCLUDED IN:                                 *                 
*              ACREP9702                                      *                 
*              ACLFM03                                                          
*              ACCAP08                                                          
***************************************************************                 
         TITLE 'AC1RMNT - 1R RECORD MAINTENANCE - DELETE OR RESTORE'            
AC1RMNT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*1RMNT**,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         MVC   RPARMS(RPARML),0(R1)                                             
         ST    RD,SAVERD           FOR ERROR EXIT                               
*                                                                               
         MVI   ERROR,0             CLEAR RETURN CODE                            
*                                                                               
         LA    RE,BUFFER           CLEAR BUFFER                                 
         ST    RE,AIO                                                           
         LA    RF,L'BUFFER                                                      
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         BAS   RE,SETHIER          GET HIERARCHY                                
*                                                                               
         BAS   RE,SET2A            FORMAT KEY OF RATE RECORD                    
*                                                                               
         CLI   RACTION,C'R'        RESTORE RATES?                               
         BE    RESTIT              YES                                          
         CLI   RACTION,C'D'        DELETE RATES?                                
         BNE   OPTERR              NO, INDICATE AN ERROR                        
         EJECT                                                                  
*                                                                               
*                                  DELETE CHARGE RATE RECORDS (X'2A')           
*                                                                               
DELETIT  OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                FIND ANY RATE RECORDS                        
*                                                                               
         USING ACKEYD,R6                                                        
DELE02   L     R6,AIO                                                           
         CLC   KEY(PCHKTSK-PCHKEY),KEYSAVE                                      
         BNE   DELE04                                                           
*                                                                               
         OI    ACSTATUS,X'80'                                                   
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 SEQ                                                              
         B     DELE02              SEE IF ANYMORE                               
*                                                                               
*                                                                               
*                                  DELETE ADJUST RATE RECORDS (X'19')           
*                                                                               
DELE04   BAS   RE,SET19            SET KEY FOR ADJUST RATE RECORDS              
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                                                             
*                                                                               
         USING ACKEYD,R6                                                        
DELE06   L     R6,AIO                                                           
         CLC   KEY(PAJKOFF-PAJKEY),KEYSAVE SAME COMPANY?                        
         BNE   DELE10                                                           
*                                                                               
         CLC   KEY+(PAJKDOF-PAJKEY)(PAJKTSK-PAJKDOF),RATEKEY+2                  
         BNE   DELE08                                                           
*                                                                               
         OI    ACSTATUS,X'80'                                                   
         GOTO1 WRITE                                                            
*                                                                               
DELE08   GOTO1 SEQ                                                              
         B     DELE06              SEE IF ANYMORE                               
*                                                                               
DELE10   BAS   RE,SET3E            SET KEY FOR TOTAL HOURS RECORDS              
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                                                             
*                                                                               
         USING ACKEYD,R6                                                        
DELE12   L     R6,AIO                                                           
         CLC   KEY(TTHKTIME-TTHKEY),KEYSAVE   SAME KEY?                         
         BNE   DELEX                                                            
*                                                                               
         OI    ACSTATUS,X'80'                                                   
         GOTO1 WRITE                                                            
*                                                                               
DELE14   GOTO1 SEQ                                                              
         B     DELE12              SEE IF ANYMORE                               
*                                                                               
DELEX    B     MODEXIT             END OF DELETE                                
         DROP  R6                                                               
         EJECT                                                                  
         USING ACKEYD,R6                                                        
RESTIT   OI    DMINBTS,X'08'       READ DELETED RECORDS                         
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                FIND ANY RATE RECORDS                        
*                                                                               
REST02   L     R6,AIO                                                           
         CLC   KEY(PCHKTSK-PCHKEY),KEYSAVE                                      
         BNE   REST04                                                           
*                                                                               
         NI    ACSTATUS,X'FF'-X'80'                                             
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 SEQ                                                              
         B     REST02              SEE IF ANYMORE                               
*                                                                               
REST04   BAS   RE,SET19            SET KEY FOR ADJUST RATE RECORDS              
         OI    DMINBTS,X'08'       READ DELETED RECORDS                         
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                                                             
*                                                                               
         USING ACKEYD,R6                                                        
REST06   L     R6,AIO                                                           
         CLC   KEY(PAJKOFF-PAJKEY),KEYSAVE SAME COMPANY?                        
         BNE   REST10                                                           
*                                                                               
         CLC   KEY+(PAJKDOF-PAJKEY)(PAJKTSK-PAJKDOF),RATEKEY+2                  
         BNE   REST08                                                           
*                                                                               
         NI    ACSTATUS,X'FF'-X'80'                                             
         GOTO1 WRITE                                                            
*                                                                               
REST08   GOTO1 SEQ                                                              
         B     REST06              SEE IF ANYMORE                               
*                                                                               
REST10   BAS   RE,SET3E            SET KEY FOR TOTAL HOURS RECORDS              
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
         GOTO1 HIGH                                                             
*                                                                               
         USING ACKEYD,R6                                                        
REST12   L     R6,AIO                                                           
         CLC   KEY(TTHKTIME-TTHKEY),KEYSAVE   SAME KEY?                         
         BNE   RESTX                                                            
*                                                                               
         NI    ACSTATUS,X'FF'-X'80'                                             
         GOTO1 WRITE                                                            
*                                                                               
REST14   GOTO1 SEQ                                                              
         B     REST12              SEE IF ANYMORE                               
*                                                                               
RESTX    B     MODEXIT                                                          
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
OPTERR   MVI   ERROR,79            INDICATE INVALID OPTION                      
         B     MODEXIT                                                          
*                                                                               
DMGERROR MVC   ERROR,DMCB+8        SAVE DATAMGR RETURN CODE                     
*                                                                               
MODEXIT  LA    R1,RPARMS                                                        
         MVC   4(1,R1),ERROR                                                    
         L     RD,SAVERD           EXIT BACK TO CALLER                          
         XMOD1 1                                                                
         EJECT                                                                  
         USING ACKEYD,R4                                                        
SETHIER  NTR1                                                                   
         LA    R4,KEY                                                           
         MVC   KEY,SPACES                                                       
         L     RF,RACCT                                                         
         MVC   KEY(3),0(RF)                                                     
         GOTO1 HIGH                                                             
         CLC   KEY,KEYSAVE                                                      
         BE    *+6                                                              
         DC    H'0'                MUST HAVE LEDGER RECORD                      
*                                                                               
         MVI   ELCODE,ACLELQ       GET HEIRARCHY ELEMENT                        
         BAS   RE,GETELIO                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACLELD,R6                                                        
         MVC   LDGTLVA,ACLVALS     GET HIERARCHY LEVELS                         
         MVC   LDGTLVB,ACLVALS+(L'ACLVALS*1)                                    
         MVC   LDGTLVC,ACLVALS+(L'ACLVALS*2)                                    
         MVC   LDGTLVD,ACLVALS+(L'ACLVALS*3)                                    
*                                                                               
SETHX    XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         USING PCHRECD,R6                                                       
SET2A    NTR1                                                                   
         L     R5,RACCT            BUILD KEY FOR RATE RECORD                    
         L     R6,AIO                                                           
*                                                                               
         XC    PCHKEY,PCHKEY                                                    
         MVI   PCHKTYP,PCHKTYPQ                                                 
         MVC   PCHKCPY,0(R5)                                                    
*                                                                               
         LA    R1,3(R5)            OFFICE LEVEL                                 
         SR    RE,RE                                                            
         IC    RE,LDGTLVA                                                       
         SH    RE,=H'1'                                                         
         EXMVC RE,PCHKDOF,0(R1)                                                 
         OC    PCHKDOF,SPACES                                                   
*                                                                               
         LA    R1,3(R5)            DEPARTMENT                                   
         SR    R0,R0                                                            
         IC    R0,LDGTLVA                                                       
         AR    R1,R0                                                            
*                                                                               
         SR    RE,RE                                                            
         IC    RE,LDGTLVB                                                       
         SR    RE,R0                                                            
         SH    RE,=H'1'                                                         
         EXMVC RE,PCHKDEP,0(R1)                                                 
         OC    PCHKDEP,SPACES                                                   
*                                                                               
         LA    R1,3(R5)            SUB-DEPARTMENT                               
         SR    R0,R0                                                            
         IC    R0,LDGTLVB                                                       
         AR    R1,R0                                                            
*                                                                               
         SR    RE,RE                                                            
         IC    RE,LDGTLVC                                                       
*                                                                               
         SR    R0,R0                                                            
         IC    R0,LDGTLVB                                                       
*                                                                               
         SR    RE,R0                                                            
         SH    RE,=H'1'                                                         
         EXMVC RE,PCHKSUB,0(R1)                                                 
         OC    PCHKSUB,SPACES                                                   
*                                                                               
         LA    R1,3(R5)            STAFF                                        
         SR    R0,R0                                                            
         IC    R0,LDGTLVC                                                       
         AR    R1,R0                                                            
*                                                                               
         SR    RE,RE                                                            
         IC    RE,LDGTLVD                                                       
*                                                                               
         SR    R0,R0                                                            
         IC    R0,LDGTLVC                                                       
*                                                                               
         SR    RE,R0                                                            
         SH    RE,=H'1'                                                         
         EXMVC RE,PCHKSTF,0(R1)                                                 
         OC    PCHKSTF,SPACES                                                   
         MVC   RATEKEY,PCHKEY      SAVE THE KEY FOR ADJUST RECORDS              
*                                                                               
SET2AX   XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         USING PAJRECD,R6                                                       
SET19    NTR1                                                                   
         L     R5,RACCT            BUILD KEY FOR ADJUST RATE RECORD             
         L     R6,AIO                                                           
*                                                                               
         XC    PAJKEY,PAJKEY                                                    
         MVI   PAJKTYP,PAJKTYPQ                                                 
         MVC   PAJKCPY,0(R5)                                                    
*                                                                               
SET19X   XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         USING TTHRECD,R6                                                       
SET3E    NTR1                                                                   
         L     R5,RACCT            BUILD KEY FOR TIME TOTAL HOURS               
         L     R6,AIO                                                           
*                                                                               
         XC    TTHKEY,TTHKEY                                                    
         MVI   TTHKTYP,TTHKTYPQ                                                 
         MVI   TTHKSUB,TTHKSUBQ                                                 
         MVC   TTHKCULA,0(R5)                                                   
*                                                                               
SET3EX   XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 RDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'ACCOUNT',KEY,AIO           
         CLI   DMCB+8,0            ANY ERRORS ?                                 
         BE    HIGHX               NO, OK                                       
         CLI   DMCB+8,2            YES, IS RECORD DELETED ?                     
         BNE   DMGERROR            NO, MUST BE BAD ERROR                        
*                                                                               
HIGHX    XIT1                                                                   
*                                                                               
SEQ      NTR1                                                                   
         GOTO1 RDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'ACCOUNT',AIO,AIO           
         CLI   DMCB+8,0            ANY ERRORS ?                                 
         BE    SEQX                NO, OK                                       
         CLI   DMCB+8,2            YES, IS RECORD DELETED ?                     
         BNE   DMGERROR            NO, MUST BE BAD ERROR                        
SEQX     XIT1                                                                   
*                                                                               
WRITE    NTR1                                                                   
         GOTO1 RDATAMGR,DMCB,(DMINBTS,=C'DMWRT'),=C'ACCOUNT',AIO,AIO            
         CLI   DMCB+8,0                                                         
         BNE   DMGERROR                                                         
*                                                                               
WRITEX   XIT1                                                                   
         EJECT                                                                  
GETELIO  L     R6,AIO                                                           
         GETEL (R6),DATADISP,ELCODE                                             
*                                                                               
DATADISP DC    H'49'                                                            
SPACES   DC    256C' '                                                          
         EJECT                                                                  
XIT      XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
RELO     DS    A                                                                
*                                                                               
RPARMS   DS    0D                                                               
RACTION  DS    0C                  ACTION = D OR R                              
RACCT    DS    A                   ADDRESS OF 1R ACCOUNT                        
RDATAMGR DS    A                   A(DATAMGR)                                   
RPARML   EQU   *-RPARMS                                                         
*                                                                               
LDGTLVA  DS    C                   HIERARCHY LEVELS                             
LDGTLVB  DS    C                                                                
LDGTLVC  DS    C                                                                
LDGTLVD  DS    C                                                                
*                                                                               
AIO      DS    A                                                                
DMCB     DS    6F                                                               
SAVERD   DS    A                   TO EXIT BACK TO CALLER                       
*                                                                               
ERROR    DS    C                   ERROR FLAG, OR DATAMGR ERROR CODE            
KEYSAVE  DS    CL42                SAVE KEY BEFORE DATAMGR CALL                 
RATEKEY  DS    CL(L'PCHKEY)        SAVE KEY OF RATE RECORD                      
*                                                                               
ELCODE   DS    X                   ELEMENT CODE                                 
ELEMENT  DS    CL256               PERSON ELEMENT                               
*                                                                               
DMINBTS  DS    X                   DATAMGR INTERFACE BYTE                       
*                                                                               
KEY      DS    CL42                                                             
         ORG   KEY                                                              
BUFFER   DS    CL2000              BUFFER FOR DATAMGR  (AIO)                    
WORKX    EQU   *                                                                
         EJECT                                                                  
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007AC1RMNT   05/01/02'                                      
         END                                                                    
