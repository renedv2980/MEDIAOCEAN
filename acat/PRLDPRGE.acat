*          DATA SET PRLDPRGE   AT LEVEL 006 AS OF 03/29/16                      
*PROCESS USING(WARN(15))                                                        
*CATALP PRLDPRGE                                                                
         TITLE 'LDPURGE - PRT - LOAD PURGE TEST'                                
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*                                                                               
*        UPSI  00000001 PURGE X'80' DELETES                                     
*        UPSI  00000010 PURGE X'C3' CLOSEOUTS                                   
*        UPSI  00000100 PURGE TEST AGY RECORDS                                  
*        UPSI  00001000 PURGE EMPTY RECORDS                                     
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,PRLDPRGE                                                       
*                                                                               
CNTL     EQU   27                  DISPLACEMENT OF CONTROL BYTES                
*                                  FROM START OF REC                            
AGY      EQU   0                   DISPLACEMENT OF AGENCY ID                    
         SPACE 2                                                                
* GET UPSI ON FIRST TIME                                                        
PU       BC    0,PU0                                                            
         OI    PU+1,X'F0'          SET BRANCH                                   
         LR    R0,R1               SAVE R1                                      
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
         LR    R1,R0               RESTORE R1                                   
         SPACE 2                                                                
PU0      TM    UPSI,X'0F'          ANY PURGES                                   
         BC    8,XIT               NO                                           
         L     R3,0(R1)            POINT TO RECORD                              
*                                                                               
         CLI   3(R3),X'D2'         TEST AUTOPAY RECORD                          
         BE    APY                                                              
         TM    UPSI,X'08'          TEST FOR EMPTY RECORD                        
         BZ    PU1                                                              
         CLC   25(2,R3),MINLEN                                                  
         BH    PU1                                                              
         MVI   0(R1),255                                                        
         B     XIT                                                              
*                                                                               
PU1      TM    CNTL(R3),X'C0'      ANY DELETE INDS ON                           
         BC    7,ACAN              YES. A CANDIDATE                             
* NO DELETE INDICATORS. TEST FOR TEST AGENCY RECORD PURGE OPTION                
PU2      TM    UPSI,X'04'                                                       
         BC    1,TAG               YES. GO TEST FOR TEST AG                     
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
* THIS IS A CANDIDATE FOR A PURGE                                               
ACAN     TM    UPSI,X'03'          DELETE OR CLOSEOUT PURGE                     
         BC    8,PU2               NO, GO TEST FOR TEST AG                      
         BC    1,BOTH              DELETE BOTH X'80' AND X'C0'                  
         SPACE 2                                                                
* DELETE ONE OR THE OTHER                                                       
         TM    UPSI,X'01'                                                       
         BC    1,O80               ONLY X'80'                                   
         TM    UPSI,X'02'                                                       
         BC    1,OC0               ONLY X'C0'                                   
         B     PU2                                                              
         SPACE                                                                  
* DELETE ONLY X'80'                                                             
O80      TM    CNTL(R3),X'C0'                                                   
         BC    1,PU2               BOTH ON, NO DELETE                           
         TM    CNTL(R3),X'80'                                                   
         BC    8,PU2                                                            
         B     TSTPURG             TEST CAN BE PRUGED - ONLY X'80'              
O801     MVI   0(R1),255                                                        
         B     XIT                                                              
         SPACE 2                                                                
* DELETE ONLY X'C0'                                                             
OC0      TM    CNTL(R3),X'C0'                                                   
         BC    1,O801                                                           
         B     PU2                                                              
         SPACE 2                                                                
* DELTEE BOTH                                                                   
BOTH     TM    CNTL(R3),X'C0'                                                   
         BC    1,O801                                                           
         TM    CNTL(R3),X'80'                                                   
         BC    1,TSTPURG           TEST CAN BE PURGED - ONLY X'80'              
         B     PU2                                                              
         SPACE 2                                                                
* TEST AGENCY PURGE                                                             
TAG      DS    0H                                                               
         CLI   3(R3),X'20'         TEST AGY PURGE ONLY FOR BUYS                 
         BNE   XIT                                                              
*                                                                               
         CLC   AGY(2,R3),=C'T1'                                                 
         BE    O801                                                             
         CLC   AGY(2,R3),=C'T2'                                                 
         BE    O801                                                             
         CLC   AGY(2,R3),=C'T3'                                                 
         BE    O801                                                             
         B     XIT                                                              
         EJECT                                                                  
*                                  TEST CAN BE PURGED                           
TSTPURG  DS    0H                                                               
         CLI   3(R3),X'20'         TEST BUY REC                                 
         BNE   O801                                                             
*                                  NO-OP BUY RECORD PURGE                       
         B     XIT                                                              
*                                                                               
*                                  TRY INSERTION ORDER STATUS FIRST             
         LA    R2,33(R3)                                                        
         SR    R7,R7                                                            
         IC    R7,25(R3)                                                        
         SLL   R7,8                                                             
         IC    R7,26(R3)                                                        
         LA    R7,0(R3,R7)                                                      
         MVI   0(R7),0             SET EOR                                      
TP2      DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    O801                OK TO PURGE                                  
         CLI   0(R2),X'70'                                                      
         BE    TP4                                                              
         CLI   0(R2),X'25'         PAY ELEM                                     
         BE    TP6                                                              
         CLI   0(R2),X'26'         BILL ELEM                                    
         BE    TP7                                                              
         CLI   0(R2),X'28'         FINANCIAL "OPEN" BILLING                     
         BE    TP7                                                              
TP3      DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     TP2                                                              
*                                                                               
TP4      DS    0H                                                               
         LA    R4,2(R2)                                                         
         B     TP8                                                              
TP6      DS    0H                                                               
         LA    R4,2(R2)                                                         
         B     TP8                                                              
TP7      DS    0H                                                               
         LA    R4,5(R2)                                                         
         B     TP8                                                              
*                                                                               
TP8      DS    0H                                                               
         OC    0(3,R4),0(R4)       TEST ACTIVE ELEM                             
         BNZ   PU2                 YES - NO PURGE                               
         B     TP3                                                              
*                                                                               
*                                                                               
APY      DS    0H                  DELETE AUTOPAY RECS > 2 WEEKS OLD            
         OC    APYDATE,APYDATE                                                  
         BNZ   APY2                                                             
         LR    R7,R1               SAVE R1                                      
         GOTO1 =V(DATCON),DMCB,(5,WORK),(0,WORK)       TODAY'S DATE             
         GOTO1 =V(ADDAY),DMCB,WORK,WORK,-14            MINUS 14 DAYS            
         GOTO1 =V(DATCON),DMCB,(0,WORK),(2,APYDATE)                             
         XC    APYDATE,=X'FFFF'                        COMPLEMENT DATE          
         LR    R1,R7               RESTORE R1                                   
*                                                                               
* DATES ARE COMPLEMENTED SO BRANCHES LOOK BACKWARDS                             
*                                                                               
APY2     CLC   4(2,R3),APYDATE     KEEP RECS < CUTOFF DATE                      
         BL    PU1                                                              
         MVI   0(R1),255           PURGE RECS > CUTOFF                          
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
MINLEN   DC    H'34'                                                            
UPSI     DC    X'00'                                                            
DMCB     DS    6F                                                               
WORK     DS    CL24                                                             
APYDATE  DC    XL2'00'             AUTOPAY DATE FOR DELETION                    
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006PRLDPRGE  03/29/16'                                      
         END                                                                    
