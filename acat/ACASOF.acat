*          DATA SET ACASOF     AT LEVEL 012 AS OF 10/13/97                      
*CATALP ACASOF                                                                  
       TITLE 'RETURN UNBILLED AMOUNT OF A TRANSACTION AS OF ...'                
*                                                                               
***********************************************************************         
* DOCUMENTATION HERE                                                            
***********************************************************************         
ACASOF   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 ASOWRKX-ASOWRKD,**ASOF**,CLEAR=YES,RR=RE                         
         USING ASOWRKD,RC          RC=A(LOCAL W/S)                              
         ST    RE,ASORELO          SAVE RELO FACTOR                             
         LR    RA,R1               RA=A(PARAMETER BLOCK)                        
         USING ACASOFD,RA                                                       
         SR    RF,RF                                                            
         ICM   RF,15,ACACOMF       RF=A(COMFACS)                                
         BNZ   *+6                                                              
         DC    H'0'                A(COMFACS) MISSING IN PARM BLOCK             
         USING COMFACSD,RF                                                      
         MVC   ASODATCN,CDATCON                                                 
         MVC   ASODMGR,CDATAMGR                                                 
         DROP  RF                                                               
*                                                                               
         MVI   ASOSPACE,C' '                                                    
         MVC   ASOSPACE+1(L'ASOSPACE-1),ASOSPACE                                
*                                                                               
         TM    ACAMODE,ACAMINIT    INITIALIZE (PREREAD BILLS)                   
         BO    INIT                                                             
*                                                                               
         TM    ACAMODE,ACAMTRN     PROCESS A TRANSACTION                        
         BO    TRN                                                              
*                                                                               
         DC    H'0'                UNKNOWN MODE                                 
*                                                                               
         EJECT                                                                  
***********************************************************************         
* PROCESS THE TRANSACTION                                             *         
***********************************************************************         
*                                                                               
TRN      ICM   R2,15,ACAATRN       R2=A(TRANSACTION RECORD)                     
         USING TRNRECD,R2                                                       
         LA    R3,TRNRFST          R3=A(FIRST ELEMENT ON NEW FILE)              
         TM    ACAMODE,ACAMNEWA    NEW FILE STRUCTURE                           
         BO    *+10                                                             
         LA    R3,ACCORFST                                                      
         AR    R3,R2               R3=A(FIRST ELEMENT ON OLD FILE)              
*                                                                               
         USING TRNELD,R3                                                        
         CLI   0(R3),TRNELQ        TRANSACTION ELEMENT?                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ZAP   ACAUNBLD,=P'0'                                                   
         ZAP   ACAUNBHR,=P'0'                                                   
         BAS   RE,GETMOS                                                        
         CLI   ACAFROM,ACAFTRN     TEST TRANSACTION METHOD                      
         BE    *+12                                                             
*                                                                               
         BAS   RE,EXCLUDE          EXCLUDED FROM BILLING?                       
         BE    TRNX                                                             
*                                                                               
         ZAP   ACAUNBLD,TRNAMNT    ASSUME FULLY BILLABLE                        
         ZAP   ASOTRNAM,TRNAMNT    SAVE TRAN AMOUNT                             
*                                                                               
         BAS   RE,SETHOURS         SET ASOTRNHR                                 
         ZAP   ACAUNBHR,ASOTRNHR   ASSUME FULLY BILLABLE HOURS ALSO             
*                                                                               
         CLC   ASOTRMOS,ACASOFDT                                                
         BNH   TRN10                                                            
         ZAP   ACAUNBLD,=P'0'                                                   
         ZAP   ASOTRNAM,=P'0'                                                   
         ZAP   ACAUNBHR,=P'0'                                                   
         ZAP   ASOTRNHR,=P'0'                                                   
*                                                                               
TRN10    XR    R1,R1                                                            
TRN20    CLI   0(R3),0             EOR                                          
         BE    TRN50                                                            
*                                                                               
         CLI   0(R3),RBIELQ        REVERSAL BILLING INFO ELEMENT                
         BNE   *+8                                                              
         BAS   RE,REV                                                           
*                                                                               
         CLI   0(R3),CUBELQ        CLIENT BILLING REVERSAL                      
         BNE   *+8                                                              
         BAS   RE,CUB                                                           
*                                                                               
         IC    R1,1(R3)            BUMP THROUGH RECORD                          
         LA    R3,0(R1,R3)                                                      
         B     TRN20                                                            
*                                                                               
*                                                                               
TRN50    L     R3,ACAPTABF         CHECK PTA ELEMENTS                           
         USING PTAELD,R3                                                        
TRN70    CLI   0(R3),0             E O BUFFER                                   
         BE    TRNX                                                             
         CLI   0(R3),PTAELQ        NOT EMPTY                                    
         BNE   TRN75                                                            
         CLI   ACAFROM,ACAFTRN     TEST TRANSACTION METHOD                      
         BNE   *+12                                                             
         CLI   PTATYPE,PTATRAL     TEST BILLING PTA                             
         BNE   TRN75                                                            
         BAS   RE,PTA                                                           
*                                                                               
TRN75    IC    R1,1(R3)            BUMP THROUGH BUFFER                          
         LA    R3,0(R1,R3)                                                      
         B     TRN70                                                            
TRNX     B     ASOX                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* SEE IF THE PTA EL IN 0(R3) AFFECTED THE BILLABLE AS OF THE ASOF DATE          
***********************************************************************         
         USING PTAELD,R3                                                        
PTA      NTR1                                                                   
         CLI   PTATYPE,0           IS THIS A FILLER PTA                         
         BE    PTAX                YES                                          
*                                                                               
         TM    PTASTAT1,PTASPEND   IS THIS ACTIVITY PENDING                     
         BO    PTAX                YES                                          
*                                                                               
         CLC   PTADATE,ACASAEND    IS THIS ACTIVITY TOO NEW                     
         BH    PTAX                YES IGNORE                                   
*                                                                               
         LA    R4,PTAMOA                                                        
         OC    PTAMOA,PTAMOA       IS MOS SET IN ELEMENT                        
         BNZ   PTA30               YES                                          
*                                                                               
         LA    R4,PTAWDAT          TRY WRITE OFF DATE                           
         CLI   PTATYPE,PTATWOF     WRITE OFF                                    
         BE    PTA30                                                            
         CLI   PTATYPE,PTATWOFR    WRITE OFF RECOVERY                           
         BE    PTA30                                                            
*                                                                               
         CLI   PTATYPE,PTATRAL     IS THIS A BILLING PTA                        
         BE    *+6                 NO, UNKNOWN TYPE                             
         DC    H'0'                                                             
*                                                                               
         MVC   ASONUM,PTARBLNO     GET BILLING MOS                              
         MVC   ASODATE,PTARBLDT                                                 
         BAS   RE,GETBYN_D         GET THE MOS OF THE PTA ELEMENT               
         LA    R4,ASOMOS           MOS OF THIS PTA ELEMENT                      
*                                                                               
         CLC   ASOMOS,ASODATE                                                   
         BE    PTA30                                                            
         OI    ACAMODE,X'20'       FLAG BILL MOS NEW BDATE "MOS"                
*                                                                               
PTA30    CLC   ACASOFDT,0(R4)      DID THIS HAPPEN AFTER MY ASOF DATE           
         BL    PTAX                YES                                          
         SP    ACAUNBLD,PTANET                                                  
         LH    R1,PTAHOURS                                                      
         CVD   R1,ASODUB                                                        
         SP    ACAUNBHR,ASODUB                                                  
*                                                                               
PTAX     B     ASOX                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* SEE IF THE BILLS REVERSED AFFECTED THE BILLABLE AS OF THE ASOF DATE           
***********************************************************************         
         USING RBIELD,R3                                                        
REV      NTR1                                                                   
*                                                                               
         MVC   ASODATE,RBIDAT                                                   
         BAS   RE,GETBYDAT         GET THE MOS OF THE ORIGINAL BILL             
*                                                                               
         CLC   ACASOFDT,ASOMOS     DID THIS HAPPEN AFTER MY ASOF DATE           
         BL    REVX                YES                                          
         SP    ACAUNBLD,ASOTRNAM                                                
         SP    ACAUNBHR,ASOTRNHR                                                
*                                                                               
         MVC   ASODATE,RBIUND                                                   
         BAS   RE,GETBYDAT         GET THE MOS OF THE REVERSAL                  
*                                                                               
         CLC   ACASOFDT,ASOMOS     DID THIS HAPPEN AFTER MY ASOF DATE           
         BL    REVX                YES                                          
         AP    ACAUNBLD,ASOTRNAM   REVERSED WITHIN RANGE, CONSIDER UNBL         
         AP    ACAUNBHR,ASOTRNHR                                                
*                                                                               
REVX     B     ASOX                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* SEE IF THE BILLS REVERSED AFFECTED THE BILLABLE AS OF THE ASOF DATE           
* A27 REVERSAL VERSION                                                          
***********************************************************************         
         USING CUBELD,R3                                                        
CUB      NTR1                                                                   
*                                                                               
         MVC   ASONUM,CUBNO                                                     
         MVC   ASODATE,CUBDTE                                                   
         BAS   RE,GETBYN_D         GET THE MOS OF THE ORIGINAL BILL             
*                                                                               
         CLC   ACASOFDT,ASOMOS     DID THIS HAPPEN AFTER MY ASOF DATE           
         BL    CUBX                YES                                          
         ICM   R1,15,CUBAMNT       NO SUBTRACT OUT BILLED AMOUNT                
         CVD   R1,ASODUB                                                        
         SP    ACAUNBLD,ASODUB                                                  
         ICM   R1,15,CUBHRS        AND HOURS                                    
         CVD   R1,ASODUB                                                        
         SP    ACAUNBHR,ASODUB                                                  
***********************************************************************         
*    CODE SHOULD BE MODIFIED FOR UK, DATA MAY BE IN ANOTHER ELEMENT   *         
***********************************************************************         
*&&US                                                                           
         MVC   ASONUM,CUBUNNO                                                   
         MVC   ASODATE,CUBUNDTE                                                 
         BAS   RE,GETBYN_D         GET THE MOS OF THE REVERSAL                  
*                                                                               
         CLC   ACASOFDT,ASOMOS     DID THIS HAPPEN AFTER MY ASOF DATE           
         BL    CUBX                YES                                          
         ICM   R1,15,CUBAMNT       NO SUBTRACT OUT BILLED                       
         CVD   R1,ASODUB                                                        
         AP    ACAUNBLD,ASODUB     REVERSED WITHIN RANGE                        
         ICM   R1,15,CUBHRS                                                     
         CVD   R1,ASODUB                                                        
         AP    ACAUNBHR,ASODUB                                                  
*&&                                                                             
CUBX     B     ASOX                                                             
         DROP  R3                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* RETURN, IN ASOMOS, THE MOS OF THE BILL NUMBER IN ASONUM THAT HAS A            
*BILL DATE OF ASODATE                                                           
*----------------------------------------------------------------------         
*                                                                               
         USING PTAELD,R3                                                        
GETBYN_D NTR1                                                                   
*                                                                               
         XC    ASOMOS,ASOMOS                                                    
         TM    ACAMODE,ACAMNOLO    DID THEY LOOK AHEAD FOR BILLS                
         BO    GETM60              NO, SET MOS FROM DATE                        
*                                                                               
         USING ACABLTBD,R2                                                      
         LA    R0,ACABMAXB                                                      
         L     R2,ACABILLS                                                      
GETM20   OC    ACABLNO,ACABLNO     ANY MORE DATA                                
         BZ    GETM60              NO, SET MOS FROM ASODATE                     
*                                                                               
         CLC   ASONUM,ACABLNO      MUST MATCH BILL NUMBER/DATE                  
         BNE   GETM30                                                           
         CLC   ASODATE,ACABLDT                                                  
         BE    GETM50                                                           
*                                                                               
GETM30   LA    R2,ACABTBLN(R2)                                                  
         BCT   R0,GETM20                                                        
         B     GETM60              CAN'T FIND BILL NUMBER IN TABLE              
*                                                                               
GETM50   MVC   ASOMOS,ACABMOS                                                   
         B     ASOX                                                             
*                                                                               
GETM60   BAS   RE,SETMOS                                                        
         B     ASOX                                                             
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*----------------------------------------------------------------------         
* RETURN, IN ASOMOS, THE MOS IF THE BILL WHICH HAS THE BILLDATE IN              
* ASODATE  ASOMOS IS X'YM' ASODATE IS COMPRESSED                                
* IF ASOMOS = X'0000', BILL NOT FOUND                                           
*----------------------------------------------------------------------         
*                                                                               
         USING PTAELD,R3                                                        
GETBYDAT NTR1                                                                   
         USING ACABLTBD,R2                                                      
         XC    ASOMOS,ASOMOS                                                    
         TM    ACAMODE,ACAMNOLO    DID THEY LOOK AHEAD FOR BILLS                
         BO    GBD60               NO, SET MOS FROM DATE                        
*                                                                               
         LA    R0,ACABMAXB                                                      
         L     R2,ACABILLS                                                      
GBD20    OC    ACABLNO,ACABLNO     ANY MORE DATA                                
         BZ    GBD60               NO, SET ASOMOS FROM ASODATE                  
*                                                                               
         CLC   ASODATE,ACABLRDT                                                 
         BE    GBD50                                                            
         LA    R2,ACABTBLN(R2)                                                  
         BCT   R0,GBD20                                                         
         B     GBD60               CAN'T FIND BILL NUMBER IN TABLE              
*                                                                               
GBD50    MVC   ASOMOS,ACABMOS                                                   
         B     ASOX                                                             
*                                                                               
GBD60    BAS   RE,SETMOS                                                        
         B     ASOX                                                             
         DROP  R2,R3                                                            
*----------------------------------------------------------------------         
* SET ASOMOS FROM ASODATE, IF YOU CAN'T GET THE BILL AND NEED A MOS             
*----------------------------------------------------------------------         
SETMOS   NTR1                                                                   
         GOTO1 ASODATCN,ASODMCB,(2,ASODATE),(1,ASODUB) CNV FROM PACK            
         MVC   ASOMOS,ASODUB       SAVE ONLY THE X'YM'                          
         B     ASOX                                                             
*----------------------------------------------------------------------         
* SET TRANSACTION MONTH OF SERVIXE                                              
*----------------------------------------------------------------------         
GETMOS   SR    R0,R0                                                            
         LR    RF,R3                                                            
GETMOS3  IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(RF),TRSELQ                                                     
         BNE   GETMOS3                                                          
         USING TRSELD,RF                                                        
         MVC   ASOTRMOS,TRSPMOS                                                 
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* SET TRANSACTION HOURS (ASOTRNHR)                                              
*----------------------------------------------------------------------         
SETHOURS SR    R0,R0                                                            
         ZAP   ASOTRNHR,=P'0'                                                   
         LR    RF,R3                                                            
SETH03   IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0                                                          
         BER   RE                                                               
         CLI   0(RF),PRTELQ                                                     
         BNE   SETH03                                                           
         USING PRTELD,RF                                                        
         ZAP   ASOTRNHR,PRTHOUR                                                 
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* RETURN EQUAL IF THE TRANSACTION IS TO BE EXCLUDED FROM BILLING                
* CURRENTLY EXCLUDE TYPES 14, 57 AND 34-TRANSFER TO (WHEN FROM NBILL)           
*----------------------------------------------------------------------         
EXCLUDE  NTR1                                                                   
         CLI   TRNTYPE-TRNELD(R3),14  TYPE 14                                   
         BE    EXCLUD2             CHECK IF CBI TYPE 14 FIRST                   
*                                                                               
         CLI   TRNTYPE-TRNELD(R3),57                                            
         BE    EXCEQ                                                            
*                                                                               
         CLI   TRNTYPE-TRNELD(R3),34                                            
         BNE   EXCNEQ              RETURN NEQ, CALC BABLE                       
*                                                                               
         BAS   RE,CHK4ETO          IS THIS A TRANSFER TO                        
         BNE   EXCNEQ              NO, ACCEPT TRANSFER FROM TYPE 34'S           
*                                                                               
EXCLUD2  BAS   RE,CHKTRX           IF HOLD FROM BILLING STATUS IS FOUND         
         BE    EXCEQ               EXCLUDE FROM UNBILLED                        
         B     EXCNEQ                                                           
*                                                                               
*                                                                               
EXCEQ    B     EQUAL                                                            
EXCNEQ   B     NOTEQUAL                                                         
*                                                                               
*        TRN IS R3, EXCLUDE ONLY TRANSFER TOS                                   
*        IF ITS A TRANSFER FROM, DON'T EXCLUDE FROM                             
CHK4ETO  NTR1                                                                   
         USING PXDELD,R3                                                        
         XR    R1,R1                                                            
CHK4E10  CLI   0(R3),0                                                          
         BE    EXCNEQ                                                           
*                                                                               
         CLI   0(R3),PXDELQ                                                     
         BNE   CHK4E50                                                          
         CLI   PXDTYPE,PXDTTO                                                   
         BE    EXCEQ                                                            
*                                                                               
CHK4E50  IC    R1,1(R3)                                                         
         LA    R3,0(R1,R3)                                                      
         B     CHK4E10                                                          
*                                                                               
         EJECT                                                                  
* TRN IS R3, RETURN EQUAL IF HOLD  FROM ALLOCATION BIT IS ON IN TRXEL           
* DONE BECAUSE INP TYPE 34'S DONT PUT A PTA ON THE ORIGINAL CHARGE              
CHKTRX   NTR1                                                                   
         USING TRXELD,R3                                                        
         XR    R1,R1                                                            
CHKTRX10 CLI   0(R3),0             NO TRXEL                                     
         BE    EXCNEQ              MUST BE OLD TYPE 34                          
*                                                                               
         CLI   0(R3),TRXELQ                                                     
         BNE   CHKTRX50                                                         
         TM    TRXSTA1,TRXSXALC    EXCLUDE FROM ALLOCATION                      
         BO    EXCEQ               YES, TRAN ORIGINATED IN NBILL                
*                                                                               
CHKTRX50 IC    R1,1(R3)                                                         
         LA    R3,0(R1,R3)                                                      
         B     CHKTRX10                                                         
*                                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* BUILD A TABLE OF BILLS                                                        
*----------------------------------------------------------------------         
INIT     DS    0H                                                               
         BAS   RE,CLRBUFF                                                       
         USING ACABLTBD,R2                                                      
         L     R2,ACABILLS                                                      
         USING TRNRECD,R3                                                       
         LA    R3,ASOKEY                                                        
         MVC   TRNKEY,ASOSPACE                                                  
         L     RE,ACAAJOB                                                       
         MVC   TRNKCULA,0(RE)      TR                                           
         MVC   TRNKWORK,=C'99'                                                  
         BAS   RE,HIGH                                                          
*                                                                               
INI10    LA    R3,ASOKEY                                                        
         CLC   TRNKEY(TRNKWORK-TRNKEY+L'TRNKWORK),ASOKEYSV                      
         BNE   INIX                                                             
         CLC   TRNKREF,ASOSPACE    IS THIS A TRANSACTION (BILL)                 
         BNH   INI80               NO                                           
*                                                                               
         BAS   RE,GETREC                                                        
         LA    R3,ASOIO                                                         
         LA    R4,TRNRFST                                                       
         USING TRNELD,R4                                                        
         CLI   TRNEL,TRNELQ                                                     
         BNE   INI80               GET NEXT                                     
*                                                                               
         GOTO1 ASODATCN,ASODMCB,(1,TRNDATE),(2,ACABLDT)                         
         MVC   ACABLNO,TRNREF                                                   
         MVC   ACABLRDT,TRNNARR+33                                              
         MVC   ACABMOS,TRNRSMOS    MOS FROM KEY                                 
*                                                                               
         LA    R2,ACABTBLN(R2)     NEXT TABLE ENTRY                             
*                                                                               
INI80    BAS   RE,SEQ              NEXT TRANSACTION                             
         B     INI10                                                            
*                                                                               
INIX     B     ASOX                                                             
*                                                                               
CLRBUFF  NTR1                                                                   
         L     RE,ACABILLS                                                      
         LH    RF,=Y(ACABTBSZ)                                                  
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     ASOX                                                             
*                                                                               
HIGH     NTR1                                                                   
         MVC   ASOCOMM,=CL8'DMRDHI'  READ HIGH                                  
         MVC   ASOKEYSV,ASOKEY                                                  
         GOTO1 ASODMGR,ASODMCB,ASOCOMM,=C'ACCDIR ',ASOKEY,ASOKEY                
         B     ASOX                                                             
*                                                                               
SEQ      NTR1                                                                   
         MVC   ASOCOMM,=CL8'DMRSEQ'  READ SEQUENTIAL                            
         MVC   ASOKEYSV,ASOKEY                                                  
         GOTO1 ASODMGR,ASODMCB,ASOCOMM,=C'ACCDIR ',ASOKEY,ASOKEY                
         B     ASOX                                                             
*                                                                               
GETREC   NTR1  WORK=(R2,12)                                                     
         USING ACCRECD,R3                                                       
         LA    R3,ASOKEY                                                        
         LA    R3,ACCKDA           ADDRESS DISK ADDRESS FOR GETREC              
         GOTO1 ASODMGR,ASODMCB,=C'GETREC  ',=C'ACCMST ',(R3),ASOIO,(R2)         
         B     ASOX                                                             
*                                                                               
EQUAL    CR    RB,RB                                                            
         B     ASOX                                                             
*                                                                               
NOTEQUAL LTR   RB,RB                                                            
         B     ASOX                                                             
*                                                                               
ASOX     XIT1                                                                   
         EJECT                                                                  
*----------------------------------------------------------------------         
         LTORG                                                                  
*----------------------------------------------------------------------         
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER ACPTAASO LOCAL WORKING STORAGE                       *         
***********************************************************************         
         SPACE 1                                                                
ASOWRKD  DSECT                                                                  
ASODUB   DS    D                                                                
ASODATCN DS    A                   A(DATCON)                                    
ASODMGR  DS    A                   A(DATAMGR)                                   
ASODMCB  DS    8F                                                               
ASOFILE  DS    CL8                                                              
ASOCOMM  DS    CL8                                                              
ASOELEM  DS    XL255               AREA FOR PTAEL BUILDING                      
ASORELO  DS    A                   RELOCATE OFFSET                              
*                                                                               
ASODATE  DS    XL2                 PACKED BILLED DATE TO GET A BILL BY          
ASONUM   DS    CL6                 GET A BILL BY BILL NUMBER                    
ASOMOS   DS    XL2                 RETURN MOS VALUE                             
ASOTRMOS DS    XL2                 TRANSACTION MOS                              
*                                                                               
ASOTRNAM DS    PL6                 FULL AMOUNT OF TRANSACTION                   
ASOTRNHR DS    PL6                 FULL HOURS FOR TRANSACTION                   
ASOKEY   DS    CL(ACCKLEN)                                                      
ASOKEYSV DS    CL(ACCKLEN)                                                      
ASOIO    DS    CL2049              IO BUFFER                                    
ASOSPACE DS    CL255                                                            
ASOWRKX  EQU   *                                                                
         SPACE 1                                                                
       ++INCLUDE ACASOFD                                                        
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012ACASOF    10/13/97'                                      
         END                                                                    
