*          DATA SET DMDMGRLA   AT LEVEL 024 AS OF 05/01/02                      
*CATALP DMDMGRLA                                                                
         TITLE 'DMGRLINK - LINK TO DATAMGR MODULES VIA DDSIO'                   
         SPACE 1                                                                
***********************************************************************         
* THIS MODULE LINKS DATAMGR CALLS TO DDSIO. ON FIRST CALL DDSIO IS    *         
* LOADED INTO STORAGE. WHEN CALLING DDSIO THE HIGH ORDER BYTE OF R4   *         
* CONTAINS THE DATAMGR MODULE NUMBER. NOTE THAT THE ROUTINE NUMBER    *         
* CORRESPONDS TO THE LIST OF ADCONS IN DMDDSIO (VDMGRMOD).            *         
***********************************************************************         
         SPACE 1                                                                
         PRINT NOGEN                                                            
DMGRLINK CSECT                                                                  
         ENTRY DDSIO                                                            
         ENTRY DMGRCNT                                                          
         REQUS                                                                  
         DC    C'DMGRLINK'                                                      
*                                                                               
         ENTRY DATAMGR                                                          
         USING *,RF                                                             
DATAMGR  MVI   VADWAIT,0                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DADDS                                                            
         USING *,RF                                                             
DADDS    MVI   VADWAIT,1                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY ISDDS                                                            
         USING *,RF                                                             
ISDDS    MVI   VADWAIT,2                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMDANDX                                                          
         USING *,RF                                                             
DMDANDX  MVI   VADWAIT,3                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DYNALLOC                                                         
         USING *,RF                                                             
DYNALLOC MVI   VADWAIT,4                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY WORKER                                                           
         USING *,RF                                                             
WORKER   MVI   VADWAIT,5                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMDABUFF                                                         
         USING *,RF                                                             
DMDABUFF MVI   VADWAIT,6                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMDALINK                                                         
         USING *,RF                                                             
DMDALINK MVI   VADWAIT,7                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMDAPTRS                                                         
         USING *,RF                                                             
DMDAPTRS MVI   VADWAIT,8                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY PQOPEN                                                           
         USING *,RF                                                             
PQOPEN   MVI   VADWAIT,9                                                        
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMENQDEQ                                                         
         USING *,RF                                                             
DMENQDEQ MVI   VADWAIT,10                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMOD000                                                          
         USING *,RF                                                             
DMOD000  MVI   VADWAIT,11                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMACCEMU                                                         
         USING *,RF                                                             
DMACCEMU MVI   VADWAIT,12                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY LOCKSPC                                                          
         USING *,RF                                                             
LOCKSPC  MVI   VADWAIT,13                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
*                                                                               
         ENTRY DMDDNAME                                                         
         USING *,RF                                                             
DMDDNAME MVI   VADWAIT,14                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
         EJECT                                                                  
         DROP  RF                                                               
         ENTRY LOCKUP                                                           
         USING *,RF                                                             
LOCKUP   MVI   VADWAIT,15                                                       
         STCM  RF,8,HOB                                                         
         B     DMGL                                                             
         EJECT                                                                  
         DROP  RF                                                               
DMGL     STM   RE,RC,12(RD)        SAVE CALLERS REGISTERS & CALL DDSIO          
         LA    RC,72(RD)                                                        
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
*                                                                               
         BASR  RB,0                ESTABLISH ADDRESSABILITY                     
         USING *,RB                                                             
         MVC   0(4,RD),DMGLLIT                                                  
         LR    RD,RC                                                            
         ST    R1,APARM                                                         
*                                                                               
         L     RF,DMGRCNT                                                       
         AH    RF,=H'1'                                                         
         ST    RF,DMGRCNT                                                       
*                                                                               
         ICM   RF,15,ADDSIO        LOAD DDSIO ON FIRST CALL                     
         BNZ   DMGL2                                                            
         LOAD  EPLOC=DDSIO                                                      
         ST    R0,ADDSIO                                                        
         LR    RF,R0                                                            
*                                                                               
         ST    R2,SAVER2                                                        
         MVC   WAIT,VADWAIT                                                     
         MVI   VADWAIT,13                                                       
         LA    R1,INISTRD          DO DUMMY LOCKSPC CALL FOR DMGR               
         LM    R2,R4,VUTL                                                       
         ICM   R7,15,=CL4'ABEA'    NOW WE CAN BLAME ANNIE...                    
         L     R6,VMASTC           PASS V(MASTC) IN R6                          
         ICM   RF,8,HOB                                                         
         BASR  RE,RF                                                            
*                                                                               
         MVC   VADWAIT(1),WAIT                                                  
         L     R2,SAVER2                                                        
         L     RF,ADDSIO                                                        
*                                                                               
DMGL2    CLM   R2,8,=X'FF'         BOBL'S TEST FOR BOBL'S CALL                  
         BNE   DMGL3                                                            
         L     R1,APARM                                                         
         LM    R3,R4,VSSB          FASA                                         
         L     R5,VUTL             R5 REPLACES R2 FOR BOBL'S CALL               
         B     DMGL4                                                            
*                                                                               
DMGL3    LM    R1,R4,APARM         CALL DDSIO PASSING R1 THRU R4                
*                                                                               
DMGL4    ICM   R7,15,=CL4'ABEA'    NOW WE CAN BLAME ANNIE...                    
         L     R6,VMASTC           PASS V(MASTC) IN R6                          
         ICM   RF,8,HOB                                                         
         BASR  RE,RF                                                            
*                                                                               
         L     RD,4(RD)            RESTORE CALLERS REGISTERS & RETURN           
         LM    RE,RC,12(RD)                                                     
         BR    RE                                                               
         SPACE 2                                                                
APARM    DC    A(0)                                                             
VUTL     DC    V(UTL)                                                           
VSSB     DC    V(SSB)                                                           
VADWAIT  DC    V(ADWAIT)                                                        
VMASTC   DC    V(MASTC)                                                         
         DC    4A(0)                                                            
*                                                                               
INISTRT  DC    XL8'2000800100000000'                                            
INISTRD  DC    XL8'2000000100000000'                                            
         SPACE 1                                                                
ADDSIO   DC    A(0)                                                             
DDSIO    DC    CL8'DDSIO'                                                       
DMGLLIT  DC    C'DMGL'                                                          
DMGRCNT  DC    F'0'                                                             
SAVER2   DC    F'0'                                                             
WAIT     DS    X                                                                
HOB      DS    X                                                                
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024DMDMGRLA  05/01/02'                                      
         END                                                                    
