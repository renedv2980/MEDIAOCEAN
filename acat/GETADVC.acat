*          DATA SET GETADVC    AT LEVEL 033 AS OF 05/16/01                      
*CATALP GETADVC                                                                 
         TITLE 'GETADVC-MODULE TO BUILD TABLE OF ADVERTISER CLIENTS'            
*                                                                               
*                                                                               
*        *** NOTE - TO USE YOU MUST BE CONNECTED TO THE CONTROL FILE            
*                                                                               
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
* BPLA 4/7/98    CHECK FOR MAXIMUM CLIENTS - MOW 299 (MAXCLTS)                  
*                OUTPUT AREA SHOULD NOW BE 6000 BYTES                           
*                ALL CALLING PROGRAMS WERE CHANGED 3/98                         
*                                                                               
* BPLA 5/23/94   ISSUE DATE CONTROL                                             
*                                                                               
* BPLA 12/17/93  EXPAND I/O AREA TO 2000 BYTES                                  
*                                                                               
* BPLA 5/25/93   ADD CODE TO HANDLE ADV FILTERS                                 
*                THEY WORK LIKE ESTIMATE FILTERS                                
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*        0(R1)   ADDRESS OF SYSTEM/MEDIA/AOR/ADV/(AGY)                          
*                FOR ALL AGYS SET AGY TO 2X'00'                                 
*                TO FILTER AGYS SET AGY TO C'*' FOLLOWED BY                     
*                THE 3 BYTE FILTER - '*' MEANS IGNORE THIS POSITF I/O           
*                                     IF HIGH ORBER BIT IS OFF                  
*                                     MEANS THIS IS A NEGATIVE FILTER           
*                                     (ALL BUT)                                 
*                                     ANY OTHER CHARACTERS MUST MATCH           
*                                     THE AOR AGENCY FILTER VALUE               
*                                                                               
*        4(R1)   ADDRESS OF 6000 BYTE OUTPUT AREA   - LAST ENTRY                
*                SET TO X'FFFF' TO MARK END OF LIST                             
*                                                                               
*        8(R1)   ADDRESS OF DATAMGR                                             
*                                                                               
*        LAYOUT OF ENTRIES IN OUTPUT (20 BYTES PER CLIENT)                      
*                                                                               
*        USE    GETADVCD TO COVER                                               
*                                                                               
*        CL1    (+0)   AGENCY SYSTEM  (1 FOR PRINT 1)                           
*        CL1    (+1)   AGENCY SE NUMBER                                         
*        CL2    (+2)   AGENCY                                                   
*        CL3    (+4)   AGENCY CLIENT CODE                                       
*        CL3    (+7)   ADVERTISER CLIENT CODE                                   
*        CL6    (+10)  START AND END DATES                                      
*        CL4    (+16)  CONTROLS                                                 
*                                                                               
*        NUMBER OF ENTRIES IS RETURNED IN THIRD PARAMETER                       
*                                                                               
GETADVC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETADV,CLEAR=YES                                   
         USING WORKD,RC                                                         
         LR    R8,R1               R8=A(PARAMETER LIST)                         
         LM    R2,R3,0(R8)                                                      
         ST    R3,ANXTCLT                                                       
         MVC   0(2,R3),=X'FFFF'    SET END OF TABLE                             
         SR    R5,R5               USED TO COUNT ENTRIES                        
*                                                                               
         MVC   DATAMGR+1(3),9(R8)  V(DATAMGR)                                   
         MVI   DATAMGR,0                                                        
         XC    8(4,R8),8(R8)        USED TO RETURN NUMBER OF CLIENTS            
GET0     DS    0H                                                               
*                                                                               
         LA    R9,KEY                                                           
         USING ADVREC,R9                                                        
         XC    ADVKEY,ADVKEY                                                    
         MVI   ADVREC,ADVRECQ                                                   
         MVI   ADVTYP,ADVTYPQ                                                   
         MVC   ADVSYS(9),0(R2)   SYSTEM/MEDIA/AOR/ADV/(AGY)                     
         CLI   7(R2),C'*'        SEE IF FILTER GIVEN                            
         BNE   *+10                                                             
         XC    ADVAGY,ADVAGY     CLEAR AGENCY                                   
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),GENDIR,KEY,KEY                           
         B     GET7                                                             
*                                                                               
GET5     GOTO1 DATAMGR,DMCB,(0,DMRSEQ),GENDIR,KEY,KEY                           
*                                                                               
GET7     CLC   ADVSYS(7),0(R2)     CHK THROUGH ADV                              
         BNE   GETX                                                             
         OC    7(2,R2),7(R2)       SEE IF AGY GIVEN                             
         BZ    GET10                                                            
         CLI   7(R2),C'*'        SEE IF FILTER GIVEN                            
         BE    GET10                                                            
         CLC   ADVAGY(2),7(R2)   CHK FOR RIGHT AGY                              
         BNE   GETX                                                             
*                                                                               
         DROP  R9                                                               
*                                                                               
GET10    DS    0H                  READ THIS RECORD                             
*                                                                               
         GOTO1 DATAMGR,DMCB,GETREC,GENFIL,KEY+36,IO,DMWORK                      
*                                                                               
         LA    R7,IO                                                            
         USING ADVKEYD,R7                                                       
*                                  FIRST FIND AGY ELEMENT TO GET                
*                                  SYSTEM NUMBER                                
         LA    R1,ADVFRST                                                       
         SR    R0,R0                                                            
ANY10    CLI   0(R1),0                                                          
         BNE   *+10                                                             
         SR    R1,R1               CLEAR R1 IF ELEMENT NOT FOUND                
         B     ANY12                                                            
         CLI   0(R1),X'10'         LOOK FOR AGENCY ELEMENT                      
         BE    *+14                AND SAVE SYSTEM (P1 - P6)                    
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     ANY10                                                            
*                                                                               
         CLI   7(R2),C'*'                 SEE IF FILER GIVEN                    
         BNE   ANY10X                                                           
         LA    R0,3            FOR BCT                                          
         LA    R6,AGYFLTR-AGYD(R1)                                              
         LA    RE,8(R2)                                                         
ANY10C   CLI   0(RE),C'*'          MEANS ACCEPT ANY VALUE                       
         BE    ANY10F                                                           
         CLI   0(RE),C' '          MEANS ACCEPT ANY VALUE                       
         BE    ANY10F                                                           
         TM    0(RE),X'40'        SEE IF NEGATIVE FILTER                        
         BZ    ANY10D                                                           
         CLC   0(1,R6),0(RE)                                                    
         BNE   GET5               DOES NOT PASS POSITIVE FILTER                 
         B     ANY10F                                                           
*                                                                               
ANY10D   MVC   BYTE,0(RE)                                                       
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(R6)                                                       
         BE    GET5              DOES NOT PASS NEGATIVE FILTER                  
*                                                                               
ANY10F   LA    R6,1(R6)          BUMP FILTERS                                   
         LA    RE,1(RE)                                                         
         BCT   R0,ANY10C                                                        
*                                                                               
ANY10X   MVC   SVASYS,AGYPRT-AGYD(R1)     SYSTEM                                
         MVC   SVACNTL,AGYCNTL-AGYD(R1)   SYSTEM                                
         LA    R1,PRINTSES                                                      
ANY11    CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                BAD PRINT SYSTEM                             
         CLC   0(1,R1),SVASYS+4                                                 
         BE    ANY11D                                                           
         LA    R1,2(R1)                                                         
         B     ANY11                                                            
*                                                                               
ANY11D   MVC   SVASE,1(R1)         SAVE SE NUMBER                               
*                                                                               
         B     ANY15                                                            
*                                                                               
ANY12    B     GET5                GO DO SEQ READ                               
*                                                                               
ANY15    DS    0H              FIND CLIENT ELEMENTS AND ADD TO TABLE            
         LA    R1,ADVFRST                                                       
         SR    R0,R0                                                            
ANY20    CLI   0(R1),0                                                          
         BE    ANY50              END OF RECORD                                 
         CLI   0(R1),X'20'         LOOK FOR CLIENT ELEMENT                      
         BE    ANY30                                                            
ANY25    IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     ANY20                                                            
*                                                                               
ANY30    L     R4,ANXTCLT                                                       
         USING GETADVCD,R4                                                      
         MVC   GETVSYS,SVASYS+4          SAVE "N" OF PRNTN                      
         MVC   GETVSE,SVASE             SE NUMBER                               
         MVC   GETVAGY,ADVAGY           AGENCY                                  
         MVC   GETVACLT,CLTAGY-CLTD(R1)                                         
         MVC   GETVVCLT,CLTADV-CLTD(R1)                                         
         MVC   GETVSTRD,CLTSTR-CLTD(R1)                                         
         MVC   GETVENDD,CLTEND-CLTD(R1)                                         
         XC    GETVCNTL,GETVCNTL                                                
         CLI   SVACNTL,C'Y'                                                     
         BNE   *+8                                                              
         OI    GETVCNTL,X'01'         PUB LINK REQUIRED                         
         CLI   SVACNTL+1,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'02'      CONTRACT RATE LOOK-UP ($CON)                 
         CLI   SVACNTL+2,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'04'      CONTRACT LEVEL LOOK-UP ($CON)                
         CLI   SVACNTL+3,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'08'      CONTRACT RATE LOOK-UP ($BUY)                 
         CLI   SVACNTL+4,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'10'      ADV SCHEDULE CHECKING ($BUY)                 
         CLI   SVACNTL+5,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'20'      NO ADV CONTRACTS - AGENCY USES AOR           
         CLI   SVACNTL+6,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'40'      AOR CONTRACT LOCK/UNLOCK ALLOWED             
*                                                                               
         CLI   SVACNTL+7,C'Y'                                                   
         BNE   *+8                                                              
         OI    GETVCNTL,X'80'      AOR ISSUE RECORD DATE REQUIRED               
*                                                                               
         LA    R5,1(R5)                                                         
         CH    R5,MAXCLTS                                                       
         BNL   MAXERR                                                           
         LA    R4,GETVLEN(R4)                                                   
         MVC   0(2,R4),=X'FFFF'        SET END OF TABLE                         
         ST    R4,ANXTCLT                                                       
         B     ANY25                   GO DO NEXT CLT ELEMENT                   
*                                                                               
         DROP  R4                                                               
*                                                                               
MAXERR   DC    H'0'                   MAXIMUM AOR CLIENTS EXCEEDED              
*****************      ALL PROGRAMS ACCESSING THE MODULE SHOULD                 
*****************      THEIR AOR CLT TABLE EXPANDED                             
*                                                                               
ANY50    B     GET5                   GO DO NEXT CONTROL RECORD                 
*                                                                               
*                                                                               
GETX     DS     0H                                                              
         ST     R5,8(R8)          RETURN NUMBER OF CLIENTS                      
         XIT                                                                    
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
GENDIR   DC    C'GENDIR  '                                                      
GENFIL   DC    C'GENFIL  '                                                      
*                                                                               
MAXCLTS  DC    H'299'                                                           
*                                                                               
       ++INCLUDE PRINTSES                                                       
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE **                        
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
DMWORK   DS    12D                                                              
ANXTCLT  DS    A                                                                
BYTE     DS    CL1                                                              
SVADTS   DS    CL6                                                              
SVASYS   DS    CL5                  PRINT SYSTEM I.E. PRNT5                     
SVASE    DS    XL1                  SE NUMBER                                   
SVACNTL  DS    CL10                                                             
*                                                                               
KEY      DS    CL40                                                             
KEYSAVE  DS    CL40                                                             
IO       DS    2000C      GENFILE RECORDS ARE 2000 BYTES                        
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CTGENADVD                                                      
         EJECT                                                                  
       ++INCLUDE GETADVCD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'033GETADVC   05/16/01'                                      
         END                                                                    
