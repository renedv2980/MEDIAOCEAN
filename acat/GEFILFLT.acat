*          DATA SET GEFILFLT   AT LEVEL 009 AS OF 08/13/03                      
*CATALP GEFILFLT                                                                
         TITLE 'GENERALISED FILTER OVERLAY'                                     
FILFLT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 OVRWRKL,GEFILFLT,RR=RE                                           
         USING TWAD,RA                                                          
         USING WORKD,R9                                                         
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         USING OVERWRKD,RC                                                      
         ST    RE,BORELO                                                        
         MVC   SVPARMS,0(R1)                                                    
         ST    R1,CALLR1                                                        
         L     R5,ASVEBLK                                                       
         USING TWSAVE,R5                                                        
PSFRR    USING FRRELD,PSFRREL                                                   
GSFRP    USING FRPELD,GSFRPEL                                                   
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
         SPACE 1                                                                
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/4                                                      
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         SPACE 1                                                                
EXIT     L     R1,CALLR1           RETURN PARAMETERS                            
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
         XIT1  ,                   EXIT WITH CC SET                             
         EJECT                                                                  
***********************************************************************         
* TABLE ITERATION ROUTINE  - EXPECTS RF TO HOLD A(TABLE)              *         
*                          - EXPECTS R1 TO HOLD VERB                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK                                                           
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATED VERB                        
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE THIS TABLE                           
*                                                                               
ITER02   ICM   RF,15,OBJADR        ROUTINE TO HANDLE THE VERB                   
         LA    R1,SVPARMS                                                       
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   CLI   GSFRP.FRPTYPE,FRPTCLRF                                           
         BNE   OBJ02                                                            
         LH    R0,=Y(FLTELSV-TWAD)                                              
         A     R0,ATWA                                                          
         LA    R1,FLTELSVL                                                      
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVI   GSFRP.FRPTYPE,0                                                  
*                                                                               
OBJ02    L     R1,SVPARMS                                                       
         LA    RF,TABLEOO          KNOWN OBJECTS                                
         B     ITER                                                             
*                                                                               
TABLEOO  DC    AL1(OKEY),AL1(0,0,0),AL4(KEY)                                    
         DC    AL1(ORECH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OSES),AL1(0,0,0),AL4(NTRSES)                                 
         DC    AL1(OLIST),AL1(0,0,0),AL4(LIST)                                  
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(EXITH)                               
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* KEY OBJECT                                                          *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 A(KEY)                                                           *         
* P4 HOLDS SUB-ACTION                                                 *         
***********************************************************************         
         SPACE 1                                                                
KEY      LM    R0,R2,SVPARMS                                                    
         USING FDRRECD,R2                                                       
         LA    RF,KEYTABL                                                       
         B     ITER                ITERATE KEY TABLE                            
*                                                                               
KEYTABL  DC    AL1(KFIRST),AL1(0,0,0),AL4(KEYFRST)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR KEY OBJECT                                           *         
***********************************************************************         
         SPACE 1                                                                
KEYFRST  L     R1,SVPARMS4         R1=INVOKING ACTION                           
         LA    RF,KFTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                ITERATE TABLE                                
*                                                                               
KFTABL   DC    AL1(KVAL),AL1(0,0,0),AL4(KFKVAL)      VALIDATE                   
         DC    AL1(KFVAL),AL1(0,0,0),AL4(KFKFVAL)    VALIDATE FILTER            
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY                                    *         
***********************************************************************         
         SPACE 1                                                                
KFKVAL   XC    FDRKEY,FDRKEY       INITIALIZE KEY OF FIELD RECORD               
         MVI   FDRKMIN,FDRKMINQ    SET MINOR SYSTEM                             
         MVI   FDRKTYP,FDRKTYPQ    SET FIELD RECORD                             
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY FILTER                             *         
***********************************************************************         
         SPACE 1                                                                
KFKFVAL  XC    FDRKEY,FDRKEY       INITIALIZE KEY OF FIELD RECORD               
         MVI   FDRKMIN,FDRKMINQ    SET MINOR SYSTEM                             
         MVI   FDRKTYP,FDRKTYPQ    SET FIELD RECORD                             
         MVC   FDRKSYS,GCOVSYS                                                  
         MVC   FDRKPRG,PSPRG                                                    
         OC    FDRKPRG,FDRKPRG                                                  
         BNZ   *+10                                                             
         MVC   FDRKPRG,PSSHPRG                                                  
         MVC   FDRKREC,PSREC                                                    
         OC    FDRKREC,FDRKREC                                                  
         BNZ   *+10                                                             
         MVC   FDRKREC,PSSHREC     SET SYS/PRG/REC                              
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1=DATA IDENTIFIER                           
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         L     R2,SVPARMS4                                                      
         USING FDRRECD,R2                                                       
         XR    R1,R1                                                            
         IC    R1,SVPARMS3         GET GLOBAL VERB                              
         LA    RF,DTATABL          TABLE OF GLOBAL VERBS                        
         B     ITER                ITERATE TABLE                                
*                                                                               
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
         USING KNOWTABD,RF                                                      
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
*                                                                               
         LM    R1,R2,SVPARMS3      R1 HOLDS VERB                                
         USING TLSTD,R2                                                         
         LA    R3,TLFDR                                                         
         USING FDRELD,R3                                                        
         BR    RF                                                               
*                                                                               
DTATABL  DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
         SPACE 1                                                                
KNOWTAB  DC    AL2(00080),AL4(FNTDTA)    FILTER TAG                             
         DC    AL2(00081),AL4(FFEDTA)    FILTER FIELD                           
         DC    AL2(EOT)                                                         
         SPACE 1                                                                
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FILFLT   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR FILTER NAME TAG                                     *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
FNTDTA   LA    RF,FNTTBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
FNTTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISFNT)                                 
         DC    AL1(DFDIS),AL1(0,0,0),AL4(DISFNT)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A FILTER NAME TAG                                           *         
***********************************************************************         
         SPACE 1                                                                
DISFNT   MVC   FVIFLD(L'TLTAG),TLTAG        DO WE HAVE A TAG?                   
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* DATA OBJECT FOR FILTER FIELD ENTRY VALUES                           *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
FFEDTA   LA    RF,FFETBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
FFETBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISFFE)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALFFE)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY FILTER FIELD VALUES                                         *         
***********************************************************************         
         SPACE 1                                                                
DISFFE   MVC   SVFRREL,GSFRREL     SAVE CURRENT RECORD ELEMENT                  
         MVC   GSFRREL,PSFRREL     SET PS RECORD ELEMENT AS CS                  
         GOTOX ('SWCHFC',AGROUTS),PSFRR.FRRSYS                                  
*                                                                               
         GOTOX AGEN,BOPARM,('GCBPS',ODATA),DFDIS,FVADDR,LISTKEY,FDRELD          
*                                                                               
         MVC   GSFRREL,SVFRREL     RESTORE CURRENT RECORD ELEMENT               
         GOTOX ('SWCHFC',AGROUTS),=AL1(QSCON)                                   
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE FILTER FIELD VALUES                                        *         
***********************************************************************         
         SPACE 1                                                                
VALFFE   MVC   SVFRREL,GSFRREL     SAVE CURRENT RECORD ELEMENT                  
         MVC   GSFRREL,PSFRREL     SET PS RECORD ELEMENT AS CS                  
         GOTOX ('SWCHFC',AGROUTS),PSFRR.FRRSYS                                  
*                                                                               
         MVC   LSAVEKEY,LISTKEY                                                 
         GOTOX AGEN,BOPARM,('GCBPS',ODATA),DFVAL,FVADDR,LISTKEY,FDRELD          
         MVC   GSFRREL,SVFRREL     RESTORE CURRENT RECORD ELEMENT               
         BE    VFFEOK                                                           
         GOTOX ('SWCHFC',AGROUTS),=AL1(QSCON)                                   
         B     EXITL                                                            
*                                                                               
VFFEOK   CLI   GCFLAG,FF           MODIFIERS ON THIS FIELD?                     
         BNE   *+8                                                              
         MVI   MOD,FF              A MODIFIER WAS SET                           
*                                                                               
         CLI   MOD,FF              HAS MODIFIER BEEN SET AT ALL?                
         BNE   *+10                                                             
         MVC   LISTKEY,LSAVEKEY                                                 
*                                                                               
         GOTOX ('SWCHFC',AGROUTS),=AL1(QSCON)                                   
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* NTRSES OBJECT                                                       *         
* -------------                                                       *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(INITIAL LIST KEY)                                        *         
* P4 HOLDS A(MEMORY COVERED BY FESD)                                  *         
***********************************************************************         
         SPACE 1                                                                
NTRSES   LM    R0,R3,SVPARMS                                                    
         USING FDRRECD,R2                                                       
         USING FESD,R3                                                          
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DC    AL1(SNTRIN),AL1(0,0,0),AL4(NTRIN)                                
         DC    AL1(SXITOUT),AL1(0,0,0),AL4(XITOUT)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* BUILD PARAMETER LIST FOR NTRSES TRANSFER                            *         
***********************************************************************         
         SPACE 1                                                                
XITOUT   LA    RF,FESGSSV                                                       
Z        USING GSSAVE,RF                                                        
         OI    Z.DLINDS,DLFLTSET   IN CASE DOWNLOADING                          
         MVC   Z.LSINIKEY,LISTKEY                                               
         LH    R0,=Y(FLTELSV-TWAD)                                              
         A     R0,ATWA                                                          
         LA    R1,FLTELSVL                                                      
         LH    RE,=Y(FESFLSV-FESD)                                              
         LA    RE,FESD(RE)                                                      
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         B     EXITOK                                                           
         DROP  Z                                                                
         SPACE 2                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
NTRIN    MVC   LISTKEY,LSINIKEY                                                 
         LH    R0,=Y(FLTELSV-TWAD)                                              
         A     R0,ATWA                                                          
         LA    R1,FLTELSVL                                                      
         LH    RE,=Y(FESFLSV-FESD)                                              
         LA    RE,FESD(RE)                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* LIST OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS CURRENT KEY BUILD AREA                                     *         
* P4 HOLDS PREVIOUS KEY                                               *         
***********************************************************************         
         SPACE 1                                                                
LIST     LM    R0,R3,SVPARMS                                                    
THIS     USING FDRRECD,R2                                                       
         LA    RF,LISTABL                                                       
         B     ITER                                                             
*                                                                               
LISTABL  DC    AL1(LGETFRST),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LGETNEXT),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LLSTFRST),AL1(0,0,0),AL4(FTFLST)                             
         DC    AL1(LINIT),AL1(0,0,0),AL4(ILST)                                  
         DC    AL1(LTSARFIL),AL1(0,0,0),AL4(TSARFIL)                            
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALISE LIST                                                     *         
***********************************************************************         
         SPACE 1                                                                
ILST     MVI   LSSUBLEN,0                                                       
         NI    LSSTAT1,FF-(LSSENTK)                                             
         OI    LSSTAT1,LSSBALL                                                  
         MVC   LSCOLLIN,=AL2(39)                                                
         MVC   LSLINROW,=AL2(2)                                                 
         OI    LSSTAT1,LSSMULIN+LSSTSAR                                         
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST BUILD                                           *         
***********************************************************************         
         SPACE 1                                                                
FTFLST   XC    MOD,MOD                                                          
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* BUILD LIST OF FILTERS                                               *         
***********************************************************************         
         SPACE 1                                                                
K        USING FDRRECD,IOKEY                                                    
BLST     MVC   K.FDRKEY,THIS.FDRKEY                                             
         XC    K.FDRKTEST,K.FDRKTEST                                            
         XR    R1,R1                                                            
         ICM   R1,3,K.FDRKNUM                                                   
         LA    R1,1(R1)                                                         
         STCM  R1,3,K.FDRKNUM                                                   
*                                                                               
         L     RF,SVPARMS5         GET A(RECORD)                                
         SRL   RF,4-2                                                           
         L     RF,AIO1-L'AIO1(RF)                                               
*                                                                               
         GOTOX ('GETFLD',AGROUTS),BOPARM,GFHIGH,(RF)                            
         BNE   EXITL               END OF FILE                                  
*                                                                               
         CLC   IOKEY(FDRKNUM-FDRRECD),THIS.FDRRECD                              
         BNE   EXITL                                                            
*                                                                               
         MVC   THIS.FDRKEY,K.FDRKEY                                             
         MVC   K.FDRKCTRY,CUCTRY                                                
         XI    K.FDRKCTRY,FF                                                    
         MVI   K.FDRKSUB,FF                                                     
         MVC   K.FDRKTEST,ASTEST                                                
*                                                                               
         L     RF,SVPARMS5         GET A(RECORD)                                
         SRL   RF,4-2                                                           
         L     RF,AIO1-L'AIO1(RF)                                               
         GOTOX ('GETFLD',AGROUTS),BOPARM,GFREAD,(RF)                            
         BE    *+6                                                              
         DC    H'0'                DOES NOT COMPUTE...                          
*                                                                               
         L     RF,SVPARMS5         GET A(RECORD)                                
         SRL   RF,4-2                                                           
         L     RF,AIO1-L'AIO1(RF)                                               
*                                                                               
         GOTOX VHELLO,BOPARM,(C'G',GCFILNAM),('FDRELQ',(RF)),0                  
         CLI   12(R1),0            FDREL?                                       
         BNE   BLST                NO - IGNORE THEN                             
*                                                                               
         ICM   R4,15,12(R1)                                                     
L        USING FDRELD,R4                                                        
         TM    L.FDRINDS1,FDR1DDS  DDS ONLY FIELD?                              
         BZ    *+12                NO                                           
         TM    CUSTAT,CUSDDS       DDS TERMINAL?                                
         BZ    BLST                NOT FOR YOU SUNSHINE                         
*                                                                               
         L     RF,SVPARMS5         GET A(RECORD) AGAIN...                       
         SRL   RF,4-2                                                           
         L     RF,AIO1-L'AIO1(RF)                                               
*                                                                               
         GOTOX VHELLO,BOPARM,(C'G',GCFILNAM),('FLTRLQ',(RF)),0                  
         CLI   12(R1),0                                                         
         BNE   BLST                NO FLTRL - NOT A FILTER                      
*                                                                               
         ICM   R4,15,12(R1)                                                     
         TM    L.FDRINDS1,FDR1PRO  CAN'T INPUT TO A PROTECTED FIELD             
         BO    BLST                                                             
*                                                                               
         MVC   THIS.FDRRECD(FDRKLEN),K.FDRRECD                                  
         B     EXITOK                                                           
         DROP  L,K                                                              
         SPACE 2                                                                
***********************************************************************         
* SET UP TSAR RECORD FROM FILE                                        *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
         USING FDRRECD,R2                                                       
         USING TLSTD,R3                                                         
TSARFIL  MVC   TLRLEN,=AL2(TLLLNQ)                                              
         GOTOX VHELLO,BOPARM,(C'G',GCFILNAM),('FLTRLQ',FDRRECD),0               
         L     RF,12(R1)                                                        
         MVC   TLFDR,0(RF)                                                      
         USING FDRELD,TLFDR                                                     
*                                                                               
         MVC   FVIFLD,BCSPACES     RESET TAG DD REF DISPLAY                     
         OC    FDRRTAG,FDRRTAG     DO WE HAVE A TAG TO DISPLAY?                 
         BZ    EXITOK              NO                                           
*                                                                               
         MVC   FVIFLD(L'FDRRTAG),FDRRTAG                                        
         CLI   FDRFTOL,0                                                        
         BE    TSF02                                                            
         CLI   FDRFTOL,40                                                       
         BH    TSF02                                                            
         MVC   FVIFLD+3(1),FDRFTOL                                              
*                                                                               
TSF02    ICM   RF,15,=C'SL  '                                                   
         ICM   RF,2,FDRKSYS                                                     
         GOTOX VDICTAT,BOPARM,(RF),FVIFLD                                       
         MVC   TLTAG,FVIFLD        SAVE DISPLAY TAG                             
         B     EXITOK                                                           
         POP   USING                                                            
         SPACE 2                                                                
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
FF       EQU   X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
SVFRREL  DS    XL(FRRLNQ)                                                       
OVRWRKL  EQU   *-OVERWRKD                                                       
*                                                                               
*        GEFILWORK                                                              
         PRINT OFF                                                              
       ++INCLUDE GEFILWORK                                                      
         PRINT ON                                                               
*        DDFLDHDR                                                               
         PRINT OFF                                                              
       ++INCLUDE DDFLDHDR                                                       
         PRINT ON                                                               
*                                                                               
TLSTD    DSECT                                                                  
         ORG   TLKSRT                                                           
TLTAG    DS    XL20                                                             
         ORG   TLUSER                                                           
TLFDR    DS    XL(FLTRLNQ)                                                      
TLLLNQ   EQU   *-TLSTD                                                          
*                                                                               
TWAD     DSECT                                                                  
         ORG   TWSAVE                                                           
LISTKEY  DS    CL(L'LSINIKEY)                                                   
LSAVEKEY DS    CL(L'LSINIKEY)                                                   
MOD      DS    XL1                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009GEFILFLT  08/13/03'                                      
         END                                                                    
