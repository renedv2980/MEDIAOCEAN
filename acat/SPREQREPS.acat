*          DATA SET SPREQREPS  AT LEVEL 005 AS OF 05/01/02                      
*CATALP SPREQREP                                                                
         TITLE 'MODULE EDITS REQUESTS AND (OPTIONALLY) PRINTS REPORT'           
SPREQREP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 182,**RQRP**,R8                                                  
         USING REQREPD,RC                                                       
         L     RA,=V(SPWORKC)      PARAMETER 1 RETURNED BY MODULE               
         USING SPWORKD,RA                      0=REQUEST OK                     
*                                              1=REQUEST ERROR                  
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD+4096,R9                                                  
         LR    R2,R1                                                            
         XC    0(4,R2),0(R2)                                                    
*                                  TURN OFF BOX PRINTING                        
         L     RE,ABOX                                                          
         MVI   BOXOFF-BOXD(RE),C'Y'                                             
*                                                                               
         MVC   PRSWITCH,RCREQREP                                                
         L     RE,=V(MASTC)                                                     
         LA    RE,MCREQREP-MASTD(RE)                                            
         CLI   0(RE),0             TEST RUNTIME OVERRIDE                        
         BE    *+10                                                             
         MVC   PRSWITCH,0(RE)      YES - USE IT                                 
         CLI   PRSWITCH,C'N'                                                    
         BE    REPX                                                             
*                                                                               
         MVC   MYDMCB,DMCB         SAVE PARAMETER LIST                          
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL      TRANSLATE LIST                               
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,2                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DCLIST                                                        
         STCM  RF,7,DDIADR                                                      
         LA    RF,DSLIST                                                        
         STCM  RF,7,DDOADR                                                      
         GOTO1 DICTATE                                                          
         DROP  R1                                                               
         MVC   DMCB,MYDMCB         RESTORE P.L.                                 
*                                                                               
         MVI   RQFLTRSW,C'N'                                                    
         CLC   QEST(3),=C'NO '                                                  
         BNE   HEADS                                                            
         CLC   QESTEND(3),SPACES                                                
         BE    HEADS                                                            
         MVI   RQFLTRSW,C'Y'                                                    
         EJECT                                                                  
*                   PRINT SOME MINIMAL HEADLINES                                
         SPACE 3                                                                
HEADS    DS    0H                                                               
         LA    RE,REQTAB                                                        
         LA    RF,22                                                            
         MVC   5(20,RE),=CL20' '   CLEAR TABLE ENTRIES                          
         LA    RE,25(RE)                                                        
         BCT   RF,*-10                                                          
*                                                                               
         LA    RE,REQTAB                                                        
         MVC   5(L'SP@MED,RE),SP@MED                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@CLI,RE),SP@CLI                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@PROGR,RE),SP@PROGR                                        
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@MKTGR,RE),SP@MKTGR                                        
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@PRBID,RE),SP@PRBID                                        
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@PRO,RE),SP@PRO                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@MRKT,RE),SP@MRKT                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@STATN,RE),SP@STATN                                        
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@EST,RE),SP@EST                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@ESTGE,RE),SP@ESTGE                                        
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@STDT,RE),SP@STDT                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@ENDT,RE),SP@ENDT                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@DOA,RE),SP@DOA                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@AFLT,RE),SP@AFLT                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@PTF,RE),SP@PTF                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@DDC,RE),SP@DDC                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@DMO,RE),SP@DMO                                            
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@OPT1,RE),SP@OPT1                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@OPT2,RE),SP@OPT2                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@OPT3,RE),SP@OPT3                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@OPT4,RE),SP@OPT4                                          
         LA    RE,25(RE)                                                        
         MVC   5(L'SP@OPT5,RE),SP@OPT5                                          
         LA    RE,REQRQSTR         REQUESTOR NAME                               
         MVC   5(20,RE),=CL20' '                                                
         MVC   5(L'SP@RQNAM,RE),SP@RQNAM                                        
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P+24(L'SP@RUNON),SP@RUNON                                        
         MVC   RQDUB(2),RCDATE+6                                                
         MVC   RQDUB+2(2),RCDATE                                                
         MVC   RQDUB+4(2),RCDATE+3                                              
         GOTO1 DATCON,RQWORK,(0,RQDUB),(5,P+31)                                 
*---->   MVC   P+45(30),=C'DETAILS OF REQUEST      PAGE 1'                      
         MVC   P+45(L'SP@DORP1),SP@DORP1                                        
         GOTO1 PRINT,RQWORK,SPACES,=C'BC01'                                     
         BAS   RE,PRINT1                                                        
*---->   MVC   P+45(30),=C'------------------      ------'                      
         MVC   P+45(L'SP@DORUL),SP@DORUL                                        
         MVC   P+28(L'SP@AT),SP@AT                                              
*&&DO*&& GETIME STANDARD                                                        
*&&OS                                                                           
         TIME                                                                   
         LR    R1,R0                                                            
         SRL   R1,4                                                             
*&&                                                                             
         ST    R1,RQWORK                                                        
         XC    RQDUB,RQDUB                                                      
         MVC   RQDUB+5(3),RQWORK                                                
         OI    RQDUB+7,X'0F'                                                    
         CVB   R3,RQDUB                                                         
         EDIT  (R3),(5,P+31),2,DUB=RQDUB,WRK=RQWORK                             
         BAS   RE,PRINT3                                                        
         SPACE 2                                                                
*---->   MVC   P+24(25),=C'REQUEST FIELD        DATA'                           
         MVC   P+24(L'SP@RFDA),SP@RFDA                                          
         MVC   P+69(L'SP@ERR),SP@ERR                                            
         BAS   RE,PRINT1                                                        
*---->   MVC   P+24(25),=C'-------------        ----'                           
         MVC   P+24(L'SP@RFDUL),SP@RFDUL                                        
         MVC   P+69(L'SPUERR),SPUERR                                            
         BAS   RE,PRINT3                                                        
         EJECT                                                                  
*                   LOOP THROUGH TABLE AND HANDLE REQUESTS                      
         SPACE 3                                                                
VALIDATE DS    0H                                                               
         L     R6,SPECS                                                         
         SR    R4,R4                                                            
VAL2     DS    0H                                                               
         CLI   0(R6),0             END OF SPECS                                 
         BE    VAL4                                                             
         IC    R4,1(R6)                                                         
         AR    R6,R4                                                            
         B     VAL2                                                             
VAL4     DS    0H                                                               
         CLC   =C'REQLST=',1(R6)                                                
         BNE   VAL8                                                             
*                                  HAVE REPLACEMENT                             
         LA    RE,REQRPL                                                        
         LA    RF,REQRQSTR-REQRPL                                               
         XCEF                                                                   
         LA    R7,REQRPL                                                        
         LA    R6,8(R6)                                                         
VAL6     DS    0H                                                               
         CLI   0(R6),0             EOL                                          
         BE    VAL8                                                             
         MVC   0(25,R7),0(R6)                                                   
         LA    R7,25(R7)                                                        
         LA    R6,25(R6)                                                        
         B     VAL6                                                             
VAL8     DS    0H                                                               
         CLC   =C'REQ2LST=',1(R6)  2ND REQUEST CARD                             
         BNE   VAL10                                                            
         LA    RE,REQ2RPL                                                       
         LA    RF,REQ2RPLL                                                      
         XCEF  ,                                                                
         LA    R7,REQ2RPL                                                       
         LA    R6,9(R6)                                                         
         B     VAL6                                                             
*                                                                               
VAL10    LA    R3,REQTAB                                                        
         LA    R6,QRECORD-1                                                     
         LA    R7,2                                                             
         SPACE 2                                                                
LOOP     CLC   0(3,R3),=C'END'                                                  
         BNE   LOOP1                                                            
         LA    R3,REQ2TAB                                                       
         LA    R6,QAREA2-1                                                      
         BCT   R7,LOOP                                                          
* PRINT OUT 80 COL IMAGES OF REQUEST CARDS                                      
         BAS   RE,PRINT1           SKIP A LINE                                  
*---->   MVC   P(14),=C'COLUMN NUMBERS'                                         
         MVC   P(L'SP@COLNM),SP@COLNM                                           
         MVC   P+23(80),COLS                                                    
         BAS   RE,PRINT1                                                        
*---->   MVC   P(20),=C'REQUEST CARD 1 IMAGE'                                   
         MVC   P(L'SP@RC1IM),SP@RC1IM                                           
         MVC   P+23(80),QAREA                                                   
         LA    R1,P+23+37          POINT TO FIRST CHAR OF START YEAR            
         CLI   0(R1),X'FA'         TEST Y2K FORMAT                              
         BL    IMG2                NO                                           
         IC    RE,0(R1)                                                         
         SH    RE,=H'250'                                                       
         STC   RE,0(R1)                                                         
         OI    0(R1),X'F0'                                                      
*                                                                               
IMG2     LA    R1,P+23+43          POINT TO FIRST CHAR OF END YEAR              
         CLI   0(R1),X'FA'         TEST Y2K FORMAT                              
         BL    IMG4                NO                                           
         IC    RE,0(R1)                                                         
         SH    RE,=H'250'                                                       
         STC   RE,0(R1)                                                         
         OI    0(R1),X'F0'                                                      
*                                                                               
IMG4     BAS   RE,PRINT2                                                        
         CLC   QAREA2,SPACES                                                    
         BE    REPX                                                             
*---->   MVC   P(20),=C'REQUEST CARD 2 IMAGE'                                   
         MVC   P(L'SP@RC2IM),SP@RC2IM                                           
         MVC   P+23(80),QAREA2                                                  
         BAS   RE,PRINT2                                                        
         B     REPX                                                             
COLS     DC    CL40'1...5...10...15...20...25...30...35...40'                   
         DC    CL40'...45...50...55...60...65...70...75...80'                   
LOOP1    CLI   0(R3),0                                                          
         BE    LOOP8               BYPASS GAPS                                  
         B     LOOP2                                                            
         SPACE 2                                                                
         SPACE 2                                                                
LOOP2    PACK  RQDUB,0(2,R3)       GET ADDRESS AND LENGTH OF FIELD              
         CVB   R4,RQDUB                                                         
         LA    R4,0(R4,R6)                                                      
         PACK  RQDUB,2(2,R3)                                                    
         CVB   R5,RQDUB                                                         
         BCTR  R5,R0                                                            
         CLI   4(R3),C'N'          NUMERIC TESTS                                
         BNE   LOOP4                                                            
         EX    R5,SPACOMP          SPACES OK                                    
         BE    LOOP6                                                            
         CLC   0(3,R4),=C'ALL'     AND SO IS ALL                                
         BE    LOOP6                                                            
         MVC   RQWORK(8),=8X'F0'                                                
         EX    R5,MUVZONES                                                      
         CLC   RQWORK(8),=8X'F0'                                                
         BE    LOOP6                                                            
         CLI   PRSWITCH,C'Y'                                                    
         BNE   ERRET                                                            
*---->   MVC   P+69(17),=C'FIELD NOT NUMERIC'                                   
         MVC   P+69(L'SP@FNNM),SP@FNNM                                          
         MVI   3(R2),1                                                          
         B     LOOP6                                                            
         SPACE 2                                                                
ERRET    MVI   PRSWITCH,C'Y'                                                    
         B     VALIDATE                                                         
         SPACE 2                                                                
SPACOMP  CLC   0(0,R4),SPACES                                                   
MUVZONES MVZ   RQWORK(0),0(R4)                                                  
MUVFIELD MVC   P+45(0),0(R4)                                                    
         SPACE 2                                                                
LOOP4    CLI   4(R3),C'D'          DATE TESTS                                   
         BNE   LOOP5                                                            
         CH    R5,=H'5'                                                         
         BE    LOOP4A                                                           
*                                  FOR DATE FIELD, IF LENGTH NOT 6              
*                                  USE DATCON WITH LENGTH AS CODE               
*                                  COMPR = 2, BINARY = 3                        
         LA    R5,1(R5)                                                         
         GOTO1 DATCON,DMCB,((R5),0(R4)),P+45                                    
         B     LOOP6                                                            
*                                                                               
LOOP4A   DS    0H                                                               
         CLC   0(2,R4),=C'ES'                                                   
         BE    LOOP6                                                            
         CLC   0(6,R4),SPACES                                                   
         BE    LOOP6                                                            
         MVC   DATE,0(R4)                                                       
         CLC   DATE+4(2),=C'  '                                                 
         BNE   *+10                                                             
         MVC   DATE+4(2),=C'01'                                                 
         GOTO1 GETDAY,RQWORK,DATE,RQDUB                                         
         CLC   RQDUB(3),SPACES                                                  
         BE    LOOP4B                                                           
* NEED TO FIX Y2K YEAR                                                          
         CLI   PRSWITCH,C'Y'       TEST TO PRINT                                
         BNE   LOOP6                                                            
         GOTO1 DATCON,RQWORK,DATE,(X'20',RQDUB)                                 
         MVC   P+45(2),RQDUB       MOVE YY JUST OBTAINED                        
         MVC   P+47(4),2(R4)       MOVE REQUESTED MMDD                          
         B     LOOP6                                                            
*                                                                               
LOOP4B   CLI   PRSWITCH,C'Y'       DATE NOT VALID                               
         BNE   ERRET                                                            
*---->   MVC   P+69(16),=C'NOT A VALID DATE'                                    
         MVC   P+69(L'SP@NTVD),SP@NTVD                                          
         MVI   3(R2),1                                                          
         B     LOOP6                                                            
*                                                                               
LOOP5    DS    0H                                                               
         CLI   4(R3),C'B'          BINARY NUMBER                                
         BNE   LOOP5D                                                           
         EX    R5,SPACOMP          CHECK FIELD IS NON-SPACES                    
         BE    LOOP8                                                            
         XC    RQDUB,RQDUB                                                      
         LTR   R5,R5                                                            
         BZ    LOOP5B                                                           
         TM    0(R4),X'80'         IF LEN NOT 1 TREAT AS SIGNED                 
         BZ    LOOP5B                                                           
         MVC   RQDUB,=4X'FF'                                                    
LOOP5B   DS    0H                                                               
         LR    RF,R5               R5 = LENGTH - 1                              
         SH    RF,=H'3'                                                         
         LPR   RF,RF                                                            
         LA    RF,RQDUB(RF)        RF = TO ADDR FOR MOVE                        
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R4)                                                    
         L     RF,RQDUB                                                         
         EDIT  (RF),(10,P+45),ALIGN=LEFT,MINUS=YES                              
*                                                                               
LOOP5D   DS    0H                                                               
         SPACE 2                                                                
LOOP6    CLI   PRSWITCH,C'Y'       ONLY PRINT IF OPTION ON                      
         BNE   LOOP8                                                            
         EX    R5,SPACOMP          AND FIELD IS NON-SPACES                      
         BE    LOOP8                                                            
         MVC   P+24(20),5(R3)                                                   
         CLI   P+45,C' '           IS DATA ALREADY THERE                        
         BNE   LOOP7               YES                                          
         EX    R5,MUVFIELD                                                      
         CLC   =C'38',0(R3)        TEST START DATE                              
         BNE   LOOP6A                                                           
         CLI   P+45,X'FA'          TEST Y2K YEAR                                
         BL    LOOP6A                                                           
         IC    RE,P+45                                                          
         SH    RE,=H'250'                                                       
         STC   RE,P+45                                                          
         OI    P+45,X'F0'                                                       
*                                                                               
LOOP6A   CLC   =C'44',0(R3)        TEST END DATE                                
         BNE   LOOP6B                                                           
         CLI   P+45,X'FA'          TEST Y2K YEAR                                
         BL    LOOP6B                                                           
         IC    RE,P+45                                                          
         SH    RE,=H'250'                                                       
         STC   RE,P+45                                                          
         OI    P+45,X'F0'                                                       
*                                                                               
LOOP6B   CLC   =C'2703',0(R3)      PROCESSING QESTEND                           
         BNE   LOOP7               NO                                           
         CLI   RQFLTRSW,C'Y'       TEST FILTERING                               
         BNE   LOOP7               NO                                           
*---->   MVC   P+24(20),=CL20'ESTIMATE FILTERS'                                 
         MVC   P+24(L'SP@ESTFI),SP@ESTFI                                        
         MVC   P+45(5),SPACES                                                   
         LA    RE,QRECORD+26                                                    
         LA    RF,P+45                                                          
         LA    R0,3                                                             
*                                                                               
LOOPFLT2 TM    0(RE),X'40'         TEST NEGATIVE FILTER                         
         BO    *+12                NO                                           
         MVI   0(RF),C'-'                                                       
         LA    RF,1(RF)                                                         
*                                                                               
         MVC   0(1,RF),0(RE)                                                    
         OI    0(RF),C' '          INSURE UPPER CASE                            
         LA    RE,1(RE)            NEXT FILTER CHAR                             
         LA    RF,2(RF)            SPACE BETWEEN OUTPUT CHARS                   
         BCT   R0,LOOPFLT2                                                      
*                                                                               
         SPACE 2                                                                
LOOP7    DS    0H                                                               
         BAS   RE,PRINT2                                                        
         SPACE 2                                                                
LOOP8    LA    R3,25(R3)                                                        
         B     LOOP                                                             
         EJECT                                                                  
*                   TABLE OF REQUEST FIELDS                                     
         SPACE 3                                                                
REQTAB   DS    0CL25                                                            
         DC    CL25'0501AMEDIA'                                                 
         DC    CL25'0603ACLIENT'                                                
         DC    CL25'0901APRODUCT GROUP'                                         
         DC    CL25'1001AMARKET GROUP'                                          
         DC    CL25'1101APROCESS BY ID'                                         
         DC    CL25'1203APRODUCT'                                               
         DC    CL25'1504AMARKET'                                                
         DC    CL25'1905ASTATION'                                               
         DC    CL25'2403AESTIMATE'                                              
         DC    CL25'2703AEST- GROUP END'                                        
         DC    CL25'3806ASTART DATE'                                            
         DC    CL25'4406AEND DATE'                                              
REQRPL   DS    0C                                                               
         DC    CL25'3001ADEMO OVERRIDE ACTIVE'                                  
         DC    CL25'5801AAFFILIATION FILTER'                                    
         DC    CL25'5901APROGRAM TYPE FILTER'                                   
         DC    CL25'6001ADAYPART DETAIL CON.'                                   
         DC    CL25'6101ADAYPART MENU OVERIDE'                                  
         DC    CL25'6201AOPTION 1'                                              
         DC    CL25'6301AOPTION 2'                                              
         DC    CL25'6401AOPTION 3'                                              
         DC    CL25'6501AOPTION 4'                                              
         DC    CL25'6601AOPTION 5'                                              
         DC    10XL25'00'                                                       
REQRQSTR DS    0C                                                               
         DC    CL25'6912AREQUESTOR NAME'                                        
         DC    C'END'                                                           
         SPACE 2                                                                
REQ2TAB  DS    0CL25               TABLE FOR 2ND REQUEST CARD                   
         DC    CL25'0101ACURRENCY'                                              
         DC    CL25'0201ALANGUAGE'                                              
         DC    CL25'0301AINCLUDE GST'                                           
         DC    CL25'0401ADEMO ADJUST FACTOR'                                    
         DC    CL25'0505ACLIENT GROUP'                                          
         DC    CL25'1003APARTNER PRODUCT'                                       
REQ2RPL  DC    30XL25'00'                                                       
REQ2RPLL EQU   *-REQ2RPL                                                        
         DC    C'END'                                                           
         EJECT                                                                  
*                   PRINTING AIDS                                               
         SPACE 2                                                                
PRINT1   MVC   RQCON,=C'BL01'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT2   MVC   RQCON,=C'BL02'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT3   MVC   RQCON,=C'BL03'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
GOPRINT  NTR1                                                                   
         OC    P,SPACES            INSURE PRINTABLE CHARACTERS                  
         GOTO1 PRINT,RQWORK,P,RQCON                                             
         MVC   P,SPACES                                                         
         XIT1                                                                   
REPX     DS    0H                                                               
         XMOD1 1                                                                
         EJECT                                                                  
*                                                                               
DCLIST   DS    0C                                                               
         DCDDL SP#AT,2                                                          
         DCDDL SP#COLNM,18                                                      
         DCDDL SP#DORP1,30                                                      
         DCDDL SP#DORUL,30                                                      
         DCDDL SP#ERR,6,C                                                       
         DCDDL SP#ERR,6,CU,LABEL=SPUERR                                         
         DCDDL SP#ESTFI,20                                                      
         DCDDL SP#FNNM,17                                                       
         DCDDL SP#NTVD,16                                                       
         DCDDL SP#RC1IM,20                                                      
         DCDDL SP#RC2IM,20                                                      
         DCDDL SP#RFDA,25                                                       
         DCDDL SP#RFDUL,25                                                      
         DCDDL SP#RQNAM,16                                                      
         DCDDL SP#RUNON,6                                                       
*                                                                               
         DCDDL SP#MED,5                                                         
         DCDDL SP#CLI,6                                                         
         DCDDL SP#PROGR,13                                                      
         DCDDL SP#MKTGR,12                                                      
         DCDDL SP#PRBID,13                                                      
         DCDDL SP#PRO,7                                                         
         DCDDL SP#MRKT,6                                                        
         DCDDL SP#STATN,7                                                       
         DCDDL SP#EST,8                                                         
         DCDDL SP#ESTGE,14                                                      
         DCDDL SP#STDT,11                                                       
         DCDDL SP#ENDT,8                                                        
*                                                                               
         DCDDL SP#DOA,20                                                        
         DCDDL SP#AFLT,18                                                       
         DCDDL SP#PTF,19                                                        
         DCDDL SP#DDC,19                                                        
         DCDDL SP#DMO,20                                                        
         DCDDL SP#OPT1,8                                                        
         DCDDL SP#OPT2,8                                                        
         DCDDL SP#OPT3,8                                                        
         DCDDL SP#OPT4,8                                                        
         DCDDL SP#OPT5,8                                                        
*                                                                               
DCLISTX  DC    X'00'                                                            
*                                                                               
DSLIST   DS    0C                                                               
         DSDDL PRINT=YES                                                        
DSLISTX  EQU   *                                                                
         EJECT                                                                  
         LTORG                                                                  
         PRINT NOGEN                                                            
         EJECT                                                                  
*     DSECT FOR MODULE                                                          
         SPACE 3                                                                
REQREPD  DSECT                                                                  
RQDUB    DS    D                                                                
RQWORK   DS    CL24                                                             
PRSWITCH DS    CL1                                                              
RQCON    DS    CL4                                                              
DATE     DS    CL6                                                              
RQAREA   DS    CL96                                                             
BUFFER   DS    1280C                                                            
RQFLTRSW DS    C                                                                
MYDMCB   DS    CL24                                                             
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDDICTATED                                                     
       ++INCLUDE SPDDEQUS                                                       
       ++INCLUDE DDMASTD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005SPREQREPS 05/01/02'                                      
         END                                                                    
