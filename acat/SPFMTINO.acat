*          DATA SET SPFMTINO   AT LEVEL 015 AS OF 04/07/08                      
*CATALP SPFMTINO                                                                
         TITLE 'SPFMTINO - FORMAT SPOT INVOICE NUMBERS'                         
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   CHANGE LOG                                                                  
*                                                                               
*   BPLA   2/08   ACCEPT ADDRESS OF BILLING HEADER AND USE ITS                  
*                 MEDIA BYTES AND SEQUENCING OPTION TO FORMAT                   
*                 THE FULL INVOICE NUMBER                                       
*                                                                               
*   DEIS   6/04   SUPPORT "U" (UNPACK) CALL TO CONVERT XL2 TO CL4 ONLY          
*                                                                               
*   DEIS   3/04   SUPPORT NEW EXPANDED INVOICE NUMBERING SCHEME                 
*                  FIRST BYTE OF 'NNNN' PORTION OF INVNUM MAY BE ALPHA.         
*                  I.E.: 0001...9999,A000,A001...A999,B000...Z999               
*                 ALSO SUPPORT INVOICE NUMBER PASSED IN CL6 FORMAT FROM         
*                  BILL HEADERS                                                 
*                                                                               
*   DEIS   3/01   MORE CHANGES TO ACCOMMODATE MORE O&M                          
*                 B1X PROFILE SWITCHES                                          
*                 CONCERNS SPECIAL 'X' VALUE IN B1XPROF+6                       
*                                                                               
*                 O&M BILLING MONTH FORMATS:                                    
*                 UNTIL MAR/99   01-12                                          
*                 APR/99-DEC99   YM FORMAT                                      
*                 JAN/00-DEC/00  MY FORMAT                                      
*                 JAN/01         BASE YEAR FORMAT (1994)                        
*                                                                               
*   BPLA   01/00  MORE 2YK BUG FIXES AND CHANGES TO NEW PROFILE                 
*                 LOGIC                                                         
*                                                                               
*   BPLA   12/99  NEW B1XPROF+6 OPTION THAT SWITCHES YM TO MY                   
*                 WHEN B1XPROF+12(2) YM MODE START YYMM                         
*                 IS USED                                                       
*                 NOTE THAT IF USED WITH B1 PROFILE +5                          
*                 OPTION 3 FORMAT WILL BE MMY INSTEAD OF YMM                    
*                                                                               
*                 B1XPROF+6  Y = MY FORMAT INSTEAD OF YM                        
*                            X = IF INVOICE DATE IS BEFORE                      
*                                YM FORMAT START DATE (B1XPROF+14(2)            
*                                FORMAT MONTH AS YM                             
*                                UNLESS THE BILL DATE IS MAR/99                 
*                                OR BEFORE IN WHICH CASE JUST                   
*                                DISPLAY MM                                     
***                          X IS A SPECIAL VALUE FOR O&M                       
***                            TO HANDLE THEIR B1X PROFILE SWITCHING            
*                                                                               
*   BPLA   4/98   CHANGES FOR Y2K AND SPECIAL Y/M FORMAT (YMM)                  
*                 NEW VALUE FOR B1PROF+4                                        
*                 MUST BE USED WITH B1XPROF+14(2) YM MODE START YYMM            
*                 3 = MED-YMM  EXAMPLE:  N-8010001                              
*                     8  = LAST DIGIT OF YEAR                                   
*                     01 = JANUARY                                              
*                   0001 = INVOICE NUMBER                                       
*                   THIS AFFECTS BOTH FULL AND SHORT OUTPUT FORMATS             
*                                                                               
*   INPUT                                                                       
*   -----                                                                       
*                                                                               
*   P1 BYTE   0    C'B'    MEANS P5 WILL CONTAIN THE ADDRESS OF THE             
*                          BILLING HEADER                                       
*      BYTES  1-3  A(DATE)      YYMMDD (TODAY OR DATE OF BILL)                  
*                                                                               
*   P2 BYTE   0    X'00' = INV NO. POINTED TO BY BYTES 1-3 IS CL4               
*                  X'02' = INV NO. POINTED TO BY BYTES 1-3 IS XL2               
*                  X'06' = INV NO. POINTED TO BY BYTES 1-3 IS CL6               
*                  C'P'  = PACK (USED TO VALIDATE/CONVERT A CL4 NUMBER          
*                          TO XL2. ONLY P2 IS NEEDED ON INPUT, AND ONLY         
*                          P2 IS SET ON OUTPUT.)                                
*                  C'U'  = UNPACK (USED TO CONVERT A XL2 NUMBER                 
*                          TO CL4. ONLY P2 IS NEEDED ON INPUT, AND ONLY         
*                          P2 IS SET ON OUTPUT.)                                
*      BYTES  1-3  A(INVOICE NUMBER)  CL4, XL2, OR CL6                          
*                                                                               
*   P3 BYTE   0    CL1 - MEDIA CODE                                             
*      BYTES  1-3  A(B1 PROFILE)                                                
*                                                                               
*   P4 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(B1X PROFILE)                                               
*                                                                               
*   P5 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(BILLHEADER)    WHEN C'B' IS IN HIGH ORDER BYTE             
*                                   OF P1                                       
*                                                                               
*   OUTPUT   NOTE- SPFMTINO SETS THE ADDRESSES                                  
*   ------                                                                      
*                                                                               
*   P1 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(FULL INVOICE NUMBER) - ME-MN-NNNN OR MN-ME-NNNN             
*                                                                               
*   P2 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(SHORT INV NO.) - MN-NNNN                                    
*                  NOTE: IF HOB OF P2 IS C'P', WE RETURN THE                    
*                   A(XL2) NUMBER ONLY. IF THIS ADDRESS POINTS TO A             
*                   HALFWORD OF NULLS, THEN INPUT NUMBER IS INVALID.            
*                   IF HOB OF P2 IS C'U', WE RETURN THE                         
*                   A(CL4) NUMBER ONLY.                                         
*                                                                               
*   P3 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MONTH OR YEAR/MONTH) - 2 CHARACTERS                         
*                                                                               
*   P4 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MEDIA) -2 CHARACTERS                                        
*                                                                               
***********************************************************************         
         SPACE 2                                                                
SPFMTINO CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**FMTI**,CLEAR=YES                                        
*                                                                               
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         MVC   PARMS(20),0(R1)                                                  
*                                                                               
         L     RF,PARM2            A(INVOICE NUMBER ON INPUT)                   
         CLI   PARM2,C'P'          CONVERT/VALIDATE FINAL 4 CHARS ONLY?         
         BE    FIPACK                                                           
         CLI   PARM2,C'U'          CONVERT XL2 TO CL4 ONLY?                     
         BNE   FI01A                                                            
*                                                                               
         MVC   HALF,0(RF)          INVOICE NUMBER AS PASSED IN                  
         NI    HALF,X'FF'-(STABINV_REVERSAL+STABINV_REVERSED)                   
         SR    R1,R1                                                            
         ICM   R1,3,HALF           R1 = TRUE BINARY INVOICE NUMBER              
         SR    R0,R0               PREPARE FOR DIVIDE                           
         D     R0,=F'1000'         QUOTIENT IN R1, REMAINDER IN R0              
         LA    RE,INVLETS(R1)      INDEX INTO INVOICE LETTER TABLE              
         MVC   ISHORT(1),0(RE)     PICK UP FIRST CHARACTER                      
         CVD   R0,DUB              SIMPLY CONVERT REMAINDER TO CHAR.            
         OI    DUB+7,X'0F'                                                      
         UNPK  ISHORT+1(3),DUB                                                  
         B     FI20                NO FURTHER FORMATTING IS NEEDED              
*                                                                               
FIPACK   DS    0H                                                               
         SR    R0,R0               CLEAR THOUSANDS COUNTER                      
         LA    R1,INVLETS          POSSIBLE FIRST CHARS FOR INVNUM              
FI00     CLI   0(R1),X'FF'         EOT?                                         
         BE    FI20                YES: INVALID INVOICE NO. PASSED IN           
         CLC   0(1,RF),0(R1)       FIND MATCH ON FIRST CHARACTER                
         BE    *+16                GOT IT                                       
         LA    R1,1(R1)                                                         
         AHI   R0,1                BUMP THOUSANDS COUNTER                       
         B     FI00                                                             
         MHI   R0,1000             THIS IS THE THOUSANDS RANGE                  
*                                                                               
         MVC   DUB(3),1(RF)        LAST THREE DIGITS MUST BE NUMERIC            
         NC    DUB(3),=X'F0F0F0'                                                
         CLC   DUB(3),=X'F0F0F0'                                                
         BNE   FI20                THEY AREN'T: INVALID INVOICE NUMBER          
         PACK  DUB,1(3,RF)         SIMPLY CONVERT LOW-ORDER 3 CHARS...          
         CVB   R1,DUB              ...TO BINARY                                 
         AR    R0,R1                                                            
         STCM  R0,3,ISHORT         THE SUM IS THE BINARY INVOICE NUMBER         
         B     FI20                NO OUTPUT FORMATTING IS NEEDED               
*                                                                               
FI01A    DS    0H                                                               
*                                  FORMAT INVOICE NUMBER                        
         L     RF,PARM3                                                         
         MVC   B1PROF,0(RF)        SAVE B1 PROFILE                              
*                                                                               
         MVC   ILONG,=CL10' '      SET OUTPUT IN ILONG                          
*                                                                               
         CLI   PARM1,C'B'          DO I HAVE A(BILLREC) ?                       
         BE    FIBILL                                                           
*                                                                               
FI00A    LA    RF,B1PROF           B1 PROF                                      
         MVC   ILONG(2),2(RF)      MEDIA EXPANSION                              
*                                  *=BLANK, **=USE MEDIA CODE                   
         CLI   ILONG+0,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+0,C' '                                                     
         CLI   ILONG+1,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+1,C' '                                                     
         CLC   ILONG+0(2),=C'  '                                                
         BNE   *+10                                                             
         MVC   ILONG+0(1),PARM3                                                 
         MVC   INOMED,ILONG        SAVE MEDIA EXP                               
         MVI   ILONG+2,C'-'                                                     
*                                                                               
         L     R2,PARM1            A(DATE)                                      
         MVC   MYDATE,0(R2)                                                     
         MVC   ILONG+3(2),MYDATE+2 MONTH                                        
*                                                                               
*Y2K*                                                                           
         MVI   WORK,C'0'     SET 3 DIGIT YEAR IN WORK (FOR PACKING)             
         MVC   WORK+1(2),MYDATE                                                 
         CLI   MYDATE,X'F9'       CHECK FOR SPECIAL Y2K YEARS (FA-FF)           
         BNH   FI01P              NO - PROCESS NORMALLY                         
         LA    RE,YRTAB                                                         
FI01B    CLC   0(3,RE),=X'FFFFFF'                                               
         BNE   *+6                                                              
         DC    H'0'               BAD YEAR - MUST BE FA-FF                      
         CLC   MYDATE(1),0(RE)                                                  
         BE    FI01D                                                            
         LA    RE,3(RE)                                                         
         B     FI01B                                                            
*                                                                               
FI01D    MVC   WORK(2),1(RE)       EXPANDED YEAR FROM TABLE (FA =10)            
         MVC   WORK+2(1),MYDATE+1  LAST DIGIT OF YEAR                           
*                                                                               
FI01P    DS    0H                                                               
         PACK  DUB,WORK(3)         YEAR OF BILL DATE                            
         CVB   R0,DUB                                                           
         STC   R0,SAVBYR   SAVE BINARY YEAR (FOR PROFILE CHECKING)              
         STC   R0,SAVBYRA  ALSO SAVE AS ADJUSTED (FOR INV FORMATTING)           
*                                                                               
         PACK  DUB,MYDATE+2(2)                                                  
         CVB   R0,DUB                                                           
         STC   R0,SAVBMN      SAVE BINARY MONTH (AFTER YEAR)                    
*                                                                               
         L     R3,PARM4            A(B1X PROF)                                  
         OC    14(2,R3),14(R3)     YM MODE START YEAR/MONTH                     
         BZ    FI05                NO                                           
*                                                                               
         MVC   YMSTYM,14(R3)       SAVE PROFILE VALUE                           
         CLI   14(R3),80           IF LOWER THAN 80                             
         BH    FI03B               ASSUME CENTURY 2000                          
         ZIC   R0,14(R3)                                                        
         AHI   R0,100              ADJUST BINARY YEAR OF PROFILE                
         STC   R0,YMSTYM                                                        
*                                                                               
FI03B    DS    0H                                                               
         CLC   SAVBYRMN,YMSTYM     CHECK VS. PROFILE YEAR AND MONTH             
         BL    FI03X                                                            
*                                                                               
         CLI   6(R3),C'X'         CHECK SPECIAL O&M FORMATTING                  
         BNE   FI04                                                             
         CLC   SAVBYRMN,=X'6501'   SEE IF AFTER JAN/01                          
         BL    FI04                                                             
*                                                                               
*        I GET HERE IF BILLING DATE IS BEFORE YM START-UP DATE                  
*                                                                               
FI03X    DS    0H                                                               
         CLI   6(R3),C'X'      B1X PROFILE+6                                    
         BNE   FI05            X IS A SPECIAL VALUE THAT                        
*                              TELLS ME TO THIS INVOICE MONTH                   
*                              AS YM (Y TELLS ME FORMAT IT AS MY)               
         CLC   0(4,R2),=C'9904'   SEE IF BEFORE APR/99                          
         BL    FI08            YES - DO MM FORMAT ANYWAY                        
*                                                                               
         CLC   SAVBYRMN,=X'6501'   ON OR AFTER JAN/01                           
         BNL   FI05                SHOULD HONOR BASE YEAR                       
*                                                                               
*    I CAN ONLY GET HERE IF 14(2,R3) (B1XPROF+14) IS NON-ZERO                   
*        AND BILL DATE IS AFTER YM START DATE                                   
*        OR 6(R3) (B1X PROFILE+6) IS X AND BILL DATE IS BEFORE                  
*        START DATE.                                                            
*                                                                               
FI04     DS    0H                  ELSE YEAR AS 1-9 AND MONTH AS 1-C            
         ZIC   R0,SAVBYRA    USE ADJUSTED YEAR                                  
         CVD   R0,DUB                                                           
         UNPK  WORK(3),DUB                                                      
         OI    WORK+2,X'F0'                                                     
         MVC   ILONG+3(1),WORK+2   2ND DIGIT OF YEAR                            
*                                                                               
         PACK  DUB,MYDATE+2(2)     MONTH                                        
         CVB   RE,DUB                                                           
         LA    RE,MTHTAB-1(RE)                                                  
         MVC   ILONG+4(1),0(RE)                                                 
*                                                                               
         CLC   SAVBYRMN,YMSTYM   CHECK YEAR AND MONTH                           
         BL    FI08              COULD HAPPEN IF 6(R3) IS "X"                   
*                                                                               
         CLI   6(R3),C'Y'        SEE IF SWITCHING Y AND M                       
         BE    FI04D             TO MY                                          
         CLI   6(R3),C'X'        OR SPECIAL TIMING OF SWITCH                    
         BNE   FI08              TO MY                                          
*                                                                               
FI04D    MVC   ILONG+3(1),0(RE)   MTH                                           
         MVC   ILONG+4(1),WORK+2  2ND DIGIT OF YEAR                             
         B     FI08                                                             
*                                                                               
FI05     DS    0H                                                               
*                              AS YM (Y TELLS ME FORMAT IT AS MY)               
         SR    RF,RF                                                            
         ICM   RF,1,4(R3)          INVOICE NUMBER BASE YEAR                     
         BZ    FI08                NONE                                         
*                                                                               
         ZIC   RE,SAVBYR           YEAR                                         
         SR    RE,RF               UNLESS THIS YEAR HIGHER THAN BASE            
         BNP   FI08                IGNORE                                       
         MHI   RE,12                                                            
         PACK  DUB,MYDATE+2(2)     MONTH                                        
         CVB   R0,DUB                                                           
         AR    R0,RE           MONTH =  MONTH + (DIFF IN YEARS * 12)            
*                                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+3(2),DUB                                                   
*                                                                               
FI08     DS    0H                                                               
         MVC   INOMTH,ILONG+3      SAVE MONTH (OR YM OR MY)                     
*                                                                               
         MVI   ILONG+5,C'-'                                                     
*                                                                               
         CLI   6(R3),C'X'          SPECIAL FORMAT FOR O&M                       
         BNE   FI08A                                                            
         CLC   SAVBYRMN,=X'6304'   APRIL 1999                                   
         BL    FI08BX              IF BEFORE DO 01-12                           
*                                                                               
FI08A    DS    0H                                                               
         CLC   SAVBYRMN,YMSTYM     CHECK VS. PROFILE YEAR AND MONTH             
         BL    FI08BX              YM MODE START YEAR                           
*                                                                               
         LA    RF,B1PROF           B1 PROFILE                                   
         CLI   4(RF),C'3'          CHCK FOR SPECIAL YM FORMATTING               
         BNE   FI08BX                                                           
         MVC   ILONG+4(2),MYDATE+2 USE FULL MONTH (NO DASH)                     
         CLI   6(R3),C'Y'          SEE IF REVERSING YM TO MY                    
         BNE   FI08BX                                                           
         MVC   ILONG+3(2),MYDATE+2 MONTH FIRST                                  
         MVC   ILONG+5(1),WORK+2   SHOULD STILL BE 2ND DIGIT OF YEAR            
****                                                                            
****    NOTE THAT INOMTH WILL STILL HAVE                                        
***     OCT="A",NOV="B" AND DEC="C"                                             
*                                                                               
FI08BX   DS    0H                                                               
         L     RF,PARM2                                                         
         CLI   PARM2,6             CL6 INVOICE NUMBER PASSED IN?                
         BNE   FI08C                                                            
         MVC   ILONG+6(4),2(RF)    YES: PICK UP LAST FOUR DIGITS                
*                                                                               
* AT SOME POINT IN THE MID-1990S, THE FIRST 2 CHARACTERS OF THE INVOICE         
* NUMBER AS PRINTED ON THE BILL WERE SAVED WITH THE BILL HEADERS. PRIOR         
* TO THAT, THE ONLY WAY TO DERIVE THE FIRST TWO DIGITS WAS BY USING THE         
* PROFILE VALUES. THE CODE THAT FOLLOWS IS OUR BEST GUESS AS TO WHAT TO         
* DO. FOR ALL NEW INVOICES, WE USE THE INVOICE NUMBER AS IT APPEARS IN          
* THE BILL HEADER. FOR "HISTORICAL" BILL HEADERS, WE USE THE PROFILES.          
*                                                                               
*Y2K                                                                            
         CLC   SAVBYRMN,=X'5F08'   IS THE BILL RUNDATE PRIOR TO AUG/95?         
         BL    *+10                YES: USE VALUES DERIVED FROM B1/B1X          
         MVC   INOMTH,0(RF)        NO: PICK UP FIRST TWO DIGITS AS IS           
         B     FI08D                                                            
*                                                                               
FI08C    DS    0H                                                               
         MVC   ILONG+6(4),0(RF)    INVOICE NUMBER AS CL4                        
         CLI   PARM2,2             BUT IS IT BINARY INSTEAD                     
         BNE   FI08D                                                            
         MVC   HALF,0(RF)          INVOICE NUMBER AS PASSED IN                  
         NI    HALF,X'FF'-(STABINV_REVERSAL+STABINV_REVERSED)                   
         SR    R1,R1                                                            
         ICM   R1,3,HALF           R1 = TRUE BINARY INVOICE NUMBER              
         SR    R0,R0               PREPARE FOR DIVIDE                           
         D     R0,=F'1000'         QUOTIENT IN R1, REMAINDER IN R0              
         LA    RE,INVLETS(R1)      INDEX INTO INVOICE LETTER TABLE              
         MVC   ILONG+6(1),0(RE)    PICK UP FIRST CHARACTER                      
         CVD   R0,DUB              SIMPLY CONVERT REMAINDER TO CHAR.            
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+7(3),DUB                                                   
*                                                                               
FI08D    DS    0H                                                               
         LA    RF,B1PROF           B1 PROF                                      
         CLI   4(RF),C'1'          MODE (ME-MN OR MN=ME)                        
         BE    FI10                                                             
         CLI   4(RF),C'3'          MODE ME-MN WITH SPECIAL YM                   
         BE    FI10                                                             
         MVC   ILONG(2),ILONG+3                                                 
         MVC   ILONG+3(2),INOMED                                                
*                                                                               
FI10     DS    0H                                                               
         MVC   ISHORT(2),INOMTH    SHORT IS MM-NNNN                             
         MVI   ISHORT+2,C'-'                                                    
         MVC   ISHORT+3(4),ILONG+6                                              
         CLI   4(RF),C'3'          MODE ME-MN WITH SPECIAL YM                   
         BNE   *+10                                                             
         MVC   ISHORT(3),ILONG+3   SHORT MAY BE YMMNNNN                         
*                                  IF YM MODE NOT ACTIVE YET                    
*                                  ILONG WILL STILL HAVE DASH                   
*                                                                               
*                                  SQUEEZE BLANKS OUT OF ILONG                  
         LA    RF,9                                                             
         LA    R5,ILONG                                                         
*                                                                               
FI12     DS    0H                                                               
         CLI   0(R5),C' '                                                       
         BH    *+14                                                             
         MVC   0(1,R5),1(R5)                                                    
         MVI   1(R5),C' '                                                       
         LA    R5,1(R5)                                                         
         BCT   RF,FI12                                                          
*                                                                               
FI20     DS    0H                                                               
*                                  SET OUTPUT                                   
         L     R1,SAVR1                                                         
         LA    RF,ILONG                                                         
         ST    RF,0(R1)            A(LONG FORMAT)                               
         LA    RF,ISHORT                                                        
         ST    RF,4(R1)            A(SHORT FORMAT)                              
         LA    RF,INOMTH                                                        
         ST    RF,8(R1)            A(MONTH OR YEAR/MONTH)                       
         LA    RF,INOMED                                                        
         ST    RF,12(R1)           A(MEDIA EXPANSION)                           
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*        FORMAT USING DATA IN A(BILLREC)                                        
FIBILL   DS    0H                                                               
         L     R8,PARM5                                                         
         USING BILLRECD,R8                                                      
*                                   NOTE - NO EASY WAY TO BE SURE               
*                                   THIS IS REALLY A BILL REC                   
         CLC   BINVMED(3),=C'   '   ARE FORMAT OPTIONS PRESENT?                 
         BNH   FIB10                                                            
         MVC   ILONG(2),BINVMED                                                 
         LA    RF,B1PROF                                                        
         MVC   4(1,RF),BINVSEQ     ALTER B1 PROFILE                             
*                                  TO MATCH BILL'S                              
         B     FIB20                                                            
*                                                                               
FIB10    DS    0H                                                               
         LA    RF,B1PROF           B1 PROFILE                                   
         MVC   ILONG(2),2(RF)      MEDIA EXPANSION                              
*                                  *=BLANK, **=USE MEDIA CODE                   
         CLI   ILONG+0,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+0,C' '                                                     
         CLI   ILONG+1,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+1,C' '                                                     
         CLC   ILONG+0(2),=C'  '                                                
         BNE   *+10                                                             
         MVC   ILONG+0(1),PARM3                                                 
*                                                                               
FIB20    MVC   INOMED,ILONG        SAVE MEDIA EXP                               
         MVI   ILONG+2,C'-'                                                     
*                                                                               
         MVC   ILONG+3(2),BINVNO   USE DATA FROM BILLHEADER                     
         MVI   ILONG+5,C'-'                                                     
         MVC   ILONG+6(4),BINVNO+2                                              
         MVC   INOMTH,BINVNO                                                    
*                                                                               
         B     FI08D               REST IS SAME AS OLD                          
*                                                                               
         EJECT                                                                  
*Y2K*                                                                           
YRTAB    DC    X'FA',C'10'      YEARS 100-109 (2000-2009)                       
         DC    X'FB',C'11'      YEARS 110-119 (2010-2019)                       
         DC    X'FC',C'12'      YEARS 120-129 (2020-2029)                       
         DC    X'FD',C'13'      YEARS 130-139 (2030-2039)                       
         DC    X'FE',C'14'      YEARS 140-149 (2040-2049)                       
         DC    X'FF',C'15'      YEARS 150-159 (2050-2059)                       
         DC    X'FFFFFF'          END OF TABLE                                  
*                                                                               
MTHTAB   DC    C'123456789ABC'                                                  
*                                                                               
INVLETS  DC    C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ',X'FF'                    
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
WORK     DS    CL20                                                             
MYDATE   DS    CL6          PASSED DATE (MAY BE ADJUSTED)                       
YMSTYM   DS    XL2          BINARY YM START YM (FROM PROFILE)                   
*                           (YEAR ADJUSTED FOR Y2K)                             
SAVBYRMN DS    0XL2                                                             
SAVBYR   DS    X            SAVED BINARY YEAR OF TODAY OR BILL DATE             
SAVBMN   DS    X            SAVED BINARY MONTH OF TODAY OR BILL DATE            
SAVBYRA  DS    X         ADJUSTED BINARY YEAR FOR FORMATTING                    
B1PROF   DS    CL16         SAVED FROM PARM3                                    
*                                                                               
*     IF NOT INCREMENTING MONTH (B1X PROF+5) WILL BE SAME AS SAVBYR             
*                                                                               
DUB      DS    D                                                                
SAVR1    DS    F                                                                
HALF     DS    H                                                                
PARMS    DS    0F                                                               
PARM1    DS    A                                                                
PARM2    DS    A                                                                
PARM3    DS    A                                                                
PARM4    DS    A                                                                
PARM5    DS    A                                                                
*                                                                               
ILONG    DS    CL10                                                             
ISHORT   DS    CL7                                                              
INOMED   DS    CL2                                                              
INOMTH   DS    CL2                                                              
*                                                                               
WORKDL   EQU   *-WORKD                                                          
         SPACE 2                                                                
         PRINT OFF                                                              
       ++INCLUDE SPGENSTAB                                                      
BILLRECD DSECT                                                                  
       ++INCLUDE SPGENBILL                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015SPFMTINO  04/07/08'                                      
         END                                                                    
