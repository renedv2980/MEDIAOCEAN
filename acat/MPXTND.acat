*          DATA SET MPXTND     AT LEVEL 006 AS OF 05/01/02                      
*CATALP MPXTND                                                                  
         TITLE 'MPXTND  - EXTEND PROC TAB FOR B/Q PAIRS IN FORMULA'             
         PRINT NOGEN                                                            
MPXTND   CSECT                                                                  
         SPACE 2                                                                
*                                                                               
*        INPUT IS THREE FULLWORDS :-                                            
*        PARAM 1 - PROGRAM NUMBER                                               
*        PARAM 2 - A(READREX CONTROL BLOCK)                                     
*        PARAM 3 - A(MPDITAB)                                                   
*        PARAM 4 - A(WORK TABLE AREA)                                           
*        PARAM 5 - A(SCREEN HEADER) FOR ERROR EXIT                              
*                                                                               
*        RETURNS TOTAL SIZE OF DATA VECTOR (FIXED + VARIABLE) IN FIRST          
*        FOUR BYTES OF PARAM LIST.                                              
*        EXTENDS THE DATA VECTORS TO INCLUDE ANY BASE/QUALIFIER PAIRS           
*        REQUIRED FOR CALCULATION THAT HAVE NOT BEEN EXPLICITLY                 
*        REQUESTED BY THE USER.                                                 
*        FILLS IN VECTOR TABLE OFFSETS FOR EACH BASE QUALIFIER PAIR             
*        DEFINED IN THE PROCESSING TABLE (BOTH AS PRIMARY ENTRIES               
*        AND AS ENTRIES WITHIN FORMULAE.)                                       
*                                                                               
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPXTND                                               
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         SH    RC,=H'04'                                                        
         L     RC,0(RC)            GET A(GENCONS GLOBAL STORAGE)                
         USING GEND,RC                                                          
         EJECT                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R2,4(R1)            A(RXCTL)                                     
         L     R4,RXPCMTAB-RXCTLD(R2)                                           
         ST    R4,APROCTAB         SAVE PROCESSING TABLE ADDRESS                
         SH    R4,=H'4'            END ADDRESS OF TABLE STORED HERE             
         L     R4,0(R4)                                                         
         STCM  R4,15,PROCMAX       CAN'T EXTEND TABLE BEYOND THIS POINT         
*                                                                               
         L     R2,12(R1)           A(DVOTAB) FROM ROOTS DYNAMIC ALLOC           
         L     R3,0(R2)            LENGTH OF DVO TABLE                          
         LA    R2,4(R2)            POINT TO USABLE AREA                         
         ST    R2,ADVOTAB          SAVE TABLE ADDRESS                           
         SR    R4,R4               ADDR OF 'FROM' FIELD                         
         SR    R5,R5               LENGTH AND FILL OF ZEROS                     
         MVCL  R2,R4               INIT INTERNAL TABLE                          
*                                                                               
         L     R4,APROCTAB         A(PROCESSING TABLE)                          
         USING PCMDD,R4                                                         
         USING DVOD,R2                                                          
*                                                                               
*        GET AND SAVE THE END OF THE PROCESSING TABLE TO AVOID                  
*        RE FINDING IT EACH TIME A NEW ENTRY IS TO BE ADDED.                    
*                                                                               
XT00     DS    0H                  SEARCH FOR E.O.T. ON PROC TAB                
         CLI   PCLNTH,0            FOUND E.O.T. ?                               
         BE    XT01                                                             
         LA    R4,PCLEN(R4)        BUMP TO NEXT                                 
         B     XT00                                                             
*                                                                               
XT01     DS    0H                                                               
         ST    R4,PROCEND          SAVE END OF PROC TAB ADDR                    
*                                  FOR NEW ENTRIES                              
         L     R4,APROCTAB         A(PROCESSING TABLE)                          
*                                                                               
XT05     DS    0H                                                               
         CLI   PCLNTH,0            END OF PROCTAB ?                             
         BE    XT70                YES - ALL DONE                               
*                                                                               
*                                                                               
*        CHECK IF PRIMARY ENTRY OF PROC TABLE IS IN DVO TABLE                   
*                                                                               
XT10     DS    0H                                                               
         MVC   BQPAIR,PCBASE       LOAD BASE/QUALIFER FOR DVSCAN                
         MVC   SUFFIX,PCSFX        LIKEWISE SUFFIX                              
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BNE   XT30                NO - FILL IN ENTRY AND FLAG PRIMARY          
         MVC   DVBASE(2),BQPAIR    GENERATE NEW ENTRY                           
         CLI   PCTYP,MPVTYPV       IN VARIABLE PART OF VECTOR ??                
         BNE   XT30                NO -LEAVE SUFFIX AS ZERO                     
         MVC   DVSFX,SUFFIX                                                     
XT30     DS    0H                                                               
         MVC   DVTYP,PCTYP         INSERT VECTOR TYPE                           
         MVC   DVDATLEN,PCDATLEN   INSERT VECTOR DATA LENGTH                    
         OI    DVFLAG,X'80'        FLAG AS A PRIMARY ENTRY                      
*                                                                               
*        NOW CHECK FORMULA BASE QUALIFIERS AND GENERATE                         
*        SECONDARY ENTRIES                                                      
*                                                                               
         CLI   PCFB1,0             ANY FORMULA ?                                
         BE    XT60                NO - FINISHED THIS PROC TAB ENTRY            
*                                                                               
         MVC   BQPAIR,PCFB1        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BNE   XT40                SKIP IF FOUND                                
         BAS   RE,PROCGEN          GENERATE PROCESSING TABLE ENTRY              
*                                                                               
XT40     DS    0H                                                               
         CLI   PCFB2,0             2ND OPERAND ?                                
         BE    XT60                NO - FINISHED THIS PROC TAB ENTRY            
         MVC   BQPAIR,PCFB2        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BNE   XT50                SKIP IF FOUND                                
         BAS   RE,PROCGEN          GENERATE PROCESSING TABLE ENTRY              
*                                                                               
XT50     DS    0H                                                               
         CLI   PCFB3,0             3RD OPERAND ?                                
         BE    XT60                NO - FINISHED THIS PROC TAB ENTRY            
         MVC   BQPAIR,PCFB3        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BNE   XT60                SKIP IF FOUND                                
         BAS   RE,PROCGEN          GENERATE PROCESSING TABLE ENTRY              
XT60     DS    0H                                                               
         LA    R4,PCLEN(R4)        BUMP TO NEXT PROC TAB  ENTRY                 
         B     XT05                PROCESS THIS ENTRY                           
         SPACE 1                                                                
*REACH PROGRAM REQUIRES IMPS AS A NUMBER WITH NO SUFFIX IF IMPS                 
*IS REQUIRED FOR ANY PROC TAB SUFFIXED ENTRY. NOTE THAT IMPS                    
*WITH NO SUFFIX MAY WELL EXIST ANYWAY.                                          
XT70     L     R0,0(R8)            GET PROGRAM NUMBER                           
         LA    R1,MPREACH          FIND OUT IF CALLER IS REACH PROG             
         CR    R1,R0                                                            
         BNE   XT90                                                             
XT80     MVI   BQPAIR,MPBIMP       IMPRESSIONS                                  
         MVI   BQPAIR+1,MPQ#       RAW NUMBER                                   
         XC    SUFFIX,SUFFIX       WITH NO SUFFIX                               
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            CHECK FOR END OF TABLE                       
         BNE   XT90                FOUND -SO O.K.                               
         LA    R1,MPEVAL           FIDDLE SO THAT TABLE ENTRY FOUND             
         ST    R1,0(R8)                                                         
         BAS   RE,PROCGEN          GENERATE NEW ENTRY                           
         LA    R1,MPREACH          UNSET FIDDLE                                 
         ST    R1,0(R8)                                                         
         DROP  R4                                                               
*                                                                               
XT90     BAS   RE,DVOCALC          CALCULATE AND INSERT OFFSETS                 
*                                                                               
         B    XIT                                                               
         EJECT                                                                  
**********************************************************************          
*                                                                    *          
*        PROCGEN :- GENERATE PROCESSING TABLE ENTRIES FOR B/Q PAIRS  *          
*                   THAT ARE REQUIRED FOR CALCULATIONS BUT NOT       *          
*                   REQUESTED BY THE USER.                           *          
*                                                                    *          
**********************************************************************          
*                                                                               
PROCGEN  DS    0H                                                               
         L     R3,8(R8)            A(MPDITAB)                                   
         A     R3,8(R3)            A(CONTAB) - CONTROL TABLE                    
         USING MPDIPD,R3                                                        
PG10     DS    0H                                                               
         CLI   MPPTYP,C'P'         STILL IN CONTAB ???                          
         BE    PG20                YES - CONTINUE                               
         DC    H'0'                IF B/Q EXISTS IN A FORMULA IT                
*                                  MUST EXIST AS A VAILD PAIR IN IT'S           
*                                  OWN RIGHT                                    
         DC    C'**FORMULA BQPAIR NOT IN MPDITAB**'                             
*                                                                               
PG20     DS    0H                                                               
         CLC   BQPAIR,MPPBCOD      FOUND BASE/QUALIFIER ??                      
         BNE   PG25                                                             
         L     R1,0(R8)            PROGRAM NUMBER                               
         LH    R5,MPPVALF          VALIDITY FLAG                                
         NR    R5,R1               VALID FOR THIS PROG                          
         BZ    PG25                BRANCH - NOT VALID FOR PROGRAM               
PG24     DS    0H                                                               
         TM    MPPCTLF,MPSUFFR     IS SUFFIX REQUIRED TO DEFINE CALC ?          
         BZ    PG30                NO - FOUND ENTRY IN MPDITAB                  
         CLC   SUFFIX+1(1),MPPSUFF CORRECT SUFFIX VALUE ???                     
         BE    PG30                YES - FOUND ENTRY IN MPDITAB                 
PG25     DS    0H                                                               
         ZIC   R1,MPPLEN           GET LENGTH OF ENTRY IN MPDITAB               
         AR    R3,R1               BUMP TO NEXT                                 
         B     PG10                                                             
*                                                                               
*        FOUND RELEVANT B/Q ENTRY                                               
*        SO INSERT AT END OF PROC TAB                                           
*                                                                               
PG30     DS    0H                                                               
         L     R5,PROCEND          GET 1ST SPARE ENTRY                          
         LA    R5,PCLEN(R5)        POINT AT END                                 
         C     R5,PROCMAX          CHECK STILL WITHIN LIMITS OF TABLE           
         BNH   PG32                                                             
         MVI   ERROR,CANTDO        CAN'T PROCESS - PROCTAB TOO SMALL            
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR             ONLINE - SO INFORM USER                      
         DC    H'0'                OFFLINE - DIE - PROCTAB TOO SMALL            
*                                                                               
PG32     L     R5,PROCEND         POINT TO 1ST SPARE ENTRY (SAFE)               
         USING PCMDD,R5                                                         
         XC    0(PCLEN,R5),0(R5)  CLEAR ENTRY                                   
*                                                                               
*        MOVE DATA FROM FIXED TABLES TO PROCESSING COMMAND TABLE                
*                                                                               
         MVI   PCLNTH,PCLEN                                                     
         MVC   PCBASE(2),BQPAIR                                                 
         MVC   PCSFX,SUFFIX        INSERT SUFFIX                                
         MVC   PCTYP,MPPVTYP       INSERT VECTOR TYPE                           
         CLI   PCTYP,MPVTYPV       IN VARIABLE PART OF DATA VECTOR ??           
         BE    PG35                YES - SKIP                                   
         XC    PCSFX,PCSFX         NO SUFFIX FOR FIXED OR GLOBAL VALUES         
PG35     DS    0H                                                               
         MVC   PCWGT,MPPWGT                                                     
         MVC   PCDATFMT,MPPINTD                                                 
         MVC   PCDATLEN,MPPDLEN                                                 
         MVC   PCDATSCF,MPPDSCF                                                 
*                                                                               
         CLI   MPPHOW,MPOPSUBR     IS THE 'FORMULA' A(SUBROUTINE) ???           
         BNE   PG40                NO - SET UP FORMULA                          
         MVC   PCFHOW,MPPHOW       ELSE - DEFINE AS A SUBR                      
         MVC   PCFASUBR,MPPASUBR   DEFINE ITS ADDRESS (IF GENERAL)              
*                                  OR SUBR NUMBER (IF LOCAL)                    
         B     PG50                                                             
PG40     DS    0H                                                               
         MVC   PCFB1,MPPFOP1B                                                   
         MVC   PCFQ1,MPPFOP1Q                                                   
         MVC   PCFOP1,MPPFO1                                                    
         MVC   PCFB2,MPPFOP2B                                                   
         MVC   PCFQ2,MPPFOP2Q                                                   
         MVC   PCFOP2,MPPFO2                                                    
         MVC   PCFB3,MPPFOP3B                                                   
         MVC   PCFQ3,MPPFOP3Q                                                   
*                                                                               
*        INSERT NEW ENTRY IN DATA VECTOR OFFSET TABLE                           
*                                                                               
PG50     DS    0H                                                               
         MVC   DVBASE(2),BQPAIR    GENERATE NEW ENTRY                           
         MVC   DVTYP,PCTYP                                                      
         MVC   DVSFX,PCSFX                                                      
         MVC   DVDATLEN,PCDATLEN   INSERT VECTOR DATA LENGTH                    
*                                                                               
*        POINT TO NEXT SPARE BLOCK IN PROC TABLE                                
*                                                                               
         LA    R5,PCLEN(R5)                                                     
         LA    RF,PCLEN(R5)        POINT AT END OF ENTRY                        
         C     RF,PROCMAX          CHECK STILL WITHIN LIMITS OF TABLE           
         BNH   PG60                                                             
         MVI   ERROR,CANTDO        CAN'T PROCESS - PROCTAB TOO SMALL            
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR             ONLINE - SO INFORM USER                      
         DC    H'0'                OFFLINE - DIE - PROCTAB TOO SMALL            
*                                                                               
PG60     XC    0(PCLEN,R5),0(R5)                                                
         ST    R5,PROCEND                                                       
         DROP  R5                                                               
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
*                                                                    *          
*        CALCULATE OFFSETS FOR EACH BASE QUALIFIER IN DVO TABLE      *          
*        AND INSERT IN THE PROCESSING TABLE                          *          
*        NOTE - VECTOR TYPE 'F' GETS ITS OFFSET FROM THE FIXED VECTOR*          
*        AREA - VECTOR TYPE 'G' GETS ITS OFFSET FROM THE GLOBAL RDREX*          
*        AREA ELSE THE OFFSET IS CALCULATED FROM END OF FIXED PORTION*          
*        OF DATA VECTOR.                                             *          
**********************************************************************          
*                                                                               
DVOCALC  DS    0H                                                               
         ST    RE,SAVERE           SAVE RETURN ADDRESS                          
         LA    R1,MPFREQ           FREQ REPORT NUMBER                           
         CH    R1,2(R8)            ARE WE IN FREQ PROG ???                      
         BNE   DC01                                                             
         LA    R3,VTFQFLEN         LENGTH OF FIXED PART OF FREQ VECTOR          
         B     DC02                                                             
DC01     DS    0H                                                               
         LA    R3,VTELEN           LENGTH OF FIXED PART OF VEH  VECTOR          
DC02     DS    0H                                                               
         L     R2,ADVOTAB          DATA VECTOR OFFSETS TABLE                    
DC10     DS    0H                                                               
         CLI   DVBASE,0            E.O.T. ?                                     
         BE    DC25                                                             
         CLI   DVTYP,MPVTYPV       IN VARIABLE PART OF DATA VECTOR ??           
         BNE   DC10A               NO -SKIP TO FIND OFFSET                      
*                                                                               
         STCM  R3,15,DVOFFSET      SAVE CURRENT OFFSET FOR THIS ENTRY           
         ZIC   R1,DVDATLEN         LENGTH OF THIS FIELD                         
         LA    R3,0(R1,R3)         OFFSET FOR NEXT FIELD                        
         LA    R2,DVLEN(R2)        BUMP TO NEXT ENTRY                           
         B     DC10                PROCESS NEXT ENTRY                           
*                                                                               
DC10A    DS    0H                                                               
         CLI   DVTYP,MPVTYPF       IN FIXED PART OF DATA VECTOR ??              
         BNE   DC20A               NO - MUST BE GLOBAL                          
         L     R5,8(R8)            A(MPDITAB)                                   
         A     R5,24(R5)           A(FXDTAB)                                    
         USING FXDTABD,R5                                                       
DC10B    DS    0H                                                               
         CLI   FXTYP,C'F'          STILL IN FXDTAB TABLE ?                      
         BE    DC10C                                                            
         DC    H'0'                                                             
         DC    C'**FIXED B/Q NOT IN FXDTAB**'                                   
DC10C    DS    0H                                                               
         CLC   DVBASE(2),FXBASE                                                 
         BE    DC10D                                                            
         ZIC   R1,FXLEN                                                         
         AR    R5,R1               BUMP TO NEXT FXDTAB ENTRY                    
         B     DC10B                                                            
DC10D    DS    0H                                                               
         MVC   DVOFFSET,FXOFFSET   SET UP FIXED OFFSET                          
         LA    R2,DVLEN(R2)        BUMP TO NEXT DVO ENTRY                       
         B     DC10                                                             
         DROP  R5                                                               
*                                                                               
DC20A    DS    0H                                                               
         L     R5,8(R8)            A(MPDITAB)                                   
         A     R5,28(R5)           A(GBLTAB)                                    
         USING GBLTABD,R5                                                       
DC20B    DS    0H                                                               
         CLI   GBTYP,C'G'          STILL IN GBLTAB TABLE ?                      
         BE    DC20C                                                            
         DC    H'0'                                                             
         DC    C'**GLOBAL B/Q NOT IN GBLTAB**'                                  
DC20C    DS    0H                                                               
         CLC   DVBASE(2),GBBASE                                                 
         BE    DC20D                                                            
         ZIC   R1,GBLEN                                                         
         AR    R5,R1               BUMP TO NEXT FXDTAB ENTRY                    
         B     DC20B                                                            
DC20D    DS    0H                                                               
         MVC   DVOFFSET,GBOFFSET   SET UP FIXED OFFSET                          
         LA    R2,DVLEN(R2)        BUMP TO NEXT DVO ENTRY                       
         B     DC10                                                             
         DROP  R5                                                               
*                                                                               
DC25     DS    0H                                                               
         ST    R3,DVECLEN          SAVE LENGTH OF VECTOR TO                     
*                                  PASS BACK TO CALLER                          
         L     R4,APROCTAB         A(PROCTAB)                                   
         USING PCMDD,R4                                                         
*                                                                               
*        INSERT OFFSETS IN PROCESSING TABLE                                     
*                                                                               
DC30     DS    0H                                                               
         CLI   PCLNTH,0            E.O.T. ?                                     
         BE    DC50                                                             
*                                                                               
         MVC   BQPAIR,PCBASE       LOAD BASE/QUALIFER FOR DVSCAN                
         MVC   SUFFIX,PCSFX        LOAD SUFFIX FOR SCAN                         
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BE    DCFATAL             MUST BE IN TABLE !!!                         
         MVC   PCWHERE,DVOFFSET    STORE OFFSET                                 
*                                                                               
         CLI   PCFB1,0             ANY FORMULA ?                                
         BE    DC40                NO - FINISHED THIS PROC TAB ENTRY            
         MVC   BQPAIR,PCFB1        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BE    DCFATAL             MUST BE IN TABLE !!!                         
         MVC   PCFINDX1,DVOFFSET   STORE OFFSET                                 
*                                                                               
         CLI   PCFB2,0             2ND OPERAND ?                                
         BE    DC40                NO - FINISHED THIS PROC TAB ENTRY            
         MVC   BQPAIR,PCFB2        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BE    DCFATAL             MUST BE IN TABLE !!!                         
         MVC   PCFINDX2,DVOFFSET   STORE OFFSET                                 
*                                                                               
         CLI   PCFB3,0             3RD OPERAND ?                                
         BE    DC40                NO - FINISHED THIS PROC TAB ENTRY            
         MVC   BQPAIR,PCFB3        LOAD BASE/QUALIFIER PAIR FOR DVSCAN          
*                                  SUFFIX ALREADY LOADED                        
         BAS   RE,DVSCAN           LOOK FOR ENTRY                               
         CLI   DVBASE,0            END OF TABLE ?                               
         BE    DCFATAL             MUST BE IN TABLE !!!                         
         MVC   PCFINDX3,DVOFFSET   STORE OFFSET                                 
*                                                                               
DC40     DS    0H                                                               
         LA    R4,PCLEN(R4)        BUMP TO NEXT PROC TAB  ENTRY                 
         B     DC30                PROCESS NEXT ENTRY                           
         DROP  R4                                                               
*                                                                               
DC50     DS    0H                                                               
         L     RE,SAVERE           RESTORE RETURN ADDRESS                       
         BR    RE                                                               
*                                                                               
DCFATAL  DC    H'0'               IF WE GET HERE THEN DVO TABLE DOES            
*                                 NOT CONTAIN ALL REQUIRED ENTRIES              
*                                 - PROGRAM BUG.                                
         EJECT                                                                  
**********************************************************************          
*                                                                    *          
*        ROUTINE TO SCAN DATA VECTOR OFFSET TABLE                    *          
*        FOR BASE/QUAL PAIR AND RETURN R2 POINTING TO EXISTING       *          
*        ENTRY OR NEXT AVAILABLE SLOT. (IE R2 POINTS AT BASE NO.)    *          
*                                                                    *          
**********************************************************************          
*                                                                               
DVSCAN   DS    0H                                                               
         L     R2,ADVOTAB          DVO TABLE                                    
DVSCAN01 DS    0H                                                               
         CLI   DVBASE,0            REACHED END OF TABLE ???                     
         BE    DVSCANX             YES - NEW ENTRY                              
         CLC   BQPAIR,DVBASE       ALREADY IN TABLE ???                         
         BNE   DVSCAN02            NO  - TRY NEXT                               
         CLI   DVTYP,MPVTYPV       VARIABLE VECTOR ENTRY ??                     
         BNE   DVSCANX             NO - IGNORE SUFFIX                           
         CLC   SUFFIX,DVSFX        CORRECT SUFFIX ???                           
         BE    DVSCANX             YES - FOUND SO EXIT                          
DVSCAN02 DS    0H                                                               
         LA    R2,DVLEN(R2)        BUMP TO NEXT DVO                             
         B     DVSCAN01                                                         
DVSCANX  DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
TRAPERR  L     R2,16(R8)           GET SCREEN HEADER ADDRESS                    
         GOTO1 ERREX                                                            
*                                                                               
XIT      DS    0H                                                               
         MVC   0(4,R8),DVECLEN    PASS BACK VECTOR LENGTH                       
         XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*DDSPLWORKD,MPDITABD,MPPCMDD,MPVTABD,MPRXCTLD,MPEQUATES                         
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPDITABD                                                       
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPVTABD                                                        
       ++INCLUDE MPRXCTLD                                                       
       ++INCLUDE MPEQUATES                                                      
         PRINT ON                                                               
         SPACE 3                                                                
DVOD     DSECT                     DATA VECTOR OFFSET TABLE                     
DVBASE   DS    X                                                                
DVQUAL   DS    X                                                                
DVSFX    DS    XL2                                                              
DVOFFSET DS    XL4                                                              
DVTYP    DS    C                                                                
DVDATLEN DS    X                                                                
DVFLAG   DS    X                                                                
DVLEN    EQU   *-DVOD                                                           
*                                                                               
WORKD    DSECT                                                                  
APROCTAB DS    A                                                                
ADVOTAB  DS    A                                                                
PROCMAX  DS    F                   END ADDRESS OF TOTAL PROCTAB AREA            
PROCEND  DS    F                   END ADDRESS OF TABLE USED SO FAR             
DVECLEN  DS    F                                                                
SAVERE   DS    F                                                                
BQPAIR   DS    XL2                                                              
SUFFIX   DS    XL2                                                              
WORKX    DS    0H                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006MPXTND    05/01/02'                                      
         END                                                                    
