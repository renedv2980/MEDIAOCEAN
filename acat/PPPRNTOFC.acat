*          DATA SET PPPRNTOFC  AT LEVEL 031 AS OF 11/11/05                      
*CATALP PRNTOFC                                                                 
         PRINT NOGEN                                                            
         TITLE 'PP4102  CLIENT PRODUCT ESTIMATE LIST - PRNTOFC'                 
***********************************************************************         
*                                                                     *         
*      PRINT A MEDIA OFFICE CODE                                      *         
*        IF AGENCY USING 2 CH MEDIA OFFICE CODES PRINT THE 2 CH       *         
*              EXTERNAL VERSION OF THE CODE                           *         
*        ELSE PRINT THE HEX CODE FOR UN PRINTABLE CHARACTERS          *         
*                                                                     *         
*NTRY    PARM0+0 - C'R' PRINT RANGE IF PRESENT                        *         
*        PARM0   - A(OFFICE CODE - INTERNAL VERSION)                  *         
*                                                                     *         
*        PARM1+0 - C'L' OR C'S' - LONG OR SHORT NAME TO PRINT         *         
*        PARM1   - A(PRINT AREA)                                      *         
*                                                                     *         
*        PARM2   - A(OFFICER)                                         *         
*                                                                     *         
*        PARM3   - A(2 CH AGENCY)                                     *         
*                                                                     *         
*        PARM4   - A(COMFACS)                                         *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
PRNTOFC  CSECT                                                                  
         NMOD1 WORKDL,**POFC**                                                  
*                                                                               
         USING WORKD,RC            ESTABLISH WORKIJNG STORAGE                   
*                                                                               
         LR    R2,R1               SAVE A(PARAMETER LIST)                       
*                                                                               
         MVC   VOFFICER,8(R2)      SAVE OFFICER ADDRESS                         
         MVC   VCOMFACS,16(R2)     SAVE COMFACS ADDRESS                         
*                                                                               
*        TRANSLATE INTERNAL OFFICE CODE                                         
*                                                                               
         XC    WORK,WORK                                                        
         LA    R3,WORK             ESTABLISH OFFICER CONTROL BLOCK              
         USING OFFICED,R3                                                       
*                                                                               
         MVI   OFCSYS,C'P'         PRINT SYSTEM                                 
         L     RF,12(R2)                                                        
         MVC   OFCAGY,0(RF)        AGENCY                                       
         L     R4,0(R2)            POINT TO INTERNAL OFFICE CODE                
         MVC   OFCOFC,0(R4)        OFFICE CODE                                  
*                                                                               
         SR    R0,R0                                                            
*                                                                               
         CLI   4(R2),0             SKIP IF NO NAME WANTED                       
         BE    *+8                                                              
         LHI   R0,1                                                             
*                                                                               
         L     R4,4(R2)            POINT TO PRINT AREA                          
*                                                                               
*        TRANSLATE OFFICE CODE AND PRINT                                        
*                                                                               
         GOTOR VOFFICER,DMCB,(C'2',OFFICED),((R0),VCOMFACS),           X        
               (4(R2),4(R4))                                                    
*                                                                               
         TM    OFCINDS,OFCINOLA    SKIP IF AGY NOT ON 2 CH OFFICES              
         BO    PRNTOFC1                                                         
*                                                                               
         CLI   0(R1),0             EXIT ON ERRORS                               
         BNE   PRNTOFCX                                                         
*                                                                               
         MVC   0(2,R4),OFCOFC2     PRINT 2 CH OFFICE CODE                       
*                                                                               
         B     PRNTOFCX                                                         
*                                                                               
         DROP  R3                                                               
*                                                                               
PRNTOFC1 DS    0H                                                               
*                                                                               
         L     R3,0(R2)            POINT TO OFFICE CODE                         
         L     R4,4(R2)            POINT TO PRINT AREA                          
*                                                                               
         MVC   0(1,R4),0(R3)       PRINT OFFICE CODE                            
*                                                                               
*                                  CONDITIONALLY SHOW HEX OF OFFICE             
*                                                                               
         CLI   0(R3),X'40'         SHOW HEX VALUE ?                             
         BE    PRNTOFCX            NO                                           
         CLI   0(R3),X'00'         SHOW HEX VALUE ?                             
         BE    PRNTOFCX            NO                                           
         CLI   0(R3),X'D0'         SHOW HEX VALUE ?                             
         BE    PRNTOFC2            YES                                          
         CLI   0(R3),X'E0'         SHOW HEX VALUE ?                             
         BE    PRNTOFC2            YES                                          
         CLI   0(R3),C'A'          SHOW HEX VALUE ?                             
         BL    PRNTOFC2            YES                                          
         CLI   0(R3),C'9'          SHOW HEX VALUE ?                             
         BNH   PRNTOFCX            NO                                           
*                                                                               
PRNTOFC2 DS    0H                                                               
*                                                                               
******   GOTO1 HEXOUT,DMCB,0(R3),2(R4),1,0                                      
*                                                                               
         UNPK  DUB(3),0(2,R3)      OFFICE CODE NOW F0F0 TO FFFF                 
*                                                                               
         TR    DUB(2),HEXTABLE-C'0' TRANSLATE TO 00-FF                          
*                                                                               
         MVC   0(2,R4),DUB         PRINT HEX VALUE                              
*                                                                               
PRNTOFCX DS    0H                                                               
         XIT1                                                                   
*                                                                               
HEXTABLE DC    C'0123456789ABCDEF'                                              
*                                                                               
WORKD    DSECT                     WORKING STORAGE                              
DUB      DS    D                   WORKAREA                                     
VOFFICER DS    A                   A(OFFICER)                                   
VCOMFACS DS    A                   A(COMFACS)                                   
DMCB     DS    6F                  PARAMETER LIST                               
WORK     DS    XL128               WORKAREA                                     
*                                                                               
WORKDL   EQU   *-WORKD             SORKING STORAGE LENGTH                       
*                                                                               
         LTORG                                                                  
*                                                                               
       ++INCLUDE DDOFFICED                                                      
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031PPPRNTOFC 11/11/05'                                      
         END                                                                    
