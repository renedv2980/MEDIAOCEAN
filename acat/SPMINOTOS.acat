*          DATA SET SPMINOTOS  AT LEVEL 021 AS OF 05/01/02                      
*CATALP SPMINOTO                                                                
         TITLE 'SPMINOTO - MINUS OUT SPOTS WTH NO AFFID'                        
SPMINOTO CSECT                                                                  
*                                                                               
***********************************************************************         
*                                                                               
* PARAM 1   BYTE  0     NOT USED                                                
*                       ON RETURN X'80' MEANS REC WAS CHANGED                   
*                                 X'40' MEANS REC OVERFLOW                      
*           BYTES 0-3   A(SPOT BUY RECORD)                                      
*                                                                               
* PARAM 2   BYTE  0     PRODUCT                                                 
*           BYTES 0-3   A(2-BYTE START/END DATES)                               
*                                                                               
* PARAM 3   BYTE  0     2ND PRODUCT IF PIGGY REQ                                
*           BYTES 0-3   A(RECUP)                                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**MINO**                                                  
*                                                                               
         L     R6,0(R1)            A(BUY RECORD)                                
         USING BUYRECD,R6                                                       
         L     R5,4(R1)            A(START-END DATES)                           
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,SAVR1                                                         
         MVI   0(R1),0             CLEAR RETURN STATUS                          
         EJECT                                                                  
*                                                                               
         LA    R2,BDELEM                                                        
         USING REGELEM,R2                                                       
         MVI   WKELEM,0                                                         
         MVI   HAVAFF,C'N'                                                      
         MVC   RECUP,8(R1)         A(RECUP)                                     
*                                                                               
         MVC   PRD1,4(R1)          1ST PRODUCT                                  
         MVC   PRD2,8(R1)          2ND PRODUCT                                  
*                                                                               
MN02     DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
*                                                                               
         CLI   0(R2),0             EOR                                          
         BE    MN13                                                             
         CLI   0(R2),X'1F'         ELEM 20 OR HIGHER IS OUT OF SPOT             
         BH    MN02                ELEM RANGE, IGNORE                           
*                                                                               
         CLI   0(R2),X'0B'                                                      
         BL    MN02                                                             
         BE    MN04                                                             
         CLI   0(R2),X'0D'                                                      
         BH    MN10                                                             
*                                                                               
MN04     DS    0H                  HAVE OB, OC, OR OD                           
         BAS   RE,MINOTO           MINUS SAVED SPOT IF NECESSARY                
         BNZ   MNX                 OVERFLOW                                     
         CLI   RLEN,10             SKIP IF NOT ALLOCATED                        
         BE    MN02                                                             
         CLI   PRD1,X'FF'          'ALL' PRODUCTS                               
         BE    MN05                                                             
         CLI   PRD1,0              'ALL PRODUCTS'                               
         BE    MN05                                                             
         CLC   PRD1,RPPRD          EXACT MATCH                                  
         BNE   MN02                                                             
*                                                                               
MN05     DS    0H                                                               
         CLI   RLEN,14             IF ONLY ONE PRD IN ELEM                      
         BH    MN07                                                             
         CLI   PRD2,0              MUST NOT HAVE REQUESTED 2ND PROD             
         BNE   MN02                                                             
         B     MN08                                                             
*                                                                               
MN07     DS    0H                  ELEM HAS 2 PRODS                             
         CLI   PRD2,X'FF'          REQ'D 2ND PRD MUST BE 'ALL'                  
         BE    MN08                                                             
         CLC   PRD2,RPPRD+4        OR MATCH                                     
         BNE   MN02                                                             
*                                                                               
MN08     DS    0H                                                               
         TM    RSTATUS,X'C0'       MINUS OR MINUSED SPOT, SKIP                  
         BNZ   MN02                                                             
         CLC   RDATE,0(R5)         SKIP IF NOT WITHIN DATE RANGE                
         BL    MN02                                                             
         CLC   RDATE,2(R5)                                                      
         BH    MN13                DONE IF HIGH                                 
         MVC   WKELEM,0(R2)        SAVE GOOD ELEM                               
         ST    R2,ASPOT            AND ITS ADDRESS                              
         B     MN02                                                             
*                                                                               
MN10     DS    0H                  ELEM HIGHER THAN 0D                          
         CLI   0(R2),X'10'         AFFID ELEM                                   
         BNE   *+8                                                              
         MVI   HAVAFF,C'Y'                                                      
         B     MN02                                                             
*                                                                               
MN13     DS    0H                                                               
         BAS   RE,MINOTO           MINUS SAVED SPOT IF NECESSARY                
         BNZ   MNX                 OVERFLOW                                     
         B     MN50                AND DONE IN ANY CASE                         
*                                                                               
MN50     DS    0H                                                               
MNX      DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
*                                                                               
MINOTO   DS    0H                                                               
         LR    R0,RE                                                            
         CLI   WKELEM,0                                                         
         BE    MO20                                                             
         CLI   HAVAFF,C'Y'                                                      
         BE    MO20                                                             
*                                                                               
         MVI   WKELEM,X'0C'                MAKE NEW ELEM MINUS                  
         OI    WKELEM+RSTATUS-REGELEM,X'80'                                     
         XC    WKELEM+4(2),WKELEM+4        CLEAR ANY PAY DATE                   
*                                                                               
         L     RF,ASPOT                                                         
         OI    RSTATUS-REGELEM(RF),X'40'   MAKE OLD ELEM MINUSED                
*                                                                               
         SR    RE,RE               SET TO ADD AFTER MISSED SPOT                 
         IC    RE,1(RF)                                                         
         AR    RE,RF                                                            
         ST    RE,DMCB+8                                                        
         MVI   DMCB+8,C'R'                                                      
*                                                                               
         GOTO1 RECUP,DMCB,(C'S',BUYREC),WKELEM                                  
*                                                                               
         CLI   8(R1),C'R'                                                       
         BE    MO16                                                             
         L     R1,SAVR1                                                         
         OI    0(R1),X'40'         SET OVERFLOW                                 
         B     MO20                                                             
*                                                                               
MO16     DS    0H                                                               
         L     RF,DMCB+8                                                        
         SR    R2,R2                                                            
         IC    R2,1(RF)                                                         
         AR    R2,RF               POINT BEYOND ELEMENT JUST ADDED              
         L     R1,SAVR1                                                         
         MVI   0(R1),X'80'         SET REC ALTERED                              
*                                                                               
MO20     DS    0H                                                               
         MVI   WKELEM,0                                                         
         MVI   HAVAFF,C'N'                                                      
         LR    RE,R0                                                            
         L     R1,SAVR1                                                         
         TM    0(R1),X'40'         SET CC BASED ON OVERFLOW                     
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
WORKD    DSECT                                                                  
RECUP    DS    F                                                                
DMCB     DS    6F                                                               
SAVR1    DS    F                                                                
ASPOT    DS    F                                                                
HAVAFF   DS    C                                                                
PRD1     DS    X                                                                
PRD2     DS    X                                                                
WKELEM   DS    XL30                                                             
*                                                                               
WORKDL   EQU   *-WORKD                                                          
         EJECT                                                                  
*++INCLUDE SPGENBUY                                                             
BUYRECD  DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPGENBUY                                                       
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021SPMINOTOS 05/01/02'                                      
         END                                                                    
