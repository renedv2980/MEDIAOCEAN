*          DATA SET ACTYPCHK   AT LEVEL 017 AS OF 02/18/10                      
*CATALP ACTYPCHK                                                                
TYPCHK   TITLE ' ACCUMULATE TRANSACTION AMOUNTS BY TYPE'                        
TYPCHK   CSECT                                                                  
         PRINT NOGEN                                                            
BGN      NMOD1 0,**TYPC**                                                       
         LR    R9,R1                                                            
         USING PARMD,R9                                                         
         CLI   ACTION,C'O'         PROCESS TRANSACTION - OLD                    
         BE    PRCT                                                             
         CLI   ACTION,C'N'         PROCESS TRANSACTION - NEW                    
         BE    PRCT                                                             
         CLI   ACTION,C'P'         PRINT REPORT                                 
         BE    REPT                                                             
         CLI   ACTION,C'I'         INITIALIZE TABLE                             
         BE    INIT                                                             
         CLI   ACTION,C'A'         RETURN ACDDRESS OF TABLE                     
         BE    *+6                                                              
         DC    H'0'                INVALID ACTION                               
         LA    RE,ACCUM                                                         
         STCM  RE,7,RTNA                                                        
         B     XIT                                                              
*                                                                               
INIT     LA    RE,ACCUM            ZERO ACCUMULATORS                            
         LA    R0,ACCN                                                          
INIT3    ZAP   20(8,RE),=P'0'                                                   
         ZAP   28(8,RE),=P'0'                                                   
         ZAP   36(8,RE),=P'0'                                                   
         LA    RE,L'ACCUM(RE)                                                   
         BCT   R0,INIT3                                                         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* PROCESS TRANSACTION                                                 *         
***********************************************************************         
         SPACE 1                                                                
PRCT     SR    R2,R2               R2=A(TRANSACTION RECORD)                     
         ICM   R2,7,RECORD                                                      
         USING TRNRECD,R2                                                       
         CLC   TRNKUNT,LASTUL      TEST CHANGE OF UNIT LEDGER                   
         BE    PRCT3                                                            
         MVI   LSTAT,0                                                          
         LA    R1,LDGLST           TEST LEDGER TYPE                             
         LA    R0,LDGN                                                          
         CLC   0(2,R1),TRNKUNT                                                  
         BE    *+16                                                             
         LA    R1,L'LDGLST(R1)                                                  
         BCT   R0,*-14                                                          
         B     *+10                                                             
         MVC   LSTAT,2(R1)         SET LEDGER STATUS                            
*                                                                               
PRCT3    MVC   STAT1,LSTAT                                                      
         MVI   STAT2,0                                                          
         CLI   LSTAT,CSH           TEST CASH LEDGER                             
         BNE   PRCT4                                                            
         CLI   TRNKCULC+2,C'/'     TEST FUNNY CONTRA                            
         BNE   PRCT4                                                            
         CLI   TRNKCULC+6,C'/'     FOR CASH RECEIPT(FROM CLEARANCE)             
         BNE   PRCT4                                                            
         MVI   STAT1,MED           TREAT AS MEDIA PAYABLE                       
*                                                                               
PRCT4    LA    R3,TRNRECD+ACCORFST                                              
         CLI   ACTION,C'O'                                                      
         BE    *+8                                                              
         LA    R3,TRNRFST                                                       
         USING TRNELD,R3                                                        
         CLI   TRNEL,TRNELQ                                                     
         BE    *+6                                                              
         DC    H'0'                NOT A TRANSACTION                            
         MVI   DRCR,0                                                           
         TM    TRNSTAT,TRNSDR      TEST DEBIT                                   
         BNO   *+12                                                             
         OI    DRCR,DR                                                          
         B     *+8                                                              
         OI    DRCR,CR                                                          
*                                                                               
         TM    DRCR,CR             TEST CREDIT                                  
         BNO   PRCT8                                                            
         CLI   TRNTYPE,3           TEST TYPE 3                                  
         BNE   PRCT8                                                            
         LR    R4,R3                                                            
         SR    R0,R0                                                            
*                                                                               
PRCT5    IC    R0,1(R4)            FIND CHECK EXTRA DETAIL                      
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PRCT11                                                           
         CLI   0(R4),CEDELQ                                                     
         BNE   PRCT5                                                            
         MVI   STAT2,CHK           SET SYSTEM CHECK                             
         B     PRCT11                                                           
                                                                                
PRCT8    CLI   TRNTYPE,25                                                       
         BNE   PRCT11                                                           
         MVI   STAT2,A25                                                        
         CLC   TRNBREF,SPACES                                                   
         BNH   PRCT11                                                           
         MVI   STAT2,INP           SET TYPE 25 FROM INP                         
         EJECT                                                                  
***********************************************************************         
* ADD ITEM TO TABLE                                                   *         
***********************************************************************         
         SPACE 1                                                                
PRCT11   LA    RF,TYPLST           MATCH TYPE TO TABLE                          
PRCT13   CLI   0(RF),ALL           LAST ENTRY                                   
         BE    PRCT19                                                           
         CLC   TRNTYPE,0(RF)       TEST TYPE                                    
         BNE   PRCT17                                                           
         CLI   1(RF),0             TEST STATUS 1                                
         BE    *+14                                                             
         CLC   STAT1,1(RF)                                                      
         BNE   PRCT17                                                           
         CLI   2(RF),0             TEST STATUS 2                                
         BE    *+14                                                             
         CLC   STAT2,2(RF)                                                      
         BNE   PRCT17                                                           
         CLC   4(4,RF),=AL4(CLEAR)  TEST CLEARANCE LINE                         
         BNE   *+14                                                             
         CLC   TRNBTCH+2(4),SPACES  TEST SYSTEM GENERATED ITEM                  
         BNE   PRCT17                                                           
         MVC   BYTE,DRCR                                                        
         NC    BYTE,3(RF)          TEST DEBIT / CREDIT                          
         BNZ   PRCT19                                                           
PRCT17   LA    RF,L'TYPLST(RF)                                                  
         B     PRCT13                                                           
*                                                                               
PRCT19   LA    R1,20               R1=DISPLACEMENT TO DR/CR                     
         TM    DRCR,DR                                                          
         BO    *+8                                                              
         LA    R1,8(R1)                                                         
         ICM   RE,15,4(RF)         RE=ADDRESS OF ACCUM LINE                     
         AP    36(8,RE),=P'1'      COUNT ITEMS                                  
         AR    RE,R1                                                            
         AP    0(8,RE),TRNAMNT                                                  
         ICM   RE,15,8(RF)         RE=SUBTOTAL LINE                             
         BZ    *+18                                                             
         AP    36(8,RE),=P'1'      COUNT ITEMS                                  
         AR    RE,R1                                                            
         AP    0(8,RE),TRNAMNT                                                  
         LA    RE,TOTAL            RE=TOTAL LINE                                
         AP    36(8,RE),=P'1'      COUNT ITEMS                                  
         AR    RE,R1                                                            
         AP    0(8,RE),TRNAMNT                                                  
         B     XIT                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* PRINT THE REPORT                                                    *         
***********************************************************************         
         SPACE 1                                                                
REPT     BAS   RE,SETBOX                                                        
         SR    R3,R3                                                            
         ICM   R3,7,PLN            R3=PRINT LINE                                
         USING PLND,R3                                                          
         ICM   RF,15,PRINTER       RF=PRINT ROUTINE                             
         MVC   2(L'HEAD1,R3),HEAD1                                              
         BASR  RE,RF               PRINT HEADLINES                              
         MVC   2(L'HEAD2,R3),HEAD2                                              
         BASR  RE,RF                                                            
         BASR  RE,RF                                                            
         LA    R2,ACCUM            R2=ACCUMULATORS                              
         LA    R0,ACCN             R0=NUMBER OF ACCUMS                          
*                                                                               
REPT3    MVC   PLDES,0(R2)        CAPTION                                       
         EDIT  (P8,20(R2)),(14,PLDR),2,MINUS=YES                                
         EDIT  (P8,28(R2)),(14,PLCR),2,MINUS=YES                                
         EDIT  (P8,36(R2)),(14,PLNUM),ZERO=BLANK                                
         ZAP   DUB,20(8,R2)                                                     
         SP    DUB,28(8,R2)                                                     
         EDIT  (P8,DUB),(14,PLDIF),2,MINUS=YES                                  
         LA    R2,L'ACCUM(R2)                                                   
         BASR  RE,RF               PRINT IT                                     
         BCT   R0,REPT3                                                         
XIT      XIT1  ,                                                                
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* SET UP BOXES                                                        *         
***********************************************************************         
         SPACE 1                                                                
SETBOX   ICM   R7,15,BOXAREA                                                    
         BZR   RE                  NO BOXES                                     
         USING BOXD,R7                                                          
         MVI   BOXROWS,C' '                                                     
         MVC   BOXROWS+1(L'BOXROWS-1),BOXROWS                                   
         MVI   BOXROWS+6,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXCOLS,C' '                                                     
         MVC   BOXCOLS+1(L'BOXCOLS-1),BOXCOLS                                   
         LA    R3,BOXCOLS                                                       
         USING PLND,R3                                                          
         MVI   PLBXL,C'L'                                                       
         MVI   PLBX1,C'C'                                                       
         MVI   PLBX2,C'C'                                                       
         MVI   PLBX3,C'C'                                                       
         MVI   PLBX4,C'C'                                                       
         MVI   PLBXR,C'R'                                                       
         BR    RE                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS                                                      *         
***********************************************************************         
         SPACE 1                                                                
BYTE     DC    X'00'                                                            
DUB      DC    D'0'                                                             
WORK     DC    CL64' '                                                          
SPACES   DC    CL132' '                                                         
*                                                                               
LSTAT    DC    X'00'               LEDGER STATUS                                
MED      EQU   1                   MEDIA PAYABLE                                
EXP      EQU   2                   EXPENSE                                      
PRD      EQU   3                   PRODUCTION                                   
CSH      EQU   4                   CASH                                         
TAX      EQU   5                   TAX                                          
*                                                                               
STAT1    DC    X'00'               STATUS                                       
STAT2    DC    X'00'               STATUS                                       
CHK      EQU   1                   SYSTEM CHECK                                 
INP      EQU   2                   GENERATED VIA INP                            
A25      EQU   3                   GENERATED VIA A25                            
*                                                                               
DRCR     DC    X'00'               DEBIT/CREDIT                                 
DR       EQU   X'80'               DEBIT                                        
CR       EQU   X'40'               CREDIT                                       
ALL      EQU   X'FF'                                                            
*                                                                               
LASTUL   DC    CL2' '              LAST LEDGER                                  
LDGLST   DS    0CL3                LEDGERS                                      
         DC    C'SP',AL1(MED)                                                   
         DC    C'SQ',AL1(MED)                                                   
         DC    C'SS',AL1(MED)                                                   
         DC    C'ST',AL1(MED)                                                   
         DC    C'SU',AL1(MED)                                                   
         DC    C'SX',AL1(EXP)                                                   
         DC    C'SY',AL1(EXP)                                                   
         DC    C'SW',AL1(PRD)                                                   
         DC    C'SV',AL1(PRD)                                                   
         DC    C'SJ',AL1(PRD)                                                   
         DC    C'SC',AL1(CSH)                                                   
         DC    C'SB',AL1(CSH)                                                   
         DC    C'SG',AL1(TAX)                                                   
LDGN     EQU   ((*-LDGLST)/L'LDGLST)   NUMBER OF LEDGERS                        
*                                                                               
HEAD1    DC   C'Description                  Debits          Credits   X        
                   Difference     Transactions'                                 
HEAD2    DC   C'--------------------     -------------   ------------- X        
                 -------------    ------------'                                 
         EJECT                                                                  
***********************************************************************         
* TYPE LIST                                                           *         
***********************************************************************         
         SPACE 1                                                                
TYPLST   DS    0CL12                                                            
*              TYPE,STAT1,STAT2,DR/CR,A(ACCUMULATOR),A(SUB-TOTAL)               
*              CLEARANCES                                                       
         DC    AL1(00),AL1(0),AL1(0),AL1(DR+CR),AL4(CLEAR),AL4(0)               
         DC    AL1(33),AL1(MED),AL1(0),AL1(DR+CR),AL4(CLEAR),AL4(0)             
         DC    AL1(34),AL1(MED),AL1(0),AL1(DR+CR),AL4(CLEAR),AL4(0)             
         DC    AL1(35),AL1(MED),AL1(0),AL1(DR+CR),AL4(CLEAR),AL4(0)             
         DC    AL1(49),AL1(MED),AL1(0),AL1(CR),AL4(CLEAR),AL4(0)                
         DC    AL1(50),AL1(MED),AL1(0),AL1(CR),AL4(CLEAR),AL4(0)                
         DC    AL1(33),AL1(TAX),AL1(0),AL1(DR),AL4(CLEAR),AL4(0)                
         DC    AL1(34),AL1(TAX),AL1(0),AL1(DR),AL4(CLEAR),AL4(0)                
         DC    AL1(35),AL1(TAX),AL1(0),AL1(DR),AL4(CLEAR),AL4(0)                
         DC    AL1(49),AL1(TAX),AL1(0),AL1(DR),AL4(CLEAR),AL4(0)                
         DC    AL1(50),AL1(TAX),AL1(0),AL1(DR),AL4(CLEAR),AL4(0)                
*                                                                               
*              CHECKS                                                           
         DC    AL1(129),AL1(0),AL1(0),AL1(DR),AL4(CHECK),AL4(0)                 
         DC    AL1(03),AL1(0),AL1(CHK),AL1(CR),AL4(CHECK),AL4(0)                
*                                                                               
*              PROD BILLING                                                     
         DC    AL1(06),AL1(0),AL1(0),AL1(DR+CR),AL4(PBILL),AL4(0)               
*              $BILL $CBILL                                                     
         DC    AL1(07),AL1(0),AL1(0),AL1(DR+CR),AL4(CBILL),AL4(0)               
*                                                                               
*              MEDIA TRANSFER                                                   
         DC    AL1(09),AL1(0),AL1(0),AL1(DR+CR),AL4(MTRAN),AL4(0)               
*                                                                               
*              G/L UPDATE                                                       
         DC    AL1(25),AL1(0),AL1(A25),AL1(DR+CR),AL4(GLUPD),AL4(0)             
*                                                                               
*              TALENT                                                           
         DC    AL1(50),AL1(PRD),AL1(0),AL1(DR+CR),AL4(TALNT),AL4(0)             
*                                                                               
*              INPUT                                                            
         DC    AL1(01),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP01),AL4(INPUT)           
         DC    AL1(03),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP03),AL4(INPUT)           
         DC    AL1(05),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP05),AL4(INPUT)           
         DC    AL1(08),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP08),AL4(INPUT)           
         DC    AL1(10),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP10),AL4(INPUT)           
         DC    AL1(12),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP12),AL4(INPUT)           
         DC    AL1(14),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP14),AL4(INPUT)           
         DC    AL1(15),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP15),AL4(INPUT)           
         DC    AL1(19),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP19),AL4(INPUT)           
         DC    AL1(20),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP20),AL4(INPUT)           
         DC    AL1(21),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP21),AL4(INPUT)           
         DC    AL1(22),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP22),AL4(INPUT)           
         DC    AL1(25),AL1(0),AL1(INP),AL1(DR+CR),AL4(TYP25),AL4(INPUT)         
         DC    AL1(26),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP26),AL4(INPUT)           
         DC    AL1(27),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP27),AL4(INPUT)           
         DC    AL1(30),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP30),AL4(INPUT)           
         DC    AL1(33),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP33),AL4(INPUT)           
         DC    AL1(34),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP34),AL4(INPUT)           
         DC    AL1(36),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP36),AL4(INPUT)           
         DC    AL1(37),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP37),AL4(INPUT)           
         DC    AL1(41),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP41),AL4(INPUT)           
         DC    AL1(45),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP45),AL4(INPUT)           
         DC    AL1(46),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP46),AL4(INPUT)           
         DC    AL1(47),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP47),AL4(INPUT)           
         DC    AL1(48),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP48),AL4(INPUT)           
         DC    AL1(49),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP49),AL4(INPUT)           
         DC    AL1(50),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP50),AL4(INPUT)           
         DC    AL1(53),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP53),AL4(INPUT)           
         DC    AL1(54),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP54),AL4(INPUT)           
         DC    AL1(55),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP55),AL4(INPUT)           
         DC    AL1(56),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP56),AL4(INPUT)           
         DC    AL1(57),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP57),AL4(INPUT)           
         DC    AL1(58),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP58),AL4(INPUT)           
         DC    AL1(60),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP60),AL4(INPUT)           
         DC    AL1(61),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP61),AL4(INPUT)           
         DC    AL1(62),AL1(0),AL1(0),AL1(DR+CR),AL4(TYP62),AL4(INPUT)           
*                                                                               
*              OTHER                                                            
         DC    AL1(255),AL1(0),AL1(0),AL1(DR+CR),AL4(OTHER),AL4(0)              
*                                                                               
ACCUM    DS    0CL44                                                            
CLEAR    DC    CL20'Clearances          ',3PL8'0'                               
CHECK    DC    CL20'Checks              ',3PL8'0'                               
PBILL    DC    CL20'Production Billing  ',3PL8'0'                               
CBILL    DC    CL20'$Cbill / $Bill      ',3PL8'0'                               
MTRAN    DC    CL20'Media Transfer      ',3PL8'0'                               
GLUPD    DC    CL20'G/L Update          ',3PL8'0'                               
TALNT    DC    CL20'Talent              ',3PL8'0'                               
*                                                                               
TYP01    DC    CL20' 01 Invoice         ',3PL8'0'                               
TYP03    DC    CL20' 03 Manual Check    ',3PL8'0'                               
TYP05    DC    CL20' 05 Multi-Jrnl Entry',3PL8'0'                               
TYP08    DC    CL20' 08 Internal Invoice',3PL8'0'                               
TYP10    DC    CL20' 10 Payable Invoice ',3PL8'0'                               
TYP12    DC    CL20' 12 Order           ',3PL8'0'                               
TYP14    DC    CL20' 14 Write Off       ',3PL8'0'                               
TYP15    DC    CL20' 15 Billable Expense',3PL8'0'                               
TYP19    DC    CL20' 19 One-Sided       ',3PL8'0'                               
TYP20    DC    CL20' 20 Special J/E     ',3PL8'0'                               
TYP21    DC    CL20' 21 Invoice         ',3PL8'0'                               
TYP22    DC    CL20' 22 Manual Check    ',3PL8'0'                               
TYP25    DC    CL20' 25 G/L Update      ',3PL8'0'                               
TYP26    DC    CL20' 26 Manual Media/Bil',3PL8'0'                               
TYP27    DC    CL20' 27 Timesheets      ',3PL8'0'                               
TYP30    DC    CL20' 30 Cash Allocation ',3PL8'0'                               
TYP33    DC    CL20' 33 Invoice/Internal',3PL8'0'                               
TYP34    DC    CL20' 34 Job-To-Job      ',3PL8'0'                               
TYP36    DC    CL20' 36 Advance Payment ',3PL8'0'                               
TYP37    DC    CL20' 37 Void Check      ',3PL8'0'                               
TYP41    DC    CL20' 41 Timesheets      ',3PL8'0'                               
TYP45    DC    CL20' 45 Multi-Analyzed  ',3PL8'0'                               
TYP46    DC    CL20' 46 Multi-Invoice   ',3PL8'0'                               
TYP47    DC    CL20' 47 Estimated Prodn ',3PL8'0'                               
TYP48    DC    CL20' 48 Reverse Est/Prod',3PL8'0'                               
TYP49    DC    CL20' 49 Billable Time   ',3PL8'0'                               
TYP50    DC    CL20' 50 Type 50         ',3PL8'0'                               
TYP53    DC    CL20' 53 Multi-Anal. Acrl',3PL8'0'                               
TYP54    DC    CL20' 54 Multi-Anal. Revl',3PL8'0'                               
TYP55    DC    CL20' 55 Income Accrual  ',3PL8'0'                               
TYP56    DC    CL20' 56 Income Reverse  ',3PL8'0'                               
TYP57    DC    CL20' 57 Write-Offs      ',3PL8'0'                               
TYP58    DC    CL20' 58 Interagency     ',3PL8'0'                               
TYP60    DC    CL20' 60 Order Invoice   ',3PL8'0'                               
TYP61    DC    CL20' 61 Multi-Invoice   ',3PL8'0'                               
TYP62    DC    CL20' 62 Unit Pricing    ',3PL8'0'                               
INPUT    DC    CL20'*Total Input*       ',3PL8'0'                               
*                                                                               
OTHER    DC    CL20'Other               ',3PL8'0'                               
TOTAL    DC    CL20'** Total **         ',3PL8'0'                               
ACCN     EQU   ((*-ACCUM)/L'ACCUM)   NUMBER OF ACCUMULATOR LINES                
         DC    X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* LITERAL POOL                                                        *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR PARAMETER LIST                                            *         
***********************************************************************         
         SPACE 1                                                                
PARMD    DSECT                                                                  
PARM1    DS    0F                                                               
ACTION   DS    CL1           I=INIT, O=OLD TRANSACTN, N=NEW ,P=PRINT            
RECORD   DS    AL3           A(TRANACTION RECORD)                               
         ORG   RECORD                                                           
PLN      DS    AL3           A(PRINT LINE)                                      
         ORG   RECORD                                                           
RTNA     DS    AL3           A(TABLE)                                           
PARM2    DS    0F                                                               
PRINTER  DS    AL4           A(PRINT ROUTINE)                                   
BOXAREA  DS    AL4           A(BOX AREA)                                        
*                                                                               
PLND     DSECT                                                                  
PLBXL    DS    CL1                 LEFT                                         
         DS    CL1                                                              
PLDES    DS    CL20                DESCRIPTION                                  
         DS    CL3                                                              
PLBX1    DS    CL1                                                              
PLDR     DS    CL14                DEBIT                                        
         DS    CL1                                                              
PLBX2    DS    CL1                                                              
PLCR     DS    CL14                CREDIT                                       
         DS    CL1                                                              
PLBX3    DS    CL1                                                              
PLDIF    DS    CL14                DIFFERENCE                                   
         DS    CL1                                                              
PLBX4    DS    CL1                                                              
PLNUM    DS    CL14                NUMBER                                       
PLBXR    DS    CL1                 RIGHT                                        
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDBIGBOX                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017ACTYPCHK  02/18/10'                                      
         END                                                                    
