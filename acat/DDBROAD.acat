*          DATA SET DDBROAD    AT LEVEL 003 AS OF 01/12/96                      
*CATALP BROAD                                                                   
         TITLE 'SCAN UTL OR UTL ENTRY FOR PENDING BROADCAST'                    
BROAD    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**BROA**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(W/S)                                    
         MVC   PARMS,0(R1)                                                      
*                                                                               
         XC    RETCODE,RETCODE     CLEAR RETURN BYTE                            
*                                                                               
         TBIN  DDSTIME=YES         GET TIME CONVERT TO BINARY HHMM              
*&&US*&& AR    R0,R1               ADJUST FOR U.S. CLOCK DIFFERENCE             
         SRDL  R0,32               PREPARE FOR DIVIDE                           
         D     R0,=F'60'           CONVERT TO MINUTES                           
         SR    R0,R0               PREPARE FOR DIVIDE                           
         D     R0,=F'60'           R1 = HOURS, R0 = MINUTES                     
         STC   R1,TIME                                                          
         STC   R0,TIME+1                                                        
         EJECT                                                                  
*************************************************************                   
*        BUILD CHKPT DATA FOR BROADCAST                     *                   
*************************************************************                   
         SPACE 1                                                                
BUILDBC  EQU   *                                                                
         L     R3,ACHKPT           R3=A(CHKPNT)                                 
         USING CHKPTD,R3                                                        
*                                                                               
         L     R5,ABCTAB           SET BXLE FOR BCTAB R5                        
         USING BCTABD,R5                                                        
*                                                                               
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         OC    BCTLCNUM,BCTLCNUM   TEST IF BCTAB EMPTY                          
         BZ    BILDXX                                                           
*                                                                               
         LA    R5,BCTABL(R5)       SKIP FIRST ENTRY                             
*                                                                               
         XC    BCBYTE,BCBYTE       ZERO BITMASK                                 
         XC    BCBIT,BCBIT                                                      
*                                                                               
BILD     OC    BCTNUM,BCTNUM                                                    
         BZ    BILDXX              END OF BCTAB                                 
*                                                                               
         LH    RE,BCBIT            BUMP BIT MASK                                
         LH    RF,BCBYTE                                                        
         LA    RE,1(RE)                                                         
         CH    RE,=H'7'                                                         
         BNH   *+10                                                             
         SR    RE,RE                                                            
         LA    RF,1(RF)                                                         
         STH   RE,BCBIT            RE=WHICH BIT                                 
         STH   RF,BCBYTE           RF=WHICH BYTE                                
*                                                                               
         TM    BCTFLAG,BCTFDEL     IGNORE DELETED ENTRIES IN BCTAB              
         BO    BILDX                                                            
*                                                                               
         IC    RE,BITMASK(RE)      RE=BIT MASK                                  
         LA    RF,CHBCPNDG(RF)     RF=A(BYTE IN BRDCST PENDING LIST)            
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    0(RF),0             TEST IF BIT ALREADY ON FOR TRM               
         BO    BILDX               YES WE FOUND THIS MSG PREVIOUSLY             
*                                                                               
         STC   RE,BCMASK                                                        
         ST    RF,BCADDR                                                        
*                                                                               
         L     R4,AUTL             R4=A(UTL)                                    
         BAS   RE,TMATCH           TRY TO MATCH UTL TO BCTAB ENTRY              
         BNE   BILDX                                                            
*                                                                               
         L     RF,BCADDR           TURN ON BIT IN BC PENDING LIST               
         OC    0(1,RF),BCMASK                                                   
         OI    RETCODE,X'80'       SET TWA0 UPDATED - BC PENDING                
*                                                                               
BILDX    BXLE  R5,R6,BILD                                                       
*                                                                               
BILDXX   XMOD1 1                   END OF BRDCST TABLE                          
         EJECT                                                                  
*************************************************************                   
*        MATCH BCTAB ENTRY @ R5 WITH UTL @ R4               *                   
*        EXIT CC=EQU IF MATCHED CC=NEQ IF NOT               *                   
*************************************************************                   
         SPACE 1                                                                
         USING UTLD,R4                                                          
TMATCH   NTR1                                                                   
         OC    BCTSTTM,BCTSTTM     MSG HAS START TIME?                          
         BZ    *+14                                                             
         CLC   TIME,BCTSTTM                                                     
         BL    TMATCHX                                                          
         OC    BCTENTM,BCTENTM     MSG HAS END TIME?                            
         BZ    TMA010                                                           
         CLC   TIME,BCTENTM                                                     
         BH    TMATCHX                                                          
*                                                                               
TMA010   CLI   BCTCTRY,X'FF'       FILTER ON COUNTRY                            
         BE    TMA020                                                           
         CLC   BCTCTRY,TCTRY       EXACT MATCH IS OK                            
         BE    TMA020                                                           
         CLI   BCTCTRY,0           IF COUNTRY IS ZERO                           
         BNE   TMATCHX                                                          
         CLC   DEFCTRY,TCTRY       THEN OUR DEFAULT IS OK                       
         BNE   TMATCHX                                                          
*                                                                               
TMA020   CLI   BCTOVSYS,0          FILTER ON GENERIC SYSTEM                     
         BE    TMA030                                                           
         CLC   BCTOVSYS,TOVSYS                                                  
         BNE   TMATCHX                                                          
         CLI   BCTPRG,0            FILTER ON PROGRAM                            
         BE    TMA030                                                           
         CLC   BCTPRG,TPRG                                                      
         BNE   TMATCHX                                                          
*                                                                               
TMA030   CLI   BCTSYS,0            FILTER ON ACTUAL SYSTEM                      
         BE    TMA040                                                           
         CLC   BCTSYS,TSYS                                                      
         BNE   TMATCHX                                                          
         CLI   BCTPRG,0            AGAIN ON PROGRAM                             
         BE    TMA040                                                           
         CLC   BCTPRG,TPRG                                                      
         BNE   TMATCHX                                                          
*                                                                               
TMA040   CLI   BCTLUID,0           FILTER ON LUID                               
         BE    TMA050                                                           
         LA    R0,8                PROCESS 8 CHRS                               
         LA    RE,BCTLUID                                                       
         LA    RF,TSYM                                                          
TMA041   CLI   0(RE),C'*'          ALLOW WILD CARD CHRS                         
         BE    TMA042                                                           
         CLI   0(RE),C' '          PASS NULLS OR SPACES                         
         BNH   TMA042                                                           
         CLC   0(1,RE),0(RF)       ELSE MUST MATCH                              
         BNE   TMATCHX                                                          
TMA042   LA    RE,1(RE)            NEXT BYTE OF LUID                            
         LA    RF,1(RF)                                                         
         BCT   R0,TMA041                                                        
*                                                                               
TMA050   B     TMATCHY             END OF FILTERS                               
*                                                                               
TMATCHY  CR    RB,RB               EXIT CC=EQU                                  
         B     *+6                                                              
TMATCHX  LTR   RB,RB               EXIT CC=NEW                                  
         XIT1                                                                   
         DROP  R4                                                               
         EJECT                                                                  
BITMASK  DC    X'8040201008040201'                                              
DEFCTRY  DS    0X                                                               
*&&UK*&& DC    X'01'                                                            
*&&US*&& DC    X'02'                                                            
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
*                                                                               
         DS    0F                                                               
PARMS    DS    0CL12                                                            
PFLAGS   DS    0X                                                               
ABCTAB   DS    A                                                                
RETCODE  DS    0X                                                               
AUTL     DS    A                                                                
ACHKPT   DS    AL4                                                              
P4       DS    AL4                                                              
*                                                                               
TIME     DS    H                   TIME NOW HHMM                                
BCBYTE   DS    H                                                                
BCBIT    DS    H                                                                
BCMASK   DS    X                                                                
BCADDR   DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*FADSECTS                                                                       
*FABCTAB                                                                        
*FACHKPT                                                                        
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
       ++INCLUDE FABCTAB                                                        
       ++INCLUDE FACHKPT                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDBROAD   01/12/96'                                      
         END                                                                    
