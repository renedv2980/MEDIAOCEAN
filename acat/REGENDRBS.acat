*          DATA SET REGENDRBS  AT LEVEL 099 AS OF 05/18/00                      
*CATALP REGENDRB                                                                
***********************************************************************         
*                                                                     *         
*         REGENDRB --- PERFORMS LOOK UP OF REP-ID RECORD IN CONTROL   *         
*                  --- FILE                                           *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 03MAY00 (FDOY) --- ORIGINATION DATE                                 *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENDRB - REP ID LOOKUP ROUTINE'                               
***********************************************************************         
*  INPUT:                                                             *         
*              PARAMETER 1 =       A(3 CHAR REP CODE)                 *         
*                                      BYTE 1 C'R' = RADIO REP        *         
*                                             C'T' = TELEVISION REP   *         
*                                                                     *         
*                        2 =       A(25 BYTE STORAGE ENTRY)           *         
*                                      BYTE 1 X'80' READ HIGH FOR     *         
*                                             CODE IN P1, RETURN      *         
*                                             1ST RECORD FOUND WITH   *         
*                                             CORRECT MEDIA CODE, AND *         
*                                             PLACE NEXT RECORD'S     *         
*                                             CODE IN P1 IF VALID     *         
*                                        !!!!!OVERWRITES INPUT FIELD!!*         
*                                                                     *         
*                        3 =       A(COMFACS)                         *         
*                                      BYTE 1 SET TO X'80' ON RETURN  *         
*                                             IF REP RECORD NOT FOUND *         
*                                                                     *         
*                                             SET TO X'40' ON RETURN  *         
*                                             IF NO RECORDS FOLLOW    *         
*  OUTPUT:                                                            *         
*              MOVES 25 BYTE RECORD EXTRACT INTO ADDRESS SPECIFIED BY *         
*              INPUT PARAM 2 (FORMATTED SAME AS DDDARETAB ENTRY). IF  *         
*              NO RECORD FOUND FOR INPUT CODE, PARAM 3, BYTE 1 SET    *         
*              TO NON-ZERO VALUE.                                     *         
*                                                                     *         
***********************************************************************         
REGENDRB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDRB                                             
         USING WORKD,RC                                                         
*                                                                               
         BAS   RE,INIT              INITIALIZE PROGRAM                          
*                                                                               
         MVI   SWSYS#,X'0A'         SET FOR CONTROL SYSTEM SWITCH               
         BAS   RE,MYSWITCH          SWITCH TO CONTROL SYS                       
*                                                                               
         BAS   RE,GETREP            DO REP LOOKUP                               
*                                                                               
         MVC   SWSYS#,CALLSYS#      SET FOR SWITCH TO CALLER'S SYS              
         BAS   RE,MYSWITCH          SWITCH BACK TO CALLING SYS                  
*                                                                               
REGENDX  B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*              INIT ROUTINE                                       *             
*                                                                 *             
*******************************************************************             
*                                                                               
INIT     NTR1                                                                   
*-------------------------------------------------------------*                 
*                 STORE PARAMETERS                            *                 
*-------------------------------------------------------------*                 
         ST    R1,APARAMS         SAVE ADDRESS OF PARAM LIST                    
*                                       FOR FUTURE REFERENCE                    
*                                                                               
         MVC   MEDIA,0(R1)        SAVE 1ST PARAMETER PART A                     
         ZICM  R2,1(R1),3                (RETRIEVE CODE WITH                    
         MVC   REPCODE,0(R2)                         PART B)                    
         ST    R2,REPADDR                  AND STORE PART B                     
*                                                                               
         MVC   OPTFLG,4(R1)       SAVE 2ND PARAMETER PART A                     
         ZICM  R2,5(R1),3                                                       
         ST    R2,OUTADDR                            PART B                     
*                                                                               
         L     R3,8(R1)                3RD PARAMETER A(COMFACS)                 
         USING COMFACSD,R3                                                      
         L     R4,CDATAMGR                                                      
         ST    R4,VDATAMGR        SAVE A(DATAMGR)                               
         L     R4,CGETFACT                                                      
         ST    R4,VGETFACT        SAVE A(GETFACT)                               
         L     R4,CSWITCH                                                       
         ST    R4,VSWITCH         SAVE A(SWITCH)                                
         DROP  R3                                                               
*                                                                               
*-------------------------------------------------------------*                 
*                 SET VARIOUS FIELDS                          *                 
*-------------------------------------------------------------*                 
         MVC   DATADISP,=AL2(REPELDQ) RECORD DATA DISPLACEMENT                  
*                                     FOR GETEL                                 
*                                                                               
         MVI   DMINBTS,0          NO SPECIAL DATAMGR BITS                       
*                                                                               
         XC    REPENTRY,REPENTRY         CLEAR REPENTRY                         
*                                                                               
*-------------------------------------------------------------*                 
*             GET UTL, AND EXTRACT CALLER'S SE#               *                 
*-------------------------------------------------------------*                 
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD                                   
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         MVC   CALLSYS#,F@TSYS    SAVE CALLER'S SYS SE#                         
         DROP  R1                                                               
*                                                                               
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*              GETREP ROUTINE                                     *             
*                                                                 *             
*******************************************************************             
*                                                                               
GETREP   NTR1                                                                   
*                                                                               
         BAS   RE,BLDKEY                 BUILD KEY FOR REPID                    
         BAS   RE,HIGH                   LOOK FOR REP ID RECORD                 
         TM    OPTFLG,HIGHOPTQ           GET HIGH OPTION?                       
         BZ    GETREP20                   NO                                    
*                                                                               
         CLC   KEY(REPKREP-REPKEYD),KEYSAVE CORRECT MEDIA?                      
         BNE   GETREP50                       NO                                
         BAS   RE,GETREC                      YES                               
         BAS   RE,SETSEQ                    SET NEXT RECORD CODE                
         B     GETREP30                     BUILD REPENTRY                      
*                                                                               
GETREP20 CLC   KEY(REPKLENQ),KEYSAVE     EXACT RECORD FOUND?                    
         BNE   GETREP50                    NO                                   
         BAS   RE,GETREC                   YES                                  
*                                                                               
*----------------------------------------------------------*                    
*             BUILD REPENTRY FOR RETURN                    *                    
*----------------------------------------------------------*                    
GETREP30 LA    R6,IOAREA                                                        
         USING REPREPD,R6                                                       
         MVI   ELCODE,REPREPCQ                                                  
         BAS   RE,GETEL                  GET REP ELEMENT                        
         BE    *+6                                                              
         DC    H'0'                      MUST BE THERE                          
*                                                                               
         MVC   CODE,REPCODE              FILL IN REP ENTRY                      
         OC    CODE,SPACES               AND ENSURE CHAR                        
         MVC   NAME,REPNAME              FIELDS ARE SPACE                       
         OC    NAME,SPACES               PADDED                                 
         MVC   PREFIX,REPPREF                                                   
         OC    PREFIX,SPACES                                                    
         MVC   LENGTH,REPPRELN                                                  
         MVC   FLAG,REPFLAG                                                     
         B     GETREPX                   AND EXIT                               
         DROP  R6                                                               
*                                                                               
*                                                                               
GETREP50 L     R1,APARAMS                PARAMETER LIST ADDR                    
         OI    8(R1),REPNOTFQ            RETURN REP NOT FOUND                   
         XC    REPENTRY,REPENTRY                                                
*                                                                               
GETREPX  L     R1,OUTADDR                 ADDRESS OF OUTPUT BLOCK               
         MVC   0(L'REPENTRY,R1),REPENTRY  RETURN REPENTRY                       
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*              BUILD KEY ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
*                                                                               
BLDKEY   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING REPKEYD,R1                                                       
*                                                                               
         MVI   REPKSYS,REPKSYSQ          BUILD SEARCH KEY                       
         MVI   REPKTYP,REPKTYPQ                                                 
         MVC   REPKMEDA,MEDIA                                                   
         MVC   REPKREP,REPCODE                                                  
         MVC   KEYSAVE,KEY                                                      
         DROP  R1                                                               
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*                 SWITCH ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
*                                                                               
MYSWITCH NTR1                                                                   
         XC    DMCB(8),DMCB              CLEAR PARAMETER LIST                   
         GOTO1 VSWITCH,DMCB,(SWSYS#,0)   DO SWITCH                              
         CLI   4(R1),0                   SUCCESSFUL?                            
         BE    *+6                          YES                                 
         DC    H'0'                         NO                                  
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*                 DATAMANAGER CALLS                               *             
*                                                                 *             
*******************************************************************             
*                                                                               
HIGH     NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'GENDIR',          *        
               KEYSAVE,KEY,0                                                    
         B     EXIT                                                             
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'GENFILE',         *        
               KEY+(REPKDA-REPKEYD),IOAREA,DMWORK                               
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*                 SETSEQ ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
*                                                                               
SETSEQ   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'GENDIR',          *        
               KEYSAVE,KEY,0                                                    
         CLC   KEY(REPKREP-REPKEYD),KEYSAVE CORRECT MEDIA?                      
         BNE   SETSEQ50                                                         
*                                                                               
         L     R2,REPADDR                                                       
         LA    R1,KEY                                                           
         USING REPKEYD,R1                                                       
         MVC   0(L'REPKREP,R2),REPKREP                                          
         DROP  R1                                                               
         B     SETSEQX                                                          
*                                                                               
SETSEQ50 L     R1,APARAMS                PARAMETER LIST ADDR                    
         OI    8(R1),NOMOREQ             RETURN REP NOT FOUND                   
*                                                                               
SETSEQX  B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*******************************************************************             
*                                                                 *             
*                 GETEL ROUTINE                                   *             
*                                                                 *             
*******************************************************************             
*                                                                               
         PRINT GEN                                                              
         GETEL R6,DATADISP,ELCODE                                               
         PRINT NOGEN                                                            
         EJECT                                                                  
*                                                                               
*                                                                               
*******************************************************************             
*                                                                 *             
*                 COMMON BRANCHES                                 *             
*                                                                 *             
*******************************************************************             
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC              SET CONDITION CODE AT EXIT                    
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*/////////////////////////////////////////////////////////////////*             
*                       STORAGE                                   *             
*/////////////////////////////////////////////////////////////////*             
*                                                                               
SPACES   DC    10X'40'                                                          
*                                                                               
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL48                                                             
KEYSAVE  DS    CL48                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
DMINBTS  DS    X                                                                
OPTFLG   DS    X                OPTIONS CHOSEN BY CALLING ROUTINE               
HIGHOPTQ EQU   X'80'              RETURN RESULT OF READ HIGH                    
*                                                                               
CALLSYS# DS    XL1              CALLING SYSTEM SE#                              
SWSYS#   DS    XL1              'SWITCH TO'    SE#                              
MEDIA    DS    CL1              MEDIA TYPE FOR REP (T OR R)                     
REPCODE  DS    CL3                                                              
*                                                                               
REPENTRY DS    0CL25                                                            
CODE     DS    CL3                                                              
NAME     DS    CL10                                                             
FLAG     DS    XL1                                                              
LENGTH   DS    XL1                                                              
PREFIX   DS    CL10                                                             
*                                                                               
APARAMS  DS    A                                                                
REPNOTFQ EQU   X'80'   "REP NOT FOUND" RETURN FLAG                              
NOMOREQ  EQU   X'40'   "NO MORE RECORDS" RETURN FLAG                            
REPADDR  DS    A                                                                
OUTADDR  DS    A                                                                
VDATAMGR DS    A                                                                
VGETFACT DS    A                                                                
VSWITCH  DS    A                                                                
IOAREA   DS    1000X                                                            
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
*                                                                               
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE CTGENREPD                                                      
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'099REGENDRBS 05/18/00'                                      
         END                                                                    
