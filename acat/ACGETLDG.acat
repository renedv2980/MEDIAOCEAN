*          DATA SET ACGETLDG   AT LEVEL 001 AS OF 06/17/04                      
*CATALP GETLDG                                                                  
*********************************************************************           
*  PARAMETERS                                                       *           
*                                                                   *           
*  ON ENTRY:  P1 = A(COMP/UNIT/LEDG)                                *           
*             P2 = A(BUFFERIN)                                      *           
*             P3 = A(BUFFER)                                        *           
*             P3 = A(COMFACS)                                       *           
*                                                                   *           
*  RETURNS:   P1 = A(RECORD)                                        *           
*                                                                   *           
*********************************************************************           
         SPACE 1                                                                
GETLDG   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**GTLG**,R9                                                
         USING WORKD,RC            RC=A(GLOBAL W/S)                             
*                                                                               
         MVC   PARAMS(PARAMLNQ),0(R1)                                           
         ICM   RF,15,PACUL                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   CUL,0(RF)                                                        
         XC    STATUS,STATUS                                                    
         L     RF,PCOMFACS                                                      
         MVC   VDATAMGR,0(RF)                                                   
*                                                                               
         OC    PBUFFRIN,PBUFFRIN   BUFFERIN PASSED?                             
         BZ    GTLDG20                                                          
*                                                                               
         USING LEDGTBD,R4                                                       
         LA    R4,LDGWRK           BUILD KEY                                    
         MVC   LDGWRK,SPACES                                                    
         MVC   LEDGCPY(L'CUL),CUL                                               
*                                                                               
         GOTO1 PBUFFRIN,DMCB,('BUFFAGET',PBUFF),(0,(R4)),PCOMFACS               
         MVC   BUFFRET,BUFFERRS-BUFFPARM(R1)                                    
         TM    BUFFRET,BUFFERNF                                                 
         BO    GTLDG20                                                          
         OI    STATUS,STABUFF      MARK FOUND IN BUFFER                         
         B     GTLDG30                                                          
*                                                                               
GTLDG20  BAS   RE,RDLEDG           READ LEGDER RECORD                           
         BNE   EXIT                                                             
         LA    R4,LDGWRK                                                        
*                                                                               
GTLDG30  ST    R4,PLDGBLK          RETURN ADDRESS OF RECORD                     
         OC    PBUFFRIN,PBUFFRIN   BUFFERIN PASSED?                             
         BZ    EXIT                                                             
         TM    STATUS,STABUFF      FOUND IN BUFFER?                             
         BO    EXIT                                                             
         GOTO1 PBUFFRIN,DMCB,('BUFFAPUT',PBUFF),(0,(R4)),PCOMFACS               
         MVC   BUFFRET,BUFFERRS-BUFFPARM(R1)                                    
         CLI   BUFFRET,0                                                        
         BE    EXIT                                                             
         DC    H'0'                                                             
*                                                                               
EXIT     LA    R1,PARAMS                                                        
         XIT1  REGS=(R1)                                                        
         DROP  R4                                                               
         EJECT                                                                  
**********************************************************************          
* READ LEDGER RECORD TO GET LEVEL LENGTHS                            *          
**********************************************************************          
         SPACE 1                                                                
RDLEDG   NTR1                                                                   
         USING LEDGTBD,R2                                                       
         LA    R2,LDGWRK                                                        
         MVC   LDGWRK,SPACES                                                    
*                                                                               
         USING LDGRECD,R6                                                       
         LA    R6,SVKEY                                                         
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKCPY,COMPANY                                                  
         MVC   LDGKUNT(2),UNIT                                                  
*                                                                               
         GOTO1 =A(DMREADDR),DMCB,(RC)                                           
         CLI   8(R1),0                                                          
         BNE   RDLEDX                                                           
*                                                                               
         GOTO1 =A(DMGETREC),DMCB,(RC)                                           
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,IO                                                            
         MVC   LEDGCPY(L'LEDGCPY+L'LEDGTUL),0(R6)                               
         LA    R4,LDGRFST                                                       
         SR    R0,R0                                                            
RDLED10  CLI   0(R4),0             END OF RECORD?                               
         BE    RDLEDX                                                           
         CLI   0(R4),LDGELQ        X'14' - LEDGER ELEMENT                       
         BE    RDLED30                                                          
         CLI   0(R4),ACLELQ        X'16' - LEVEL LENGTHS                        
         BE    RDLED40                                                          
         CLI   0(R4),RSTELQ        X'30' - ACCOUNT STATUS                       
         BE    RDLED50                                                          
RDLED20  IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     RDLED10                                                          
*                                                                               
         USING LDGELD,R4                                                        
RDLED30  MVC   LEDGTTYP,LDGTYPE                                                 
         MVC   LEDGTLIK,LDGLIKE                                                 
         MVC   LEDGTOFP,LDGOPOS                                                 
         CLI   LDGLN,LDGSTAT2-LDGELD                                            
         BL    *+10                                                             
         MVC   LEDGTST2,LDGSTAT2                                                
         MVC   LEDGTDDL,LDGDPOS     EXTRACT DEPARTMENT DISPLACEMENT             
         B     RDLED20                                                          
         DROP  R4                                                               
*                                                                               
         USING ACLELD,R4                                                        
RDLED40  MVC   LEDGTDSA,ACLVLEN+(L'ACLVALS*0)                                   
         MVC   LEDGTDSB,ACLVLEN+(L'ACLVALS*1)                                   
         MVC   LEDGTDSC,ACLVLEN+(L'ACLVALS*2)                                   
         MVC   LEDGTDSD,ACLVLEN+(L'ACLVALS*3)                                   
*                                                                               
         XC    LEDGTLVA(LEDGLVLQ),LEDGTLVA  INIT INDIVIDUAL LENGTHS             
*                                                                               
         LA    R5,LEDGTDSA         R4 = COMBINED LENGTHS                        
         LA    R0,LEVELQ           R0 = MAXIMUM NUMBER OF LEVELS                
         LA    R2,LEDGTLVA         R2 = FIRST LEVEL INDIVIDUAL LENGTH           
         SR    R3,R3               R3 = PREV LEVEL LENGTH (INITIALLY 0)         
         SR    RE,RE                                                            
RDLED45  ICM   RE,1,0(R5)          CURRENT LEVEL LENGTH                         
         BZ    RDLED20             NO MORE LEVELS - ADJUST LEVNUM               
         SR    RE,R3               SUBTRACT CURRENT FROM PREVIOUS               
         STC   RE,0(R2)            CURRENT INDIVIDUAL LENGTH                    
         IC    R3,0(R5)            UPDATE R3                                    
         LA    R5,1(R5)                                                         
         LA    R2,1(R2)                                                         
         BCT   R0,RDLED45                                                       
         B     RDLED20                                                          
         DROP  R4                                                               
*                                                                               
         USING RSTELD,R4                                                        
RDLED50  MVC   LEDGTSEC,RSTSECY+1                                               
         B     RDLED20                                                          
         DROP  R4                                                               
*                                                                               
RDLEDX   B     EXIT                                                             
         DROP  R2,R6                                                            
         EJECT                                                                  
*********************************************************************           
* CONSTANTS                                                         *           
*********************************************************************           
         SPACE 1                                                                
SPACES   DC    CL50' '                                                          
*                                                                               
*********************************************************************           
* LITERALS                                                          *           
*********************************************************************           
         LTORG                                                                  
*                                                                               
*********************************************************************           
* DATAMGER INTERFACE                                                *           
*********************************************************************           
         SPACE 1                                                                
DMCTFIL  NMOD1 0,CTF               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'CTFILE ',SVKEY,IOKEY                 
         B     DMX                                                              
*                                                                               
DMWRTDR  NMOD1 0,WRT               WRITE BACK TO DIR                            
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'00',=C'DMWRT'),=C'ACCDIR',IOKEY,IOKEY           
         B     DMX                                                              
*                                                                               
DMADDDR  NMOD1 0,ADD               ADD KEY TO DIR                               
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'00',=C'DMADD'),=C'ACCDIR',IOKEY,IOKEY           
         B     DMX                                                              
*                                                                               
DMSEQDR  NMOD1 0,SEQ               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMRSEQ),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMHIGHDR NMOD1 0,HIGH              READ HIGH                                    
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMRDHI),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMREADDR NMOD1 0,READ              READ                                         
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMREAD),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMGETREC NMOD1 0,GREC              GET RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         USING ACCRECD,R3                                                       
         LA    R3,IO                                                            
         MVC   SVDA,ACCKDA         SAVE OFF DISK ADDRESS                        
         GOTO1 VDATAMGR,DMCB,DMGET,=C'ACCMST ',SVDA,IO,DMWORK                   
         B     DMX                                                              
         DROP  R3                                                               
*                                                                               
DMPUTREC NMOD1 0,PREC              PUT RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,=CL8'PUTREC',=C'ACCMST',SVDA,IO,DMWORK             
*                                                                               
DMX      XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* CONSTANTS                                                         *           
*********************************************************************           
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMADD    DC    C'DMADD   '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMGET    DC    C'GETREC  '                                                      
*                                                                               
DMWORK   DC    XL96'00'            FOR GETREC/PUTREC                            
         EJECT                                                                  
*********************************************************************           
* LITERALS                                                          *           
*********************************************************************           
         LTORG                                                                  
*********************************************************************           
* WORKING STORAGE                                                   *           
*********************************************************************           
WORKD    DSECT                                                                  
PARAMS   DS    0C                                                               
PACUL    DS    A                   ADDRESS OF COMPANY/UNIT/LEDGER               
PBUFFRIN DS    A                   ADDRESS OF BUFFERIN                          
PBUFF    DS    A                   ADDRESS OF BUFFERIN BUFFER                   
PCOMFACS DS    A                   ADDRESS OF COMFACS                           
         ORG   PACUL                                                            
PLDGBLK  DS    A                                                                
         ORG                                                                    
PARAMLNQ EQU   *-PARAMS                                                         
*                                                                               
VDATAMGR DS    A                   ADDRESS OF DATAMGR                           
BUFFRET  DS    XL1                                                              
*                                                                               
CUL      DS    0CL3                                                             
COMPANY  DS    X                                                                
UNIT     DS    C                                                                
LEDGER   DS    C                                                                
*                                                                               
DMCB     DS    0XL24               PARAMETER LIST                               
P1       DC    F'0'                                                             
P2       DC    F'0'                                                             
P3       DC    F'0'                                                             
P4       DC    F'0'                                                             
P5       DC    F'0'                                                             
P6       DC    F'0'                                                             
*                                                                               
LDGWRK   DS    CL(LEDGTABL)                                                     
*                                                                               
LEVNUM   DS    XL1                 NUMBER OF LEVELS FOR LEDGER                  
LEVEL    DS    XL1                 ACCOUNT LEVEL                                
LEVELQ   EQU   4                   MAXIMUM NUMBER OF LEVELS                     
*                                                                               
STATUS   DS    X                                                                
STABUFF  EQU   X'80'               FOUND IN BUFFERIN                            
*                                                                               
SVKEY    DS    CL42                SAVED AREA FOR DATAMGR                       
SVDA     DS    XL4                                                              
IOKEY    DS    0XL42                                                            
IO       DS    XL2042                                                           
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*********************************************************************           
* ++INCLUDES                                                        *           
*********************************************************************           
         SPACE 1                                                                
* ACGENFILE                                                                     
* DDBUFFD                                                                       
* ACLDGWRKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDBUFFD                                                        
       ++INCLUDE ACLDGWRKD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001ACGETLDG  06/17/04'                                      
         END                                                                    
