*          DATA SET DDMODLINK  AT LEVEL 001 AS OF 11/18/20                      
*CATALP MODLINK                                                                 
         TITLE 'MODLINK - LINK TO LOADABLE MODULES'                             
***********************************************************************         
* THIS MODULE LINKS MODULE CALLS FROM THE LOAD MODULE THAT INCLUDES   *         
* IT TO THE LOAD MODULE THAT CONTAINS THE REQUIRED MODULE.            *         
* REPLACES DMDMGRL. INCLUDE IN ANY MODULE THAT NEEDS TO CALL ANY OF   *         
* THE ITEMS LISTED ON A 'MODENTRY' MACRO                              *         
* NOTE MODLIB LOAD MODULE NAME HAS TEMPORARY 'A' SUFFIX FOR TESTING   *         
***********************************************************************         
         MACRO                                                                  
&ENTRY   MODENTRY &EQU=                                                         
         ENTRY &ENTRY                                                           
         AIF   (T'&EQU NE 'O').EQNTRY                                           
         USING *,RF                                                             
&ENTRY   MVHHI MLNTRYNO,&ENTRY.Q   USES TEMP 'USING MLPARMS,SAVEWORK'           
         J     MODL                                                             
         DROP  RF                                                               
         MEXIT                                                                  
.EQNTRY  ANOP                                                                   
&ENTRY   EQU   &EQU                                                             
         MEXIT                                                                  
         MEND                                                                   
*                                                                               
MODLINK  CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY MODLIB                                                           
         ENTRY AMODLIB                                                          
         ENTRY MODLCNT                                                          
         REQUS                                                                  
         DC    C'MODLINK '                                                      
*                                                                               
*        FOLLOWING TEMPORARY USING STATEMENT WORKS BECAUSE MLPARMS WILL         
*        BE DEFINED AT RD+72 (NORMAL WORK SPACE)                                
*                                                                               
         USING SAVEAREA,RD                                                      
         USING MLPARMS,SAVEWORK    TEMPORARY FOR MODENTRY MACROS                
DATAMGR  MODENTRY ,                ROUTINE NO. 01                               
DADDS    MODENTRY ,                ROUTINE NO. 02                               
ISDDS    MODENTRY ,                ROUTINE NO. 03                               
DMDANDX  MODENTRY ,                ROUTINE NO. 04                               
DYNALLOC MODENTRY ,                ROUTINE NO. 05                               
WORKER   MODENTRY ,                ROUTINE NO. 06                               
DMDABUFF MODENTRY ,                ROUTINE NO. 07                               
DMDALINK MODENTRY ,                ROUTINE NO. 08                               
DMDAPTRS MODENTRY ,                ROUTINE NO. 09                               
PQOPEN   MODENTRY ,                ROUTINE NO. 10                               
DMENQDEQ MODENTRY ,                ROUTINE NO. 11                               
DMOD000  MODENTRY ,                ROUTINE NO. 12                               
DMACCEMU MODENTRY ,                ROUTINE NO. 13                               
LOCKSPC  MODENTRY ,                ROUTINE NO. 14                               
DMDDNAME MODENTRY ,                ROUTINE NO. 15                               
LOCKUP   MODENTRY ,                ROUTINE NO. 16                               
MQRPT    MODENTRY ,                ROUTINE NO. 17                               
DMRCVUSS MODENTRY ,                ROUTINE NO. 18                               
DMISGENQ MODENTRY ,                ROUTINE NO. 19                               
DMSHMUSS MODENTRY ,                ROUTINE NO. 20                               
DMSYSFIL MODENTRY ,                ROUTINE NO. 21                               
DMFATABS MODENTRY ,EQU=DMSYSFIL                                                 
GETRET   MODENTRY ,                ROUTINE NO. 22                               
DMDYNDD  MODENTRY ,                ROUTINE NO. 23                               
*&&UK                                                                           
PROMOTE  MODENTRY ,                ROUTINE NO. 24 UK ONLY                       
*&&                                                                             
ADDAY    MODENTRY ,                ROUTINE NO. 25                               
GETDAY   MODENTRY ,                ROUTINE NO. 26                               
DATCON   MODENTRY ,                ROUTINE NO. 27                               
DATCONX  MODENTRY ,                ROUTINE NO. 28                               
DATVAL   MODENTRY ,                ROUTINE NO. 29                               
PERVAL   MODENTRY ,                ROUTINE NO. 30                               
PERVERT  MODENTRY ,                ROUTINE NO. 31                               
         DROP  RD                                                               
*                                                                               
MODL     STM   RE,RC,12(RD)        SAVE CALLERS REGISTERS                       
         LR    RE,RD               RE=A(LAST SAVE AREA)                         
         LA    RD,72+MLPARMSL(,RD) RD=A(NEXT SAVE AREA)                         
         ST    RE,4(,RD)           SAVE BACKWD CHAIN IN NEXT SAVE AREA          
         ST    RD,8(,RE)           SAVE FWD CHAIN IN THIS SAVEAREA              
*                                                                               
         LARL  RB,MODLINK          ESTABLISH ADDRESSABILITY                     
         USING MODLINK,RB                                                       
         MVC   0(4,RE),MDLKLIT     SET IDENTIFIER IN LAST SAVEAREA              
         LA    RC,72(,RE)          RC=A(MY WORK AREA (MLPARMS))                 
         USING MLPARMS,RC          REPLACES TEMPORARY MLPARMS USING             
*                                                                               
         ST    R1,MLAPARM          SAVE A(CALLER'S PARMS)                       
         LA    RE,MLFIXED                                                       
         ST    RE,MLAFIXED         SET A(FIXED ADDRESSES) IN MY PARMS           
*                                                                               
         L     RF,MODLCNT          COUNT CALLS                                  
         AHI   RF,1                                                             
         ST    RF,MODLCNT                                                       
*                                                                               
         LT    R3,AMODLIB                                                       
         JNZ   MODL10                                                           
         LOAD  EPLOC=MODLIB        LOAD MODLIB ON FIRST CALL                    
         ST    R0,AMODLIB                                                       
         LR    R3,R0               R3=A(MODLIB)                                 
*                                                                               
         USING MLRTND,R4                                                        
MODL10   LLH   R4,MLNTRYNO         R4=ENTRY POINT NUMBER                        
         BCTR  R4,0                MINUS 1 AS 1 BASED                           
         SLL   R4,3                *8 (ENTRY POINT TABLE WIDTH)                 
         AR    R4,R3               R4=A(REQUIRED ENTRY)                         
         CLC   MLNTRYNO,MLRTN#     MAKE SURE CORRECT ENTRY                      
         JNE   *+2                 DIE IF NOT (TABLE ERROR?)                    
*                                                                               
         L     R3,MLRTNMOD         R3=A(REQUIRED LOAD MOD TAB ENTRY)            
         USING MLMODD,R3                                                        
         LT    RF,MLMODADDR        WAS ROUTINE MODULE LOADED?                   
         JNZ   MODL20              YES                                          
         LOAD  EPLOC=MLMODNAME     LOAD REQUIRED MODULE IF NOT ALREADY          
         ST    R0,MLMODADDR        SAVE LOADED ADDRESS                          
         LR    RF,R0               RF=A(REQUIRED LOAD MODULE)                   
*                                                                               
         LT    R1,MLVSSB                                                        
         JZ    *+2                 MUST HAVE SSB                                
         USING SSBD,R1                                                          
         OC    SSOMASTC,SSOMASTC                                                
         JNZ   MODL14                                                           
         MVC   SSOMASTC,MLVMASTC                                                
         OC    SSOMORTN,SSOMORTN   LIST OF ROUTINES V(RTNS)                     
         JNZ   MODL14                                                           
         LA    RE,MLNTRYS          CAREFUL WITH MULTI-THREADED APPS             
         ST    RE,SSOMORTN         USED TO SHARE MODULES W/LOADED PHASE         
                                                                                
MODL14   TM    MLMODIND,DMGRMODQ   FIRST TIME FOR ANY DMGR MODULE?              
         JZ    MODL20                                                           
         OC    SVAMOD,SVAMOD       ALREADY DONE THIS?                           
         JNZ   MODL20              YES, SKIP IT                                 
                                                                                
*        JZ    MODL16              UNLESS DSPACE=N WAS SET                      
         CLI   SSOXTND,X'FF'                                                    
         JNE   *+2                 MUST BE EXTENDED SSB                         
*        JNE   MODL16                                                           
         CLI   SSODSPAC,C'N'                                                    
         JE    MODL20                                                           
         DROP  R1                  SSBD                                         
                                                                                
**********************************************************************          
* DSPACE IN SSB MUST BE SET AT THIS POINT, ELSE YOU WILL ATTEMPT TO             
*                  CONNECT TO THE PRODUCTION TABS                               
**********************************************************************          
MODL16   ST    RF,SVAMOD           SAVE ORIGINAL MODULE ADDRESS                 
         LA    R1,LOCKSPCP         LOCKSPC PARAMETERS, ATTACH TABS              
         LA    RF,LOCKSPC          LOCKSPC ENTRY POINT IN THIS MODULE           
         BASR  RE,RF               CALL OURSELF FOR LOCKSPC                     
         L     RF,SVAMOD           RESTORE ORIGINAL MODULE ADDRESS              
*                                                                               
MODL20   L     R1,MLMODCNT         COUNT TIMES CALLED FOR EACH MODULE           
         AHI   R1,1                                                             
         ST    R1,MLMODCNT                                                      
*                                                                               
         LR    R1,RC               R1=MLPARMS ADDRESS                           
         BASR  RE,RF               CALL MODULE (MODULE SAVES NOTHING!)          
*                                                                               
         L     RD,4(,RD)           RESTORE CALLERS REGISTERS & RETURN           
         LM    RE,RC,12(RD)                                                     
         BSM   0,RE                                                             
         DROP  R3,R4               MLMODD,MLRTND                                
*                                                                               
AMODLIB  DC    A(0)                                                             
MODLIB   DC    CL8'MODLIB'                                                      
*                                                                               
MDLKLIT  DC    CL4'MDLK'                                                        
MODLCNT  DC    F'0'                                                             
*        FOLLOWING TWO ITEMS ONLY USED ON FIRST DATA MANAGER CALL               
SVAMOD   DC    F'0' NON ZERO AFTER FIRST TIME LOCKSPC CALL DONE                 
LOCKSPCP DC    XL2'2000',AL2(DTCTB),AL4(0,0)  LOCKSPC PARAMS                    
*        DC    XL8'2000800100000000'                                            
*                                                                               
*        DDMODLINKD INCLUDED *WITHOUT* DSECT STATEMENT                          
       ++INCLUDE DDMODLINKD                                                     
*                                                                               
         LTORG                                                                  
*                                                                               
SAVEAREA DSECT ,                   DEFINES SAVEAREA AT RD                       
         DS    CL72                NORMAL 9D SAVE AREA AT RD                    
SAVEWORK EQU   *                   WORK AREA (USUALLY RC)                       
*                                                                               
SSBD     DSECT                                                                  
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
*FATABSDEQU                                                                     
       ++INCLUDE FATABSDEQU                                                     
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDMODLINK 11/18/20'                                      
         END                                                                    
