*          DATA SET SPMEDEQUIV AT LEVEL 035 AS OF 09/08/16                      
*CATALP MEDEQUIV                                                                
         TITLE 'EQUIVALENCY/DAYPART DEDUCTION'                                  
VMDEQUIV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 2,**EQIV**                                                       
         USING EQUIVD,RC                                                        
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         LR    R8,R1                                                            
         EJECT                                                                  
*              ESTABLISH VALUES FOR BUY OR GOAL                                 
         SPACE 3                                                                
         L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   MYSL,MEDSPTLN                                                    
         L     R7,ADBUY                                                         
         USING PEPREC,R7                                                        
         CLI   PEPKCDE,X'0D'                                                    
         BNE   EQ1                                                              
         CLI   PEPKTYP,X'15'                                                    
         BNE   EQ1                                                              
         MVI   BORG,C'P'                                                        
         MVC   MYDP,PEPKDPT                                                     
         B     EQ4                                                              
         DROP  R7                                                               
         SPACE 2                                                                
EQ1      DS    0C                                                               
         USING GOALREC,R7                                                       
         CLI   GKEYTYPE,X'02'                                                   
         BNE   EQ2                                                              
         MVI   BORG,C'G'                                                        
         MVC   MYDP,GKEYDPT                                                     
         B     EQ4                                                              
         SPACE 2                                                                
EQ2      DS    0H                                                               
         USING BUYREC,R7                                                        
         MVI   BORG,C'B'                                                        
         MVC   MYDP,BDDAYPT                                                     
         EJECT                                                                  
*              DAY PART LOOK UP                                                 
         SPACE 3                                                                
EQ4      L     R2,ADDPTTAB                                                      
         SR    R3,R3                                                            
         SPACE 2                                                                
EQ6      CLC   0(1,R2),MYDP        LOOK UP DAY-PART TABLE                       
         BE    EQ8                                                              
         AHI   R2,5                                                             
         AHI   R3,1                                                             
         CHI   R3,36                                                            
         BL    EQ6                                                              
         CLI   MYDP,C'Z'           DEFAULT                                      
         BNE   *+8                  NO - SET DEFAULT                            
         B     EQ8                                                              
*                                                                               
         MVI   MYDP,C'Z'                                                        
         B     EQ4                                                              
         SPACE 2                                                                
EQ8      STC   R3,DPNUM                                                         
* 09/08/2016, TZIH, MODD-67                                                     
* DAYPART NAMES SHOW UP WITH "CENT" CHARACTER IN BUY EXTRACTS                   
* SUPPRESSING THE 'RECODE' LOGIC.  RCOPT_NOCENT BIT SET IN SPREPDB02            
         TM    RCOPTS,RCOPT_NOCENT                                              
         BO    EQ9                                                              
*                                                                               
         CLI   0(R2),C'$'          RECODE $ DAYPARTS FOR MS REPORTS             
         BNE   *+8                                                              
         MVI   2(R2),C'Â›'                                                       
*                                                                               
EQ9      DS    0H                                                               
         MVC   DPHIGH,1(R2)                                                     
         MVC   OUT+1(5),0(R2)      SAVE THESE                                   
         LA    RE,LNDPT(R2)        SAVE PLANNING DPT ADDR.                      
         L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   MEDMPDPT,0(RE)      PLANNING DAYPART                             
         MVC   MEDDPGNO,OUT+2      GROUP NUMBER                                 
         NI    MEDDPGNO,X'F0'                                                   
         CLI   MEDDPGNO,0                                                       
         BNE   *+10                                                             
         MVC   MEDDPGNO,OUT+2                                                   
         DROP  R2                                                               
         L     R2,ADDPTTAB                                                      
         SPACE 2                                                                
EQ10     CLC   1(1,R2),OUT+2       NOW LOOK FOR FIRST OCCURENCE OF              
         BE    EQ12                DAY PART SUB-TOTAL CODE                      
         LA    R2,5(R2)                                                         
         B     EQ10                                                             
         SPACE 2                                                                
EQ12     MVC   OUT(3),2(R2)                                                     
         MVC   OUT+3(3),2(R2)      OVERRIDE ORIGINAL DAYPART                    
         SPACE 2                                                                
         L     R2,ADDPTTAB         NOW LOOK FOR FIRST OCCURENCE OF              
EQ13     DS    0H                  DAYPART SUB-TOTAL CODE                       
         MVC   DPHIGH2,1(R2)                                                    
         NI    DPHIGH,X'F0'        KILL LOW ORDER BITS                          
         NI    DPHIGH2,X'F0'                                                    
         TM    DPHIGH,X'F0'       ZERO LEVEL - NO SUB-TOTAL                     
         BZ    EQ13B                                                            
         CLC   DPHIGH,DPHIGH2                                                   
         BE    EQ13A                                                            
         LA    R2,5(R2)                                                         
         B     EQ13                                                             
         SPACE 2                                                                
EQ13A    MVC   OUT(3),2(R2)                                                     
EQ13B    L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   MEDDPNO,DPNUM                                                    
         CLC   OUT(3),OUT+3        IF DAYPART = GROUP                           
         BNE   *+8                                                              
         MVI   MEDDPNO,32          BUT NOW 31                                   
         MVC   MEDDPGRP,OUT                                                     
         MVC   MEDDPART,OUT+3                                                   
         DROP  R2                                                               
         EJECT                                                                  
* GET EQUIVALENCY FACTOR                                                        
         MVI   BYTE,C'R'                                                        
         CLI   MED,C'R'                                                         
         BE    EQ14                                                             
         CLI   MED,C'X'                                                         
         BE    EQ14                                                             
         MVI   BYTE,C'T'                                                        
         CLI   MED,C'T'                                                         
         BE    EQ14                                                             
         CLI   MED,C'N'                                                         
         BE    EQ14                                                             
         CLI   MED,C'C'                                                         
         BE    EQ14                                                             
         DC    H'0'                                                             
*                                                                               
EQ14     L     R1,VSLNTAB                                                       
         LH    RE,0(R1)            ENTRY LENGTH                                 
         L     RF,2(R1)            DSPL TO EOT                                  
         AR    RF,R1               RELOCATE EOT ADDRESS                         
         AHI   R1,6                POINT TO FIRST ENTRY                         
*                                                                               
EQ14A    CLC   0(2,R1),=C'00'      TEST DEFAULT ENTRY                           
         BE    EQ14B                                                            
         CLC   0(2,R1),AGY                                                      
         BNE   *+14                                                             
EQ14B    CLC   2(1,R1),BYTE                                                     
         BE    EQ14C                                                            
         BXLE  R1,RE,EQ14A                                                      
         DC    H'0'                                                             
*                                                                               
EQ14C    SR    RE,RE                                                            
         IC    RE,MYSL                                                          
         AR    RE,RE               X 2                                          
         LA    RE,4(R1,RE)         POINT TO ENTRY FOR THIS SLN                  
         SR    RF,RF                                                            
         IC    RF,0(RE)            GET DSPL TO FACTOR                           
         LA    RF,EQTAB(RF)        POINT TO ENTRY FOR THIS SLN                  
*                                                                               
         L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   MEDEQFAC,0(RF)                                                   
         DROP  R2                                                               
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
LNDPT    EQU   182                                                              
         SPACE 3                                                                
EQUIVD   DSECT                                                                  
MYDP     DS    CL1                                                              
MYSL     DS    CL1                                                              
BORG     DS    CL1                                                              
OUT      DS    CL7                                                              
DPNUM    DS    CL1                                                              
SLNUM    DS    CL1                                                              
DPHIGH   DS    C                                                                
DPHIGH2  DS    C                                                                
         PRINT OFF                                                              
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENBUY                                                       
       ++INCLUDE SPGENGOAL                                                      
       ++INCLUDE SPGENPEPSI                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035SPMEDEQUIV09/08/16'                                      
         END                                                                    
