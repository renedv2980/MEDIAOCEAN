*          DATA SET DDZIPFAC   AT LEVEL 019 AS OF 04/30/98                      
*CATALP ZIPFAC                                                                  
         TITLE 'FACPAK INTERFACE TO PKZIP'                                      
         PRINT NOGEN                                                            
ZIPFAC   CSECT                                                                  
         NMOD1 WORKL,*ZIPFAC*                                                   
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         USING WORKD,RC                                                         
*                                                                               
         LA    RE,*+10             PROGRAM IS IN XA                             
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
         ST    R1,CALLR1                                                        
         BAS   RE,PARMTEST         TEST PARAMETERS FOR VALIDITY                 
*                                                                               
         L     RA,=V(SYSFAC)                                                    
         USING SYSFACD,RA                                                       
*                                                                               
         BAS   RE,ZIPTO            MOVE INFORMATION TO DATASPACE                
*                                                                               
         BAS   RE,ZIPWAKE          CALL PKZIP TO WAKE UP                        
*                                                                               
         BAS   RE,WAIT             WAIT UNTIL POSTED FROM PKZIP                 
*                                                                               
         BAS   RE,ZIPFR            MOVE RESULTS FROM DATASPACE                  
*                                                                               
MAINX    REAR  ARS=OFF                                                          
         LA    RE,*+6              RETURN TO 24-BIT MODE                        
         BSM   0,RE                                                             
         XMOD1 ,                                                                
         EJECT                                                                  
***********************************************************************         
* TEST PARAMETERS FOR VALIDITY                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING ZIPWORKD,R1                                                      
PARMTEST TM    ZIPFLAG,(ZIPTIGHT+ZIPLOOSE)                                      
         BNM   INVCALL                                                          
         TM    ZIPFLAG,(ZIP1024+ZIP4096)                                        
         BO    INVCALL                                                          
         OC    ZIPIN,ZIPIN                                                      
         BZ    INVCALL                                                          
         ICM   RF,15,ZIPINL                                                     
         BZ    INVCALL                                                          
         CHI   RF,TZBUFLEN                                                      
         BH    INVCALL                                                          
         OC    ZIPOUT,ZIPOUT                                                    
         BZ    INVCALL                                                          
         OC    ZIPOUTL,ZIPOUTL                                                  
         BZ    INVCALL                                                          
         BR    RE                                                               
*                                                                               
INVCALL  L     R1,CALLR1                                                        
         USING ZIPWORKD,R1                                                      
         MVI   ZIPFLAG,ZIPERR                                                   
         B     MAINX                                                            
         EJECT                                                                  
***********************************************************************         
* MOVE INFORMATION TO DATASPACE FROM CALLER                           *         
***********************************************************************         
         SPACE 1                                                                
ZIPTO    NTR1  ,                                                                
         OC    ASIDFLD,ASIDFLD     ALREADY HAVE ASID?                           
         BNZ   ZIPTO02             YES                                          
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
*                                                                               
ZIPTO02  L     R0,ASIDFLD                                                       
         STH   R0,ASID                                                          
         L     RF,VSSB                                                          
         USING SSBD,RF                                                          
         L     RE,SSBTKADR                                                      
         MVC   FACPAK,SSBSYSN1     SET UP LOCKING TOKEN FROM FAC/TASK           
         MVC   TASK,TCBTASK-TCBD(RE)                                            
*                                                                               
         LAM   R2,R2,SSBTBLET                                                   
         STAM  R2,R2,ALET                                                       
         XR    R2,R2                                                            
         SAC   512                                                              
         DROP  RF                                                               
*                                                                               
         ICM   R2,15,TABSZIP-FATABSD(R2)                                        
         AHI   R2,TABSID+TZIPDSP+TABSID                                         
         USING TZIPHDRD,R2                                                      
*                                                                               
         LA    R0,TZHNUM           NUMBER OF SLOTS IN DATASPACE                 
ZIPTO04  XR    RE,RE               COMPARISON VALUE                             
         L     RF,LOCKID           REPLACEMENT VALUE IS LOCK TOKEN              
         CS    RE,RF,TZHID                                                      
         BE    ZIPTO06             FILLED THIS SLOT                             
         LA    R2,TZHLENQ(,R2)                                                  
         BCT   R0,ZIPTO04                                                       
         DC    H'0'                NEED TO FIND A WAY TO WAIT                   
*                                                                               
ZIPTO06  ST    R2,AHEADER          SAVE A(THIS HEADER)                          
*                                                                               
         LA    RF,ECB                                                           
         STCM  RF,15,TZHECB        SET A(ECB)                                   
*                                                                               
         XC    TZHZIP,TZHZIP                                                    
         XC    TZHULEN,TZHULEN                                                  
         XC    TZHCLEN,TZHCLEN                                                  
         XC    TZHFLAG3,TZHFLAG3                                                
         XC    TZHCRC,TZHCRC                                                    
*                                                                               
         L     R1,CALLR1                                                        
         USING ZIPWORKD,R1                                                      
         TM    ZIPFLAG,ZIPTIGHT    COMPRESS?                                    
         BZ    *+8                                                              
         MVI   TZHFLAG1,TZHF1CMP                                                
         TM    ZIPFLAG,ZIPLOOSE    UNCOMPRESS?                                  
         BZ    *+8                                                              
         MVI   TZHFLAG1,TZHF1UNC                                                
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,ZIPIN+1                                                     
         L     RF,ZIPINL           SET INPUT LENGTH IN CORRECT AREA             
         TM    ZIPFLAG,ZIPTIGHT                                                 
         BZ    *+8                                                              
         STH   RF,TZHULEN                                                       
         TM    ZIPFLAG,ZIPLOOSE                                                 
         BZ    *+8                                                              
         STH   RF,TZHCLEN                                                       
*                                                                               
         TM    ZIPFLAG,ZIPASCII    ASCII?                                       
         BZ    *+8                                                              
         OI    TZHFLAG3,TZHF3ASC                                                
         TM    ZIPFLAG,ZIP1024     1024 SIZE DICTIONARY                         
         BZ    *+8                                                              
         OI    TZHFLAG3,TZHF3SML                                                
         TM    ZIPFLAG,ZIP4096     4096 SIZE DICTIONARY                         
         BZ    *+8                                                              
         OI    TZHFLAG3,TZHF3BIG                                                
*                                                                               
         CPYA  R4,R2                                                            
         ICM   R4,15,TZHBODY                                                    
         LR    R5,RF                                                            
         MVCL  R4,RE               COPY DATA TO BODY OF THIS SLOT               
         XR    R4,R4                                                            
         SAR   R4,R4                                                            
*                                                                               
         MVI   TZHFLAG2,TZHF2NEW   SET NEW WORK - DO THIS LAST                  
*                                                                               
         SAC   0                   CLEAN UP                                     
         LAM   R0,RF,ARZERO                                                     
         B     EXITOK                                                           
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* CALL PKZIP TO AWAKEN                                                *         
***********************************************************************         
         SPACE 1                                                                
ZIPWAKE  NTR1  ,                                                                
         LAM   R2,R2,ALET                                                       
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,TABSZIP-FATABSD(R2)                                        
         LA    R2,TABSID(,R2)      GO PAST HEADER TO TABLE                      
*                                                                               
         USING TABSZIPD,R2                                                      
         LA    R0,TAZNUM           NUMBER OF SLOTS IN DATASPACE                 
         MVI   POSTNUM,0                                                        
         XC    AFSTZIP,AFSTZIP                                                  
*                                                                               
ZIPWK02  OC    TAZJOB,TAZJOB       JOB REGISTERED?                              
         BZ    ZIPWK08                                                          
         OC    AFSTZIP,AFSTZIP                                                  
         BNZ   *+8                                                              
         ST    R2,AFSTZIP          SET A(FIRST ZIP IMAGE)                       
         CLI   TAZFLAG,TAZFSLP     JOB SLEEPING?                                
         BNE   ZIPWK06                                                          
*                                                                               
ZIPWK04  L     R3,TAZPOST          SET A(WAKEUP ECB) IN R3                      
         LH    R5,TAZASID          GET ASCB FOR THE PKZIP IMAGE                 
         SAC   0                                                                
*                                                                               
         LOCASCB ASID=(R5)                                                      
         LTR   RF,RF               ASCB RETURNED IN R1 OK?                      
         BNZ   ZIPWK06             NO                                           
*                                                                               
         XR    RF,RF                                                            
         IC    RF,POSTNUM                                                       
         LA    RF,1(RF)                                                         
         STC   RF,POSTNUM                                                       
*                                                                               
         LR    R4,R1               SET ASCB IN R4                               
         POST (R3),99,ASCB=(R4),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTLF)           
         B     ZIPWAKEX                                                         
*                                                                               
ZIPWK06  SAC   512                                                              
         MVI   TAZFLAG,TAZFDED     SET IMAGE ABENDED (SO NO MORE POSTS)         
         CLI   POSTNUM,255                                                      
         BE    ZIPFUCK                                                          
*                                                                               
ZIPWK08  LA    R2,TZLENQ(,R2)      TRY NEXT IMAGE                               
         BCT   R0,ZIPWK02                                                       
         CLI   POSTNUM,0           POSTED ANYONE?                               
         BNE   ZIPWAKEX            YES                                          
         ICM   R2,15,AFSTZIP       A(FIRST ZIP IMAGE) SET?                      
         BZ    ZIPFUCK             NO                                           
         MVI   POSTNUM,255                                                      
         B     ZIPWK04             GO POST FIRST ONE FOR GOOD MEASURE           
*                                                                               
ZIPFUCK  DC    H'0',C'NO ZIP POST' DID NOT POST ANY ZIP IMAGES                  
*                                                                               
ZIPWAKEX LAM   R0,RF,ARZERO                                                     
         B     EXITOK                                                           
*                                                                               
POSTLF   POST  ERRET=ZIPWK06,ECBKEY=YES,MF=L                                    
         EJECT                                                                  
***********************************************************************         
* MOVE RESULTS FROM DATASPACE TO CALLER                               *         
***********************************************************************         
         SPACE 1                                                                
ZIPFR    NTR1  ,                                                                
         L     R1,CALLR1                                                        
         USING ZIPWORKD,R1                                                      
*                                                                               
         LAM   R2,R2,ALET                                                       
         L     R2,AHEADER                                                       
         SAC   512                                                              
         USING TZIPHDRD,R2                                                      
*                                                                               
         L     RE,ZIPOUT                                                        
         CLI   TZHFLAG1,TZHF1CMP   COMPRESS?                                    
         BNE   *+8                                                              
         LH    RF,TZHCLEN          PICK UP COMPRESSED LENGTH                    
         CLI   TZHFLAG1,TZHF1UNC   UNCOMPRESS?                                  
         BNE   *+8                                                              
         LH    RF,TZHULEN          PICK UP UNCOMPRESSED LENGTH                  
         ST    RF,ZIPOUTL          SET INPUT LENGTH IN CORRECT AREA             
         CHI   RF,TZBUFLEN                                                      
         BNH   *+6                                                              
         DC    H'0'                TOO MUCH TO COPY BACK                        
*                                                                               
         CPYA  R4,R2                                                            
         ICM   R4,15,TZHBODY                                                    
         LR    R5,RF                                                            
         MVCL  RE,R4               COPY DATA TO OUTPUT BUFFER                   
         XR    R4,R4                                                            
         SAR   R4,R4                                                            
*                                                                               
         XC    TZHZIP,TZHZIP                                                    
         XC    TZHULEN,TZHULEN                                                  
         XC    TZHCLEN,TZHCLEN                                                  
         XC    TZHFLAG3,TZHFLAG3                                                
         XC    TZHCRC,TZHCRC                                                    
         XC    TZHECB,TZHECB                                                    
         XC    TZHID,TZHID         CLEAR THIS LAST!                             
         SAC   0                                                                
         B     EXITOK                                                           
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* SET WAIT UNTIL POSTED                                               *         
***********************************************************************         
         SPACE 1                                                                
WAIT     ST    RE,SAVERE                                                        
         LA    RE,*+6                                                           
         BSM   0,RE                                                             
*                                                                               
         XC    ECB,ECB                                                          
         LA    R1,ECB                                                           
         ICM   RF,15,=V(ADWAIT)                                                 
         BZ    WAIT02                                                           
         BASSM RE,RF                                                            
         LA    RE,*+10                                                          
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
WAIT02   WAIT  ECB=ECB                                                          
         L     RE,SAVERE                                                        
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITL    CLI   *,255               SET CC LOW                                   
         B     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
         DC    CL8'ASIDFLD'                                                     
ASIDFLD  DC    D'0'                                                             
*                                                                               
ARZERO   DS    16F'0'                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* OTHER REQUIRED BOOKS                                                *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE FAPLO                                                          
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 2                                                                
WORKD    DSECT                                                                  
CALLR1   DS    A                   A(CALLERS R1)                                
SAVERE   DS    A                                                                
ALET     DS    A                                                                
AHEADER  DS    A                   A(THIS TZIPHDRD)                             
ECB      DS    A                   ECB TO POST COMPLETION/WAIT ON               
LOCKID   DS    0A                  LOCKING TOKEN                                
FACPAK   DS    X                                                                
TASK     DS    X                                                                
ASID     DS    H                                                                
AFSTZIP  DS    A                   A(FIRST ZIP IMAGE FOUND)                     
POSTNUM  DS    X                   COUNT OF POSTS                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 2                                                                
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FATABSD                                                                       
         PRINT OFF                                                              
       ++INCLUDE FATABSD                                                        
         PRINT ON                                                               
* FATABSZIP                                                                     
         PRINT OFF                                                              
       ++INCLUDE FATABSZIP                                                      
         PRINT ON                                                               
*DDZIPWRKD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDZIPWRKD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019DDZIPFAC  04/30/98'                                      
         END                                                                    
