*          DATA SET DDBUFFAHI  AT LEVEL 011 AS OF 08/23/04                      
*CATALP BUFFAHI                                                                 
         TITLE 'MODULE CONTROLS CORE/DISK BUFFER ALLOCATION'                    
       ++INCLUDE DMGREQUS                                                       
BUFFALO  CSECT                                                                  
         ENTRY BILLDTF                                                          
         ENTRY RECPRTRK                                                         
         PRINT NOGEN                                                            
         NMOD1 75,**BUFF**                                                      
         LA    R8,2048(RB)                                                      
         LA    R8,2048(R8)                                                      
         USING BUFFALO+4096,R8                                                  
         USING BUFFD,RC                                                         
         L     RA,4(R1)                                                         
         USING BUFFALOD,RA                                                      
         LR    R9,R1                                                            
         LA    R2,RELO                                                          
         S     R2,RELO                                                          
         ST    R2,RELDISP                                                       
         EJECT                                                                  
*              SELECT APPROPRIATE ROUTINE                                       
         SPACE 3                                                                
         SR    R4,R4                                                            
         LA    R2,ACTLIST                                                       
         L     R3,0(R9)                                                         
         SPACE 2                                                                
ACT2     CLI   0(R2),X'FF'                                                      
         BE    ACTERR                                                           
         CLC   0(3,R2),0(R3)                                                    
         BE    ACT4                                                             
         LA    R4,4(R4)                                                         
         LA    R2,3(R2)                                                         
         B     ACT2                                                             
         SPACE 2                                                                
ACT4     B     BRANCH(R4)                                                       
         SPACE 2                                                                
ACTERR   DC    H'0'                                                             
         DC    C'ACTION NOT RECOGNIZED'                                         
         SPACE 2                                                                
ACTLIST  DC    C'SET'                                                           
         DC    C'PUT'                                                           
         DC    C'GET'                                                           
         DC    C'HIG'                                                           
         DC    C'SEQ'                                                           
         DC    C'ADD'                                                           
         DC    C'CLE'                                                           
         DC    C'RES'                                                           
         DC    X'FF'                                                            
         SPACE 2                                                                
BRANCH   B     SET                                                              
         B     PUT                                                              
         B     GET                                                              
         B     HIGH                                                             
         B     SEQ                                                              
         B     ADD                                                              
         B     CLEAR                                                            
         B     RESET                                                            
         EJECT                                                                  
*              COMPLETE CSECT                                                   
         SPACE 3                                                                
SET      DS    0H                                                               
         SPACE 2                                                                
RESET    DS    0H                  COMPLETE CSECT DETAILS                       
         BAS   RE,CHECKEY                                                       
         LA    R0,RECPRTRK         CLEAR REC/TRACK TABLE.                       
         LH    R1,RECPRTRL                                                      
         SR    R3,R3               ZERO SOURCE LENGTH FORCES                    
         MVCL  R0,R2               THIS TO XC FROM R0 FOR R1.                   
         XC    BUFFADTI,BUFFADTI   FORCE RE-INIT OF TRACK INDEX, ETC.           
         LA    R5,4                                                             
         CLI   BUFFFLVR,C'P'                                                    
         BNE   *+8                                                              
         LA    R5,8                                                             
         CLI   BUFFFLVR,C'D'                                                    
         BNE   *+6                                                              
         SR    R5,R5                                                            
         ST    R5,BUFFWCOL                                                      
         M     R4,BUFFCOLS                                                      
         ST    R5,BUFFWROW                                                      
         XC    BUFFSOFA,BUFFSOFA                                                
         MVI   BUFFFLIP,0                                                       
         LA    R2,BUFFAREA                                                      
         A     R2,BUFFLALL                                                      
         ST    R2,BUFFADDR                                                      
         L     R2,=V(DADDS)                                                     
         A     R2,RELDISP                                                       
         ST    R2,BUFFDADS                                                      
         L     R2,=V(BINSRCH)                                                   
         A     R2,RELDISP                                                       
         ST    R2,BUFFBIN                                                       
         L     R2,=V(PRNTBL)                                                    
         A     R2,RELDISP                                                       
         ST    R2,VPRNTBL                                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO REESTABLISH KEY/RECORD SIZE                           
         SPACE 3                                                                
CHECKEY  NTR1                                                                   
         LA    R2,BUFFLIST                                                      
         SR    R3,R3                                                            
         SPACE 2                                                                
CK2      CLI   0(R2),X'FF'         END OF LIST                                  
         BE    CK4                                                              
         IC    R3,0(R2)            L'CUMULATIVE KEY                             
         LA    R2,2(R2)                                                         
         B     CK2                                                              
         SPACE 2                                                                
CK4      ST    R3,BUFFLKEY         SET KEY LENGTH                               
         L     R3,BUFFROWS         RECHECK RECORD LENGTH                        
         M     R2,BUFFCOLS                                                      
         SLL   R3,2                                                             
         CLI   BUFFFLVR,C'P'                                                    
         BNE   *+8                                                              
         SLL   R3,1                                                             
         CLI   BUFFFLVR,C'D'                                                    
         BNE   *+6                                                              
         SR    R3,R3                                                            
         ST    R3,BUFFLDTA                                                      
         A     R3,BUFFLKEY                                                      
         A     R3,BUFFLCOM                                                      
         ST    R3,BUFFLALL         SET TOTAL LENGTH                             
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO PUT AN ITEM INTO THE BUFFER                          
*              OR TO ADD TO ONE THAT IS ALREADY THERE                           
         SPACE 3                                                                
PUT      L     R2,8(R9)            A(RECORD TO BE PUT)                          
         OC    BUFFHOOK,BUFFHOOK                                                
         BZ    PUT1                                                             
         GOTO1 BUFFHOOK,PARAS,(R2),(RA)                                         
         SPACE 2                                                                
PUT1     L     R3,BUFFADDR         OTHER PARAS FOR BINSRCH                      
         L     R4,BUFFSOFA                                                      
         L     R5,BUFFLALL                                                      
         L     R6,BUFFLKEY                                                      
         L     R7,BUFFCRMX                                                      
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'RECORD BEFORE BINSRCH'                               
         BAS   RE,TRACE                                                         
         STM   R2,R7,PARAS                                                      
         MVI   PARAS,X'01'                                                      
         GOTO1 BUFFBIN,PARAS                                                    
         MVC   BUFFSOFA,PARAS+8    BINSRCH PASSES BACK NUMBER USED              
         CLI   PARAS,X'01'         WAS THE RECORD THERE ALREADY                 
         BE    PUT2                                                             
         OC    PARAS+1(3),PARAS+1                                               
         BZ    PUT2                                                             
         L     R2,PARAS            YES - SO ADD IN USERS VALUES                 
         L     R3,8(R9)                                                         
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'BINSRCH PREVIOUS'                                    
         BAS   RE,TRACE                                                         
         ST    R3,PARAS                                                         
         MVC   FIELD,=CL20'MY RECORD'                                           
         BAS   RE,TRACE                                                         
         BAS   RE,RECADD                                                        
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'ADDED RECORD'                                        
         BAS   RE,TRACE                                                         
         B     XIT                                                              
         SPACE 2                                                                
PUT2     OC    PARAS+1(3),PARAS+1  NO - DID IT FIT IN THE CORE BUFFER           
         BNZ   PUT4                NO                                           
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'DIDNT FIT - MERGING'                                 
         BAS   RE,TRACE                                                         
         BAS   RE,MERGE            SO DUMP OR MERGE BUFFER                      
         XC    BUFFSOFA,BUFFSOFA   RESET NUMBER SO FAR                          
         B     PUT                 AND GO BACK FOR ANOTHER SHOT                 
         SPACE 2                                                                
PUT4     L     R2,PARAS            NO - BUT IT DID FIT                          
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'NEW BINSRCH RECORD'                                  
         BAS   RE,TRACE                                                         
         SLL   R2,8                                                             
         SRL   R2,8                R2 HAS ADDRESS OF WHERE                      
         CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         L     R3,BUFFCOLS         JUST NEED TO CLEAR ROWS 2-N                  
*                                  (USER SUPPLIER ROW1)                         
         L     R4,BUFFWCOL         R4=WIDTH OF COLUMN                           
         L     R7,BUFFROWS                                                      
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BZ    XIT                                                              
         MR    R6,R3                                                            
         LR    R5,R7               R5=NUMBER OF BUCKETS TO BE CLEARED           
         A     R2,BUFFLKEY                                                      
         A     R2,BUFFLCOM                                                      
         A     R2,BUFFWROW         BUMP TO ROW 2                                
         CLI   BUFFFLVR,C'P'                                                    
         BE    PUT8                                                             
         S     R2,BUFFWROW         CHECK FOR BIG BINARY NUMBERS                 
         L     R0,BUFFCOLS                                                      
         SPACE 2                                                                
PUT5     TM    0(R2),X'C0'                                                      
         BNM   PUT5B                                                            
         CVDX  DUB,0(R2)                                                        
         CVBX  0(R2),DUB                                                        
         SPACE 2                                                                
PUT5B    LA    R2,4(R2)                                                         
         BCT   R0,PUT5                                                          
         SPACE 2                                                                
PUT6     XC    0(4,R2),0(R2)                                                    
         AR    R2,R4                                                            
         BCT   R5,PUT6                                                          
         B     XIT                                                              
         SPACE 2                                                                
PUT8     ZAP   0(8,R2),=P'0'                                                    
         AR    R2,R4                                                            
         BCT   R5,PUT8                                                          
         B     XIT                                                              
         EJECT                                                                  
*              LOGICAL READ ROUTINES                                            
         SPACE 3                                                                
GET      DS    0H                                                               
         SPACE 2                                                                
HIGH     LM    R2,R3,8(R9)         A(RECORD), LEVEL                             
         L     R4,BUFFLKEY                                                      
         BCTR  R4,0                R4=L'KEY - 1                                 
         EX    R4,KEYSAVE                                                       
         CLI   BUFFFLIP,0          IS RECORD IN CORE                            
         BNE   HIGH2                                                            
         L     R6,BUFFADDR                                                      
         L     R5,BUFFSOFA                                                      
         LTR   R5,R5                                                            
         BZ    EOFEXIT                                                          
         B     HIGH10                                                           
         SPACE 2                                                                
HIGH2    OC    BUFFSOFA,BUFFSOFA   MAY NEED TO MERGE                            
         BZ    HIGH4                                                            
         BAS   RE,MERGE                                                         
         XC    BUFFSOFA,BUFFSOFA                                                
         SPACE 2                                                                
HIGH4    L     R6,BUFFADTI                                                      
         L     R5,BUFFNOTI                                                      
         USING TRACKD,R6                                                        
         SPACE 2                                                                
HIGH6    EX    R4,TIXCOMP          FIND TRACK INDEX                             
         BNH   HIGH8                                                            
         SPACE 2                                                                
HIGH7    A     R6,BUFFLNTI                                                      
         BCT   R5,HIGH6                                                         
         B     EOFEXIT                                                          
         SPACE 2                                                                
HIGH8    ST    R5,TRKSLEFT                                                      
         ST    R6,ALASTIX          GET THIS BLOCK INTO CORE                     
         MVC   DSKADR+2(2),=X'0100'                                             
         MVC   DSKADR(2),TRACKDA                                                
         L     R7,AIOAREA                                                       
         GOTO1 BUFFDADS,PARAS,A(RDID),(R7),0,BILL,DSKADR,0                      
         BAS   RE,DADCHECK                                                      
         LH    R5,TRACKNUM                                                      
         LR    R6,R7                                                            
         SPACE 2                                                                
HIGH10   BAS   RE,FILTER           NOW LOOK FOR RECORD IN THIS BLOCK            
         BE    HIGH11                                                           
         CLI   TOOFAR,C'Y'                                                      
         BE    EOFEXIT                                                          
         B     HIGH12                                                           
         SPACE 2                                                                
HIGH11   EX    R4,RECCOMP                                                       
         BNH   HIGH14                                                           
         SPACE 2                                                                
HIGH12   A     R6,BUFFLALL                                                      
         BCT   R5,HIGH10                                                        
         CLI   BUFFFLIP,0                                                       
         BE    EOFEXIT                                                          
         L     R5,TRKSLEFT                                                      
         L     R6,ALASTIX                                                       
         B     HIGH7                                                            
         SPACE 2                                                                
HIGH14   BE    READOK              MISSED                                       
         L     R1,0(R9)                                                         
         CLC   0(3,R1),=C'HIGH'    OK FOR HIGH                                  
         BNE   MISSEXIT                                                         
         B     READOK                                                           
         SPACE 2                                                                
SEQ      LM    R2,R3,8(R9)                                                      
         L     R4,BUFFLKEY                                                      
         BCTR  R4,0                                                             
         EX    R4,KEYSAVE                                                       
         L     R5,RECSLEFT         SEQUENTIAL - IS NEXT RECORD IN CORE          
         L     R6,ALASTREC                                                      
         B     SEQ4                                                             
         SPACE 2                                                                
SEQ2     BAS   RE,FILTER                                                        
         BE    READOK                                                           
         CLI   TOOFAR,C'Y'                                                      
         BE    EOFEXIT                                                          
         SPACE 2                                                                
SEQ4     A     R6,BUFFLALL                                                      
         BCT   R5,SEQ2                                                          
         CLI   BUFFFLIP,0                                                       
         BE    EOFEXIT                                                          
         L     R6,ALASTIX          BUMP TO NEXT TI                              
         L     R5,TRKSLEFT                                                      
         A     R6,BUFFLNTI                                                      
         BCT   R5,SEQ6                                                          
         B     EOFEXIT                                                          
         SPACE 2                                                                
SEQ6     ST    R5,TRKSLEFT                                                      
         ST    R6,ALASTIX                                                       
         MVC   DSKADR+2(2),=X'0100'                                             
         MVC   DSKADR(2),TRACKDA                                                
         L     R7,AIOAREA                                                       
         GOTO1 BUFFDADS,PARAS,A(RDID),(R7),0,BILL,DSKADR,0                      
         BAS   RE,DADCHECK                                                      
         LH    R5,TRACKNUM                                                      
         LR    R6,R7                                                            
         B     SEQ2                                                             
         SPACE 2                                                                
READOK   ST    R5,RECSLEFT                                                      
         ST    R6,ALASTREC                                                      
         BAS   RE,BREAKS                                                        
         L     R5,BUFFLALL                                                      
         LTR   R3,R3                                                            
         BZ    READOK2                                                          
         SPACE 2                                                                
         L     R5,BUFFLKEY         OPTION TO READ AT LEVEL N (R3)               
         A     R5,BUFFLCOM                                                      
         MOVE  ((R2),(R5)),(R6)    SO PUT OUT KEY AND DATA                      
         CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         AR    R2,R5               BUMP PAST KEY                                
         AR    R6,R5                                                            
         BCTR  R3,0                                                             
         MH    R3,BUFFWROW+2                                                    
         AR    R6,R3               BUMP TO LEVEL N                              
         L     R5,BUFFWROW                                                      
         SPACE 2                                                                
READOK2  MOVE  ((R2),(R5)),(R6)                                                 
         B     XIT                                                              
         SPACE 2                                                                
EOFEXIT  MVI   8(R9),X'80'                                                      
         B     XIT                                                              
         SPACE 2                                                                
MISSEXIT MVI   8(R9),X'10'                                                      
         B     XIT                                                              
         SPACE 2                                                                
RECCOMP  CLC   0(0,R2),0(R6)                                                    
TIXCOMP  CLC   0(0,R2),TRACKKEY                                                 
         EJECT                                                                  
*              ROUTINE TO ESTABLISH CONTROL BREAKS                              
         SPACE 2                                                                
BREAKS   NTR1                                                                   
         LA    R3,BUFFLIST                                                      
         LA    R4,1                                                             
         SR    R5,R5                                                            
         SPACE 2                                                                
BREAKS2  IC    R5,0(R3)                                                         
         BCTR  R5,0                                                             
         EX    R5,COMPKEY                                                       
         BNE   BREAKS4                                                          
         LA    R3,2(R3)                                                         
         LA    R4,1(R4)                                                         
         CLI   0(R3),X'FF'                                                      
         BNE   BREAKS2                                                          
         SR    R4,R4                                                            
         SPACE 2                                                                
BREAKS4  STC   R4,BUFFCB                                                        
         MVC   BUFFUSER,1(R3)                                                   
         B     XIT                                                              
         SPACE 2                                                                
KEYSAVE  MVC   CBKEY(0),0(R2)                                                   
COMPKEY  CLC   CBKEY(0),0(R6)                                                   
         EJECT                                                                  
*              FILTERING RECORDS                                                
         SPACE 3                                                                
FILTER   NTR1                                                                   
         MVI   TOOFAR,C'N'                                                      
         CLI   4(R9),0             OPTION TO FILTER ON FIRST BYTE               
         BE    FILTER2                                                          
         CLC   4(1,R9),0(R6)                                                    
         BE    FILTER2                                                          
         BH    NO                                                               
         MVI   TOOFAR,C'Y'                                                      
         B     NO                                                               
         SPACE 2                                                                
FILTER2  CLI   BUFFFLVR,C'D'                                                    
         BE    YES                                                              
         LTR   R3,R3                                                            
         BZ    YES                                                              
         A     R6,BUFFLKEY         NOW CHECK FOR ACTIVITY                       
         A     R6,BUFFLCOM                                                      
         L     R2,BUFFCOLS                                                      
         BCTR  R3,0                                                             
         MH    R3,BUFFWROW+2                                                    
         AR    R6,R3                                                            
         CLI   BUFFFLVR,C'B'                                                    
         BNE   FILTER6                                                          
         SPACE 2                                                                
FILTER4  OC    0(4,R6),0(R6)                                                    
         BNZ   YES                                                              
         LA    R6,4(R6)                                                         
         BCT   R2,FILTER4                                                       
         B     NO                                                               
         SPACE 2                                                                
FILTER6  CP    0(8,R6),=P'0'                                                    
         BNE   YES                                                              
         LA    R6,8(R6)                                                         
         BCT   R2,FILTER6                                                       
         SPACE 2                                                                
NO       LA    R2,1                                                             
         LTR   R2,R2                                                            
         B     XIT                                                              
         SPACE 2                                                                
YES      SR    R2,R2                                                            
         LTR   R2,R2                                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO ADD AND CLEAR                                        
         SPACE 3                                                                
ADD      DS    0H                                                               
         SPACE 2                                                                
CLEAR    CLI   BUFFFLIP,0                                                       
         BNE   AC8                                                              
         SPACE 2                                                                
AC2      L     R2,BUFFADDR                                                      
         L     R3,BUFFSOFA                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         SPACE 2                                                                
AC4      BAS   RE,MAINT                                                         
         CLI   TOOFAR,C'Y'                                                      
         BE    XIT                                                              
         BAS   RE,ANYDATA                                                       
         BNE   AC6                                                              
         A     R2,BUFFLALL                                                      
         BCT   R3,AC4                                                           
         B     XIT                                                              
         SPACE 2                                                                
AC6      MVC   P2,BUFFADDR         TAKE ZERO RECORD OUT OF CORE                 
         MVC   P3,BUFFSOFA                                                      
         MVC   P4,BUFFLALL                                                      
         MVC   P5,BUFFLKEY                                                      
         MVC   P6,P3                                                            
         GOTO1 BUFFBIN,PARAS,(X'80',(R2))                                       
         CLI   PARAS,X'01'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     R5,BUFFSOFA                                                      
         BCTR  R5,0                                                             
         ST    R5,BUFFSOFA                                                      
         BCT   R3,AC4                                                           
         B     XIT                                                              
         SPACE 2                                                                
AC8      OC    BUFFSOFA,BUFFSOFA   DISK ROUTINES                                
         BZ    *+8                                                              
         BAS   RE,MERGE            MERGE ANY LURKERS                            
         XC    BUFFSOFA,BUFFSOFA                                                
         L     R7,AIOAREA                                                       
         L     R6,BUFFADTI         SET UP FOR EACH TRACK                        
         L     R5,BUFFNOTI                                                      
         USING TRACKD,R6                                                        
         SPACE 2                                                                
AC10     MVC   SAVENUM,TRACKNUM                                                 
         MVC   DSKADR(2),TRACKDA                                                
         MVC   DSKADR+2(2),=X'0100'                                             
         GOTO1 BUFFDADS,PARAS,A(RDID),(R7),0,BILL,DSKADR,0                      
         MVC   DADPARS,PARAS                                                    
         BAS   RE,DADCHECK                                                      
         BAS   RE,ACTRK                                                         
         BAS   RE,PUTTRACK                                                      
         CLC   SAVENUM,TRACKNUM    DID MAINT TAKE OUT RECORDS                   
         BE    AC14                                                             
         LH    R1,TRACKDA          YES                                          
         SLL   R1,1                                                             
         LA    R1,RECPRTRK-2(R1)                                                
         MVC   0(2,R1),TRACKNUM    ADJUST RECORDS PER TRACK                     
         OC    TRACKNUM,TRACKNUM                                                
         BZ    AC12                                                             
         LH    R1,TRACKNUM         STILL SOME LEFT                              
         BCTR  R1,0                HIGHEST KEY ON TRACK MAY CHANGE              
         MH    R1,BUFFLALL+2                                                    
         AR    R1,R7                                                            
         L     R4,BUFFLKEY                                                      
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     AC14                                                             
         MVC   TRACKKEY(0),0(R1)                                                
         SPACE 2                                                                
AC12     MVC   P2,BUFFADTI         NO RECORDS LEFT                              
         MVC   P3,BUFFNOTI         NEED TO TAKE OUT TRACK INDEX                 
         MVC   P4,BUFFLNTI                                                      
         MVC   P5,BUFFLKEY                                                      
         MVC   P6,P3                                                            
         MVI   P5,TRACKKEY-TRACKD                                               
         GOTO1 BUFFBIN,PARAS,(X'80',(R6))                                       
         CLI   PARAS,X'01'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     R1,BUFFNOTI         R6 WILL NOW ADDRESS NEXT ENTRY               
         BCTR  R1,0                                                             
         ST    R1,BUFFNOTI                                                      
         LTR   R1,R1               IF THEY'VE ALL GONE                          
         BNZ   *+8                                                              
         MVI   BUFFFLIP,0          WE'RE BACK IN CORE                           
         BCT   R5,AC10                                                          
         B     XIT                                                              
         SPACE 2                                                                
AC14     A     R6,BUFFLNTI         BUMP TO NEXT TI                              
         CLI   TOOFAR,C'Y'                                                      
         BE    XIT                                                              
         BCT   R5,AC10                                                          
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO MAINTAIN RECORDS ON A TRACK                           
         SPACE 3                                                                
ACTRK    NTR1                                                                   
         LR    R2,R7                                                            
         LH    R3,TRACKNUM                                                      
         SPACE 2                                                                
ACTRK2   BAS   RE,MAINT                                                         
         BAS   RE,ANYDATA                                                       
         BE    ACTRK4                                                           
         XC    PARAS(24),PARAS     ZERO RECORD CAN COME OUT                     
         LH    R4,TRACKNUM                                                      
         ST    R4,P3                                                            
         ST    R4,P6                                                            
         BCTR  R4,0                                                             
         STH   R4,TRACKNUM                                                      
         MVC   P4,BUFFLALL                                                      
         MVC   P5,BUFFLKEY                                                      
         GOTO1 BUFFBIN,PARAS,(X'80',(R2)),(R7)                                  
         CLI   PARAS,X'01'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         BCT   R3,ACTRK2                                                        
         B     XIT                                                              
         SPACE 2                                                                
ACTRK4   A     R2,BUFFLALL                                                      
         BCT   R3,ACTRK2                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK IF RECORD HAS SIGNIFICANT DATA                  
         SPACE 3                                                                
ANYDATA  NTR1                                                                   
         CLI   BUFFFLVR,C'D'                                                    
         BE    ANYYES                                                           
         L     RF,BUFFCOLS                                                      
         M     RE,BUFFROWS                                                      
         L     RE,BUFFLKEY                                                      
         A     RE,BUFFLCOM                                                      
         AR    RE,R2                                                            
         CLI   BUFFFLVR,C'B'                                                    
         BE    ANYDATA4                                                         
         SPACE 2                                                                
ANYDATA2 CP    0(8,RE),=P'0'                                                    
         BNE   ANYYES                                                           
         LA    RE,8(RE)                                                         
         BCT   RF,ANYDATA2                                                      
         B     ANYNO                                                            
         SPACE 2                                                                
ANYDATA4 OC    0(4,RE),0(RE)                                                    
         BNZ   ANYYES                                                           
         LA    RE,4(RE)                                                         
         BCT   RF,ANYDATA4                                                      
         SPACE 2                                                                
ANYNO    SR    RF,RF                                                            
         B     ANYALL                                                           
         SPACE 2                                                                
ANYYES   LA    RF,1                                                             
         SPACE 2                                                                
ANYALL   CH    RF,=H'1'                                                         
         B     XIT                                                              
         EJECT                                                                  
*              MAIN MERGE LOGIC - CORE/DISK MERGE                               
         SPACE 2                                                                
MERGE    NTR1                                                                   
         CLI   BUFFFLIP,0          IS THIS THE FIRST DISK ACTIVITY              
         BNE   MERGE10                                                          
         MVI   BUFFFLIP,C'A'                                                    
         GOTO1 OPENER                                                           
*                                                                               
         L     R3,BUFFADDR                                                      
         L     R6,BUFFADTI                                                      
         L     R4,BUFFCRMX                                                      
         SPACE 2                                                                
MERGE3   L     R2,BUFFNBLK                                                      
         LR    R5,R2                                                            
         MH    R5,BUFFLALL+2                                                    
         SPACE 2                                                                
MERGE4   CR    R4,R2               WILL THE REMAINING RECORDS                   
         BH    MERGE6              FIT IN A BLOCK                               
         LR    R2,R4               YES SO WRITE A BLOCK WITH THESE              
         BAS   RE,ADDBLOCK         RECORDS AND WE'RE DONE                       
         B     XIT                                                              
         SPACE 2                                                                
MERGE6   BAS   RE,ADDBLOCK         ELSE WRITE A BLOCK                           
         AR    R3,R5                                                            
         SR    R4,R2                                                            
         A     R6,BUFFLNTI                                                      
         B     MERGE4                                                           
         EJECT                                                                  
*              ROUTINES TO MERGE CORE RECORDS INTO DISK BLOCKS                  
         SPACE 3                                                                
MERGE10  L     R3,BUFFADDR                                                      
         L     R4,BUFFSOFA                                                      
         L     R5,BUFFNOTI                                                      
         USING TRACKD,R6                                                        
         L     R6,BUFFADTI                                                      
         L     R2,BUFFLKEY                                                      
         BCTR  R2,0                                                             
         SPACE 2                                                                
MERGE12  EX    R2,KEYCOMP          COULD THIS RECORD BE ON THIS TRACK           
         BNH   MERGE14                                                          
         SPACE 2                                                                
MERGE13  A     R6,BUFFLNTI         NO SO TRY NEXT TRACK                         
         BCT   R5,MERGE12                                                       
         B     MERGE3              NO MORE TRACK INDEXES SO GO OFF TO           
*                                  WRITE REMAINING RECORDS                      
KEYCOMP  CLC   0(0,R3),TRACKKEY                                                 
         SPACE 2                                                                
MERGE14  L     R7,AIOAREA          YES - SO GET BLOCK INTO CORE                 
         MVC   DSKADR(2),TRACKDA                                                
         MVC   DSKADR+2(2),=X'0100'                                             
         GOTO1 BUFFDADS,PARAS,A(RDID),(R7),0,BILL,DSKADR,0                      
         MVC   DADPARS,PARAS                                                    
         BAS   RE,DADCHECK                                                      
         SPACE 2                                                                
MERGE16  XC    PARAS(24),PARAS     SET UP FOR BINSRCH                           
         ST    R3,P1                                                            
         ST    R7,P2                                                            
         MVC   P3+2(2),TRACKNUM                                                 
         MVC   P4,BUFFLALL                                                      
         MVC   P5,BUFFLKEY                                                      
         MVC   P6,BUFFNDSK                                                      
         MVI   P1,X'01'                                                         
         GOTO1 BUFFBIN,PARAS       IS THE RECORD THERE ALREADY                  
         OC    P1+1(3),P1+1                                                     
         BZ    MERGE24                                                          
         CLI   PARAS,X'01'                                                      
         BE    MERGE18                                                          
         LR    R0,R2                                                            
         L     R2,PARAS                                                         
         BAS   RE,RECADD           YES - SO ADD IN NEW VALUES                   
         LR    R2,R0                                                            
         B     MERGE20                                                          
         SPACE 2                                                                
MERGE18  OC    P1+1(3),P1+1        DID IT FIT                                   
         BZ    MERGE24             YES                                          
         MVC   TRACKNUM,P3+2       UPDATE N'RECORDS ON TRACK                    
         LH    R1,TRACKDA          AND OUR DIRECTORY                            
         SLL   R1,1                                                             
         LA    R1,RECPRTRK-2(R1)                                                
         MVC   0(2,R1),TRACKNUM                                                 
         SPACE 2                                                                
MERGE20  A     R3,BUFFLALL         BUMP TO NEXT CORE RECORD                     
         BCT   R4,MERGE22                                                       
         BAS   RE,PUTTRACK         DONE  - WRITE BACK LAST BLOCK                
         B     XIT                                                              
         SPACE 2                                                                
MERGE22  EX    R2,KEYCOMP          IS THE NEXT RECORD ON THIS TRACK             
         BNH   MERGE16                                                          
         BAS   RE,PUTTRACK                                                      
         B     MERGE13                                                          
         SPACE 2                                                                
PUTTRACK NTR1                                                                   
         MVC   PARAS(24),DADPARS                                                
         GOTO1 BUFFDADS,PARAS,A(WTID)                                           
         BAS   RE,DADCHECK                                                      
         B     XIT                                                              
         SPACE 2                                                                
MERGE24  BAS   RE,BLKSPLIT         BLOCK IS FULL - SO SPLIT                     
         LA    R5,1(R5)                                                         
         B     MERGE12                                                          
         EJECT                                                                  
*              ROUTINES TO SPLIT A BLOCK                                        
         SPACE 3                                                                
BLKSPLIT NTR1                                                                   
*                                  R6=A(TI ENTRY)                               
         BAS   RE,PUTTRACK         WRITE BACK FIRST BLOCK                       
         L     R4,BUFFLALL         R7=A(BLOCK)                                  
         LR    R5,R4               NOW MODIFY TRACK INDEX                       
         MH    R5,BUFFNBLK+2                                                    
         USING TRACKD,R6                                                        
         LA    R2,0(R5,R7)         R2 POINTS TO START OF SPLIT DATA             
         LR    R3,R2                                                            
         SR    R3,R4               R3=A(LAST RECORD IN BLOCK 1)                 
         L     R1,BUFFLKEY                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TRACKKEY(0),0(R3)   HIGHEST RECORD                               
         MVC   TRACKNUM,BUFFNBLK+2                                              
         LH    R1,TRACKDA                                                       
         SLL   R1,1                                                             
         LA    R1,RECPRTRK-2(R1)                                                
         MVC   0(2,R1),TRACKNUM    ADJUST REC/TRACK                             
         SPACE 2                                                                
         L     R5,BUFFNDSK                                                      
         S     R5,BUFFNBLK                                                      
         LA    R6,DUMTIX           BUILD NEW TRACK INDEX                        
         USING TRACKD,R6                                                        
         STH   R5,TRACKNUM         N'RECORDS                                    
         LH    R0,NOTRK                                                         
         LA    R1,RECPRTRK                                                      
         SPACE 2                                                                
SPLIT2   OC    0(2,R1),0(R1)       FIND AN EMPTY TRACK                          
         BZ    SPLIT4                                                           
         LA    R1,2(R1)                                                         
         BCT   R0,SPLIT2                                                        
         DC    H'0'                                                             
         SPACE 2                                                                
SPLIT4   STH   R5,0(R1)            SAVE N'RECORDS                               
         LR    R0,R1                                                            
         LA    R1,RECPRTRK-2       COMPUTE RELATIVE TRACK NUM.                  
         SR    R0,R1                                                            
         SRL   R0,1                                                             
         STH   R0,TRACKDA          AND PUT THIS IN TIX.                         
         BCTR  R5,0                                                             
         MH    R5,BUFFLALL+2                                                    
         AR    R5,R2               R5=A(LAST RECORD IN NEW BLOCK)               
         L     R1,BUFFLKEY                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TRACKKEY(0),0(R5)   COMPLETE TIX ENTRY                           
         LA    R1,BILL                                                          
         USING DTFPHD,R1                                                        
         XC    DNEXT,DNEXT                                                      
         MVC   DNEXT(2),TRACKDA                                                 
         L     R5,BUFFLDSK                                                      
         GOTO1 BUFFDADS,PARAS,A(WTCKD),(R2),(R5),BILL,DSKADR,0                  
         BAS   RE,DADCHECK                                                      
         SPACE 2                                                                
         MVC   P6,BUFFMXTI         NOW ADD NEW TRACK INTO TIX                   
         LM    R2,R4,BUFFADTI      AD/LN/NUM IN TIX                             
         L     R5,BUFFLKEY                                                      
         GOTO1 BUFFBIN,PARAS,(X'01',(R6)),(R2),(R4),(R3),(4,(R5))               
         OC    PARAS+1(3),PARAS+1                                               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   BUFFNOTI,P3                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO ADD A TRACK                                           
         SPACE 3                                                                
ADDBLOCK NTR1                                                                   
*                                  R2=N(RECORDS TO FIT                          
*                                  R3=A(DATA)                                   
*                                  R6=A(TRACK INDEX ENTRY)                      
         L     R7,AIOAREA                                                       
         L     R5,BUFFLDSK         R5=L'RECORD                                  
         XCEF  (R7),(R5)                                                        
         LR    R4,R2                                                            
         MH    R4,BUFFLALL+2       R4=L'DATA                                    
         MOVE  ((R7),(R4)),(R3)                                                 
         LH    RE,NOTRK                                                         
         LA    R1,RECPRTRK         FIND A TRACK WITH NO DATA                    
         LR    R0,R1                                                            
         SPACE 2                                                                
ADDBL2   OC    0(2,R1),0(R1)                                                    
         BZ    ADDBL4                                                           
         LA    R1,2(R1)                                                         
         BCT   RE,ADDBL2                                                        
         DC    H'0'                                                             
         SPACE 2                                                                
ADDBL4   STH   R2,0(R1)            SAVE N'RECORDS HERE                          
         SR    R1,R0                                                            
         SRL   R1,1                                                             
         LA    R1,1(R1)            WORK OUT RELATIVE TRACK NUMBER               
         LR    R0,R1                                                            
         LA    R1,BILL                                                          
         USING DTFPHD,R1                                                        
         XC    DNEXT,DNEXT                                                      
         STH   R0,DNEXT                                                         
         GOTO1 BUFFDADS,PARAS,A(WTCKD),(R7),(R5),BILL,DSKADR,0                  
         BAS   RE,DADCHECK                                                      
         SPACE 2                                                                
         USING TRACKD,R6           CREATE TI LINE                               
         L     R1,BUFFNOTI                                                      
         LA    R1,1(R1)                                                         
         ST    R1,BUFFNOTI                                                      
         C     R1,BUFFMXTI                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         STH   R0,TRACKDA                                                       
         STH   R2,TRACKNUM                                                      
         AR    R7,R4               FIND THE LAST KEY                            
         S     R7,BUFFLALL         BACK UP TO LAST RECORD                       
         L     R1,BUFFLKEY                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   TRACKKEY(0),0(R7)                                                
         EJECT                                                                  
*              ROUTINE TO CARRY OUT MAINTENANCE ON RECORD (CLEAR)               
         SPACE 3                                                                
MAINT    NTR1                                                                   
         MVI   TOOFAR,C'N'                                                      
         CLI   4(R9),0             OPTION TO FILTER ON FIRST BYTE               
         BE    MAINT1                                                           
         CLC   4(1,R9),0(R2)                                                    
         BE    MAINT1                                                           
         BH    ANYYES                                                           
         MVI   TOOFAR,C'Y'                                                      
         B     ANYYES                                                           
         SPACE 2                                                                
MAINT1   L     RF,0(R9)                                                         
         LA    R9,8(R9)                                                         
         A     R2,BUFFLKEY         R2=A(ROW 1)                                  
         A     R2,BUFFLCOM                                                      
         L     R3,BUFFWCOL         R3=DISPLACEMENT BETWEEN COLUMNS              
         L     R5,BUFFWROW         R5=WIDTH OF ROW                              
         L     R4,BUFFCOLS         R4=NUMBER OF COLUMNS                         
         CLC   0(3,RF),=C'ADD'                                                  
         BE    MAINT10                                                          
         SPACE 2                                                                
MAINT2   L     R7,0(R9)            CLEAR ROW N                                  
         SLL   R7,24                                                            
         SRL   R7,24               R7=N                                         
         C     R7,BUFFROWS                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         BCTR  R7,0                                                             
         MR    R6,R5               R7=DISPLACEMENT FROM START OF ACCUMS         
         AR    R7,R2                                                            
         LR    R6,R4                                                            
         CLI   BUFFFLVR,C'P'                                                    
         BE    MAINT6                                                           
         SPACE 2                                                                
MAINT4   XC    0(4,R7),0(R7)                                                    
         AR    R7,R3                                                            
         BCT   R6,MAINT4                                                        
         B     MAINT8                                                           
         SPACE 2                                                                
MAINT6   ZAP   0(8,R7),=P'0'       CLEAR PACKED                                 
         AR    R7,R3                                                            
         BCT   R6,MAINT6                                                        
         SPACE 2                                                                
MAINT8   CLI   0(R9),X'80'         IF WE'RE NOT AN END OF PARAMETERS,           
         BE    XIT                                                              
         LA    R9,4(R9)            BUMP TO NEXT                                 
         B     MAINT2                                                           
         EJECT                                                                  
*              ROUTINES TO ADD ROW R1 TO ROWS R2...RN                           
         SPACE 3                                                                
MAINT10  L     R7,0(R9)            ADDRESS ROW R1                               
         BCTR  R7,0                                                             
         MR    R6,R5                                                            
         AR    R7,R2                                                            
         ST    R7,SAVEROW1         AND SAVE IT                                  
         LA    R9,4(R9)            BUMP TO R2 IN PARAMETERS                     
         SPACE 2                                                                
MAINT12  L     R7,0(R9)            R7=R2 THRU RN                                
         SLL   R7,24                                                            
         SRL   R7,24                                                            
         C     R7,BUFFROWS                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         BCTR  R7,0                                                             
         MR    R6,R5                                                            
         AR    R7,R2               R7=DISPLACEMENT OF ROW TO ADD INTO           
         LR    R6,R4               R6=NUMBER OF COLUMNS                         
         L     R1,SAVEROW1         R1=A(ROW1)                                   
         CLI   BUFFFLVR,C'P'                                                    
         BE    MAINT16                                                          
         SPACE 2                                                                
MAINT14  MVC   SAVEA,0(R1)                                                      
         MVC   SAVEB,0(R7)                                                      
*&&US                                                                           
         TM    BUFFXOPT,BUFFXBIG                                                
         BO    MAINT15D             DON'T COMPRESS LARGE BINARY NUMBERS         
*&&                                                                             
         TM    0(R1),X'C0'                                                      
         BM    MAINT15                                                          
         TM    0(R7),X'C0'                                                      
         BM    MAINT15                                                          
         L     R0,0(R1)            BINARY ADDS                                  
         A     R0,0(R7)                                                         
         ST    R0,0(R7)                                                         
         TM    0(R7),X'C0'                                                      
         BNM   MAINT15B                                                         
         SPACE 2                                                                
MAINT15  LR    R0,R1                                                            
         CVDX  DUB,SAVEA                                                        
         CVDX  DUB2,SAVEB                                                       
         AP    DUB,DUB2                                                         
         CVBX  0(R7),DUB                                                        
         LR    R1,R0                                                            
         SPACE 2                                                                
MAINT15B AR    R1,R3                                                            
         AR    R7,R3                                                            
         BCT   R6,MAINT14                                                       
         B     MAINT18                                                          
         SPACE 2                                                                
MAINT15D L     R0,0(R1)            BINARY ADDS (NO COMPRESSION)                 
         A     R0,0(R7)                                                         
         ST    R0,0(R7)                                                         
         B     MAINT15B                                                         
         SPACE 2                                                                
MAINT16  AP    0(8,R7),0(8,R1)     PACKED ADDS                                  
         AR    R1,R3                                                            
         AR    R7,R3                                                            
         BCT   R6,MAINT16                                                       
         SPACE 2                                                                
MAINT18  CLI   0(R9),X'80'         IF WE'RE NOT AT END OF PARAMETERS,           
         BE    ANYYES                                                           
         LA    R9,4(R9)            BUMP TO NEXT IN LIST                         
         B     MAINT12                                                          
         EJECT                                                                  
*              ROUTINE TO ADD TWO RECORDS TOGETHER                              
         SPACE 3                                                                
RECADD   NTR1                                                                   
*        CLI   BUFFFLVR,C'D'       **NO-OP 7/13/87 GRANT**                      
*        BE    XIT                                                              
         L     R5,BUFFLKEY         (ADD R3 RECORD TO R2 RECORD)                 
         AR    R2,R5               BUMP PAST KEYS                               
         AR    R3,R5                                                            
         L     R5,BUFFLCOM                                                      
         LTR   R5,R5                                                            
         BZ    RECADD1                                                          
         CLI   BUFFDOPT,C'Y'       USE OPTION TO OVERRIDE COMMENT               
         BE    MOVECOM                                                          
         LR    R1,R5                                                            
         BCTR  R1,0                                                             
         EX    R1,ANYCOM           ALLOW UPDATE OF COMMENT IF                   
         BE    RECADD0             PREVIOUS ONE IS SPACES                       
         SPACE 1                                                                
MOVECOM  MOVE  ((R2),(R5)),(R3)                                                 
         SPACE 1                                                                
RECADD0  AR    R2,R5                                                            
         AR    R3,R5                                                            
         B     RECADD1                                                          
         SPACE 1                                                                
ANYCOM   CLC   0(0,R2),SPACES                                                   
         SPACE 2                                                                
RECADD1  CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         L     R5,BUFFCOLS                                                      
         L     R4,BUFFWCOL                                                      
         CLI   BUFFFLVR,C'P'                                                    
         BE    RECADD4                                                          
         SPACE 2                                                                
RECADD2  MVC   SAVEA,0(R3)                                                      
         MVC   SAVEB,0(R2)                                                      
*&&US                                                                           
         TM    BUFFXOPT,BUFFXBIG DON'T COMPRESS                                 
         BO    RECADD3D                                                         
*&&                                                                             
         TM    0(R3),X'C0'                                                      
         BM    RECADD3                                                          
         TM    0(R2),X'C0'                                                      
         BM    RECADD3                                                          
         L     R0,0(R3)            BINARY ADDITION                              
         A     R0,0(R2)                                                         
         ST    R0,0(R2)                                                         
         TM    0(R2),X'C0'                                                      
         BNM   RECADD3B                                                         
         SPACE 2                                                                
RECADD3  CVDX  DUB,SAVEA                                                        
         CVDX  DUB2,SAVEB                                                       
         AP    DUB,DUB2                                                         
         CVBX  0(R2),DUB                                                        
         SPACE 2                                                                
RECADD3B AR    R2,R4                                                            
         AR    R3,R4                                                            
         BCT   R5,RECADD2                                                       
         B     XIT                                                              
         SPACE 2                                                                
RECADD3D L     R0,0(R3)            BINARY ADDITION                              
         A     R0,0(R2)                                                         
         ST    R0,0(R2)                                                         
         B     RECADD3B                                                         
         SPACE 2                                                                
RECADD4  AP    0(8,R2),0(8,R3)                                                  
         AR    R2,R4                                                            
         AR    R3,R4                                                            
         BCT   R5,RECADD4                                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO MONITOR DADDS ERRORS                                  
         SPACE 3                                                                
DADCHECK OC    8(2,R1),8(R1)                                                    
         BCR   8,RE                                                             
         MVC   DADPARAS,0(R1)                                                   
         DC    F'0'                                                             
         DC    C'DADDS ERROR - PARAMETERS'                                      
DADPARAS DS    CL20                                                             
         SPACE 2                                                                
OPENER   NTR1                                                                   
         CLI   OPENSW,C'Y'         HAVE WE OPENED IT YET.                       
         BE    OPN005              YES.                                         
         MVI   OPENSW,C'Y'                                                      
         LA    R1,BILL                                                          
         OI    21(R1),X'80'        MARK AS FOR OUTPUT.                          
*&&US*&& MVC   22(7,R1),=CL7'BUFFWK '                                           
*&&UK*&& MVC   22(7,R1),=CL7'SORTWK9'                                           
         CLI   BUFFILNO,C' '                                                    
         BNH   *+10                                                             
         MVC   22+6(1,R1),BUFFILNO   USER FILE NUMBER                           
         USING DTFPHD,R1                                                        
         XC    PARAS,PARAS                                                      
         GOTO1 BUFFDADS,PARAS,A(DAOPEN),0,0,BILL                                
         GOTO1 (RF),(R1),A(DATRNS),,,,FILEA,NOTRK                               
*                                                                               
OPN005   LA    R3,DSKTAB           GET DEVICE TRACK CAPACITY.                   
OPN010   CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                UNSUPPORTED DEVICE.                          
         CLC   DEVTYP,2(R3)        CHECK THE DEVICE CODE.                       
         BE    *+12                GOT IT.                                      
         LA    R3,3(R3)            TRY THE NEXT ONE.                            
         B     OPN010                                                           
*                                                                               
         ZICM  R2,0(R3),2          R2 = TRACK CAPACITY.                         
         ST    R2,BUFFLDSK         SAVE IT FOR BUFFALO.                         
         STCM  R2,3,52(R1)         ALSO IN DTF FOR RPS.                         
         OI    52(R1),X'80'        BLOCKING IS NON-DDS STANDARD.                
         L     R7,=A(IOA)          SAVE A(IOAREA)                               
         A     R7,RELDISP                                                       
         ST    R7,AIOAREA                                                       
*                                                                               
OPN014   OC    BUFFADTI,BUFFADTI   FIRST OPEN OR (RE)SET FORCES RECALC          
         BNZ   OPNXIT              OF BUFFALO BLOCKING INFO AND                 
         L     R2,BUFFLDSK         TRACK INDEX CHARACTERISTICS.                 
         SRDA  R2,32                                                            
         D     R2,BUFFLALL         DIVEDE BY LOG REC LNTH FOR                   
         ST    R3,BUFFNDSK         LOGRECS/BLOCK.                               
*                                                                               
         CLC   NOTRK,MAXTRAX       ENSURE NO OF TRKS FROM XTNTS                 
         BNH   *+10                FITS RECPRTRK LIMIT.                         
         MVC   NOTRK,MAXTRAX                                                    
         L     R2,=A(BUFFTKIX)     INITIALIZE TRACK INDEX DATA.                 
         A     R2,RELDISP                                                       
         LA    R3,4(R2)                                                         
         ST    R3,BUFFADTI         A(TRACK INDEX)                               
         L     R2,0(R2)            R2 = L'TRACK INDEX.                          
         L     R4,BUFFLKEY                                                      
         LA    R4,4(R4)            R4 = L'TRACK INDEX ENTRY.                    
         ST    R4,BUFFLNTI         SAVE L'TRACK INDEX ENTRY.                    
         SRDA  R2,32               DO L'INDEX/L'ENTRY                           
         DR    R2,R4               GIVING MAX NO OF ENTRIES.                    
         CH    R3,NOTRK            LIMIT NO OF TRKS AVAILABLE                   
         BNL   *+12                 BY CAPACITY OF TRACK INDEX                  
         STH   R3,NOTRK            IF NECESSARY.                                
         B     *+8                                                              
         LH    R3,NOTRK                                                         
         ST    R3,BUFFMXTI                                                      
         XC    BUFFNOTI,BUFFNOTI   NUMBER SO FAR = 0                            
         L     R2,BUFFNDSK                                                      
         LA    R2,1(R2)                                                         
         SRL   R2,1                INITIAL NO OF LOG RECS/BLOCK =               
         ST    R2,BUFFNBLK         (TRK CAPACITY+1)/2.                          
*                                                                               
OPNXIT   B     XIT                                                              
*                                                                               
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*              DTF FOR BILL                                                     
         SPACE 3                                                                
         PRINT GEN                                                              
BILLDTF  DS    0D                                                               
BILL     DMDA                                                                   
         PRINT NOGEN                                                            
*                                                                               
AIOAREA  DS    A                   A(IOAREA).                                   
DSKADR   DS    F                                                                
BLDXTNT  DS    0F                                                               
FILEA    DC    X'FFFF0101'         EOF VALUE FOR DADDS DATRNS.                  
NOTRK    DC    H'0'                HIGH REL TRACK PASSED FROM DADDS.            
DEVDATA  DC    AL1(0)              DEVICE DATA TYPE, CKD OR FBA.                
DEVTYP   DC    AL1(0)              DEVICE TYPE, 1 = 3340, 2 = 3350.             
         SPACE 3                                                                
*              ROUTINE TO TRACE                                                 
         SPACE 2                                                                
TRACE    BR    RE                  NOOP THIS TO ENABLE TRACING                  
         NTR1                                                                   
         L     R2,PARAS                                                         
         L     R3,BUFFWROW                                                      
         A     R3,BUFFLKEY                                                      
         A     R3,BUFFLCOM                                                      
         MVC   PARAS+8(4),=C'DUMP'                                              
         GOTO1 VPRNTBL,PARAS,(20,FIELD),(R2),,(R3),=C'2D'                       
         GOTO1 VPRNTBL,PARAS,0,(RA),,128,=C'2D'                                 
         B     XIT                                                              
         SPACE 2                                                                
FIELD    DC    CL20' '                                                          
         EJECT                                                                  
*        LTORG AND THINGS                                                       
         SPACE 2                                                                
RELO     DC    A(*)                                                             
VPRNTBL  DS    V                                                                
SPACES   DC    CL132' '                                                         
DUMTIX   DS    CL250                                                            
SAVENUM  DS    CL2                                                              
TOOFAR   DC    C'N'                                                             
OPENSW   DC    C'N'                                                             
ALASTREC DC    A(0)                                                             
RECSLEFT DC    F'0'                                                             
ALASTIX  DC    A(0)                                                             
TRKSLEFT DC    F'0'                                                             
*                                  DISK DEVICE TYPE TABLE.                      
DSKTAB   DC    AL2(8000),X'01'     X'01' = 3340, TRK CAPACITY = 8000.           
         DC    AL2(19000),X'02'    X'02' = 3350, TRK CAPACITY = 19000.          
         DC    AL2(35616),X'03'    X'03' = 3375, TRK CAPACITY = 35616.          
         DC    AL2(47476),X'04'    X'04' = 3380, TRK CAPACITY = 47476.          
         DC    AL2(47476),X'05'    X'05' = 3390, TRK CAPACITY = 56664.          
         DC    AL1(255)                                                         
* NOTE - I KNOW THIS IS A LITTLE INEFFICIENT, BUT IT ISN'T WORTH                
*        MAKING IOA LONGER JUST FOR 3390'S.                                     
*                                                                               
DADPARS  DS    CL24                                                             
SAVEROW1 DS    F                                                                
DUMMY    DS    0H                                                               
         LTORG                                                                  
*                                                                               
         DS    0H                                                               
*                                                                               
MAXTRAX  DC    AL2(L'RECPRTRK/2)                                                
RECPRTRL DC    AL2(L'RECPRTRK)                                                  
RECPRTRK DS    CL8000                                                           
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
BUFFD    DSECT                                                                  
DUB      DS    D                                                                
DUB2     DS    D                                                                
SAVEA    DS    F                                                                
SAVEB    DS    F                                                                
RELDISP  DS    F                                                                
PARAS    DS    6F                                                               
         ORG   PARAS                                                            
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
SAVEKEY  DS    CL255                                                            
CBKEY    DS    CL255                                                            
WHY     DS    CL1                                                               
       ++INCLUDE DDBUFFALOD                                                     
         EJECT                                                                  
       ++INCLUDE DMDTFPH                                                        
IOA      CSECT                                                                  
         DS    48500C                                                           
         SPACE 2                                                                
BUFFTKIX CSECT                                                                  
         DC    F'60000'                                                         
         DS    60000C                                                           
         SPACE 2                                                                
TRACKD   DSECT                     COVERS ITEMS IN TRACK INDEX ABOVE            
TRACKDA  DS    H                   RELATIVE TRACK                               
TRACKNUM DS    H                   NUMBER OF RECORDS IN THIS BLOCK              
TRACKKEY DS    0C                  HIGHEST KEY ON TRACK                         
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011DDBUFFAHI 08/23/04'                                      
         END                                                                    
