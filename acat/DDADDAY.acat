*          DATA SET DDADDAY    AT LEVEL 003 AS OF 04/24/97                      
*CATALP ADDAY                                                                   
         SPACE 1                                                                
* CL1    C'Y'=YEARS,C'M'=MONTHS,C'D'=DAYS (DEFAULT)                             
* AL3    A(INPUT DATE - C'YYMMDD')                                              
*                                                                               
* XL1    X'80'=SET OUTPUT DAY TO LAST DAY OF MONTH (C'Y' AND C'M' ONLY)         
*        X'40'=DONT SET OUTPUT DAY TO END OF MONTH IF INPUT IS END DAY          
*        X'20'=DONT OUTPUT DDS INTERNAL YR2000 DATE                             
* AL3    A(OUTPUT DATE - C'YYMMDD')                                             
*                                                                               
* XL4    SIGNED FULL WORD NUMBER OR YEARS, MONTHS, OR DAYS (SEE P1(1))          
         SPACE 1                                                                
         TITLE 'DDADDAY - COMPUTES AN INCREMENTED DATE'                         
         PRINT NOGEN                                                            
ADDAY    CSECT                                                                  
         NMOD1 WRKX-WRKD,*ADDAY                                                 
         USING WRKD,RC                                                          
         ST    R1,SAVER1                                                        
         LM    R2,R4,0(R1)         R2=A(INPUT),R3=A(OUTPUT),R4=DAYS             
         SR    R0,R0                                                            
         CLI   0(R2),C'9'          TEST YEAR 2000 STYLE INPUT DATE              
         BNH   ADDAY1                                                           
         MVC   DUB1(2),0(R2)                                                    
         SR    R0,R0                                                            
         IC    R0,DUB1                                                          
         SH    R0,=H'10'                                                        
         STC   R0,DUB1                                                          
         LA    R0,100                                                           
         PACK  DUB,DUB1(2)                                                      
         CVB   R7,DUB                                                           
         B     ADDAY2                                                           
ADDAY1   PACK  DUB,0(2,R2)                                                      
         CVB   R7,DUB              R7=INPUT YEAR                                
         CH    R7,=H'27'           ASSUME C'00'-C'27' ARE 2000-2027             
         BH    ADDAY2                                                           
         LA    R0,100                                                           
ADDAY2   AR    R7,R0                                                            
         PACK  DUB,2(2,R2)                                                      
         CVB   R6,DUB              R6=INPUT MONTH                               
         PACK  DUB,4(2,R2)                                                      
         CVB   R5,DUB              R5=INPUT DAY                                 
*                                                                               
ADDAY3   LTR   R4,R4               TEST NUMBER OF YEARS/MONTHS/DAYS             
         BNZ   ADDAY4                                                           
         TM    4(R1),X'80'         TEST SET TO END OF MONTH REQUESTED           
         BZ    OUTPUT                                                           
         CLI   0(R1),C'Y'          OK FOR YEARS                                 
         BE    AY                                                               
         CLI   0(R1),C'M'          OK FOR MONTHS                                
         BE    AM                                                               
         B     OUTPUT                                                           
*                                                                               
ADDAY4   CLI   0(R1),C'Y'          TEST YEARS ARE UNITS                         
         BE    AY                                                               
         CLI   0(R1),C'M'          TEST MONTHS AS UNITS                         
         BE    AM                                                               
         EJECT                                                                  
***********************************************************************         
* DAY ADD/SUBTRACT ROUTINE - R4 HAS NUMBER OF DAYS                    *         
***********************************************************************         
         SPACE 1                                                                
AD       LTR   R4,R4               ADD/SUBTRACT DAYS FROM INPUT DATE            
         BM    SD                                                               
*                                                                               
AD2      LA    R2,MONTABLE(R6)     GET NUMBER OF DAYS IN MONTH                  
         SR    R8,R8                                                            
         IC    R8,0(R2)            R8=MAX DAYS IN MONTH                         
         CH    R6,=H'2'                                                         
         BNE   AD4                                                              
         STC   R7,DUB                                                           
         TM    DUB,X'03'           YEAR DIVISIBLE BY 4 ?                        
         BNZ   AD4                                                              
         LA    R8,1(R8)            ADJUST DAYS IN FEB FOR LEAP YEAR             
*                                                                               
AD4      EQU   *                   ADD TO DAYS                                  
         CLR   R5,R8               INPUT DAYS=MAX DAYS ?                        
         BE    AD6                                                              
         LA    R5,1(R5)            INCREMENT INPUT DAYS                         
         BCT   R4,AD4              DECREMENT DAYS TO ADD AND LOOP               
         B     OUTPUT                                                           
*                                                                               
AD6      EQU   *                   END OF MONTH - CONVERT TO NEXT               
         LA    R5,1                                                             
         LA    R6,1(R6)            INCREMENT MONTH                              
         CH    R6,=H'12'                                                        
         BH    AD8                                                              
         BCT   R4,AD2              DECREMENT DAYS TO ADD AND LOOP               
         B     OUTPUT                                                           
*                                                                               
AD8      EQU   *                   END OF YEAR - CONVERT TO NEXT                
         LA    R6,1                                                             
         LA    R7,1(R7)            INCREMENT YEAR                               
         BCT   R4,AD2              DECREMENT DAYS TO ADD AND LOOP               
         B     OUTPUT                                                           
         SPACE 2                                                                
SD       LPR   R4,R4               SUBTRACT DAYS - COMPLEMENT R4                
SD4      SH    R5,=H'1'            DECREMENT DAY NUMBER                         
         BNP   SD6                                                              
         BCT   R4,SD4              DECREMENT DAYS TO SUBTRACT AND LOOP          
         B     OUTPUT                                                           
*                                                                               
SD6      EQU   *                   ADJUST TO END OF PREVIOUS MONTH              
         SH    R6,=H'1'            DECREMENT MONTH                              
         BNP   SD8                                                              
         LA    R2,MONTABLE(R6)     SET DAY NUM TO MAX DAYS IN MONTH             
         IC    R5,0(R2)                                                         
         CH    R6,=H'2'                                                         
         BNE   SD10                                                             
         STC   R7,DUB                                                           
         TM    DUB,X'03'           YEAR DIVISIBLE BY 4 ?                        
         BNZ   SD10                                                             
         LA    R5,1(R5)            ADJUST DAYS IN FEB FOR LEAP YEAR             
         B     SD10                                                             
*                                                                               
SD8      EQU   *                   CONVERT TO END OF LAST YEAR                  
         SH    R7,=H'1'            DECREMENT YEAR                               
         BNM   *+8                                                              
         LA    R7,99               WRAP AROUND FOR CENTURY                      
         LA    R5,31               DAY NUMBER                                   
         LA    R6,12               MONTH NUMBER                                 
*                                                                               
SD10     BCT   R4,SD4              DECREMENT DAYS TO SUBTRACT AND LOOP          
         B     OUTPUT                                                           
         EJECT                                                                  
***********************************************************************         
* ADD/SUBTRACT MONTHS ROUTINES - R4 HAS NUMBER OF MONTHS              *         
***********************************************************************         
         SPACE 1                                                                
AM       LA    R2,MONTABLE(R6)     ADD/SUBTRACT MONTHS                          
         SR    R8,R8                                                            
         IC    R8,0(R2)            R8=MAX DAYS IN INPUT MONTH                   
         CH    R6,=H'2'                                                         
         BNE   AM2                                                              
         STC   R7,DUB                                                           
         TM    DUB,X'03'           TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   *+8                                                              
         LA    R8,1(R8)                                                         
AM2      LTR   R4,R4               TEST NEGATIVE MONTHS PASSED                  
         BM    SM                                                               
*                                                                               
AM4      SR    RE,RE               COMPUTE YEARS AND MONTHS                     
         LR    RF,R4               R4=NUMBER OF MONTHS (INPUT VALUE)            
         D     RE,=F'12'           RE=MONTHS,RF=YEARS                           
         AR    R6,RE               R6=OUTPUT MONTH                              
         CH    R6,=H'12'                                                        
         BNH   *+12                                                             
         SH    R6,=H'12'                                                        
         LA    RF,1(RF)                                                         
         AR    R7,RF               R7=OUTPUT YEAR                               
*                                                                               
AM6      LA    R2,MONTABLE(R6)     GET NUMBER OF DAYS IN OUTPUT MONTH           
         SR    R9,R9                                                            
         IC    R9,0(R2)            R9=MAX DAYS IN OUTPUT MONTH                  
         CH    R6,=H'2'                                                         
         BNE   AM10                                                             
         STC   R7,DUB                                                           
         TM    DUB,X'03'           TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   AM10                                                             
         LA    R9,1(R9)                                                         
*                                                                               
AM10     CLR   R5,R9               IS DAY > MAXIMUM FOR OUTPUT MONTH            
         BNH   AM12                NO                                           
         LR    R5,R9               YES WE HAVE TO SET TO MAXIMUM                
         B     OUTPUT                                                           
AM12     L     R1,SAVER1           TEST IF ALWAYS SET LAST DAY OF MONTH         
         TM    4(R1),X'80'                                                      
         BO    AM14                                                             
         CLR   R5,R8               IS DAY=MAXIMUM FOR INPUT MONTH               
         BNE   OUTPUT                                                           
         TM    4(R1),X'40'         TEST IF DONT WANT DAYS ADJUST                
         BO    OUTPUT                                                           
AM14     LR    R5,R9               SET DAY TO MAXIMUM FOR OUTPUT MONTH          
         B     OUTPUT                                                           
         SPACE 2                                                                
SM       LPR   R4,R4               SUBTRACT MONTHS - COMPLEMENT R4              
*                                                                               
SM4      SR    RE,RE               COMPUTE YEARS AND MONTHS                     
         LR    RF,R4               R4=NUMBER OF MONTHS (INPUT VALUE)            
         D     RE,=F'12'           RE=MONTHS,RF=YEARS                           
         SR    R6,RE               R6=OUTPUT MONTH                              
         BP    SM6                                                              
         AH    R6,=H'12'                                                        
         AH    RF,=H'1'                                                         
SM6      SR    R7,RF               R7=OUTPUT YEAR                               
         BNM   SM8                                                              
         LA    R7,99               WRAP IF YEAR GOES NEGATIVE                   
*                                                                               
SM8      B     AM6                 FUSS WITH OUTPUT DAY - SAME AS ADD           
         EJECT                                                                  
***********************************************************************         
* ADD/SUBTRACT YEARS ROUTINES                                         *         
***********************************************************************         
         SPACE 1                                                                
AY       LA    R2,MONTABLE(R6)     ADD/SUBTRACT YEARS                           
         SR    R8,R8                                                            
         IC    R8,0(R2)            R8=MAX DAYS IN INPUT MONTH                   
         CH    R6,=H'2'                                                         
         BNE   AY2                                                              
         STC   R7,DUB                                                           
         TM    DUB,X'03'           TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   *+8                                                              
         LA    R8,1(R8)                                                         
*                                                                               
AY2      LTR   R4,R4               TEST NEGATIVE YEARS PASSED                   
         BM    SY                                                               
         AR    R7,R4               ADD YEARS                                    
         B     AM6                 PROCESS DAYS IN OUTPUT MONTH                 
         SPACE 2                                                                
SY       LPR   R4,R4               SUBTRACT YEARS - COMPLEMENT R4               
         SR    R7,R4                                                            
         BNM   *+8                                                              
         LA    R7,99               WRAP AROUND FOR CENTURY                      
         B     AM6                 PROCESS DAYS IN OUTPUT MONTHS                
         EJECT                                                                  
***********************************************************************         
* FORMAT OUTPUT AND EXIT                                              *         
***********************************************************************         
         SPACE 1                                                                
OUTPUT   CVD   R5,DUB              CONVERT OUTPUT DAYS TO DECIMAL               
         OI    DUB+7,X'0F'                                                      
         UNPK  4(2,R3),DUB                                                      
         CVD   R6,DUB              CONVERT OUTPUT MONTHS TO DECIMAL             
         OI    DUB+7,X'0F'                                                      
         UNPK  2(2,R3),DUB                                                      
         CH    R7,=H'100'          CONVERT OUTPUT YEARS                         
         BNL   OUTPUT2                                                          
         CVD   R7,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R3),DUB                                                      
         B     EXIT                                                             
OUTPUT2  SH    R7,=H'100'          ADJUST FOR 20TH CENTURY                      
         CVD   R7,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R3),DUB                                                      
         L     R1,SAVER1                                                        
         TM    4(R1),X'20'         TEST IF CALLER DOESN'T WANT YR2000           
         BO    EXIT                                                             
         IC    R7,0(R3)                                                         
         LA    R7,10(R7)                                                        
         STC   R7,0(R3)                                                         
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
MONTABLE DC    X'001F1C1F1E1F1E1F1F1E1F1E1F'                                    
         LTORG                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
SAVER1   DS    F                                                                
WRKX     EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDADDAY   04/24/97'                                      
         END                                                                    
