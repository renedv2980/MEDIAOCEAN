*          DATA SET TABILL     AT LEVEL 151 AS OF 07/10/17                      
*CATALP TABILL                                                                  
*                                                                               
         TITLE 'TABILL - TALENT BILLING APPLICATION'                            
TABILL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TABILL,R7,R6,R5                                                
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R9,ASUBSYSD         SYSTEM SPECIFIC WORK                         
         USING SUBSYSD,R9                                                       
         LA    RA,BUFF             BILL DSECT                                   
         LA    RA,8(RA)                                                         
         USING BILLD,RA                                                         
         L     R1,=A(SAVERC)                                                    
         ST    RC,0(R1)                                                         
         EJECT                                                                  
*                                                                               
         GOTO1 INITIAL,DMCB,0                                                   
*                                                                               
         CLI   MODE,RUNFRST                                                     
         BNE   MAIN05                                                           
***      TM    TGSYSTAT,TASYSAOS   IF AOS, SHOW                                 
***      BZ    MXX                                                              
         L     R3,ABOX                                                          
         USING BOXD,R3                                                          
         MVI   BOXMAXL,59          59 LINES, NEED 1 MORE LINE                   
         B     MXX                                                              
         DROP  R3                                                               
*                                                                               
MAIN05   CLI   MODE,PRINTREP                                                    
         BNE   MXX                                                              
*                                                                               
         MVC   AIO,AIO2                                                         
*                                                                               
         GOTO1 =A(INIT),DMCB,(RC)  INITIALIZE                                   
         TM    TBOPTS2,TBFILE      IF PROCESSING SPECIAL RETRO FILE             
         BNO   MAIN10                                                           
         GOTO1 =A(ALLINIT),DMCB,(RC)                                            
         BAS   RE,SETRETRO         SET UP INV INFO                              
         BE    MAIN20                                                           
         B     MX                                                               
*                                                                               
MAIN10   BAS   RE,SETINV           SET LOCAL INVOICE NUMBER                     
         BE    MAIN15                                                           
         CP    ERRINVC,=P'0'       IF INVOICES PUT IN ERROR                     
         BNE   MAIN25              PRINT FINAL PAGE                             
         BRAS  RE,CKUPDT2          OR IF UPDATE OPTION FOR SOONS                
         BE    MAIN25              PRINT FINAL PAGE                             
         B     MX                  ELSE, EXIT                                   
*                                                                               
MAIN15   GOTO1 =A(ALLINIT),DMCB,(RC)                                            
*                                                                               
         CLI   RECNUM,AC           AGENCY COPY?                                 
         BE    *+8                                                              
         CLI   RECNUM,EB           E-BILLING                                    
         BE    *+12                                                             
         CLI   RECNUM,BI           OR BILLING COPY?                             
         BNE   MAIN19                                                           
         GOTOR AGYFLIST            IF AGENCY IS AN FLIST, SET UP AGYTAB         
         BNE   MAIN19                                                           
*                                                                               
         L     R0,=AL4(NAGY*AGYLNQ)                                             
         GOTO1 AMYTRACE,DMCB,(RC),=C'AGYTAB',A(AGYTAB),(R0)                     
*                                                                               
         GOTOR GETFAGY             GET FIRST AGENCY IN AGYTAB                   
         BE    MAIN17                                                           
MAIN16   GOTOR GETNAGY             GET NEXT AGENCY IN AGYTAB                    
         BNE   MAIN18                                                           
         MVI   READPTR,C'N'        CAN READ OPEN ITEM PTRS AGAIN                
MAIN17   BAS   RE,PREP                                                          
         B     MAIN16                                                           
                                                                                
MAIN18   ZICM  R0,RECN,2                                                        
         CHI   R0,0                ANY INVOICES TO PROCESS?                     
         BH    MAIN20                                                           
         BE    MAIN21                                                           
         DC    H'00'                                                            
*                                                                               
MAIN19   BAS   RE,PREP             GET ALL INVOICE RECORDS & SORT THEM          
         BNE   *+8                                                              
*                                                                               
MAIN20   BAS   RE,LOOPINV          LOOP THROUGH ALL INVOICES & PROCESS          
MAIN21   TM    TBOPTS2,TBFILE      IF NOT SPECIAL RETRO FILE                    
         BO    *+14                                                             
         OC    TBVINV,TBVINV       OR IF RUNNING SPECIFIC INVOICES              
         BNZ   MAIN10              CONTINUE                                     
*                                                                               
MAIN25   BRAS  RE,ADDSYS                   ADD SYSTEM RECORD                    
         GOTO1 =A(FINAL),DMCB,(RC) DO FINAL PROCESSING OF ENTIRE RUN            
*                                                                               
MX       GOTOR CLOSTAPE                                                         
*                                                                               
         TM    WHEN,X'20'                  SOON ONLY                            
         BZ    MX2                                                              
*                                                                               
         LHI   R0,TXTDLNQ                  FREE UP ALLOCATED SPACE              
         L     R1,ATAXDSCT                                                      
         FREEMAIN R,A=(1),LV=(0)                                                
*                                                                               
         LHI   R0,PBLNQ                    FREE UP ALLOCATED SPACE              
         L     R1,POSTBLK                                                       
         FREEMAIN R,A=(1),LV=(0)                                                
*                                                                               
         L     R0,=A(20000*SRTILEN)        FREE UP ALLOCATED SPACE              
         L     R1,ASRTINVT                                                      
         FREEMAIN R,A=(1),LV=(0)                                                
*                                                                               
         L     R0,=A(TABCHKLN*TABLEN)      FREE UP ALLOCATED SPACE              
         L     R1,ACHKTAB                                                       
         FREEMAIN R,A=(1),LV=(0)                                                
*                                                                               
         L     R0,=A(MAXYTD*TYTDLEN)       FREE UP ALLOCATED SPACE              
         L     R1,AYTDTAB                                                       
         FREEMAIN R,A=(1),LV=(0)                                                
*                                                                               
MX2      DS    0H                                                               
         BRAS  RE,MAKVIS                                                        
MXX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET UP RETRO INFORMATION INTO WORKING STORAGE                          
SETRETRO NTR1                                                                   
         LA    R2,DATAFILE                                                      
         L     R3,AIO2                                                          
         USING DFD,R3                                                           
         OPEN  ((R2),(INPUT))                                                   
*                                                                               
SETR10   GET   (R2),(R3)           GET INFO INTO AIO2                           
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'84',DFAGY)                                
         BNE   SETRERR                                                          
         MVC   TIFAGY,TGAGY        SET AGENCY FILTER                            
         GOTO1 TINVCON,DMCB,DFINV,TBVINV,DATCON                                 
         CLI   0(R1),X'FF'         VALID INVOICE NUMBER                         
         BE    SETRERR                                                          
         XC    TBVINV,COMPLM       COMPLEMENT THE INVOICE                       
         GOTO1 RECVAL,DMCB,TLINCDQ,(X'84',TBVINV)                               
         BNE   SETRERR                                                          
         NI    TBOPTS,X'FF'-TBRERUN   CLEAR RERUN BIT                           
         BAS   RE,PREP                                                          
         B     SETR10                                                           
*                                                                               
SETRERR  DS    0H                                                               
         L     R4,ANXTERR          FILL IN ERROR TABLE                          
         USING ERRINVD,R4                                                       
         MVC   ERRAGY,DFAGY                                                     
         MVC   ERRINV,DFINV                                                     
         LA    R4,ERRLEN(R4)                                                    
         ST    R4,ANXTERR                                                       
         B     SETR10                                                           
*                                                                               
SETREND  CLOSE DATAFILE            CLOSE DATA FILE                              
         SR    R3,R3                                                            
         ICM   R3,3,RECN           IF NO INVOICES WERE FOUND - EXIT             
         BZ    NO                                                               
         GOTO1 QSORT,DMCB,ASRTINVT,(R3),SRTILEN,SRTILEN-L'SRTDA,0               
         B     YES                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        SET CURRENT INVOICE NUMBER                                             
*            FRSTINV - FIRST INVOICE NUMBER                                     
*            LASTINV - LAST INVOICE NUMBER                                      
*                                                                               
SETINV   NTR1                                                                   
         OC    FRSTINV,FRSTINV     IF RUNNING SPECIFIC INVOICES                 
         BZ    YES                                                              
*                                                                               
SI10     MVC   TBVINV,NEXTINV      SET TBVINV WITH NEXT INVOICE                 
         MVC   HALF,NEXTINV+3      GET INVOICE 'NUMBER'                         
         XC    HALF,COMPLM         UNCOMPLEMENT IT                              
         ZAP   DUB,=P'0'                                                        
         MVO   DUB,HALF            MAKE IT A PACKED NUMBER                      
         AP    DUB,=P'1'           BUMP TO NEXT INVOICE NUMBER                  
         MP    DUB,=P'10'          SHIFT OVER                                   
         MVC   NEXTINV+3(2),DUB+5                                               
         XC    NEXTINV+3(2),COMPLM & SET IT IN NEXTINV                          
*                                                                               
         CLC   TBVINV,LASTINV      IF PASSED LAST INVOICE                       
         BL    NO                  EXIT (INVOICES ARE COMPLEMENTED)             
         GOTOR SETACT              SET ACTIVE POINTER                           
         GOTO1 HIGH                                                             
         ZIC   RE,LENKEY           COMPARE KEY FOR SPECIFIC LENGTH              
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   KEY(0),KEYSAVE      IF KEY FOUND                                 
         BNE   SI10                                                             
*                                                                               
         GOTO1 GETREC              GET THE RECORD                               
         L     R4,AIO                                                           
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   SI10                                                             
         USING TAIND,R4                                                         
         TM    TAINSTAT,TAINSPAY   BUT ONLY PROCESS IF IT WAS PAID              
         BNO   SI10                                                             
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        READ INVOICE RECORDS AND SORT THEM                                     
*             READ ALL RELEVANT RECORDS TO INVOICE AND STORE INFO               
*             PER INVOICE RECORD                                                
*                                                                               
PREP     NTR1                                                                   
         OC    TBVINV,TBVINV       IF RUNNING A SPECIFIC INVOICE                
         BNZ   PREP50                 USE ACTIVE POINTER                        
         OC    TBVCOM,TBVCOM       IF RUNNING A SPECIFIC CID                    
         BNZ   PREP90                 USE PAYMENT HISTORY POINTER               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    PREP60                 USE CHECK DATE PTRS                       
         TM    TBOPTS,TBRERUN      IF JOB IS A RERUN - USE                      
         BO    PREP70                 BILL DATE POINTER                         
         B     PREP80              ELSE USE OPEN ITEMS POINTER                  
*                                                                               
PREP50   GOTOR SETACT              SET ACTIVE POINTER                           
         B     PREP100                                                          
*                                                                               
PREP60   BRAS  RE,SETCK            SET CHECK DATE POINTER                       
         B     PREP100                                                          
*                                                                               
PREP70   BAS   RE,SETBI            SET BILL DATE POINTER                        
         B     PREP100                                                          
*                                                                               
PREP80   BAS   RE,SETPA            SET PASSIVE POINTER (OPEN ITEMS)             
         B     PREP100                                                          
*                                                                               
PREP90   BAS   RE,SETPH            SET PASSIVE (HISTORY) POINTER                
*                                                                               
PREP100  GOTO1 HIGH                                                             
         B     PREP120                                                          
*                                                                               
PREP110  XC    UNIONS(ULLEN),UNIONS  CLR ALL UNIONS USED IN PREV INV            
         GOTO1 SEQ                   GET NEXT RECORD                            
*                                                                               
PREP120  ZIC   RE,LENKEY             COMPARE KEY FOR SPECIFIC LENGTH            
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   KEY(0),KEYSAVE                                                   
         BE    PREP122                                                          
*                                                                               
PREP121  TM    TBOPTS2,TBFILE      IF PROCESSING SPECIAL RETRO FILE             
         BO    XIT                 EXIT                                         
*                                                                               
         TM    TBOPTS,TBRERUN      IF THIS IS ANY RERUN -                       
         BNO   PREP180                                                          
         OC    TBVINV,TBVINV       AND NOT RUNNING A SPECIFIC INVOICE           
         BNZ   PREP180                                                          
         CLI   UNICOPY,C'Y'        BUT NOT A UNION COPY                         
         BE    PREP180                                                          
         CLI   READPTR,C'Y'        CHECK IF WE HAVE ALREADY READ                
         BE    PREP180             OPEN ITEM PTRS FOR CODS                      
         MVI   READPTR,C'Y'                                                     
         BAS   RE,SETPA            READ OPEN PTRS TO GET COD INVOICES           
         B     PREP100                                                          
*                                                                               
PREP122  TM    TBOPTS,TBNOPOST     IF NOT POSTING                               
         BZ    PREP126                                                          
         USING T703FFD,R3                                                       
         L     R3,ATWA                                                          
         CLI   TWAWRITE,C'N'       AND WRITE=NO                                 
         BE    PREP130             THEN ALLOW CONVERTED                         
*                                                                               
PREP126  GOTO1 =A(CKCONV),DMCB,(RC) IF THIS IS A CONVERTED INVOICE              
         BE    PREP110             IGNORE IT                                    
*                                                                               
PREP130  GOTO1 =A(CKDATES),DMCB,(RC)  FOR REPORTS OVER A PERIOD                 
         BE    PREP131             CHECK IF WITHIN DATES                        
         CLI   RECNUM,AC                                                        
         BE    PREP121                                                          
         CLI   RECNUM,EB                                                        
         BE    PREP121                                                          
         B     PREP180                                                          
*                                                                               
PREP131  TM    TBOPTS,TBRERUN      IF JOB IS A RE-RUN                           
         BO    PREP140             CHECK IF ITS THE REQUESTED DATE              
         USING TLINPD,R1                                                        
         LA    R1,KEY              IF THIS IS OPEN ITEMS POINTER                
         CLI   TLINPCD,TLINBCDQ    (CAN CHECK AT DIRECTORY LEVEL)               
         BNE   PREP140                                                          
         TM    TLINBST2,TAINSAPR   SKIP INVOICE IF NOT                          
         BZ    PREP110                   QC APPROVED OR IF IT'S                 
         TM    TLINBST2,TAINSERR+TAINSBIL                                       
         BNZ   PREP110                   IN ERROR OR ALREADY BILLED             
         DROP  R1                                                               
*                                                                               
PREP140  MVC   SVKEY,KEY           SAVE KEY TO RESTORE READ SEQ                 
         MVC   AIO,AIO1                                                         
         GOTO1 GETREC              GET INVOICE RECORD                           
*                                                                               
         L     R4,AIO              GET INVOICE STATUS ELEMENT                   
         USING TLIND,R4                                                         
         CLI   TLINCD,TLINCDQ      MAKE SURE THIS IS AN INVOICE                 
         BNE   PREP110             (& NOT HISTORY COMMENT)                      
         DROP  R4                                                               
*                                                                               
         USING TAIND,R4                                                         
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    PREP150                                                          
         MVI   ERRSTAT,TAINEINE    IF THERE'S NO INVOICE ELEMENT                
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     PREP110             AND GET NEXT INVOICE                         
*                                                                               
PREP150  BAS   RE,FILTINV          CHECK IF WE WANT TO PROCESS                  
         BNE   PREP110             THIS INVOICE                                 
*                                                                               
         TM    WHEN,X'20'          IF RUNNING SOON, DON'T DO RERUN              
         BZ    PREP155                                                          
         OC    TBSPDATE,TBSPDATE   AND BILLING PERIOD NOT SUPPLIED              
         JNZ   PREP155                                                          
         NI    TBOPTS,X'FF'-TBRERUN    RESET RERUN BIT                          
*                                                                               
PREP155  OC    TBVINV,TBVINV       IF RUNNING A SPECIFIC INVOICE                
         BZ    PREP160                                                          
         TM    TAINSTAT,TAINSBIL   AND THIS INVOICE HAS BEEN BILLED             
         BZ    PREP160                                                          
         BRAS  RE,CKUPDT2          SKIP INVOICE IF RUNNING UPDATE OPT           
         BE    PREP110                                                          
         OI    TBOPTS,TBRERUN                                                   
*                                                                               
PREP160  BAS   RE,FILTCO           FILTER COMMERCIAL DETAILS ELEMENT            
         BNE   PREP110                                                          
*                                                                               
         LA    R1,KEY                                                           
         USING TLDRD,R1                                                         
         MVC   TBDA,TLDRDA         SAVE DISK ADDRESS FOR SORT KEY               
         BAS   RE,PRINV            PROCESS THE INVOICE                          
         MVC   KEY,SVKEY           RESTORE KEY AND READ SEQUENCE                
         GOTO1 HIGH                                                             
         B     PREP110                                                          
*                                                                               
PREP180  SR    R3,R3                                                            
         ICM   R3,3,RECN           IF NO INVOICES WERE FOUND - EXIT             
         BZ    NO                                                               
         GOTO1 QSORT,DMCB,ASRTINVT,(R3),SRTILEN,SRTILEN-L'SRTDA,0               
         B     YES                                                              
         DROP  R1,R4                                                            
         EJECT                                                                  
*                                                                               
*        LOOP THROUGH ALL INVOICES, PROCESS & PRINT THEM                        
*                                                                               
LOOPINV  NTR1                                                                   
         SR    R2,R2                                                            
         ICM   R2,3,RECN           SET R2 FOR LOOP                              
         MVC   ANXTINV,ASRTINVT    SET START OF INVOICE SORT TABLE              
         XC    TGAGY,TGAGY         CLEAR FIRST AGENCY                           
         MVI   TBTPO,X'FF'         SET SAVED TP OFFICE = 0 TO GET 1ST           
*                                                                               
LOOP10   BAS   RE,INVPROC          PROCESS A SINGLE INVOICE                     
         BNE   LOOP80                                                           
         BRAS  RE,CHKREG           CHECK REGRESSION STATUS                      
         BNE   LOOP90              SKIP THE INVOICE                             
         CLI   UNICOPY,C'Y'        FOR UNION COPIES                             
         BNE   LOOP15                                                           
         CLI   TBCOTYPE,CTYASIAN   NON-ASIAN                                    
         BE    LOOP15                                                           
         CLI   TBCOTYPE,CTYSPAN    AND NON-SPANISH                              
         BE    LOOP15                                                           
         CLC   CUNION,=C'SAG'      SKIP GENERATING INVS FOR SAG/NY(C)           
         BNE   LOOP15                                                           
         CLC   CLOCAL(2),=C'NY'                                                 
         BE    *+10                                                             
         CLC   CLOCAL(2),=C'LA'                                                 
         BNE   LOOP15                                                           
         OC    TBVINV,TBVINV       AND NO SPECIFIC INVOICE                      
         BZ    LOOP90                                                           
*                                                                               
LOOP15   BAS   RE,BLDPERF          BUILD TABLE OF CHECK RECS                    
         BE    LOOP20                                                           
         CLI   ERRSTAT,0           IF INV ALREADY IN ERROR                      
         BNE   LOOP80              EXIT                                         
         MVI   ERRSTAT,TAINECKS    IF THERE ARE NO CHECKS                       
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     LOOP80                                                           
*                                                                               
LOOP20   BAS   RE,UPDCOMML         UPDATE NECESSARY INFO IN COMML               
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE                        
         BO    LOOP22              SKIP GETTING INVOICE NUMS FOR SUBS           
         GOTO1 =V(TABISPLT),DMCB,(RC),EINVNUM                                   
*                                                                               
LOOP22   GOTO1 =A(GETSI),DMCB,(RC) SET COMMENT FOR PRIS AND SUBS                
         BRAS  RE,GETSOAP          SET COMMENT FOR SOAP RESIDUALS               
         MVC   SVCKNUM,CKREC       SAVE THE NUMBER OF PERFORMERS                
         MVC   SVCKNUM2,CKREC2                                                  
         MVC   SVCKNUM3,CKREC3                                                  
*                                                                               
LOOP25   GOTO1 =A(PAGES),DMCB,(RC),(R3)                                         
         MVC   ANXTCHK,ACHKTAB     SET ADDRESS OF THE CHECK TABLE               
         CLI   UNICOPY,C'Y'                                                     
         BNE   LOOP30                                                           
         BAS   RE,UNIINIT          INIT FOR UNION COPY                          
         ZIC   R4,TBUNCOPY         N'COPIES FOR THIS UNION                      
         B     LOOP50                                                           
*                                                                               
LOOP30   ZIC   R4,TBAYCOPY         N'BILLING COPIES FOR THIS AGENCY             
         CLI   RECNUM,AC           IF DOING AGENCY'S COPIES                     
         BE    *+12                                                             
         CLI   RECNUM,DA           OR DMB&B AGY COPIES                          
         BNE   LOOP50                                                           
         TM    TBOPTS2,TBOVNCPY    AND N'COPIES OVERRIDDEN                      
         BO    *+6                 PRINT THE EXACT NUMBER                       
*                                                                               
LOOP40   BCTR  R4,0                SUBTRACT 1 PRINTED ORIGINALLY                
         LTR   R4,R4               IF N'COPIES IS 0 OR 1 THEN DONE              
         BNP   LOOP60                                                           
         MVI   PGNUM,0             CLEAR PAGE COUNTER FOR NEXT COPY             
*                                                                               
LOOP50   MVC   CKREC,SVCKNUM       RESET THE NUMBER OF PERFORMERS               
         MVC   CKREC2,SVCKNUM2                                                  
         MVC   CKREC3,SVCKNUM3                                                  
         XC    TBGROSS(AMTS2),TBGROSS                                           
         XC    TOTGRS(EAMTS),TOTGRS                                             
         XC    SUISURCA(SSURAMTS),SUISURCA                                      
         NI    EVNTSTAT,X'FF'-EVNTPSUI                                          
         MVC   ANXTCHK,ACHKTAB     RE-SET ADDRESS OF THE CHECK TABLE            
*                                                                               
         BRAS  RE,CKBOVER          CHECK FOR BOVER ELEMENT                      
         ZIC   R1,COPY                                                          
         LA    R1,1(R1)            INCREMENT COPY NUMBER                        
         STC   R1,COPY                                                          
         MVI   FORCEHED,C'Y'       FORCE PAGE EJECT                             
         GOTO1 =V(TABIPRT),DMCB,(RC)        PRINT THE INVOICE                   
         OI    STAT3,STCLPRTQ      CLOSE PRINT QUEUE                            
*                                                                               
         CLI   RECNUM,EB           IF DOING E-BILLING, ONLY 1 COPY              
         BE    LOOP60                                                           
         CLI   RECNUM,AC           IF DOING AGENCY'S COPIES                     
         BE    LOOP40                                                           
         CLI   RECNUM,DA           OR DMB&B AGY COPIES                          
         BE    LOOP40                                                           
         CLI   UNICOPY,C'Y'        OR UNION COPY                                
         BE    LOOP40              THEN LOOP FOR OTHER COPIES                   
*                                                                               
LOOP60   CLI   UNICOPY,C'Y'        IF NOT UNION COPY                            
         BE    LOOP80              GET NEXT INVOICE                             
         OI    SBYTE,TAINSBIL      SET STATUS BILLED - RECORD & PTR             
*                                                                               
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE                        
         BO    LOOP65              SKIP CHECK LEVEL WRITES                      
         BAS   RE,UPDCAST          UPDATE CAST EXPIRATION DATE                  
*                                                                               
LOOP65   BAS   RE,UPDCHK           UPDATE CHECK RECORDS/GUARANTEES              
         BAS   RE,UPDINV           RE-WRITE INVOICE WITH NEW STATUS             
         GOTO1 =A(DELSCRS),DMCB,(RC)   DELETE SCRN REC FOR THIS INV             
*                                                                               
         GOTO1 =V(TABISPLT),DMCB,(RC),EINVPST  DO INVOICE POSTINGS              
*                                                                               
         TM    TBSTA2,TAINSPRM     SKIP GENERAL POSTING IF PRIMARY              
         BNO   LOOP70                                                           
         GOTO1 EXTRCT,DMCB,(RC)    AND EXTRACTING                               
         BE    LOOP80                                                           
LOOP70   GOTO1 =V(TABIPOST),DMCB,(RC),EPUTTAB                                   
*                                                                               
LOOP80   CLI   RECNUM,BI           IF BILLING COPY                              
         BE    *+12                                                             
         CLI   RECNUM,PB           OR PRINT BILLING                             
         BNE   *+12                                                             
         TM    TBISTA2,TAPDSSUB    AND NOT SUBSIDIARY INVOICE                   
         BO    *+8                                                              
         BAS   RE,COUNT            ADD INVOICE TO CORRECT COUNTER               
*                                                                               
LOOP90   BCT   R2,LOOP10           GET NEXT INVOICE                             
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET BILL DATE POINTER IN KEY                                           
*                                                                               
         USING TLINPD,R1           USE INVOICE BILL DATE POINTER                
SETBI    NTR1                                                                   
         LA    R1,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   LENKEY,TLINDDTE-TLINPD-1                                         
         MVI   TLINPKEY,TLINDCDQ                                                
         MVC   TLINDDTE,TBSPDATE   DATE TO RUN ON                               
         MVC   TLINDAGY,TIFAGY     SET AGENCY                                   
*                                                                               
SETBX    B     XIT                                                              
         DROP  R1                                                               
         SPACE 3                                                                
*                                                                               
*        SET PASSIVE (OPEN ITEMS) POINTER IN KEY                                
*                                                                               
         USING TLINPD,R1           INVOICE PASSIVE POINTER                      
SETPA    NTR1                                                                   
         LA    R1,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   LENKEY,TLINBAGY-TLINPD-1 COMPARE ALL AGENCIES                    
         MVI   TLINPKEY,TLINBCDQ                                                
         CLC   TIFAGY,SPACES                                                    
         BNH   SETPX                                                            
         MVI   LENKEY,TLINBINV-TLINPD-1 COMPARE SPECIFIC AGENCY                 
         MVC   TLINBAGY,TIFAGY                                                  
*                                                                               
SETPX    B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*                                                                               
*        SET PASSIVE (PAYMENT HISTORY) POINTER IN KEY                           
*                                                                               
         USING TLINPD,R1           INVOICE PASSIVE POINTER                      
SETPH    NTR1                                                                   
         XC    KEY,KEY             RESET KEY TO USE PAYMENT PASSIVE PTR         
         LA    R1,KEY                                                           
         USING TLINPD,R1                                                        
         MVI   LENKEY,TLINHINV-TLINPD-1                                         
         MVI   TLINPKEY,TLINHCDQ                                                
         MVC   TLINHCOM,TBVCOM                                                  
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*        CHECK TO SEE IF WE WANT TO PROCESS THIS INVOICE                        
*                                                                               
         USING TAIND,R4                                                         
FILTINV  NTR1                                                                   
         TM    TBOPTS,TBRETRO      IF ONLY DOING RETRO INVOICES                 
         BNO   FILT20                                                           
         TM    TAINSTA2,TAINSRTH   AND THIS ISN'T A RETRO INVOICE               
         BNO   NO                  IGNORE INVOICE                               
         B     FILT30                                                           
*                                                                               
FILT20   TM    TAINSTA2,TAINSRTH   & VISA VERSA                                 
         BO    NO                                                               
*                                                                               
FILT30   CLI   READPTR,C'Y'        IF OPEN ITEM PTRS FOR COD'S                  
         BNE   FILT50                                                           
         TM    TAINSTAT,TAINSHLD   & IT'S IS A COD                              
         BNO   NO                                                               
         TM    TAINSTA2,TAINSHLP   & IT WAS PRINTED                             
         BNO   NO                                                               
         CLI   TAINLEN,X'43'       THEN IF THIS IS A NEW ELEM                   
         BNH   FILT40                                                           
         CLC   TAINHDTE,TGTODAY1   & IT WAS PRINTED TODAY                       
         BNE   NO                                                               
         B     FILT90              CONTINUE TO CHECK                            
*                                                                               
FILT40   CLC   TAINQDTE,TGTODAY1   ELSE IF OLD ELEM - WAS IT APP TODAY          
         BNE   NO                                                               
         B     FILT90              CONTINUE TO CHECK                            
*                                                                               
FILT50   CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   FILT70                                                           
         OC    TBRUNSTR,TBRUNSTR   AND RUN DATE PERIOD REQUESTED                
         BZ    FILT60                                                           
         CLC   TAINCKRN,TBRUNSTR   INSURE CHECK RUN DATE WITHIN IT              
         BL    NO                                                               
         CLC   TAINCKRN,TBRUNEND                                                
         BH    NO                                                               
*                                                                               
FILT60   B     FILT90              SKIP TO PAYMENT DETAILS EL FILTERING         
*                                                                               
FILT70   BRAS  RE,CKUPDT2          IF UPDATE OPTION FOR SOONS,                  
         BNE   FILT75                                                           
         TM    TAINSTAT,TAINSAPR   INVOICE MUST BE APPROVED                     
         BO    FILT80                                                           
         B     NO                                                               
*                                                                               
FILT75   OC    TBVINV,TBVINV       IF REQUESTING A SPECIFIC INVOICE             
         BNZ   FILT90                                                           
         OC    TBVCOM,TBVCOM       OR COMMERCIAL - GET IT                       
         BNZ   FILT90                                                           
         TM    TBOPTS,TBRETRO      IF DOING RETRO INVOICES                      
         BNO   FILT80                                                           
         CLC   TAINQDTE,TGTODAY1   ONLY TAKE INVOICES APPROVED TODAY            
         BNE   NO                                                               
*                                                                               
FILT80   CLC   TAINQDTE,TGTODAY1   IF IT WAS APPROVED AFTER TODAY               
         BH    NO                                                               
         TM    TAINSTA2,TAINSHLR   OR (IF WAS NOT YET RELEASED                  
         BO    FILT90                                                           
         TM    TAINSTA2,TAINSHLP       & IT WAS ALREADY PRINTED)                
         BO    NO                  IGNORE INVOICE                               
*                                                                               
FILT90   BAS   RE,FILTPD           FILTER ON PAYMENT DETAILS ELEMENT            
         BNE   NO                                                               
*                                                                               
         TM    TBOPTS,TBRERUN      IF JOB IS A RE-RUN                           
         BNO   YES                                                              
         L     R4,AIO              GET INVOICE STATUS ELEMENT                   
         USING TAIND,R4                                                         
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                 (WAS ALREADY READ)                           
         DC    H'0'                                                             
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS JOB IS A UNION COPY                  
         BE    FILT160                                                          
         CLI   READPTR,C'Y'        OR IF PROCESSING COD OPEN PTRS               
         BE    YES                                                              
         OC    TBVINV,TBVINV       OR IF REQUESTING A SPECIFIC INV              
         BNZ   YES                                                              
****     OC    TBVCOM,TBVCOM              OR COMMERCIAL                         
****     BNZ   YES                 CONTINUE PROCESSING                          
         TM    TAINSTAT,TAINSBIL   ELSE CHECK IF IT'S BEEN BILLED               
         BZ    NO                                                               
         CLC   TAINBDTE,TBSPDATE   TEST BILL DATE IS IN REQUESTED               
         BL    NO                                                               
         CLC   TAINBDTE,TBEPDATE   PERIOD                                       
         BH    NO                                                               
         B     YES                                                              
*                                                                               
FILT160  TM    TAINSTA2,TAINSADJ   DON'T DO PAYROLL ADJUSTMENTS                 
         BO    NO                                                               
         TM    TAINSTAT,TAINSCHK   ENSURE CHECKS HAVE BEEN WRITTEN              
         BNO   NO                                                               
*                                                                               
         L     R4,AIO              THEN GET THE UNION ELEMENTS                  
         USING TAULD,R4                                                         
         MVI   ELCODE,TAULELQ      FOR THIS INVOICE                             
         BAS   RE,GETEL                                                         
         BNE   NO                  IGNORE INV IF UNION ELEM NOT FOUND           
         ZIC   R1,TAULNULS         NUMBER OF UNION LOCALS                       
         LA    R4,TAULULS          A(1ST UNION/LOCAL)                           
         DROP  R4                                                               
*                                                                               
FILT170  BAS   RE,CKUNION          IF THIS UNION DOES NOT GET A COPY            
         BNE   FILT200                TRY NEXT ONE                              
         LA    R3,UNIONS           UNIONS ALREADY CHOOSEN                       
         LA    R2,UNILST           LAST UNION                                   
*                                                                               
FILT180  CR    R3,R2               IF INTERNAL TABLE IS TOO SMALL               
         BL    *+6                    DIE                                       
         DC    H'0'                                                             
         CLC   0(6,R4),0(R3)       IF THIS UNION/LOCAL ALREADY INPUT            
         BE    FILT200                GET NEXT UNI/LOCAL                        
         OC    0(6,R3),0(R3)       IF END OF UNIONS - ADD THIS UNION            
         BZ    FILT190                                                          
         LA    R3,6(R3)            BUMP TO NEXT UNION IN SAVED LIST             
         B     FILT180                                                          
*                                                                               
FILT190  MVC   0(6,R3),0(R4)       ENTER THIS UNION IN TABLE                    
*                                                                               
FILT200  LA    R4,6(R4)            GET NEXT UNION/LOCAL                         
         BCT   R1,FILT170                                                       
*                                                                               
         OC    UNIONS(ULLEN),UNIONS DOES AT LEAST 1 UNION GET A COPY            
         BZ    NO                   NO - GET NEXT INVOICE                       
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        FILTER ON PAYMENT DETAILS ELEMENT                                      
*                                                                               
FILTPD   NTR1                                                                   
         L     R4,AIO              GET PAYMENT DETAILS ELEMENT                  
         USING TAPDD,R4                                                         
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    FPD10                                                            
         MVI   ERRSTAT,TAINEPDI    IF NO PAYMENT DETAIL/INVOICE                 
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     FPDNO                                                            
*                                                                               
FPD10    MVC   TBTPO,TAPDOFF       SAVE TP OFFICE                               
         MVC   TBCLI,TAPDCLI            CLIENT CODE                             
         GOTO1 USEVAL,DMCB,TAPDUSE,TAPDTYPE                                     
         TM    TAPDOPT3,TAPDOGRY   IF THIS IS GREY P & G RECORD                 
         BO    FPDNO                                                            
         TM    TAPDOPT3,TAPDODUM   OR THIS IS A DUMMY RECORD                    
         BO    FPDNO                  IGNORE IT                                 
         CLI   RECNUM,DA           IF RUNNING DA ONLY GET                       
         BNE   FPD20                                                            
         CLC   TAPDEOR,=C'DM '     INVOICES WHOSE EMP OF RECORD IS DM           
         BNE   FPDNO                                                            
*                                                                               
FPD20    CLI   RECNUM,AC           IF RUNNING AC DON'T GET                      
         BNE   FPD30                                                            
         CLC   TAPDEOR,=C'DM '     INVOICES WHOSE EMP OF RECORD IS DM           
         BE    FPDNO                                                            
*                                                                               
FPD30    MVI   TSRTSTA,SRTSREG     SET SORT STATUS HIGH                         
         TM    TAPDPST1,TAPDPCRD   IF THIS IS A CREDIT PAYMENT                  
         BNO   *+8                                                              
         MVI   TSRTSTA,SRTSCRD     SET STATUS BIT                               
         TM    TAPDSTA2,TAPDSPRI    IF THIS IS A GUARANTEE PAYMENT              
         BO    FPD40                                                            
         CLI   TGUSEQU,UGRT                                                     
         BNE   *+8                                                              
FPD40    MVI   TSRTSTA,SRTSGRT     SET STATUS BIT                               
*                                                                               
         OC    TBVINV,TBVINV       IF REQUESTING A SPECIFIC INVOICE             
         BNZ   FPD45               GIVE IT TO THEM                              
         CLI   RECNUM,EB           ALL OTHER REPORTS OTHER THAN                 
         BE    FPD45                                                            
         CLI   RECNUM,AC           ALL OTHER REPORTS OTHER THAN                 
         BE    FPD45                                                            
         CLI   RECNUM,DA           EB, AC AND DA                                
         BE    FPD45                                                            
         TM    TAPDSTA2,TAPDSSUB   SKIP SUBSIDIARY INVOICES                     
         BNO   FPD45                                                            
         CLI   RECNUM,BI           IF BI OR PB - READS SUBS IN CASES OF         
         BE    *+12                                                             
         CLI   RECNUM,PB                                                        
         BNE   FPDNO                                                            
         TM    TBOPTS,TBRERUN      IF RE-RUN MODE                               
         BO    FPD45                                                            
         LR    R2,R4                                                            
         L     R4,AIO                                                           
         USING TAIND,R4                                                         
         MVI   ELCODE,TAINELQ      OR IF INVOICE CANCEL(LER)                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    TAINSTAT,TAINSCIN                                                
         BO    *+12                                                             
         TM    TAINSTA2,TAINSHLP   OR COD PRINTED ALREADY                       
         BNO   FPDNO                                                            
         LR    R4,R2                                                            
         USING TAPDD,R4                                                         
*                                                                               
FPD45    CLI   RECNUM,CR           IF THIS IS CRCOPY                            
         BNE   FPD50                                                            
         TM    TAPDPST1,TAPDPCRD   THEN ONLY PROCESS CREDIT PAYMENTS            
         BNO   FPDNO                                                            
*                                                                               
FPD50    CLI   RECNUM,SC           IF THIS IS SCOPY                             
         BNE   FPDYES                                                           
         TM    TAPDPST1,TAPDPBNP   THEN ONLY PROCESS BILL NO PAYROLL            
         BO    FPDYES                                                           
         TM    TGUSSTAT,SESSION    AND SESSIONS                                 
         BO    FPDYES                                                           
         LA    R1,SCTAB            AND OTHER USES IN SCTAB                      
*                                                                               
FPD55    CLI   0(R1),X'FF'         TEST NOT END OF TABLE                        
         BE    FPDNO                                                            
         CLC   TGUSEQU,0(R1)       TEST FOR MATCH                               
         BE    FPDYES                                                           
         LA    R1,1(R1)            BUMP TO NEXT ENTRY                           
         B     FPD55                                                            
*                                                                               
FPDYES   B     YES                                                              
FPDNO    B     NO                                                               
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        FILTER ON PAYMENT DETAILS ELEMENT                                      
*                                                                               
FILTCO   NTR1                                                                   
         L     R4,AIO                                                           
         USING TACOD,R4                                                         
         MVI   ELCODE,TACOELQ      GET COMMERCIAL DETAILS ELEMENT               
         BAS   RE,GETEL                FOR BILL TO CODE                         
         BE    FILTCO10                                                         
         MVI   ERRSTAT,TAINECOI    IF THERE'S NO COMML DETAILS ELEMENT          
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     NO                  AND GET NEXT INVOICE                         
*                                                                               
FILTCO10 CLI   RECNUM,VB           IF P+ BILLING                                
         BNE   FILTCO15                                                         
         CLI   TACOMED,TACOMEDE    REJECT IF NOT EVENT MEDIA                    
         BNE   NO                                                               
         B     FILTCO18                                                         
*                                                                               
FILTCO15 CLI   TACOMED,TACOMEDE    REJECT IF EVENT = MEDIA AND                  
         BNE   FILTCO18                                                         
         CLI   RECNUM,BI           BILLING OR                                   
         BE    NO                                                               
         CLI   RECNUM,PB           PRINT BILLING                                
         BE    NO                                                               
*                                                                               
FILTCO18 CLI   UNICOPY,C'Y'        IF UNION COPY                                
         BE    FILTCO20            PROCESS BOTH REGULAR AND PRINT               
         CLI   RECNUM,BI           IF REGULAR BILLING RUN                       
         BNE   *+12                                                             
         CLI   TACOMED,TACOMEDP    REJECT PRINT COMMERCIALS                     
         BE    NO                                                               
         CLI   RECNUM,PB           IF PRINT BILLING RUN                         
         BNE   *+12                                                             
         CLI   TACOMED,TACOMEDP    REJECT IF NOT PRINT COMMERICALS              
         BNE   NO                                                               
*                                                                               
FILTCO20 MVC   TBBTC,TACOATT       SET BILL TO CODE                             
         B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        CHECK IF UNION GETS A UNION COPY                                       
*        INPUT - R4 - POINTS TO UNION LOCAL                                     
*                                                                               
         SPACE 1                                                                
CKUNION  NTR1                                                                   
         USING UNILCLD,R3                                                       
         L     R3,=V(UNITAB)       UNION TABLE - NEEDED THIS RUN                
         LR    R2,R4                                                            
*                                                                               
         CLI   UNIFILT,C'Y'        IF THIS UNION REPORT IS A TURNAROUND         
         BNE   CU10                                                             
         L     R4,AIO                                                           
         USING TACOD,R4                                                         
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   CU10                                                             
         CLI   TACOTYPE,CTYASIAN   AND THIS IS AN ASIAN COMMERCIAL              
         BE    CUX                 WE WANT UNION COPY                           
         CLI   TACOTYPE,CTYSPAN    OR THIS IS A SPANISH COMMERCIAL              
         BE    CUX                 WE WANT UNION COPY                           
*                                                                               
CU10     C     R3,=V(UNITABX)      SHOULD NOT REACH END OF TABLE                
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OC    0(UNILEN1,R3),0(R3) EOT?                                         
         BZ    NO                                                               
         CLC   0(UNILEN1,R3),0(R2) UNION LOCAL FOUND                            
         BE    CU20                                                             
         LA    R3,UNILEN(R3)       BUMP TO NEXT ENTRY IN TABLE                  
         B     CU10                                                             
*                                                                               
CU20     CLI   UNIFILT,C' '        IF ALL UNIONS REQUESTED                      
         BNH   CUX                                                              
         CLC   UNITURN,UNIFILT     OR IF THIS UNION IS SAME AS FILTER           
         BE    CUX                 THEN WE WANT THIS UNION COPY                 
         B     NO                                                               
*                                                                               
CUX      B     YES                 RETURN CC EQ                                 
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        INVOICE RECORD GOTTEN - STORED IN TABLE TO                             
*                BE SORTED LATER                                                
*                                                                               
         SPACE 1                                                                
         USING SRTINVD,R3                                                       
         USING TLIND,R4                                                         
PRINV    NTR1                                                                   
         L     R3,ANXTINV          A(ENTRY IN SORT TABLE)                       
         XC    0(SRTILEN,R3),0(R3)                                              
         L     R4,AIO                                                           
         MVC   TGINV,TLININV       SAVE INVOICE NUMBER FOR SRT KEY              
         CLI   FTIME,C'Y'          FIRST TIME IN                                
         BE    PI20                MUST READ AGY REC AT LEAST 1X                
         CLC   TGAGY,TLINAGY       IF SAME AGENCY AS PREVIOUS ONE               
         BE    PI50                                                             
*                                                                               
PI20     MVI   FTIME,C'N'                                                       
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'A4',TLINAGY)  GET RECORD                  
         BE    PI30                                                             
         MVI   ERRSTAT,TAINEAGY    IF NO AGENCY RECORD                          
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     PIX                                                              
         DROP  R4                                                               
*                                                                               
         USING TAAYD,R4                                                         
PI30     L     R4,AIO              GET AGENCY ELEMENT FOR TP OFF CODE           
         MVI   ELCODE,TAAYELQ                                                   
         BAS   RE,GETEL                                                         
         BE    PI40                                                             
         MVI   ERRSTAT,TAINEAYE    IF NO AGENCY ELEMENT                         
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     PIX                                                              
*                                                                               
PI40     MVC   TBAYSTAT,TAAYSTAT   SAVE STATUS BYTES                            
         MVC   TBAYSTA2,TAAYSTA2                                                
         MVC   TBAYSTA3,TAAYSTA3                                                
         MVC   TBAYSTA4,TAAYSTA4                                                
         MVC   TBAYSTA5,TAAYSTA5                                                
         MVC   TBAYSTA6,TAAYSTA6                                                
         MVC   AYSTSIG,TAAYSGNS                                                 
*                                                                               
PI50     CLI   RECNUM,DA           IF DMB&B AGY COPY - CHECK IF AGY             
         BE    PI65                WANTS TO PRINT OWN COPY                      
         CLI   RECNUM,AC           IF AGY COPY                                  
         BNE   PI70                                                             
*                                                                               
PI60     MVI   SRTACCDE,SRTCHAND   SET SORT CODE                                
         TM    TBAYSTA2,TAAYSHND   FOR HAND DELIVERY                            
         BO    PI65                                                             
         MVI   SRTACCDE,SRTCOVER                                                
         TM    TBAYSTA2,TAAYSOVN   FOR OVERNIGHT DELIVERY                       
         BO    PI65                                                             
         MVI   SRTACCDE,SRTCPCH                                                 
         TM    TBAYSTA2,TAAYSPCH   FOR POUCH DELIVERY                           
         BO    PI65                                                             
         MVI   SRTACCDE,SRTCCHI                                                 
         TM    TBAYSTA2,TAAYSCHI   FOR CHICAGO POUCH DELIVERY                   
         BO    PI65                                                             
         MVI   SRTACCDE,SRTCPRI                                                 
         TM    TBAYSTA3,TAAYSPRI   PRIORITY MAIL                                
         BO    PI65                                                             
         MVI   SRTACCDE,SRTCMAIL   DEFAULT TO SORT FOR MAIL DELIVERY            
*                                                                               
PI65     L     R1,ATWA                                                          
         USING T703FFD,R1                                                       
         CLC   =C'ALL',SBIAGY      IF PROCESSING ALL AGENCIES                   
         BNE   PI70                                                             
         TM    TBAYSTAT,TAAYSPAC   TEST IF AGY WANTS TO PRINT COPIES            
         BO    PI150               ITSELF                                       
         DROP  R1                                                               
*                                                                               
PI70     OC    OFFLIST,OFFLIST     IF THIS IS FOR SPECIFIC OFFICES              
         BZ    PI90                                                             
         LA    R1,OFFLIST          ENSURE ONLY THOSE OFFICES GET                
         LA    R2,L'OFFLIST                                                     
*                                                                               
PI80     CLC   0(1,R1),TBTPO       INTO THE SORT                                
         BE    PI90                                                             
         LA    R1,1(R1)            BUMP OFFICE TABLE                            
         BCT   R2,PI80                                                          
         B     PIX                                                              
*                                                                               
PI90     LA    R4,UNIONS           1 SORT ENTRY FOR EACH UNION/INVOICE          
*                                                                               
PI100    MVC   SRTTPO,TBTPO        SET UP SORT REC - TP OFFICE                  
         MVC   SRTAGY,TGAGY                        - AGENCY                     
         CLI   RECNUM,BI           BILLING SORTS BY STATUS                      
         BE    *+12                                                             
         CLI   RECNUM,PB                                                        
         BNE   PI110                                                            
         MVC   SRTSTAT,TSRTSTA                                                  
*                                                                               
PI110    CLI   RECNUM,DA           DA SORTS BY BILL TO CODE                     
         BE    PI120                                                            
         CLI   RECNUM,AC           AC SORTS BY ATTENTION NAME OR CLI            
         BE    *+8                                                              
         CLI   RECNUM,EB                                                        
         BNE   PI130                                                            
         MVC   AIO,AIO1            BI SAVED FN NAME ON INVOICE REC              
         TM    TBOPTS2,TBSRTEST    IF SORT BY EST # INSTEAD OF ATTN             
         BZ    PI115                                                            
         GOTO1 MVCTST,DMCB,TANUTEST,(L'SRTEST,SRTEST),TANUELQ                   
         B     PI130                                                            
PI115    GOTO1 MVCTST,DMCB,TAFNTATT,(L'SRTATTN,SRTATTN),TAFNELQ                 
         B     PI130                                                            
*                                                                               
PI120    MVC   SRTATT,TBBTC                                                     
*                                                                               
PI130    MVC   SRTINV,TGINV                        - INVOICE #                  
         XC    SRTINV,COMPLM                         (UNCOMPLEMENTED)           
         MVC   SRTDA,TBDA                          - DISK ADDR                  
*                                                                               
         CLI   UNICOPY,C'Y'        UNION COPY SORTS BY                          
         BNE   PI140                                                            
         MVC   SRTUNI,0(R4)               UNION                                 
         MVC   SRTLCL,3(R4)               LOCAL                                 
*                                  1 ENTRY PER UNION/INVOICE                    
PI140    LH    R2,RECN             COUNT NUMBER OF RECORDS FOR SORT             
         LA    R2,1(R2)                                                         
         STH   R2,RECN                                                          
         LA    R3,SRTILEN(R3)      BUMP SORT TABLE                              
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY -                    
         BNE   PI150                  GET NEXT UNION                            
         LA    R2,UNILST           END OF TABLE                                 
         CR    R4,R2                                                            
         BE    PI150                                                            
         LA    R4,6(R4)                                                         
         OC    0(6,R4),0(R4)       ANY MORE UNIONS?                             
         BNZ   PI100                                                            
*                                                                               
PI150    ST    R3,ANXTINV                                                       
         C     R3,ASRTINVX         COMPARE ENTRY TO END OF TABLE                
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PIX      MVC   AIO,AIO1            RESTORE IO AREA                              
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        INITIALIZATION FOR UNION COPIES                                        
*                                                                               
         SPACE 1                                                                
UNIINIT  NTR1                                                                   
         MVI   PGNUM,0             CLEAR PAGE COUNTER FOR NEXT COPY             
         XC    CKREC(6),CKREC            THE NUMBER OF PERFORMERS               
         XC    TBGROSS(AMTS2),TBGROSS    ALL AMOUNTS                            
         XC    TOTGRS(EAMTS),TOTGRS                                             
*                                        SORT INVOICES FOR UNION                
         USING TABCHKD,R3                                                       
         L     R3,ANXTCHK                                                       
*                                                                               
UNII20   CLC   TABUNI,CUNION       COUNT HOW MANY CHECKS TO THIS UNION          
         BNE   UNII50                                                           
         LH    R1,CKREC                                                         
         LA    R1,1(R1)            INCREMENT NUMBER OF PERFORMERS               
         STH   R1,CKREC                                                         
         OC    TABOV2,TABOV2       NEED EXTRA LINE FOR OV SCALE %               
         BNZ   UNII30                                                           
         CLI   TABCNUM,C' '        OR CORPORATION                               
         BH    UNII30                                                           
         CLI   TABXLN,C'Y'         OR EXTRA LINE FOR TAG                        
         BNE   UNII40                                                           
*                                                                               
UNII30   LH    R1,CKREC2           NEED EXTRA LINE                              
         LA    R1,1(R1)                                                         
         STH   R1,CKREC2                                                        
*                                                                               
UNII40   LA    R3,TABLEN(R3)       BUMP TABLE                                   
         B     UNII20                                                           
*                                                                               
UNII50   LH    R2,CKREC                                                         
         GOTO1 =A(PAGES),DMCB,(RC),(R2)                                         
         DROP  R3                                                               
*                                                                               
         L     R3,=V(UNITAB)       UNION TABLE - NEEDED THIS RUN                
         USING UNILCLD,R3                                                       
*                                                                               
UNII60   C     R3,=V(UNITABX)      END OF TABLE                                 
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   UNIUNI,CUNION       FIND UNION/LOCAL IN TABLE                    
         BNE   UNII70                                                           
         CLC   UNILCL,CLOCAL                                                    
         BNE   UNII70                                                           
         MVC   TBUNCOPY,UNINCPY    SET NUMBER OF COPIES                         
         B     UNIIX                                                            
*                                                                               
UNII70   LA    R3,UNILEN(R3)       BUMP TABLE                                   
         B     UNII60                                                           
*                                                                               
UNIIX    B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        ADD INVOICE TO COUNTER                                                 
*                                                                               
COUNT    TM    STATUS,STERR                                                     
         BNO   CT10                                                             
         AP    ERRINVC,=P'1'       ADD 1 TO ERROR COUNTER                       
*                                                                               
CT10     TM    STATUS,STBNP                                                     
         BNO   CT20                                                             
         AP    BNPINV,=P'1'        ADD 1 TO BNP COUNTER                         
*                                                                               
CT20     TM    STATUS,STCOD                                                     
         BNO   CT30                                                             
         AP    CODINV,=P'1'        ADD 1 TO COD COUNTER                         
*                                                                               
CT30     TM    STATUS,STURG                                                     
         BNO   CT40                                                             
         AP    URGINV,=P'1'        ADD 1 TO URGENT COUNTER                      
*                                                                               
CT40     TM    STATUS,STNOP                                                     
         BNO   CT50                                                             
         AP    NOPINV,=P'1'        ADD 1 TO COD REL NO PRINT COUNTER            
*                                                                               
CT50     TM    STATUS,STREG                                                     
         BNO   CX                                                               
         AP    REGINV,=P'1'        ADD 1 TO REGULAR COUNTER                     
*                                                                               
CX       BR    RE                                                               
         EJECT                                                                  
*                                                                               
*        PROCESS A SINGLE INVOICE RECORD                                        
*                GET ALL RELEVANT INFO FROM INV REC FIRST                       
*                READ ALL NECESSARY RECORDS AND STORE THE INFORMATION           
*                                                                               
         USING SRTINVD,R3                                                       
INVPROC  NTR1                                                                   
         L     R3,ANXTINV          SET R3 TO NEXT INVOICE ENTRY                 
         BRAS  RE,INVINIT          INITIALIZATIONS                              
         MVC   CUNION,SRTUNI       SET UNION AND LOCAL                          
         MVC   CLOCAL,SRTLCL                                                    
*                                                                               
         USING TLDRD,R2                                                         
         LA    R2,KEY                                                           
         XC    KEY,KEY             CLEAR KEY & SET INV REC'S D/A                
         MVC   TLDRDA,SRTDA                                                     
         MVC   AIO,AIO1            GET INVOICE RECORD & KEEP IT IN AIO1         
         GOTO1 GETREC                                                           
         DROP  R2                                                               
*                                                                               
         L     R4,AIO              GET INFO FROM KEY                            
         USING TLIND,R4                                                         
         MVC   TGAGY,TLINAGY       AGENCY CODE                                  
         MVC   TGINV,TLININV       INVOICE NUMBER - COMPLEMENTED                
         DROP  R4                                                               
*                                                                               
         BRAS  RE,SETEFDT          SET EFFECTIVE DATE                           
         BRAS  RE,GETSYS           GET RATES FROM SYSTEM RECORD (GST)           
*                                                                               
         GOTOR SETEURT,DMCB,AIO    SET EURO CONVERSION RATE                     
         GOTOR CVTEPAU,DMCB,AIO    CONVERT EURO PAY OPTIONS TO US$              
         GOTOR CVTEPDU,DMCB,AIO    CONVERT EURO PAYMENT DETAILS TO US$          
*                                                                               
         LA    RF,TBILLD                                                        
         AH    RF,=Y(TBOPTS3-TBILLD)  RF -> TBOPTS3                             
         TM    0(RF),TBNOBOVR      SKIPPING BOVER FOR COMPARE                   
         BO    IP03                                                             
         GOTOR SAVENBR,DMCB,ACURTARA  SAVE NEW BILLING RATES                    
*                                                                               
IP03     BAS   RE,GETCS            GET INFO FROM TACS ELEMENT                   
         BAS   RE,GETACO           GET INFO FROM TACO ELEMENT                   
         BAS   RE,GETIN            GET INFO FROM INV STATUS ELEMENT             
         BAS   RE,GETPD            GET INFO FROM PAYMENT DETAILS ELEM           
         BNE   IPX                                                              
         BAS   RE,GETASB           GET INFO FROM SOAP CABLE ELEMENT             
         BAS   RE,GETUP            GET INFO FROM UPGRADE ELEMENT                
         BAS   RE,GETND            GET NETWORK/CLASS A DETAILS ELEMENT          
         BAS   RE,GETNP            GET NETWORK/CLASS A PROGRAM ELEMENT          
         BAS   RE,GETNU            GET CREDIT OF INVOICE ELEMENT                
         BRAS  RE,GETUSCN          GET LINKED US/CAN INVOICE #                  
         GOTOR GETLF               GET INFO FROM LIFT OR VERSIONS EL.           
         BAS   RE,GETCC            GET INFO FROM CONTRACT ELEMENT               
         GOTO1 =V(TABISPLT),DMCB,(RC),EGETSI GET INFO FROM TASID TO TBL         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF THIS IS A PRINT INVOICE                   
         BE    IP05                MUST READ COMMERCIAL                         
         OC    TBPRD,TBPRD         IF THERE IS NOT A PRODUCT                    
         BNZ   IP20                GET IT OFF OF COMMERCIAL                     
*                                                                               
IP05     MVC   AIO,AIO2            SET TO READ COMML VIA INTERNAL NO.           
         BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BNE   IP06                                                             
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'30',0)                                   
         B     IP07                                                             
IP06     GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'20',0)                                   
IP07     BE    IP10                                                             
         MVI   ERRSTAT,TAINECOM    IF THERE'S NO COMML RECORD                   
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     IPX                                                              
*                                                                               
IP10     L     R4,AIO              GET PHOTO INFO & PRODUCT NAME                
         GOTO1 MVCTST,DMCB,TANUTPHO,(L'TBPHOTO,TBPHOTO),TANUELQ                 
         GOTO1 (RF),(R1),TAFNTPHO,(L'TBPHFFNM,TBPHFFNM),TAFNELQ                 
         OC    TBPRD,TBPRD         IF THERE ISN'T A PRODUCT ALREADY             
         BNZ   IP20                GET IT                                       
         GOTO1 MVCTST,DMCB,TAFNTPRD,(L'TBPRNAME,TBPRNAME),TAFNELQ               
*                                                                               
IP20     MVC   AIO,AIO1                                                         
         GOTO1 MVCTST,DMCB,TACMTYPG,(L'TBCOM,TBCOM),TACMELQ COMMENT             
         GOTO1 (RF),(R1),TANUTSIG,(L'TBSIGN,TBSIGN),TANUELQ  SIGNATORY          
         GOTO1 (RF),(R1),TANUTEST,(L'TBEST,TBEST),TANUELQ   ESTIMATE            
         GOTO1 (RF),(R1),TANUTAUT,(L'TBPO,TBPO),TANUELQ     PO NUMBER           
         GOTO1 (RF),(R1),TAFNTVTL,(L'TBCONAME,TBCONAME),TAFNELQ                 
         CLC   TBCONAME,SPACES     IF THERE IS NO VERSION TITLE,                
         BH    IP25                GET COMML TITLE                              
         GOTO1 MVCTST,(R1),TAFNTTTL,(L'TBCONAME,TBCONAME),TAFNELQ               
*                                  GET COMML TITLE - FREE FORM NAME             
IP25     CLC   SRTTPO,TBTPO        SAME TP OFFICE AS PREVIOUS ONE               
         BE    IP30                                                             
         BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BE    IP28                ONLY POST ONE TIME                           
         GOTO1 =V(TABIPOST),DMCB,(RC),EPOSTSE                                   
*        XC    POSTTAB(POSTLEN),POSTTAB     FLUSH TABLE                         
         L     RF,APOSTTAB                                                      
         XC    0(256,RF),0(RF)                                                  
                                                                                
IP28     BAS   RE,GETTPO           GET INFO FROM OFFICE RECORD                  
         BNE   IPX                                                              
*                                                                               
IP30     BAS   RE,GETAGY           GET AGY RECORD & ALL INFO                    
         BNE   IPX                                                              
         BAS   RE,GETCLI           GET CLI RECORD & ALL INFO                    
         BNE   IPX                                                              
         BAS   RE,GETPRD           GET PRD RECORD & ALL INFO                    
         BNE   IPX                                                              
         BAS   RE,GETATT           GET ATT RECORD & ALL INFO                    
         BNE   IPX                                                              
         BAS   RE,GETEMP           GET EMP RECORD & ALL INFO                    
         BNE   IPX                                                              
         GOTO1 =A(SETATT),DMCB,(RC) SET ATT NAME & ADDRESS                      
         GOTO1 SETPCIF,DMCB,(RC)   SET PROD CLI INT FOR INV REC                 
*                                  THEN SET INFO FOR SUBSIDIARY INVS            
         GOTO1 =V(TABISPLT),DMCB,(RC),ESETPCIF                                  
         CLI   ERRSTAT,TAINEIFE    IF NO INTERFACE ELEMENT                      
         BNE   *+8                                                              
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
*                                                                               
         BRAS  RE,INITCNPD         INIT INVOICE'S CAN$ PAYMENT DETAILS          
*                                                                               
IPX      L     R1,ANXTINV          BUMP INVOICE TABLE                           
         LA    R1,SRTILEN(R1)                                                   
         ST    R1,ANXTINV                                                       
         CLI   ERRSTAT,0           IF THERE WAS AN ERROR                        
         BNE   NO                  RETURN SKIP THE INVOICE                      
         B     YES                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM TACO ELEMENT IN INVOICE                                  
*                                                                               
GETACO   NTR1                                                                   
         L     R4,AIO              GET COMML DETAILS ELEMENT                    
         MVI   ELCODE,TACOELQ                                                   
         USING TACOD,R4                                                         
         BAS   RE,GETEL            ELEMENT MUST BE THERE - ALREADY READ         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TGCID,TACOCID       COMMERCIAL ID                                
         MVC   TBFCYC,TACOFCYC     FIRST FIXED CYCLE                            
         MVC   TBAIR,TACOAIR       FIRST AIR DATE                               
         MVC   TBMED,TACOMED       MEDIA                                        
         MVC   TBSEC,TACOSEC       COMML LENGTH                                 
         MVC   TBATT,TACOATT       ATTENTION CODE                               
         MVC   TBCOTYPE,TACOTYPE   COMMERCIAL TYPE                              
         MVC   TBCOEXP,TACOEXP     COMMERCIAL EXPIRATION DATE                   
         MVC   TBTID,TACOTID       TRACK ID                                     
         MVC   TBTLN,TACOTLN       TRACK LENGTH                                 
         MVC   TBACTYPE,TACOCTYP   ACTRA TYPE                                   
*                                                                               
         TM    TACOSTAT,TACOSPRT   IF COMMERCIAL SET TO HAVE BILLING            
         BZ    XIT                 DISPLAY AS PRINT                             
         MVI   TBMED,C'P'          CHANGE MEDIA TO PRINT NOW                    
GTACOX   B     XIT                                                              
         DROP  R4                                                               
         SPACE 3                                                                
*                                                                               
*        GET INFO FROM TASB ELEMENT IN INVOICE                                  
*                                                                               
GETASB   NTR1                                                                   
         XC    TBCID2,TBCID2                                                    
         L     R4,AIO              GET SOAP CABLE ELEMENT                       
         MVI   ELCODE,TASBELQ                                                   
         USING TASBD,R4                                                         
         BAS   RE,GETEL            ELEMENT MUST BE THERE - ALREADY READ         
         BNE   GSBX                                                             
*                                                                               
         MVC   TBCID2,TASBCID2     2ND COMMERCIAL ID                            
*                                                                               
GSBX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM TACS ELEMENT IN INVOICE                                  
*                                                                               
GETCS    NTR1                                                                   
         L     R4,AIO              GET COMML STUDIO ELEMENTS                    
         MVI   ELCODE,TACSELQ                                                   
         USING TACSD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GCSX                                                             
*                                                                               
GCS10    CLI   TACSTYPE,TACSTYPF   FILM INFO                                    
         BNE   GCS20                                                            
         MVC   TBFDAT,TACSDATE     FILM DATE                                    
         MVC   TBFSTUD,TACSSTUD    FILM STUDIO                                  
         MVC   TBFCITY,TACSCITY    FILM CITY                                    
         CLI   TACSLEN,TACSLNQ                                                  
         BL    GCS50                                                            
         MVC   TBFSTAT,TACSSTAT    FILM STATE                                   
         B     GCS50                                                            
*                                                                               
GCS20    CLI   TACSTYPE,TACSTYPR   RECORDING INFO                               
         BNE   GCS30                                                            
         MVC   TBRDAT,TACSDATE     RECORDING DATE                               
         MVC   TBRSTUD,TACSSTUD    RECORDING STUDIO                             
         MVC   TBRCITY,TACSCITY    RECORDING CITY                               
         CLI   TACSLEN,TACSLNQ                                                  
         BL    GCS50                                                            
         MVC   TBRSTAT,TACSSTAT    RECORDING STATE                              
         B     GCS50                                                            
*                                                                               
GCS30    CLI   TACSTYPE,TACSTYPM   SHOULD BE MUSIC                              
         BNE   GCS40                                                            
         MVC   TBMDAT,TACSDATE     MUSIC DATE                                   
         MVC   TBMSTUD,TACSSTUD    MUSIC STUDIO                                 
         MVC   TBMCITY,TACSCITY    MUSIC CITY                                   
         CLI   TACSLEN,TACSLNQ                                                  
         BL    GCS50                                                            
         MVC   TBMSTAT,TACSSTAT    MUSIC STATE                                  
         B     GCS50                                                            
*                                                                               
GCS40    CLI   TACSTYPE,TACSTYPS   SHOOT INFO                                   
         BNE   GCS50                                                            
         MVC   TBSDAT,TACSDATE     SHOOT DATE                                   
         MVC   TBSSTUD,TACSSTUD    SHOOT STUDIO                                 
         MVC   TBSCITY,TACSCITY    SHOOT CITY                                   
*                                                                               
GCS50    BAS   RE,NEXTEL           GET NEXT                                     
         BE    GCS10                                                            
*                                                                               
GCSX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM CONTRACT ELEMENT IN INVOICE                              
*                                                                               
GETCC    NTR1                                                                   
         L     R4,AIO              GET COMML CONTRACT ELEMENT                   
         MVI   ELCODE,TACCELQ                                                   
         USING TACCD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GCCX                                                             
         ZIC   R1,TACCNCON         NUMBER OF CONTRACTS                          
         CHI   R1,4                                                             
         BNH   *+8                                                              
         LHI   R1,4                                                             
         LA    R4,TACCCON          1ST CONTRACT NUMBER                          
         LA    R2,TBCONT                                                        
*                                                                               
GCC10    MVC   0(L'TACCCON,R2),0(R4)                                            
         LA    R4,L'TACCCON(R4)    NEXT CONTRACT                                
         LA    R2,L'TACCCON(R2)                                                 
         BCT   R1,GCC10                                                         
*                                                                               
GCCX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM PAYMENT DETAILS ELEMENT IN INVOICE                       
*                                                                               
GETPD    NTR1                                                                   
         L     R4,AIO              GET PAYMENT DETAIL ELEMENT                   
         MVI   ELCODE,TAPDELQ                                                   
         USING TAPDD,R4                                                         
         BAS   RE,GETEL                                                         
         BE    GPD10                                                            
         MVI   ERRSTAT,TAINEPDI    IF NO PAYMENT DETAIL/INVOICE                 
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     NO                                                               
*                                                                               
GPD10    MVC   SVOPT1,TAPDOPT1     OPTIONS                                      
         MVC   SVOPT2,TAPDOPT2                                                  
         MVC   SVOPT4,TAPDOPT4                                                  
         MVC   SVOPT5,TAPDOPT5                                                  
         TM    TAPDPSTS,TAPDPBNP                                                
         BNO   *+8                                                              
         OI    STATUS,STBNP        SET COUNTER = BNP                            
*                                                                               
         TM    TAPDPST1,TAPDPCRD                                                
         BNO   *+8                                                              
         OI    STATUS,STCRDT       SET STATUS = CREDIT                          
*                                                                               
         MVC   TGEMP,TAPDEOR       EMPLOYER OF RECORD                           
         MVC   TGCLI,TAPDCLI       CLIENT                                       
         MVC   TBPRD,TAPDPRD       PRODUCT                                      
         MVC   TGCOM,TAPDCOM       INTERNAL COMMERCIAL NUMBER                   
         MVC   TBISTAT,TAPDSTAT    STATUS BYTE                                  
         MVC   TBISTA2,TAPDSTA2    SECOND STATUS BYTE                           
         MVC   TBISTA3,TAPDSTA3    THIRD STATUS BYTE                            
         MVC   TBIPST2,TAPDPST2                                                 
         MVC   TBAREA,TAPDAREA     AREA                                         
         MVC   TBPUSE,TAPDPUSE     PRINT USE                                    
         MVI   TBNPOST,C'N'                                                     
         CLC   TGEMP,=C'DM '       IF EMPLOYER = DM                             
         BNE   GPD30                                                            
         TM    TAPDSTAT,TAPDSCAN   & PAYMENT IS NOT CANADIAN                    
         BO    GPD30                                                            
         MVI   TBNPOST,C'Y'        SET FLAG NOT TO POST                         
*                                                                               
GPD30    CLI   TBCOTYPE,CTYDEM     DON'T SET/PRINT CYCLE DATES                  
         BE    GPD35               FOR DEMO COMMERCIAL TYPE                     
         MVC   TBCDTS,TAPDCYCS     CYCLE START - END DATES                      
*                                                                               
GPD35    MVC   TBUSE,TAPDUSE       USE CODE                                     
         MVC   TBTYPE,TAPDTYPE     TYPE OF PAYMENT FOR USE                      
         GOTO1 USEVAL,DMCB,TAPDUSE,TAPDTYPE                                     
         MVI   TBPRTINV,C'N'                                                    
         CLI   TGUSMEDS,PRINT      IF THIS IS A PRINT INVOICE                   
         BNE   *+8                                                              
         MVI   TBPRTINV,C'Y'       SET FLAG                                     
*                                                                               
         MVC   TBCWKS,TGUSWKS      CYCLE WEEKS                                  
         MVC   TBPER,TAPDESPD      PERIOD                                       
         MVC   TBMAJ,TAPDMAJ       MAJORS                                       
         MVC   TBUNITS,TAPDUNIT    UNITS                                        
         TM    TGUSTYST,INSERTS    IF INSERTS                                   
         BZ    *+10                                                             
         MVC   TBUNITS,TAPDINS     THEN SET FROM TAPDINS                        
         CLI   TGUSEQU,UTAG        IF TAGS                                      
         BNE   GPD37                                                            
         XC    TBUNITS,TBUNITS                                                  
         MVC   TBUNITS+1(1),TAPDTAGS  THEN SET FROM TAPDTAGS                    
GPD37    CLI   TGUSEQU,UDEM        IF DEMOS                                     
         BE    GPD38                                                            
         CLI   TGUSEQU,USNA                                                     
         BE    GPD38                                                            
         CLI   TGUSEQU,UCDM                                                     
         BNE   GPD39                                                            
GPD38    XC    TBUNITS,TBUNITS                                                  
         MVC   TBUNITS+1(1),TAPDDEMS  THEN SET FROM TAPDDEMS                    
*                                                                               
GPD39    MVC   TBSTUS,TAPDSTUS     START USE NUMBER                             
         MVC   TBUSES,TAPDUSES     NUMBER OF TOTAL USES                         
*                                                                               
         MVI   TBBDSTAT,0          CLEAR TABD STATUS BYTE FIELD                 
         MVI   TGCUR,C'U'          DEFAULT CURRENCY = US                        
                                                                                
         TM    TAPDPST2,TAPDPEUR   IF PAYMENT IS IN EUROS                       
         BZ    GPD39A                                                           
         MVI   TGCUR,C'E'          SET CURRENCY TO EUROS                        
         B     GPDX                                                             
                                                                                
GPD39A   TM    TAPDSTAT,TAPDSCAN   IF PAYMENT IS CANADIAN                       
         BNO   GPDX                                                             
         MVI   TGCUR,C'C'          SET CURRENCY TO CANADIAN                     
         ZAP   TBGCVT,TBCCVT       SET SYSTEM CONVERSION RATE                   
         GOTO1 EXTRCT,DMCB,(RC)    IF NOT EXTRACTING                            
         BE    GPD40                                                            
         TM    TAPDOPT3,TAPDOCCR   OR IF OVERRIDE CANADIAN CONV RATE            
         BNO   GPDX                EXIT                                         
*                                                                               
GPD40    L     R4,AIO              GET BILLING DETAILS ELEMENT                  
         MVI   ELCODE,TABDELQ                                                   
         USING TABDD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GPDX                                                             
         OC    TABDCCVT,TABDCCVT                                                
         BZ    GPD50                                                            
         ZAP   TBGCVT,TABDCCVT     & SET OVERRIDE RATE                          
GPD50    MVC   TBBDSTAT,TABDSTAT   & STATUS BYTE                                
*                                                                               
GPDX     B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM UPGRADE ELEMENT IN INVOICE                               
*                                                                               
GETUP    NTR1                                                                   
         L     R4,AIO              GET UPGRADE ELEMENT                          
         MVI   ELCODE,TAUPELQ                                                   
         USING TAUPD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GUPX                                                             
         MVI   UPMAJFL,C'Y'                                                     
         MVC   TEMP(1),TBMAJ       IF THERE IS AN UPG ELEMENT                   
         MVC   TEMP+1(2),TBUNITS                                                
         MVC   TBMAJ,TAUPIMAJ      THEN IT'S INFO GOES INTO ORIGINAL            
         MVC   TBUNITS,TAUPIUNT         MAJORS                                  
         MVC   TBUPMAJ,TEMP        AND THE INFO IN X'80' ELEM IS UPG            
         MVC   TBUPUNIT,TEMP+1                                                  
*                                                                               
GUPX     B     XIT                                                              
         DROP  R4                                                               
         SPACE 2                                                                
*                                                                               
*        GET INFO FROM INVOICE STATUS ELEMENT                                   
*                                                                               
GETIN    NTR1                                                                   
         L     R4,AIO              GET INVOICE STATUS ELEMENT                   
         MVI   ELCODE,TAINELQ                                                   
         USING TAIND,R4            IT MUST BE THERE - ALREADY READ              
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    TBHDTE,TBHDTE                                                    
         TM    TAINSTA2,TAINSHLP   IF INVOICE WAS PRINTED                       
         BNO   GIN10                                                            
         MVC   TBHDTE,TAINQDTE     SET PRINT DATE = QC DATE                     
         CLI   TAINLEN,X'43'       IF NEW ELEM LENGTH                           
         BNH   GIN10                                                            
         MVC   TBHDTE,TAINHDTE     SET IT TO PRINT DATE                         
*                                                                               
GIN10    MVC   TBSTAT,TAINSTAT                                                  
         MVC   TBSTA2,TAINSTA2     SECOND STATUS BYTE                           
         TM    TBSTAT,TAINSBIL     IF INVOICE HAS BEEN BILLED                   
         BNO   GIN30                                                            
         TM    TBSTAT,TAINSCHK     AND (CHECKS HAVE BEEN WRITTEN                
         BO    GIN20                                                            
         TM    STATUS,STBNP        OR IT'S A BNP INVOICE)                       
         BNO   GIN30                                                            
*                                                                               
GIN20    MVI   NOBIPTR,C'Y'        SET OPEN ITEM PTR DOESN'T EXIST              
*                                                                               
GIN30    MVC   TBIDTE,TAINIDTE     DATE                                         
         MVC   TBITIM,TAINITIM     TIME                                         
         TM    TBSTAT,TAINSHLD     IF IT'S A COD                                
         BNO   GIN40                                                            
         OI    STATUS,STCOD        SET COUNTER STATUS                           
         B     GIN60                                                            
*                                                                               
GIN40    TM    TBSTA2,TAINSURG     IF IT'S URGENT                               
         BNO   GIN50                                                            
         OI    STATUS,STURG        SET COUNTER STATUS                           
         B     GIN60                                                            
*                                                                               
GIN50    TM    TBSTA2,TAINSHLR     IF INVOICE IS RELEASED                       
         BNO   GIN55                                                            
         TM    TBSTA2,TAINSHLP     & WAS PRINTED                                
         BZ    GIN55                                                            
         OI    STATUS,STNOP        SET COUNTER STATUS                           
         B     GIN60                                                            
*                                                                               
GIN55    OI    STATUS,STREG        ELSE MUST BE REGULAR INVOICE                 
*                                                                               
GIN60    OC    TAINBDTE,TAINBDTE   IF WE HAVE AN ORIGINAL BILL DATE             
         BNZ   GIN70               USE IT                                       
         TM    TBOPTS,TBRERUN      IF JOB IS NOT A RE-RUN                       
         BNO   GIN80               LEAVE RUN DATE = TODAY                       
*                                                                               
GIN70    TM    STATUS,STCOD                                                     
         BO    GIN75                                                            
         GOTO1 DATCON,DMCB,(1,TAINBDTE),(8,TBRUNDTE)                            
         MVC   TBTODAY,TAINBDTE    ELSE USE ORIGINAL BILL DATE                  
*                                                                               
GIN75    TM    TBSTA2,TAINSHLR     IF INVOICE IS RELEASED COD                   
         BZ    GIN80                                                            
         OC    TAINHDTE,TAINHDTE                                                
         BZ    GIN80                                                            
         GOTO1 DATCON,DMCB,(1,TAINHDTE),(8,TBRUNDTE)                            
         MVC   TBTODAY,TAINHDTE    USE COD PRINT DATE                           
*                                                                               
GIN80    MVC   TBTPC,TAINPST       STAFF CODE OF PAYER                          
         MVC   TBQC,TAINQST        STAFF CODE OF QC PERSON                      
         MVC   TBCKDTE,TAINCDTE    SET CHECK DATE                               
*                                                                               
         TM    TBSTAT,TAINSCIN     IF INVOICE CANCEL(LER)                       
         BNO   GINX                                                             
         XC    TBOBBDTE,TBOBBDTE                                                
         L     R4,AIO                                                           
         USING TAOBD,R4                                                         
         MVI   ELCODE,TAOBELQ      GET ORIGINAL BILL DATE EL                    
         BAS   RE,GETEL                                                         
         BNE   GINX                                                             
         MVC   TBOBBDTE,TAOBBDTE   SAVE ORIGINAL BILL DATE                      
*                                                                               
GINX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFO FROM NETWORK/CLASS A DETAILS ELEMENT                          
*                                                                               
GETND    NTR1                                                                   
         L     R4,AIO              GET NETWORK/CLASS A DETAILS ELEMENT          
         MVI   ELCODE,TANDELQ                                                   
         USING TANDD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GNDX                                                             
         MVC   TBSTUL,TANDSTUL     START USE NUMBER FOR LIFT ONLY               
         MVC   TBUSEL,TANDUSEL     N'USES TO LIFT                               
*                                                                               
GNDX     B     XIT                                                              
         DROP  R4                                                               
         SPACE 3                                                                
*                                                                               
*        GET INFO FROM NETWORK/CLASS A PROGRAM ELEMENT                          
*                                                                               
GETNP    NTR1                                                                   
         L     R4,AIO              GET NETWORK/CLASS A PROGRAM                  
         MVI   ELCODE,TANPELQ          DETAILS ELEMENT                          
         USING TANPD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   GNPX                                                             
         L     R1,ANXTNET          ADDRESS OF NETWORK TABLE                     
         USING NETTABD,R1                                                       
*                                                                               
GNP10    MVC   NETDATE,TANPDATE    USE DATE                                     
         MVC   NETNAME,TANPPNME    PROGRAM NAME                                 
         MVC   NETNWK,TANPNWK      NETWORK                                      
         MVC   NETLFT,TANPLFT      LIFT CODE                                    
         BAS   RE,NEXTEL                                                        
         BNE   GNPX                                                             
         LA    R1,NETLEN(R1)       BUMP TABLE                                   
         B     GNP10                                                            
*                                                                               
GNPX     B     XIT                                                              
         DROP  R1,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET CREDIT OF INVOICE ELEMENT                                          
*                                                                               
GETNU    NTR1                                                                   
         L     R4,AIO              GET CREDIT OF INVOICE ELEMENT                
         USING TANUD,R4                                                         
         MVI   ELCODE,TANUELQ                                                   
         MVI   WORK,TANUTINV                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   GNUX                                                             
         L     R4,TGELEM           ADDRESS OF ELEMENT                           
         LA    R2,TBCOM2           EXTRA COMMENT                                
         MVC   TBCOM2(24),=C'CREDIT OF INVOICE NUMBER'                          
         GOTO1 TINVCON,DMCB,TANUMBER,25(R2)                                     
         MVI   31(R2),C'.'                                                      
         OI    STATUS,STCRDT                                                    
*                                                                               
GNUX     B     XIT                                                              
         DROP  R4                                                               
         SPACE 5                                                                
*                                                                               
*        GET INFO FROM TPO RECORD                                               
*                                                                               
         USING SRTINVD,R3                                                       
GETTPO   NTR1                                                                   
         MVC   AIO,AIO2                                                         
         MVC   TBTPO,SRTTPO                                                     
         GOTO1 RECVAL,DMCB,TLOFCDQ,(X'A4',TBTPO)                                
         BE    GTP10                                                            
         MVC   AIO,AIO1                                                         
         MVI   ERRSTAT,TAINEOFF    IF THERE'S NO OFFICE RECORD                  
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     NO                                                               
*                                                                               
GTP10    GOTO1 MVCTST,DMCB,0,(L'TBOFNAME,TBOFNAME),TANAELQ NAME                 
         GOTO1 MVCTST,DMCB,0,(L'TBOFADDR,TBOFADDR),TAADELQ                      
         GOTO1 SQUASHER,DMCB,TBOFADDR,120                                       
*                                                                               
GTPX     MVC   AIO,AIO1                                                         
         B     YES                                                              
         DROP  R3                                                               
         EJECT                                                                  
*        GET AGENCY RECORD AND ALL NECESSARY INFO FROM IT                       
*                                                                               
GETAGY   NTR1                                                                   
         MVC   AIO,AIO2                                                         
         CLC   TGAGY,LAGY          IF SAME AGENCY - DON'T RE-READ               
         BE    GAYX                                                             
*                                                                               
         CLI   OPEN,C'Y'           IF WORKER FILE NOT OPEN                      
         BNE   GAY10                  DON'T CLOSE IT                            
         MVI   SWPOST,POSTPROD                                                  
         GOTO1 =V(TABIPOST),DMCB,(RC),EEOFPOST                                  
*                                      FILE AND CLOSE IT                        
GAY10    XC    IFAYR,IFAYR         CLEAR AGY INTERFACE INFO                     
         XC    LCLI(LINFO-LCLI),LCLI     PREVIOUS INV'S INFO                    
         MVI   LATT,X'FF'             SET TO FORCE NEW ATTN REC                 
         MVI   INTXCLI,C'N'                                                     
         XC    SIGNINFO,SIGNINFO      CLEAR SIGNATORY INFO                      
*                                                                               
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'A4',TGAGY)                                
         BE    *+6                 THERE MUST BE AN AGENCY RECORD               
         DC    H'0'                      IT WAS ALREADY READ                    
*                                                                               
         GOTO1 MVCTST,DMCB,0,(L'TBAYNAME,TBAYNAME),TANAELQ NAME                 
         GOTO1 (RF),(R1),0,(L'TBAYADDR,TBAYADDR),TAADELQ   ADDRESS              
         GOTO1 (RF),(R1),TANUTVND,(L'TBVEN,TBVEN),TANUELQ  VENDOR               
         GOTO1 (RF),(R1),TAFNTATT,(L'TBAYATTN,TBAYATTN),TAFNELQ                 
*                                                                               
         L     R4,AIO              GET AGENCY ELEMENT                           
         USING TAAYD,R4                                                         
         MVI   ELCODE,TAAYELQ                                                   
         BAS   RE,GETEL            MUST BE THERE - WAS JUST READ                
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   TGAGG,TAAYAGG       STORE AGENCY GROUP                           
         MVC   TBAYSTAT,TAAYSTAT   STATUS BYTES                                 
         MVC   TBAYSTA2,TAAYSTA2                                                
         MVC   TBAYSTA3,TAAYSTA3                                                
         MVC   TBAYSTA4,TAAYSTA4                                                
         MVC   TBAYSTA5,TAAYSTA5                                                
         MVC   TBAYSTA6,TAAYSTA6                                                
         MVC   AYSTSIG,TAAYSGNS                                                 
         MVC   TBTPO,TAAYTPOF      TALENT PARTNERS OFFICE CODE                  
         TM    TBOPTS2,TBOVNCPY    IF N'COPIES NOT OVERRIDDEN                   
         BO    *+10                                                             
         MVC   TBAYCOPY,TAAYNBIL   SET NUMBER OF BILLING COPIES                 
         MVI   TBAYDAYS,10         DEFAULT NUMBER OF DAYS DUE                   
         CLI   TAAYDAYS,0                                                       
         BE    *+10                                                             
         MVC   TBAYDAYS,TAAYDAYS   NUMBER OF DAYS DUE                           
         MVC   TBAYLBOX,TAAYLBOX   LOCK BOX CODE                                
         DROP  R4                                                               
*                                                                               
         L     R4,AIO              GET BILLING RULES ELEMENT                    
         USING TABRD,R4                                                         
         MVI   ELCODE,TABRELQ                                                   
         BAS   RE,GETEL                                                         
         B     GAY25                                                            
GAY20    BAS   RE,NEXTEL                                                        
GAY25    BE    GAY30                                                            
         MVI   ERRSTAT,TAINEBRE    NO BILLING RULES ELEMENT                     
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GAY30    TM    TABRSTAT,TABRSACP                                                
         BO    GAY20                                                            
         MVC   AYBTYPE,TABRTYPE    BILLING TYPE                                 
         MVC   AYHRULE,TABRHRLS    HANDLING RULE                                
         MVC   AYSTAT,TABRSTAT     STATUS                                       
         NI    AYSTAT,TABRS75K     ONLY ALLOW CERTAIN STATUS FIELDS             
         MVC   AYRECV,TABRRECV                RECEIVABLE ACCOUNT                
         MVC   AYRFUTA(L'TABRRATE),TABRFUTA   BILLING RATES                     
*                                                                               
         BRAS  RE,ADDLBR           ADDITIONAL BILLING RULES                     
*                                                                               
         GOTOR GETEMSR             GET EMS RATES                                
         BE    GAY33                                                            
         MVI   ERRSTAT,TAINEMS     PROBLEM WITH EMS AGY 8800 RECORD             
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GAY33    GOTO1 BTYPVAL,DMCB,AYBTYPE           GET BILL TYPE INFO                
         BE    GAY35                                                            
         MVI   ERRSTAT,TAINEBTY    UNKNOWN BILLING TYPE                         
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GAY35    MVC   AYTYPO,TGBTYPO                 SET POOL TYPE                     
         TM    TABRSTAT,TABRSSRC              OVERRIDE RATES IF                 
         BNO   GAY40                                                            
         MVC   AYRFUTA(L'TABRRATE),TGBSRC     STANDARD RATE CARD                
*                                                                               
GAY40    TM    TBOPTS,TBRERUN      IF JOB IS A RERUN, SKIP                      
         BO    GAY43                                                            
         GOTOR SAVENBR,DMCB,AAGYTARA SAVE NEW BILLING RATES                     
                                                                                
GAY43    BRAS  RE,SAVESIGN         SAVE SIGNATORY FEE AND CAP                   
*                                                                               
         MVI   INTFACE,C'N'        DEFAULT, NOT ON PROD INTERFACE               
         MVI   CLIINT,C'N'         AND NOT CLIENT LEVEL INTERFACE               
         XC    TGPCLI,TGPCLI                                                    
         XC    IFCLR,IFCLR         CLEAR CLIENT INTERFACE INFO                  
         XC    LPCLI,LPCLI                                                      
         TM    TABRSTAT,TABRSINT+TABRSCIN  IF AGENCY ON INTERFACE               
         BZ    GAYX                                                             
         MVI   INTFACE,C'A'        SET FLAG                                     
         TM    TABRSTAT,TABRSCIN   IF CLIENT LEVEL INTERFACE                    
         BZ    *+8                                                              
         MVI   CLIINT,C'Y'         SET FLAG                                     
         GOTO1 RECVAL,DMCB,TLIFCDQ,(X'24',0)                                    
         BE    GAY50                                                            
         MVI   ERRSTAT,TAINEINT    NO INTERFACE RECORD                          
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
         USING TAIFD,R4            SAVE AGENCY INTERFACE ELEMENT                
GAY50    L     R4,AIO                                                           
         MVI   ELCODE,TAIFELQ                                                   
         BAS   RE,GETEL                                                         
         BE    GAY60                                                            
         MVI   ERRSTAT,TAINEIFE    NO INTERFACE ELEMENT                         
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GAY60    ZIC   R2,1(R4)            LENGTH OF ELEMENT                            
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   IFAYR(0),0(R4)      SAVE INTERFACE ELEMENT                       
         GOTO1 USERVAL,DMCB,(X'80',TAIFAGY)                                     
         BE    *+6                 GET HEXCOMP FOR THIS AGY                     
         DC    H'0'                                                             
         MVI   SWPOST,POSTPROD                                                  
         GOTO1 =V(TABIPOST),DMCB,(RC),EOPENWK                                   
*                                                                               
GAYX     MVC   AIO,AIO1                                                         
         MVC   LAGY,TGAGY          SET LAST AGENCY                              
         B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET CLIENT RECORD AND ALL NECESSARY INFO FROM IT -                     
*                                                                               
GETCLI   NTR1                                                                   
         CLC   LCLI,TGCLI          SAME CLIENT                                  
         BE    GCL320              DON'T RE-READ                                
         MVC   AIO,AIO2                                                         
*                                                                               
GCL10    XC    CLBTYPE(CLBINFO),CLBTYPE  CLIENT INFO                            
         MVI   TBCLSTAT,0                                                       
         MVI   TBCLSTA2,0                                                       
         MVI   LPRD,X'FF'          CLEAR LAST PRODUCT READ                      
         GOTO1 RECVAL,DMCB,TLCLCDQ,(X'A4',TGCLI)                                
         BE    GCL20                                                            
         MVI   ERRSTAT,TAINECLI    NO CLIENT RECORD ERROR                       
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GCL20    GOTO1 MVCTST,DMCB,0,(L'TBCLNAME,TBCLNAME),TANAELQ                      
         GOTO1 (RF),(R1),TAFNTATT,(L'TBCLATTN,TBCLATTN),TAFNELQ                 
         GOTO1 (RF),(R1),0,(L'TBCLADDR,TBCLADDR),TAADELQ                        
*                                                                               
         MVI   INTXCLI,C'N'        INIT CLIENT NOT EXCLUDED                     
         CLI   CLIINT,C'Y'         IF CLIENT LEVEL INTERFACE                    
         BNE   *+8                                                              
         MVI   INTXCLI,C'Y'        INIT CLIENT EXCLUDED                         
*                                                                               
         MVI   CLHRULE,0                                                        
*                                                                               
         L     R4,AIO              GET BILLING RULES ELEMENT                    
         USING TABRD,R4                                                         
         MVI   ELCODE,TABRELQ                                                   
         BAS   RE,GETEL                                                         
         B     GCL26                                                            
GCL25    BAS   RE,NEXTEL                                                        
GCL26    BNE   GCL50                                                            
         TM    TABRSTAT,TABRSACP   EXCLUDING ADDITIONAL BILLING RULES           
         BO    GCL25                                                            
         TM    TABRSTAT,TABRSINT   IF CLIENT ON INTERFACE                       
         BZ    *+8                                                              
         MVI   INTXCLI,C'N'        SET CLIENT NOT EXCLUDED                      
         TM    TABRSTAT,TABRSNIN   IF CLIENT EXCLUDED FROM INTERFACE            
         BNO   GCL30                                                            
         MVI   INTXCLI,C'Y'        SET FLAG                                     
*                                                                               
GCL30    MVC   CLBTYPE,TABRTYPE     BILLING TYPE                                
         MVC   CLHRULE,TABRHRLS    HANDLING RULE                                
         MVC   CLSTAT,TABRSTAT      STATUS                                      
         NI    CLSTAT,TABRS75K      ONLY ALLOW CERTAIN STATUS FIELDS            
         MVC   CLRECV,TABRRECV               RECEIVABLE ACCOUNT                 
         MVC   CLRFUTA(L'TABRRATE),TABRFUTA  BILLING RATES                      
*                                                                               
         CLI   CLBTYPE,0                                                        
         BE    GCL40                                                            
         GOTO1 BTYPVAL,DMCB,CLBTYPE                                             
         BE    GCL40                                                            
         MVI   ERRSTAT,TAINEBTY    UNKNOWN BILLING TYPE                         
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GCL40    MVC   CLTYPO,TGBTYPO                 SET POOL TYPE                     
         TM    TABRSTAT,TABRSSRC             STANDARD RATE CARD                 
         BNO   GCL50                                                            
         MVC   CLRFUTA(L'TABRRATE),TGBSRC                                       
*                                                                               
GCL50    BRAS  RE,ADDLBR           ADDITIONAL BILLING RULES                     
*                                                                               
         GOTOR STEMSRC                      STORE EMS CLIENT RATES              
         BE    GCL80                        EMS DEFAULTS TO AGY 8800            
*                                                                               
**L50    MVC   GRFUTA(L'TABRRATE),CLRFUTA   DEFAULT TO CLIENT INFO              
*NO-OP   OC    CLRFUTA(L'TABRRATE),CLRFUTA  IS THERE CLIENT OVERRIDE            
*02/03   BNZ   GCL70                                                            
**       MVC   GRFUTA(L'TABRRATE),AYRFUTA   NO - USE AGENCY                     
*                                                                               
GCL70    MVC   GTYPE,CLBTYPE                SET GENERAL BILLING TYPE            
         MVC   GRFUTA(L'TABRRATE),CLRFUTA   SET GENERAL BILLING RATES           
         MVC   GSTAT,CLSTAT                 SET GENERAL STATUS                  
         MVC   GTYPO,CLTYPO                 SET GENERAL POOL TYPE               
         CLI   GTYPE,0                                                          
         BNE   GCL80                                                            
         MVC   GTYPE,AYBTYPE                IF NO INPUT ON CLIENT LEVEL         
         MVC   GRFUTA(L'TABRRATE),AYRFUTA                                       
         MVC   GSTAT,AYSTAT                 THEN USE AGENCY LEVEL INFO          
         MVC   GTYPO,AYTYPO                                                     
*                                                                               
GCL80    DS    0H                                                               
GCL90    MVC   GRHRULE,CLHRULE     SET GENERAL HANDLING RULE                    
         CLI   GRHRULE,0           USE CLIENT'S HANDLING RULE                   
         BNE   GCL100              UNLESS NO BILLING TYPE TOO                   
         MVC   GRHRULE,AYHRULE                                                  
GCL100   MVC   GRECV,CLRECV        RECEIVABLE ACCOUNT                           
         OC    GRECV,GRECV                                                      
         BNZ   GCL200                                                           
         MVC   GRECV,AYRECV                                                     
*                                                                               
GCL200   L     R4,AIO              GET CLIENT INFORMATION ELEMENT               
         USING TACID,R4                                                         
         MVI   ELCODE,TACIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GCL300                                                           
         MVC   TBCLSTAT,TACISTAT   STATUS BYTE                                  
         MVC   TBCLSTA2,TACISTA2   STATUS BYTE 2                                
         DROP  R4                                                               
*                                                                               
GCL300   LA    RF,TBILLD                                                        
         AH    RF,=Y(TBOPTS3-TBILLD)  RF -> TBOPTS3                             
         TM    0(RF),TBNOBOVR      SKIPPING BOVER FOR COMPARE                   
         BO    GCL310                                                           
         BRAS  RE,CHKBTYP          CHECK BILLING TYPE                           
         BE    GCL310                                                           
         MVI   ERRSTAT,TAINEBOM    BILLING TYPE MISMATCH                        
         BAS   RE,SETERR                                                        
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GCL310   TM    TBOPTS,TBRERUN      IF JOB IS A RERUN, SKIP                      
         BO    GCLI313                                                          
         GOTOR SAVENBR,DMCB,ACLITARA  SAVE NEW BILLING RATES                    
GCLI313  BRAS  RE,GETGHRL          GET APPL GRT HANDLING RULE                   
*                                                                               
GCL320   TM    STATUS,STCRDT       CREDIT INVOICE?                              
         BNO   GCL330                                                           
         TM    TBIPST2,TAPDPCIB    IF CREDIT INVOICE HAS SAVED BRATES           
         JZ    GCL330                                                           
         GOTOR SETCBR,DMCB,ACURTARA,('CREDITBR',0)  SET CURR BILL RATES         
         B     GCLX                                                             
GCL330   GOTOR SETCBR,DMCB,ACURTARA,0   SET CURRENT BILLING RATES               
*                                                                               
GCLX     GOTOR SETBVST,DMCB,ACURTARA    SET BOVER STATUS                        
         MVC   AIO,AIO1                                                         
         MVC   LCLI,TGCLI          SET LAST CLIENT READ                         
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        GET PRODUCT RECORD AND ALL NECESSARY INFO FROM IT                      
*                                                                               
GETPRD   NTR1                                                                   
         MVC   AIO,AIO2                                                         
         CLC   LPRD,TBPRD          SAME PRODUCT                                 
         BE    GPRX                                                             
*                                                                               
         MVI   TBPRSTAT,0                                                       
         MVC   TBPRADDR,SPACES                                                  
         MVC   TBPRATTN,SPACES                                                  
         CLC   TBPRD,SPACES        IS THERE A PRODUCT                           
         BNH   GPRX                                                             
         GOTO1 RECVAL,DMCB,TLPRCDQ,(X'A4',TBPRD)                                
         BE    GPR10                                                            
         MVI   ERRSTAT,TAINEPRD    NO PRODUCT RECORD ERROR                      
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GPR10    GOTO1 MVCTST,DMCB,0,(L'TBPRNAME,TBPRNAME),TANAELQ                      
         GOTO1 (RF),(R1),0,(L'TBPRADDR,TBPRADDR),TAADELQ                        
         GOTO1 (RF),(R1),TAFNTATT,(L'TBPRATTN,TBPRATTN),TAFNELQ                 
*                                                                               
         L     R4,AIO              GET PRODUCT INFORMATION ELEMENT              
         USING TAPID,R4                                                         
         MVI   ELCODE,TAPIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GPRX                                                             
         MVC   TBPRSTAT,TAPISTAT   STATUS BYTE                                  
         DROP  R4                                                               
*                                                                               
GPRX     MVC   AIO,AIO1                                                         
         MVC   LPRD,TBPRD          SET LAST PRODUCT READ                        
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        GET ATTENTION RECORD - ATTENTION NAME/OVERRIDE ADDRESS                 
*                                                                               
GETATT   NTR1                                                                   
         MVC   AIO,AIO2                                                         
         CLC   LATT,TBATT          SAME ATT CODE                                
         BE    GATX                                                             
*                                                                               
         MVC   TBATADDR,SPACES                                                  
         MVC   TBATNAME,SPACES                                                  
         CLC   TBATT,SPACES        WAS THERE AN ATTENTION CODE IN COMML         
         BH    GAT05               NO RESTORE OLD AGENCY NAME                   
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'A4',TGAGY)                                
         BE    *+6                 THERE MUST BE AN AGENCY RECORD               
         DC    H'0'                      IT WAS ALREADY READ                    
         GOTO1 MVCTST,DMCB,0,(L'TBAYNAME,TBAYNAME),TANAELQ                      
         B     GATX                                                             
*                                                                               
GAT05    GOTO1 RECVAL,DMCB,TLATCDQ,(X'A4',TBATT)                                
         BE    GAT10                                                            
         MVI   ERRSTAT,TAINEATT    NO ATTENTION RECORD                          
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GAT10    GOTO1 MVCTST,DMCB,0,(L'TBATADDR,TBATADDR),TAADELQ                      
         GOTO1 (RF),(R1),(X'80',0),(L'TBAYNAME,TBAYNAME),TANAELQ                
         GOTO1 (RF),(R1),TAFNTATT,(L'TBATNAME,TBATNAME),TAFNELQ                 
*                                                                               
GATX     MVC   AIO,AIO1                                                         
         MVC   LATT,TBATT          SET ATT CODE                                 
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        GET EMPLOYER RECORD - EMPLOYER OF RECORD NAME                          
*                                                                               
GETEMP   NTR1                                                                   
         MVC   AIO,AEMPIO                                                       
         CLC   LEOR,TGEMP          SAME EMP OF RECORD                           
         BE    GEMX                DON'T RE-READ                                
*                                                                               
         GOTO1 RECVAL,DMCB,TLEMCDQ,(X'A4',TGEMP)                                
         BE    GEM10                                                            
         MVI   ERRSTAT,TAINEEMP    NO EMPLOYER RECORD                           
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         MVC   AIO,AIO1                                                         
         B     NO                                                               
*                                                                               
GEM10    GOTO1 MVCTST,DMCB,0,(L'TBEMNAME,TBEMNAME),TANAELQ                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTGST                                  
         MVC   TBEMGST#,TGNAME     GST NUMBER                                   
         GOTO1 MVCTST,DMCB,0,(L'TBEMADDR,TBEMADDR),TAADELQ                      
         GOTO1 SQUASHER,DMCB,TBEMADDR,120                                       
*                                                                               
         XC    DEFSTATE,DEFSTATE   CLEAR DEFAULT TAXABLE STATE                  
         L     R4,AIO                                                           
         MVI   ELCODE,TAEDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GEMX                                                             
         USING TAEDD,R4                                                         
         MVC   DEFSTATE(2),TAEDDST USE DEFAULT TAXABLE STATE                    
         MVI   DEFSTATE+2,C' '                                                  
*                                                                               
GEMX     MVC   AIO,AIO1                                                         
         MVC   LEOR,TGEMP          SET EMP OF RECORD                            
         B     YES                                                              
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
*        ROUTINE TO BUILD A TABLE OF ALL CHECK RECORDS                          
*                                                                               
         SPACE 1                                                                
BLDPERF  NTR1                                                                   
         XC    PRCOLL(2),PRCOLL    CLEAR PRINTING FLAGS                         
         XC    CKREC(6),CKREC      CLEAR THE NUMBER OF PERFORMERS               
         MVC   TIFAGY,TGAGY        SET SYSIO FILTERS                            
         MVC   TIFINV,TGINV        FILTER ON UN-COMPLEMENTED INVOICE            
         XC    TIFINV(L'TGINV),COMPLM     NUMBER                                
         XC    TIFUN,TIFUN                                                      
         OI    TIQFLAG2,TIQFPRI    READ PRIMARY CHECKS                          
         OI    TIQFLAG2,TIQFSUB    AND SUBSIDIARY CHECKS                        
         XC    TIFLOCL,TIFLOCL                                                  
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY SET                  
         BNE   BLD10                                                            
         MVC   TIFUN,CUNION               UNION                                 
         CLI   UNIFILT,C'N'        IF REQUESTING NON-TURNAROUNDS                
         BNE   BLD05                                                            
         CLC   =C'AFT',CUNION      FOR UNION AFTRA                              
         BE    BLD10                                                            
         CLC   =C'SAG',CUNION      & UNION SAG                                  
         BE    BLD10               DON'T SET LOCAL FILTER                       
*                                                                               
BLD05    MVC   TIFLOCL,CLOCAL      ELSE SET LOCAL                               
*                                                                               
BLD10    MVC   TIQRSTR,TBRUNSTR           RUN START DATE                        
         MVC   TIQREND,TBRUNEND           RUN END DATE                          
         MVI   TIREAD,TLCKCDQ             CHECK RECORDS                         
         LA    R1,IOHOOK                                                        
         ST    R1,TIHOOK                                                        
         CLC   TGEMP,=C'P+ '       IF EMPLOYER IS P+                            
         BNE   BLD15                                                            
         TM    TGSYSTA2,TASYSEEN   AND SAVING NAME ORDER ON EMP/EVTIME          
         BZ    BLD15                                                            
         MVI   TIREAD,TLCANCDQ            CAST RECORD BY NAME                   
         MVI   TISUBRD,TLCKCDQ            CHECK RECORDS                         
         MVC   TIFCOM,TGCOM               INTERNAL COMM NUMBER                  
*                                                                               
BLD15    XC    TIQSTART,TIQSTART                                                
         GOTO1 TASYSIO,DMCB,TASYSIOD                                            
         SR    R1,R1                                                            
         ICM   R1,3,CKREC          WERE THERE ANY CHECKS                        
         BZ    NO                     NO - SKIP THE INVOICE                     
         CLI   ERRSTAT,0           CHK ERROR STATUS FOR THIS INVOICE            
         BNE   NO                  IF IN ERROR - EXIT                           
         TM    PRCOLL,CPNH         MAKE SURE PNH IS IN LEFT COLUMN              
         BO    BLD20                                                            
         TM    PRCOLR,CPNH         IF THERE IS PNH IN RIGHT COLUMN              
         BNO   BLD20                                                            
         MVC   PRCOLR,PRCOLL                                                    
         MVI   PRCOLL,CPNH                                                      
*                                                                               
BLD20    CLI   UNICOPY,C'Y'        IF UNION COPY - DON'T CHECK AMTS             
         BE    YES                                                              
*                                                                               
         BAS   RE,FIXEURO          MAY NEED TO ADJUST EURO ROUNDING             
         BAS   RE,COMPAMT          COMPARE INV AMTS TO CHECK TOTALS             
         BE    YES                 IF OK - EXIT                                 
         MVI   ERRSTAT,TAINEAMT    AMOUNTS ON INVOICE/CHECKS IN ERROR           
         BAS   RE,SETERR           SET INVOICE STATUS IN ERROR                  
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        HOOK TO SYSIO                                                          
*                                                                               
         SPACE 1                                                                
IOHOOK   NTR1                                                                   
         CLI   TIMODE,PROCREC      RECORD GOTTEN FROM SYSIO                     
         BNE   IOX                                                              
         CLI   TIREAD,TLCANCDQ     ARE THESE CAST RECORDS BY NAME?              
         BE    *+12                                                             
         CLI   TIREAD,TLCKCDQ      THESE CHECK RECORDS?                         
         BNE   IOX                                                              
         CLI   ERRSTAT,0           CHK ERROR STATUS FOR THIS INVOICE            
         BE    PRCKREC             IF OK - CONTINUE                             
*                                                                               
IOX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*         PROCESS CHECK RECORDS                                                 
*         STORE ALL RELEVANT INFO IN TABLE - COUNTING NUMBER OF RECORDS         
*               TO BE ABLE TO CALCULATE PAGE X OF N                             
*                                                                               
         USING TABCHKD,R3                                                       
PRCKREC  XC    TFULL,TFULL         PAYMENT AMOUNT FOR THIS PERFORMER            
         L     R3,ANXTCHK          CHECK TABLE                                  
         MVC   TABDA,TIDSKADD      DISK ADDRESS                                 
         L     R4,TIAREC           CHECK RECORD                                 
         USING TLCKD,R4            GET INFO FROM KEY FIRST                      
         MVC   TABSSN,TLCKSSN      S/S NUMBER                                   
         MVC   TABCAT,TLCKCAT      CATEGORY                                     
         MVC   TABSEQ,TLCKSORT+4   SEQUENCE NUMBER                              
         DROP  R4                                                               
*                                                                               
PCK10    MVI   BYTE,X'A0'                                                       
         TM    TBOPTS,TBTRACE      TRACE GUARANTEES TOO                         
         BNO   PCK20                                                            
         MVI   BYTE,X'E0'                                                       
*                                                                               
PCK20    GOTOR GUARCK,DMCB,(RC),(BYTE,TIAREC),(X'40',AIO1),SYSCOMM              
         MVC   TABGSTAT,0(R1)      SET ERROR FROM TAPPGUAR                      
         BE    PCK40                                                            
         BRAS  RE,FIRSTCYC         IF APPLY DATE IS WITHIN PRIMARY              
         BE    PCK40               COMM'LS FIRST CYCLE, ERROR MAY BE OK         
         TM    TBOPTS,TBRERUN      IF THIS IS A RERUN                           
         BO    PCK40                   IGNORE ERRORS                            
         CLI   TABGSTAT,128        ALL WARNING NUMBERS >= 128                   
         BL    PCK30                                                            
         MVC   ERRSTAT,TABGSTAT                                                 
         GOTO1 =A(PUTERR),DMCB,(RC) PUT INVOICE IN TABLE                        
         XC    ERRSTAT,ERRSTAT                                                  
         CLI   TABGSTAT,TAINWGDU   IF THIS PARTICULAR WARNING                   
         BE    PCK22                                                            
         CLI   TABGSTAT,TAINWGCN                                                
         BNE   PCK25                                                            
PCK22    MVI   TABGSTAT,X'FE'      SIMULATE UPDATE RETURN CODE                  
         B     PCK40                                                            
PCK25    MVI   TABGSTAT,X'FF'      ELSE SIMULATE NO CHANGES MADE                
         B     PCK40               AND CONTINUE TO PROCESS THE INVOICE          
*                                                                               
PCK30    MVC   ERRSTAT,TABGSTAT    ERROR FROM TAPPGUAR                          
         BAS   RE,SETERR                                                        
         B     PCKX                                                             
*                                                                               
PCK40    MVC   AIO,AIO1                                                         
         USING TAPDD,R4                                                         
         GOTOR GETPDEL,DMCB,AIO    GET PAYMENT DETAIL ELEMENT                   
         BE    PCK50               FROM INVOICE RECORD AFTER BEING              
         MVI   ERRSTAT,TAINEPDC    UPDATED BY TAPPGUAR                          
         BAS   RE,SETERR           NO PAYMENT DETAILS ELEMENT                   
         B     PCKX                SET INVOICE IN ERROR                         
*                                                                               
PCK50    MVC   TGROSS(PDAMTS),TAPDGRS                                           
         XC    TACOMM,TACOMM                                                    
         XC    TASIGN(8),TASIGN                                                 
         DROP  R4                                                               
*                                                                               
         CLI   RECNUM,AC           IF NOT RUNNING AGENCY COPY                   
         JE    PCK51                                                            
         GOTOR CVTEPDU,DMCB,TIAREC CONVERT EURO PAYMENT DETAILS TO US$          
*                                                                               
PCK51    GOTOR BLDCNPD,DMCB,TIAREC BUILD CHECK'S CAN$ PAYMENT DETAILS           
         JNE   PCK52               ELEMENT                                      
         BRAS  RE,UPDCNPD          AND ADD TO INVOICE'S CAN$ TOTALS             
*                                                                               
PCK52    BRAS  RE,GETCCD           GET CHECK DETAILS ELEMENT                    
         BNE   PCKX                                                             
         BAS   RE,GETCW4           GET W4 REC DTLS - SAVE YTD SOAP SPNH         
         BNE   PCKX                                                             
         BAS   RE,GETCCA           GET CAST DETAILS ELEMENT                     
         BNE   PCKX                                                             
         BAS   RE,GETCPD           GET PAYMENT DETAILS ELEMENT                  
         BNE   PCKX                                                             
         BAS   RE,GETCSD           GET SESSION DETAILS ELEMENT                  
         BAS   RE,GETCTI           GET TAX ID ELEMENT                           
         BNE   PCKX                                                             
         BAS   RE,GETCBY           GET TABY ELEMENT                             
         BAS   RE,GETCYE           GET TAYE ELEMENT                             
         BRAS  RE,GETCNU           GET TANU ELEMENT (P+ ADV. RECON.)            
         BRAS  RE,GETCTU           GET TATU ELEMENT (P+ ADV. RECON.)            
         CLI   TBPRTINV,C'Y'       IF THIS IS A PRINT INVOICE                   
         BNE   PCK53                                                            
         BAS   RE,GETCDL           GET DEAL ELEMENT                             
         BAS   RE,GETTANUT         CHECK AGENT CHECK                            
         B     PCK55                                                            
PCK53    CLC   TABSSN,=C'000006816' ELSE IF CHECK FOR ACTRAWORKS                
         BNE   *+8                                                              
         OI    TABSTAT2,TABSTACT    SET STATUS BIT                              
*                                                                               
**K55    TM    TGSYSTAT,TASYSAOS                                                
**       BZ    PCK57                                                            
PCK55    BRAS  RE,AOSSSN           CHECK IF SSN FOR AOS FBS                     
         BNE   PCK57                                                            
         OI    TABSTAT2,TABSTAOS   SET STATUS BIT                               
         BRAS  RE,GETMAD           GET MISC AMOUNT ELEMENT FOR AOS              
*                                                                               
PCK57    BRAS  RE,PNHUNI           ADD PNH AMOUNT TO CORRECT UNION              
         LH    R1,CKREC            COUNT NUMBER OF RECORDS FOR LOOP             
         LA    R1,1(R1)                                                         
         STH   R1,CKREC                                                         
         LA    R3,TABLEN(R3)       BUMP CHECK TABLE                             
         ST    R3,ANXTCHK                                                       
*                                                                               
PCKX     MVC   KEY,TIKEY           RESTORE SYSIO'S READ SEQUENCE                
         GOTO1 HIGH                                                             
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        GET CAST ELEMENT                                                       
*                                                                               
         USING TACAD,R4            CAST DETAILS ELEM                            
         USING TABCHKD,R3                                                       
GETCCA   NTR1                                                                   
         L     R4,TIAREC                                                        
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BE    GCCA10                                                           
         MVI   ERRSTAT,TAINECST    NO CAST DETAILS ELEMENT                      
         BAS   RE,SETERR           SET INVOICE IN ERROR                         
         B     NO                                                               
*                                                                               
GCCA10   MVC   TABUNI,TACAUN       UNION                                        
         MVC   TABLOCL,TACALOCL    LOCAL                                        
         MVC   TABFSERV,TACAFRST   1ST SERVICES DATE                            
         MVC   TABCAEXP,TACAEXP    EXPIRATION DATE                              
         MVC   TABAGNT,TACANCDE    AGENT CODE                                   
         MVC   TABSTATE,TACAUNIT   STATE UNIT CODE                              
         MVC   TABTAXST,TACAUNIT   TAXABLE STATE                                
*                                  8/16/99 - BUG TABW4TY NOT SET YET            
         CLI   TABW4TY,TAW4TYCO            - NOT FIXING BECAUSE MUST            
         BE    GCCA40                      - ALWAYS GO THROUGH                  
         CLI   TABW4TY,TAW4TYCA            - SETTAXBL                           
         BE    GCCA40                                                           
         CLI   TABW4TY,TAW4TYFO                                                 
         BE    GCCA40                                                           
         BAS   RE,SETTAXBL         SET TAXABLE STATE                            
*                                                                               
GCCA40   MVC   TABDBL,TACADBL      NUMBER OF DOUBLES                            
         MVC   TABOV2,TACAOV2      2ND OVERSCALE %                              
         OC    TABOV2,TABOV2       NEED EXTRA LINE FOR 2ND OV SCALE %           
         BZ    GCCA50                                                           
         LH    R1,CKREC2                                                        
         LA    R1,1(R1)                                                         
         STH   R1,CKREC2                                                        
         MVI   TABXLN,C'Y'         SET EXTRA LINE NEEDED                        
*                                                                               
GCCA50   TM    TACASTAT,TACASTLF   ON LIFT                                      
         BNO   GCCA60                                                           
         MVI   TABLIFT,C'Y'                                                     
*                                                                               
GCCA60   TM    TACASTAT,TACASTLO   ONLY ON LIFT                                 
         BNO   GCCA70                                                           
         MVI   TABLIFT,C'O'                                                     
*                                                                               
GCCA70   MVC   TABONOFF,TACAONOF   ON OFF CAMERA                                
         MVC   TABCAST2,TACASTA2   2ND STATUS BYTE                              
         MVC   TABYR,TACAYEAR      YEAR                                         
         MVC   TABCNUM,TACACORP    CORPORATION NUMBER                           
         MVC   TABGRTCD,TACAGUA    GUARANTEE CODE                               
         CLI   TBPRTINV,C'Y'       IF THIS IS A PRINT INVOICE                   
         BNE   GCCAX               SET                                          
         MVC   TABSDATE,TACASDTE      SHOOT DATE                                
         MVC   TABPAYEE,TACAPAYE      PAYEE CODE                                
         MVC   TABARATE,TACARATE      AGENT COMMISSION RATE                     
*                                                                               
GCCAX    B     YES                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET PAYMENT DETAILS ELEMENT                                            
*                                                                               
         USING TABCHKD,R3                                                       
         USING TAPDD,R4                                                         
GETCPD   NTR1                                                                   
         GOTOR GETPDEL,DMCB,TIAREC GET PAYMENT DETAILS ELEMENT                  
         JE    *+6                                                              
         DC    H'00'                                                            
*                                                                               
         MVC   TABSTAT,TAPDSTAT    STATUS BYTE                                  
         MVC   TABACDE,TAPDACDE    APPLIED CODE                                 
         MVC   TABICDE,TAPDICDE    INCLUDE CODE                                 
*                                                                               
         GOTOR ITN                 IF ITN USE CALCULATE USE RANGE               
         BE    GCPD10                                                           
*                                                                               
         TM    TGUSSTA3,NWKUSE     OR NETWORK USAGE (CLA,LNA,LNN,LNC)           
         BNO   GCPD5                                                            
         MVC   TABSTUS,TAPDSTUS    STARTING USE CODE                            
         MVC   TABUSES,TAPDUSES    NUMBER OF USES                               
*                                                                               
         CLI   TGUSEQU,UCLA        IF USE IS CLASS A                            
         BNE   GCPD5                                                            
         CLI   TABYR,C'8'          CAST YEAR IS 2000 OR GREATER                 
         BE    GCPD10                                                           
         CLI   TABYR,C'9'                                                       
         BE    GCPD10                                                           
         TM    TAPDOPT2,TAPDOPTU   AND USAGE HISTORY IS NOT OVERRIDDEN          
         BO    GCPD4                                                            
         DROP  R4                                                               
*                                                                               
         SR    R1,R1               CLEAR PAX PROGRAM COUNTER                    
*                                                                               
         L     R4,AIO1             R4=A(INVOICE RECORD)                         
         USING TANPD,R4                                                         
         MVI   ELCODE,TANPELQ      GET NETWORK/CLASS A PRO DET ELEMS            
         BAS   RE,GETEL                                                         
         B     *+8                                                              
GCPD3    BAS   RE,NEXTEL                                                        
         BNE   GCPD3AB                                                          
         CLI   TANPNWK,C'X'        REJECT ALL NON-PAX PROGRAMS                  
         BNE   GCPD3                                                            
*                                                                               
         CLI   TANPLFT,C'Y'        IF LIFT PROGRAM                              
         BNE   GCPD3A                                                           
         CLI   TABLIFT,C'Y'        ONLY COUNT IF PERFORMER IS                   
         BE    GCPD3AA             ON LIFT                                      
         CLI   TABLIFT,C'O'                                                     
         BNE   GCPD3                                                            
         B     GCPD3AA                                                          
*                                                                               
GCPD3A   CLI   TABLIFT,C'O'        IF NOT A LIFT PROGRAM DO NOT COUNT           
         BE    GCPD3               IF PERFORMER ONLY ON LIFT                    
*                                                                               
GCPD3AA  AHI   R1,1                ADD ONE TO PAX PROGRAM COUNTER               
         B     GCPD3               AND GET NEXT PROGRAM                         
*                                                                               
GCPD3AB  LNR   R1,R1               WHEN ALL PROGRAMS READ                       
         AH    R1,TABUSES          SUBTRACT NUMBER OF PAX PROGRAMS              
         STH   R1,TABUSES          FROM TOTAL NUMBER OF PROGRAMS                
*                                                                               
         XC    HALF,HALF                                                        
         TM    STAT2,STAVERS       IF INVOICE IS A VERSION PAYMENT              
         BZ    GCPD3AC             PUT SEQUENCE NUMBER IN HALF                  
         MVC   HALF,TABSEQ                                                      
         B     GCPD3AD                                                          
*                                                                               
GCPD3AC  MVC   AIO,AIO2            IF INVOICE IS NOT A VERSION PAYMENT          
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING TLCOPD,R4                                                        
         MVI   TLCOPCD,TLCOCCDQ    READ COMMERCIAL RECORD                       
         MVC   TLCOCCOM,TGCOM                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(TLCOCCOM+L'TLCOCCOM-TLCOPD),KEYSAVE                          
         BNE   GCPD3AD                                                          
         GOTO1 GETREC                                                           
         GOTOR GETLF               AND SAVE LIFT INFORMATION                    
*                                                                               
GCPD3AD  MVC   AIO,AIO2            NOW MUST GET THE USAGE HISTORY               
         LA    R4,KEY              RECORD INTO AIO2                             
         XC    KEY,KEY                                                          
         USING TLUHD,R4                                                         
         MVI   TLUHCD,TLUHCDQ      RECORD CODE                                  
         MVC   TLUHCOM,TGCOM       INTERNAL COMMERCIAL NUMBER                   
         MVC   TLUHCSEQ,HALF       CAST SEQUENCE NUMBER                         
         MVC   TLUHUSE,TGUSCDE     USE CODE                                     
         MVC   TLUHINV,TGINV       COMPLIMENTED INVOICE NUMBER                  
         OI    DMINBTS,X'08'                                                    
         GOTO1 HIGH                                                             
         CLC   TLUHKEY(TLUHINV+L'TLUHINV-TLUHD),KEYSAVE                         
         BNE   GCPD4                                                            
         GOTO1 GETREC                                                           
         NI    DMINBTS,X'FF'-X'08'                                              
*                                                                               
         L     R4,AIO              R4=A(USAGE HISTORY RECORD)                   
         USING TAUHD,R4                                                         
         MVI   ELCODE,TAUHELQ      GET USAGE HISTORY ELEMENT                    
         BAS   RE,GETEL                                                         
         BNE   GCPD3B                                                           
         LH    RE,TAUHUSN          DECREMENT LAST USE NUMBER                    
         SH    RE,TAUHUSNP         BY # OF PAX PROS EX FROM CLA PREV            
         SH    RE,TABUSES          BY # OF USES ON THIS PAYMENT                 
*                                                                               
         TM    STAT2,STAVERS       IF COMMERCIAL HAS A LIFT                     
         BO    GCPD3C                                                           
         CLI   TBLSEC,0                                                         
         BE    GCPD3C                                                           
         CLI   TABLIFT,C'O'        AND PERFORMER IS NOT ON IT                   
         BE    GCPD3B                                                           
         CLI   TABLIFT,C'Y'                                                     
         BE    GCPD3C                                                           
         LH    RE,TAUHUSN          SUBTRACT NUMEBER OF LIFT USES                
         SH    RE,TAUHUSNL                                                      
         SH    RE,TAUHUSNP                                                      
         AH    RE,TAUHLUSP                                                      
         SH    RE,TABUSES                                                       
         B     GCPD3C              IF PERFORMER ONLY ON LIFT                    
GCPD3B   LH    RE,TAUHUSNL         DECREMENT LAST LIFT USE NUMBER               
         SH    RE,TAUHLUSP         BY # LIFT PAX PAYS EX FROM CLA               
         SH    RE,TABUSES          BY # OF USES ON THIS PAYMENT                 
*                                                                               
GCPD3C   AHI   RE,1                ADJUST FOR STARTING USE NUMBER               
         STH   RE,TABSTUS          AND STORE IN TABSTUS                         
*                                                                               
         TM    STAT2,STAVERS       RESET LIFT INFORMATION                       
         BO    GCPD4               TO WHAT'S ON INVOICE                         
         MVC   AIO,TIAREC                                                       
         GOTOR GETLF                                                            
*                                                                               
GCPD4    MVC   AIO,AIO1                                                         
                                                                                
         USING TAPDD,R4                                                         
         GOTOR GETPDEL,DMCB,TIAREC GET PAYMENT DETAILS ELEMENT                  
         JE    GCPD10                                                           
         DC    H'00'                                                            
                                                                                
GCPD5    MVC   TABUNITS,TAPDUNIT   ELSE UNITS                                   
         MVC   TABMAJ,TAPDMAJ      AND MAJORS                                   
*                                                                               
GCPD10   CLI   TABACDE,APPLSESS    IF APPLIED CODE FOR SESSION                  
         BNE   *+8                                                              
         OI    STAT2,STAPPLS       SET BIT                                      
         CLI   TABACDE,APPLHLD     IF APPLIED CODE FOR HOLD                     
         BNE   *+8                                                              
         OI    STAT2,STAPPLH       SET BIT                                      
*                                                                               
         MVC   TABW4TY,TAPDW4TY    W4 TYPE                                      
         CLI   TABW4TY,TAW4TYCO    IF CORPORATION                               
         BE    GCPD20                                                           
         CLI   TABW4TY,TAW4TYCA    OR CANADIAN                                  
         BE    GCPD20                                                           
         CLI   TABW4TY,TAW4TYFO    OR FOREIGNER                                 
         BNE   *+8                                                              
GCPD20   OI    STAT2,STCORP        SET CORP ON INVOICE                          
*                                                                               
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BE    *+14                                                             
         CLC   TABUNI,=C'AFT'      ONLY IF UNION IS AFT                         
         BNE   GCPD22                                                           
         GOTO1 =A(ADJPNH),DMCB,(RC),(R4)  ADJUST P&H IF NEEDED                  
*                                                                               
GCPD22   MVC   TABOV1,TAPDOV1      OVERSCALE                                    
         MVC   TABGRS(TABPDL),TAPDGRS   ALL PAYMENT DEATIL AMOUNTS              
*                                                                               
         CLC   TAPDUSE,=C'EVE'     IF P+ BILLING                                
         BNE   GCPD23                                                           
         CLI   TAPDLEN,TAPDLNQ                                                  
         BL    GCPD23                                                           
         MVC   TABTXNW,TAPDTXNW    TAXABLE NON WAGES                            
         MVC   TABNTNW,TAPDNTNW    NON TAXABLE NON WAGES                        
*                                                                               
GCPD23   TM    TAPDSTA3,TAPDSGCU                                                
         BZ    *+8                                                              
         OI    TABSTAT2,TABSTGCU   SET GCON UPDATED STATUS                      
*                                                                               
         TM    TAPDSTA3,TAPDSNR1                                                
         BZ    *+8                                                              
         OI    TABSTAT2,TABSTSN1   PAID FIRST NON-RENDERED VERSION              
*                                                                               
         OC    TABPNH,TABPNH       IF THIS PERSON HAS P&H SET FLAG              
         BZ    GCPD30                                                           
         TM    PRCOLL,CPNH         CHECK IF FLAG ALREADY SET                    
         BO    GCPD30                                                           
         TM    PRCOLR,CPNH                                                      
         BO    GCPD30                                                           
         CLI   PRCOLL,0            & THERE'S NOTHING IN LEFT COLUMN             
         BNE   GCPD25                                                           
         OI    PRCOLL,CPNH         SET FLAG TO PRINT H&W IN LEFT COLUMN         
         B     GCPD30                                                           
*                                                                               
GCPD25   OI    PRCOLR,CPNH         ELSE SET TO PRINT IT IN RIGHT COLUMN         
*                                                                               
GCPD30   OC    TABHNW,TABHNW       IF THIS PERSON HAS H&W                       
         BZ    GCPD50                                                           
         TM    PRCOLL,CHNW         CHECK IF FLAG ALREADY SET                    
         BO    GCPD50                                                           
         TM    PRCOLR,CHNW                                                      
         BO    GCPD50                                                           
         CLI   PRCOLL,0            & THERE'S NOTHING IN LEFT COLUMN             
         BNE   GCPD40                                                           
         OI    PRCOLL,CHNW         SET FALG TO PRINT H&W IN LEFT COLUMN         
         B     GCPD50                                                           
*                                                                               
GCPD40   OI    PRCOLR,CHNW         ELSE SET TO PRINT IT IN RIGHT COLUMN         
         B     GCPD90                                                           
*                                                                               
GCPD50   OC    TABMDED,TABMDED     IF THIS PERSON HAD MISC DED                  
         BZ    GCPD70                                                           
         CLI   PRCOLL,0            & THERE'S NOTHING IN LEFT COLUMN             
         BNE   GCPD60                                                           
         OI    PRCOLL,CMDED        SET FLAG TO PRINT MDED IN LEFT COL           
         B     GCPD70                                                           
*                                                                               
GCPD60   CLI   PRCOLR,0            ELSE IF THERE'S NOTHING IN RIGHT COL         
         BNE   GCPD90                                                           
         OI    PRCOLR,CMDED        SET TO PRINT IT IN RIGHT COLUMN              
         B     GCPD90                                                           
*                                                                               
GCPD70   OC    TABINR,TABINR       IF THIS PERSON HAD I&R                       
         BZ    GCPD90                                                           
         CLI   PRCOLL,0            & THERE'S NOTHING IN LEFT COLUMN             
         BNE   GCPD80                                                           
         OI    PRCOLL,CINR1        SET FLAG TO PRINT I&R IN LEFT COLUMN         
         OI    PRCOLR,CINR2                                                     
         B     GCPD90                                                           
*                                                                               
GCPD80   CLI   PRCOLR,0            ELSE IF THERE'S NOTHING IN RIGHT COL         
         BNE   GCPD90                                                           
         OI    PRCOLR,CINR2        SET TO PRINT IT IN RIGHT COLUMN              
*                                                                               
GCPD90   CLI   TAPDACDE,APPLWSPU   IF THIS IS A WILDSPOT UPGRADE                
         BE    GCPD95                                                           
         CLI   TAPDACDE,APPLCAB    OR CABLE                                     
         BNE   GCPD100                                                          
*                                                                               
GCPD95   XC    TABAPPL,TABAPPL     IGNORE APPLIED AMOUNT WHEN TOTALING          
*                                                                               
GCPD100  BAS   RE,TOTAMT           CHECK AMOUNTS TO COMPARE TO INVOICE          
         CLI   TAPDACDE,APPLWSPU   IF WILDSPOT UPGRADE                          
         BE    GCPD110                                                          
         CLI   TAPDACDE,APPLCAB    OR CABLE                                     
         BNE   GCPDX                                                            
*                                                                               
GCPD110  MVC   TABAPPL,TAPDAPPL    THEN RESTORE THE AMOUNT                      
*                                                                               
GCPDX    B     YES                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET SESSION DETAILS ELEMENT                                            
*                                                                               
         USING TABCHKD,R3                                                       
         USING TASDD,R4                                                         
GETCSD   NTR1                                                                   
         L     R4,TIAREC                                                        
         MVI   ELCODE,TASDELQ      SESSION DETAILS ELEMENT                      
         BAS   RE,GETEL                                                         
         BNE   GCSDX                                                            
         TM    TGUSSTA2,BSSTYPE    TELEVISION SESSION                           
         BZ    GCSD10                                                           
         MVC   TABSP,TASDSP        N'SPOTS                                      
         MVC   TABDAY,TASDDAY      N'DAYS                                       
         MVC   TABOT,TASDOT        OT HRS                                       
         MVC   TABDT,TASDDT        DOUBLE TIME HOURS                            
         MVC   TABTRV,TASDTRV      TRAVEL TIME HOURS  (2 DEC)                   
         MVC   TABPDW,TASDPDW      PRIOR DAY WARDROBE (2 DEC)                   
         MVC   TABTAG,TASDTAG      N'TAGS                                       
*                                                                               
         CLI   TABTAG,0            IF THERE ARE TAGS                            
         BE    GCSDX                                                            
         CLI   TABXLN,C'Y'         & DIDN'T ALREADY ADD A LINE                  
         BE    GCSDX                                                            
*                                                                               
         OC    TABHRS(TABHRLNQ),TABHRS     & IF THERE ARE EXTRA HRS             
         BZ    GCSDX                                                            
         LH    R1,CKREC2                                                        
         LA    R1,1(R1)                                                         
         STH   R1,CKREC2                                                        
         MVI   TABXLN,C'Y'         THEN NEED AN EXTRA LINE                      
         B     GCSDX                                                            
*                                                                               
GCSD10   CLI   TASDEQU,UBSM        MUSIC SESSION                                
         BE    GCSD15                                                           
         CLI   TASDEQU,UIMS        MUSIC SESSION                                
         BNE   GCSD20                                                           
*                                                                               
GCSD15   MVC   TABSP,TASDMSP       N'SPOTS                                      
         MVC   TABTRV,TASDMHM      HRS/MINUTES                                  
         B     GCSDX                                                            
*                                                                               
GCSD20   CLI   TASDEQU,UBSR        RADIO SESSION                                
         BE    GCSD25                                                           
         CLI   TASDEQU,UADO                                                     
         BE    GCSD25                                                           
         CLI   TASDEQU,URRR                                                     
         BNE   GCSDX                                                            
GCSD25   MVC   TABSP,TASDRSP       N'SPOTS                                      
         MVC   TABTRV,TASDRHM      HRS/MINUTES                                  
         MVC   TABDAY,TASDRTG      N'TAGS                                       
*                                                                               
GCSDX    B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET TAX ID ELEMENT                                                     
*                                                                               
         USING TABCHKD,R3                                                       
         USING TATID,R4                                                         
GETCTI   NTR1                                                                   
         CLI   TABCNUM,C' '        IF THERE IS A CORP                           
         BNH   GCTIX                                                            
         CLI   TABXLN,C'Y'         AND AN EXTRA LINE WASN'T ADDED YET           
         BE    GCTI10                                                           
         LH    R1,CKREC2           ADD IT                                       
         LA    R1,1(R1)                                                         
         STH   R1,CKREC2                                                        
         MVI   TABXLN,C'Y'                                                      
*                                                                               
GCTI10   L     R4,TIAREC                                                        
         MVI   ELCODE,TATIELQ      GET TAX ID ELEM - CORP NUMBER                
         BAS   RE,GETEL                                                         
         BNE   GCTINO                                                           
*                                                                               
GCTI20   CLI   TATITYPE,TATITYCO                                                
         BNE   GCTI30                                                           
         CLC   TATICRPN,TABCNUM    CORRECT CORP NUMBER                          
         BNE   GCTI30                                                           
         MVC   TABCID,TATIID                                                    
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'AC',TABCID),0                             
         BNE   *+10                                                             
         MVC   TABCNAME,TGNAME                                                  
         MVC   AIO,AIO1            RESTORE I/O AREA                             
         B     GCTIX                                                            
*                                                                               
GCTI30   BAS   RE,NEXTEL                                                        
         BE    GCTI20                                                           
         B     GCTINO                                                           
*                                                                               
GCTIX    B     YES                                                              
*                                                                               
GCTINO   MVI   ERRSTAT,TAINETID    NO TAX ID ELEMENT FOR CORP                   
         BAS   RE,SETERR           SET INVOICE IN ERROR                         
         B     NO                                                               
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET TABY ELEMENT FOR RATE LEVEL                                        
*                                                                               
         USING TABCHKD,R3                                                       
         USING TABYD,R4                                                         
GETCBY   NTR1                                                                   
         L     R4,TIAREC                                                        
         MVI   ELCODE,TABYELQ      BILLING YTD ELEMENT                          
         BAS   RE,GETEL                                                         
         BNE   GCBYX                                                            
         MVC   TABLEVL,TABYLVL     SET RATE LEVEL                               
*                                                                               
GCBYX    B     YES                                                              
         DROP  R3,R4                                                            
         SPACE 2                                                                
*                                                                               
*        GET TADL (DEAL) ELEMENT                                                
*                                                                               
         USING TABCHKD,R3                                                       
         USING TADLD,R4                                                         
GETCDL   NTR1                                                                   
         L     R4,TIAREC                                                        
         MVI   ELCODE,TADLELQ      DEAL ELEMENT                                 
         BAS   RE,GETEL                                                         
         BNE   GCDLX                                                            
         MVC   TABEXPDT,TADLEXP    EXPIRATION DATE                              
         MVC   TABPUBDT,TADLPUB    PUB DATE                                     
         OC    TADLRATE,TADLRATE   IF AGENT COMMOSSION RATE IS NOT ZERO         
         BZ    GCDLX                                                            
         MVC   TABARATE,TADLRATE   USE IT                                       
*                                                                               
GCDLX    B     YES                                                              
         DROP  R3,R4                                                            
         SPACE 2                                                                
*                                                                               
*        GET TANUD - CAST SSN CONTRIB TO PRINT AGENT CHK                        
*                                                                               
         USING TABCHKD,R3                                                       
GETTANUT NTR1                                                                   
         MVC   AIO,TIAREC          SET AIO TO CHECK RECORD                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTCST))                                     
         BNE   *+8                                                              
         OI    TABPSTAT,TABPSAGT   PRINT AGENT CHECK                            
         MVC   AIO,AIO1            RESET AIO                                    
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        GET TAYE ELEMENT                                                       
*                                                                               
         USING TABCHKD,R3                                                       
         USING TAYED,R4                                                         
GETCYE   NTR1                                                                   
         L     R2,AIO              SAVE AIO                                     
         MVC   AIO,TIAREC                                                       
         MVI   ELCODE,TAYEELQ      BILL YTD EARNINGS ELEMENT                    
         MVI   WORK,TAYETBIL                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   GCYEX                                                            
         L     R4,TGELEM           ADDRESS OF ELEMENT                           
         MVC   TABXAMT(TAYETLN),TAYETXBL    SET TAXABLE AMOUNTS                 
*                                                                               
GCYEX    ST    R2,AIO              RESTORE AIO                                  
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET W4 RECORD                                                          
*                                                                               
         USING TABCHKD,R3                                                       
GETCW4   NTR1                                                                   
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'AC',TABSSN),0                             
         BE    *+12                                                             
         MVI   ERRSTAT,TAINEW4     NO W4 RECORD                                 
         B     GCW4NO                                                           
         MVC   TABNAME,TGNAME      PERFORMER NAME                               
*                                                                               
         L     R4,AIO              R4 = A(W4 DETAILS ELEMENT)                   
         USING TAW4D,R4                                                         
         MVI   ELCODE,TAW4ELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+12                                                             
         MVI   ERRSTAT,TAINEW4E    NO W4 ELEMENT                                
         B     GCW4NO                                                           
*                                                                               
         MVC   TABRLOCL,TAW4LOCL   RESIDENCE LOCAL                              
*                                                                               
*2/10/06 CLI   TBPRTINV,C'Y'       IF THIS IS NOT A PRINT INVOICE               
*2/10/06 BE    GCW410                                                           
*        CLI   TGCUR,C'U'          BUT IT IS US$                                
*        BNE   GCW410                                                           
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTGST  AND GST# ON W4                  
         BNE   GCW410                                                           
         LA    RE,NOGSTTAB                                                      
GCW404   CLI   0(RE),X'FF'         AND SS# IS NOT ONE OF AUTOMATICS             
         BE    GCW405                                                           
         CLC   TABSSN,0(RE)                                                     
         BE    GCW410                                                           
         LA    RE,L'NOGSTTAB(RE)                                                
         B     GCW404                                                           
GCW405   OI    TABSTAT2,TABSTGST   SET STATUS BIT                               
*                                                                               
GCW410   MVI   ELCODE,TAYSELQ      GET YTD SPNH EL                              
         MVC   FULL,TGTODY20       FOR CORRECT YEAR                             
         XC    YTDSSPNH,YTDSSPNH   PRE-CLEAR IN CASE NO EL FOUND                
         XC    YTDWSPNH,YTDWSPNH   PRE-CLEAR IN CASE NO EL FOUND                
         GOTO1 GETL,DMCB,(4,FULL)                                               
         BNE   GCW419                                                           
         L     R4,TGELEM                                                        
         USING TAYSD,R4                                                         
         MVC   YTDSSPNH,TAYSSSPH   SAVE YTD SOAP SPNH FOR LATER                 
         MVC   YTDWSPNH,TAYSWSPH   SAVE YTD SOAP WRI SPNH FOR LATER             
*                                                                               
GCW419   XC    TABSOR,TABSOR                                                    
         L     R4,AIO              R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         MVI   ELCODE,TAWHELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
GCW420   BAS   RE,NEXTEL                                                        
         BNE   GCW430                                                           
         USING TAWHD,R4                                                         
         OC    TAWHEMP,TAWHEMP     IF NO EMPLOYER DEFINED                       
         BNZ   *+10                                                             
         MVC   TAWHEMP,TGTPEMP     SET TO DEFAULT EMPLOYER                      
*                                                                               
         CLC   TAWHEMP,TGTPEMP     ONLY USE TALENT PARTNERS FOR NOW             
         BNE   GCW420              ** NOT SUPPORTING MULT. RULES YET **         
         CLC   TAWHUNIT,=C'FD '    SKIP IF THIS IS FEDERAL                      
         BE    GCW420                                                           
         CLI   TAWHUNIT+2,C' '     FIND STATE OF RESIDENCE                      
         BNE   GCW420                                                           
         MVC   TABSOR,TAWHUNIT     SAVE STATE OF RESIDENCE                      
*                                                                               
GCW430   DS    0H                                                               
*                                                                               
*&&DO                                                                           
         XC    TABCID,TABCID       COMMENTED OUT 5/19/2003                      
         L     R4,AIO              CODE WAS SETTING CORPORATE ID                
         USING TATID,R4            EVEN IF CAST WAS NOT BEING                   
         MVI   ELCODE,TATIELQ      PAID AS A CORPORATION                        
         BAS   RE,GETEL            SETTING CORPORATE ID SHOULD BE               
         BNE   GCW4X               HANDLED LATER BY GETCTI ROUTINE              
         MVC   TABCID,TATIID                                                    
*&&                                                                             
*                                                                               
GCW4X    MVC   AIO,AIO1                                                         
         B     YES                                                              
*                                                                               
GCW4NO   BAS   RE,SETERR           SET INVOICE IN ERROR                         
         MVC   AIO,AIO1                                                         
         B     NO                                                               
         DROP  R3,R4                                                            
         SPACE 1                                                                
NOGSTTAB DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000000066'                                                   
*        DC    CL9'000002533'                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
*                                                                               
* THIS ROUTINE DETERMINES THE TAXABLE STATE.  USUALLY IT IS THE STATE           
* OF WORK.  IF THE SOW IS NOT DEFINED ON THE EMPLOYER RECORD AND IF THE         
* STATE OF RESIDENCE IS DEFINED ON THE EMPLOYER RECORD THEN THE SOR IS          
* THE TAXABLE STATE FOR PURPOSES OF DISABILITY, UNEMPLOYMENT, ETC.              
* IF NEITHER ARE DEFINED ON THE EMPLOYER RECORD THEN THE ROUTINE                
* ATTEMPTS TO ASSIGN THE DEFAULT TAXABLE STATE FROM THE EMPLOYER                
* RECORD.  THE STATE U.C.# FOR THE TAXABLE STATE IS SET ACCORDINGLY.            
*                                                                               
         USING TABCHKD,R3                                                       
SETTAXBL NTR1                                                                   
         MVC   TFULL,TABTAXST                                                   
         CLI   TFULL+2,C' '        IF THIS IS A CITY                            
         BNH   SETT05                                                           
         GOTO1 TAXVAL,DMCB,(3,TFULL)                                            
         MVC   TFULL,TGTASTCY      FIND THE STATE IT BELONGS TO                 
         MVC   TABTAXST,TGTASTCY   SET CORRECT TAXABLE STATE                    
         MVC   TABSTATE,TGTASTCY   & CHANGE TO PRINT STATE NOT CITY             
*                                                                               
SETT05   BAS   RE,GETSTATE         LOOK UP STATE OF WORK ON EMP RECORD          
         BE    SETTXX              IF FOUND - DONE                              
*                                                                               
         MVC   TFULL,TABSOR        STATE OF RESIDENCE                           
         BAS   RE,GETSTATE         LOOK UP STATE OF WORK ON EMP RECORD          
         BNE   SETT10              IF FOUND                                     
         MVC   TABTAXST,TABSOR     SET TAXABLE STATE IS STATE OF RES.           
         B     SETTXX                                                           
*                                                                               
SETT10   MVC   TABTAXST,DEFSTATE   IF NOT USE DEFAULT STATE                     
*                                                                               
SETTXX   B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        FIND STATE IN EMPLOYER RECORD                                          
*                      TFULL - STATE                                            
*                                                                               
GETSTATE NTR1                                                                   
         MVC   AIO,AEMPIO          SEARCH EMPLOYER RECORD FOR TAX ID            
         MVI   ELCODE,TATIELQ          ELEMENT FOR STATE OF WORK                
         MVI   FULL,TATITYUN                                                    
         MVC   FULL+1(3),TFULL                                                  
         GOTO1 GETL,DMCB,(4,FULL)  FIRST TRY TO FIND UNEMPLOYMENT ID            
         BNE   GS10                                                             
         MVC   AIO,AIO1                                                         
         B     YES                                                              
*                                                                               
GS10     MVI   FULL,TATITYTX       THEN TRY TO FIND TAX ID                      
         GOTO1 GETL,DMCB,(4,FULL)                                               
         MVC   AIO,AIO1                                                         
         BE    YES                 SET CC                                       
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        KEEP TOTAL OF ALL AMOUNTS TO ENSURE AMOUNTS MATCH                      
*                  INVOICE AMOUNTS - ELSE INVOICE IN ERROR                      
*                  R3 = A(CURRENT ENTRY IN CHECKS TABLE)                        
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
TOTAMT   NTR1                                                                   
         LA    R1,TBGRS            START AT BEGINING OF AMOUNTS                 
         LA    R3,TABGRS                                                        
         LA    R4,NTBLAMT          NUMBER OF AMOUNTS TO TOT TO INVOICE          
*                                                                               
TA10     L     R2,0(R1)            TOTAL AMOUNT                                 
         A     R2,0(R3)            + CHECK AMOUNT                               
         ST    R2,0(R1)            = TOTAL                                      
         LA    R1,4(R1)            NEXT AMOUNT                                  
         LA    R3,4(R3)                                                         
         BCT   R4,TA10                                                          
*                                                                               
         B     XIT                                                              
         DROP  R3                                                               
         SPACE 2                                                                
*                                                                               
*        ATTEMPT TO ADJUST EURO INVOICE TOTALS FOR ROUNDING                     
*                                                                               
FIXEURO  NTR1                                                                   
         TM    TBIPST2,TAPDPEUR    IF PAYMENT IS IN EUROS                       
         BZ    XIT                                                              
         BAS   RE,COMPAMT          AND INVOICE AMOUNTS DO NOT                   
         BE    XIT                 EQUAL CHECK AMOUNTS                          
                                                                                
         MVC   BLOCK(TACOMM-TGROSS),TGROSS                                      
                                                                                
         LA    R1,BLOCK            R1=A(INVOICE AMOUNTS)                        
         LA    R2,TBGRS            R2=A(CHECK AMOUNTS)                          
         LHI   R3,(TACOMM-TGROSS)/4                                             
                                                                                
FE10     CLC   0(4,R1),0(R2)      IF AMOUNTS ARE NOT EQUAL                      
         BE    FE20                                                             
         L     RE,0(R2)                                                         
         S     RE,0(R1)                                                         
         CHI   RE,1               BUT ONLY OFF BY .01                           
         BH    XIT                THEN OK TO UPDATE INVOICE                     
                                                                                
FE20     LA    R1,4(R1)           BUMP TO NEXT INVOICE AMOUNT                   
         LA    R2,4(R2)           AND NEXT CHECK AMOUNT                         
         BCT   R3,FE10                                                          
                                                                                
         MVC   TGROSS(TACOMM-TGROSS),TBGRS                                      
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ     GET INVOICE'S PAYMENT DETAILS                 
         BAS   RE,GETEL           ELEMENT                                       
         BNE   XIT                AND REPLACE THE AMOUNTS                       
         MVC   TAPDGRS(TACOMM-TGROSS),TBGRS                                     
         B     XIT                                                              
         DROP  R4                                                               
*                                                                               
*        COMPARE CHECK TOTALS WITH INVOICE TOTALS                               
*                                                                               
         SPACE 1                                                                
COMPAMT  NTR1                                                                   
         TM    TBOPTS,TBRERUN                                                   
         BO    YES                                                              
         CLC   TGROSS(LAMTS),TBGRS                                              
         BNE   NO                                                               
         B     YES                                                              
         EJECT                                                                  
*        UPDATE THE TAPD ELEMENT ON THE INVOICE TO INDICATE                     
*        PRIMARY INVOICE RECORD                                                 
*               RECORD AT THIS POINT IN AIO                                     
*                                                                               
SETPRM   NTR1                                                                   
         L     R4,AIO                                                           
         USING TAPDD,R4            PAYMENT DETAILS ELEMENT                      
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    TAPDSTA2,TAPDSPRM   SET PRIMARY INVOICE RECORD                   
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
*              ROUTINE READS & UPDATES COMMERCIAL IF NECESSARY                  
*                                                                               
         SPACE 1                                                                
UPDCOMML NTR1                                                                   
         MVI   CSWITCH,C'N'                                                     
         BRAS  RE,UPDCAIR          UPDATE 1ST AIR DATE                          
         BNE   UPDCX                                                            
         MVC   AIO,AIO2            COMMERCIAL IS IN AIO2                        
         USING TACOD,R4                                                         
         L     R4,TGFULL                                                        
         LTR   R4,R4                                                            
         BZ    UPDCX                                                            
*                                                                               
UPDC20   OC    TACOEXP,TACOEXP     IF THERE IS AN EXPIRATION DATE               
         BZ    UPDC50                                                           
         TM    TGUSSTAT,SESSION    AND IT'S A REUSE PAYMENT                     
         BO    UPDC50                                                           
*                                                                               
UPDC30   OC    TACOFCYC,TACOFCYC   IF THERE IS A FIRST FIXED CYCLE              
         BZ    UPDC35                                                           
         MVC   TBSVDTE,TACOFCYC                                                 
         MVC   TEMP(3),TACOFCYC    SET FFC AS BASE TO UPDATE 1ST TIME           
         B     UPDC40                                                           
*                                                                               
UPDC35   XC    TBSVDTE,TBSVDTE     ELSE CLEAR SAVED DATE - ENSURE ADD           
         MVC   TEMP(3),TACOEXP     SET EXP DATE                                 
*                                                                               
UPDC40   CLC   TBCDTS(3),TACOEXP   AND THE CYCLE START DATE                     
         BNH   UPDC50                  > THAN THE EXPIRATION DATE               
         MVC   DUB(3),TEMP         USE CALCULATED EXPIRATION DATE               
         BAS   RE,GETEXP           TO GET NEW EXPIRATION DATE                   
         BNE   UPDC50                                                           
         MVC   TACOEXP,TEMP        & SET IT IN ELEMENT                          
         MVC   TBCOEXP,TACOEXP     & TO PRINT & UPDATE TACOD ON INV REC         
         MVI   CSWITCH,C'Y'                                                     
         B     UPDC40                                                           
*                                                                               
UPDC50   DS    0H                                                               
*&&DO                                                                           
UPDC50   TM    TGUSSTA2,HLDTYPE         IF HOLDING FEE PAYMENT                  
         BZ    UPDC60                   TURN OFF CHANGED SINCE LAST             
         NI    TACOSTA2,X'FF'-TACOCHHF  HOLDING FEE NOTICE STATUS               
         MVI   CSWITCH,C'Y'                                                     
*&&                                                                             
*                                                                               
**NO-OP                                                                         
**09/13                                                                         
**UPDC60   BRAS  RE,SETCSF           SET CSF FEE STATUS IF PAYING CSF           
**NO-OP                                                                         
*                                                                               
UPDC60   DS    0H                                                               
*                                                                               
UPDC70   CLI   CSWITCH,C'Y'        DO WE NEED TO WRITE BACK RECORD              
         BNE   UPDCX                                                            
         BAS   RE,PUTIT                                                         
         GOTO1 AMYTRACE,DMCB,(RC),=C'COMML',AIO,0                               
*                                                                               
         BRAS  RE,APTRS                                                         
*                                                                               
UPDCX    MVC   AIO,AIO1            RESET AIO                                    
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET NEW EXPIRATION DATE - SET CC EQ IF FOUND, ELSE CC NOT EQ           
*                                  DUB  - BASE PWOS DATE                        
*                                  TEMP - RETURN DATE                           
*                                                                               
         SPACE 1                                                                
GETEXP   NTR1                                                                   
         MVC   WORK(3),DUB                                                      
         CLC   DUB+1(2),=X'0229'   IF IT'S FEB 29                               
         BNE   GETEX8                                                           
         GOTO1 HEXOUT,DMCB,DUB,TEMP,1,0                                         
         PACK  DUB,TEMP(2)         TEST IF LEAP YEAR                            
         CVB   R4,DUB                                                           
         MVC   DUB(3),WORK         RESET DUB                                    
         SLL   R4,30                                                            
         LTR   R4,R4                                                            
         BZ    GETEX8                                                           
         MVC   DUB+1(2),=X'0301'   IF NOT, CHANGE IT TO MAR 01                  
         SPACE                                                                  
GETEX8   CLC   TBSVDTE,WORK                   IF THIS IS THE FIRST TIME         
         BE    GETEX10                            DON'T ADD 1 DAY               
         GOTO1 DATCON,DMCB,(1,DUB),(0,TEMP)   ELSE CONVERT TO EBCDIC            
         GOTO1 ADDAY,DMCB,TEMP,WORK,1         ADD 1 DAY                         
         GOTO1 DATCON,DMCB,(0,WORK),(1,DUB)   & CONVERT BACK TO PWOS            
*                                                                               
GETEX10  XC    ELEM,ELEM                                                        
         GOTO1 EXPIRE,DMCB,ELEM,DUB,TEMP  GET EXPIRATION DATE                   
         CLC   TEMP(3),=3X'00'            IF ZEROS RETURNED                     
         BE    NO                         SET CC NOT EQ                         
         B     YES                        ELSE SET CC EQ                        
         EJECT                                                                  
*                                                                               
*        ROUTINE GETS CAST RECORDS FOR UPDATE                                   
*                                                                               
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
UPDCAST  NTR1                                                                   
         L     R3,ACHKTAB          SET ADDRESS OF THE CHECK TABLE               
         MVC   AIO,AIO2                                                         
*                                                                               
UCST10   OC    0(TABDSK,R3),0(R3)  END OF CHECK TABLE                           
***T10   OC    0(TABLEN,R3),0(R3)  END OF CHECK TABLE                           
         BZ    UCSTX               TGAGY & TGCOM ARE SET                        
         MVC   TGSSN,TABSSN        SS NUMBER                                    
         MVC   TGCAT,TABCAT        CATEGORY                                     
         XC    TGCSORT,TGCSORT                                                  
         MVC   TGCSORT+4(2),TABSEQ SEQUENCE NUMBER                              
         BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BNE   UCST11                                                           
         GOTO1 RECVAL,DMCB,TLCACCDQ,(X'34',0)                                   
         B     UCST12                                                           
UCST11   GOTO1 RECVAL,DMCB,TLCACCDQ,(X'24',0)                                   
UCST12   BNE   UCST40              RECORD SHOULD BE THERE                       
*                                                                               
         MVI   CSWITCH,C'N'                                                     
         L     R4,AIO                                                           
         USING TACAD,R4                                                         
         MVI   ELCODE,TACAELQ      GET CAST DETAILS ELEMENT                     
         BAS   RE,GETEL                                                         
         BNE   UCST40                                                           
*                                                                               
         BRAS  RE,LOOKFFC          IF USE IS IN FFCTAB,                         
         BNE   UCST15                                                           
         GOTO1 ASAVPTRS,DMCB,ASPBLK                                             
         MVC   TACAFCYC,TBCDTS     UPDATE FFC WITH CYCLE START DATE             
         MVI   CSWITCH,C'Y'                                                     
*                                                                               
UCST15   OC    TACAEXP,TACAEXP     IF THERE IS AN EXPIRATION DATE               
         BZ    UCST35                                                           
         TM    TGUSSTAT,SESSION    AND IT'S A REUSE PAYMENT                     
         BO    UCST35                                                           
*                                                                               
UCST20   XC    TBSVDTE,TBSVDTE     CLEAR SAVED DATE - ENSURE ADD 1              
         MVC   TEMP(3),TACAEXP     SET EXP AS BASE TO UPDATE 1ST TIME           
*                                                                               
UCST30   CLC   TBCDTS(3),TACAEXP   AND THE CYCLE START DATE                     
         BNH   UCST35                  > THAN THE EXPIRATION DATE               
         MVC   DUB(3),TEMP         USE CALCULATED EXPIRATION DATE               
         BAS   RE,GETEXP           TO GET NEW EXPIRATION DATE                   
         BNE   UCST35                                                           
         MVC   TACAEXP,TEMP        & SET IT IN ELEMENT                          
         MVC   TABCAEXP,TACAEXP    & SET TO UPDATE TACAD ON CHECK REC           
         MVI   CSWITCH,C'Y'                                                     
         B     UCST30                                                           
*                                                                               
UCST35   CLI   CSWITCH,C'Y'        DO WE NEED TO WRITE BACK RECORD              
         BNE   UCST40                                                           
*                                                                               
         BAS   RE,PUTIT                                                         
         GOTO1 AMYTRACE,DMCB,(RC),=C'CAST',AIO,0                                
*                                                                               
         BRAS  RE,LOOKFFC          IF USE IS IN FFCTAB,                         
         BNE   *+8                                                              
         BRAS  RE,APTRS            ADD POINTERS                                 
*                                                                               
UCST40   LA    R3,TABLEN(R3)                                                    
         B     UCST10              GET NEXT CAST MEMBER                         
*                                                                               
UCSTX    MVC   AIO,AIO1                                                         
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        ADD A DIRECTORY POINTER                                                
*                                                                               
ADDIT    NTR1                                                                   
         GOTO1 ADD                 ADD DIRECTORY / PASSIVE PTR                  
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        WRITE A DIRECTORY POINTER                                              
*                                                                               
WRITEIT  NTR1                                                                   
         GOTO1 WRITE               WRITE DIRECTORY / PASSIVE PTR                
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        PUT A RECORD                                                           
*                                                                               
PUTIT    NTR1                                                                   
         GOTO1 PUTREC              WRITE THE RECORD                             
         B     XIT                                                              
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
*        DO AN EXECUTED MOVE FOR VARIABLE LENGTH ELEMENTS                       
*                            P1(0) - OVERRIDE BIT (X'80')                       
*                            P1    - STRING FOR GETL                            
*                            P2(0) - LEN OF DESTINATION                         
*                            P2    - A(DESTINATION)                             
*                            P3    - ELCODE                                     
*                                                                               
         SPACE 1                                                                
MVCTST   NTR1                                                                   
         ZIC   R2,11(R1)            ELCODE                                      
         STC   R2,ELCODE                                                        
         MVC   BYTE,3(R1)          SEARCH STRING FOR GETL                       
         L     R4,4(R1)            A(DESTINATION)                               
         ZIC   R3,4(R1)            LEN OF DESTINATION                           
         STH   R3,HALF             SAVE LEN OF DESTINATION                      
         BCTR  R3,0                SUB 1 FOR EXECUTED MVC                       
         TM    0(R1),X'80'         TEST IF OVERRIDE                             
         BO    MV10                YES - ALREADY HAVE INFO                      
         EX    R3,*+8                    DON'T ERASE                            
         B     *+10                                                             
         MVC   0(0,R4),SPACES      ELSE - INIT WITH SPACES                      
*                                                                               
MV10     SR    R2,R2                                                            
         CLI   BYTE,0              IF THERE'S SEARCH STRING                     
         BE    MV20                                                             
         LA    R2,1                   SET LENGTH OF IT                          
*                                                                               
MV20     GOTO1 GETL,DMCB,((R2),BYTE)                                            
         BNE   MVX                 ELEM NOT FOUND - EXIT                        
         L     R3,TGELEM                                                        
         ZIC   R1,1(R3)            LENGTH OF ELEMENT                            
         AHI   R2,2                R2 = 2 OR 3 - LENGTH BEFORE DATA             
         CLI   ELCODE,TAADELQ      IF ADDRESS ELEMENT                           
         BNE   MV30                                                             
         LA    R2,1(R2)            PASS BYTE CONTAINING N'ADDDR LNS             
*                                                                               
MV30     SR    R1,R2               - ELCODE, LEN & (TYPE IF EXISTS)             
         AR    R3,R2               POSITION R3 TO START OF DATA                 
         BCTR  R1,0                SUBTRACT 1 FOR MOVE                          
         LH    R2,HALF             GET LEN OF DESTINATION                       
         BCTR  R2,0                SUBTRACT 1 FOR MOVE                          
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),SPACES                                                   
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)       EXECUTED                                     
*                                                                               
MVX      B     XIT                                                              
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
*        GO THROUGH THE CHECK TABLE AND SEE IF CHECK RECORDS                    
*            NEED TO BE CHANGED                                                 
*                                                                               
         SPACE 1                                                                
UPDCHK   NTR1                                                                   
*                                                                               
         USING TABCHKD,R3                                                       
         L     R3,ACHKTAB          CHECK TABLE                                  
         ST    R3,ANXTCHK                                                       
*                                                                               
UCK10    OC    0(TABDSK,R3),0(R3)  LAST ENTRY IN TABLE                          
***10    OC    0(TABLEN,R3),0(R3)  LAST ENTRY IN TABLE                          
         BZ    UCKX                                                             
         ST    R3,ATHISCHK         SAVE A(CURRENT CHECK TABLE ENTRY)            
         MVC   TGSSN,TABSSN        SS NUMBER                                    
         GOTO1 CATVAL,DMCB,TABCAT  CATEGORY                                     
         XC    KEY,KEY             GET THE CHECK RECORD                         
         LA    R2,KEY              TO SEND TO TAPPGUAR/UPDATE                   
         USING TLDRD,R2                                                         
         MVC   TLDRDA,TABDA        WITH UPDATED INFO                            
         MVC   AIO,AIO2                                                         
         BAS   RE,SETCHK           SET SYSFIL/DIR TO CHECKS                     
         BRAS  RE,CKUPDTE          CHECK FOR UPDATE OPTION                      
         GOTO1 GETREC                                                           
         L     R4,AIO                                                           
         USING TLCKD,R4                                                         
         MVC   TGCSORT,TLCKSORT                                                 
         GOTO1 ASAVPTRS,DMCB,ASPBLK   SAVE POINTERS IN ASPBLK                   
         DROP  R2,R4                                                            
*                                                                               
         GOTOR BLDCNPD,DMCB,AIO    BUILD CHECK'S CAN$ PAYMENT DETAILS           
*                                                                               
         L     R4,AIO              GET PAYMENT DETAILS ELEMENT                  
         USING TAPDD,R4                                                         
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                 IT WAS READ ALREADY                          
         DC    H'0'                                                             
*                                                                               
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE SKIP                   
         BO    UCK40                                                            
         TM    TBSTA2,TAINSPRM     SKIP IF PRIMARY INVOICE                      
         BO    UCK20                                                            
         CLI   TGUSEQU,UGRT        OR IF GUARANTEE PAYMENT                      
         BE    UCK20                                                            
         TM    TABSTAT2,TABSTGCU   OR GCON UPDATE REQUIRED                      
         BO    UCK20                                                            
         CLI   TABGSTAT,X'FE'      OR UPDATE REQUIRED RETURNED BY               
         BE    UCK20               TAPPGUAR NEED TO UPDATE RECORD               
*                                                                               
         MVC   TAPDSPNH,TABSPNH    ELSE RESET SUBJ TO P&H                       
         MVC   TAPDPNH,TABPNH      AND P&H AMOUNT IN CASE PREV ADJUSTED         
         B     UCK40               SKIP CALL TO TAPPGUAR                        
*                                                                               
UCK20    TM    TBSTA2,TAINSPRM     IF PRIMARY INVOICE                           
         BNO   *+8                                                              
         OI    TAPDSTA2,TAPDSPRM   INDICATE PRIMARY HERE TOO                    
*                                                                               
         CLI   TGUSEQU,UGRT        IF GUARANTEE PAYMENT                         
         BE    UCK25                                                            
         TM    TABSTAT2,TABSTGCU   OR GCON UPDATE REQUIRED                      
         BO    UCK25                                                            
         CLI   TABGSTAT,X'FE'      OR UPDATE REQUIRED                           
         BNE   UCK40                                                            
UCK25    MVC   TAPDSTAT,TABSTAT    RESET STATUS                                 
         MVC   TAPDACDE,TABACDE    RESET APPLY CODE                             
         MVC   TAPDGRS(TAPDAMTL),TABGRS RESET ALL PAYMENT DETAIL AMTS           
         TM    TABSTAT2,TABSTGCU                                                
         BZ    *+8                                                              
         OI    TAPDSTA3,TAPDSGCU   SET GCON UPDATED STATUS                      
         MVI   BYTE,0                                                           
         TM    TBOPTS,TBTRACE      TRACE GUARANTEES TOO                         
         BNO   UCK30                                                            
         MVI   BYTE,X'40'                                                       
*                                                                               
UCK30    BAS   RE,SETTAL           SET SYSFIL/DIR TO TALENT                     
         GOTOR GUARCK,DMCB,(RC),(BYTE,AIO2),(X'40',AIO1),SYSCOMM                
         BAS   RE,SETCHK           SET SYSFIL/DIR TO CHECKS                     
*                                                                               
         XC    KEY,KEY             GET CHECK RECORD TO PREVENT                  
         LA    R2,KEY                                                           
         USING TLDRD,R2            GETREC/PUTREC SYNDROME                       
         MVC   TLDRDA,TABDA                                                     
         DROP  R2                                                               
         L     R1,=V(AREA)                                                      
         ST    R1,AIO                                                           
         BRAS  RE,CKUPDTE          CHECK FOR UPDATE OPTION                      
         GOTO1 GETREC              GET THE REC                                  
*                                                                               
UCK40    MVC   AIO,AIO2            RESET I/O TO POINT TO UPDATED REC            
         BRAS  RE,UPDTABY          UPDATE TABY ELEMENT TO THE RECORD            
         BAS   RE,UPDTACA          UPDATE TACA ELEMENT TO THE RECORD            
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE SKIP                   
         BO    *+8                                                              
         BAS   RE,UPDTAYE          UPDATE TAYE ELEMENT TO THE RECORD            
         GOTO1 AMYTRACE,DMCB,(RC),=C'CHK',AIO,0                                 
*                                                                               
         BAS   RE,PUTIT            WRITE BACK CHECK RECORD                      
         BRAS  RE,APTRS                                                         
*                                                                               
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BE    *+14                                                             
         CLC   TABUNI,=C'AFT'            ONLY IF UNION IS AFT                   
         BNE   UCK50                                                            
         GOTO1 =A(UPDW4),DMCB,(RC),(R3)  UPDATE YTD SPNH INFO ON W4 REC         
*                                                                               
UCK50    CLI   TGUSEQU,USOP        FOR SOP PAYMENTS                             
         BNE   UCK60                                                            
         TM    TGCATYPE,EXTRA      EXCEPT CATEGORIES TREATED LIKE EXTRA         
         BO    UCK60                                                            
         GOTO1 =A(ECAST),DMCB,(RC) HANDLE ECAST RECORDS                         
         BAS   RE,SETCHK           SET SYSFIL/DIR BACK TO CHECKS                
*                                                                               
UCK60    GOTO1 =V(TABISPLT),DMCB,(RC),EADDCKS  ADD SUBSIDIARY CHECKS            
*                                              ** AIO <> AIO2                   
         LA    R3,TABLEN(R3)       NEXT CHECK IN TABLE                          
         L     R1,ACHKTAB          CHECK TABLE                                  
         A     R1,=AL4(TABCHKLN*TABLEN)                                         
         CR    R3,R1               END OF CHECK TABLE                           
         BL    UCK10                                                            
*                                                                               
UCKX     BAS   RE,SETTAL           SET SYSFIL/DIR TO TALENT                     
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*        UPDATE CAST DETAILS ELEMENT                                            
*                                                                               
         USING TABCHKD,R3                                                       
UPDTACA  NTR1                                                                   
         TM    TBSTAT,TAINSCHK     IF CHECKS NOT ALREADY WRITTEN                
         BO    UPDTACAX                                                         
*                                                                               
         L     R4,AIO              R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   UPDTACAX                                                         
         USING TACAD,R4            R4=A(CAST DETAILS ELEMENT)                   
         MVC   TACAEXP,TABCAEXP    RESET INCAST CAST EXP DATE UPDATED           
UPDTACAX B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        UPDATE/ADD YTD EARNINGS ELEMENT                                        
*                                                                               
         USING TABCHKD,R3                                                       
UPDTAYE  NTR1                                                                   
         XR    R0,R0                                                            
         USING TAPDD,R4                                                         
         L     R4,AIO              IF THIS IS A EURO PAYMENT                    
         MVI   ELCODE,TAPDELQ      SET R0 TO 1                                  
         BAS   RE,GETEL            ELSE SET IT AS 0                             
         BNE   UYE10                                                            
         TM    TAPDPST2,TAPDPEUR                                                
         BZ    UYE10                                                            
         LHI   R0,1                                                             
         DROP  R4                                                               
*                                                                               
         USING TAYED,R4                                                         
UYE10    L     R4,AIO                                                           
         MVI   ELCODE,TAYEELQ      GET YTD EARNINGS ELEMENT                     
         MVI   WORK,TAYETBIL                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BE    UYE20                                                            
         XC    ELEM,ELEM                                                        
         LA    R4,ELEM                                                          
         MVI   TAYEEL,TAYEELQ                                                   
         MVI   TAYELEN,TAYELNQ                                                  
         MVI   TAYETYPE,TAYETBIL                                                
         GOTO1 ADDELEM                                                          
         B     UYE10               POINT TO ELEMENT                             
*                                                                               
UYE20    L     R4,TGELEM                                                        
         TM    STATUS,STCOD                                                     
         BO    *+10                IF THIS IS A COD - DON'T SET YTD             
         MVC   TAYEEARN(TAYENLN),TABNYTD    SET NEW YTD AMOUNTS                 
         MVC   TAYETERN(TAYETLN),TABXYTD    SET NEW TAXABLE AMOUNTS             
*                                                                               
         LTR   R0,R0               IF CURRENCY IS EUROS                         
         BZ    UYEX                                                             
         ZAP   DUB,TBGCVT          CONVERT CONVERSION RATE TO BINARY            
         CVB   R1,DUB                                                           
         ST    R1,FULL             AND CONVERT YTD AMOUNTS                      
         GOTOR CVT2EURO,DMCB,('TAYENLN/4',TAYEEARN)                             
         GOTOR CVT2EURO,DMCB,('TAYETLN/4',TAYETERN)                             
UYEX     B     XIT                                                              
         EJECT                                                                  
*        SET SYSFIL/DIR TO CHECK FILE                                           
*                                                                               
         SPACE 1                                                                
SETCHK   MVC   SYSFIL,=CL8'CHKFIL'                                              
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        SET SYSFIL/DIR TO TALENT FILE                                          
*                                                                               
         SPACE 1                                                                
SETTAL   MVC   SYSFIL,=CL8'TALFIL'                                              
         MVC   SYSDIR,=CL8'TALDIR'                                              
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
*        SET STATUS CODE ON INVOICE RECORD AND IN DIRECTORY                     
*            BILLED/IN ERROR AND SET BILL DATE                                  
*                                                                               
         SPACE 1                                                                
UPDINV   NTR1                                                                   
         MVC   AIO,AIO1            AIO1 HAS INVOICE RECORD                      
         L     R4,AIO                                                           
         USING TLIND,R4                                                         
         MVC   KEY,TLINKEY         SET KEY FOR RE-READ                          
*                                                                               
         TM    TBOPTS,TBRERUN      IF THIS ISN'T A RERUN                        
         BO    UINV10                                                           
         CLI   NOBIPTR,C'Y'        AND THE OPEN ITEM PTR EXISTS                 
         BE    UINV10                                                           
*                                                                               
         TM    WHEN,X'20'          SOON?                                        
         BO    UINV10              READ INVOICE                                 
*                                                                               
UINV05   LA    RF,TBILLD                                                        
         AH    RF,=Y(INVPTRS-BILLD)                                             
         GOTO1 AGENPTRS,DMCB,(RF)       BUILD POINTER BLOCK                     
         LA    RF,TBILLD                                                        
         AH    RF,=Y(INVPTRS-BILLD)                                             
         GOTO1 =A(GETPTR),DMCB,('TLINBCDQ',(RF))     GET OPEN ITEM PTR          
*                                                                               
UINV10   GOTO1 HIGH                                                             
         CLC   KEY(L'TLDRKEY),KEYSAVE                                           
         BE    *+6                 MUST BE THERE - MID PROCESSING               
         DC    H'0'                                                             
         MVC   SVKEY,KEY           SAVE THE KEY                                 
*                                                                               
         MVC   AIO,AIO2                                                         
         CLI   SBYTE,TAINSERR      IF INVOICE IN ERROR                          
         BNE   UINV15                                                           
         MVC   AIO,AIO1            GET RECORD OVER UPDATED INVOICE              
*                                                                               
UINV15   BRAS  RE,CKUPDTE          CHECK FOR UPDATE OPTION                      
         GOTO1 GETREC              RE-GET RECORD TO PREVENT GETREC/             
*                                         PUTREC SYNDROME                       
         LA    RF,TBILLD                                                        
         AH    RF,=Y(INVPTRS-BILLD)                                             
         GOTO1 ASAVPTRS,DMCB,(RF)                                               
         MVC   AIO,AIO1            INVOICE TO BE WRITTEN - IN AIO1              
         CLI   SBYTE,TAINSERR      IF INVOICE NOT  IN ERROR                     
         BE    UINV30                                                           
         BRAS  RE,UPDT             UPDATE BILLED RECORD - ADD ELEMENTS          
         TM    TBSTA2,TAINSPRM     IF PRIMARY INVOICE                           
         BNO   *+8                                                              
         BAS   RE,SETPRM           SET PRIMARY IN TAPD ELEMENT ALSO             
         GOTOR GUARCK,DMCB,(RC),(X'10',AIO2),(X'40',0),SYSCOMM                  
         BE    *+6                 ADD INVOICE TOTALS TO BUFFER                 
         DC    H'0'                                                             
*                                                                               
         GOTOR SETEURT,DMCB,AIO    SET EURO CONVERSION RATE                     
         GOTOR BLDEBD,DMCB,AIO     BUILD EUROS BILLING DETAILS ELEMENT          
*                                                                               
         BRAS  RE,BLDCNBD          BUILD CAN$ BILLING DETAILS ELEM              
*                                                                               
         L     R4,AIO              GET INVOICE STATUS ELEMENT                   
         MVI   ELCODE,TAINELQ                                                   
         USING TAIND,R4                                                         
         BAS   RE,GETEL                                                         
         BE    *+6                 WAS ALREADY READ                             
         DC    H'0'                                                             
         TM    STATUS,STCOD        IF THIS IS A COD - THEN MARK IT              
         BO    UINV20                 PRINTED                                   
         OC    TAINSTAT,SBYTE      SET INVOICE BILLED                           
         MVC   TAINBDTE,TGTODAY1                                                
         MVC   TBILLDTE,TAINBDTE   SET THE BILL DATE                            
         B     UINV40                                                           
*                                                                               
UINV20   OI    TAINSTA2,TAINSHLP   SET COD INVOICE PRINTED                      
         CLI   TAINLEN,X'43'       MAKE SURE THIS IS NEW ELEMENT                
         BNH   UINV40              & HAS ENOUGH ROOM                            
         MVC   TAINHDTE,TGTODAY1   SET PRINT DATE TO TODAY                      
         B     UINV40                                                           
*                                                                               
         USING TANUD,R4                                                         
UINV30   CLI   ERRSTAT,TAINEW4     IF NO W4 RECORD                              
         BE    UINV30A                                                          
         CLI   ERRSTAT,TAINEW4E    NO W4 ELEMENT                                
         BNE   UINV35                                                           
UINV30A  LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TANUEL,TANUELQ      SAVE PID OF THE PROBLEM PERFORMER            
         MVI   TANULEN,9                                                        
         MVI   TANUTYPE,TANUEPID                                                
         GOTO1 SSNPACK,DMCB,TABSSN,TANUMBER                                     
         GOTO1 ADDELEM                                                          
         DROP  R4                                                               
*                                                                               
UINV35   L     R4,AIO              GET INVOICE STATUS ELEMENT                   
         MVI   ELCODE,TAINELQ                                                   
         USING TAIND,R4                                                         
         BAS   RE,GETEL                                                         
         BE    *+6                 WAS ALREADY READ                             
         DC    H'0'                                                             
         OC    TAINSTAT,SBYTE      SET INVOICE IN ERROR/BILLED                  
         MVC   TAINTERR,ERRSTAT    SET TYPE OF ERROR                            
         OI    STATUS,STERR        SET COUNTER STATUS = ERROR                   
         GOTO1 =A(PUTERR),DMCB,(RC) PUT INVOICE IN ERROR TABLE                  
         GOTOR GUARCK,DMCB,(RC),(X'08',AIO2),(X'40',0),SYSCOMM                  
         BE    *+6                 CLEAR INVOICE TOTALS FROM BUFFER             
         DC    H'0'                                                             
*                                                                               
UINV40   GOTO1 AMYTRACE,DMCB,(RC),=C'INV',AIO,0                                 
*                                                                               
         TM    TBOPTS2,TBFIXINV+TBWRYES    IF FIXING INVOICES                   
         BNO   UINV60              & ACTUALLY WRITING TO THE FILE               
         USING T703FFD,R3                                                       
         L     R3,ATWA                                                          
         MVI   TWAWRITE,C'Y'       SET WRITE = Y IN TWA FOR FIX                 
*                                                                               
UINV60   BAS   RE,PUTIT            WRITE THE RECORD                             
         TM    TBOPTS2,TBFIXINV+TBWRYES    IF FIXING INVOICES                   
         BNO   *+8                         & ACTUALLY WROTE TO THE FILE         
         MVI   TWAWRITE,C'N'               SET WRITE BACK TO NO                 
*                                                                               
UINV70   LA    R2,SVKEY                                                         
         USING TLDRD,R2                                                         
         MVC   SVDA,TLDRDA         SAVE DISK ADDRESS                            
         TM    TBOPTS2,TBFIXINV    IF FIXING INVOICES                           
         BNO   UINV80              DON'T CHECK RERUN STATUS                     
         TM    TBOPTS,TBRERUN      IF THIS ISN'T A RERUN                        
         BO    UINV90                                                           
*                                                                               
UINV80   MVI   BYTE,X'04'                                                       
         TM    TBOPTS,TBTRACE      (CHECK FOR TRACE)                            
         BNO   *+8                                                              
         MVI   BYTE,X'14'                                                       
         TM    TBOPTS2,TBFIXINV+TBWRYES    IF FIXING INVOICES                   
         BNO   *+8                 & ACTUALLY WRITING TO THE FILE               
         MVI   TWAWRITE,C'Y'       SET WRITE = Y IN TWA FOR FIX                 
         LA    RF,TBILLD                                                        
         AH    RF,=Y(INVPTRS-BILLD)                                             
         GOTO1 AADDPTRS,DMCB,(BYTE,(RF))      ADD POINTERS                      
         TM    TBOPTS2,TBFIXINV+TBWRYES    IF FIXING INVOICES                   
         BNO   *+8                         & ACTUALLY WROTE TO THE FILE         
         MVI   TWAWRITE,C'N'               SET WRITE BACK TO NO                 
*                                                                               
UINV90   CLI   SBYTE,TAINSERR      IF INVOICE NOT  IN ERROR                     
         BE    UINVX                                                            
         GOTO1 =V(TABISPLT),DMCB,(RC),EADDIVS  ADD SUBSIDIARY INVOICES          
UINVX    B     XIT                                                              
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
*                                                                               
*        MARK INVOICE IN ERROR                                                  
*                                                                               
SETERR   NTR1                                                                   
         OI    SBYTE,TAINSERR                                                   
         BAS   RE,UPDINV           THEN SET STATUS IN ERROR                     
         B     XIT                                                              
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
YES      SR    RC,RC               SET CONDITION CODE                           
NO       LTR   RC,RC                                                            
*                                                                               
XIT      XIT1                                                                   
         SPACE 3                                                                
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*                                                                               
*  CONSTANTS                                                                    
*                                                                               
*                                                                               
DATAFILE DCB   DDNAME=DATAFILE,DSORG=PS,EODAD=SETREND,MACRF=GM,        X        
               RECFM=FB,LRECL=12,BLKSIZE=0                                      
         SPACE 2                                                                
COMPLM   DC    X'FFFFFFFFFFFF'                                                  
*                                                                               
         SPACE 2                                                                
*              TABLE OF USES FOR SC (SESSION COPIES)                            
*                                                                               
SCTAB    DC    AL1(UOTH,UOTM,UPEN,UPEM,UDWN,USOT,USPN,UREN,UOTC,UCPN)           
         DC    AL1(USRE,UGRT,ULFT,UARN),X'FF'                                   
         EJECT                                                                  
LDCBLK   DS    3A                  BLOCK FOR TALDCPTR                           
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 1                                                                
         DROP  R5,R6,R7,RB         DROP PREV BASE REGS                          
         EJECT                                                                  
*                                                                               
*        INITIALIZATION FOR EACH NEW INVOICE                                    
*                                                                               
INVINIT  NTR1  BASE=*,LABEL=*                                                   
         L     R2,=V(POSTPOOL)     POSTING TABLE                                
         L     R3,=V(POSTPX)                                                    
         BRAS  RE,CLRAREA          CLEAR POSTING TABLE                          
*                                                                               
         L     R2,ACHKTAB          CHECK TABLE                                  
         ST    R2,ANXTCHK                                                       
         LR    R3,R2                                                            
         A     R3,=AL4(TABCHKLN*TABLEN)                                         
         BRAS  RE,CLRAREA          CLEAR CHECK TABLE                            
*                                                                               
         L     R2,=V(SITAB)        SUBISIDIARY INVOICE TABLE                    
         L     R3,=V(SITABX)                                                    
         BRAS  RE,CLRAREA          CLEAR IT                                     
*                                                                               
         L     R2,=V(FRCIVAMT)     FORCE INVOICE AMOUNTS TABLE                  
         L     R3,=V(FRCIVX)                                                    
         BRAS  RE,CLRAREA          CLEAR IT                                     
*                                                                               
         L     R2,=V(NETTAB)       NETWORK TABLE                                
         ST    R2,ANXTNET                                                       
         L     R3,=V(NETTABX)                                                   
         BRAS  RE,CLRAREA          CLEAR NETWORK TABLE                          
*                                                                               
         MVC   AIO,AIO1                 SET IO AREA                             
         XC    TBUNI1(TBALLUNI),TBUNI1  CLEAR UNIONS                            
         XC    TGROSS(AMTS),TGROSS      CLEAR ALL AMOUNTS                       
         XC    TBCANAMT(CAMTS),TBCANAMT                                         
         XC    TOTGRS(EAMTS),TOTGRS                                             
******   XC    TBGPAY1(PAYAMT),TBGPAY1                                          
         XC    TBAREA1(AREA1X),TBAREA1  INVOICE INFORMATION                     
*****    XC    TBMED(USEX),TBMED   INVOICE INFORMATION                          
         XC    TBCOM(TBCOMX),TBCOM COMMENTS                                     
         XC    TGUSTYST,TGUSTYST   USE TYPE                                     
         LH    R1,=Y(USEDET1-BILLD)                                             
         AR    R1,RA                                                            
         XC    0(L'USEDET1+L'USEDET2,R1),0(R1)  USE DETAILS                     
*                                                                               
         XC    AOSBASE,AOSBASE     CLEAR AOS AMTS                               
         XC    AOSAMT,AOSAMT                                                    
         XC    AOSPSTRT,AOSPSTRT                                                
         XC    AOSPST,AOSPST                                                    
         XC    TBPSTU,TBPSTU       CLEAR PST AMOUNT                             
*        XC    TBPSTAOS,TBPSTAOS   CLEAR AOS PST AMOUNT                         
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         XC    0(4,RE),0(RE)       CLEAR AOS PST AMOUNT                         
*                                                                               
         L     R2,ATALIM           A(TALIM WORKING AREA)                        
         USING TMD,R2                                                           
         XCEF  (R2),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         DROP  R2                                                               
         MVI   NOBIPTR,C'N'        INIT - OPEN ITEM PTR EXISTS                  
         MVI   FORCEHED,C'Y'       FORCE PAGE EJECT                             
         MVI   ERRSTAT,0           SET FLAG - NO ERRORS YET                     
         MVI   SBYTE,0                                                          
         MVI   FLAG,C'N'           INIT FLAGS & COUNTERS TO NO                  
         MVC   FLAG+1(FLAGX-FLAG-1),FLAG                                        
         XC    COUNT1(COUNTX),COUNT1                                            
         XC    STATUS,STATUS       CLEAR TYPE OF INVOICE                        
***      XC    STAT2,STAT2                                                      
         NI    STAT2,STOPEN        CLEAR ALL BITS EXCEPT OPEN TAPE              
         XC    STAT3,STAT3                                                      
         MVC   TBRUNDTE,TGTODAY8   SET DATES                                    
         XC    HNWFUND,HNWFUND     CLEAR TOTAL HNW THAT HAS FUND (EMS)          
*                                                                               
         L     R1,ACURTAPG                                                      
         XC    0(TAPGLNQ,R1),0(R1) CLEAR POSTING DETAILS                        
*                                                                               
         USING TXTD,R1                                                          
         L     R1,ATAXDSCT                                                      
         XC    TXTXTTAB,TXTXTTAB   CLEAR EXTRA TAX TABLE                        
         DROP  R1                                                               
                                                                                
         XC    TBCCVT,TBCCVT       CLEAR CAN CONV RATE                          
         XC    TBECVT,TBECVT       CLEAR EURO CONV RATE                         
         XC    TGGSTRAT,TGGSTRAT   CLEAR GST RATE                               
INIX     J     XIT                                                              
         EJECT                                                                  
*                                                                               
*        CLEAR AREA OF STORAGE        R2 = A(AREA)                              
*                                     R3 = A(AREA END)                          
*                                                                               
CLRAREA  NTR1  BASE=*,LABEL=*                                                   
         SR    R3,R2               SIZE OF AREA TO CLEAR                        
         XR    RE,RE                                                            
         LR    RF,RE                                                            
         MVCL  R2,RE                                                            
         J     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ADD TODAY'S SYSTEM RECORD IF NOT ALREADY ADDED                         
*                                                                               
ADDSYS   NTR1  BASE=*,LABEL=*                                                   
         USING TLSYD,R4                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLSYCD,TLSYCDQ          READ SYSTEM RECORD                       
         MVI   TLSYTYPE,TLSYSAVE       SAVED SYSTEM RECORD                      
         MVC   TLSYSVDT,TGTODAY1       TODAY'S DATE                             
         GOTO1 HIGH                                                             
         CLC   KEY(TLSYLEN-TLSYD),KEYSAVE    IF IT EXISTS, EXIT                 
         JE    XIT                                                              
         DROP  R4                                                               
                                                                                
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)   GET REGULAR SYSTEM REC           
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING TLSYD,R4                                                         
         L     R4,AIO                                                           
         MVI   TLSYTYPE,TLSYSAVE       SAVED SYSTEM RECORD                      
         MVC   TLSYSVDT,TGTODAY1       TODAY'S DATE                             
         XC    TLSYSTAT,TLSYSTAT       CLEAR STATUS                             
         GOTO1 ADDREC                                                           
         J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================*         
*  ROUTINE TO SET EFFECTIVE DATE FOR INVOICE                                    
*  USE TODAY'S DATE IF NOT A CREDIT                                             
*  IF NEW CREDITING ENABLED, AND A CREDIT, USE PUR PRINT DT                     
*  IF NO PUR PRINT DATE, USE BILLED DATE OF ORIGINAL INV                        
*=====================================================================*         
SETEFDT  NTR1  BASE=*,LABEL=*                                                   
         MVC   TBEFFDTE,TGTODAY1   SET AS TODAY'S DATE                          
                                                                                
         TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         JZ    XIT                                                              
                                                                                
         L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         USING TAPDD,R4                                                         
         BRAS  RE,GETEL                                                         
         BNE   GSYS50                                                           
         TM    TAPDPST2,TAPDPCIB   AND IF CREDIT INVOICE HAS SAVED              
         JZ    XIT                 BILLING RATES, USE PUR/BILL DATE             
         DROP  R4                                                               
                                                                                
         BRAS  RE,GETCRED          GET CREDITED INVOICE IN AIO2                 
         JNE   XIT                                                              
         L     R4,AIO2                                                          
         MVI   ELCODE,TAINELQ      GET INVOICE ELEMENT                          
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         USING TAIND,R4                                                         
         OC    TAINHDTE,TAINHDTE   PUR PRINT DATE                               
         BZ    SEFDT10                                                          
         MVC   TBEFFDTE,TAINHDTE                                                
         J     XIT                                                              
*                                                                               
SEFDT10  OC    TAINBDTE,TAINBDTE   IF NO PUR PRINT DATE, GET BILLED DT          
         JZ    XIT                                                              
         MVC   TBEFFDTE,TAINBDTE                                                
         J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================*         
* READ CREDITED (ORIGINAL) INVOICE RECORD IN AIO2                               
* IF INVOICE DOES NOT EXIST, RETURN CC NEQ                                      
*=====================================================================*         
GETCRED  NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO1                                                         
         MVI   ELCODE,TANUELQ                                                   
         MVI   WORK,TANUTINV                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         JNE   NO                                                               
         USING TANUD,R4          GET CREDIT OF INVOICE ELEMENT                  
         L     R4,TGELEM         ADDRESS OF ELEMENT                             
         MVC   WORK(6),TANUMBER  CREDITED INVOICE NUMBER                        
         XC    WORK(6),=6X'FF'   COMPLEMENT                                     
         MVC   TEMP(L'TGINV),TGINV   SAVE ACTUAL INVOICE NUMBER                 
         DROP  R4                                                               
*                                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLINCDQ,(X'A0',WORK)                                 
         MVC   AIO,AIO1                                                         
         MVC   TGINV,TEMP        RESTORE ACTUAL INVOICE NUMBER                  
         JNE   NO                                                               
         J     YES                                                              
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================*         
* GET SYSTEM RECORD TO SAVE GST, CAN CONV RATE, AND EURO CONV RATE              
* IF INVOICE IS A NEW STYLE CREDIT, GET SYSTEM RECORD FROM EFFECTIVE            
* DATE OF ORIGINAL INVOICE BEING CREDITED                                       
*=====================================================================*         
GETSYS   NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         BZ    GSYS50                                                           
         L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         USING TAPDD,R4                                                         
         BRAS  RE,GETEL                                                         
         BNE   GSYS50                                                           
         TM    TAPDPST2,TAPDPCIB   AND IF CREDIT INVOICE HAS SAVED              
         BZ    GSYS50              BILLING RATES, USE EFFECTIVE DATE            
         DROP  R4                                                               
                                                                                
         USING TLSYD,R4                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLSYCD,TLSYCDQ          READ SYSTEM RECORD                       
         MVI   TLSYTYPE,TLSYSAVE       SAVED SYSTEM RECORD                      
         MVC   TLSYSVDT,TBEFFDTE       EFFECTIVE DATE                           
         GOTO1 HIGH                                                             
         CLC   KEY(TLSYLEN-TLSYD),KEYSAVE  IF IT DOES NOT EXIST, READ           
         BNE   GSYS50                      TODAY'S SYSTEM RECORD                
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
         B     GSYS60                                                           
         DROP  R4                                                               
                                                                                
GSYS50   MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)  GET TODAY'S SYSTEM RECORD         
         BE    *+6                                                              
         DC    H'0'                                                             
GSYS60   L     R4,AIO                                                           
         USING TASYD,R4                                                         
         MVI   ELCODE,TASYELQ      GET SYSTEM ELEMENT                           
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         ZAP   TBCCVT,TASYCCVT     SET CANADIAN CONVERSION RATE                 
         ZAP   TBECVT,TASYECVT     AND SET EURO CONVERSION RATE                 
                                                                                
         MVC   TGGSTRAT,=AL4(GSTRT)   DEFAULT 5.00% GST                         
         L     R4,AIO                                                           
         USING TAPCD,R4                                                         
         MVI   ELCODE,TAPCELQ         GET CANADIAN PROVINCE ELEMENT             
         BRAS  RE,GETEL                                                         
         BNE   GSYSX                                                            
         MVC   TGGSTRAT,TAPCGSTR      SET CANADIAN GST RATE                     
                                                                                
GSYSX    MVC   AIO,AIO1                                                         
         J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*=============================*                                                 
* MAKE EBILL VISIBLE          *                                                 
*=============================*                                                 
MAKVIS   NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,EB           IF THIS IS EBILL                             
         JNE   YES                                                              
         TM    STAT3,STCLPRTQ      CLOSE PRINT QUEUE                            
         JZ    YES                                                              
                                                                                
         LA    RF,TBILLD                                                        
         AH    RF,=Y(TBOPTS3-TBILLD)  RF -> TBOPTS3                             
         TM    0(RF),TBECPYPR      ANY INVOICES ON THE PRINT QUEUE?             
         JZ    YES                 (RELEASED PUR'S NOT PRINTED)                 
                                                                                
         L     R4,ATWA                                                          
         USING T703FFD,R4                                                       
         L     R4,TWAMASTC         MASTC                                        
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
                                                                                
         LA    R0,1(R4)            R0=A(USER KEY)                               
         GOTO1 DATAMGR,DMCB,(0,=C'CLOSE'),=C'PRTQUE',(R0),(R4),REMOTABF         
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         GOTO1 DATAMGR,DMCB,(0,=C'VISIBLE'),=C'PRTQUE',(R0),(R4),      C        
               REMOTABF                                                         
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         J     YES                                                              
                                                                                
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*=============================*                                                 
*  TEST TO MAKE EBILL VISIBLE *                                                 
*=============================*                                                 
*                                                                               
*                                                                               
*        CHECK IF NEED TO CALL GUARANTEE ROUTINE                                
*              WHEN CALLING THIS ROUTINE - CALLER SETS DMCB FOR                 
*                   CALL TO TAPPGUAR                                            
*              P1         - (RC)                                                
*              P2         - A(CHECK RECORD)                                     
*              P2 BYTE 0  - STATUS BYTE TO SEND TO TAPPGUAR                     
*              P3         - A(INVOICE RECORD) OR 0                              
*              P3 BYTE 0  - STATUS BYTE 2 TO SEND TO TAPPGUAR                   
*              P4         - SYSCOMM                                             
*                                                                               
         SPACE 1                                                                
GUARCK   NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO1             A(INVOICE RECORD)                            
         USING TAIND,R4            FIND INVOICE STATUS ELEMENT                  
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'0'                WAS ALREADY READ                             
*                                                                               
         MVI   0(R1),0             SET FOR RETURN                               
         TM    TBOPTS,TBRERUN      IF JOB IS A RERUN - USE                      
         JO    YES                                                              
         TM    TAINSTAT,TAINSCHK   OR IF CHECKS WERE ALREADY WRITTEN            
         JO    YES                                                              
         TM    TAINSTA2,TAINSHLP   OR IF PUR INVOICE ALREADY PRINTED            
         JO    YES                                                              
         TM    TAINSTAT,TAINSCIN   OR IF IT'S IS A CANCELLED INVOICE            
         JNO   GC10                                                             
         L     R4,AIO1                                                          
         USING TAPDD,R4                                                         
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         L     R2,TAPDGUAR         AND GUAR CREDITS = 0                         
         LTR   R2,R2                                                            
         JZ    YES                                                              
*                                                                               
GC10     GOTO1 =V(TAPPGUAR)        DON'T GO TO TAPPGUAR                         
         JE    YES                                                              
         MVC   BYTE,0(R1)          SET ERROR TO RETURN TO CALLER                
*                                                                               
         J     NO                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        ADD PNH OF PERFORMER TO CORRECT UNION                                  
*                                                                               
         USING TABCHKD,R3                                                       
PNHUNI   NTR1  BASE=*,LABEL=*                                                   
         MVC   TFULL,TABPAYI       PAYMENT AMOUNT                               
         OC    TFULL,TFULL         IS THIS A CORP                               
         BNZ   PUN10                                                            
         MVC   TFULL,TABPAYC                                                    
*                                                                               
PUN10    CLI   TBUNI1,C' '         FIRST CHECK OF INVOICE                       
         BH    PUN20                                                            
         MVC   TBUNI1(L'TBUNI1+L'TBLOC1),TABUNI  SAVE UNION+LOCAL               
         B     PUN30                                                            
*                                                                               
PUN20    CLC   TBUNI1(L'TBUNI1+L'TBLOC1),TABUNI  SAME UNION + LOCAL             
         BNE   PUN40                    NO - CHECK NEXT ENTRY                   
*                                                                               
PUN30    L     R1,TBPNH1      TOTAL P&H PER UNION/LOCAL                         
         A     R1,TABPNH                                                        
         ST    R1,TBPNH1                                                        
*                                                                               
         L     R1,TBHNW1      TOTAL H&W PER LOCAL                               
         A     R1,TABHNW                                                        
         ST    R1,TBHNW1                                                        
*                                                                               
         L     R1,TBGPAY1     GROSS PAYMENT = SUBJECT TO PNH                    
         A     R1,TABSPNH                                                       
         ST    R1,TBGPAY1                                                       
         B     PUNX                                                             
*                                                                               
PUN40    CLI   TBUNI2,C' '         NO INPUT YET                                 
         BH    PUN50                                                            
         MVC   TBUNI2(L'TBUNI2+L'TBLOC1),TABUNI  SAVE IN 2ND UNION              
         B     PUN60                                                            
*                                                                               
PUN50    CLC   TBUNI2(L'TBUNI2+L'TBLOC2),TABUNI                                 
         BNE   PUN70                                                            
*                                                                               
PUN60    L     R1,TBPNH2                                                        
         A     R1,TABPNH                                                        
         ST    R1,TBPNH2                                                        
*                                                                               
         L     R1,TBHNW2           TOTAL H&W PER LOCAL                          
         A     R1,TABHNW                                                        
         ST    R1,TBHNW2                                                        
*                                                                               
         L     R1,TBGPAY2          GROSS PAYMENT                                
         A     R1,TABSPNH                                                       
         ST    R1,TBGPAY2                                                       
         B     PUNX                                                             
*                                                                               
PUN70    CLI   TBUNI3,C' '         NO INPUT YET                                 
         BH    PUN80                                                            
         MVC   TBUNI3(L'TBUNI3+L'TBLOC1),TABUNI                                 
         B     PUN90                                                            
*                                                                               
PUN80    CLC   TBUNI3(L'TBUNI3+L'TBLOC3),TABUNI                                 
         BNE   PUN100                                                           
*                                                                               
PUN90    L     R1,TBPNH3                                                        
         A     R1,TABPNH                                                        
         ST    R1,TBPNH3                                                        
*                                                                               
         L     R1,TBHNW3           TOTAL H&W PER LOCAL                          
         A     R1,TABHNW                                                        
         ST    R1,TBHNW3                                                        
*                                                                               
         L     R1,TBGPAY3          GROSS PAYMENT                                
         A     R1,TABSPNH                                                       
         ST    R1,TBGPAY3                                                       
         B     PUNX                                                             
*                                                                               
PUN100   DS    0H                                                               
         L     R1,TBPNH4                                                        
         A     R1,TABPNH                                                        
         ST    R1,TBPNH4                                                        
*                                                                               
         L     R1,TBHNW4           TOTAL H&W PER LOCAL                          
         A     R1,TABHNW                                                        
         ST    R1,TBHNW4                                                        
*                                                                               
         L     R1,TBGPAY4          GROSS PAYMENT                                
         A     R1,TABSPNH                                                       
         ST    R1,TBGPAY4                                                       
*                                                                               
PUNX     J     XIT                                                              
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        CHECK IF SSN IS ONE FOR AOS FBS                                        
*----------------------------------------------------------------------         
         USING TABCHKD,R3                                                       
AOSSSN   NTR1  BASE=*,LABEL=*                                                   
         LA    R4,AOSSSNTB         R4=A(AOS SSN TABLE)                          
*                                                                               
AOSSSN5  CLI   0(R4),X'FF'         END OF TABLE                                 
         JE    NO                                                               
         CLC   TABSSN,0(R4)                                                     
         JE    YES                                                              
         AHI   R4,L'AOSSSNTB                                                    
         B     AOSSSN5                                                          
*                                                                               
AOSSSNTB DS    0CL9                TALIM ALSO HAS SIMILAR TABLE W/ RATE         
         DC    CL9'000112843'                                                   
         DC    CL9'000112851'                                                   
         DC    CL9'000117357'                                                   
         DC    CL9'000117358'                                                   
         DC    CL9'000117359'                                                   
         DC    CL9'000117360'                                                   
         DC    CL9'000117361'                                                   
         DC    CL9'000117362'                                                   
         DC    CL9'000117363'                                                   
         DC    CL9'000117364'                                                   
         DC    CL9'000117365'                                                   
         DC    X'FF'                                                            
*                                                                               
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        GET MISC AMOUNT ELEMENT FROM CHECK FOR AOS                             
*----------------------------------------------------------------------         
         USING TAMAD,R4                                                         
GETMAD   NTR1  BASE=*,LABEL=*                                                   
         L     R4,TIAREC           R4=A(CHECK RECORD)                           
         MVI   ELCODE,TAMAELQ      FIND MISC ELEMENT                            
         BRAS  RE,GETEL                                                         
GETMAD10 BNE   GETMADX             NONE, LEAVE                                  
         CLI   TAMATYPE,TAMATYPA   AOS ONLY                                     
         BE    GETMAD20                                                         
         BRAS  RE,NEXTEL                                                        
         B     GETMAD10                                                         
*                                                                               
GETMAD20 MVC   AOSBASE,TAMAASBS    AOS BASE                                     
         MVC   AOSAMT,TAMAASAM     AOS AMOUNT                                   
         MVC   AOSPSTRT,TAMAASPR   AOS PST RATE                                 
         MVC   AOSPST,TAMAASPA     AOS PST AMOUNT                               
*                                                                               
GETMADX  XIT1                                                                   
*                                                                               
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*        UPDATE THE INVOICE RECORD - UPDATE AND ADD ELEMENTS                    
*               RECORD AT THIS POINT IN AIO                                     
*                                                                               
UPDT     NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO1                                                         
         MVI   BYTE,C'N'           INIT FLAG TO NOT ADD TAFN EL                 
         TM    TBOPTS2,TBFIXINV    IF FIXING INVOICES                           
         BO    *+12                                                             
         TM    TBOPTS,TBRERUN      AND FILE MARKING VERSION                     
         BO    UPD5                                                             
         L     R4,AIO                                                           
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   UPD5                                                             
         USING TACOD,R4            R4=A(COMMERCIAL DETAILS ELEMENT)             
         MVC   TACOEXP,TBCOEXP     RESET INCASE COMML EXP DATE UPDATED          
         CLI   TACOLEN,TACOLNQ2    NEW STYLE?                                   
         BL    UPD5                NO, SKIP                                     
         MVC   TACOAEXP,TBACTEXP   RESET INCASE ACTRA EXP DATE UPDATED          
*                                                                               
UPD5     GOTOR UPDEMS              UPDATE EMS BILLING DETAILS                   
*                                                                               
         L     R4,AIO                                                           
         USING TABDD,R4            BILL DETAILS ELEMENT                         
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   UPD50                                                            
         TM    TBOPTS2,TBFIXINV    IF FIXING INVOICES                           
         BO    UPD10               DON'T CHECK IF RERUN                         
         TM    TBOPTS,TBRERUN      IF JOB IS A RE-RUN                           
         BO    UPD30                                                            
*                                                                               
UPD10    TM    TBSTAT,TAINSCIN     OR IF CANCELLED INVOICE                      
         BO    UPD30                                                            
         TM    TBSTA2,TAINSHLR     OR IF INVOICE IS RELEASED                    
         BNO   UPD20                                                            
         TM    TBSTA2,TAINSHLP        & WAS PRINTED                             
         BO    UPD30               THEN INFO ALREADY SET                        
*                                                                               
UPD20    MVI   BYTE,C'Y'           SET FLAG TO ADD TAFN EL                      
         MVC   TABDTYPE,GTYPE                                                   
         MVC   TABDTOT,TBTOT       TOTAL                                        
         MVC   TABDTAX,TBTAX       TAX                                          
         MVC   TABDHND,TBHANDI     INDIVIDUAL HANDLING FEE                      
         MVC   TABDHNDC,TBHANDC    CORPORATE HANDLING FEE                       
         MVC   TABDFICR,FICR       FICA CREDIT                                  
         MVC   TABDCSF,TCSF        CONTRACT SERVICE FEE                         
         CLI   TABDLEN,TABDLN2Q    OLD WAY?                                     
         BL    UPD21                                                            
         MVC   TABDACOM,TACOMM     AGENCY COMMISSION                            
         MVC   TABDSIGN,TASIGN     SIGNATORY FEE                                
UPD21    TM    TBIPST2,TAPDPEUR    IF THIS IS A EURO PAYMENT                    
         BZ    UPD22                                                            
         ZAP   TABDCCVT,TBGCVT     SET EURO CONVERSION RATE                     
UPD22    TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   UPD25                                                            
         ZAP   TABDCCVT,TBGCVT     SET CANADIAN CONVERSION RATE                 
         MVC   TABDGST,TBCGST      GST IN CAN$                                  
         L     RF,AOSPST                                                        
         A     RF,TBCPST                                                        
         ST    RF,TABDPST                                                       
         OI    TABDSTAT,TABDSCNW   INVOICE PRINTED NEW WAY                      
         B     UPD30                                                            
UPD25    MVC   TABDGST,TBGSTU      ELSE, SET POSSIBLE GST FOR US$               
         L     RF,AOSPST                                                        
         ICM   RE,15,TBPSTU                                                     
         AR    RF,RE                                                            
         ST    RF,TABDPST                                                       
*                                                                               
UPD30    CLI   TBNPOST,C'Y'        IF THIS IS NOT A POSTING INVOICE             
         BE    UPD40                                                            
         TM    STATUS,STCOD        OR IF THIS IS A COD - THEN DON'T ADD         
         BO    UPD40                  IT TO $ ACCUMULATOR                       
         MVC   TBTOT,TABDTOT                                                    
*                                                                               
UPD40    CLI   BYTE,C'Y'           CHECK IF SHOULD ADD TAFN EL                  
         BNE   UPD50                                                            
         CLC   TBATTN,SPACES       IF HAVE ATTENTION NAME                       
         BE    UPD50                                                            
         LA    R4,ELEMENT                                                       
         USING TAFND,R4            ADD NAME ELEMENT FOR IT                      
         XC    ELEMENT,ELEMENT                                                  
         MVI   TAFNEL,TAFNELQ                                                   
         MVI   TAFNTYPE,TAFNTATT                                                
         SPACE                                                                  
         LA    R1,TBATTN+L'TBATTN-1  INIT R1 TO A(END OF NAME FIELD)            
UPD42    CLI   0(R1),X'40'           CHECK FOR FIRST NON-SPACE                  
         BNE   UPD44                                                            
         BCT   R1,UPD42              KEEP BACKING UP TILL FIND ONE              
         SPACE                                                                  
UPD44    LA    R2,TBATTN                                                        
         SR    R1,R2               SET R1=L'NAME-1                              
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TAFNNAME(0),TBATTN                                               
         SPACE                                                                  
         AHI   R1,4                R1+4 = L'ELEMENT                             
         STC   R1,TAFNLEN                                                       
         GOTO1 ADDELEM                                                          
*                                                                               
UPD50    GOTO1 =A(ADSSUR),DMCB,SUISURCA    ADD SURCHARGE ELEMENTS?              
         TM    TBISTA2,TAPDSSUB    SKIP - IF SUBSIDIARY INVOICE                 
         BNO   UPD60                                                            
         CLI   RECNUM,BI           ON BILLING COPY                              
         BE    *+12                                                             
         CLI   RECNUM,PB           OR PRINT BILLING                             
         BNE   UPD60                                                            
         GOTO1 EXTRCT,DMCB,(RC)    AND EXTRACTING                               
         BE    UPX                                                              
*                                                                               
UPD60    L     R1,CTGROSS          TOTAL GROSS WAGES                            
         A     R1,TPAYI                                                         
         ST    R1,CTGROSS                                                       
         L     R1,CTMISC           TOTAL MISC PAYMENTS                          
         A     R1,TPAYC                                                         
         A     R1,TREXP                                                         
         TM    TBISTAT,TAPDSCAN    IF THIS IS US $ PAYMENT                      
         BO    UPD63                                                            
         A     R1,TBGSTU           POSSIBLE US$ GST                             
         ICM   RE,15,TBPSTU        POSSIBLE US$ PST                             
         AR    R1,RE                                                            
UPD63    ST    R1,CTMISC                                                        
         L     R1,CTTOTAL          GROSS + MISC PAYMENTS                        
         A     R1,TBGM                                                          
         TM    TBISTAT,TAPDSCAN    IF THIS IS US $ PAYMENT                      
         BO    UPD65                                                            
         A     R1,TBGSTU           POSSIBLE US$ GST                             
         ICM   RE,15,TBPSTU        POSSIBLE US$ PST                             
         AR    R1,RE                                                            
UPD65    ST    R1,CTTOTAL                                                       
         DROP  R4                                                               
                                                                                
         USING TXTABD,R5                                                        
         L     R5,ATAXDSCT                                                      
                                                                                
UPD70    CLI   TXTABUN,X'00'                                                    
         BE    UPD80                                                            
         USING TAXTD,R4                                                         
         LA    R4,ELEMENT                                                       
         MVI   TAXTEL,TAXTELQ                                                   
         MVI   TAXTLEN,TAXTLNQ                                                  
         MVC   TAXTUNIT(2),TXTABUN                                              
         MVI   TAXTUNIT+2,C' '                                                  
         MVC   TAXTTAXS,TXTABTAX                                                
         GOTO1 ADDELEM             ADD POSTING DETAILS ELEMENT NOW              
         AHI   R5,TXTABLNQ                                                      
         B     UPD70                                                            
         DROP  R4                                                               
                                                                                
         USING TMD,R1                                                           
UPD80    L     R1,ATALIM           IF USING NEW BILLING RATES                   
         OC    TMACURRA,TMACURRA                                                
         JZ    UPX                                                              
                                                                                
         MVI   TGBYTE,0            CLEAR FLAG TO ADD ELEMENT                    
         L     R4,AIO                                                           
         USING TARAD,R4            BILLING RATES ELEMENT                        
         MVI   ELCODE,TARAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    UPD90                                                            
         MVI   TGBYTE,1            SET FLAG TO ADD ELEMENT                      
         LA    R4,ELEMENT                                                       
UPD90    L     RF,TMACURRA                                                      
         MVC   TARAD(TARALNQ),0(RF)                                             
         MVC   TARATYPE,GTYPE                                                   
         CLI   TGBYTE,0            DOES ELEMENT ALREADY EXIST?                  
         BE    UPD100                                                           
         GOTO1 ADDELEM             ADD BILLING RATES ELEMENT NOW                
         DROP  R1,R4                                                            
                                                                                
         USING TMD,RE                                                           
UPD100   L     RE,ATALIM           IF USING NEW BILLING RATES                   
         OC    TMACURPG,TMACURPG   IF CAPTURING POSTINGS AT                     
         JZ    UPX                 NEW LOWER LEVEL ...                          
         L     RF,TMACURPG                                                      
                                                                                
         TM    TBSTAT,TAINSCIN     CANCELLER - ALREADY HAS TAPG ELEM            
         JO    UPX                                                              
                                                                                
         USING TAPGD,R4                                                         
         LA    R4,ELEMENT                                                       
         MVC   TAPGD(TAPGLNQ),0(RF)                                             
         GOTO1 ADDELEM             ADD POSTING DETAILS ELEMENT NOW              
                                                                                
UPX      J     XIT                                                              
         DROP  R4,RE                                                            
         EJECT                                                                  
*                                                                               
*                                                                               
*              SAVE SUI SURCHARGE TAXES IN TAXT ELEMENT                         
*                                                                               
ADSSUR   NTR1                                                                   
         TM    TGSYSTA2,TASYSNCS   ALLOW TO CHARGE IT?                          
         JZ    UPX                 NO, LEAVE                                    
                                                                                
         L     R2,0(R1)                                                         
         LA    R3,ADSSTB                                                        
ADSSUR10 CLI   0(R3),X'FF'         ANYMORE?                                     
         JE    UPX                                                              
         OC    0(4,R2),0(R2)       SKIP ZERO SURCHARGES                         
         JZ    ADSSUR30                                                         
         XC    ELEMENT,ELEMENT                                                  
         USING TAXTD,R4                                                         
         LA    R4,ELEMENT                                                       
         MVI   TAXTEL,TAXTELQ                                                   
         MVI   TAXTLEN,TAXTLNQ                                                  
         MVC   TAXTUNIT(2),0(R3)                                                
         MVI   TAXTUNIT+2,C' '                                                  
         MVC   TAXTTAXS,0(R2)                                                   
         GOTO1 ADDELEM             ADD POSTING DETAILS ELEMENT NOW              
ADSSUR30 AHI   R3,2                                                             
         AHI   R2,4                                                             
         J     ADSSUR10                                                         
                                                                                
ADSSTB   DC    CL2'CA'                                                          
         DC    CL2'NY'                                                          
         DC    X'FF'                                                            
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*              SET ADDITIONAL BILLING RULES FOR OFFICE O                        
*                                                                               
ADDLBR   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R4,AIO              GET BILLING RULES ELEMENT                    
         USING TABRD,R4                                                         
         MVI   ELCODE,TABRELQ                                                   
         BRAS  RE,GETEL                                                         
         B     ADDLBR7                                                          
ADDLBR5  BRAS  RE,NEXTEL                                                        
ADDLBR7  BNE   ADDLBRX                                                          
         TM    TABRSTAT,TABRSACP   SPECIAL ACOPY BILLING RULES                  
         BZ    ADDLBR5                                                          
*                                                                               
         MVC   ADRBTYPE,TABRTYPE                                                
         MVC   ADRFUTA(L'TABRRATE),TABRFUTA   BILLING RATES                     
ADDLBRX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              SET COMMENT FOR SOAP RESIDUAL INVOICES                           
*                                                                               
GETSOAP  NTR1  BASE=*,LABEL=*                                                   
         OC    TBCOM2,TBCOM2       DON'T BOTHER IF SOMETHING IN COMMENT         
         BNZ   GETSOAPX                                                         
         XC    BLOCK(60),BLOCK     MAKE BLOCK DUMMY FIELD FOR CHAROUT           
         MVI   BLOCK,60            L'FLD=8(L'HDR)+52(MAX L'FLD IN PAY)          
         GOTO1 CHAROUT,DMCB,TACMELQ,BLOCK,TACMTYPS  EPISODE INPUT               
         BNE   GETSOAPX                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TASBELQ      GET SOAP CABLE ELEMENT                       
         BRAS  RE,GETEL                                                         
         BNE   GETSOAP5                                                         
         SPACE                                                                  
         USING TASBD,R4                                                         
         USING CABLED,R3                                                        
         LA    R3,BLOCK+60                                                      
         XC    0(CABLNQ,R3),0(R3)                                               
         MVC   CABLABLE,=C'GRS/EPI'       MOVE IN LABLE                         
         EDIT  TASBGRSE,(11,CABGRSE),2    EDIT GRS/EPI AMOUNT                   
         MVC   CABCOMMA,=C','             MOVE IN COMMA                         
         MVC   CABEPIS,BLOCK+8            MOVE EPIS INPUT AFTER GRS/EPI         
         GOTO1 SQUASHER,DMCB,(R3),CABLNQ  SQUASH IT                             
         MVC   TBCOM2,0(R3)               MOVE TO COMMENT FIELD                 
         B     GETSOAPX                                                         
         SPACE                                                                  
GETSOAP5 MVC   TBCOM2(14),=C'EPISODES PAID:'                                    
         MVC   TBCOM2+15(52),BLOCK+8                                            
GETSOAPX XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE GETS LINKED US/CAN INVOICE NUMBER FROM                   
*              INVOICE RECORD AND SAVES THIS IN THE COMMENT                     
*                                                                               
         SPACE 1                                                                
GETUSCN  NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO                                                           
         USING TAUCD,R4                                                         
         MVI   ELCODE,TAUCELQ                                                   
         BRAS  RE,GETEL            GET US/CAN INVOICE ELEMENT                   
         BNE   GUSCNX                                                           
*                                                                               
         LA    R3,TBCOM2                                                        
         OC    TBCOM2,TBCOM2                                                    
         BZ    GUSCN20                                                          
         LA    R3,TBCOM2+L'TBCOM2-1  SCAN BACKWARDS LOOKING                     
         LA    R1,L'TBCOM2-1         FOR NEXT AVAILABLE SPOT                    
GUSCN10  CLI   0(R3),C' '                                                       
         BH    *+14                                                             
         BCTR  R3,0                                                             
         BCT   R1,GUSCN10                                                       
         B     *+8                                                              
         LA    R3,2(R3)                                                         
*                                                                               
GUSCN20  CLC   TAUCINU,TGINV       US OR CAN INVOICE?                           
         BE    GUSCN30                                                          
         MVC   0(2,R3),=C'U$'                                                   
         MVC   WORK,TAUCINU        US LINKED INVOICE                            
         B     GUSCN40                                                          
*                                                                               
GUSCN30  MVC   0(2,R3),=C'C$'                                                   
         MVC   WORK,TAUCINC        CAN LINKED INVOICE                           
*                                                                               
GUSCN40  XC    WORK,=6X'FF'        UNCOMPLEMENT                                 
         GOTO1 TINVCON,DMCB,WORK,2(R3),DATCON                                   
         DROP  R4                                                               
*                                                                               
GUSCNX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE GETS ADVANCE RECONCILIATION ELEMENT IF P+                
*                                                                               
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
         USING TANUD,R4                                                         
GETCNU   NTR1  BASE=*,LABEL=*                                                   
         CLC   =CL3'EVE',TBUSE     IF P+ BILLING,                               
         JNE   XIT                                                              
         L     R4,TIAREC                                                        
         MVI   ELCODE,TANUELQ                                                   
         BRAS  RE,GETEL            GET ADVANCE RECONC. ELEMENT                  
         B     *+8                                                              
GCNU10   BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         CLI   TANUTYPE,TANUTARE                                                
         BNE   GCNU10                                                           
         MVC   TABADVR,TANUOVAM    GET ADVANCE RECONC. AMOUNT                   
         J     XIT                                                              
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE GETS TAX UNIT ELEMENT IF P+                              
*                                                                               
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
         USING TATUD,R4                                                         
GETCTU   NTR1  BASE=*,LABEL=*                                                   
         CLC   =CL3'EVE',TBUSE     IF P+ BILLING,                               
         JNE   XIT                                                              
         L     R4,TIAREC                                                        
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL            GET ADVANCE RECONC. ELEMENT                  
         B     *+8                                                              
GCTU10   BRAS  RE,NEXTEL                                                        
         BNE   GCTU20                                                           
         CLC   =CL3'FD ',TATUUNIT  GET FEDERAL ELEMENT                          
         BNE   GCTU10                                                           
         B     GCTU40                                                           
                                                                                
GCTU20   L     R4,TIAREC           IF NO FD ELEMENT,                            
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
GCTU30   BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         CLC   =CL3'CN ',TATUUNIT  GET CANADIAN ELEMENT                         
         BNE   GCTU30                                                           
                                                                                
GCTU40   MVC   TABCNTR,TATUSTRE    GET TAXABLE REIMB FOR NON-IND.               
         J     XIT                                                              
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE READS COMMERCIAL & UPDATES 1ST AIR DATE,                 
*              2ND SEASON 1ST AIR DATE AND EXPIRATION DATE                      
*                                                                               
         SPACE 1                                                                
UPDCAIR  NTR1  BASE=*,LABEL=*                                                   
         XC    TGFULL,TGFULL                                                    
         MVC   AIO,AIO2            SET TO READ COMML VIA INTERNAL NO.           
         BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BNE   UPDCA2                                                           
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'30',0)                                   
         B     UPDCA3                                                           
UPDCA2   GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'20',0)                                   
UPDCA3   MVC   AIO,AIO1            RESET IF IN ERROR                            
         BNE   UPDCANO                                                          
         MVC   AIO,AIO2                                                         
         L     R4,AIO              GET TACO ELEMENT                             
         USING TACOD,R4                                                         
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   UPDCANO                                                          
         ST    R4,TGFULL           SAVE ADDRESS                                 
*                                                                               
         CLI   TACOLEN,TACOLNQ2    NEW STYLE?                                   
         BL    *+10                NO, SKIP                                     
         MVC   TBACTEXP,TACOAEXP   ACTRA EXP DATE FROM COMML                    
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK                                             
*                                                                               
         TM    TGUSSTAT,SESSION    IF THIS IS RE-USE PAYMENT                    
         BO    UPDCAYES                                                         
         TM    TGUSSTA2,HLDTYPE    BUT NOT A HOLDING FEE TYPE                   
         BO    UPDCAYES                                                         
*                                                                               
         LA    R1,NOAIRTAB         AND NOT IN EXCLUDED USE TABLE                
UPDCA5   CLI   0(R1),X'FF'              TEST NOT END OF TABLE                   
         BE    UPDCA8                                                           
         CLC   TGUSEQU,0(R1)            TEST FOR MATCH                          
         BE    UPDCAYES                                                         
         LA    R1,1(R1)                 BUMP TO NEXT ENTRY                      
         B     UPDCA5                                                           
*                                                                               
UPDCA8   OC    TACOAIR,TACOAIR     IF THERE IS NO AIR DATE                      
         BNZ   UPDCA10                                                          
         MVC   TACOAIR,TBCDTS      THEN UPDATE 1ST AIR DATE                     
         MVI   CSWITCH,C'Y'                                                     
         MVC   TBAIR,TACOAIR       TO BE CYCLE START DATE                       
*                                                                               
         CLI   TACOCTYP,CCTY04A    IF COMMERCIAL IS ACTRA TYPE 2404A            
         BE    UPDCA8A                                                          
         CLI   TACOCTYP,CCTY2404   OR ACTRA TYPE 2404                           
         BNE   UPDCAYES                                                         
*                                                                               
*                                  CALCULATE AND SAVE ACTRA EXPIRY DATE         
         USING PERVALD,R2                                                       
UPDCA8A  OC    TBFDAT,TBFDAT       IS THERE A FILM DATE?                        
         BZ    UPDCA9                                                           
         GOTO1 DATCON,DMCB,(1,TBFDAT),(8,WORK)   FILM DATE + 24 MONTHS          
         MVC   WORK+8(6),=C'-(24M)'                                             
         LA    R2,BLOCK                                                         
         GOTO1 PERVAL,DMCB,(14,WORK),('PVIN1DYL',BLOCK)                         
         CLI   TACOLEN,TACOLNQ2    NEW STYLE?                                   
         BL    *+10                NO, SKIP                                     
         MVC   TACOAEXP,PVALPEND                                                
UPDCA9   OC    TACOAIR,TACOAIR    IS THERE AN AIR DATE?                         
         BZ    UPDCA9B                                                          
         GOTO1 DATCON,DMCB,(1,TACOAIR),(8,WORK)  AIR DATE + 18 MONTHS           
         MVC   WORK+8(6),=C'-(18M)'                                             
         LA    R2,BLOCK                                                         
         GOTO1 PERVAL,DMCB,(14,WORK),('PVIN1DYL',BLOCK)                         
         CLI   TACOLEN,TACOLNQ2    NEW STYLE?                                   
         BL    UPDCAYES            NO, SKIP                                     
         OC    TACOAEXP,TACOAEXP  IF NO EXPIRY DATE FROM FILM DATE,             
         BZ    UPDCA9A                                                          
         CLC   TACOAEXP,PVALPEND  OR EXPIRY FROM FILM DATE IS LATER             
         BL    UPDCA9B            THAN EXPIRY FROM AIR DATE,                    
UPDCA9A  MVC   TACOAEXP,PVALPEND  USE EXPIRY FROM AIR DATE                      
UPDCA9B  CLI   TACOLEN,TACOLNQ2    NEW STYLE?                                   
         BL    *+10                                                             
         MVC   TBACTEXP,TACOAEXP  UPDATE ACTRA EXPIRY DATE                      
         B     UPDCAYES                                                         
         DROP  R2                                                               
*                                                                               
UPDCA10  BRAS  RE,SET2AIR          SET SECOND FIRST AIR DATE                    
*                                                                               
UPDCAYES MVC   AIO,AIO1                                                         
         SR    RC,RC                                                            
UPDCANO  LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
*              TABLE OF USES TO EXCLUDE WHEN UPDATING FIRST AIR DATE            
*                                                                               
NOAIRTAB DC    AL1(UOTH,UOTM,UPEN,UPEM,UDWN,USOT,USPN,UREN,UOTC,UCPN)           
         DC    AL1(USRE,UGRT,ULFT,UCNL,UPRM,UPNH,UINF,UDOR,UPRL,UARN)           
         DC    AL1(USCN)                                                        
         DC    X'FF'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&DO                                                                           
*              ROUTINE DECIDES IF CSF FEE NEEDS TO BE CHARGED                   
*              AND SETS STATUS ON COMMERCIAL RECORD                             
*              R4 --> TACOEL                                                    
*                                                                               
SETCSF   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LR    R3,R4                                                            
         TM    TBSTA2,TAINSHLP                                                  
         BO    SCSF000                                                          
         TM    TBOPTS,TBRERUN      RERUN - DON'T CHARGE IF NONE                 
         BZ    SCSF010                                                          
SCSF000  L     R4,AIO1                                                          
         USING TABDD,R4                                                         
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   SCSFX                                                            
         OC    TABDCSF,TABDCSF     IS THERE A CSF AMOUNT?                       
         BZ    SCSFX                                                            
         B     SCSF600                                                          
*                                                                               
         USING TACOD,R4                                                         
SCSF010  LR    R4,R3                                                            
         TM    TACOSTA2,TACOSJPC   IF CSF FEE ALREADY PAID,                     
         BZ    SCSF020                                                          
         TM    STATUS,STCRDT       AND INVOICE IS A CREDIT,                     
         BZ    SCSFX                                                            
         TM    SVOPT4,TAPDOJPC     MUST BE USING PAY OPTION                     
         BZ    SCSFX                                                            
         B     SCSF030                                                          
*                                                                               
SCSF020  TM    STATUS,STCRDT       SKIP CREDIT INVOICES                         
         BO    SCSFX                                                            
SCSF030  TM    TBSTAT,TAINSCAN     SKIP CANCELLEE                               
         BO    SCSFX                                                            
         TM    TACOSTA2,TACOSNCS   SKIP IF COMM SAYS DON'T CHARGE               
         BO    SCSFX                                                            
         TM    TBSTAT,TAINSCIN     CANCELLER - SET STATUS AND SKIP              
         BZ    SCSF050                                                          
         BRAS  RE,CXLCSF           CHECK FOR CANCELLED CSF                      
         B     SCSFX                                                            
*                                                                               
SCSF050  CLC   TBFCYC,=X'A90701'   FFC MUST BE ON OR AFTER JUL01/09             
         BL    SCSFX                                                            
                                                                                
         TM    SVOPT4,TAPDOJPC     IF NOT USING PAY OPTION                      
         BO    SCSF100                                                          
         CLC   TBCDTS(3),=X'B30401' CYC START MUST BE BEFORE APR01/13           
         BNL   SCSFX                                                            
         OC    TBCDTS(3),TBCDTS     AND NOT BLANK                               
         BZ    SCSFX                                                            
*                                                                               
         TM    TBPRSTAT,TAPIJPCN   IF PRODUCT STATUS IS NOCSF,                  
         BO    SCSFX               SKIP                                         
         TM    TBPRSTAT,TAPIJPCY   IF PRODUCT STATUS IS CSF,                    
         BO    SCSF100             SEE IF USE IS VALID FOR CSF                  
         TM    TBCLSTA2,TACIJPCN   IF CLIENT STATUS IS NOCSF,                   
         BO    SCSFX               SKIP                                         
         TM    TBCLSTAT,TACIJPCY   IF CLIENT STATUS IS CSF,                     
         BO    SCSF100             SEE IF USE IS VALID FOR CSF                  
         TM    TBAYSTA6,TAAYJPCY   IF AGENCY STATUS IS NOT CSF,                 
         BZ    SCSFX               SKIP                                         
*                                                                               
SCSF100  CLI   TBMED,TACOMEDT      MEDIA MUST BE TV                             
         BE    SCSF120                                                          
         CLI   TBMED,TACOMEDC      OR CABLE                                     
         BNE   SCSFX                                                            
SCSF120  TM    TGUSSTAT,SESSION+CANUSE IF THIS IS RE-USE PAYMENT                
         BNZ   SCSFX                   AND NOT CANADIAN,                        
         TM    TGUSSTA2,HLDTYPE    BUT NOT A HOLDING FEE TYPE                   
         BO    SCSFX                                                            
*                                                                               
         CLI   TBCOTYPE,0          COMMERCIAL TYPE CAN BE BLANK                 
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYASIAN   ASIAN                                        
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYADD     ADDENDUM                                     
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYANIM    ANIMATICS                                    
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYPUB     PUBLIC SERVICE                               
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYSEAS2   SEASONAL                                     
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYSTRM    SHORT TERM                                   
         BE    SCSF200                                                          
         CLI   TBCOTYPE,CTYSPAN    OR SPANISH                                   
         BNE   SCSFX                                                            
*                                                                               
SCSF200  L     R3,ACHKTAB          CHECK TABLE                                  
         USING TABCHKD,R3                                                       
SCSF250  OC    0(TABDSK,R3),0(R3)  END OF TABLE                                 
***F250  OC    0(TABLEN,R3),0(R3)  END OF TABLE                                 
         BZ    SCSFX                                                            
         CLC   TABUNI,=C'SAG'      FIND ONE CAST WITH UNION SAG                 
         BE    SCSF300                                                          
         CLC   TABUNI,=C'AFT'      OR AFTRA                                     
         BE    SCSF300                                                          
         LA    R3,TABLEN(R3)       NEXT CHECK IN TABLE                          
         B     SCSF250                                                          
         DROP  R3                                                               
*                                                                               
SCSF300  LA    R1,NOCSFTAB         AND NOT IN EXCLUDED USE TABLE                
SCSF400  CLI   0(R1),X'FF'              TEST END OF TABLE                       
         BE    SCSF500                                                          
         CLC   TGUSEQU,0(R1)            TEST FOR MATCH                          
         BE    SCSFX                                                            
         LA    R1,1(R1)                 BUMP TO NEXT ENTRY                      
         B     SCSF400                                                          
*                                                                               
SCSF500  TM    STATUS,STCRDT       IF CREDITED INVOICE                          
         BZ    SCSF600                                                          
         TM    SVOPT4,TAPDOJPC     AND USING PAY OPTION                         
         BZ    SCSF600                                                          
         OI    STAT3,STCSFCR       SET STATUS TO CREDIT CSF FEE                 
         NI    TACOSTA2,X'FF'-TACOSJPC  TURN OFF JPC CSF PAID STATUS            
         MVI   CSWITCH,C'Y'                                                     
         B     SCSFX                                                            
*                                                                               
SCSF600  OI    TACOSTA2,TACOSJPC   SET STATUS JPC CSF PAID                      
         OI    STAT2,STCSF         SET STATUS TO TAKE CSF                       
         MVI   CSWITCH,C'Y'                                                     
*                                                                               
SCSFX    XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
*              TABLE OF USES TO EXCLUDE WHEN ADDING CSF FEE                     
*                                                                               
NOCSFTAB DC    AL1(UDWN,UPPF,UPRR,UPNH,UOTH,USOT,USRE,USPN,USLF,UALF)           
         DC    AL1(UARN,UGRT,ULFT,UPEN,UREN,UGRR,UPRT,USCS,USFS)                
         DC    X'FF'                                                            
*&&                                                                             
*&&DO                                                                           
*              TABLE OF USES TO EXCLUDE WHEN ADDING CSF FEE                     
*                                                                               
NOCSFTAB DC    AL1(UDWN,UCNL,UPPF,UPUB,UFGM,UNBM,UPRM,UPRR,URGM,UINS)           
         DC    AL1(URRS,UIFS,UFGS,UPNH,URRR,UISS,UOTH,UOTM,USSS,USHL)           
         DC    AL1(USMU,USOT,USRE,USPN,USNA,USCS,USFS,USRS,USLF,USCN)           
         DC    AL1(UADT,UADO,UADH,UADD,UALF,UARN,UARS,UARR,UDEM,UTAG)           
         DC    AL1(UGRT,UHLD,ULFT,UPEN,UPEM,UFMU,UIMS,UAUD,UREN,UGRR)           
         DC    AL1(UNBS,UMRR,UMUS,UPRT)                                         
         DC    X'FF'                                                            
*&&                                                                             
*&&DO                                                                           
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE DECIDES INVOICE CANCELLEE IS REVERSING CSF FEE           
*              IF YES, WILL SET STATUS TO REVERSE CSF FEE FOR PROD POST         
*              AIO1 HAS INVOICE RECORD                                          
*                                                                               
         SPACE 1                                                                
CXLCSF   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R4,AIO1                                                          
         USING TABDD,R4                                                         
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   CXCSFX                                                           
         OC    TABDCSF,TABDCSF     IS THERE A CSF AMOUNT?                       
         BZ    CXCSFX                                                           
         XR    R1,R1                                                            
         SHI   R1,CSFRATE          NEGATE THE CSF AMOUNT                        
         C     R1,TABDCSF          DOES INVOICE HAVE NEGATIVE CSF RATE?         
         BNE   CXCSFX                                                           
         CLI   TGCUR,C'C'          CANNOT BE CANADIAN                           
         BE    CXCSFX                                                           
         CLI   TGCUR,C'E'          CANNOT BE EUROS                              
         BE    CXCSFX                                                           
         OI    STAT2,STCSFC        SET STATUS TO CANCEL CSF FEE                 
CXCSFX   XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*                                                                               
*        ROUTINE LOOKS FOR USE IN FFCTAB TO SEE IF FFC ON CAST                  
*        RECORD SHOULD BE UPDATED WITH CYCLE START DATE OF PYMT                 
*                                                                               
LOOKFFC  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,FFCTAB                                                        
LFFC10   CLI   0(R1),X'FF'                                                      
         BE    LFFCNO                                                           
         CLC   TGUSEQU,0(R1)                                                    
         BNE   LFFC20                                                           
         CLI   TGUSTYP,ULFTFFCN                                                 
         BE    LFFCNO                                                           
         B     LFFCYES                                                          
LFFC20   LA    R1,1(R1)                                                         
         B     LFFC10                                                           
*                                                                               
LFFCYES  SR    RC,RC                                                            
LFFCNO   LTR   RC,RC                                                            
         XIT1                                                                   
FFCTAB   DC    AL1(ULFT,UALF,USLF)                                              
         DC    X'FF'                                                            
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO ADD POINTERS FROM AAPBLK                              
         SPACE 1                                                                
APTRS    NTR1  BASE=*,LABEL=*                                                   
         MVI   BYTE,X'08'                                                       
         TM    TBOPTS,TBTRACE                                                   
         BNO   *+8                                                              
         OI    BYTE,X'10'                                                       
         GOTO1 AADDPTRS,DMCB,(BYTE,ASPBLK),AAPBLK                               
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO SET SECOND SEASON'S FIRST AIR DATE                    
         SPACE 1                                                                
         USING TACOD,R4                                                         
SET2AIR  NTR1  BASE=*,LABEL=*                                                   
         CLI   TACOTYPE,CTYSEAS2   IF COMMERCIAL TYPE IS SEASONAL               
         BNE   S2AX                                                             
         TM    TGUSSTA2,HLDTYPE    AND THIS IS NOT A HOLDING FEE                
         BNZ   S2AX                                                             
         OC    TACOSAIR,TACOSAIR   AND THERE IS NO 2ND AIR DATE                 
         BNZ   S2A20                                                            
         SPACE 1                                                                
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(1,TACOAIR),(8,WORK)                                 
         MVI   TGBYTE,13                                                        
         MVC   WORK+8(5),=C'-(3M)'                                              
         TM    TBAYSTAT,TAAYS13W   DATE MUST BE MORE THAN 13 WEEKS/             
         BZ    S2A10               3 MONTHS AFTER FIRST AIR DATE                
         MVI   TGBYTE,14           TO QUALIFY AS SECOND FIRST AIR DATE          
         MVC   WORK+8(6),=C'-(13W)'                                             
         SPACE 1                                                                
S2A10    GOTO1 PERVAL,DMCB,(TGBYTE,WORK),('PVINSGLS+PVIN1DYL',BLOCK)            
         CLI   4(R1),PVRCOK                                                     
         BNE   S2AX                                                             
         SPACE 1                                                                
         CLC   TBCDTS,BLOCK+PVALPEND-PERVALD                                    
         BNH   S2AX                                                             
         MVC   TACOSAIR,TBCDTS                                                  
         MVI   CSWITCH,C'Y'                                                     
         B     S2AX                                                             
         SPACE 1                                                                
S2A20    XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(1,TACOSAIR),(8,WORK)                                
         MVI   TGBYTE,13                                                        
         MVC   WORK+8(5),=C'-(3M)'                                              
         TM    TBAYSTAT,TAAYS13W   DATE MUST BE MORE THAN 13 WEEKS/             
         BZ    S2A30               3 MONTHS AFTER SECOND AIR DATE               
         MVI   TGBYTE,14           TO QUALIFT AS FIRST AIR DATE                 
         MVC   WORK+8(6),=C'-(13W)'                                             
         SPACE 1                                                                
S2A30    GOTO1 PERVAL,DMCB,(TGBYTE,WORK),('PVINSGLS+PVIN1DYL',BLOCK)            
         CLI   4(R1),PVRCOK                                                     
         BNE   S2AX                                                             
         SPACE 1                                                                
         CLC   TBCDTS,BLOCK+PVALPEND-PERVALD                                    
         BNH   S2AX                                                             
         MVC   TACOAIR,TBCDTS                                                   
         XC    TACOSAIR,TACOSAIR                                                
         MVI   CSWITCH,C'Y'                                                     
S2AX     XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              SET CHECK DATE POINTER IN KEY                                    
         SPACE 2                                                                
         USING TLINPD,R1           USE INVOICE CHECK DATE POINTER               
SETCK    NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   LENKEY,TLINKDTE-TLINPD-1                                         
         MVI   TLINPKEY,TLINKCDQ                                                
         MVC   TLINDDTE,TBSPDATE   START DATE OF RUN                            
         MVC   TLINKAGY,TIFAGY     SET AGENCY                                   
SETCX    XIT1                                                                   
         DROP  R1                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              SAVE SIGNATORY FEE AND CAP INFORMATION                           
         SPACE 2                                                                
SAVESIGN NTR1  BASE=*,LABEL=*                                                   
         USING TARAD,RE                                                         
         L     RE,AAGYTARA                                                      
         CLI   TARAEL,TARAELQ      DO WE HAVE AN AGENCY TARA?                   
         BNE   SVSIG10                                                          
         MVC   SIGNRATE,TARAASFE+2  SIGNATORY RATE                              
         MVC   SIGNCAP,TARAASCA     SIGNATORY FEE CAP                           
         MVC   AYSTSIG,TARASGNS     SIG RULE STATUS                             
         B     SSIGNX                                                           
         DROP  RE                                                               
                                                                                
         USING TANUD,R4                                                         
SVSIG10  MVI   ELCODE,TANUELQ      GET SIGNATORY RATE                           
         GOTO1 GETL,DMCB,(1,=AL1(TANUTSGN))                                     
         BNE   SSIGNX                                                           
         L     R4,TGELEM                                                        
         ZIC   RF,TANULEN                                                       
         AHI   RF,-3                                                            
         GOTO1 CASHVAL,DMCB,TANUMBER,(RF)                                       
         CLI   0(R1),0                                                          
         BNE   SSIGNX                                                           
         MVC   SIGNRATE,6(R1)                                                   
         SPACE                                                                  
         GOTO1 GETL,DMCB,(1,=AL1(TANUTSCP))                                     
         BNE   SSIGNX             GET SIGNATORY FEE CAP                         
         L     R4,TGELEM                                                        
         ZIC   RF,TANULEN                                                       
         AHI   RF,-3                                                            
         GOTO1 CASHVAL,DMCB,TANUMBER,(RF)                                       
         CLI   0(R1),0                                                          
         BNE   SSIGNX                                                           
         MVC   SIGNCAP,4(R1)                                                    
SSIGNX   XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        SET ACTIVE POINTER IN KEY                                              
*                                                                               
SETACT   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         XC    KEY,KEY                                                          
         USING TLIND,R1            ACTIVE POINTER                               
         MVI   LENKEY,TLINAGY-TLIND-1                                           
         MVI   TLINKEY,TLINCDQ                                                  
         CLC   TIFAGY,SPACES       COMPARE ALL AGENCIES                         
         BNH   SETAX                                                            
         MVI   LENKEY,TLININV-TLIND-1                                           
         MVC   TLINAGY,TIFAGY      NO USE A SPECIFIC AGENCY                     
         OC    TLINAGY,SPACES                                                   
         OC    TBVINV,TBVINV       IF RUNNING A SPECIFIC INVOICE                
         BZ    SETAX                                                            
         MVI   LENKEY,TLININV+L'TLININV-TLIND-1                                 
         MVC   TLININV,TBVINV      SET INVOICE #                                
*                                                                               
SETAX    XIT1                                                                   
         DROP  R1                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*======================================================================         
*              ROUTINE EXTRACTS A POINTER FROM BLOCK                            
*                                                                               
*                                  P1 BYTE 0    = 1ST BYTE OF KEY               
*                                  P1=BYTES 1-3 = A(POINTER BLOCK)              
*======================================================================         
GETPTR   NTR1  BASE=*,LABEL=*                                                   
         XC    KEY,KEY             PRE-CLEAR KEY                                
         L     R4,0(R1)                                                         
         USING TLDRD,R4                                                         
*                                                                               
GPTR10   CLC   TLDRCD,0(R1)        MATCH ON RECORD CODE                         
         BE    GPTRX                                                            
         CLI   0(R4),0             TEST FOR END OF BLOCK                        
         BNE   *+6                                                              
         DC    H'0'                MISSING POINTER                              
*                                                                               
         LA    R4,L'TLDRREC(R4)    TRY NEXT                                     
         B     GPTR10                                                           
*                                                                               
GPTRX    MVC   KEY,TLDRREC         RETURN POINTER IN KEY                        
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*        INITIALIZATION                                                         
*                                                                               
         DS    0D                                                               
INIT     NMOD1 0,*INIT*                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC              GENCON DSECT                                
         SPACE                                                                  
         MVC   HEADHOOK,=V(MYHOOK)  SET HEADHOOK                                
         MVC   AMYTRACE,=A(MYTRACE) SET COMMON ROUTINES                         
         MVC   AMYTRAC2,=A(MYTRACE2)                                            
         MVC   ACVTTAB,=A(CVTTAB)                                               
         MVC   MULT,=A(MULTIPLY)                                                
         MVC   SETPCIF,=A(STSPECIF)                                             
         MVC   EXTRCT,=A(XTRCT)                                                 
*                                                                               
*&&DO                                                                           
***NO-OP                                                                        
***06/14                                                                        
         MVC   AIO,AIO2            GET SYSTEM RECORD FOR CANADIAN CVT           
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
         BE    *+6                 RATE                                         
         DC    H'0'                                                             
         L     R4,AIO                                                           
         USING TASYD,R4                                                         
         MVI   ELCODE,TASYELQ      GET SYSTEM ELEMENT                           
         BAS   RE,GETEL3                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         ZAP   TBCCVT,TASYCCVT     SET CANADIAN CONVERSION RATE                 
         ZAP   TBECVT,TASYECVT     AND SET EURO CONVERSION RATE                 
                                                                                
         MVC   TGGSTRAT,=AL4(GSTRT)   DEFAULT 5.00% GST                         
         L     R4,AIO                                                           
         USING TAPCD,R4                                                         
         MVI   ELCODE,TAPCELQ         GET CANADIAN PROVINCE ELEMENT             
         BAS   RE,GETEL3                                                        
         BNE   IN05                                                             
         MVC   TGGSTRAT,TAPCGSTR      SET CANADIAN GST RATE                     
         DROP  R4                                                               
*&&                                                                             
*                                                                               
IN05     L     R3,ATWA                                                          
         USING T703FFD,R3                                                       
         MVC   AMASTD,TWAMASTC     SAVE ADDRESS OF MASTER                       
         L     R2,TWADCONS                                                      
         USING TWADCOND,R2                                                      
         MVC   ALOGOC,TLOGOC       GET A(LOGOC)                                 
         DROP  R2                                                               
         LR    RE,R3               SAVE ADDRESSES OF SAVED PP BLOCK             
         A     RE,=A(6144)             AND ADD PP BLOCK - STARTING              
         ST    RE,ASPBLK               AT 6144 PAST BEGIN OF TWA AND            
         A     RE,=A(PPBLOCKL)         SPANNING A TOTAL OF 6144 BYTES           
         ST    RE,AAPBLK                                                        
*                                                                               
         L     R1,AMASTD                                                        
         USING MASTD,R1                                                         
         MVC   ALOGO,MCVLOGO       GET A(LOGO)                                  
         TM    TBOPTS2,TBFILE      IF ONLY DOING FILE RETRO INVOICES            
         BO    IN10                                                             
         TM    TBOPTS,TBRETRO      OR IF ONLY DOING RETRO INVOICES              
         BNO   IN20                                                             
*                                                                               
IN10     MVI   TWAWRITE,C'N'       SET WRITE = N IN TWA                         
         MVI   MCPOSTNG,C'N'       & POSTING = N IN MASTER                      
*                                                                               
IN20     CLI   MCRERUN,C'Y'        IF RERUN = Y IN MASTER                       
         BNE   IN30                                                             
         OI    TBOPTS,TBRERUN      THEN SET INTERNAL RERUN FLAG                 
         OC    TBSPDATE,TBSPDATE   IF START DATE = 0                            
         BNZ   IN30                                                             
         MVC   TBSPDATE,TGTODAY1   THEN SET START DATE = RERUN DATE             
*                                                                               
IN30     CLI   MCPOSTNG,C'N'       IF POSTING = N IN MASTER                     
         BNE   IN40                                                             
         OI    TBOPTS,TBNOPOST     THEN SET POST = N                            
         DROP  R1                                                               
*                                                                               
IN40     MVC   AIO,AIO1                                                         
         MVC   TPORIG+8(2),TWAORIG    REQUESTING ID                             
         DROP  R3                                                               
         GOTO1 USERVAL,DMCB,(X'80',TPORIG)      GET HEXCOMP OF TP               
         MVC   TPHEX,TGACCHX                                                    
         MVC   TPUSER,TGUSERID                                                  
         MVI   SWPOST,POSTRCV                                                   
         GOTO1 =V(TABIPOST),DMCB,(RC),EOPENWK                                   
         MVC   TBTODAY,TGTODAY1                                                 
*                                                                               
         XC    AMOUNT,AMOUNT                                                    
         MVC   MNTH(1),TBECHDTE+1                                               
         MVC   MNTH+1(1),TBECHDTE+3                                             
         CLI   TBECHDTE+2,C'1'                                                  
         BNE   IN50                                                             
         ZIC   R1,MNTH+1           CONVERT 0-2 TO A-C                           
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         LA    R1,MOSLIST(R1)                                                   
         MVC   MNTH+1(1),0(R1)                                                  
*                                                                               
IN50     MVI   INTFACE,C'N'        NOT ON PRODUCTION INTERFACE                  
         MVI   CLIINT,C'N'         NOT CLIENT LEVEL INTERFACE                   
         MVI   INTXCLI,C'N'        CLIENT EXCLUDED                              
         MVI   OPEN,C'N'           WORKER FILE NOT OPEN                         
         XC    KEY,KEY             CLEAR KEY                                    
         LH    R1,=Y(NARR-BILLD)                                                
         AR    R1,RA                                                            
         USING NARRD,R1                                                         
         MVI   NARRD,C' '                                                       
         MVC   NARRD+1(NARLNQ-1),NARRD                                          
         MVI   NARRLEN,0                                                        
*                                                                               
         BAS   RE,LDTAB            LOAD TABLES                                  
*                                                                               
         L     R2,=V(EMPIO)        EMP I/O AREA                                 
         ST    R2,AEMPIO                                                        
**NO-OP* L     R2,=V(YTDTAB)       YTD TABLE (6/30 - TABLE NOW LOADED)          
**NO-OP* ST    R2,AYTDTAB                                                       
**NO-OP* L     R2,=V(YTDTABX)                                                   
**NO-OP* ST    R2,AYTDTABX                                                      
         L     R2,=V(TAYTDTAB)     TAYTD TABLE                                  
         ST    R2,ATAYTD                                                        
         L     R2,=V(EPITAB)       EPISODE TABLE                                
         L     R3,=V(EPITABX)      CLEAR IT                                     
         SR    R3,R2               SIZE OF AREA TO CLEAR                        
         XR    RE,RE                                                            
         LR    RF,RE                                                            
         MVCL  R2,RE                                                            
         L     R2,=AL4(TMD-BILLD)  LENGTH OF DSECT                              
         AR    R2,RA               START OF TALIM AREA                          
         ST    R2,ATALIM           A(TALIM WORKING AREA)                        
*                                                                               
         MVC   SUBWORK,SPACES                                                   
         ZAP   TALRECS,=P'0'                                                    
         ZAP   TALCASH,=P'0'                                                    
         ZAP   PRODRECS,=P'0'                                                   
         ZAP   PRODCASH,=P'0'                                                   
         ZAP   REGINV,=P'0'                                                     
         ZAP   NOPINV,=P'0'                                                     
         ZAP   BNPINV,=P'0'                                                     
         ZAP   CODINV,=P'0'                                                     
         ZAP   URGINV,=P'0'                                                     
         ZAP   NYPINV,=P'0'                                                     
         ZAP   ERRINVC,=P'0'                                                    
         ZAP   TALCRDTS,=P'0'                                                   
*                                                                               
         MVC   NEXTINV,FRSTINV     SET NEXT INV TO FIRST INVOICE                
*                                                                               
         LHI   R0,TXTDLNQ                                                       
         GETMAIN R,LV=(0)                                                       
         ST    R1,ATAXDSCT         SAVE IT, TXTABD IS DSECT FOR IT              
         AHI   R1,L'TXTXTTAB                                                    
         ST    R1,APOSTTAB                                                      
*                                                                               
         LHI   R0,PBLNQ                                                         
         GETMAIN R,LV=(0)                                                       
         ST     R1,AAGYTARA                                                     
         AHI    R1,TARALNQ                                                      
         ST     R1,ACLITARA                                                     
         AHI    R1,TARALNQ                                                      
         ST     R1,ACURTARA                                                     
         AHI    R1,TARALNQ                                                      
         ST     R1,ACPYTARA                                                     
         AHI    R1,TARALNQ                                                      
         ST     R1,ACURTAPG                                                     
*                                                                               
INITX    XIT1                                                                   
         EJECT                                                                  
*                                                                               
*        LOAD TABLES INTO CORE RES AREA                                         
*                                                                               
LDTAB    NTR1                                                                   
         L     R2,ATWA             GET ADDRESSIBILITY TO TSPFUSER               
         USING T703FFD,R2                                                       
         L     R2,TWADCONS                                                      
         USING TWADCOND,R2                                                      
         L     R2,TSPFUSER         IF TABLE IS ALREADY LOADED                   
*                                                                               
         OC    0(4,R2),0(R2)                                                    
         BNZ   LTAB10              DON'T LOAD IT AGAIN                          
*                                                                               
         TM    WHEN,X'20'                                                       
         BO    LTAB05                                                           
*                                                                               
         LOAD  EP=TABIINV,ERRET=LOADERR                                         
         B     *+6                                                              
LOADERR  DC    H'0'                                                             
         ST    R0,0(R2)            SET A(INV TABLE) IN CORE RES AREA            
         B     LTAB10                                                           
*                                  USE GETMAIN INSTEAD                          
LTAB05   L     R0,=A(20000*SRTILEN)                                             
         GETMAIN R,LV=(0)                                                       
         ST    R1,0(R2)                                                         
*                                                                               
LTAB10   MVC   ASRTINVT,0(R2)                                                   
*                                                                               
         OC    4(4,R2),4(R2)       SEE IF CHECK TABLE LOADED                    
         BNZ   LTAB20              DON'T LOAD IT AGAIN                          
*                                                                               
         TM    WHEN,X'20'                                                       
         BO    LTAB15                                                           
*                                                                               
         LOAD  EP=TABICHK,ERRET=LOADERR2                                        
         B     *+6                                                              
LOADERR2 DC    H'0'                                                             
         ST    R0,4(R2)            SET A(CHK TABLE) IN CORE RES AREA            
         B     LTAB20                                                           
*                                  USE GETMAIN INSTEAD                          
LTAB15   L     R0,=A(TABCHKLN*TABLEN)                                           
         GETMAIN R,LV=(0)                                                       
         ST    R1,4(R2)                                                         
*                                                                               
LTAB20   MVC   ACHKTAB,4(R2)                                                    
*                                                                               
         OC    8(4,R2),8(R2)       SEE IF PERFORMER YTD TABLE LOADED            
         BNZ   LTAB30              DON'T LOAD IT AGAIN                          
*                                                                               
         TM    WHEN,X'20'                                                       
         BO    LTAB25                                                           
*                                                                               
         LOAD  EP=TABIYTD,ERRET=LOADERR3                                        
         B     *+6                                                              
LOADERR3 DC    H'0'                                                             
         ST    R0,8(R2)            SET A(YTD TABLE) IN CORE RES AREA            
         B     LTAB30                                                           
*                                  USE GETMAIN INSTEAD                          
LTAB25   L     R0,=A(MAXYTD*TYTDLEN)                                            
         GETMAIN R,LV=(0)                                                       
         ST    R1,8(R2)                                                         
*                                                                               
LTAB30   MVC   AYTDTAB,8(R2)                                                    
*                                                                               
         B     INITX                                                            
         SPACE 2                                                                
         DROP  R2                                                               
         EJECT                                                                  
GETEL3   DS    0H                                                               
         AH    R4,DATADISP                                                      
FIRSTEL3 CLI   0(R4),0                                                          
         BNE   *+10                                                             
         CLI   0(R4),1                                                          
         BR    RE                                                               
         CLC   ELCODE,0(R4)                                                     
         BCR   8,RE                                                             
NEXTEL3  SR    RF,RF                                                            
         IC    RF,1(R4)                                                         
         LTR   RF,RF                                                            
         BNZ   *+10                                                             
         CLI   1(R4),1                                                          
         BR    RE                                                               
         AR    R4,RF                                                            
         B     FIRSTEL3                                                         
         SPACE 3                                                                
*                                                                               
MOSLIST  DC    C'ABCDEFGHIJKL'                                                  
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        SET/CLEAR CERTAIN VARIABLES                                            
*                                                                               
         DS    0D                                                               
ALLINIT  NMOD1 0,*ALLIN*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         XC    RECN,RECN           RESET NUMBER OF RECORDS TO PROCESS           
         MVI   LASTPG,C'N'         RESET NOT LAST PAGE TO BE PRINTED            
         MVI   READPTR,C'N'        HAVE NOT READ OPEN ITEMS PTR                 
         XC    UNIONS(ULLEN),UNIONS                                             
         BAS   RE,SETSYS           SET UP SYSIO BLOCK                           
         BRAS  RE,CKUPDT2          IF UPDATE OPTION FOR SOONS,                  
         BNE   ALLIN05                                                          
         CLI   FTIME,C'N'          AND NOT FIRST TIME IN,                       
         BE    *+8                 DON'T CLEAR TABLES                           
ALLIN05  BAS   RE,CLRTABS          CLEAR TABLES                                 
         L     R2,ASRTINVT         CLEAR INVOICE SORT TABLE                     
         ST    R2,ANXTINV                                                       
         LR    R3,R2                                                            
         A     R3,=AL4(INVTABLN*SRTILEN)                                        
         ST    R3,ASRTINVX                                                      
         BAS   RE,CLRAREA2                                                      
*                                                                               
         OC    TBVINV,TBVINV       IF RUNNING SPECIFIC INVOICES                 
         BZ    ALLIN10                                                          
         CLI   FTIME,C'N'          AND NOT FIRST TIME IN,                       
         BE    ALLIN20             DO NOT CLEAR YTD TABLE                       
*                                                                               
ALLIN10  L     R2,AYTDTAB          CLEAR YTD TABLE & SET END OF TABLE           
         LR    R3,R2                                                            
         A     R3,=AL4(TYTDLEN*MAXYTD)                                          
         ST    R3,AYTDTABX                                                      
         BAS   RE,CLRAREA2                                                      
*                                                                               
ALLIN20  BRAS  RE,CKUPDT2          IF UPDATE OPTION FOR SOONS,                  
         BNE   ALLIN25                                                          
         CLI   FTIME,C'N'          DON'T RESET AMNTS IF NOT FIRST TIME          
         BE    ALLINX                                                           
ALLIN25  MVI   FTIME,C'Y'          FIRST TIME IN                                
         XC    LAGY(LINFO-LAGY),LAGY     CLEAR PREVIOUS INV'S INFO              
         XC    AYBTYPE(AYBINFO+CLBINFO+GINFO),AYBTYPE                           
         XC    CTAMTS(NCTAMTS*4),CTAMTS                                         
ALLINX   XIT1                                                                   
         EJECT                                                                  
*                                                                               
*        SET UP BASIC SYSIO BLOCK                                               
*                                                                               
         SPACE 1                                                                
         USING T703FFD,R2                                                       
SETSYS   NTR1                                                                   
         L     R2,ATWA                                                          
         MVC   TIACOMFC,ACOMFACS                                                
         MVC   TIACCS,TWAACCS      PASS THROUGH LIMIT ACCESS                    
         MVC   TIAUTH,TWAAUTH           AND AUTHORIZATION                       
         MVC   TIUSERID,TWAORIG         AND REQUESTING ID                       
         MVC   TBTWAAGY+8(2),TWAORIG                                            
         GOTO1 USERVAL,DMCB,(X'80',TBTWAAGY)                                    
         B     ALLINX                                                           
         DROP  R2                                                               
         SPACE 2                                                                
*                                                                               
*        GET UNION/LOCAL RECORDS TO SET TABLE                                   
*                                                                               
         SPACE 1                                                                
SETUNI   NTR1                                                                   
         MVI   TIREAD,TLLOCDQ      UNION/LOCAL RECORDS                          
         XC    TIFUN,TIFUN         CLEAR FILTERS                                
         XC    TIFLOCL,TIFLOCL                                                  
         MVC   TIFUN,TGUNI         SET FILTERS                                  
         MVC   TIFLOCL,TGLCL                                                    
         XC    TIQSTART,TIQSTART                                                
         LA    R1,ULTAB                                                         
         ST    R1,TIHOOK                                                        
         GOTO1 TASYSIO,DMCB,TASYSIOD                                            
         B     ALLINX                                                           
         EJECT                                                                  
*                                                                               
*        CLEAR TABLES                                                           
*                                                                               
CLRTABS  NTR1                                                                   
*        XC    POSTTAB(POSTLEN),POSTTAB     FLUSH TABLE                         
         L     RF,ATAXDSCT                                                      
         AHI   RF,256                                                           
         XC    0(256,RF),0(RF)                                                  
                                                                                
         L     R2,=V(AREA)         CLEAR POSTINGS AREA                          
         L     R3,=V(AREAX)                                                     
         BAS   RE,CLRAREA2                                                      
*                                                                               
         L     R2,=V(ERRTAB)       CLEAR ERROR TABLE                            
         ST    R2,ANXTERR                                                       
         L     R3,=V(ERRTABX)                                                   
         BAS   RE,CLRAREA2                                                      
*                                                                               
         L     R2,=V(UNITAB)       CLEAR UNION TABLE                            
         ST    R2,ANXTUNI                                                       
         L     R3,=V(UNITABX)                                                   
         BAS   RE,CLRAREA2                                                      
         BAS   RE,SETUNI           SET UP UNION TABLE                           
*                                                                               
         L     R2,=V(BNPTAB)       CLEAR BNP TABLE                              
         ST    R2,ANXTBNP                                                       
         L     R3,=V(BNPTABX)                                                   
         BAS   RE,CLRAREA2                                                      
*                                                                               
         B     ALLINX                                                           
         SPACE 2                                                                
*                                                                               
*        CLEAR AREA OF STORAGE        R2 = A(AREA)                              
*                                     R3 = A(AREA END)                          
*                                                                               
CLRAREA2 NTR1                                                                   
         SR    R3,R2               SIZE OF AREA TO CLEAR                        
         XR    RE,RE                                                            
         LR    RF,RE                                                            
         MVCL  R2,RE                                                            
         B     ALLINX                                                           
         EJECT                                                                  
*                                                                               
*        SET UNION TABLE TO CONTAIN ALL UNION/LOCALS                            
*                                                                               
ULTAB    NTR1                                                                   
         CLI   TIMODE,PROCREC      RECORD GOTTEN FROM SYSIO                     
         BNE   ALLINX                                                           
         CLI   TIREAD,TLLOCDQ      THESE UNION RECORDS?                         
         BNE   ALLINX                                                           
         L     R3,ANXTUNI          NEXT ADDR TO PUT UNI/LOCAL                   
         USING UNILCLD,R3                                                       
         C     R3,=V(UNITABX)      END OF TABLE                                 
         BNE   *+6                                                              
         DC    H'0'                TABLE TOO SMALL                              
*                                                                               
         LA    R4,TIKEY            POINT TO KEY                                 
         USING TLLOD,R4                                                         
         MVC   UNIUNI,TLLOUN       MOVE IN UNION                                
         MVC   UNILCL,TLLOLCL      MOVE IN LOCAL                                
         MVI   UNITURN,C'N'        DEFAULT NOT TURNAROUND                       
         MVI   UNINCPY,1           SET DEFAULT N'COPIES = 1                     
*                                                                               
         L     R4,TIAREC           A(RECORD)                                    
         MVI   ELCODE,TALOELQ      GET LOCAL ELEMENT                            
         USING TALOD,R4                                                         
         BAS   RE,GETEL6                                                        
         BNE   ULX                                                              
         OC    TALONCPY,TALONCPY                                                
         BZ    *+10                                                             
         MVC   UNINCPY,TALONCPY    SET N'COPIES                                 
         TM    TALOSTAT,TALOSUCP   UNION COPY REQUIRED/TURNAROUND               
         BNO   ULX                                                              
         MVI   UNITURN,C'Y'        TURNAROUNDS                                  
*                                                                               
ULX      LA    R3,UNILEN(R3)       BUMP TABLE                                   
         ST    R3,ANXTUNI                                                       
         B     ALLINX                                                           
         DROP  R3,R4                                                            
         SPACE 2                                                                
         GETELN (R4),DATADISP,ELCODE,6                                          
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        FINAL PROCESSING FOR BILLING RUN                                       
*                                                                               
         DS    0D                                                               
FINAL    NMOD1 0,*FINAL*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         SPACE                                                                  
         XC    STATUS,STATUS       CLEAR LAST INVOICE STATUS                    
         XC    TBSTA2,TBSTA2                                                    
         CLI   RECNUM,BI           IF NOT BILLING COPY                          
         BE    FIN05                                                            
         CLI   RECNUM,VB           OR P+ BILLING                                
         BE    FIN05                                                            
         CLI   RECNUM,PB           OR PRINT BILLING                             
         BNE   FINX                EXIT                                         
*                                                                               
FIN05    BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BNE   FIN08                                                            
         OC    CTAMTS(NCTAMTS*4),CTAMTS  IF NO AMOUNTS, NOTHING TO POST         
         BZ    FIN10                                                            
         B     FIN09                                                            
FIN08    OC    TBVINV,TBVINV       OR IF RUNNING SPECIFIC INVOICES              
         BNZ   FIN10               SKIP POSTING                                 
FIN09    GOTO1 =V(TABIPOST),DMCB,(RC),EPOSTSE                                   
         GOTO1 =V(TABIPOST),DMCB,(RC),EPOSTSX                                   
         MVI   SWPOST,POSTRCV                                                   
         GOTO1 =V(TABIPOST),DMCB,(RC),EEOFPOST                                  
         CLI   OPEN,C'Y'           IF THERE IS AN OPEN WORKER FILE              
         BNE   FIN10                                                            
         MVI   SWPOST,POSTPROD                                                  
         GOTO1 =V(TABIPOST),DMCB,(RC),EEOFPOST                                  
*                                                                               
FIN10    MVI   LASTPG,C'Y'         THEN DO NOT PRINT SUMMARY PAGE               
         GOTO1 =V(TABIPRT),DMCB,(RC) PRINT OUT THE NUMBER OF INVOICES           
*                                  PROCESSED/INVOICES IN ERROR                  
         OI    STAT3,STCLPRTQ      CLOSE PRINT QUEUE                            
FINX     XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
*                                                                               
*        PUT INVOICE IN ERROR TABLE                                             
*                                                                               
         DS    0D                                                               
PUTERR   NMOD1 0,*PTERR*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         L     R1,AIO1             USE AGENCY & INVOICE I/O                     
         L     R2,ANXTERR          FILL IN ERROR TABLE                          
         USING ERRINVD,R2                                                       
         CLI   0(R1),TLINCDQ       ACTIVE POINTER                               
         BNE   PE10                                                             
         USING TLIND,R1                                                         
         MVC   ERRAGY,TLINAGY                                                   
         MVC   ERRINV,TLININV                                                   
         B     PE40                                                             
         DROP  R1                                                               
*                                                                               
         USING TLINPD,R1                                                        
PE10     CLI   0(R1),TLINBCDQ      OPEN ITEMS                                   
         BNE   PE20                                                             
         MVC   ERRAGY,TLINBAGY                                                  
         MVC   ERRINV,TLINBINV                                                  
         B     PE40                                                             
*                                                                               
PE20     CLI   0(R1),TLINDCDQ      BILL DATE POINTER                            
         BNE   PE30                                                             
         MVC   ERRAGY,TLINDAGY                                                  
         MVC   ERRINV,TLINDINV                                                  
         B     PE40                                                             
*                                                                               
PE30     CLI   0(R1),TLINKCDQ      CHECK DATE POINTER                           
         BNE   PE40                                                             
         MVC   ERRAGY,TLINKAGY                                                  
         MVC   ERRINV,TLINKINV                                                  
         DROP  R1                                                               
*                                                                               
PE40     MVC   ERRNUM,ERRSTAT      ERROR NUMBER                                 
         LA    R2,ERRLEN(R2)                                                    
         ST    R2,ANXTERR                                                       
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         SPACE 1                                                                
         DROP  RB                   DROP PREV BASE REGS                         
         EJECT                                                                  
*                                                                               
*        CHECK THE DATES OF REPORTS THAT RUN FOR A PERIOD                       
*                                                                               
*                  P1 = SYSTEM SPECIFIC W/S                                     
*                                                                               
         DS    0D                                                               
CKDATES  NMOD1 0,*CKDTS*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         LA    R1,KEY                                                           
         USING TLINPD,R1                                                        
         CLI   TLINPCD,TLINKCDQ    IF CHECK DATE PTR                            
         BNE   CKD30                                                            
*                                                                               
CKD10    CLC   TIFAGY,SPACES       PROGRAM MUST FILTER AGENCY                   
         BNH   CKD20                                                            
         CLC   TIFAGY,TLINKAGY     IF THIS ISN'T REQUESTED AGENCY               
         BE    CKD20                                                            
         MVC   KEY,KEYSAVE                                                      
         MVI   TLINKAGY,X'FF'      READ HIGH TO SKIP THIS DATE                  
         GOTO1 HIGH                                                             
         XC    TLINKAGY(L'TLINPKEY-(TLINKAGY-TLINPCD)),TLINDAGY                 
         MVC   TLINKAGY,TIFAGY     RESET AGENCY & READ HIGH AGAIN               
         GOTO1 HIGH                TO GET TO THE AGENCY WITHIN THE DATE         
         CLI   TLINPCD,TLINKCDQ    IF THIS IS A CHECK DATE POINTER              
         BNE   CKDNO                                                            
         B     CKD10                                                            
*                                                                               
CKD20    CLC   TLINKDTE,TBSPDATE   & REQUESTED DATE >= START DATE               
         BL    CKDNO                                                            
         CLC   TLINKDTE,TBEPDATE     AND <= END DATE                            
         BH    CKDNO                                                            
         B     CKYES               THEN NEED TO PROCESS THIS INVOICE            
*                                                                               
         USING TLINPD,R1                                                        
CKD30    CLI   TLINPCD,TLINDCDQ    IF THIS IS A BILL DATE POINTER               
         BNE   CKYES                                                            
*                                                                               
CKD35    CLC   TIFAGY,SPACES       PROGRAM MUST FILTER AGENCY                   
         BNH   CKD40                                                            
         CLC   TIFAGY,TLINDAGY     IF THIS ISN'T REQUESTED AGENCY               
         BE    CKD40                                                            
         MVC   KEY,KEYSAVE                                                      
         XC    TLINDAGY(L'TLINPKEY-(TLINDDTE-TLINPCD)-1),TLINDAGY               
         MVI   TLINDAGY,X'FF'      READ HIGH TO SKIP THIS DATE                  
         GOTO1 HIGH                                                             
         XC    TLINDAGY(L'TLINPKEY-(TLINDDTE-TLINPCD)-1),TLINDAGY               
         MVC   TLINDAGY,TIFAGY     RESET AGENCY & READ HIGH AGAIN               
         GOTO1 HIGH                                                             
         CLI   TLINPCD,TLINDCDQ    IF THIS IS STILL A BILL  DT PTR              
         BNE   CKDNO                                                            
         B     CKD35               CHECK AGENCY                                 
*                                                                               
CKD40    CLC   TLINDDTE,TBSPDATE   IF REQUESTED DATE >= START DATE              
         BL    CKDNO                                                            
         CLC   TLINDDTE,TBEPDATE      AND <= END DATE                           
         BH    CKDNO                                                            
*                                                                               
CKYES    B     YES2                PROCESS THIS INVOICE                         
*                                                                               
CKDNO    B     NO2                 FINISHED PROCESSING THE REQUEST              
         DROP  R1                                                               
         SPACE                                                                  
YES2     SR    RC,RC               SET CONDITION CODE                           
NO2      LTR   RC,RC                                                            
         XMOD1                                                                  
         SPACE                                                                  
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        CHECK TO SEE IF THIS IS A CONVERTED INVOICE                            
*                                                                               
         DS    0D                                                               
CKCONV   NMOD1 0,*CKCON*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         LA    R2,KEY                                                           
         CLI   0(R2),TLINCDQ                                                    
         BNE   CKC10                                                            
         USING TLIND,R2                                                         
         MVC   TGINV,TLININV       SET INVOICE NUMBER                           
         B     CKC90                                                            
         DROP  R2                                                               
*                                                                               
         USING TLINPD,R2                                                        
CKC10    CLI   0(R2),TLINHCDQ                                                   
         BNE   CKC20                                                            
         MVC   TGINV,TLINHINV      SET INVOICE NUMBER                           
         B     CKC90                                                            
*                                                                               
CKC20    CLI   0(R2),TLINBCDQ                                                   
         BNE   CKC30                                                            
         MVC   TGINV,TLINBINV      SET INVOICE NUMBER                           
         B     CKC90                                                            
*                                                                               
CKC30    CLI   0(R2),TLINDCDQ                                                   
         BNE   CKC50                                                            
         MVC   TGINV,TLINDINV      SET INVOICE NUMBER                           
         B     CKC100                                                           
*                                                                               
CKC50    CLI   0(R2),TLINKCDQ                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   TGINV,TLINKINV      SET INVOICE NUMBER                           
         B     CKC100                                                           
*                                                                               
CKC90    XC    TGINV,COMPLM3       UNCOMPLEMENTED                               
*                                                                               
CKC100   CLI   TGINV+5,0           CHECK IF CONVERTED INVOICE                   
         BE    NO3                                                              
         CLI   TGINV+5,X'80'       OR CANCELLED INVOICE                         
         BE    NO3                                                              
         B     YES3                                                             
         SPACE 2                                                                
YES3     SR    RC,RC               SET CONDITION CODE                           
NO3      LTR   RC,RC                                                            
         XMOD1                                                                  
         SPACE                                                                  
COMPLM3  DC    X'FFFFFFFFFFFF'                                                  
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        SET ATTENTION NAME AND ADDRESS                                         
*                                                                               
         DS    0D                                                               
SETATT   NMOD1 0,*STATT*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         MVC   TBADDR,TBATADDR     SET LOWEST LEVEL ADDRESS                     
         CLC   TBATADDR,SPACES     ATTENTION ADDRESS                            
         BH    SA10                                                             
         MVC   TBADDR,TBPRADDR     SET PRODUCT ADDRESS                          
         CLC   TBPRADDR,SPACES                                                  
         BH    SA10                                                             
         MVC   TBADDR,TBCLADDR     SET CLIENT ADDRESS                           
         CLC   TBCLADDR,SPACES                                                  
         BH    SA10                                                             
         MVC   TBADDR,TBAYADDR     SET AGENCY ADDRESS                           
*                                                                               
SA10     MVC   TBATTN,TBATNAME     SET LOWEST LEVEL NAME                        
         CLC   TBATNAME,SPACES     ATTENTION NAME                               
         BH    SAX                                                              
         MVC   TBATTN,TBPRATTN     SET PRODUCT                                  
         CLC   TBPRATTN,SPACES                                                  
         BH    SAX                                                              
         MVC   TBATTN,TBCLATTN     SET CLIENT NAME                              
         CLC   TBCLATTN,SPACES                                                  
         BH    SAX                                                              
         MVC   TBATTN,TBAYATTN     SET AGENCY NAME                              
         CLC   TBAYATTN,SPACES                                                  
         BH    SAX                                                              
         MVC   TBATTN,SPACES                                                    
*                                                                               
SAX      CR    RB,RB                                                            
         XMOD1                                                                  
         SPACE                                                                  
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*        ROUTINE UPDATES W4 RECORD WITH THIS CHECK'S SUBJ. TO P&H               
*        ASSUMES AIO=AIO2, WHICH HAS UPDATED CHECK RECORD                       
*                                                                               
*                  P1 = SYSTEM SPECIFIC W/S                                     
*                  P2 = A(ENTRY IN CHECK TABLE)                                 
*                                                                               
         SPACE                                                                  
         DS    0D                                                               
UPDW4    NMOD1 0,*UPDW4*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         L     R3,4(R1)                                                         
         USING TABCHKD,R3          R3=A(ENTRY IN CHECK TABLE)                   
         SPACE                                                                  
         TM    TBSTAT,TAINSCHK     ONLY IF CHECKS NOT ALREADY WRITTEN           
         BO    UPDW4X2                                                          
         TM    TBSTA2,TAINSHLP     DON'T BOTHER IF COD PRINTED                  
         BO    UPDW4X2                                                          
         TM    TBOPTS,TBRERUN      OR IF JOB IS A RE-RUN                        
         BO    UPDW4X2                                                          
         SPACE                                                                  
         USING TAPDD,R4                                                         
         L     R4,AIO2             SET R4=A(UPDATED CHECK REC)                  
         MVI   ELCODE,TAPDELQ      GET TAPD EL                                  
         BAS   RE,GETEL4                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         OC    TAPDSPNH,TAPDSPNH   DON'T BOTHER IF NO SUBJ. TO P&H              
         BZ    UPDW4X2                                                          
         TM    TGUSSTA3,SOAPUSE    MUST BE SOAP USE                             
         BZ    UPDW4X2                                                          
         MVC   FULL,TAPDSPNH       SAVE SUBJ TO P&H IN FULL                     
*                                                                               
         MVC   SYSFIL,SVSYSFIL     SET SYSFIL/DIR TO TALENT                     
         MVC   SYSDIR,SVSYSDIR                                                  
*                                                                               
         MVC   AIO,=V(AREA)        SET IOAREA FOR W4 RECORD                     
         BRAS  RE,CKUPDT2          CHECK FOR UPDATE OPTION                      
         BNE   UPDW42                                                           
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'B0',TABSSN),0                             
         B     UPDW43                                                           
UPDW42   GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',TABSSN),0                             
UPDW43   BE    *+6                 W4 RECORD MUST BE THERE                      
         DC    H'0'                                                             
         MVI   ELCODE,TAYSELQ        GET YTD SPNH EL                            
         MVC   TGFULL,TGTODY20       SET TGFULL WITH CORRECT YEAR               
*        MVC   TGFULL+2(2),TGTODAY0  SET TGFULL WITH CORRECT YEAR               
*        MVC   TGFULL(2),=C'19'      ASSUME 20TH CENTURY                        
*                                                                               
         TM    TBSTAT,TAINSCIN     IF INVOICE CANCEL(LER)                       
         BZ    UPDW45                                                           
         CLC   =XL1'95',TBOBBDTE   CHECK ORIG BILL DATE NOT BEFORE 1995         
         BH    UPDW4X2                                                          
         GOTO1 DATCON,DMCB,(1,TBOBBDTE),(20,TGDUB)                              
         MVC   TGFULL,TGDUB        SET ORGINAL BILL DATE                        
*        GOTO1 DATCON,DMCB,(1,TBOBBDTE),(0,TGDUB)                               
*        MVC   TGFULL+2(2),TGDUB   SET YEAR OF ORGINAL BILL DATE                
UPDW45   GOTO1 GETL,DMCB,(4,TGFULL)                                             
         BNE   UPDW410                                                          
         L     R4,TGELEM           R4=A(TAYS EL)                                
         USING TAYSD,R4                                                         
         L     R0,FULL               THIS CHECK'S SUBJ P&H                      
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BNE   UPDW47                                                           
         A     R0,TAYSWSPH         + PREV YTD SPNH WRITERS                      
         ST    R0,TAYSWSPH         = NEW YTD SPNH WRITERS                       
         B     UPDW4X                                                           
*                                                                               
UPDW47   A     R0,TAYSSSPH         + PREV YTD SPNH                              
         ST    R0,TAYSSSPH         = NEW YTD SPNH                               
         B     UPDW4X                                                           
*                                                                               
UPDW410  XC    ELEMENT,ELEMENT     INIT ELEMENT TO ADD                          
         LA    R4,ELEMENT                                                       
         MVI   TAYSEL,TAYSELQ                                                   
         MVI   TAYSLEN,TAYSLNQ                                                  
         MVC   TAYSYEAR,TGFULL     SET CORRECT YEAR                             
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BNE   *+14                                                             
         MVC   TAYSWSPH,FULL       SET NEW YTD SPNH WRITER                      
         B     *+10                                                             
         MVC   TAYSSSPH,FULL       SET NEW YTD SPNH                             
         GOTO1 ADDELEM                                                          
*                                                                               
UPDW4X   GOTO1 PUTREC               WRITE THE RECORD                            
         MVC   SYSFIL,=CL8'CHKFIL'  RESET SYSFIL/DIR TO CHECK                   
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         GOTO1 AMYTRACE,DMCB,(RC),=C'W4 ',AIO,0                                 
*                                                                               
UPDW4X2  MVC   AIO,AIO2             RESET AIO TO POINT TO UPDATED CHK           
         XIT1                                                                   
         SPACE 2                                                                
         GETELN (R4),DATADISP,ELCODE,4                                          
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        ADJUSTS P&H AMOUNT FOR SOAPS ON CHECK AND INVOICE RECORD               
*        IF CAST'S YTD SPNH >= $230,000                                         
*                                                                               
*                  P1 = SYSTEM SPECIFIC W/S                                     
*                  P2 = A(TAPD EL ON CHECK)                                     
*                                                                               
         DS    0D                                                               
ADJPNH   NMOD1 0,*ADJP*                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         L     R4,4(R1)                                                         
         USING TAPDD,R4            R4=A(TAPD EL) IN CHECK RECORD                
*                                                                               
         TM    TBSTAT,TAINSCHK     ONLY IF CHECKS NOT ALREADY WRITTEN           
         BO    ADJPX                                                            
         TM    TBSTA2,TAINSHLP     DON'T BOTHER IF COD PRINTED                  
         BO    ADJPX                                                            
         TM    TBOPTS,TBRERUN      OR IF JOB IS A RE-RUN                        
         BO    ADJPX                                                            
         TM    TBSTAT,TAINSCIN     OR IF INVOICE CANCEL(LER)                    
         BO    ADJPX                                                            
         TM    STATUS,STCRDT       OR IF CREDIT INVOICE                         
         BO    ADJPX                                                            
         SPACE                                                                  
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BE    *+14                                                             
         OC    TAPDPNH,TAPDPNH     DON'T BOTHER IF NO P&H                       
         BZ    ADJPX                                                            
         TM    TGUSSTA3,SOAPUSE    ONLY IF SOAP USE                             
         BZ    ADJPX                                                            
         MVC   FULL,TAPDSPNH       SAVE CURRENT SUBJ. TO P&H                    
         MVC   TGFULL,TAPDPNH      AND CURRENT P&H                              
*                                                                               
         CLC   TABUNI,=C'AFT'      ONLY IF UNION IS AFT                         
         BE    ADJP8                                                            
         CLC   TABUNI,=C'WGA'      ONLY IF UNION IS WGA                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R1,YTDWSPNH         R1=YTD SPNH                                  
         C     R1,=F'35000000'     IF YTD SPNH ALREADY >= $350,000              
         BL    ADJP2                                                            
         MVC   TAPDPNH,=F'0'          MAKE CHECK P&H = 0                        
         MVC   TAPDSPNH,=F'0'              CHECK SUBJ. TO P&H = 0               
         B     ADJP20                                                           
*                                                                               
ADJP2    A     R1,TAPDSPNH         ADD THIS CHECK'S SPNH TO YTD SPNH            
         S     R1,=F'35000000'     - 350,000 = R1                               
         BNP   ADJPX               IF NOT POSITIVE THEN DON'T BOTHER            
         B     ADJP15                                                           
*                                                                               
ADJP8    L     R1,YTDSSPNH         R1=YTD SPNH                                  
         C     R1,=F'23000000'     IF YTD SPNH ALREADY >= $230,000              
         BL    ADJP10                                                           
         MVC   TAPDPNH,=F'0'          MAKE CHECK P&H = 0                        
         MVC   TAPDSPNH,=F'0'              CHECK SUBJ. TO P&H = 0               
         B     ADJP20                                                           
*                                                                               
ADJP10   A     R1,TAPDSPNH         ADD THIS CHECK'S SPNH TO YTD SPNH            
         S     R1,=F'23000000'     - 230,000 = R1                               
         BNP   ADJPX               IF NOT POSITIVE THEN DON'T BOTHER            
ADJP15   L     R0,TAPDSPNH                                                      
         SR    R0,R1               R0 = AMT TO PAY P&H ON                       
         ST    R0,TAPDSPNH         SAVE NEW SUBJ. TO P&H AMOUNT                 
         CVD   R0,DUB                                                           
         ZAP   WORK(16),DUB                                                     
         MP    WORK(16),=P'10'     * 10 FOR ROUNDING                            
         L     R0,TAPDPNH                                                       
         CVD   R0,DUB                                                           
         MP    WORK(16),DUB        * OLD P&H                                    
         L     R0,FULL                                                          
         CVD   R0,DUB                                                           
         DP    WORK(16),DUB        / OLD SUBJ. TO P&H                           
         SRP   WORK(8),63,5          ROUNDED                                    
         CVB   R1,WORK                                                          
         ST    R1,TAPDPNH          = NEW P&H AMOUNT                             
*                                                                               
         L     R0,TGFULL             OLD P&H AMT                                
         SR    R0,R1               - NEW P&H AMT                                
         ST    R0,TGFULL           = CHANGE IN P&H AMT = TGFULL                 
*                                                                               
         L     R0,FULL               OLD SUBJ. TO P&H                           
         S     R0,TAPDSPNH         - NEW SUBJ. TO P&H                           
         ST    R0,FULL             = CHANGE IN SUBJ. TO P&H = FULL              
*                                                                               
ADJP20   L     R4,AIO1             R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAPDELQ      GET TAPD EL                                  
         BAS   RE,GETEL5                                                        
         BE    *+6                                                              
         DC    H'0'                WAS ALREADY READ, MUST BE THERE              
*                                                                               
         L     R0,TAPDSPNH                                                      
         S     R0,FULL             ADJUST SUBJ. TO P&H ON INVOICE               
         ST    R0,TAPDSPNH                                                      
         ST    R0,TSPNH            SAVE IN TSPNH ALSO                           
*                                                                               
         L     R0,TAPDPNH                                                       
         S     R0,TGFULL           ADJUST P&H AMT ON INVOICE                    
         ST    R0,TAPDPNH                                                       
         ST    R0,TPNH             SAVE IN TPNH ALSO                            
ADJPX    XIT1                                                                   
         SPACE 2                                                                
         GETELN (R4),DATADISP,ELCODE,5                                          
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        CALCULATE THE NUMBER OF PAGES ON THE INVOICE                           
*                  IN ORDER TO BE ABLE TO PRINT 'PAGE X OF N'                   
*                                                                               
*                  P1 = SYSTEM SPECIFIC W/S                                     
*                  P2 = N'PERFORMERS ON INVOICE                                 
*                                                                               
         DS    0D                                                               
PAGES    NMOD1 0,*PAGES*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         L     R2,4(R1)            NUMBER OF PERFORMERS                         
         STH   R2,HALF                                                          
*                                                                               
*        CALCULATE NUMBER OF LINES FOR LEFT HAND SIDE HEADER                    
*                                                                               
PAG10    SR    R1,R1               CLEAR LINE COUNTER (1ST-LEFT SIDE)           
         LA    R4,PGTAB1           LEFT HAND SIDE TABLE                         
         LA    R3,TBILLD           START OF SAVED STORAGE                       
*                                                                               
PAG20    CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    PAG40                                                            
         LH    R2,0(R4)            DISPLACEMENT INTO STORAGE                    
         AR    R2,R3               ADDRESS OF VARIABLE                          
         CLC   0(12,R2),SPACES                                                  
         BNH   PAG30                                                            
         LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG30    LA    R4,2(R4)            NEXT ENTRY                                   
         B     PAG20                                                            
*                                                                               
**G40    CLC   TBCONT,SPACES       IF THERE ARE CONTRACTS                       
**       BH    *+14                                                             
**       CLC   TBTID,SPACES        OR TRACK ID TAKEN INTO ACCOUNT               
**       BNH   PAG45                                                            
**       LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG40    LA    R4,PGTAB2           2ND HALF LEFT HAND SIDE TABLE                
*                                                                               
PAG50    CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    PAG70                                                            
         LH    R2,0(R4)            DISPLACEMENT INTO STORAGE                    
         AR    R2,R3               ADDRESS OF VARIABLE                          
         OC    0(6,R2),0(R2)       DATES                                        
         BZ    PAG60                                                            
         LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG60    LA    R4,2(R4)            NEXT ENTRY                                   
         B     PAG50                                                            
*                                                                               
PAG70    LA    R1,3(R1)            CID+BLANKS (BTWN TPO/AGY & CLI/ATTN)         
         OC    TBFDAT,TBFDAT       IF ANY FILM INFO                             
         BNZ   PAG75                                                            
         OC    TBMDAT,TBMDAT       OR ANY MUSIC INFO                            
         BNZ   PAG75                                                            
         OC    TBRDAT,TBRDAT       OR ANY RECORDING INFO                        
         BNZ   PAG75                                                            
         CLC   TBTID,SPACES        OR A TRACK ID                                
         BNZ   PAG75                                                            
         CLC   TBCONT,SPACES       OR AN AFM CONTRACT                           
         BNH   PAG77                                                            
*                                                                               
PAG75    LA    R1,1(R1)            THEN NEED ANOTHER BLANK LINE                 
*                                                                               
PAG77    CLC   TBCOM(TBCOMX),SPACES   IF THERE IS A COMMENT                     
         BNH   PAG80                                                            
         LA    R1,1(R1)               THEN NEED ANOTHER BLANK LINE              
*                                                                               
PAG80    CLC   TBPRNAME,SPACES     IF PRD NAME DON'T BOTHER CHECKING            
         BH    PAG85                  1ST FIXED CYCLE                           
         OC    TBFCYC,TBFCYC       ELSE NEED TO ADD A LINE FOR IT               
         BZ    PAG85                                                            
         LA    R1,1(R1)                                                         
*                                                                               
PAG85    CLC   TBADD3,SPACES       IF 3RD ADDRESS LINE OR ATTN NAME             
         BH    PAG89               LINE FOR PAYER & QC ALREADY                  
         CLC   TBATTN,SPACES       COUNTED                                      
         BH    PAG89                                                            
         LA    R1,1(R1)                                                         
*                                                                               
PAG89    STC   R1,LEFTHED          SAVE N'LINES NEEDED FOR LEFT                 
*                                       HEADLINE                                
         SR    R1,R1               CLEAR LINE COUNTER (NOW RIGHT SIDE)          
         LA    R4,PGTAB3           RIGHT HAND SIDE TABLE                        
*                                                                               
PAG90    CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    PAG110                                                           
         LH    R2,0(R4)            DISPLACEMENT INTO STORAGE                    
         AR    R2,R3               ADDRESS OF VARIABLE                          
         CLI   0(R2),0                                                          
         BE    PAG100                                                           
         CLI   0(R2),X'40'                                                      
         BE    PAG100                                                           
         LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG100   LA    R4,2(R4)            NEXT ENTRY                                   
         B     PAG90                                                            
*                                                                               
PAG110   LA    R4,PGTAB4           2ND RIGHT HAND SIDE TABLE                    
*                                                                               
PAG120   CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    PAG140                                                           
         LH    R2,0(R4)            DISPLACEMENT INTO STORAGE                    
         AR    R2,R3               ADDRESS OF VARIABLE                          
         OC    0(2,R2),0(R2)                                                    
         BZ    PAG130                                                           
         LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG130   LA    R4,2(R4)            NEXT ENTRY                                   
         B     PAG120                                                           
*                                                                               
PAG140   CLC   TBVEN,SPACES        SPECIAL CASE - VENDOR                        
         BNH   *+8                                                              
         LA    R1,1(R1)            INCREMENT COUNTER                            
         CLC   TBEST,SPACES        SPECIAL CASE - ESTIMATE #                    
         BNH   *+8                                                              
         LA    R1,1(R1)            INCREMENT COUNTER                            
*                                                                               
PAG170   TM    TGUSTYST,MAJORS+UNITS IF MAJORS OR UNITS                         
         BZ    PAG175                                                           
         LA    R1,1(R1)            NEED 1 MORE LINE                             
         TM    TGUSTYST,UPGRADE    IF UPGRADE                                   
         BZ    PAG175                                                           
         LA    R1,1(R1)            NEED 1 MORE LINE                             
*                                                                               
PAG175   LA    R1,6(R1)            3(BOX)+1(INVOICE)+1(DATE)+1(BLANK)           
*                                                                               
         CLI   UNICOPY,C'Y'        IF UNION COPY ADD 1 TO RHS FOR               
         BNE   PAG180              CHECK DATE                                   
         LA    R1,1(R1)                                                         
*                                                                               
PAG180   STC   R1,RIGHTHED         N'LINES NEEDED FOR RIGHT SIDE                
*                                                                               
         MVC   HEADLINE,LEFTHED    SET N'HEADLINES = LEFT HEADLINES             
         CLC   LEFTHED,RIGHTHED    USE GREATER AMOUNT                           
         BNL   PAG190                                                           
         MVC   HEADLINE,RIGHTHED                                                
*                                                                               
PAG190   SR    R3,R3               CLEAR COUNTER                                
         L     R4,=V(NETTAB)                                                    
         OC    0(NETLEN,R4),0(R4)  IS THERE ANY NWK PROGRAM INFO                
         BZ    PAG230                                                           
         LA    R2,20               MAX N'PGMS                                   
*                                                                               
PAG200   OC    0(NETLEN,R4),0(R4)  ANY MORE NETWORK PROGRAM INFO                
         BZ    PAG210                                                           
         LA    R3,1(R3)            TOTAL NUMBER OF PGMS                         
         LA    R4,NETLEN(R4)       BUMP TO NEXT INPUT                           
         BCT   R2,PAG200                                                        
*                                                                               
PAG210   XR    R2,R2                                                            
         D     R2,=F'4'            4 PGMS TO A LINE                             
         LTR   R2,R2               IS THERE A REMAINDER                         
         BZ    PAG220                                                           
         LA    R3,1(R3)                                                         
*                                                                               
PAG220   LA    R3,5(R3)            LINES NEED FOR BOX + TITLES + BLANK          
*                                                                               
PAG230   STC   R3,CENTHED          N'LINES NEEDED FOR NETWORK PGMS              
         ZIC   R1,HEADLINE                                                      
         AR    R1,R3                                                            
         LA    R1,6(R1)            5(TOP BOX)+1 BLANK                           
         STC   R1,TOTALHED         TOTAL NUMBER OF HEADLINES NEEDED             
*                                                                               
         ZIC   R1,MAXLINES         N'LINES ON PAGE                              
         ZIC   RE,TOTALHED         - HEADER                                     
         SR    R1,RE               = N'LINES HAVE TO PRINT ON PAGE              
         BCTR  R1,0                NEED A LINE FOR BOTTOM BOX                   
**NO-OP  TM    TGSYSTAT,TASYSAOS   IF AOS, NEED 1 MORE BOTTOM LINE              
         OC    AOSAMT,AOSAMT       IF AOS, NEED 1 MORE BOTTOM LINE              
         BZ    *+6                                                              
         BCTR  R1,0                                                             
         ST    R1,FULL             (WE'LL ADD IT BACK IN ON LAST PAGE)          
*                                                                               
         LA    R4,1                PAGE COUNTER                                 
*                                                                               
         USING TABCHKD,R3                                                       
         L     R3,ACHKTAB          SET ADDRESS OF THE CHECK TABLE               
*                                                                               
PAG240   OC    0(TABDSK,R3),0(R3)  END OF CHECK TABLE                           
***240   OC    0(TABLEN,R3),0(R3)  END OF CHECK TABLE                           
         BZ    PAG270                                                           
         CLI   TBPRTINV,C'Y'       IF THIS IS NOT A PRINT INVOICE               
         BE    PAG245                                                           
         TM    TABSTAT2,TABSTACT   IF CHECK FOR ACTRAWORKS, WON'T PRINT         
         BZ    *+8                                                              
         LA    R1,1(R1)            IN PERF DETAILS, SO ADD 1 TO OFFSET          
*                                                                               
PAG245   CLI   TABXLN,C'Y'         DO WE NEED AN EXTRA LINE                     
         BNE   PAG250                                                           
         BCTR  R1,0                                                             
         LTR   R1,R1               THIS PAGE IS FULL                            
         BZ    PAG260                                                           
*                                                                               
PAG250   LA    R3,TABLEN(R3)       BUMP TO NEXT ENTRY IN TABLE                  
         BCT   R1,PAG240                                                        
*                                                                               
PAG260   L     R1,FULL                                                          
         LA    R4,1(R4)            PAGE COUNTER                                 
         B     PAG240                                                           
*                                                                               
PAG270   LA    R1,1(R1)            ADD THE LINE TAKEN FOR BOTTOM BOX            
         LA    R3,BBOXLEN          LINES NEEDED FOR BOTTOM BOX                  
         CLI   TBMED,TACOMEDP      PRINT NEEDS 1 EXTRA FOR BOTTOM BOX           
         BNE   *+8                                                              
         AHI   R3,1                ADD 1 LINE                                   
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BZ    PAG273                                                           
         AHI   R3,2                ADD 2 LINES (NEW ACTRAWORKS LINE)            
         B     PAG280                                                           
PAG273   OC    TBGPAY4,TBGPAY4     OR MORE THAN 3 LOCALS                        
         BNZ   PAG275                                                           
         OC    TBPNH4,TBPNH4                                                    
         BZ    *+8                                                              
PAG275   LA    R3,1(R3)            + 1                                          
*                                                                               
PAG280   CR    R1,R3                                                            
         BNL   *+8                                                              
         LA    R4,1(R4)                                                         
         STC   R4,NPAGES           N'PAGES                                      
*                                                                               
PAGX     XMOD1                                                                  
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        TABLE USED TO CALCULATE NUMBER OF LINES NEEDED                         
*                                                                               
PGTAB1   DS    0C                                                               
         DC    AL2(TBOFNAME-TBILLD)                                             
         DC    AL2(TBOFADDR-TBILLD)                                             
         DC    AL2(TBAYNAME-TBILLD)                                             
         DC    AL2(TBADD1-TBILLD)                                               
         DC    AL2(TBADD2-TBILLD)                                               
         DC    AL2(TBADD3-TBILLD)                                               
         DC    AL2(TBADD4-TBILLD)                                               
         DC    AL2(TBATTN-TBILLD)                                               
         DC    AL2(TBCLNAME-TBILLD)                                             
         DC    AL2(TBPRNAME-TBILLD)                                             
         DC    AL2(TBCONAME-TBILLD)                                             
         DC    AL2(TBCONT-TBILLD)                                               
         DC    AL2(TBCOM-TBILLD)                                                
         DC    AL2(TBCOM2-TBILLD)                                               
         DC    X'FF'                                                            
*                                                                               
PGTAB2   DS    0C                                                               
         DC    AL2(TBFDAT-TBILLD)                                               
         DC    AL2(TBRDAT-TBILLD)                                               
         DC    AL2(TBMDAT-TBILLD)                                               
         DC    X'FF'                                                            
*                                                                               
PGTAB3   DS    0C                                                               
         DC    AL2(TBMED-TBILLD)                                                
         DC    AL2(TBPO-TBILLD)                                                 
         DC    AL2(TBSEC-TBILLD)                                                
         DC    AL2(TBCWKS-TBILLD)                                               
         DC    AL2(TBSIGN-TBILLD)                                               
         DC    X'FF'                                                            
*                                                                               
PGTAB4   DS    0C                                                               
         DC    AL2(TBSTUS-TBILLD)                                               
         DC    AL2(TBUSEL-TBILLD)                                               
         DC    AL2(TBCDTS-TBILLD)                                               
         DC    AL2(TBUSE-TBILLD)    1 FOR USE TYPE                              
         DC    AL2(TBUSE-TBILLD)    1 FOR PAY TYPE                              
         DC    X'FF'                                                            
         SPACE 1                                                                
         DROP  RB                                                               
         EJECT                                                                  
*              SET COMMENT SUBSIDISARY INVOICES XXXXXX-XXXXXX                   
*                                                                               
         DS    0D                                                               
GETSI    NMOD1 0,*GETSI*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         MVI   BYTE,C'N'                                                        
         OC    TBCOM2,TBCOM2       IF SOMETHING IN COMMENT                      
         BZ    *+8                                                              
         MVI   BYTE,C'Y'           INDICATE SO                                  
*                                                                               
         LA    R3,TBCOM2+L'TBCOM2-1 SCAN BACKWARDS LOOKING                      
         LA    R1,L'TBCOM2-1        FOR NEXT AVAILABLE SPOT                     
GETSI2   CLI   0(R3),C' '                                                       
         BH    *+14                                                             
         BCTR  R3,0                                                             
         BCT   R1,GETSI2                                                        
         B     *+8                                                              
         LA    R3,2(R3)                                                         
*                                                                               
         TM    TBSTA2,TAINSPRM     IF PRIMARY INVOICE                           
         BNO   *+12                                                             
         BAS   RE,GETSIPRI         SET PRIMARY INVOICE COMMENT                  
         B     GETSIX                                                           
*                                                                               
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE                        
         BNO   GETSIX                                                           
         BAS   RE,GETSISUB         SET SUBSIDIARY INVOICE COMMENT               
*                                                                               
GETSIX   XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO SET COMMENT FOR PRIMARY INVOICE                       
         SPACE                                                                  
*                                  R3=A(COMMENT)                                
GETSIPRI NTR1                                                                   
         CLI   BYTE,C'Y'           IF SOMETHING ALREADY IN COMMENT              
         BE    GETSIP3                                                          
         MVC   0(15,R3),=C'PRIMARY INVOICE'                                     
         LA    R3,15(R3)                                                        
         MVI   0(R3),C'.'                                                       
         LA    R3,2(R3)                                                         
         MVC   0(19,R3),=C'SUBSIDIARY INVOICES'                                 
         LA    R3,20(R3)                                                        
         B     GETSIP5                                                          
*                                                                               
GETSIP3  MVC   0(12,R3),=C'PRI INV SUBS'    ABBREVIATE LITERAL                  
         LA    R3,13(R3)                                                        
*                                                                               
GETSIP5  MVC   0(3,R3),=C'ARE'                                                  
         LA    R3,4(R3)                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TASIELQ                                                   
         BAS   RE,GETEL7                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TASID,R4                                                         
         GOTO1 TINVCON,DMCB,TASIINV,0(R3),DATCON                                
         LA    R3,L'TASIINV(R3)                                                 
         MVI   0(R3),C'-'                                                       
         LA    R3,1(R3)                                                         
GETSIP10 BAS   RE,NEXTEL7                                                       
         BNE   *+14                                                             
         MVC   TEMP,TASIINV                                                     
         B     GETSIP10                                                         
*                                                                               
         GOTO1 TINVCON,DMCB,TEMP,0(R3),DATCON                                   
         B     GETSIX                                                           
         EJECT                                                                  
*              ROUTINE TO SET COMMENT FOR SUBSIDARY INVOICE                     
         SPACE                                                                  
*                                  R3=A(COMMENT)                                
GETSISUB NTR1                                                                   
         MVC   0(5,R3),=C'SPLIT'   PRINT " SPLIT "                              
         LA    R3,6(R3)                                                         
*                                                                               
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTSPL                                  
         GOTO1 TINVCON,DMCB,TGNAME,(R3)                                         
         LA    R3,7(R3)                                                         
*                                                                               
         XC    FULL,FULL           PERCENTAGE OF SUBSIDIARY                     
         SR    R2,R2               COUNT OF TOTAL SUBSIDIARIES                  
         MVI   BYTE,0              CURRENT SUBSIDIARY INVOICE                   
         MVC   TEMP,TGINV          UNCOMPLEMENT INVOICE NUMBER                  
         XC    TEMP,=6X'FF'                                                     
*                                                                               
         L     R4,AIO                                                           
         MVI   ELCODE,TASIELQ      GET SUBISIDARY INVOICE ELEMENT               
         BAS   RE,GETEL7                                                        
         B     *+8                                                              
GETSIS10 BAS   RE,NEXTEL7                                                       
         BNE   GETSIS20                                                         
         USING TASID,R4                                                         
         LA    R2,1(R2)            INCREMENT COUNT                              
         CLC   TEMP,TASIINV                                                     
         BNE   *+14                                                             
         MVC   FULL,TASIPCT        CURRENT PERCENTAGE                           
         STC   R2,BYTE             CURRENT INVOICE NUMBER                       
         B     GETSIS10            LOOP                                         
*                                                                               
GETSIS20 EDIT  BYTE,(2,0(R3)),ALIGN=LEFT  PRINT CURRENT SUBSIDIARY              
         AR    R3,R0                                                            
         MVC   1(2,R3),=C'OF'             " OF "                                
         LA    R3,4(R3)                                                         
         EDIT  (R2),(2,0(R3)),ALIGN=LEFT  TOTAL SUBSIDIARIES                    
         AR    R3,R0                                                            
         LA    R3,1(R3)                                                         
*                                                                               
         OC    FULL,FULL           IF PERCENTAGE                                
         BZ    GETSISX                                                          
         MVI   0(R3),C'('                                                       
         LA    R3,1(R3)                                                         
         EDIT  FULL,(7,0(R3)),4,ALIGN=LEFT                                      
         AR    R3,R0               +LENGTH OF EDIT                              
         SHI   R3,4                PT R2 TO DECIMAL PLACES                      
         LA    R1,3                CHECK DECIMALS FOR ZERO                      
GETSIS25 EX    R1,CKZERO                                                        
         BE    GETSIS30                                                         
         LA    R3,1(R3)                                                         
         BCT   R1,GETSIS25                                                      
         LA    R3,1(R3)            NO ZEROS AFTER DECIMAL                       
         B     GETSIS35                                                         
*                                                                               
GETSIS30 CH    R1,=H'3'            IF ALL DECIMAL PLACES ARE ZERO               
         BNE   *+10                                                             
         LA    R1,1(R1)            CLEAR DECIMAL POINT TOO                      
         BCTR  R3,0                                                             
         EX    R1,MVCSPC           SET TO SPACES                                
*                                                                               
GETSIS35 MVC   0(2,R3),=C'%)'                                                   
*                                                                               
GETSISX  B     GETSIX                                                           
         SPACE                                                                  
CKZERO   CLC   0(0,R3),=C'0000'    CHECK IF ZEROS                               
MVCSPC   MVC   0(0,R3),SPACES      SET ZEROS TO SPACES                          
         B     GETSIX                                                           
         SPACE                                                                  
         GETELN (R4),DATADISP,ELCODE,7                                          
         SPACE 3                                                                
         LTORG                                                                  
         DROP  R4,RB                                                            
         EJECT                                                                  
*                                                                               
*              ROUTINE DELETES BILLED SCREEN RECORDS                            
*                                                                               
         SPACE 1                                                                
         USING TLSCD,R3                                                         
         DS    0D                                                               
DELSCRS  NMOD1 0,*DSCRS*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         LA    R3,KEY              BUILD KEY                                    
         XC    TLSCKEY,TLSCKEY                                                  
         MVI   TLSCCD,TLSCCDQ      SCREEN RECORD CODE                           
         MVC   TLSCAGY,TGAGY       AGENCY                                       
         MVC   TLSCINV,TIFINV      INVOICE NUMBER                               
         TM    TBSTA2,TAINSSCR     INV # FOR THIS INVOICE IS                    
         BNO   DSCR05              TAINIDTE + TAINITIM                          
         MVC   TLSCINV(3),TBIDTE                                                
         MVC   TLSCINV+3(3),TBITIM                                              
*                                                                               
DSCR05   GOTO1 HIGH                READ DIRECTORY                               
*                                                                               
DSCR10   CLC   TLSCKEY(TLSCPG-TLSCKEY),KEYSAVE  STILL SAME INVOICE              
         BNE   DSCRX                                                            
         MVI   RDUPDATE,C'Y'                                                    
         MVC   AIO,AIO2                                                         
         BRAS  RE,CKUPDTE          CHECK FOR UPDATE OPTION                      
         GOTO1 GETREC              GET THE RECORD                               
         L     R3,AIO                                                           
         OI    TLSCSTAT,X'80'      DELETE FILE RECORD                           
         GOTO1 PUTREC              WRITE THE RECORD                             
*                                                                               
         GOTO1 AMYTRACE,DMCB,(RC),=C'SCRN',AIO,0                                
*                                                                               
         LA    R3,KEY                                                           
         OI    TLDRSTAT-TLDRD(R3),X'80' MARK DIRECTORY RECORD DELETED           
         GOTO1 WRITE               AND WRITE IT BACK                            
*                                                                               
         GOTO1 AMYTRAC2,DMCB,(RC),=C'SCRN REC DIR',KEY                          
*                                                                               
         OI    DMINBTS,X'08'       RE-READ RECORD I JUST WROTE                  
         GOTO1 HIGH                WHICH IS NOW DELETED                         
         NI    DMINBTS,X'F7'                                                    
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 SEQ                 LOOK FOR ANOTHER                             
         B     DSCR10                                                           
*                                                                               
DSCRX    MVC   AIO,AIO1            RESTORE I/O AREA                             
         XIT1                                                                   
         DROP  R3                                                               
         SPACE                                                                  
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        ADD/DELETE ECAST RECORDS TO/FROM FILE                                  
*             R3 - CHECK TABLE ENTRY                                            
*             ASSUMES AIO=AIO2, WHICH HAS UPDATED CHECK RECORD                  
*                                                                               
         DS    0D                                                               
ECAST    NMOD1 0,*ECAST*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
         MVC   SYSFIL,SVSYSFIL     SET SYSFIL/DIR TO TALENT                     
         MVC   SYSDIR,SVSYSDIR                                                  
         MVC   TEMP,TGINV          SET IN TEMP UN-COMPLEMENTED                  
         XC    TEMP(6),COMPLM2     INVOICE NUMBER                               
*                                                                               
         USING TABCHKD,R3                                                       
         USING TASOD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TASOELQ      GET EPISODE ELEMENT                          
         BAS   RE,GETEL2                                                        
         BNE   ECASTX                                                           
         ZIC   R2,TASONUM          NUMBER OF EPISODES IN ELEMENT                
         LA    R4,TASOSEPI                                                      
         USING TASOSEPI,R4                                                      
         XC    TGINV,TGINV         CLEAR GLOBAL INVOICE                         
         MVC   TGSSN,TABSSN                                                     
         MVC   TGCAT,TABCAT                                                     
         MVC   TGCSORT(1),TGCASORT                                              
*                                                                               
ECAST10  MVC   TBWSTAT,TASOSTAT    SAVE EPISODE STATUS BYTE                     
         MVC   TFULL(2),TASOEPI    SAVE BINARY EPINUM IN TFULL                  
         LH    RE,TASOEPI          SET CHARACTER EPISODE NUMBER                 
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TGEPI,DUB                                                        
*                                                                               
         MVC   AIO,=V(AREA)        SET IOAREA FOR ECAST RECORD                  
         TM    TGCATYPE,WRITER                                                  
         BNO   *+12                                                             
         BAS   RE,PRWEC            PROCESS ECAST RECS FOR WRITERS               
         B     ECAST50                                                          
*                                                                               
         BAS   RE,PROCEC           PROCESS ECAST RECS FOR NON-WRITERS           
*                                                                               
ECAST50  LA    R4,L'TASOSEPI(R4)   NEXT EPISODE NUMBER IN ELEMENT               
         BCT   R2,ECAST10                                                       
*                                                                               
         XC    TEMP,COMPLM2        RESTORE COMPLEMENTED GLOBAL                  
         MVC   TGINV,TEMP          INVOICE NUMBER                               
         MVC   AIO,AIO2            RESET AIO TO POINT TO UPDATED CHK            
ECASTX   XIT1                                                                   
         EJECT                                                                  
*        PROCESS WRITER ECAST RECORDS - ADD ECAST RECORD FOR                    
*                                       EACH WRITER ROLE                        
PRWEC    NTR1                                                                   
         LA    R5,WROLTAB                                                       
         USING WROLTABD,R5                                                      
*                                                                               
PRWEC10  CLC   WROLCAT,TABCAT      MATCH ON WRITER CATEGORY                     
         BNE   PRWEC20                                                          
         MVC   TBWSTAT,TASOSTAT                                                 
         CLC   TBWSTAT,WROLSTAT   IF SINGLE ROLE                                
         BNE   PRWEC12                                                          
         GOTO1 CATVAL,DMCB,WROLCAT RESET CATEGORY                               
         MVC   TGCSORT(1),TGCASORT RESET SORT                                   
         B     PRWEC15                                                          
*                                                                               
PRWEC12  NC    TBWSTAT,WROLSTAT    MATCH ON WRITER ROLE                         
         BZ    PRWEC20                                                          
         GOTO1 CATVAL,DMCB,(X'80',WROLCTEQ)                                     
         MVC   TBWSTAT,WROLSTAT    SET CURRENT WRITER STATUS                    
         MVC   TGCSORT(1),TGCASORT                                              
PRWEC15  BAS   RE,PROCEC           ADD/DELETE ECAST RECORDS                     
*                                                                               
PRWEC20  LA    R5,WROLTABL(R5)     BUMP TO NEXT ENTRY                           
         CLI   0(R5),X'FF'         LOOP IF NOT END OF TABLE                     
         BNE   PRWEC10                                                          
         GOTO1 CATVAL,DMCB,TABCAT  RESET GLOBAL CATEGORY INFO                   
         MVC   TGCSORT(1),TGCASORT AND RESET GLOBAL SORT                        
         B     ECASTX                                                           
         DROP  R5                                                               
         EJECT                                                                  
*              ADD/DELETE ECAST RECORD                                          
*                                                                               
PROCEC   NTR1                                                                   
         BAS   RE,ECREAD           READ FOR ECAST RECORD (SETS TBBYTE)          
*                                                                               
         TM    TBSTAT,TAINSCIN     FOR CANCELLED INVOICE                        
         BNO   PROCEC30                                                         
         CLI   TBBYTE,C'Y'         IF ECAST RECORDS ON FILE                     
         BNE   PROCECX                                                          
         LA    R1,KEY                                                           
         USING TLECPD,R1                                                        
         CLC   TLECCINV(5),TEMP    CHECK ECAST BELONGS TO ORIG INV#             
         BNE   PROCECX                                                          
         BAS   RE,ECCANCEL         DELETE THEM FROM FILE                        
         B     PROCECX                                                          
         DROP  R1                                                               
*                                  FOR REGULAR INVOICES                         
PROCEC30 CLI   TBBYTE,C'Y'         IF ECAST RECS NOT ON FILE                    
         BE    PROCECX                                                          
         BAS   RE,ECADD            ADD TO FILE                                  
PROCECX  B     ECASTX                                                           
         EJECT                                                                  
*              ROUTINE TO SEE IF ECAST RECORD ALREADY EXISTS                    
*                                  XIT TBBYTE = 'Y' THEN KEY=TLECPKEY           
         SPACE                                                                  
ECREAD   NTR1                                                                   
         XR    R2,R2               SAVE CATEGORY                                
         ICM   R2,7,TGCAT                                                       
*                                                                               
         TM    TGCATYPE,WRITER                                                  
         BO    ECREAD10                                                         
         GOTO1 RECVAL,DMCB,TLECCCDQ,0                                           
         CLC   KEY(TLECCINV-TLECPKEY),KEYSAVE                                   
         BNE   ECREADN                                                          
         B     ECREADY                                                          
*                                                                               
ECREAD10 MVC   TGCAT,=CL3'W'       READ HIGH FOR ANY WRITER                     
         GOTO1 RECVAL,DMCB,TLECCCDQ,0                                           
         B     ECREAD15                                                         
*                                                                               
ECREAD12 GOTO1 SEQ                                                              
ECREAD15 CLC   KEY(TLECCCAT-TLECPKEY),KEYSAVE                                   
         BNE   ECREADN                                                          
         LA    R4,KEY                                                           
         USING TLECPD,R4                                                        
         GOTO1 CATVAL,DMCB,TLECCCAT                                             
         CLC   =CL3'W',TABCAT      CHECK COMPATABLE WRITER CATEGORY             
         BNE   ECREAD18                                                         
         CLI   TGCAEQU,CTWH                                                     
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTWS                                                     
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTWB                                                     
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW                                                      
         BE    ECREAD20                                                         
         B     ECREAD12                                                         
*                                                                               
ECREAD18 CLC   =CL3'W2',TABCAT     CHECK COMPATABLE WRITER CATEGORY             
         BNE   ECREAD19                                                         
         CLI   TGCAEQU,CTW2H                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW2S                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW2B                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW2                                                     
         BE    ECREAD20                                                         
         B     ECREAD12                                                         
*                                                                               
ECREAD19 CLC   =CL3'W3',TABCAT     CHECK COMPATABLE WRITER CATEGORY             
         BNE   ECREADN                                                          
         CLI   TGCAEQU,CTW3H                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW3S                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW3B                                                    
         BE    ECREAD20                                                         
         CLI   TGCAEQU,CTW3                                                     
         BNE   ECREAD12                                                         
*                                                                               
ECREAD20 GOTO1 GETREC                                                           
         L     R4,AIO                                                           
         MVI   ELCODE,TASOELQ                                                   
         BAS   RE,GETEL2                                                        
         BNE   ECREAD12                                                         
         USING TASOD,R4                                                         
         MVC   BYTE,TASOSTAT                                                    
         NC    BYTE,TBWSTAT        CHECK MATCH ON CURRENT WRITER ROLE           
         BZ    ECREAD12                                                         
*                                                                               
ECREADY  MVI   TBBYTE,C'Y'         ECAST RECORD FOUND                           
         B     *+8                                                              
ECREADN  MVI   TBBYTE,C'N'         ECAST RECORD NOT FOUND                       
         STCM  R2,7,TGCAT          RESET CATEGORY                               
         GOTO1 CATVAL,DMCB,TGCAT                                                
         B     ECASTX                                                           
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        DELETE ECAST RECORDS FROM FILE (FOR CANCELLED INVOICES)                
*                                                                               
ECCANCEL NTR1                                                                   
*                                                                               
         BRAS  RE,CKUPDTE          CHECK FOR UPDATE OPTION                      
         GOTO1 GETREC             GET ECAST RECORD                              
         GOTO1 ASAVPTRS,DMCB,ASPBLK                                             
         L     R3,AIO                                                           
         USING TLRCD,R3                                                         
         OI    TLRCSTAT,X'80'     MARK RECORD DELETED                           
         GOTO1 PUTREC                                                           
         GOTO1 AMYTRACE,DMCB,(RC),=C'DELETED ECAST REC',AIO,0                   
*                                                                               
         XC    KEY,KEY             MARK ACTIVE DIRECTORY KEY DELETED            
         MVC   KEY(L'TLDRKEY),TLRCKEY                                           
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLDRKEY),KEYSAVE                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    KEY+TLDRSTAT-TLDRKEY,X'80'                                       
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 AMYTRAC2,DMCB,(RC),=C'DELETED ECAST KEY',KEY                     
*                                                                               
         MVI   BYTE,X'40'         TREAT AS DELETED                              
         TM    TBOPTS,TBTRACE                                                   
         BNO   ECCAN30                                                          
         OI    BYTE,X'10'         TRACE OFFLINE FOR ADDPTRS                     
ECCAN30  GOTO1 AADDPTRS,DMCB,(BYTE,ASPBLK)                                      
         B     ECASTX                                                           
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        ADD ECAST RECORD TO FILE                                               
*                                                                               
ECADD    NTR1                                                                   
*                                  SET ACTIVE ECAST KEY                         
         GOTO1 RECVAL,DMCB,TLECCDQ,(X'40',0)                                    
         LA    R3,KEY                                                           
         USING TLECD,R3                                                         
         MVC   TLECINV,TEMP        SET UN-COMPLEMENTED INVOICE NUMBER           
*                                                                               
         L     R3,AIO                                                           
         MVC   TLECKEY,KEY         SET KEY IN IOAREA                            
         MVC   TLECLEN,DATADISP    START START LENGTH                           
         XC    TLECSTAT(10),TLECSTAT                                            
*                                                                               
         L     R4,AIO2                                                          
         MVI   ELCODE,TACAELQ      GET CAST DETAILS ELEMENT                     
         BAS   RE,GETEL2                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R4                                                         
         MVC   ELEMENT,TACAEL      SET ELEMENT                                  
         LA    R4,ELEMENT                                                       
         XC    TACANCDE,TACANCDE   DON'T PASS THROUGH AGENT CODE                
         GOTO1 ADDELEM             ADD CAST DETAILS ELEMENT                     
         DROP  R4                                                               
*                                                                               
         L     R4,AIO2                                                          
         MVI   ELCODE,TACRELQ      GET APPLIED CREDIT HISTORY ELEMENT           
         BAS   RE,GETEL2                                                        
         BNE   ECADD10                                                          
         USING TACRD,R4                                                         
         MVC   ELEMENT,TACREL      SET ELEMENT                                  
         BAS   RE,SETTACR          SET TACR AMTS & INDICATOR ON RECORD          
         GOTO1 ADDELEM             ADD ADD IT                                   
         DROP  R4                                                               
*                                                                               
ECADD10  LA    R4,ELEMENT          ADD SOAP ELEMENT WITH                        
         XC    ELEMENT,ELEMENT                                                  
         USING TASOD,R4                                                         
         MVI   TASOEL,TASOELQ                                                   
         MVI   TASOLEN,TASOLNQ+L'TASOSEPI                                       
         MVI   TASONUM,1           ONE EPISODE NUMBER                           
         MVC   TASOEPI,TFULL       SET BINARY EPISODE NUMBER                    
         MVC   TASOSTAT,TBWSTAT    SET EPISODE STATUS                           
         GOTO1 ADDELEM             ADD IT                                       
         DROP  R4                                                               
*                                                                               
         GOTO1 ACTVIN,DMCB,0       ADD ACTIVITY ELEMENT                         
         GOTO1 ADDREC              ADD RECORD                                   
         GOTO1 AMYTRACE,DMCB,(RC),=C'ECAST REC',AIO,0                           
*                                                                               
         MVI   BYTE,0                                                           
         TM    TBOPTS,TBTRACE                                                   
         BNO   ECADD30                                                          
         MVI   BYTE,X'10'          OFFLINE TRACE                                
ECADD30  L     R2,ASPBLK                                                        
         XC    0(L'TLDRREC+1,R2),0(R2)                                          
         GOTO1 AADDPTRS,DMCB,(BYTE,(R2))                                        
         B     ECASTX                                                           
         EJECT                                                                  
*              ROUTINE SETS TACRD ELEMENT AMOUNTS                               
         SPACE                                                                  
SETTACR  NTR1                                                                   
         BAS   RE,THRESHLD         GET THRESHOLD                                
         BNE   SETTACRX                                                         
*                                                                               
         LA    R4,ELEMENT                                                       
         USING TACRD,R4                                                         
*                                                                               
         L     R1,TACRSCAL         R1=(PERF RATE - THRESHOLD)                   
         S     R1,TGFULL                                                        
         LTR   R1,R1               IF POSITIVE AMOUNT                           
         BNP   SETTACRX                                                         
         ST    R1,TACRAPPL         SET EQUAL TO CREDIT APPLIED                  
         ST    R1,TACRBAL                    AND CREDIT BALANCE                 
         OI    TLECSTAT,TLECSBAL   INDICATE BALANCE ON RECORD                   
*                                                                               
SETTACRX B     ECASTX                                                           
         EJECT                                                                  
*              ROUTINE PASS BACK THRESHOLD IN TGFULL                            
         SPACE                                                                  
THRESHLD NTR1                                                                   
         LA    R1,MAXEPIS          NUMBER OF EPISODE ENTRIES                    
         L     R2,=V(EPITAB)       R2=A(EPISODE TABLE)                          
         USING EPITABD,R2                                                       
*                                                                               
THRES10  OC    0(EPITABL,R2),0(R2) TEST END OF TABLE                            
         BZ    THRES20                                                          
         CLC   EPIAGY,TGAGY        MATCH ON AGENCY                              
         BNE   *+14                                                             
         CLC   EPINUM,TFULL        AND ON EPISODE NUMBER                        
         BE    THRES40                                                          
         LA    R2,EPITABL(R2)      TRY NEXT TABLE ENTRY                         
         BCT   R1,THRES10                                                       
*                                                                               
         L     R2,=V(EPITAB)       END OF TABLE - SET TO REUSE BEG.             
         XC    0(10*EPITABL,R2),0(R2)                                           
*                                                                               
THRES20  MVC   EPIAGY,TGAGY        NOT IN TABLE - ADD NEW ENTRY                 
         MVC   EPINUM,TFULL                                                     
         LA    R4,BLOCK            R4=A(EPISODE RECORD)                         
         ST    R4,AIO                                                           
         GOTO1 RECVAL,DMCB,TLEPCDQ,(X'20',0)                                    
         MVC   AIO,=V(AREA)        RESET AIO                                    
         BNE   THRESNO                                                          
*                                                                               
         USING TAEID,R4                                                         
         MVI   ELCODE,TAEIELQ                                                   
         BAS   RE,GETEL2                                                        
         BNE   THRESNO                                                          
         MVC   EPIADT,TAEIADT                                                   
*                                                                               
THRES40  MVI   BYTE,X'80'          THRESHOLD FOR 30M SHOW                       
         TM    TBAYSTA3,TAAYS30M                                                
         BO    *+8                                                              
         MVI   BYTE,0                                                           
         GOTO1 GETTHRES,DMCB,(BYTE,EPIADT)                                      
         BNE   THRESNO                                                          
         CR    RB,RB                                                            
         B     THRESX                                                           
*                                                                               
THRESNO  LTR   RB,RB                                                            
THRESX   B     ECASTX                                                           
         EJECT                                                                  
         GETEL2 R4,DATADISP,ELCODE                                              
         SPACE 3                                                                
COMPLM2  DC    X'FFFFFFFFFFFF'                                                  
         SPACE 3                                                                
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        SET INFO FOR TRACE ROUTINE                                             
*                                                                               
         DS    0D                                                               
MYTRACE  NMOD1 0,*MYTRC*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         TM    TBOPTS,TBTRACE                                                   
         BNO   MYTRACEX                                                         
         LM    R2,R4,4(R1)         R2=A(LITERAL),R3=A(IOAREA),R4=A(LEN)         
         ZIC   RF,4(R1)            RF=L'LITERAL                                 
         GOTO1 TRACE,DMCB,(R3),(R4),(R2),(RF)                                   
MYTRACEX XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
*        SET INFO FOR TRACE ROUTINE                                             
*                                                                               
         DS    0D                                                               
MYTRACE2 NMOD1 0,*MYTR2*                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC             GENCON DSECT                                 
*                                                                               
         TM    TBOPTS,TBTRACE                                                   
         BNO   MYTRAC2X                                                         
         LM    R2,R3,4(R1)         R2=A(LITERAL), R3=A(I/O AREA)                
         ZIC   R4,4(R1)            R4=L'LITERAL                                 
         GOTO1 TRACE,DMCB,(R3),L'TLDRREC,(R2),(R4)                              
MYTRAC2X XIT1                                                                   
         EJECT                                                                  
*              BREAKDOWN OF WRITER ROLES                                        
WROLTAB  DS    0CL5                                                             
         DC    CL3'W  ',AL1(TASOSH),AL1(CTWH)                                   
         DC    CL3'W  ',AL1(TASOSS),AL1(CTWS)                                   
         DC    CL3'W  ',AL1(TASOSB),AL1(CTWB)                                   
         DC    CL3'W2 ',AL1(TASOSH),AL1(CTW2H)                                  
         DC    CL3'W2 ',AL1(TASOSS),AL1(CTW2S)                                  
         DC    CL3'W2 ',AL1(TASOSB),AL1(CTW2B)                                  
         DC    CL3'W3 ',AL1(TASOSH),AL1(CTW3H)                                  
         DC    CL3'W3 ',AL1(TASOSS),AL1(CTW3S)                                  
         DC    CL3'W3 ',AL1(TASOSB),AL1(CTW3B)                                  
         DC    X'FF'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              CONVERT CAN$ TO US $ IN PERFORMERS TABLE (CHKTAB)                
         SPACE 1                                                                
         DS    0D                                                               
CVTTAB   NMOD1 0,CVTTAB                                                         
         L     RC,SAVERC                                                        
         L     R3,0(R1)            R3 = A(CURRENT CHECK TABLE ENTRY)            
         USING TABCHKD,R3                                                       
*                                                                               
         LA    R2,TABGRS           START AT AMOUNTS                             
         LA    R4,TABENTRY         N'TABLE ENTRIES                              
         BRAS  RE,CVTAMTS          CONVERT AMOUNTS                              
*                                                                               
         TM    TBOPTS,TBRERUN      IF THIS IS A RERUN                           
         BNO   *+12                                                             
         TM    TBBDSTAT,TABDSCNW   & BILLED THE OLD WAY                         
         BNO   CVTX                DONE                                         
*                                                                               
         LA    R2,TABTAX           START AT AMOUNTS                             
         LA    R4,TABBDL/L'TABTAX  N'TABLE ENTRIES                              
         BRAS  RE,CVTAMTS          CONVERT AMOUNTS                              
*                                                                               
CVTX     XIT1                                                                   
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
SAVERC   DC    A(0)                                                             
         EJECT                                                                  
*        MULTIPLY 2 FACTORS                                                     
*              P1     - 1ST FACTOR                                              
*              P2     - 2ND FACTOR                                              
*              OUTPUT - P1                                                      
*                                                                               
         DS    0D                                                               
MULTIPLY NMOD1 0,**MULT**                                                       
         L     R2,4(R1)            A(P2)                                        
         L     RE,0(R2)            2ND FACTOR                                   
         XR    R2,R2                                                            
         L     R3,0(R1)            1ST FACTOR                                   
*                                                                               
         MR    R2,RE               R2,R3=RE*R3                                  
         D     R2,=F'5000'         PERCENTAGE                                   
         LTR   R3,R3               R3 = QUOTIENT                                
         BM    *+8                                                              
         AHI   R3,1                ROUND                                        
         SRA   R3,1                                                             
         ST    R3,0(R1)                                                         
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        TEST GENERAL SITUATIONS WHICH REQUIRE EXTRACTION                       
*              CC EQUAL IF EXTRACTING                                           
*                                                                               
         DS    0D                                                               
XTRCT    NMOD1 0,**XTRT**                                                       
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
*                                                                               
         TM    TBOPTS2,TBFIXINV    IF FIXING INOICES                            
         BO    XT10                IGNORE RERUN                                 
         TM    TBOPTS,TBRERUN      ELSE EXTRACT IF RERUN                        
         BO    XTRYES                                                           
*                                                                               
XT10     TM    TBSTAT,TAINSCIN     OR IF INVOICE IS CANCELLED                   
         BO    XTRYES                                                           
         TM    TBSTA2,TAINSHLP       AND PRINTED                                
         BO    XTRYES                                                           
*                                                                               
XTRNO    LTR   RB,RB               DON'T EXTRACT - RETURN CC NE                 
         B     XTRX                                                             
*                                                                               
XTRYES   CR    RB,RB               EXTRACT - RETURN CC EQ                       
*                                                                               
XTRX     XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SET INTERFACE INFO FROM PROD CLI                                       
*                                                                               
         SPACE 1                                                                
         DS    0D                                                               
STSPECIF NMOD1 0,STPCIF                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
*                                                                               
         CLI   INTXCLI,C'Y'        IF THIS CLIENT EXCLUDE                       
         BE    SPCX                   EXIT                                      
         CLI   INTFACE,C'N'        IF NOT INTERFACE                             
         BE    SPCX                   EXIT                                      
         OC    TBEST,TBEST                                                      
         BZ    SPCX                                                             
         CLC   LPCLI,TBEST         SAME PRODUCTION CLIENT                       
         BE    SPC30                                                            
         MVC   TGPCLI(3),TBEST     SET PRODUCTION CLIENT                        
         OC    TGPCLI,SPACES                                                    
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLIFCDQ,(X'24',0)                                    
         BNE   SPC30                                                            
*                                                                               
         MVI   INTFACE,C'C'        SET FLAG                                     
         USING TAIFD,R4            SAVE AGENCY INTERFACE ELEMENT                
         L     R4,AIO                                                           
         AH    R4,DATADISP                                                      
         XR    R1,R1                                                            
SPC10    CLI   0(R4),0                                                          
         BNE   *+12                                                             
         MVI   ERRSTAT,TAINEIFE    NO INTERFACE ELEMENT                         
         B     SPC40                                                            
*                                                                               
         CLI   0(R4),TAIFELQ                                                    
         BE    SPC20                                                            
         ICM   R1,1,1(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R1                                                            
         B     SPC10                                                            
*                                                                               
SPC20    ZIC   R2,1(R4)            LENGTH OF ELEMENT                            
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   IFCLR(0),0(R4)                                                   
*                                                                               
         CLI   OPEN,C'Y'           IF WORKER FILE ALREADY OPEN                  
         BE    SPC30                  EXIT                                      
         GOTO1 USERVAL,DMCB,(X'80',TAIFAGY)                                     
         BE    *+6                 GET HEXCOMP FOR THIS ALPHA AGY               
         DC    H'0'                                                             
         MVI   SWPOST,POSTPROD                                                  
         GOTO1 =V(TABIPOST),DMCB,(RC),EOPENWK                                   
*                                                                               
SPC30    MVC   LPCLI,TBEST         SET PRODUCTION CLIENT                        
*                                                                               
SPC40    MVC   AIO,AIO1                                                         
*                                                                               
SPCX     XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CHECK IF USING UPDATE=DATE OPTION                                      
*        NEED TO READ FOR UPDATE                                                
*                                                                               
CKUPDTE  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,TBILLD                                                        
         AH    R1,=Y(TBOPTS3-TBILLD)  R1 -> TBOPTS3                             
         TM    0(R1),TBUPDDTE      IF UPDATE OPTION USED                        
         JZ    XIT                                                              
         MVI   RDUPDATE,C'Y'       READ FOR UPDATE                              
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CHECK IF USING UPDATE=DATE OPTION                                      
*                                                                               
CKUPDT2  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,TBILLD                                                        
         AH    R1,=Y(TBOPTS3-TBILLD)  R1 -> TBOPTS3                             
         TM    0(R1),TBUPDDTE      IF UPDATE OPTION USED                        
         JZ    NO                                                               
         J     YES                 RETURN CC =                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        GET INFO FROM LIFT OR VERSIONS ELEMENT IN INVOICE                      
*                                                                               
GETLF    NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO              GET LIFT ELEMENT                             
         MVI   ELCODE,TALFELQ                                                   
         USING TALFD,R4                                                         
         BRAS  RE,GETEL                                                         
         BNE   GLF5                                                             
         MVC   TBLSEC,TALFSEC      LENGTH OF LIFT                               
         MVC   TBLID,TALFLID       LIFT ID                                      
         B     GLFX                                                             
*                                                                               
GLF5     L     R4,AIO              ELSE GET VERSIONS ELEMENT                    
         MVI   ELCODE,TAVRELQ                                                   
         USING TAVRD,R4                                                         
         BRAS  RE,GETEL                                                         
         BNE   GLFX                                                             
         OI    STAT2,STAVERS                                                    
         MVC   TBVERS,TAVRVERS     VERSION CODE                                 
         MVC   TBVERSID,TAVRCID                                                 
*                                                                               
GLFX     XIT1                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*              ROUTINE CALCULATES USE RANGE FOR ITN USE                         
         SPACE 1                                                                
ITN      NTR1  BASE=*,LABEL=*                                                   
         CLI   TGUSEQU,UITN        IF USE IS ITN                                
         BNE   ITNNO                                                            
         SPACE                                                                  
         XC    TABSTUS,TABSTUS     CLEAR STARTING USE NUMBER                    
         XC    TABUSES,TABUSES     CLEAR NUMBER OF USES TO PAY                  
         SPACE                                                                  
         L     R4,AIO1             R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAUHELQ                                                   
         BRAS  RE,GETEL            IF USAGE HISTORY ELEMENT                     
         BNE   ITN01               IS PRESENT                                   
         USING TAUHD,R4                                                         
         MVC   TABSTUS,TAUHIFUS    JUST USE THE MANUALLY INPUTTED               
         MVC   TABUSES,TAUHINUS    START USE AND # OF USES                      
         B     ITNYES                                                           
         SPACE                                                                  
ITN01    MVC   AIO,AIO2                                                         
         SPACE                                                                  
         XC    VERSIONS,VERSIONS                                                
         LA    R4,KEY              BUILD PASSIVE KEY FOR CAST                   
         USING TLCAPD,R4                                                        
         XC    TLCAPKEY,TLCAPKEY                                                
         MVI   TLCAPCD,TLCACCDQ    RECORD CODE                                  
         MVC   TLCACSSN,TABSSN     SOCIAL SECURITY NUMBER                       
         MVC   TLCACCOM,TGCOM      COMMERCIAL                                   
         MVC   TLCACCAT,TABCAT     CATEGORY                                     
         GOTO1 HIGH                                                             
         CLC   KEY(TLCACCAT+L'TLCACCAT-TLCAPCD),KEYSAVE                         
         BNE   ITNYES                                                           
         GOTO1 GETREC                                                           
         MVI   ELCODE,TAFNELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAFNTVER))                                     
         BNE   ITN05                                                            
         USING TAFND,R4                                                         
         L     R4,TGELEM           SAVE WHICH VERSIONS THE CAST                 
         ZIC   RE,TAFNLEN          MEMBER IS ON                                 
         SHI   RE,4                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   VERSIONS(0),TAFNNAME                                             
         SPACE                                                                  
ITN05    LA    R4,KEY              BUILD PASSIVE KEY FOR INVOICE                
         USING TLINPD,R4                                                        
         XC    TLINPKEY,TLINPKEY                                                
         MVI   TLINPCD,TLINHCDQ    RECORD CODE                                  
         MVC   TLINHCOM,TGCOM      INTERNAL COMMERCIAL NUMBER                   
         GOTO1 HIGH                GET DIRECTORY RECORD                         
         B     ITN20                                                            
ITN10    GOTO1 SEQ                                                              
ITN20    CLC   KEY(TLINHCOM+L'TLINHCOM-TLINPCD),KEYSAVE                         
         BNE   ITN40                                                            
         GOTO1 GETREC              READ INVOICE RECORD                          
         SPACE                                                                  
         USING TAIND,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAINELQ      SKIP CANCELLED AND CANCELLER                 
         BRAS  RE,GETEL            INVOICES                                     
         BNE   ITN10                                                            
         TM    TAINSTAT,TAINSCIN+TAINSCAN                                       
         BNZ   ITN10                                                            
         DROP  R4                                                               
         SPACE 1                                                                
         L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL            R4=A(PAYMENT DETAILS ELEMENT)                
         BNE   ITN10                                                            
         USING TAPDD,R4                                                         
         CLC   TAPDUSE,=C'CLA'     HAS TO BE FOR CLASS A USE                    
         BNE   ITN10                                                            
         SPACE                                                                  
         MVC   SVSTAT,TAPDPST1     SAVE STATUS                                  
         SPACE                                                                  
         MVI   VERSTAT,0                                                        
         L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAVRELQ                                                   
         BRAS  RE,GETEL            IF INVOICE WAS PAID TO VERSION               
         BNE   ITN25                                                            
         USING TAVRD,R4                                                         
         MVC   VERSTAT,TAVRVERS    SAVE VERSION LETTER IN VERSTAT               
         SPACE                                                                  
         LA    RE,VERSIONS                                                      
ITN21    CLI   0(RE),0             IF CAST MEMBER IS NOT ON THE                 
         BE    ITN10               INVOICE'S VERSION, SKIP THIS                 
         CLC   VERSTAT,0(RE)       INVOICE                                      
         BE    ITN25                                                            
         LA    RE,1(RE)                                                         
         B     ITN21                                                            
         SPACE                                                                  
ITN25    L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TANPELQ                                                   
         BRAS  RE,GETEL            READ ALL PROGRAM ELEMENTS                    
         B     *+8                                                              
ITN30    BRAS  RE,NEXTEL                                                        
         BNE   ITN10                                                            
         USING TANPD,R4                                                         
         CLC   TANPDATE,TBCDTS     IF DATE DOES NOT FIT WITHIN                  
         BL    ITN30               CYCLE, REJECT IT                             
         CLC   TANPDATE,TBCDTS+3                                                
         BH    ITN30                                                            
         SPACE                                                                  
         TM    STAT2,STAVERS       IF INVOICE NOT TO VERSION                    
         BO    ITN33                                                            
         CLI   TABLIFT,C'O'        IF CAST MEMBER IS ONLY ON                    
         BNE   ITN32               LIFT                                         
         CLI   TANPLFT,C'Y'        THEN ONLY ACCEPT LIFT PROGRAMS               
         BNE   ITN30                                                            
         B     ITN33                                                            
         SPACE                                                                  
ITN32    CLI   TABLIFT,C'Y'        IF CAST MEMBER IS NOT ON LIFT                
         BE    ITN33                                                            
         CLI   TANPLFT,C'Y'        THEN REJECT LIFT PROGRAMS                    
         BE    ITN30                                                            
         SPACE                                                                  
ITN33    CLI   TABYR,C'8'          IF CAST YEAR >= 2000                         
         BE    ITN34                                                            
         CLI   TABYR,C'9'                                                       
         BE    ITN34                                                            
         CLI   TANPNWK,C'X'        THEN SKIP PAX PROGRAMS                       
         BE    ITN30                                                            
         SPACE                                                                  
ITN34    LH    RE,TABSTUS                                                       
         TM    SVSTAT,TAPDPCRD     DECREMENT # OF CLA USES                      
         BZ    *+12                IF CREDIT INVOICE                            
         SHI   RE,1                                                             
         B     *+8                                                              
         AHI   RE,1                OTHERWISE                                    
         STH   RE,TABSTUS          INCREMENT # OF CLA USES                      
         SPACE                                                                  
         CLI   TANPNWK,C'I'        IF PROGRAM IS ITN                            
         BNE   ITN30                                                            
         LH    RE,TABUSES                                                       
         TM    SVSTAT,TAPDPCRD     DECREMENT ITN COUNTER                        
         BZ    *+12                IF CREDIT INVOICE                            
         SHI   RE,1                                                             
         B     *+8                                                              
         AHI   RE,1                OTHERWISE                                    
         STH   RE,TABUSES          INCREMENT ITN COUNTER                        
         B     ITN30                                                            
         SPACE                                                                  
ITN40    LH    RE,TABSTUS          CONVERT TABSTUS FROM CLASS A                 
         SH    RE,TABUSES          COUNTER TO STARTING USE #                    
         AHI   RE,1                                                             
         STH   RE,TABSTUS                                                       
         SPACE                                                                  
         LH    RE,TABSTUS                                                       
         CHI   RE,0                IF START USE IS NEGATIVE #                   
         BNL   *+10                CONVERT TO ZERO                              
         XC    TABSTUS,TABSTUS                                                  
         SPACE                                                                  
         LH    RF,TABUSES                                                       
         CHI   RF,0                IF ITN USES IS NEGATIVE #                    
         BNL   *+10                CONVERT TO ZERO                              
         XC    TABUSES,TABUSES                                                  
         SPACE                                                                  
ITNYES   XR    RC,RC                                                            
ITNNO    LTR   RC,RC                                                            
         XIT1                                                                   
         SPACE                                                                  
SVSTAT   DS    XL1                                                              
VERSIONS DS    XL255                                                            
VERSTAT  DS    XL1                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE UPDATES BILLING DETAILS2 ELEMENT FOR EMS DURING          
*              BILLING ONLY                                                     
*                                                                               
UPDEMS   NTR1  BASE=*,LABEL=*                                                   
         CLC   TGAGY,=CL6'8800'    IF AGENCY IS 8800                            
         BE    UEMS00A                                                          
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BNE   UEMS00B                                                          
UEMS00A  CLI   RECNUM,BI           MUST BE BILLING COPY                         
         BE    UEMS00C                                                          
         B     UPDEMSX                                                          
*                                                                               
UEMS00B  CLI   TBTPO,C'Q'          ELSE MUST BE OFFICE Q                        
         BNE   UPDEMSX                                                          
         CLI   RECNUM,PB           AND PRINT BILLING COPY                       
         BNE   UPDEMSX                                                          
*                                                                               
UEMS00C  TM    TBOPTS2,TBFIXINV    IF FIXING INVOICES                           
         BO    UEMS10              DON'T CHECK IF RERUN                         
         TM    TBOPTS,TBRERUN      IF JOB IS A RE-RUN                           
         BO    UPDEMSX                                                          
*                                                                               
UEMS10   TM    TBSTAT,TAINSCIN     OR IF CANCELLED INVOICE                      
         BO    UPDEMSX                                                          
         TM    TBSTA2,TAINSHLR     OR IF INVOICE IS RELEASED                    
         BNO   UEMS20                                                           
         TM    TBSTA2,TAINSHLP        & WAS PRINTED                             
         BO    UPDEMSX             THEN INFO ALREADY SET                        
*                                                                               
UEMS20   MVI   TGBYTE,0            CLEAR FLAG FOR ADDING ELEMENT                
         L     R4,AIO                                                           
         USING TABDD,R4            BILL DETAILS ELEMENT                         
         MVI   ELCODE,TABDELQ2                                                  
         BRAS  RE,GETEL            ELEMENT SHOULD EXIST                         
         BE    UEMS30              IF IT DOESN'T EXIST, ADD IT                  
         MVI   TGBYTE,1            SET FLAG TO ADD ELEMENT                      
         XC    ELEM,ELEM                                                        
         LA    R4,ELEM                                                          
         MVI   TABDEL,TABDELQ2                                                  
         MVI   TABDLEN,TABDLN2Q                                                 
UEMS30   MVC   TABDTYPE,GTYPE                                                   
         CLI   TBTPO,C'O'          OFFICE O                                     
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OFFICE Q                                     
         BNE   UEMS35                                                           
         MVC   TABDTYPE,ADRBTYPE                                                
         L     RF,TBTOT            BILLING TOTAL                                
         S     RF,TBTAX                                                         
         S     RF,TBHANDI                                                       
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   UEMS33                                                           
         S     RF,TBCHANDU         CAN HANDLING IN US$                          
         B     *+8                                                              
UEMS33   S     RF,TBHANDC                                                       
         A     RF,EMSTTAX          ADD ADD'L TAX AND HANDLING BACK IN           
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   UEMS34                                                           
         A     RF,EMSTHNDU         CHLOE CAN HANDLING IN US$                    
         B     *+8                                                              
UEMS34   A     RF,EMSTHND                                                       
         ST    RF,EMSTOT                                                        
*                                                                               
UEMS35   MVC   TABDTOT,EMSTOT      TOTAL                                        
         MVC   TABDTAX,EMSTTAX     TAX                                          
         MVC   TABDHND,EMSTHND     HANDLING FEE                                 
         MVC   TABDACOM,EMSTEMS    EMS FEE                                      
         MVC   TABDCSF,TCSF        CONTRACT SERVICE FEE                         
         TM    TBIPST2,TAPDPEUR    IF THIS IS A EURO PAYMENT                    
         BZ    UEMS36                                                           
         ZAP   TABDCCVT,TBGCVT     SET EURO CONVERSION RATE                     
UEMS36   TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   UEMS40                                                           
         ZAP   TABDCCVT,TBGCVT     SET CANADIAN CONVERSION RATE                 
         MVC   TABDGST,TBCGST      GST IN CAN$                                  
         L     RF,AOSPST                                                        
         A     RF,TBCPST                                                        
         ST    RF,TABDPST          PST                                          
         OI    TABDSTAT,TABDSCNW   INVOICE PRINTED NEW WAY                      
         B     UEMS50                                                           
UEMS40   MVC   TABDGST,TBGSTU      ELSE, SET POSSIBLE GST FOR US$               
         ICM   RF,15,TBPSTU                                                     
         A     RF,AOSPST                                                        
         ST    RF,TABDPST          PST                                          
UEMS50   CLI   TGBYTE,0            DOES ELEMENT ALREADY EXIST?                  
         BE    UPDEMSX                                                          
         GOTO1 ADDELEM                                                          
UPDEMSX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE GETS EMS BILLING RATES FOR AGROUP EMS AGENCIES           
*              R4 --> TABRD ELEMENT                                             
*                                                                               
         USING TABRD,R4                                                         
GETEMSR  NTR1  BASE=*,LABEL=*                                                   
         CLC   =CL6'8800',TGAGY    IF AGENCY IS 8800                            
         BNE   GEMSYES                                                          
         MVC   AGYEMSRT,TABRFUTA   SAVE THESE RATES AS EMS AGENCY RATES         
*                                                                               
         MVC   TISVAGY,TGAGY       SAVE AGENCY                                  
         MVC   TGAGY,=CL6'8800'  GET BILLING RATES FROM AGY 8800                
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'A4',TGAGY)                                
         BNE   GEMSNO                                                           
         L     R4,AIO              GET BILLING RULES ELEMENT                    
         USING TABRD,R4                                                         
         MVI   ELCODE,TABRELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   GEMSNO                                                           
         MVC   AYBTYPE,TABRTYPE    USE AGENCY 8800'S BILLING TYPE               
         MVC   AYRFUTA(L'TABRRATE),TABRRATE   SAVE 8800 BILLING RATES           
         DROP  R4                                                               
         MVI   TBAYCOPY,2          ONLY NEED ONE BILLING COPY                   
*                                                                               
         MVC   TGAGY,TISVAGY       RESTORE AGENCY                               
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'A4',TGAGY)  RESTORE RECORD                
         BE    *+6                 THERE MUST BE AN AGENCY RECORD               
         DC    H'0'                      IT WAS ALREADY READ                    
         B     GEMSYES                                                          
*                                                                               
GEMSYES  SR    RC,RC               SET CONDITION CODE                           
GEMSNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
*              ROUTINE STORES EMS BILLING RATES FOR AGROUP EMS AGENCIES         
*              ONLY IF THE CLIENT HAS AN OVERRIDE                               
*              R4 --> TABRD ELEMENT                                             
*                                                                               
STEMSRC  NTR1  BASE=*,LABEL=*                                                   
         CLC   =CL6'8800',TGAGY    IF AGENCY IS 8800                            
         BNE   SSRCNO                                                           
         CLI   CLBTYPE,0           DOES CLIENT HAVE BILLING INFO?               
         BNE   SSRC10                                                           
         MVC   GSTAT,AYSTAT        NO, USE AGENCY STATUS AND POOL TYPE          
         MVC   GTYPO,AYTYPO                                                     
         MVC   EMSRATES,AGYEMSRT   USE AGENCY RATES AS EMS RATES                
         B     SSRC40                                                           
*                                                                               
SSRC10   MVC   EMSRATES,CLRFUTA    SAVE CLIENT RATES AS EMS RATES               
         MVC   GSTAT,CLSTAT        USE CLIENT STATUS AND POOL TYPE              
         MVC   GTYPE,CLTYPO                                                     
SSRC40   MVC   GTYPE,AYBTYPE                USE 8800'S BILLING TYPE             
         MVC   GRFUTA(L'TABRRATE),AYRFUTA   SET GENERAL BILLING RATES           
         B     SSRCYES                                                          
*                                                                               
SSRCYES  SR    RC,RC               SET CONDITION CODE                           
SSRCNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE SAVES GRT HANDLING RULE FROM TARA ELEMENT                
*              IF IT EXISTS FOR CLIENT OR AGENCY                                
*                                                                               
GETGHRL  NTR1  BASE=*,LABEL=*                                                   
         USING TARAD,RE                                                         
         L     RE,ACLITARA                                                      
         CLI   TARAEL,TARAELQ      DO WE HAVE A CLIENT TARA?                    
         JE    GGHRL10                                                          
         L     RE,AAGYTARA                                                      
         CLI   TARAEL,TARAELQ      DO WE HAVE AN AGENCY TARA?                   
         JNE   XIT                                                              
GGHRL10  MVC   GRHRULE,TARAGRUL    APPL GRT HANDLING RULE                       
         J     XIT                                                              
         DROP  RE                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE CHECKS BILLING TYPE IF THERE IS AN INVOICE               
*              LEVEL TARA ELEMENT                                               
*              IF CREDIT INVOICE, USE TARATYPE AS BILLING TYPE                  
*              IF TARATYPE IS MISSING, RETURN CC NEQ                            
*              IF NOT CREDIT INVOICE AND TARATYPE DOES NOT MATCH                
*              GTYPE, RETURN CC NEQ                                             
*                                                                               
         USING TARAD,RE                                                         
CHKBTYP  NTR1  BASE=*,LABEL=*                                                   
         L     RE,ACURTARA                                                      
         CLI   TARAEL,TARAELQ      DO WE HAVE AN INVOICE TARA?                  
         JNE   YES                                                              
***      CLC   GTYPE,TARATYPE      NOT CHECKING BOVER BTYP AT THIS TIME         
***      JE    YES                                                              
         TM    STATUS,STCRDT       IF INVOICE IS A CREDIT,                      
         JZ    YES                                                              
         TM    TBIPST2,TAPDPCIB    IF CREDIT INVOICE HAS SAVED BRATES           
         JZ    YES                                                              
         OC    TARATYPE,TARATYPE   MAKE SURE BILLING TYPE EXISTS                
         JZ    NO                                                               
         MVC   GTYPE,TARATYPE      USE BILLING TYPE OF CREDITED INVOICE         
         J     YES                                                              
         DROP  RE                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE CHECKS IF USING REG OR REG2 OPTIONS                      
*              IF NOT USING OPTIONS, AGENCY, CLIENT AND PYMT OPTION             
*              MUST NOT HAVE REG STATUS.                                        
*              IF SKIPPING INVOICE, RETURN CC NEQ                               
*                                                                               
CHKREG   NTR1  BASE=*,LABEL=*                                                   
         TM    TBOPTS2,TBREGRSN+TBREGRS2  IF NOT USING REG OR REG2,             
         BNZ   CKREG10                                                          
         TM    TBAYSTA6,TAAYSREG   AGY REG STATUS MUST BE OFF                   
         JNZ   NO                                                               
         TM    TBCLSTA2,TACISREG   CLI REG STATUS MUST BE OFF                   
         JNZ   NO                                                               
         J     YES                                                              
                                                                                
CKREG10  L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         USING TAPDD,R4                                                         
         BRAS  RE,GETEL                                                         
         BE    *+6                 IT WAS READ ALREADY                          
         DC    H'00'                                                            
         TM    TBOPTS2,TBREGRSN    REG OPTION?                                  
         BZ    CKREG20                                                          
         CLI   TAPDLEN,TAPDLNQ     CHECK LENGTH                                 
         BL    *+12                                                             
         TM    TAPDOPT5,TAPDORG2   CANNOT HAVE REG2 OPTION ON PYMT              
         JNZ   NO                                                               
         TM    TBAYSTA6,TAAYSREG   IS AGY REG STATUS ON?                        
         JNZ   YES                                                              
         TM    TBCLSTA2,TACISREG   OR IS CLI REG STATUS ON?                     
         JZ    NO                                                               
         J     YES                                                              
                                                                                
CKREG20  TM    TBOPTS2,TBREGRS2    REG2 OPTION?                                 
         JZ    YES                                                              
         CLI   TAPDLEN,TAPDLNQ     CHECK LENGTH                                 
         JL    NO                                                               
         TM    TAPDOPT5,TAPDORG2   MUST HAVE REG2 OPTION ON PYMT                
         JZ    NO                                                               
         J     YES                                                              
                                                                                
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE CLOSES TAPE FOR EMS BILLING                              
*                                                                               
CLOSTAPE NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,BI          IF BILLING OR ACOPY                           
         BE    *+12                                                             
         CLI   RECNUM,AC                                                        
         BNE   CLOSTX                                                           
         TM    STAT2,STOPEN       IF TAPE WAS OPENED                            
         BNO   CLOSTX                                                           
         NI    STAT2,X'FF'-STOPEN TURN OFF OPEN STATUS                          
         OI    STAT2,STCLOSE      CLOSE TAPE                                    
         MVI   LASTPG,C'N'        RESET NOT LAST PAGE TO BE PRINTED             
         GOTO1 =V(TABIPRT),DMCB,(RC)                                            
         NI    STAT2,X'FF'-STCLOSE                                              
CLOSTX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        ROUTINE CHEKCS IF AGENCY IS AN FLIST                                   
*                                                                               
AGYFLIST NTR1  BASE=*,LABEL=*                                                   
         L     R3,ATWA                                                          
         USING T703FFD,R3                                                       
         LA    RF,SBIAGYH                                                       
         GOTOR POSFLIST            IF AGENCY IS A POSITIVE FLIST                
         BE    AGFL10                                                           
***      GOTOR NEGFLIST            OR IF AGENCY IS A NEGATIVE FLIST             
***      BE    AGFL15                                                           
         B     AGFLNO                                                           
AGFL10   ZIC   R1,SBIAGYH+5        POSITIVE FLIST                               
         SHI   R1,2                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TGLST(0),SBIAGY+1                                                
         B     AGFL18                                                           
*&&DO                                                                           
AGFL15   ZIC   R1,SBIAGYH+5        NEGATIVE FLIST                               
         SHI   R1,3                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TGLST(0),SBIAGY+2                                                
*&&                                                                             
AGFL18   OC    TGLST,SPACES                                                     
*                                                                               
         GOTOR GETFFLST           GET FLIST                                     
         BE    AGFL30                                                           
AGFL20   GOTOR GETNFLST           GET NEXT CODE IN FLIST                        
         BNE   AGFLX                                                            
AGFL30   GOTOR AGFLTAB            STORE FLIST AGENCIES INTO AGYTAB              
         B     AGFL20                                                           
AGFLX    XC    TIFAGY,TIFAGY                                                    
         B     AGFLYES                                                          
         DROP  R3                                                               
         SPACE 2                                                                
AGFLYES  SR    RC,RC               SET CONDITION CODE                           
AGFLNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE STORES AGENCIES FROM THE FLIST INTO AGYTAB               
*                                                                               
AGFLTAB  NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(AGYTAB)       R3=A(TABLE)                                  
         USING AGYD,R3                                                          
AFLT10   CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                (INCREASE NAGY)                              
         CLI   0(R3),0             NEXT SLOT                                    
         BE    AFLT20                                                           
         LA    R3,AGYNEXT                                                       
         B     AFLT10              GO TO NEXT OPEN SLOT                         
*                                                                               
AFLT20   MVC   AGYAGY,TIFAGY       BUILD NEW ENTRY                              
         MVC   TGAGY,TIFAGY        SET TO GET AGENCY RECORD                     
         XIT1                                                                   
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE CHECKS FOR FLIST.  SETS CC NEQ IF 1ST CODE IN            
*              FLIST IS INVALID, ELSE SETS CC EQ                                
         SPACE 1                                                                
GETFFLST NTR1  BASE=*,LABEL=*                                                   
         XC    TISVAGY,TISVAGY                                                  
         MVC   FLCOUNT,=H'1'       FLIST COUNTER                                
         XC    TIFAGY,TIFAGY                                                    
         MVC   AIO,AIO2            SET AIO AND READ FLIST                       
         MVI   TGLTYP,TLGLTYPF                                                  
         GOTO1 RECVAL,DMCB,TLGLCDQ,(X'A0',0)                                    
         BE    *+6                                                              
         DC    H'00'                                                            
*                                                                               
         L     R4,AIO2                                                          
         MVI   ELCODE,TAGLELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAGLD,R4                                                         
         ZIC   R1,TAGLLEN                                                       
         SHI   R1,4                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TIFAGY(0),TAGLDATA  CHECK THAT AGENCY EXISTS                     
         OC    TIFAGY,SPACES                                                    
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'80',TIFAGY)                               
         BE    GFLYES              SET CC EQ                                    
         GOTOR AGYWILD             CHECK FOR WILDCARD AGENCY                    
         B     GFLNO               ALWAYS RETURN CC NEQ HERE                    
         DROP  R4                                                               
         SPACE 2                                                                
GFLYES   SR    RC,RC               SET CONDITION CODE                           
GFLNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO CHECK IF ANOTHER REQUEST IS NECESSARY                 
         SPACE 1                                                                
         USING TAGLD,R4            R4=A(CURRENT TAGLD ELEMENT)                  
GETNFLST NTR1  BASE=*,LABEL=*                                                   
GETNF5   XC    TISVAGY,TISVAGY                                                  
         MVC   AIO,AIO2            SET AIO AND READ FLIST                       
         MVI   TGLTYP,TLGLTYPF                                                  
         GOTO1 RECVAL,DMCB,TLGLCDQ,(X'A0',0)                                    
         BE    *+6                                                              
         DC    H'00'                                                            
         MVI   ELCODE,TAGLELQ      RESET ELEMENT CODE                           
         LH    R3,FLCOUNT                                                       
         L     R4,AIO2                                                          
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'00'                                                            
GETNF10  BRAS  RE,NEXTEL           GET NEXT ONE                                 
         BNE   GNFLNO                                                           
         BCT   R3,GETNF10          KEEP ADVANCING TILL WE GET A NEW ONE         
*                                                                               
         LH    RE,FLCOUNT          INCREMENT FLIST COUNTER                      
         AHI   RE,1                                                             
         STH   RE,FLCOUNT                                                       
*                                                                               
         LHI   R3,1                IN CASE WE GO BACK TO THE BCT LOOP           
*                                                                               
         ZIC   R1,TAGLLEN          IF CODE IS EQUAL TO PREVIOUS                 
         SHI   R1,4                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   TIFAGY(0),TAGLDATA                                               
         BE    GETNF10             SKIP IT                                      
*                                                                               
         XC    TIFAGY,TIFAGY       SAVE NEXT AGENCY FROM FLIST                  
         ZIC   R1,TAGLLEN                                                       
         SHI   R1,4                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TIFAGY(0),TAGLDATA  CHECK THAT AGENCY EXISTS                     
         OC    TIFAGY,SPACES                                                    
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'80',TIFAGY)                               
         BE    GNFLYES             SET CC EQ                                    
         GOTOR AGYWILD             IF INVALID, CHECK FOR WILDCARD AGY           
         B     GETNF5              GET NEXT ELEMENT, (MUST RESTORE AIO)         
         DROP  R4                                                               
         SPACE 2                                                                
GNFLYES  SR    RC,RC               SET CONDITION CODE                           
GNFLNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO CHECK IF AGENCY IS A WILDCARD AND                     
*              STORE WILDCARD AGENCIES IN AGYTAB OF FAGYTAB                     
AGYWILD  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,TIFAGY                                                        
         LA    R3,6                CHECK 6 CHARACTERS                           
         GOTOR CKWILD              IS THIS A WILDCARD?                          
         BNE   AWLDNO              IF INVALID, RETURN CC NOT EQ                 
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLAYD,R4                                                         
         MVI   TLAYCD,TLAYCDQ                                                   
         GOTO1 HIGH                READ FIRST AGENCY                            
AGYWLD10 CLC   KEY(1),KEYSAVE                                                   
         BNE   AWLDX                                                            
         LA    R3,6                                                             
         LA    R2,TLAYAGY                                                       
         GOTOR COMPWILD                                                         
         BNE   AGYWLD20                                                         
         MVC   TISVAGY(6),TIFAGY  SAVE AGENCY FILTER                            
         MVC   TIFAGY,TLAYAGY                                                   
         DROP  R4                                                               
         L     RE,ATWA                                                          
         USING T703FFD,RE                                                       
         LA    RF,SBIAGYH                                                       
         DROP  RE                                                               
         GOTOR AGFLTAB             STORE FLIST AGENCIES INTO AGYTAB             
         MVC   TIFAGY,TISVAGY      RESTORE AGENCY FILTER                        
AGYWLD20 GOTO1 SEQ                                                              
         B     AGYWLD10                                                         
         SPACE 2                                                                
AWLDYES  SR    RC,RC               SET CONDITION CODE                           
AWLDNO   LTR   RC,RC                                                            
AWLDX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO CHECK FOR WILD CARD                                   
*              R3=NUMBER OF CHARS, R1-->FILTER                                  
CKWILD   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
CKW10    CLI   0(R1),C'*'          IF THERE IS A '*' THEN THIS                  
         BE    CKWYES              IS A WILDCARD                                
         LA    R1,1(R1)            BUMP TO NEXT LETTER                          
         BCT   R3,CKW10                                                         
         B     CKWNO                                                            
         SPACE 2                                                                
CKWYES   SR    RC,RC               SET CONDITION CODE                           
CKWNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO COMPARE WITH WILDCARD                                 
*              R3=NUMBER OF CHARS, R1-->FILTER, R2-->FIELD IN RECORD            
COMPWILD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
CMPW10   CLI   0(R1),C'*'          IF THIS IS A WILD CARD                       
         BE    CMPW20              THEN DON'T COMPARE LETTERS                   
         CLC   0(1,R1),0(R2)       IF THE LETTER IS DIFFERENT                   
         BNE   CMPWNO              RETURN CC NEQ                                
*                                                                               
CMPW20   LA    R1,1(R1)            BUMP TO NEXT LETTER                          
         LA    R2,1(R2)                                                         
         BCT   R3,CMPW10                                                        
         B     CMPWYES                                                          
         SPACE 2                                                                
CMPWYES  SR    RC,RC               SET CONDITION CODE                           
CMPWNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE SETS FIRST AGENCY IN FLIST AS TIFAGY                     
*                                                                               
GETFAGY  NTR1  BASE=*,LABEL=*                                                   
         L     R2,ATWA                                                          
         USING T703FFD,R2                                                       
         LA    RF,SBIAGYH                                                       
         DROP  R2                                                               
         GOTOR POSFLIST            POSITIVE OR NEGATIVE FLIST?                  
         BE    GFAGY10                                                          
         DC    H'00'               MUST BE POSITIVE                             
***      GOTOR NEGFLIST                                                         
***      BE    *+6                                                              
***      DC    H'00'                                                            
***      GOTOR NEGMAGY             NEGATIVE AGENCY FLIST                        
***      BE    GFAGYYES            AGENCY NOT IN NEGATIVE FLIST                 
***      B     GFAGYNO             NO MORE AGENCIES                             
*                                                                               
         USING AGYD,R3             R3 ---> BEGINNING OF AGYTAB                  
GFAGY10  L     R3,=A(AGYTAB)       LIST OF FLIST AGENCIES                       
         CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'               (INCREASE NAGY)                              
         CLI   0(R3),0             NEXT EMPTY SLOT                              
         BE    GFAGYNO             NO MORE AGENCIES                             
         MVC   TIFAGY,AGYAGY       SET AGENCY FOR PREP ROUTINE                  
         SPACE 2                                                                
GFAGYYES SR    RC,RC               SET CONDITION CODE                           
GFAGYNO  LTR   RC,RC                                                            
         XIT1  REGS=(R3)                                                        
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE GETS NEXT AGENCY IN FLIST AND SETS AS TIFAGY             
*                                                                               
GETNAGY  NTR1  BASE=*,LABEL=*                                                   
         L     R2,ATWA                                                          
         USING T703FFD,R2                                                       
         LA    RF,SBIAGYH                                                       
         DROP  R2                                                               
         GOTOR POSFLIST            POSITIVE OR NEGATIVE FLIST?                  
         BE    GNAGY10                                                          
         DC    H'00'               MUST BE POSITIVE                             
***      GOTOR NEGFLIST                                                         
***      BE    *+6                                                              
***      DC    H'00'                                                            
***      GOTOR NEGMAGY             NEGATIVE AGENCY FLIST                        
***      BE    GNAGYYES            AGENCY NOT IN NEGATIVE FLIST                 
***      B     GNAGYNO             NO MORE AGENCIES                             
*                                                                               
         USING AGYD,R3             R3 ---> LAST AGENCY IN AGY FLIST             
GNAGY10  LA    R3,AGYNEXT                                                       
         CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'               (INCREASE NAGY)                              
         CLI   0(R3),0             NEXT EMPTY SLOT                              
         BE    GNAGYNO             NO MORE AGENCIES                             
         MVC   TIFAGY,AGYAGY       SET AGENCY FOR PREP ROUTINE                  
         SPACE 2                                                                
GNAGYYES SR    RC,RC               SET CONDITION CODE                           
GNAGYNO  LTR   RC,RC                                                            
         XIT1  REGS=(R3)                                                        
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*&&DO                                                                           
*              ROUTINE SETS EVERY AGENCY RECORD TO TIFAGY EXCEPT FOR            
*              AGENCIES IN THE NEGATIVE AGENCY FLIST                            
*                                                                               
NEGMAGY  NTR1  BASE=*,LABEL=*                                                   
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING TLAYD,R1                                                         
         MVI   TLAYCD,TLAYCDQ                                                   
         MVC   TLAYAGY,TIFAGY                                                   
         GOTO1 HIGH                                                             
NGMAGY10 GOTO1 SEQ                 GET NEXT AGENCY                              
         CLC   KEY(1),KEYSAVE      IF NO MORE AGENCY RECORDS, EXIT              
         BNE   NGMAGNO                                                          
*                                                                               
         USING AGYD,R3                                                          
         L     R3,=A(AGYTAB)      NEGATIVE AGENCY FLIST                         
         B     *+8                                                              
NGMAGY20 LA    R3,AGYNEXT                                                       
         CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'               (INCREASE NAGY)                              
         CLI   0(R3),0             IF NO MORE, AGENCY NOT IN TABLE              
         BE    NGMAGY40                                                         
         CLC   AGYAGY,TLAYAGY      IS THIS AGENCY IN NEG AGY FLIST?             
         BNE   NGMAGY20            IF IT IS, SKIP IT                            
         B     NGMAGY10                                                         
*                                                                               
NGMAGY40 MVC   TIFAGY,TLAYAGY      SET AGENCY FOR PREP ROUTINE                  
*                                                                               
NGMAGYES SR    RC,RC               SET CONDITION CODE                           
NGMAGNO  LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R1,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*              ROUTINE CHECKS IF FIELD IS A POSITIVE FLIST                      
*              RF =A(FIELD HEADER)                                              
*                                                                               
POSFLIST NTR1  BASE=*,LABEL=*                                                   
         CLI   8(RF),C'@'          POSITIVE FLIST?                              
         BNE   PFLNO                                                            
         CLI   9(RF),C'-'          MAKE SURE ITS NOT NEGATIVE                   
         BE    PFLNO                                                            
         B     PFLYES                                                           
         SPACE 2                                                                
PFLYES   SR    RC,RC               SET CONDITION CODE                           
PFLNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE CHECKS IF FIELD IS A NEGATIVE FLIST                      
*              RF =A(FIELD HEADER)                                              
*                                                                               
NEGFLIST NTR1  BASE=*,LABEL=*                                                   
         CLC   8(2,RF),=C'-@'        NEGATIVE FLIST?                            
         BE    NFLYES                                                           
         CLC   8(2,RF),=C'@-'        NEGATIVE FLIST?                            
         BE    NFLYES                                                           
         B     NFLNO                                                            
         SPACE 2                                                                
NFLYES   SR    RC,RC               SET CONDITION CODE                           
NFLNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE CHECKS IF APPLY DATE IS WITHIN PRIMARY COMMERCIAL'S  *         
*        FIRST FIXED CYCLE. IF IT IS, GUARANTEE SHOULD PAY OUT.       *         
***********************************************************************         
                                                                                
         USING TABCHKD,R3                                                       
FIRSTCYC NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO2            READ GUARANTEE RECORD INTO AIO2              
         GOTO1 RECVAL,DMCB,TLGUCDQ,(X'A4',0)                                    
         JNE   NO                                                               
*                                                                               
         USING TAGUD,R4                                                         
         L     R4,AIO              R4=A(GUARANTEE RECORD)                       
         MVI   ELCODE,TAGUELQ      GET GUARANTEE DETAILS ELEMENT                
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         OC    TAGUCOM,TAGUCOM     IF GUARANTEE IS A PER CYCLE                  
         JZ    NO                                                               
         MVC   TGFULL,TAGUCOM      SAVE PRIMARY'S INT. COMM'L NUMBER            
         DROP  R4                                                               
*                                                                               
         USING TLCOPD,R2                                                        
         LA    R2,KEY                                                           
         XC    KEY,KEY             READ PRIMARY COMM'L RECORD                   
         MVI   TLCOPCD,TLCOCCDQ                                                 
         MVC   TLCOCCOM,TGFULL                                                  
         GOTO1 HIGH                                                             
         CLC   TLCOPKEY,KEYSAVE                                                 
         JNE   NO                                                               
         GOTO1 GETREC                                                           
         DROP  R2                                                               
*                                                                               
         USING TACOD,R4                                                         
         L     R4,AIO              R4=A(PRIMARY COMMERCIAL RECORD)              
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         TM    TACOSTA2,TACOPCYC   IF PRIMARY IS A PER CYCLE COMM'L             
         JO    NO                  APPLY DATE MUST BE COVERED                   
         MVC   TGDUB,TACOFCYC      ELSE SAVE FIRST FIXED CYCLE DATE             
         DROP  R4                                                               
*                                                                               
         USING TLCAPD,R2                                                        
         XC    KEY,KEY             READ CAST RECORD ATTACHED TO                 
         MVI   TLCAPCD,TLCAGCDQ    PRIMARY COMMERCIAL                           
         MVC   TLCAGSSN,TABSSN                                                  
         MVC   TLCAGGUA,TGGUA                                                   
         XC    TLCAGGUA,=6X'FF'                                                 
         MVC   TLCAGCOM,TGFULL                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(TLCAGCAT-TLCAPD),KEYSAVE                                     
         BNE   FCYC10                                                           
         GOTO1 GETREC                                                           
         DROP  R2                                                               
*                                                                               
         USING TACAD,R4                                                         
         L     R4,AIO              R4=A(CAST RECORD)                            
         MVI   ELCODE,TACAELQ      GET CAST DETAILS ELEMENT                     
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         OC    TACAFCYC,TACAFCYC   IF FIRST FIXED CYCLE DEFINED AT              
         JZ    FCYC10              THIS LEVEL, SAVE IT                          
         MVC   TGDUB(L'TACAFCYC),TACAFCYC                                       
         DROP  R4                                                               
*                                                                               
         USING TAPDD,R4                                                         
FCYC10   L     R4,TIAREC           R4=A(CHECK RECORD)                           
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         MVC   TGTHREE,TAPDCYCS    SET PAYMENT'S APPLY DATE                     
         TM    TAPDOPT4,TAPDGRTE                                                
         JZ    FCYC20                                                           
         MVC   TGTHREE,TAPDCYCE                                                 
         DROP  R4                                                               
*                                                                               
FCYC20   GOTO1 DATCON,DMCB,(1,TGDUB),(8,WORK)                                   
         MVI   WORK+8,C'-'                                                      
         MVC   WORK+9(5),=C'(3M) ' PREPARE TO CALCULATE END DATE OF             
         TM    TBAYSTAT,TAAYS13W   PRIMARY COMMERCIAL'S FIRST FIXED             
         JZ    *+10                CYCLE                                        
         MVC   WORK+9(5),=C'(13W)'                                              
*                                                                               
         USING PERVALD,R4                                                       
         LA    R4,BLOCK                                                         
         GOTO1 PERVAL,DMCB,(14,WORK),('PVIN1DYL',(R4))  GET CYCLE END           
         CLC   TGTHREE,PVALPEND                                                 
         JH    NO                  PAYMENT IS OK IF APPLY DATE FITS             
         CLC   TGTHREE,PVALPSTA    WITHIN PRIMARY'S FIRST FIXED CYCLE           
         JL    NO                                                               
         DROP  R4                                                               
*                                                                               
         MVI   TABGSTAT,0                                                       
         J     YES                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        GET CHECK DETAIL ELEMENT                                               
                                                                                
         USING TACDD,R4            CHECK DETAILS ELEM                           
GETCCD   NTR1  BASE=*,LABEL=*                                                   
         L     R4,TIAREC                                                        
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                 IF THIS IS  A LIEN CHECK                     
         TM    TACDSTAT,TACDSLIN                                                
         JO    NO                  IGNORE IT                                    
         TM    TACDSTAT,TACDSTRS   IF THIS IS A TRUSTEE CHECK                   
         JO    NO                  IGNORE IT                                    
         J     YES                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        CHECK FOR BOVER RECORD                                                 
*        IF AGENCY IS NOT PREMIUM, CLEAR ANY PREMIUM HAND OVERRIDES             
*                                                                               
                                                                                
CKBOVER  NTR1  BASE=*,LABEL=*                                                   
                                                                                
         LA    RF,TBILLD                                                        
         AH    RF,=Y(TBOPTS3-TBILLD)  RF -> TBOPTS3                             
         TM    0(RF),TBNOBOVR      SKIPPING BOVER FOR COMPARE                   
         JO    XIT                                                              
                                                                                
         TM    TBOPTS,TBRERUN      IF THIS IS NOT A RERUN ...                   
         JO    XIT                                                              
                                                                                
         L     R4,ACURTARA         BILLING RATES ELEMENT                        
         USING TARAD,R4                                                         
         CLI   TARAEL,TARAELQ      DO HAVE AN INVOICE TARA?                     
         JNE   XIT                                                              
         TM    TARASTA1,TARASPRE   IF AGENCY IS NOT PREMIUM SERVICE,            
         JO    XIT                                                              
         DROP  R4                                                               
                                                                                
         L     R4,ACURTARA         BILLING RATES ELEMENT                        
         USING TARAD,R4                                                         
         TM    TARASTA2,TARASSPD   IS PREMIUM HANDLING OVERRIDDEN?              
         JZ    XIT                                                              
         XC    TARAHFP,TARAHFP     GET RID OF PREMIUM OVERRIDE                  
         NI    TARASTA2,X'FF'-TARASSPD   AND TURN OFF STATUS                    
         NI    BOVRSTAT,X'FF'-BOVRHNDP   TURN OFF STATUS                        
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        INITIALIZE INVOICE'S CANADIAN PAYMENT DETAILS ELEMENT                  
*        ON ENTRY ... AIO = A(INVOICE RECORD)                                   
                                                                                
INITCNPD NTR1  BASE=*,LABEL=*                                                   
         GOTOR BLDCNEC,DMCB,AIO    IF BUILD OF CANADIAN PAYMENT                 
         JNE   XIT                 DETAIL ELEMENT IS NECESSARY                  
                                                                                
         BRAS  RE,SETCNRT          SET CAN CONVERSION RATE                      
                                                                                
         TM    TBOPTS,TBRERUN      IF THIS IS NOT A RERUN ...                   
         JO    XIT                                                              
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ      GET US$ PAYMENT DETAILS ELEMENT              
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         ZIC   RE,TAPDLEN          MAKE A COPY OF US$-BASED                     
         BCTR  RE,0                BILLING DETAILS ELEMENT                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEMENT(0),TAPDEL                                                
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
         LA    R4,ELEMENT          CLEAR ALL SUBTOTALS                          
         MVI   TAPDEL,TAPDELQ4     AND ADD TO INVOICE RECORD                    
         XC    TAPDAMTS(TAPDAMTL),TAPDAMTS                                      
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),AIO,(R4),0                          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        IS BUILD OF CANADIAN PAYMENT DETAILS OR BILLING DETAILS                
*        ELEMENT NECESSARY?                                                     
*        ON ENTRY ... P1 = A(INVOICE OR CHECK RECORD)                           
                                                                                
BLDCNEC  NTR1                                                                   
         TM    TBAYSTA6,TAAYSTCA   IF AGENCY HAS CAN STATUS                     
         JO    BCN10                                                            
         TM    TBCLSTA2,TACISTCA   OR CLIENT HAS CAN STATUS ...                 
         JZ    NO                                                               
                                                                                
         USING TAPDD,R4                                                         
BCN10    L     R4,0(R1)                                                         
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         TM    TAPDSTAT,TAPDSCAN   RETURN POSITIVE CONDITION CODE               
         JZ    YES                 IN IS NOT IN CAN$                            
         J     NO                                                               
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        SET CANADIAN CONVERSION RATE                                           
*        ON ENTRY ... AIO = A(INVOICE RECORD)                                   
                                                                                
SETCNRT  NTR1  BASE=*,LABEL=*                                                   
         USING TABDD,R4                                                         
         L     R4,AIO              GET CAN$ BILLING DETAILS ELEMENT             
         MVI   ELCODE,TABDELQ4                                                  
         BRAS  RE,GETEL                                                         
         JNE   SCNRT10                                                          
         OC    TABDCCVT,TABDCCVT   IF DISCOUNTED CONVERSION RATE                
         JZ    XIT                 HAS ALREADY BEEN SAVED                       
         ZAP   TBGCVT,TABDCCVT     USE IT                                       
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
SCNRT10  CLI   TGCUR,C'U'          ELSE, IF PAYMENT IS IN US$ ...               
         JNE   SCNRT20                                                          
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JNE   SCNRT20                                                          
         TM    TAPDOPT3,TAPDOCCR   IF CANADIAN CONVERSION RATE                  
         JZ    SCNRT20             HAS BEEN OVERRIDDEN ...                      
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   SCNRT20                                                          
         ZAP   TBGCVT,TABDCCVT     ... USE IT                                   
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
SCNRT20  ZAP   TBGCVT,TBCCVT       ELSE SUBTRACT 5% FROM CANADIAN               
         SP    TBGCVT,=PL3'500'    CONVERSION RATE                              
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        BUILD CHECK RECORD'S CANADIAN PAYMENT DETAILS ELEMENT                  
*        ON ENTRY ... P1 = A(CHECK RECORD)                                      
                                                                                
BLDCNPD  NTR1  BASE=*,LABEL=*                                                   
         ZICM  R2,1(R1),3          R2=A(CHECK RECORD)                           
                                                                                
         GOTOR BLDCNEC,DMCB,(R2)   IF BUILD OF CANADIAN PAYMENT                 
         JNE   NO                  DETAIL ELEMENT IS NECESSARY ...              
                                                                                
         LR    R4,R2                                                            
         MVI   ELCODE,TAPDELQ4     ... AND HAS NOT ALREADY BEEN BUILT           
         BRAS  RE,GETEL                                                         
         JE    NO                                                               
                                                                                
         USING TAPDD,R4                                                         
         LR    R4,R2                                                            
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         ZIC   RE,TAPDLEN          MAKE A COPY OF US$-BASED                     
         BCTR  RE,0                PAYMENT DETAILS ELEMENT                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEMENT(0),TAPDEL   SAVE CAN$ PAYMENT DETAILS                    
         MVI   ELEMENT,TAPDELQ4                                                 
         DROP  R4                                                               
                                                                                
         ZAP   DUB,TBGCVT          CONVERSION RATE TO BINARY                    
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
                                                                                
         USING TAPDD,R4                                                         
         LA    R4,ELEMENT          CONVERT ALL SUBTOTALS                        
         GOTOR CVT2CAN,DMCB,('TAPDAMTL/4',TAPDGRS)  CONVERT TO CAN$             
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),(R2),(R4),0                         
         J     YES                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        UPDATE INVOICE'S CANADIAN PAYMENT DETAILS ELEMENT                      
*        ON ENTRY ... TIAREC = A(CHECK RECORD)                                  
*                     AIO1   = A(INVOICE RECORD)                                
                                                                                
UPDCNPD  NTR1  BASE=*,LABEL=*                                                   
         USING TAPDD,R4                                                         
         L     R4,TIAREC                                                        
         MVI   ELCODE,TAPDELQ4                                                  
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
         LA    R2,TAPDAMTS         R2=A(CHECK'S CAN TAPD SUBTOTALS)             
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO1             GET INVOICE'S CANADIAN PAYMENT               
         MVI   ELCODE,TAPDELQ4     DETAILS ELEMENT                              
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         LA    R1,TAPDAMTS                                                      
         LHI   R0,TAPDAMTL/4                                                    
UCP10    L     RE,0(R1)            ADD EACH CHECK SUBTOTAL TO THE               
         A     RE,0(R2)            INVOICE'S CANADIAN PAYMENT                   
         ST    RE,0(R1)            DETAILS ELEMENT                              
         LA    R1,4(R1)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,UCP10                                                         
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        CONVERT AMOUNTS FROM US$ TO CAN$                                       
*        ON ENTRY ... P1 BYTE 0 = # OF AMOUNTS                                  
*                     P1        = A(FIRST AMOUNT FIELD)                         
*                     FULL = CAN CONVERSION RATE IN BINARY                      
                                                                                
CVT2CAN  NTR1  BASE=*,LABEL=*                                                   
         ZICM  R3,1(R1),3          R3=A(AMOUNTS TO CONVERT)                     
         ZIC   R4,0(R1)            R4=# OF AMOUNTS                              
                                                                                
C2C10    XR    R0,R0                                                            
         ZICM  R1,0(R3),4                                                       
         TM    0(R3),X'80'         WAS AMOUNT NEGATIVE?                         
         BZ    *+8                                                              
         L     R0,HEXEFFC          MAKE SURE HIGH ORDER REGISTER IS NEG         
                                                                                
         M     R0,=F'100'          MULTIPLY AMOUNT BY 100                       
         D     R0,FULL             DIVIDE BY EURO CONVERSION RATE               
         MHI   R1,100              MULTIPLY QUOTIENT BY 100                     
         ST    R1,TGFULL           AND SAVE IN TGFULL                           
                                                                                
         LR    R1,R0                                                            
         MHI   R1,100              MULTIPLY REMAINDER BY 100                    
         XR    R0,R0                                                            
         TM    0(R3),X'80'         WAS AMOUNT NEGATIVE?                         
         BZ    *+8                                                              
         L     R0,HEXEFFC          MAKE SURE HIGH ORDER REGISTER IS NEG         
         D     R0,FULL             AND DIVIDE BY CAN CONVERSION RATE            
                                                                                
         L     RE,FULL             ROUND THE RESULT                             
         AHI   RE,1                                                             
         SRA   RE,1                                                             
         CR    R0,RE                                                            
         JL    *+8                                                              
         AHI   R1,1                                                             
                                                                                
         A     R1,TGFULL                                                        
         STCM  R1,15,0(R3)         SAVE CONVERTED AMOUNT                        
                                                                                
         LA    R3,4(R3)            BUMP TO NEXT AMOUNT                          
         BCT   R4,C2C10                                                         
         J     XIT                                                              
                                                                                
         DS    0F                                                               
HEXEFFC  DC    X'FFFFFFFF'                                                      
         LTORG                                                                  
         EJECT                                                                  
*        BUILD CAN$ BASED BILLING DETAILS ELEMENT FOR US INVOICES               
*        ON ENTRY ... AIO = A(INVOICE RECORD)                                   
                                                                                
BLDCNBD  NTR1  BASE=*,LABEL=*                                                   
         TM    TBOPTS,TBRERUN      IF THIS IS NOT A RERUN                       
         JO    XIT                                                              
         TM    TBAYSTA6,TAAYSTCA   IF AGENCY HAS CAN STATUS                     
         JO    BCNBD10                                                          
         TM    TBCLSTA2,TACISTCA   OR CLIENT HAS CAN STATUS ...                 
         JZ    XIT                                                              
                                                                                
         USING TAPDD,R4                                                         
BCNBD10  L     R4,0(R1)                                                         
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         TM    TAPDSTAT,TAPDSCAN   SEE IF CAN$ OR US$                           
         JZ    BCNBD20                                                          
         BRAS  RE,BLDCNBDC         IF CAN$, BUILD CAN$ BD ELEMENT               
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
BCNBD20  L     R4,AIO              US$ INVOICES                                 
         MVI   ELCODE,TAPDELQ4                                                  
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         L     R2,TAPDGRS                                                       
         A     R2,TAPDAPPL                                                      
         A     R2,TAPDGUAR                                                      
         A     R2,TAPDREXP                                                      
         A     R2,TAPDPNH                                                       
         A     R2,TAPDDUES                                                      
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         ZIC   RE,TABDLEN          MAKE A COPY OF THE US$-BASED                 
         BCTR  RE,0                BILLING DETAILS ELEMENT                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEMENT(0),TABDEL                                                
         MVI   ELEMENT,TABDELQ4                                                 
         DROP  R4                                                               
                                                                                
         BRAS  RE,SETCNRT          SET CAN CONVERSION RATE                      
         ZAP   DUB,TBGCVT          CONVERSION RATE TO BINARY                    
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
                                                                                
         USING TABDD,R4                                                         
         LA    R4,ELEMENT          CONVERT ALL SUBTOTALS                        
         ZAP   TABDCCVT,TBGCVT     SET DISCOUNTED CONVERSION RATE               
         GOTOR CVT2CAN,DMCB,('TABDAM2L/4',TABDTOT)  CONVERT TO CAN$             
                                                                                
         LA    R1,TABDTAX                                                       
         LHI   R0,TABDAM2L/4                                                    
         SHI   R0,1                                                             
BCNBD40  A     R2,0(R1)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,BCNBD40                                                       
         ST    R2,TABDTOT                                                       
                                                                                
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),AIO,(R4),0                          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        BUILD CAN$ BASED BILLING DETAILS ELEMENT FOR CAN$ INVOICES             
*        ON ENTRY ... AIO = A(INVOICE RECORD)                                   
                                                                                
BLDCNBDC NTR1  BASE=*,LABEL=*                                                   
         USING TABDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         ZIC   RE,TABDLEN          MAKE A COPY OF THE CAN$-BASED                
         BCTR  RE,0                BILLING DETAILS ELEMENT                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEMENT(0),TABDEL                                                
         MVI   ELEMENT,TABDELQ4                                                 
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
         LA    R4,ELEMENT                                                       
         MVC   TABDTOT,TBCSTOT     SAVE CAN$ TOTAL                              
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),AIO,(R4),0                          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SET EURO CONVERSION RATE                                               
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
SETEURT  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            R2=A(INVOICE RECORD)                         
                                                                                
         USING TAPDD,R4                                                         
         LR    R4,R2                                                            
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         TM    TAPDPST2,TAPDPEUR   EXIT IF INVOICE WAS NOT PAID                 
         JZ    XIT                 IN EUROS                                     
                                                                                
         ZAP   TBGCVT,TBECVT       DEFAULT TO SYSTEM CONVERSION RATE            
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
         LR    R4,R2               GET BILLING DETAILS ELEMENT                  
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
         OC    TABDCCVT,TABDCCVT   IF CONVERSION RATE HAS ALREADY               
         JZ    XIT                 BEEN DETERMINED                              
         ZAP   TBGCVT,TABDCCVT     USE IT                                       
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        CONVERT EURO-BASED PAY OPTIONS ELEMENT TO US$                          
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
CVTEPAU  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            R2=A(INVOICE RECORD)                         
                                                                                
         USING TAPDD,R4                                                         
         LR    R4,R2                                                            
         MVI   ELCODE,TAPDELQ      EXIT IF INVOICE WAS NOT PAID                 
         BRAS  RE,GETEL            IN EUROS                                     
         JNE   XIT                                                              
         TM    TAPDPST2,TAPDPEUR                                                
         JZ    XIT                                                              
         DROP  R4                                                               
                                                                                
         LR    R4,R2                                                            
         MVI   ELCODE,TAEUELQ      EXIT IF WE'VE ALREADY BUILT EURO-            
         BRAS  RE,GETEL            BASED PAYMENT DETAILS ELEMENT                
         JE    XIT                                                              
                                                                                
         USING TAPAD,R4                                                         
         LR    R4,R2                                                            
         MVI   ELCODE,TAPAELQ      READ ALL PAY OPTIONS ELEMENTS                
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
CEPAU10  BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         CLI   TAPATYPE,TAPATGST   IF TYPE IS GST OVERRIDE AMOUNT               
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATTAX   OR TAX OVERRIDE AMOUNT                       
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATHND   OR HAND OVERRIDE AMOUNT                      
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATFIC   OR FICA OVERRIDE AMOUNT                      
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATHNW   OR H&W ADJUSTMENT AMOUNT                     
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATFEE   OR FEE OVERRIDE AMOUNT                       
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATCSF   OR CSF OVERRIDE AMOUNT                       
         JE    CEPAU20                                                          
         CLI   TAPATYPE,TAPATCHA   OR CHLOE HANDLING OVERRIDE AMOUNT            
         JNE   CEPAU10                                                          
CEPAU20  LR    R1,R4               R1=A(PAY OPTIONS ELEMENT)                    
         LA    R2,TAPADATA         R2=A(AMOUNT FIELD)                           
         DROP  R4                                                               
                                                                                
         LHI   R4,1                R4=# OF AMOUNT FIELDS                        
         BRAS  RE,CVTAMTS          CONVERT AMOUNTS TO US$                       
                                                                                
         LR    R4,R1                                                            
         J     CEPAU10                                                          
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        CONVERT EURO-BASED PAYMENT DETAILS ELEMENT TO US$                      
*        ON ENTRY ... P1 = A(INVOICE OR CHECK RECORD)                           
                                                                                
CVTEPDU  NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            R3=A(INVOICE OR CHECK RECORD)                
                                                                                
         LR    R4,R3                                                            
         MVI   ELCODE,TAEUELQ      EXIT IF WE'VE ALREADY BUILT EURO-            
         BRAS  RE,GETEL            BASED PAYMENT DETAILS ELEMENT                
         JE    XIT                                                              
                                                                                
         USING TAPDD,R4                                                         
         LR    R4,R3                                                            
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         TM    TAPDPST2,TAPDPEUR   EXIT IF INVOICE WAS NOT PAID                 
         JZ    XIT                 IN EUROS                                     
                                                                                
         MVC   ELEMENT,TAPDEL      SAVE EURO PAYMENT DETAILS                    
                                                                                
         LA    R2,TAPDAMTS         R2=A(FIRST AMOUNT FIELD)                     
         DROP  R4                                                               
                                                                                
         LHI   R4,TAPDAMTL/4       R4=# OF AMOUNT FIELDS                        
         BRAS  RE,CVTAMTS          CONVERT AMOUNTS TO US$                       
                                                                                
         MVI   ELEMENT,TAEUELQ     ADD EURO PAYMENT DETAILS ELEMENT             
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),(R3),ELEMENT,0                      
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*                                  R2=A(AMOUNTS)                                
*                                  R4=N'TABLE ENTRIES                           
CVTAMTS  NTR1  BASE=*,LABEL=*                                                   
         ZAP   DUB,TBGCVT          CONVERSION RATE TO BINARY                    
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
                                                                                
CVTA10   L     RF,0(R2)            GET AMT FROM TABLE                           
         GOTO1 MULT,DMCB,(RF),FULL                                              
         MVC   0(4,R2),0(R1)       RESTORE AMT TO TABLE                         
         LA    R2,4(R2)            BUMP TABLES                                  
         BCT   R4,CVTA10                                                        
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        BUILD EUROS BILLING DETAILS ELEMENT                                    
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
BLDEBD   NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            R2=A(INVOICE OR CHECK RECORD)                
                                                                                
         LR    R4,R2                                                            
         MVI   ELCODE,TAEUELQ      EXIT IF INVOICE DOES NOT HAVE                
         BRAS  RE,GETEL            EURO-BASED PAYMENT DETAILS                   
         JNE   XIT                                                              
                                                                                
         LR    R4,R2                                                            
         MVI   ELCODE,TABDELQ3     EXIT IF WE'VE ALREADY BUILT                  
         BRAS  RE,GETEL            EURO-BASED BILLING DETAILS                   
         JE    XIT                                                              
                                                                                
         USING TABDD,R4                                                         
         LR    R4,R2                                                            
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
         ZIC   RE,TABDLEN          MAKE A COPY OF THE US$-BASED                 
         BCTR  RE,0                BILLING DETAILS ELEMENT                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEMENT(0),TABDEL                                                
         MVI   ELEMENT,TABDELQ3                                                 
         DROP  R4                                                               
                                                                                
         ZAP   DUB,TBGCVT          CONVERSION RATE TO BINARY                    
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
                                                                                
         USING TABDD,R4                                                         
         LA    R4,ELEMENT          CONVERT ALL SUBTOTALS                        
         GOTOR CVT2EURO,DMCB,('TABDAM2L/4',TABDTOT)                             
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),(R2),ELEMENT,0                      
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        CONVERT AMOUNTS FROM EURO TO US$                                       
*        ON ENTRY ... P1 BYTE 0 = # OF AMOUNTS                                  
*                     P1        = A(FIRST AMOUNT FIELD)                         
*                     FULL = EURO CONVERSION RATE IN BINARY                     
                                                                                
CVT2EURO NTR1  BASE=*,LABEL=*                                                   
         ZICM  R3,1(R1),3          R3=A(AMOUNTS TO CONVERT)                     
         ZIC   R4,0(R1)            R4=# OF AMOUNTS                              
                                                                                
C2E10    XR    R0,R0                                                            
         ZICM  R1,0(R3),4                                                       
         TM    0(R3),X'80'         WAS AMOUNT NEGATIVE?                         
         BZ    *+8                                                              
         L     R0,HEXEFFS          MAKE SURE HIGH ORDER REGISTER IS NEG         
                                                                                
         M     R0,=F'100'          MULTIPLY AMOUNT BY 100                       
         D     R0,FULL             DIVIDE BY EURO CONVERSION RATE               
         MHI   R1,100              MULTIPLY QUOTIENT BY 100                     
         ST    R1,TGFULL           AND SAVE IN TGFULL                           
                                                                                
         LR    R1,R0                                                            
         MHI   R1,100              MULTIPLY REMAINDER BY 100                    
         XR    R0,R0                                                            
         TM    0(R3),X'80'         WAS AMOUNT NEGATIVE?                         
         BZ    *+8                                                              
         L     R0,HEXEFFS          MAKE SURE HIGH ORDER REGISTER IS NEG         
         D     R0,FULL             AND DIVIDE BY EURO CONVERSION RATE           
                                                                                
         MHI   R0,10                                                            
         L     RE,FULL             ROUND THE RESULT                             
         AHI   RE,1                                                             
         SRA   RE,1                                                             
         CR    R0,RE                                                            
         JL    *+8                                                              
         AHI   R1,1                                                             
                                                                                
         A     R1,TGFULL                                                        
         STCM  R1,15,0(R3)         SAVE CONVERTED AMOUNT                        
                                                                                
         LA    R3,4(R3)            BUMP TO NEXT AMOUNT                          
         BCT   R4,C2E10                                                         
         J     XIT                                                              
                                                                                
         DS    0F                                                               
HEXEFFS  DC    X'FFFFFFFF'                                                      
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        GET PAYMENT DETAILS ELEMENT                                            
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
GETPDEL  NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            R4=A(INVOICE RECORD)                         
                                                                                
         CLI   RECNUM,AC           IF AGENCY COPY                               
         JNE   GPE10                                                            
         MVI   ELCODE,TAEUELQ      FIRST ATTEMPT TO GET PAYMENT                 
         BRAS  RE,GETEL            DETAILS IN EUROS                             
         JE    GPEYES                                                           
                                                                                
GPE10    CLI   RECNUM,AC           IF AGENCY COPY                               
         JE    GPE20                                                            
         CLI   RECNUM,EB           OR E-BILLING                                 
         JNE   GPE30                                                            
GPE20    L     R4,0(R1)                                                         
         MVI   ELCODE,TAPDELQ4     ELSE ATTEMPT TO GET CAN$ PAYMENT             
         BRAS  RE,GETEL            DETAILS ELEMENT                              
         JE    GPEYES                                                           
                                                                                
         L     R4,0(R1)                                                         
GPE30    MVI   ELCODE,TAPDELQ      ELSE ATTEMPT TO GET REGULAR PAYMENT          
         BRAS  RE,GETEL            DETAILS ELEMENT                              
         JNE   GPENO                                                            
                                                                                
GPEYES   XR    RC,RC                                                            
GPENO    LTR   RC,RC                                                            
         XIT1  REGS=(R4)                                                        
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        ADD TABYELQ ELEMENT TO THE CHECK RECORD                                
*             R3 - CHECK TABLE ENTRY                                            
*                                                                               
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
UPDTABY  NTR1  BASE=*,LABEL=*                                                   
         MVI   ELCODE,TABYELQ      REMOVE ELEMENT ADDED BY PAY                  
         GOTO1 REMELEM                                                          
         XC    ELEMENT,ELEMENT                                                  
         LA    R4,ELEMENT                                                       
         USING TABYD,R4                                                         
         MVI   TABYEL,TABYELQ      SET ELEMENT CODE                             
         MVI   TABYLEN,TABYLNQ     & LENGTH                                     
         TM    STATUS,STCOD        IF COD - DON'T SET DATE/TIME                 
         BO    UBY10                                                            
         MVC   TABYDATE,TGTODAY1   DATE                                         
         TIME  DEC                 GET TIME                                     
         STCM  R0,15,TABYTIME                                                   
*                                                                               
UBY10    MVC   TABYLVL,TABLEVL     RATE LEVEL                                   
         GOTO1 ADDELEM                                                          
         XIT1                                                                   
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
       ++INCLUDE TATARA                                                         
         EJECT                                                                  
*                                                                               
*        AGENCY FLIST TABLE                                                     
*                                                                               
         DS    0D                                                               
         DC    CL8'*AGYTAB*'                                                    
AGYTAB   DC    (NAGY*AGYLNQ)X'00'                                               
         DC    X'FF'                                                            
*                                                                               
NAGY     EQU   500                 MAXIMUM N'AGENCIES                           
*                                                                               
*        AGENCY TABLE DSECT                                                     
*                                                                               
AGYD     DSECT                                                                  
AGYAGY   DS    CL6                 AGENCY                                       
AGYLNQ   EQU   *-AGYD                                                           
AGYNEXT  EQU   *                                                                
*                                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TABIDSECT                                                      
         EJECT                                                                  
       ++INCLUDE TALDCPTRD                                                      
         EJECT                                                                  
       ++INCLUDE TABILLD                                                        
         EJECT                                                                  
       ++INCLUDE TAINTERD                                                       
         EJECT                                                                  
*        OTHER DSECTS ARE HIDDEN IN HERE                                        
         SPACE 3                                                                
*TAREPFFD                                                                       
*TAREPF3D                                                                       
*TAREPF2D                                                                       
*DDGENTWA                                                                       
*DDTWADCOND                                                                     
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDBIGBOX                                                                       
*DDMASTD                                                                        
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*ACGENBOTH                                                                      
*ACGENPOST                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*DDREMOTED                                                                      
*TAREPWORKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE TAREPFFD                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE TAREPF3D                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE TAREPF2D                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDTWADCOND                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE TAREPWORKD                                                     
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'151TABILL    07/10/17'                                      
         END                                                                    
