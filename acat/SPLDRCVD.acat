*          DATA SET SPLDRCVD   AT LEVEL 001 AS OF 11/16/10                      
*CATALP SPLDRCVD                                                                
         TITLE 'LDRCVD - MODEL - RECOVERY DUMP ROUTINE'                         
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL.                                  
*        RETURN X'FF'=DONT WRITE RECORD TO DUMP TAPE                            
*        RETURN X'FE'=DELETED REC,X'FD'=RESTORED REC                            
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF COUNTERS AND ACTIONS                
                                                                                
         NMOD1 WORKX-WORKD,ACLDRCVD                                             
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),X'01'         TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         BH    FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                FINAL CALL                                   
                                                                                
LGCLDEL  MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     EXIT                                                             
                                                                                
LGCLRST  MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     EXIT                                                             
                                                                                
DELETE   MVI   0(RA),X'FF'         RETURN DELETE RECORD FROM DUMP TAPE          
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  CLI   RFILTY,SPTFILQ      SPTFIL RECOVERY RECORD                       
         BE    FIL1                                                             
         CLI   RFILTY,SPTDIRQ      SPTDIR RECOVERY RECORD                       
         BE    DIR1                                                             
         CLI   RFILTY,XSPFILQ      XSPFIL RECOVERY RECORD                       
         BE    XFI1                                                             
         CLI   RFILTY,XSPDIRQ      XSPDIR RECOVERY RECORD                       
         BE    XDI1                                                             
         CLI   RFILTY,STAFILQ      STAFIL RECOVERY RECORD                       
         BE    STA1                                                             
         B     EXIT                                                             
                                                                                
DIR1     CLI   RRECTY,2            TEST SPTDIR CHANGE                           
         BNE   SAVE                                                             
         TM    KEYSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),SPTDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD SPTDIR COPY                 
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRD          BUMP SPTDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVDIRD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
DIR2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),SPTDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRR          BUMP SPTDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVDIRR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
FIL1     CLI   RRECTY,2            TEST SPTFIL CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),SPTFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD SPTFIL COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILD          BUMP SPTFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVFILD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
FIL2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),SPTFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILR          BUMP SPTFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVFILR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
XDI1     CLI   RRECTY,2            TEST XSPDIR CHANGE                           
         BNE   SAVE                                                             
         TM    XKESTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),XSPDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD SPTDIR COPY                 
         BNE   SAVE                                                             
         TM    XKESTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   XKEKEY,XKEKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVXDID          BUMP SPTDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVXDID                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
XDI2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),XSPDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   XKEKEY,XKEKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVXDIR          BUMP SPTDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVXDIR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
XFI1     CLI   RRECTY,2            TEST SPTFIL CHANGE                           
         BNE   SAVE                                                             
         TM    XRESTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),XSPFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD SPTFIL COPY                 
         BNE   SAVE                                                             
         TM    XRESTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   XREKEY,XREKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVXFID          BUMP SPTFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVXFID                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
XFI2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),XSPFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    XRESTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   XREKEY,XREKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVXFIR          BUMP SPTFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVXFIR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
STA1     CLI   RRECTY,2            TEST STAFIL CHANGE                           
         BNE   SAVE                                                             
         TM    STASTAT,X'80'       TEST DELETED                                 
         BZ    STA2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),SPTFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD STAFIL COPY                 
         BNE   SAVE                                                             
         TM    STASTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   STAKEY,STAKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVSTAD          BUMP STAFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVSTAD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
STA2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),STAFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    STASTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   STAKEY,STAKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVSTAR          BUMP STAFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVSTAR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
SAVE     L     R0,=A(LASTRCV)      SAVE RECOVERY RECORD                         
         LHI   R1,LASTRCVL         R1=MAXIMUM RECORD LENGTH                     
         LR    RE,R3                                                            
         AHI   RE,-4                                                            
         LH    RE,0(RE)                                                         
         AHI   RE,-4               RE=ACTUAL RECORD LENGTH                      
         BM    SAVE1                                                            
         CR    RE,R1                                                            
         BNL   SAVE1                                                            
         LR    R1,RE                                                            
SAVE1    LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON-ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
RCVDIRD  DC    F'0',CL16'SPTDIR DELETES  ',C'I ',AL2(0)                         
RCVDIRR  DC    F'0',CL16'SPTDIR RESTORES ',C'I ',AL2(0)                         
RCVFILD  DC    F'0',CL16'SPTFIL DELETES  ',C'I ',AL2(0)                         
RCVFILR  DC    F'0',CL16'SPTFIL RESTORES ',C'I ',AL2(0)                         
RCVXDID  DC    F'0',CL16'XSPDIR DELETES  ',C'I ',AL2(0)                         
RCVXDIR  DC    F'0',CL16'XSPDIR RESTORES ',C'I ',AL2(0)                         
RCVXFID  DC    F'0',CL16'XSPFIL DELETES  ',C'I ',AL2(0)                         
RCVXFIR  DC    F'0',CL16'XSPFIL RESTORES ',C'I ',AL2(0)                         
RCVSTAD  DC    F'0',CL16'STAFIL DELETES  ',C'I ',AL2(0)                         
RCVSTAR  DC    F'0',CL16'STAFIL RESTORES ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'SPT ERRORS      ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'SPT WARNINGS    ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
LASTRCV  DC    2048X'00'                                                        
LASTRCVL EQU   *-LASTRCV                                                        
                                                                                
SPTDIRQ  EQU   X'23'                                                            
SPTFILQ  EQU   X'21'                                                            
XSPDIRQ  EQU   X'36'                                                            
XSPFILQ  EQU   X'37'                                                            
STAFILQ  EQU   X'22'                                                            
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
                                                                                
RECREC   DS    0X                  RECORD                                       
RECKEY   DS    XL13                RECORD KEY                                   
RECLEN   DS    XL2                 RECORD LENGTH                                
RECSTAT  DS    XL1                 RECORD STATUS                                
RECDATA  DS    0X                  RECORD DATA                                  
                                                                                
         ORG   RECREC                                                           
KEYREC   DS    0XL18               KEY RECORD                                   
KEYKEY   DS    XL13                KEY KEY                                      
KEYSTAT  DS    XL1                 KEY STATUS                                   
KEYADDR  DS    XL4                 KEY DISK ADDRESS                             
                                                                                
         ORG   RECREC                                                           
XREREC   DS    0X                  XSP RECORD                                   
XREKEY   DS    XL32                XSP RECORD KEY                               
XRELEN   DS    XL2                 XSP RECORD LENGTH                            
XRESTAT  DS    XL4                 XSP RECORD STATUS                            
XREDATA  DS    0X                  XSP RECORD DATA                              
                                                                                
         ORG   RECREC                                                           
XKEREC   DS    0XL40               XSP KEY RECORD                               
XKEKEY   DS    XL32                XSP KEY KEY                                  
XKESTAT  DS    XL4                 XSP KEY STATUS                               
XKEADDR  DS    XL4                 XSP KEY DISK ADDRESS                         
                                                                                
         ORG   RECREC                                                           
STAREC   DS    0X                  STA RECORD                                   
STAKEY   DS    XL15                STA RECORD KEY                               
STALEN   DS    XL2                 STA RECORD LENGTH                            
STASTAT  DS    XL1                 STA RECORD STATUS                            
STADATA  DS    0X                  STA RECORD DATA                              
                                                                                
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001SPLDRCVD  11/16/10'                                      
         END                                                                    
