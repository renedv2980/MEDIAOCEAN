*          DATA SET DDAFPOUT   AT LEVEL 003 AS OF 05/01/02                      
*CATALP AFPOUT                                                                  
         TITLE 'AFPOUT - EXTRACT AFP USERID FROM PROFILE DATA'                  
**********************************************************************          
* PQPROF - EXTRACT PRINT QUEUE SPECIFIC REPORT PROFILE DATA          *          
* PARAMETERS:                                                        *          
*                                                                    *          
* AL1 N/D                                                            *          
* AL3 ADR PROFILE KEY DATA - SPPUU - S=SYSTEM, P=PROGRAM, U=USERID#  *          
*                                                                    *          
* AL1 N/D                                                            *          
* AL3 ADR RETURN DATA AREA WHICH CAN TAKE VALUES:                    *          
*     AL3(0) - RETURN ADDRESS OF CL10 AREA IN AFPOUT LOCAL WS        *          
*     AL3(NOT 0) - ADDRESS OF CL10 AREA IN CALLING PROGRAM           *          
* NOTE: VALUE RETURNED IS OUTPUT TYPE CODE OR NULL IF ERROR OR NF.   *          
*                                                                    *          
* AL1 RETURN CODE                                                    *          
*     VALUES:  NOT 0 - IO ERROR CODE                                 *          
* AL3 ADR COMFACS                                                    *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
AFPOUT   CSECT                                                                  
         NMOD1 WORKX-WORKD,*AFPOUT*,CLEAR=YES,RR=RA                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVI   OFFLINE,C'N'                                                     
         ICM   RF,15,=V(SSB)       SEE IF SSB LINKED                            
         BZ    AFOFFL                                                           
         ICM   RE,3,0(RF)          IS IT A DUMMY SSB                            
         BZ    AFOFFL                                                           
         B     INIT                                                             
*                                                                               
AFOFFL   MVI   OFFLINE,C'Y'        FLAG OFFLINE                                 
         B     INIT                                                             
         SPACE 1                                                                
**********************************************************************          
* INITIALISE WORK AREAS AND READ PROFILE RECORDS                     *          
**********************************************************************          
         SPACE 1                                                                
INIT     EQU   *                                                                
         MVC   PARM,0(R1)                                                       
         ICM   RF,7,APROFKEY                                                    
         LA    RF,0(RF)                                                         
         MVC   SYSTEM,0(RF)        GET PROFILE KEY VALUES                       
*&&US                                                                           
         CLI   SYSTEM,C'N'         WE DON'T HAVE ANY NET PROFILES!              
         BNE   *+8                 WE NEVER HAD ANY NET PROFILES!               
         MVI   SYSTEM,C'S'         WE NEVER WILL HAVE ANY NET PROFILES!         
*&&                                                                             
         MVC   PROGRAM,1(RF)                                                    
         MVC   USERIDN,3(RF)                                                    
*                                                                               
         ICM   RF,7,ACOMFACS                                                    
         LA    RF,0(RF)                                                         
         USING COMFACSD,RF         ADDRESS COMFACS                              
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VGETDAY,CGETDAY                                                  
         DROP  RF                                                               
*                                                                               
         LA    RF,PROFILE          INITIALISE WORK STORE BUFFERS                
         ST    RF,APROFILE                                                      
         LA    RF,IDREC-PROFILE(RF)                                             
         ST    RF,AIDREC                                                        
         B     PROCESS                                                          
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* PROCESS VALID PROFILE RECORDS                                      *          
**********************************************************************          
         SPACE 1                                                                
PROCESS  EQU   *                                                                
         BAS   RE,READUID          READ USER ID RECORD                          
         BNE   RETURN                                                           
         L     R4,AIDREC           GET AFP USER ID NUMBER                       
         BAS   RE,GETAFPID                                                      
         BNE   RETURN                                                           
         BAS   RE,READPROF         READ PROFILE RECORDS                         
         BNE   RETURN                                                           
         L     R4,APROFILE         GET OUTPUT TYPE CODE                         
         BAS   RE,GETOUTYP                                                      
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* RETURN OUTPUT TYPE CODE IN APPRORIATE AREA                         *          
**********************************************************************          
         SPACE 1                                                                
RETURN   EQU   *                                                                
         OC    RETAREA,RETAREA                                                  
         BNZ   RETN010                                                          
*                                  RETURN IN LOCAL AREA                         
         LA    RF,OUTYPCOD                                                      
         STCM  RF,7,RETAREA                                                     
         B     RETN020                                                          
*                                                                               
RETN010  ICM   RF,7,RETAREA        RETURN IN CALLING PROGRAM AREA               
         LA    RF,0(RF)                                                         
         MVC   0(L'OUTYPCOD,RF),OUTYPCOD                                        
*                                                                               
RETN020  EQU   *                                                                
         MVC   RETCODE,IOERR                                                    
         B     EXIT                EXIT TO CALLER                               
         EJECT                                                                  
**********************************************************************          
* READ USER ID RECORD FROM CONTROL FILE                              *          
**********************************************************************          
         SPACE 1                                                                
READUID  NTR1                                                                   
         OC    USERIDN,USERIDN     IF ORIGIN ID# PASSED                         
         BZ    RUIDNO                                                           
         L     R4,AIDREC                                                        
         USING CTIREC,R4                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,USERIDN                                                  
         MVC   IOKEY,CTIKEY                                                     
         GOTO1 VDATAMGR,DMCB,(0,DMREAD),CTFILE,CTIKEY,CTIKEY                    
         MVC   CTIKEY,IOKEY                                                     
         CLI   8(R1),0                                                          
         BE    RUIDOK                                                           
         MVC   IOERR,8(R1)         EXIT IF ERROR OR NOT FOUND                   
         B     RUIDNO                                                           
*                                                                               
RUIDNO   B     NO                                                               
RUIDOK   B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
**********************************************************************          
* READ PROFILE RECORD FROM CONTROL FILE                              *          
**********************************************************************          
         SPACE 1                                                                
READPROF NTR1                                                                   
         OC    AFPIDN,AFPIDN       IF AFP ID NUMBER FOUND                       
         BZ    RPRONO                                                           
         L     R4,APROFILE                                                      
         USING CTPREC,R4                                                        
         XC    CTPKEY,CTPKEY       READ ORIGIN LEVEL PROFILE RECORD             
         MVI   CTPKTYP,CTPKTYPQ                                                 
         MVC   CTPKSYS,SYSTEM                                                   
         MVC   CTPKPROG,PROGRAM                                                 
         MVC   CTPKORIG,AFPIDN                                                  
         MVC   IOKEY,CTPKEY                                                     
         GOTO1 VDATAMGR,DMCB,(0,DMREAD),CTFILE,CTPKEY,CTPKEY                    
         MVC   CTPKEY,IOKEY                                                     
         CLI   8(R1),0                                                          
         BE    RPROOK                                                           
         MVC   IOERR,8(R1)         EXIT IF ERROR OR NOT FOUND                   
         B     RPRONO                                                           
*                                                                               
RPRONO   B     NO                                                               
RPROOK   B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
**********************************************************************          
* GET AFP USER ID NUMBER FROM USER ID RECORDA AFP ID ELEMENT         *          
* RETURN .NE. IF NOT FOUND ELSE RETURN .EQ. AND AFPIDN               *          
**********************************************************************          
         SPACE 1                                                                
GETAFPID NTR1                                                                   
         MVI   ELCODE,CTAFPELQ                                                  
         BAS   RE,GETEL                                                         
         BNE   GAFPNO                                                           
         USING CTAFPD,R4                                                        
         MVC   AFPIDN,CTAFPNUM     AFP USER ID NUMBER                           
         B     GAFPOK                                                           
*                                                                               
GAFPNO   B     NO                                                               
GAFPOK   B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
**********************************************************************          
* GET OUTPUT TYPE                                                    *          
* PROFILE CAN CONTAIN SEVERAL TYPES OF EACH ELEMENT                  *          
* APPLY SELECTION RULE AS FOR EDREQSORT                              *          
**********************************************************************          
         SPACE 1                                                                
GETOUTYP NTR1                                                                   
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTOCOELQ                                                  
         BAS   RE,GETEL                                                         
         B     GOUT020                                                          
*                                                                               
GOUT010  BAS   RE,NEXTEL                                                        
*                                                                               
GOUT020  BNE   GOUTNO                                                           
         USING CTOCOD,R4                                                        
         BAS   RE,APPLY            APPLY PROFILE TYPE SELECTION RULE            
         BNE   GOUT010                                                          
         MVC   OUTYPCOD,CTOCODE    SAVE OUTPUT TYPE CODE                        
         CLI   SACRED,C'Y'                                                      
         BE    GOUTOK                                                           
         B     GOUT010                                                          
*                                                                               
GOUTNO   B     NO                                                               
GOUTOK   B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO SEE IF THIS PROFILE APPLIES                             *          
* (CODE COPIED FROM EDREQSORT)                                       *          
**********************************************************************          
         SPACE 1                                                                
APPLY    NTR1                                                                   
         CLI   TODAY,0                                                          
         BNE   APPLY2                                                           
         GOTO1 VDATCON,DMCB,(5,0),(0,DUB)                                       
         GOTO1 (RF),(R1),,(1,TODAY)                                             
         GOTO1 VGETDAY,(R1),DUB,DAYOFWK                                         
*                                                                               
         USING CTDCOD,R4                                                        
APPLY2   CLI   CTDCOTYP,C'S'       SACRED                                       
         BNE   APPLY4                                                           
         MVI   SACRED,C'Y'                                                      
         B     APPLYES                                                          
*                                                                               
APPLY4   CLI   CTDCOTYP,C'P'       PERMANENT                                    
         BE    APPLYES                                                          
         CLI   CTDCOTYP,C'T'       TEMPORARY                                    
         BNE   APPLY6                                                           
         CLC   TODAY,CTDCODTA      SEE IF ITS EXPIRED                           
         BH    APPLYNO                                                          
         B     APPLYES                                                          
*                                                                               
APPLY6   CLC   CTDCODTA,DAYOFWK    DAY OF WEEK PROFILE - CHECK MATCH            
         BE    APPLYES                                                          
*                                                                               
APPLYNO  B     NO                                                               
APPLYES  B     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
         GETEL (R4),CTOFFSET,ELCODE                                             
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
FIRST    DC    C' '                                                             
SPACES   DC    128C' '                                                          
DMREAD   DC    C'DMREAD '                                                       
CTFILE   DC    C'CTFILE '                                                       
         DS    0H                                                               
CTOFFSET DC    YL2(CTPDATA-CTPKEY)                                              
*                                                                               
       ++INCLUDE DMPRTQC                                                        
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
RELO     DS    A                                                                
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL12               PARAMETER LIST                               
         DS    XL1                 N/D                                          
APROFKEY DS    AL3                 A(PROFILE KEY)                               
*                                                                               
         DS    XL1                                                              
RETAREA  DS    AL3                 A(RETURN AREA FOR OUTPUT CODE)               
*                                                                               
RETCODE  DS    XL1                 ROUTINE RETURN CODE                          
ACOMFACS DS    AL3                 A(COMFACS)                                   
*                                                                               
*                                  PROFILE KEY VALUES                           
SYSTEM   DS    XL1                 SYSTEM 1 CHARACTER DEFINITION                
PROGRAM  DS    XL2                 PROGRAM 2 CHARACTER DEFINITION               
USERIDN  DS    XL2                 ORIGIN USER ID NUMBER                        
AFPIDN   DS    XL2                 AFP USER ID NUMBER                           
*                                                                               
OUTYPCOD DS    CL10                OUTPUT TYPE CODE                             
*                                                                               
OFFLINE  DS    C                   OFFLINE INDICATOR                            
*                                                                               
VDATAMGR DS    V                   COMFACS ADDRESSES                            
VDATCON  DS    V                                                                
VGETDAY  DS    V                                                                
*                                                                               
OVERFLAG DS    CL1                 FLAG ORIGIN OVERIDE PROFILE READ             
ELCODE   DS    XL1                 PROFILE ELEMENT CODE SAVE                    
SACRED   DS    CL1                 PROFILE SACRED TYPE FLAG                     
TODAY    DS    XL3                 TODAYS DATE                                  
DAYOFWK  DS    XL3                 DAY OF WEEK CODE                             
*                                                                               
APROFILE DS    A                   ADDRESS OF RECORD BUFFERS                    
AIDREC   DS    A                                                                
*                                                                               
IOERR    DS    XL1                 IO ERROR BYTE                                
IOKEY    DS    XL(L'CTPKEY)        CONTROL FILE KEY                             
PROFILE  DS    1000C               ORIGIN ID LEVEL PROFILE RECORD               
IDREC    DS    1000C               USER ID RECORD BUFFER                        
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DDREMOTED                                                      
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DMPRTQL                                                        
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE FAREPBLK                                                       
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDAFPOUT  05/01/02'                                      
         END                                                                    
