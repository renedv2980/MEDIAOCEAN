*          DATA SET DDFAXLINK  AT LEVEL 087 AS OF 05/01/02                      
*CATALP FAXLINK                                                                 
         TITLE 'FAXLINK - SUPPORT FOR PRINTING EASYLINK FAXES'                  
FAXLINK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LINKDEND-LINKD,**FXLK**                                          
         USING LINKD,RC                                                         
         L     RA,=V(FAXINFO)                                                   
         USING FAXINFOD,RA                                                      
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         MVC   SPACES,P                                                         
         CLI   FXISTAT,FXISPEND    IF FAX PENDING                               
         BNE   FAXLINK2                                                         
         MVI   FXISTAT,FXISACT        MAKE THIS ONE ACTIVE                      
         CLI   LASTSTAT,FXISACT       IF ONE ALREADY ACTIVE                     
         BNE   *+8                                                              
         BAS   RE,FAXWRAP                WRAP UP PREVIOUS                       
         MVI   LASTSTAT,FXISACT                                                 
         BAS   RE,FAXINIT             INITIALIZE THIS ONE                       
         BNE   *+12                                                             
         BAS   RE,FAXHEAD             AND PRINT A HEADER PAGE                   
         B     XIT                                                              
         MVI   FXISTAT,FXISINAC       EXIT WITH INACTIVE STATUS                 
         MVI   LASTSTAT,FXISINAC      IF FAX RECORD NOT FOUND                   
         B     XIT                                                              
         SPACE 1                                                                
FAXLINK2 CLI   FXISTAT,FXISCLOS    IF CLOSE PENDING                             
         BNE   XIT                                                              
         MVI   FXISTAT,FXISINAC                                                 
         BAS   RE,FAXWRAP             WRAP UP LAST                              
         MVI   LASTSTAT,FXISINAC                                                
         B     XIT                                                              
         EJECT                                                                  
*              INITIALIZE A FAX FILE                                            
         SPACE 3                                                                
FAXINIT  NTR1                                                                   
         BAS   RE,CLOSEBOX                                                      
         BAS   RE,GETID                                                         
         BAS   RE,GETFAX                                                        
         BNE   XITNE                                                            
         BAS   RE,GETFROM                                                       
         BAS   RE,SYSINIT          SPECIAL SYSTEM RELATED STUFF                 
         SPACE 1                                                                
         LA    R8,P                GENERATE *HDR* RECORD                        
         USING HDRD,R8                                                          
         MVC   HDRCODE,=C'*HDR*'                                                
         MVC   HDRINDS,=C'WP'                                                   
         MVC   BIGSTA,SPACES                                                    
         CLC   FXISTN,SPACES       TEST EASYLINK STATION DESTINATION            
         BNH   FAXINIT4                                                         
         TM    FXIOPTS,FXIOEASY    OPTION TO USE FAX RECORD FAX NUMBER          
         BZ    *+12                IF IT'S VALID, IE. AT LEAST 7 CHARS          
         CLI   TOFAX+6,C' '                                                     
         BH    FAXINIT4                                                         
         MVC   HDRDEST(5),FXISTN                                                
         MVC   BIGSTA(4),FXISTN                                                 
         CLI   BIGSTA+3,C'9'                                                    
         BNE   *+8                                                              
         MVI   BIGSTA+3,C' '                                                    
         LA    R1,BIGSTA+4                                                      
         CLI   BIGSTA+3,C' '                                                    
         BH    *+6                                                              
         BCTR  R1,0                                                             
         MVC   0(3,R1),=C'-TV'                                                  
         CLI   FXISTN+4,C' '                                                    
         BNH   FAXINIT2                                                         
         CLI   FXISTN+4,C'T'                                                    
         BE    FAXINIT2                                                         
         MVC   1(1,R1),FXISTN+4                                                 
         MVI   2(R1),C'M'                                                       
         SPACE 1                                                                
FAXINIT2 MVC   HDRATT(7),BIGSTA    FORMATTED STATION                            
         B     FAXINIT6                                                         
         SPACE 1                                                                
FAXINIT4 MVC   HDRDEST(4),=C'FAXW' USE FAX NUMBER FROM FAX RECORD               
         MVC   HDRDEST+5(16),TOFAX FAX                                          
         MVC   HDRATT(25),TOFAX                                                 
         LA    R1,HDRATT                                                        
         BAS   RE,EDITTEL                                                       
         MVC   HDRBILL,FXITRAPP    USE APPLICATION DATA FOR BILL                
         CLC   FXISTN,SPACES       UNLESS THERE'S AN EASYLINK STATION           
         BNH   *+10                                                             
FAXINIT6 MVC   HDRBILL,FXIEBILL    THEN USE EASYLINK BILLING INFO               
         SPACE 1                                                                
         BAS   RE,SPLAT                                                         
         MVC   P(72),FXITRN        THEN HANDLE THE ++DDS LINE                   
         BAS   RE,SPLAT                                                         
         B     XITEQ                                                            
         EJECT                                                                  
*              SYSTEM SPECIFIC                                                  
         SPACE 3                                                                
SYSINIT  NTR1                                                                   
         CLC   FXITRAPP(16),=CL16' '                                            
         BE    GENINIT                                                          
         OC    FXITRAPP(16),FXITRAPP                                            
         BZ    GENINIT                                                          
         CLC   FXITRSYS,=C'NE'     PLUG IN REQUESTOR FOR NETWORK                
         BE    NETINIT                                                          
         B     XIT                                                              
         SPACE 1                                                                
NETINIT  MVC   FXITNREQ,FXISRQST                                                
         MVC   FXITNPRG,FXITRPRG                                                
         MVC   FXITRPRG,=C'REP'                                                 
         B     XIT                                                              
         SPACE 1                                                                
GENINIT  MVC   FXITGRPH,=C'REPORT '                                             
         MVC   FXITGREP,FXITRPRG                                                
         MVC   FXITGBYH,=C'SENT BY '                                            
         MVC   FXITGBY,FXISRQST                                                 
         MVC   FXITGTOH,=C'TO '                                                 
         MVC   FXITGTO,TONAME                                                   
         MVC   FXITRPRG,=C'Z  '                                                 
         B     XIT                                                              
         EJECT                                                                  
*              PRINT A HEADER PAGE                                              
         SPACE 3                                                                
FAXHEAD  NTR1                                                                   
         GOTO1 =V(PRINT),DMCB,P-1,=C'BC01'  SKIP                                
         LA    R8,P                                                             
         USING REPORTD,R8                                                       
         MVC   REPDATA(10),=C'FAX HEADER'                                       
         BAS   RE,SPLAT                                                         
         MVC   REPDATA(10),=C'----------'                                       
         BAS   RE,SPLAT                                                         
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
         MVC   REPTITLE,=CL12'SENT TO:'                                         
         MVC   REPDATA(25),TONAME                                               
         BAS   RE,SPLAT                                                         
         CLC   BIGSTA,SPACES                                                    
         BNH   TO2                                                              
         MVC   REPDATA(7),=C'STATION'                                           
         MVC   REPDATA+8(7),BIGSTA                                              
         B     TO4                                                              
         SPACE 1                                                                
TO2      MVC   REPDATA(4),=C'FAX='                                              
         MVC   REPDATA+4(25),TOFAX                                              
         LA    R1,REPDATA+4                                                     
         BAS   RE,EDITTEL                                                       
         SPACE 1                                                                
TO4      BAS   RE,SPLAT                                                         
         BAS   RE,SPLAT                                                         
         LA    R2,TOCOMM           COUNT NUMBER OF COMMENT LINES                
         LA    R3,8                                                             
         SR    R1,R1                                                            
         LA    RE,1                                                             
         SPACE 1                                                                
TO5      CLC   0(65,R2),SPACES                                                  
         BE    *+6                                                              
         LR    R1,RE                                                            
         LA    R2,65(R2)                                                        
         LA    RE,1(RE)                                                         
         BCT   R3,TO5                                                           
         LTR   R3,R1               TEST ANY COMMENT LINES                       
         BZ    FROM0                                                            
         LA    R2,TOCOMM                                                        
         SPACE 1                                                                
TO6      MVC   REPDATA(65),0(R2)   YES-PRINT THEM, INCLUDING POSSIBLE           
         BAS   RE,SPLAT                BLANK LINES IMBEDDED                     
         LA    R2,65(R2)                                                        
         BCT   R3,TO6                                                           
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
FROM0    MVC   REPTITLE,=CL12'SENT FROM:'                                       
         CLC   FROMNAME,SPACES                                                  
         BE    FROM2                                                            
         MVC   REPDATA(25),FROMNAME                                             
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
FROM2    L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   REPDATA(33),MCORIGIN                                             
         BAS   RE,SPLAT                                                         
         MVC   REPDATA(33),MCORIGAD                                             
         BAS   RE,SPLAT                                                         
         DROP  R1                                                               
         MVC   REPDATA(3),=C'ID='                                               
         MVC   REPDATA+3(10),IDNAME                                             
         BAS   RE,SPLAT                                                         
         MVC   REPDATA(10),=C'REQUESTOR='                                       
         MVC   REPDATA+10(3),FXISRQST                                           
         BAS   RE,SPLAT                                                         
         CLC   FROMTEL,SPACES                                                   
         BE    FROM4                                                            
         MVC   REPDATA(10),=C'TELEPHONE='                                       
         MVC   REPDATA+10(25),FROMTEL                                           
         LA    R1,REPDATA+10                                                    
         BAS   RE,EDITTEL                                                       
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
FROM4    CLC   FROMFAX,SPACES                                                   
         BE    FROM6                                                            
         MVC   REPDATA(4),=C'FAX='                                              
         MVC   REPDATA+4(25),FROMFAX                                            
         LA    R1,REPDATA+4                                                     
         BAS   RE,EDITTEL                                                       
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
FROM6    BAS   RE,SPLAT                                                         
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
* FIND OUT HOW MANY LINES TO PRINT                                              
         LA    R2,FROMCOMM+L'FROMCOMM-65    LAST LINE OF COMM                   
         LA    R3,8                         UP TO 8 LINES                       
FROM7    CLC   0(65,R2),SPACES                                                  
         BNE   FROM8                                                            
         SH    R2,=H'65'                                                        
         BCT   R3,FROM7                                                         
         SPACE 1                                                                
FROM8    LTR   R3,R3               NO LINES TO PRINT                            
         BZ    FROMX                                                            
         LA    R2,FROMCOMM                                                      
FROM9    MVC   REPDATA(65),0(R2)                                                
         BAS   RE,SPLAT                                                         
         LA    R2,65(R2)                                                        
         BCT   R3,FROM9                                                         
         SPACE 1                                                                
FROM10   BAS   RE,SPLAT                                                         
         SPACE 1                                                                
FROMX    MVC   REPTITLE,=CL12'FAX SENT ON:'                                     
         GOTO1 =V(DATCON),DMCB,(5,0),(8,REPDATA)                                
         BAS   RE,SPLAT                                                         
         MVC   REPTITLE,=CL12'TIME SENT:'                                       
         THMS  DDSTIME=YES                                                      
         ST    R1,FULL             0HHMMSS+ (DDS TIME)                          
         ST    R0,PACKOF4B         0060000+ (DDS CLOCK TIME DIFFERENCE)         
*&&US*&& AP    FULL,PACKOF4B       FULL = ACTUAL TIME                           
         XC    DUB,DUB                                                          
         MVC   DUB+5(3),FULL                                                    
         OI    DUB+7,X'0F'                                                      
         CVB   R1,DUB                                                           
         C     R1,=F'2400'                                                      
         BL    *+8                                                              
         S     R1,=F'2400'                                                      
         EDIT  (R1),(5,REPDATA),2                                               
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              GET ID NAME                                                      
         SPACE 1                                                                
GETID    NTR1                                                                   
         MVC   IDNAME,SPACES                                                    
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CTIREC,R4                                                        
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,FXISIDNO                                                 
         MVC   KEYSAVE,KEY                                                      
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRDHI'),=C'CTFILE ',KEY,IO               
         CLC   IO(25),KEYSAVE                                                   
         BNE   XIT                                                              
         LA    R4,IO                                                            
         LA    R6,CTIDATA                                                       
         SPACE 1                                                                
GETID2   CLI   0(R6),CTDSCELQ                                                   
         BE    GETID4                                                           
         CLI   0(R6),0                                                          
         BE    XIT                                                              
         ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETID2                                                           
         SPACE 1                                                                
         USING CTDSCD,R6                                                        
GETID4   ZIC   R1,CTDSCLEN                                                      
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   IDNAME(0),CTDSC                                                  
         EJECT                                                                  
*              GET FAX RECORD INFO                                              
         SPACE 1                                                                
GETFAX   NTR1                                                                   
         MVC   TONAME,SPACES                                                    
         MVC   TOFAX,SPACES                                                     
         MVC   TOTEL,SPACES                                                     
         MVC   TOCOMM(130),SPACES                                               
         MVC   TOCOMM+130(130),SPACES                                           
         MVC   TOCOMM+260(130),SPACES                                           
         MVC   TOCOMM+390(130),SPACES                                           
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CTFXKEY,R4                                                       
         MVI   CTFXKTYP,CTFXEQU                                                 
         MVC   CTFXAGY,FXISAGY                                                  
         MVC   CTFXCODE,FXISFXCD                                                
         TM    FXIOPTS,FXIOSUBC    USE THE SUB-CODE?                            
         BNO   GETFA30                                                          
         MVC   CTFXSUBC,FXISSUBC   YES                                          
         TM    FXIOPTS,FXIOSPLT    SPLIT FXISSUBC IN TWO?                       
         BNO   GETFA30                                                          
*                                                                               
         OC    FXISSUB1,FXISSUB1   IS 1ST SUB CODE THERE?                       
         BZ    GETFA20             NO - USE 2ND SUB CODE                        
*                                  YES - USE 1ST SUB CODE                       
         MVC   CTFXSUBC(3),FXISSUB1                                             
         MVC   CTFXSUBC+3(3),SPACES    SPACE PADDED THE SUB CODE                
         MVC   KEYSAVE,KEY                                                      
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRDHI'),=C'CTFILE ',KEY,IO               
         CLC   IO(24),KEYSAVE      COMPARE 7-BYTE FAX CODE & SUBCODE(6)         
         BE    GETFA40             FOUND THE FAX RECORD!                        
*                                  NOT FOUND, TRY 2ND SUB CODE                  
GETFA20  OC    FXISSUB2,FXISSUB2   IS 2ND SUB CODE THERE?                       
         BZ    GETFA30                                                          
         MVC   CTFXSUBC(3),FXISSUB2                                             
         MVC   CTFXSUBC+3(3),SPACES    SPACE PADDED THE SUB CODE                
*                                                                               
GETFA30  MVC   KEYSAVE,KEY                                                      
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRDHI'),=C'CTFILE ',KEY,IO               
         CLC   IO(24),KEYSAVE      COMPARE 7-BYTE FAX CODE & SUBCODE(6)         
         BNE   XITNE                                                            
GETFA40  LA    R4,IO                                                            
         LA    R6,CTFXEL1                                                       
         B     GETFX4                                                           
         SPACE 1                                                                
GETFX2   ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         SPACE 1                                                                
GETFX4   CLI   0(R6),0                                                          
         BE    XITEQ                                                            
         CLI   0(R6),CTFX1ELQ                                                   
         BE    GETFXNO                                                          
         CLI   0(R6),CTFX2ELQ                                                   
         BE    GETFXATT                                                         
         CLI   0(R6),CTFX3ELQ                                                   
         BE    GETFXCOM                                                         
         CLI   0(R6),CTFX4ELQ                                                   
         BE    GETFXTEL                                                         
         B     GETFX2                                                           
         SPACE 1                                                                
         USING CTFX1EL,R6                                                       
GETFXNO  ZIC   R1,CTFX1LEN         FAX NUMBER                                   
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFX2                                                           
         MVC   TOFAX(0),CTFX1NUM                                                
         SPACE 1                                                                
         USING CTFX2EL,R6                                                       
GETFXATT ZIC   R1,CTFX2LEN         ATTENTION                                    
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFX2                                                           
         MVC   TONAME(0),CTFX2ATT                                               
         SPACE 1                                                                
         USING CTFX3EL,R6                                                       
GETFXCOM ZIC   R1,CTFX3LEN         COMMENT                                      
         SH    R1,=H'4'                                                         
         CH    R1,=H'64'                                                        
         BL    *+8                                                              
         LA    R1,64                                                            
         ZIC   RF,CTFX3LIN         (LINE NUMBER)                                
         CH    RF,=H'8'                                                         
         BL    *+8                                                              
         LA    RF,8                                                             
         BCTR  RF,0                                                             
         MH    RF,=H'65'                                                        
         LA    RF,TOCOMM(RF)                                                    
         EX    R1,*+8                                                           
         B     GETFX2                                                           
         MVC   0(0,RF),CTFX3MSG                                                 
         SPACE 1                                                                
         USING CTFX4EL,R6                                                       
GETFXTEL ZIC   R1,CTFX4LEN         TELEPHONE                                    
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFX2                                                           
         MVC   TOTEL(0),CTFX4TEL                                                
         EJECT                                                                  
*              GET FROM RECORD INFO                                             
         SPACE 1                                                                
GETFROM  NTR1                                                                   
         MVC   FROMNAME,SPACES                                                  
         MVC   FROMFAX,SPACES                                                   
         MVC   FROMTEL,SPACES                                                   
         MVC   FROMCOMM(130),SPACES                                             
         MVC   FROMCOMM+130(130),SPACES                                         
         MVC   FROMCOMM+260(130),SPACES                                         
         MVC   FROMCOMM+390(130),SPACES                                         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CTFXKEY,R4                                                       
         MVI   CTFXKTYP,CTFXEQU                                                 
         MVC   CTFXAGY,FXISAGY                                                  
         MVC   CTFXCODE(2),=C'**'  LOOK FOR **RRR (REQUESTOR)                   
         MVC   CTFXCODE+2(3),FXISRQST                                           
         MVC   CTFXCODE+5(2),SPACES                                             
         MVC   KEYSAVE,KEY                                                      
         GOTO1 =V(DATAMGR),DMCB,(0,=C'DMRDHI'),=C'CTFILE ',KEY,IO               
         CLC   IO(18),KEYSAVE      COMPARE 7-BYTE FAX CODE                      
         BNE   XIT                                                              
         LA    R4,IO                                                            
         LA    R6,CTFXEL1                                                       
         B     GETFM4                                                           
         SPACE 1                                                                
GETFM2   ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         SPACE 1                                                                
GETFM4   CLI   0(R6),0                                                          
         BE    XIT                                                              
         CLI   0(R6),CTFX1ELQ                                                   
         BE    GETFMNO                                                          
         CLI   0(R6),CTFX2ELQ                                                   
         BE    GETFMATT                                                         
         CLI   0(R6),CTFX3ELQ                                                   
         BE    GETFMCOM                                                         
         CLI   0(R6),CTFX4ELQ                                                   
         BE    GETFMTEL                                                         
         B     GETFM2                                                           
         SPACE 1                                                                
         USING CTFX1EL,R6                                                       
GETFMNO  ZIC   R1,CTFX1LEN         FAX NUMBER                                   
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFM2                                                           
         MVC   FROMFAX(0),CTFX1NUM                                              
         SPACE 1                                                                
         USING CTFX2EL,R6                                                       
GETFMATT ZIC   R1,CTFX2LEN         ATTENTION                                    
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFM2                                                           
         MVC   FROMNAME(0),CTFX2ATT                                             
         SPACE 1                                                                
         USING CTFX3EL,R6                                                       
GETFMCOM ZIC   R1,CTFX3LEN         COMMENT                                      
         SH    R1,=H'4'                                                         
         CH    R1,=H'64'                                                        
         BL    *+8                                                              
         LA    R1,64                                                            
         ZIC   RF,CTFX3LIN         (LINE NUMBER)                                
         CH    RF,=H'8'                                                         
         BL    *+8                                                              
         LA    RF,8                                                             
         BCTR  RF,0                                                             
         MH    RF,=H'65'                                                        
         LA    RF,FROMCOMM(RF)                                                  
         EX    R1,*+8                                                           
         B     GETFM2                                                           
         MVC   0(0,RF),CTFX3MSG                                                 
         SPACE 1                                                                
         USING CTFX4EL,R6                                                       
GETFMTEL ZIC   R1,CTFX4LEN         TELEPHONE                                    
         SH    R1,=H'3'                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         LA    R1,24                                                            
         EX    R1,*+8                                                           
         B     GETFM2                                                           
         MVC   FROMTEL(0),CTFX4TEL                                              
         EJECT                                                                  
*              WRAP UP A FAX FILE                                               
         SPACE 3                                                                
FAXWRAP  NTR1                                                                   
         BAS   RE,CLOSEBOX                                                      
         MVC   P(32),=CL32'*** END OF DDS MESSAGE ***'                          
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         SPACE 1                                                                
CLOSEBOX NTR1                                                                   
         L     R9,=V(BOXAREA)                                                   
         USING BOXD,R9                                                          
         CLI   BOXSTAT,C'I'        IF WE ARE IN MIDDLE OF BOX                   
         BNE   XIT                                                              
         MVI   BOXREQ,C'C'            CLOSE IT                                  
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         SPACE 1                                                                
SPLAT    NTR1                                                                   
         OC    P,SPACES                                                         
         GOTO1 =V(PRINT),DMCB,P-1,=C'BL01'                                      
         MVC   P,SPACES                                                         
         B     XIT                                                              
         SPACE 1                                                                
XITEQ    CR    RB,RB                                                            
         B     XIT                                                              
         SPACE 1                                                                
XITNE    LTR   RB,RB                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO EDIT TELEPHONE NUMBERS (AND FAX)                      
         SPACE 3                                                                
EDITTEL  NTR1                                                                   
*                                  R1=A(CL25 TELEPHONE)                         
         MVC   WORK(25),0(R1)                                                   
         MVC   0(25,R1),SPACES                                                  
         LA    R2,WORK                                                          
         CLC   WORK+7(4),SPACES    IF > 7 CHARACTERS                            
         BE    EDITTEL4                                                         
         CLI   WORK,C'1'                                                        
         BH    EDITTEL2                                                         
         MVC   0(2,R1),=C'1-'                                                   
         LA    R1,2(R1)                                                         
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
EDITTEL2 MVI   0(R1),C'('          ASSUME AREA CODE TO START                    
         MVC   1(3,R1),0(R2)                                                    
         MVI   4(R1),C')'                                                       
         LA    R2,3(R2)                                                         
         LA    R1,6(R1)                                                         
         SPACE 1                                                                
EDITTEL4 MVC   0(3,R1),0(R2)                                                    
         MVI   3(R1),C'-'                                                       
         MVC   4(10,R1),3(R2)                                                   
         B     XIT                                                              
         EJECT                                                                  
*              LTORG ETC                                                        
         SPACE 3                                                                
LASTSTAT DC    X'00'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR PRINT LINES                                            
         SPACE 3                                                                
HDRD     DSECT                                                                  
HDRID    DS    CL4                                                              
HDRCODE  DS    CL5                                                              
HDRDEST  DS    CL25                                                             
HDRINDS  DS    CL2                                                              
         DS    CL2                                                              
HDRATT   DS    CL16                                                             
HDRBILL  DS    CL13                                                             
         DS    CL6                                                              
         SPACE 1                                                                
REPORTD  DSECT                                                                  
         DS    CL20                INDENT                                       
REPTITLE DS    CL12                                                             
         DS    CL1                                                              
REPDATA  DS    CL80                                                             
         DS    CL19                                                             
         EJECT                                                                  
*              WORKING STORAGE FOR MODULE                                       
         SPACE 3                                                                
LINKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
PACKOF4B DS    PL4                                                              
IDNAME   DS    CL32                                                             
WORK     DS    CL64                                                             
P        DS    CL132                                                            
SPACES   DS    CL132                                                            
         SPACE 1                                                                
TONAME   DS    CL25                SENDING TO INFO                              
TOFAX    DS    CL25                                                             
TOTEL    DS    CL25                                                             
TOCOMM   DS    520C                                                             
         SPACE 1                                                                
FROMNAME DS    CL25                FROM INFO                                    
FROMFAX  DS    CL25                                                             
FROMTEL  DS    CL25                                                             
FROMCOMM DS    CL520                                                            
         SPACE 1                                                                
BIGSTA   DS    CL7                 FORMATTED STATION                            
         SPACE 1                                                                
DMCB     DS    6F                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
IO       DS    2000C                                                            
LINKDEND EQU *                                                                  
         EJECT                                                                  
       ++INCLUDE DDFAXINFOD                                                     
         EJECT                                                                  
       ++INCLUDE CTGENEDICT                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDREPMASTD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'087DDFAXLINK 05/01/02'                                      
         END                                                                    
