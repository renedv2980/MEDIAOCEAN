*          DATA SET FAEMUMSG   AT LEVEL 003 AS OF 12/12/96                      
*CATALP FAEMUMSG                                                                
         TITLE 'FAEMUMSG - EMULATE GETMSG CALL VIA GETTXT'                      
*                                                                               
GETMSG   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**NMSG**,RR=RE,CLEAR=YES                             
         USING WORKD,RC            RC=A(W/S)                                    
         LR    R3,R1               R3=A(GETMSG PARAM LIST)                      
         USING GETMSGD,R3                                                       
         LA    R2,BLOCK                                                         
         USING GETTXTD,R2          R2=A(GETTXT CONTROL BLOCK)                   
*                                                                               
         ICM   R1,15,=V(SSB)       TEST ON/OFF LINE                             
         BZ    GMOFFL                                                           
         ICM   RF,3,0(R1)          MAY BE A DUMMY SSB                           
         BZ    GMOFFL                                                           
*                                                                               
GMONL    L     R1,=V(SYSFAC)       ONLINE INITIALISE                            
         MVC   ADATAMGR,VDATAMGR-SYSFACD(R1)                                    
         L     R1,VSYSFAC0-SYSFACD(R1)       A(COMFACS)                         
         L     RF,CGETTXT-COMFACSD(R1)       RF=A(GETTXT)                       
         B     GET010                                                           
*                                                                               
GMOFFL   L     R1,=V(DATAMGR)      OFFLINE INITIALISE                           
         STCM  R1,15,ADATAMGR                                                   
         L     RF,=V(GETTXT)                                                    
         EJECT                                                                  
GET010   XC    BLOCK(L'GTBLOCK),BLOCK                                           
         MVC   GTMSGNO1,MSGNO      FORMAT TO 2 BYTE MESSAGE NO                  
         CLI   MSGNO,0             INSERT A(DMCB) IF NO MSG NO                  
         BNE   *+10                                                             
         MVC   GTADMCB,MSGADMCB                                                 
         MVI   GTMAXL,60           GETMSG ONLY HANDLED 60 BYTE MESSAGES         
         MVC   GTAOUT,MSGAOUT      A(OUTPUT FIELD)                              
         OI    GT1INDS,GT1OWRK     OUTPUT IS A WORK FIELD                       
         MVI   GTMTYP,GTMERR       FORCE ERROR TYPE MESSAGE                     
         CLI   MSGFLD,X'FF'        TEST FOR OVERRIDE SYSTEM                     
         BNE   GET015                                                           
         MVC   GTMSYS,MSGSYS       OVERRIDE MSG SYSTEM                          
*&&UK                                                                           
         CLI   GTMSYS,22           BUDGET MESSAGE SYSTEM                        
         BNE   FUDGEEND                                                         
         MVI   GTMSYS,5            PART OF MEDIA PLANNING                       
         OI    GTMSGNO,1           ADD 256                                      
FUDGEEND EQU   *                                                                
*&&                                                                             
         B     GET020                                                           
GET015   CLC   MSGADMGR,ADATAMGR+1 NO FLD INDEX IF DMGR ERR CALL                
         BE    *+10                                                             
         MVC   GTINDX,MSGFLD       MAY BE A FIELD INDEX (OR ZERO)               
         SPACE 1                                                                
GET020   GOTO1 (RF),BLOCK          CALL GETTXT - NOTE RF=A(GETTXT)              
         MVC   MSGNO,GTMAXL        RETURN L'MESSAGE AS GETMSG WOULD             
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER W/S                                                  *         
***********************************************************************         
*                                                                               
WORKD    DSECT                                                                  
ADATAMGR DS    AL4                                                              
BLOCK    DS    XL28                PARAM BLOCK FOR GETTXT                       
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER GETMSG PARAM LIST                                    *         
***********************************************************************         
*                                                                               
GETMSGD  DSECT                                                                  
MSGNO    DS    XL1                 ERROR NUMBER                                 
MSGAOUT  DS    AL3                 A(OUTPUT AREA)                               
MSGFLD   DS    XL1                 FIELD INDEX NUMBER                           
MSGADMCB DS    AL3                 A(DATAMANAGER CONTROL BLOCK)                 
MSGSYS   DS    XL1                 OVERRIDE MESSAGE SYSTEM                      
MSGADMGR DS    AL3                 A(DATA MANAGER)                              
         EJECT                                                                  
* FAGETTXTD                                                                     
       ++INCLUDE FAGETTXTD                                                      
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FAEMUMSG  12/12/96'                                      
         END                                                                    
