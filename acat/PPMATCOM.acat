*          DATA SET PPMATCOM   AT LEVEL 075 AS OF 10/10/05                      
*CATALP PPMATCOM                                                                
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         TITLE 'INSERTION ORDERS - CHECK FOR MATERIALS REPEAT'                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
MATCOM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MATCOMDX-MATCOMD,*MATCOM*,RR=RE,CLEAR=YES                        
*                                                                               
         LR    R6,RC                                                            
         USING MATCOMD,R6                                                       
*                                                                               
         ST    RE,RELO                                                          
         L     RC,0(R1)                                                         
         USING POLWRKD,RC                                                       
         USING T41FFFD,RA                                                       
         USING IOWORKD,R8                                                       
         L     R9,PPFILEC                                                       
         LA    R7,1(R9)                                                         
         LA    R7,4095(R7)                                                      
         USING POLFILED,R9,R7                                                   
*                                                                               
         TM    ADBSW,AS_WEBIO      EIO?                                         
         BZ    *+18                                                             
         XC    ERR(2),ERR          SET CONDITIONS BEFORE EXITING                
         MVI   DMCB+8,0                                                         
         J     EXIT                                                             
*                                                                               
* TRY SETTING DICTIONARY HERE USING PUBLANG                                     
*                                                                               
         L     R5,=A(DICSECTM)                                                  
         A     R5,RELO                                                          
         USING DICSECTM,R5                                                      
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL      TRANSLATE LIST                               
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVI   DDLANG,0            DEFAULT TO ENGLISH                           
         CLI   PUBLANG,C'F'                                                     
         BNE   *+8                                                              
         MVI   DDLANG,4            4 IS FRENCH                                  
*                                                                               
         MVC   MYLANG,PUBLANG                                                   
*                                                                               
         LA    RF,DCLIST                                                        
         STCM  RF,7,DDIADR                                                      
         LA    RF,DSLIST                                                        
         STCM  RF,7,DDOADR                                                      
         L     RF,VCOMFACS                                                      
         USING COMFACSD,RF                                                      
         GOTO1 CDICTATE                                                         
         DROP  R1,R5,RF                                                         
*                                                                               
         L     RE,=V(DICSECTM)                                                  
         A     RE,RELO                                                          
         ST    RE,ADICSECT                                                      
*                                                                               
         XC    BRPTDAT,BRPTDAT     CLEAR REPEAT DATE                            
         XC    RPTPUB,RPTPUB       CLEAR REPEAT PUB                             
*                                                                               
         CLI   SVRPTDT,C'0'                                                     
         BL    MATC4                                                            
         MVI   MATSW,C'N'                                                       
         BE    MATCOMX                                                          
*                                                                               
* RPT DATE IN QCARD                                                             
*                                                                               
         MVI   MATSW,C'R'                                                       
         GOTO1 DATCON,DMCB,(0,SVRPTDT),(3,BRPTDAT)                              
         B     MATC8                                                            
*                                                                               
MATC4    DS    0H                  SEARCH FOR REPEAT                            
         MVC   W(32),PLISREC       SAVE KEY OF POSSIBLE COMMENT RECORD          
*                                                                               
         MVC   SVTSP,TSPUB-STATDIF SAVE TSPUB                                   
         MVC   SVMATOPT,MATOPT     AND MATERIALS REPEAT OPTION                  
         XC    SVPLKEY,SVPLKEY                                                  
         MVI   PUBLCK,C'N'                                                      
         OC    PUBPLSH,PUBPLSH     SEE IF PUB HAS PUBLISHER                     
         BZ    MATC4X                                                           
*                                                                               
* MUST READ TO SEE IF I/O REPEAT CHECK IS ON                                    
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),TSAGY-STATDIF                                             
         MVI   KEY+3,X'11'                                                      
         MVC   KEY+4(4),PUBPLSH                                                 
         GOTOR HIGH                                                             
         BRAS  RE,TSTERR                                                        
         CLC   KEY(9),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                MISSING PUBLISHER (REP)                      
*                                                                               
         XC    SPACEND+150(200),SPACEND+150                                     
*                                                                               
         MVC   TMPSVAIO,AREC                                                    
         LA    RE,SPACEND                                                       
         ST    RE,AREC                                                          
         GOTOR GETPRT                                                           
         MVC   AREC,TMPSVAIO                                                    
         BRAS  RE,TSTERR                                                        
         LA    R2,SPACEND                                                       
         TM    PREPSTAT-PREPREC(R2),X'02'                                       
         BNO   MATC4X                                                           
         MVI   PUBLCK,C'Y'         PERFORM PUBLISHER I/O REPEAT CHECK           
         MVI   MATOPT,C'N'         OVERIDE MAT REPEAT OPTION                    
*                                                                               
* REAL MATOPT WAS SAVED AND WILL BE RESTORED SEARCH FOR REPEAT                  
*                                                                               
MATC4X   DS    0H                                                               
         XC    BRPTDAT,BRPTDAT                                                  
*                                                                               
MATC4XA  DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),TSAGY-STATDIF                                             
         MVI   KEY+3,X'20'                                                      
         MVC   KEY+4(6),TSCLT-STATDIF                                           
         MVC   KEY+10(4),TSPUB-STATDIF                                          
         CLI   MATOPT,C'Y'         RPT CHECK ACROSS EDITS + ZONES               
         BE    MATC4B                                                           
         CLI   MATOPT,C'E'         RPT CHECK ACROSS EDITS + ZONES               
         BE    MATC4B                                                           
         MVC   KEY+14(1),TSPUB+4-STATDIF                                        
         CLI   MATOPT,C'Z'         ONLY ONE ZONE                                
         BE    MATC4B                                                           
         MVC   KEY+15(1),TSPUB+5-STATDIF                                        
*                                                                               
MATC4B   DS    0H                                                               
         GOTOR HIGH                                                             
         B     MATC5B                                                           
*                                                                               
MATC5    GOTOR SEQ                                                              
MATC5B   BRAS  RE,TSTERR                                                        
         TM    KEY+25,X'80'        BYPASS DELETES                               
         BNZ   MATC5                                                            
         CLC   KEY(14),KEYSAVE                                                  
         BNE   MATC8                                                            
         CLI   MATOPT,C'Y'         CROSS ZONE AND EDIT                          
         BE    MATC5D                                                           
         CLI   MATOPT,C'E'         CROSS ZONE AND EDIT                          
         BE    MATC5D                                                           
         CLI   MATOPT,C'Z'                                                      
         BNE   MATC5C                                                           
         CLC   KEY+14(1),TSPUB+4-STATDIF                                        
         BNE   MATC8                                                            
         B     MATC5D                                                           
*                                                                               
MATC5C   DS    0H                                                               
         CLC   KEY+14(2),TSPUB+4-STATDIF                                        
         BNE   MATC8                                                            
*                                                                               
MATC5D   DS    0H                                                               
         CLC   KEY+16(3),SVFRSTDT                                               
         BNL   MATC5                                                            
         MVC   TMPSVAIO,AREC                                                    
         LA    RE,SPACEND                                                       
         ST    RE,AREC                                                          
         GOTOR GETPRT                                                           
         MVC   AREC,TMPSVAIO                                                    
         BRAS  RE,TSTERR                                                        
*                                                                               
         LA    R2,SPACEND          TEST CURRENT JOB SAME                        
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC5F                                                           
         CLC   PBDJOB-PBUYREC(6,R2),MATJOB                                      
         BNE   MATC5               NO - BYPASS                                  
         B     MATC5X                                                           
*                                                                               
MATC5F   CLC   PBDJOB-PBUYREC(6,R2),TSJOB-STATDIF                               
         BNE   MATC5               NO - BYPASS                                  
*                                                                               
MATC5X   DS    0H                                                               
         LA    R2,33(R2)                                                        
*                                                                               
MATC6    DS    0H                                                               
         MVI   ELCODE,X'70'                                                     
         BRAS  RE,NXTEL                                                         
         BNE   MATC7               EOR                                          
         USING PIOELEM,R2                                                       
*                                                                               
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC6F                                                           
         CLC   PIOJOB,MATJOB                                                    
         BNE   MATC6                                                            
         B     MATC6X                                                           
*                                                                               
MATC6F   DS    0H                                                               
         CLC   PIOJOB,TSJOB-STATDIF                                             
         BNE   MATC6                                                            
*                                                                               
MATC6X   DS    0H                                                               
         CLC   PIODATE,BTODAY      SKIP IGNORE DATE TEST                        
         BE    *+14                IF PREVIOUS IO DONE TODAY                    
         CLC   PIODATE,SVIGNDAT                                                 
         BNL   MATC6                                                            
         CLC   PIOIDATE,BRPTDAT                                                 
         BNH   MATC6                                                            
         BRAS  RE,CHKDATE                                                       
         CLI   TMPBYTE1,0          INSERTION NO LONGER ON FILE?                 
         BE    MATC6               DON'T SAVE THIS DATE                         
         MVC   BRPTDAT,PIOIDATE                                                 
         MVC   RPTPUB,KEY+10       SAVE REPEAT PUB ALSO                         
*                                  USE MOST RECENT INSERT DATE                  
         B     MATC6                                                            
*                                                                               
MATC7    DS    0H                                                               
         B     MATC5                                                            
*                                                                               
MATC8    DS    0H                                                               
         CLI   PUBLCK,C'Y'         SEE IF PERFORMING PUBLISHER CHECK            
         BNE   MATC8M                                                           
         OC    SVPLKEY,SVPLKEY     SEE IF FIRST TIME                            
         BNZ   MATC8B                                                           
         XC    KEY,KEY                                                          
         MVC   KEY(2),=C'0P'       PUBLISHER POINTERS                           
         MVC   KEY+2(3),TSAGY-STATDIF                                           
         MVC   KEY+5(4),PUBPLSH                                                 
         GOTOR HIGHPB                                                           
         BRAS  RE,TSTERR                                                        
         B     MATC8E                                                           
*                                                                               
MATC8B   MVC   KEY,SVPLKEY                                                      
         GOTOR HIGHPB                                                           
MATC8D   GOTOR SEQPB                                                            
*                                                                               
MATC8E   CLC   KEY(10),KEYSAVE                                                  
         BNE   MATC8H                                                           
         CLC   KEY+10(6),SVTSP     SEE IF ORIGINAL TSPUB                        
         BE    MATC8D              SKIP SINCE I DID IT FIRST                    
         MVC   TSPUB-STATDIF(6),KEY+10                                          
         MVC   SVPLKEY,KEY         SAVE THIS KEY                                
         B     MATC4XA                                                          
*                                                                               
MATC8H   MVC   MATOPT,SVMATOPT                                                  
         MVC   TSPUB-STATDIF(6),SVTSP                                           
*                                                                               
MATC8M   DS    0H                                                               
*                                                                               
* RESTORE COMMENT RECORD ONLY NECESSARY ON-LINE                                 
* BECAUSE OF SHARED WORK AREA                                                   
*                                                                               
         CLC   W(32),PLISREC                                                    
         BE    MATC8X                                                           
         CLC   W(3),RCSVAGY                                                     
         BNE   MATC8X                                                           
         CLI   W+3,X'40'                                                        
         BNE   MATC8X                                                           
         MVC   KEY(32),W                                                        
         GOTOR HIGH                                                             
         MVC   TMPSVAIO,AREC                                                    
         LA    RE,SPACEND                                                       
         ST    RE,AREC                                                          
         GOTOR GETPRT                                                           
         MVC   AREC,TMPSVAIO                                                    
         BRAS  RE,TSTERR                                                        
*                                                                               
MATC8X   DS    0H                                                               
         MVI   MATSW,C'N'                                                       
         OC    BRPTDAT,BRPTDAT                                                  
         BZ    MATCOMX                                                          
         MVC   WORK(12),SPACES     JUST IN CASE SOMETHING LEFT IN WORK          
*                                                                               
         GOTO1 DATCON,DMCB,(3,BRPTDAT),(5,WORK)                                 
*                                                                               
* SET REPEAT DATE ID ADLIST WHEN MULTIAD=Y                                      
*                                                                               
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC8W                                                           
         L     RE,AADLIST          BE SURE I HAVE THE ADDRESS                   
         LTR   RE,RE                                                            
         BZ    MATC8W              IF NOT JUST SKIP                             
*                                                                               
MATC8P   DS    0H                                                               
         CLC   1(6,RE),MATJOB                                                   
         BNE   MATC8S                                                           
         MVC   7(3,RE),BRPTDAT     SAVE REPEAT DATE                             
         MVC   10(6,RE),RPTPUB     AND REPEAT PUB                               
         B     MATC8W                                                           
*                                                                               
MATC8S   LA    RE,16(RE)           NEXT AD                                      
         OC    0(7,RE),0(RE)                                                    
         BNZ   MATC8P                                                           
*                                                                               
* ADCODE NOT IN TABLE - SHOULD NEVER HAPPEN                                     
* HOWEVER DON'T DIE - JUST LEAVE REPEAT DATE BLANK                              
*                                                                               
MATC8W   DS    0H                                                               
         CLI   MYLANG,C'F'                                                      
         BNE   *+8                                                              
         BRAS  RE,FRDATE                                                        
*                                                                               
         MVI   MATSW,C'R'                                                       
*                                                                               
MATC9    DS    0H                                                               
         L     RF,ADICSECT                                                      
         USING DICSECTM,RF                                                      
         MVC   P,SPACES                                                         
         LA    R1,P+05                                                          
*--->    MVC   0(19,R1),=C'YOU HAVE MATERIALS.'                                 
         MVC   0(L'PP@IOM1,R1),PP@IOM1                                          
         DROP  RF                                                               
*                                                                               
         LA    R1,132(R1)                                                       
         MVC   0(L'SPACES,R1),SPACES                                            
*                                                                               
         CLI   MYLANG,C'F'         SEE IF FRENCH                                
         BE    MATF9               GO TO SPECIAL CODE FOR REPEAT                
*                                                                               
         MVC   0(17,R1),=C'AD IS A REPEAT OF'                                   
         MVC   18(8,R1),WORK                                                    
         LA    R3,26(R1)                                                        
         CLI   TSPBFREQ-STATDIF,C'M'                                            
         BNE   MATC9A                                                           
         LA    R3,24(R1)                                                        
         MVC   21(6,R1),23(R1)                                                  
*                                                                               
MATC9A   DS    0H                                                               
         MVC   0(11,R3),=C' INSERTION'                                          
         CLI   QMEDIA,C'O'                                                      
         BNE   *+10                                                             
         MVC   0(11,R3),=C' POSTING  '                                          
*                                                                               
* NOTE - FRENCH CODE RETURNS TO MATC9AX                                         
*                                                                               
MATC9AX  DS    0H                                                               
         OC    RPTPUB,RPTPUB       CHECK FOR REPEAT PUB                         
         BZ    MATC9B                                                           
         CLC   RPTPUB,SVTSP        DON'T DISPLAY IF SAME AS ORIGINAL            
         BE    MATC9B                                                           
         GOTOR AIOPRNT,DMCB,(RC)                                                
         L     RF,ADICSECT                                                      
         USING DICSECTM,RF                                                      
*--->    MVC   P+05(3),=C'FOR'                                                  
         MVC   P+05(L'PP@FOR),PP@FOR                                            
         DROP  RF                                                               
*                                                                               
         LA    R3,P+10                                                          
         CLI   MYLANG,C'F'         SEE IF FRENCH                                
         BE    *+8                                                              
         LA    R3,P+9                                                           
*                                                                               
         GOTO1 APUBEDIT,DMCB,RPTPUB,0(R3)                                       
*                                                                               
         MVC   SVPUBKEY,PUBREC     SAVE KEY OF PUB IN PUBREC                    
*                                  SINCE I NEED TO TO REREAD IT                 
         XC    PUBREC(50),PUBREC                                                
         XC    KEY,KEY                                                          
         MVC   KEY(1),TSMED-STATDIF                                             
         MVC   KEY+1(6),RPTPUB                                                  
         MVC   KEY+7(2),TSAGY-STATDIF                                           
         GOTOR HIGHPB                                                           
         B     FP3                                                              
FP2      GOTOR SEQPB                                                            
FP3      CLC   KEY(7),KEYSAVE                                                   
         BNE   FP8                                                              
         CLC   KEY+7(2),KEYSAVE+7                                               
         BE    FP4                                                              
         CLC   KEY+7(2),=C'ZZ'                                                  
         BNE   FP2                                                              
         CLI   PAGYPROF+16,C'0'    TEST DEFAULT                                 
         BE    FP8                 NO                                           
*                                                                               
FP4      DS    0H                                                               
         CLI   KEY+9,X'81'                                                      
         BNE   FP6                                                              
         CLI   PUBREC,0                                                         
         BNE   FP10                                                             
         MVC   TMPSVAIO,AREC                                                    
         LA    RE,PUBREC                                                        
         ST    RE,AREC                                                          
         GOTOR GETPUB                                                           
         MVC   AREC,TMPSVAIO                                                    
         B     FP2                                                              
*                                                                               
FP6      DS    0H                                                               
         B     FP2                                                              
*                                                                               
FP8      DS    0H                                                               
         CLI   PUBREC,0                                                         
         BE    FPNO                                                             
*                                                                               
FP10     DS    0H                                                               
         LA    R1,P+27                                                          
FP12     CLI   0(R1),C' '                                                       
         BH    FP15                                                             
         BCT   R1,FP12                                                          
*                                                                               
FP15     MVC   2(20,R1),PUBNAME                                                 
*                                                                               
         MVC   134(20,R1),PUBZNAME IN PSECOND                                   
*                                                                               
         GOTOR AIOPRNT,DMCB,(RC)                                                
*                                                                               
* MUST REREAD PUB THAT WAS IN PUBREC                                            
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(20),SVPUBKEY                                                 
         GOTOR HIGHPB                                                           
         CLC   KEY(10),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                SOMETHING VERY WRONG                         
         MVC   TMPSVAIO,AREC                                                    
         LA    RE,PUBREC                                                        
         ST    RE,AREC                                                          
         GOTOR GETPUB                                                           
         MVC   AREC,TMPSVAIO                                                    
         B     MATCOMX                                                          
*                                                                               
FPNO     DC    H'0'                SOMETHING VERY WRONG                         
*                                                                               
MATC9B   DS    0H                                                               
*                                                                               
         GOTOR AIOPRNT,DMCB,(RC)                                                
         B     MATCOMX                                                          
         EJECT                                                                  
*                                                                               
* SPECIAL FENCH CODE                                                            
*                                                                               
MATF9    DS    0H                                                               
         L     RF,ADICSECT                                                      
         USING DICSECTM,RF                                                      
         MVC   0(L'PP@IOM3,R1),PP@IOM3                                          
*                                                                               
         LA    R3,L'PP@IOM3(R1)    SCAN BACKWARDS FOR NON-SPACE                 
         MVC   1(2,R3),=C'L'''                                                  
         MVC   3(L'PP@INSER,R3),PP@INSER                                        
         CLI   QMEDIA,C'O'                                                      
         BNE   MATF9B                                                           
         MVC   3(L'PP@INSER,R3),SPACES                                          
         MVC   3(09,R3),=C'AFFICHAGE'                                           
         DROP  RF                                                               
*                                                                               
MATF9B   DS    0H                                                               
         LA    R3,L'PP@INSER+3(R3) SCAN BACKWARDS FOR NON-SPACE                 
*                                                                               
MATF9D   CLI   0(R3),C' '                                                       
         BH    MATF9E                                                           
         BCT   R3,MATF9D                                                        
*                                                                               
MATF9E   MVC   2(8,R3),WORK                                                     
         LA    R3,10(R3)                                                        
         CLI   TSPBFREQ-STATDIF,C'M'                                            
         BNE   MATF9F                                                           
         AHI   R3,-10              BACK-UP R3                                   
         MVC   5(6,R3),7(R3)       MOVE /YEAR AND SPACES                        
*                                                                               
MATF9F   GOTOR AIOPRNT,DMCB,(RC)                                                
         B     MATCOMX                                                          
*                                                                               
MATCOMX  DS    0H                                                               
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
* SEE IF INSERTION DATE STILL ON FILE BEFORE USING IT AS A REPEAT DATE          
* TMPBYTE1 -> 0 MEANS INSERTION NO LONGER ON FILE                               
*                                                                               
CHKDATE  NTR1                                                                   
         MVI   TMPBYTE1,0                                                       
         MVC   MATSVKEY,KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(16),MATSVKEY                                                 
         MVC   KEY+16(3),PIOIDATE                                               
         GOTOR HIGH                                                             
         CLC   KEY(19),KEYSAVE                                                  
         BNE   CHKDATX                                                          
         MVI   TMPBYTE1,1                                                       
*                                                                               
CHKDATX  MVC   KEY,MATSVKEY                                                     
         GOTOR HIGH                MUST RESTORE SEQ READ                        
         J     EXIT                                                             
         EJECT                                                                  
*                                                                               
NXTEL    ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLC   ELCODE,0(R2)                                                     
         JE    NXTELX              CC IS EQUAL                                  
         CLI   0(R2),0                                                          
         JNE   NXTEL                                                            
         LTR   R2,R2               CC IS NOT EQUAL                              
NXTELX   BR    RE                                                               
*                                                                               
TSTERR   DS    0H                                                               
         CLI   ERR,0                                                            
         JNE   EXIT                                                             
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BZR   RE                                                               
         MVC   ERR+1,DMCB+8                                                     
         J     EXIT                                                             
*                                                                               
FRDATE   NTR1                      CONVERT DATE IN WORK (MMMDD/YY)              
         LA    R1,DATETAB                                                       
FRDAT2   CLI   0(R1),X'FF'         END OF TABLE                                 
         BNE   *+6                                                              
         DC    H'0'                SOMEHTING VERY WRONG                         
*                                                                               
         CLC   WORK(3),0(R1)                                                    
         BE    FRDAT10                                                          
         LA    R1,6(R1)                                                         
         B     FRDAT2                                                           
*                                  TO FRENCH                                    
FRDAT10  MVC   WORK(3),3(R1)                                                    
         J     EXIT                                                             
*                                                                               
DATETAB  DC    C'JANJAN'                                                        
         DC    C'FEBFEV'                                                        
         DC    C'MARMAR'                                                        
         DC    C'APRAVR'                                                        
         DC    C'MAYMAI'                                                        
         DC    C'JUNJUN'                                                        
         DC    C'JULJUL'                                                        
         DC    C'AUGAOU'                                                        
         DC    C'SEPSEP'                                                        
         DC    C'OCTOCT'                                                        
         DC    C'NOVNOV'                                                        
         DC    C'DECDEC'                                                        
         DC    X'FF'                                                            
*                                                                               
         DROP  RB                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
MATCOMD  DSECT                                                                  
*                                                                               
RELO     DS    F                                                                
ADICSECT DS    A                                                                
*                                                                               
MATSVKEY DS    CL32                                                             
MYLANG   DS    CL1                                                              
SVPUBKEY DS    CL20                ONLY NEED 20 BYTES                           
SVTSP    DS    CL6                                                              
SVMATOPT DS    CL1                 SAVED "REAL" MATOPT                          
PUBLCK   DS    CL1                 PUBLISHER CHECK                              
SVPLKEY  DS    CL32                                                             
*                                                                               
TMPBYTE1 DS    X                                                                
TMPSVAIO DS    XL(L'AREC)                                                       
*                                                                               
MATCOMDX EQU   *                                                                
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         PRINT OFF                                                              
*                                                                               
* DICTIONARY FOR PP72MAT AND PPMATCOM                                           
*                                                                               
       ++INCLUDE PPIOMATD                                                       
         EJECT                                                                  
*                                                                               
       ++INCLUDE PPINSWRK1                                                      
         EJECT                                                                  
*                                                                               
       ++INCLUDE POLFILE                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE PPINSWRK2                                                      
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'075PPMATCOM  10/10/05'                                      
         END                                                                    
