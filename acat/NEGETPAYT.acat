*          DATA SET NEGETPAYT  AT LEVEL 004 AS OF 05/01/02                      
*CATALP NEGETPAY                                                                
         SPACE 2                                                                
* NEGETPAY - MODULE TO GET PAYABLE AMOUNTS/GENERATE PAY ELEMENTS FOR A          
*            UNIT                                                               
*                                                                               
* PARA 1   = A(PAY PROGRAM WORKING STORAGE)                                     
* PARA 2   = A(PAY BLOCK)                                                       
*                                                                               
* PAY BLOCK IS COVERED BY PAYBLKD DSECT INCLUDED IN PAY PROGRAM WORKING         
* STORAGE                                                                       
*                                                                               
         TITLE 'NEGETPAY - MODULE TO GET PAY DATA/ELEMENTS FOR A UNIT'          
GETPAY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**GTPY**                                                       
         LM    R6,R7,0(R1)                                                      
         USING PAYWRKD,R6                                                       
         USING PAYBLKD,R7                                                       
         XC    PAYGROSS(8),PAYGROSS INITIALIZE BLOCK FIELDS                     
         MVI   PAYSTAT,0                                                        
         MVI   PAYERROR,0                                                       
         MVI   PAYRSN,0                                                         
         MVI   PAYNELS,0                                                        
         TM    NBUNITST,X'42'      TEST PRE-EMPT/MISSED                         
         BZ    GETPAY4             NEITHER                                      
         OC    NBPAYTGR,NBPAYTGR   TEST IF ANY TIME PAID                        
         BNZ   *+14                YES-SO GENERATE A MINUS                      
         OC    NBPAYIGR,NBPAYIGR   NO-NOW TEST FOR PAID INT                     
         BZ    GETPAYX             NO-NOTHING TO PAY SO EXIT                    
         SPACE 1                                                                
* MISSING OR PRE-EMPTED UNIT                                                    
*                                                                               
GETPAY1  CLI   PAYTYPE,C'I'        TEST FOR INT ONLY                            
         BE    GETPAY2             YES                                          
         OC    NBPAYTGR,NBPAYTGR   TEST FOR TIME PAID                           
         BZ    GETPAY2             NO                                           
         LM    R2,R3,NBPAYTGR      GET PAID GROSS/NET                           
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         OI    PAYSTAT,PAYABLE+TPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY2                                                          
         MVI   BYTE,C'T'                                                        
         BAS   RE,GENELS                                                        
         SPACE                                                                  
GETPAY2  CLI   PAYTYPE,C'T'        TEST FOR TIME PAID ONLY                      
         BE    GETPAYX             YES                                          
         OC    NBPAYIGR,NBPAYIGR   TEST IF ANY INT PAID                         
         BZ    GETPAYX             NO-ALL DONE                                  
         LM    R2,R3,NBPAYIGR                                                   
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         OI    PAYSTAT,PAYABLE+IPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAYX                                                          
         MVI   BYTE,C'I'                                                        
         BAS   RE,GENELS                                                        
         B     GETPAYX                                                          
         SPACE                                                                  
* REGULAR UNIT                                                                  
*                                                                               
GETPAY4  CLI   NBPRD,0             TEST FOR UNALLOCATED UNIT                    
         BE    PAYRSN1             YES                                          
         TM    NBUNITST,X'20'      TEST FOR ACTUAL COST                         
         BZ    PAYRSN2             NO-THAT'S AN ERROR                           
         CLI   PAYTYPE,C'I'        TEST FOR INT ONLY                            
         BE    GETPAY6                                                          
         L     R2,NBACTUAL         GET ACTUAL COST                              
         LR    RE,R2                                                            
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+6                                                              
         LNR   RE,RE                                                            
         S     RE,NBPAYTGR         DERIVE PAYABLE BALANCE                       
         BNZ   GETPAY5             NON-ZERO-THEREFORE TIME TO PAY               
*                                  ZERO-LOOK FOR PAYABLE 0 ACTUAL COST          
         OC    NBACTUAL,NBACTUAL   TEST FOR ZERO ACTUAL COST                    
         BNZ   GETPAY6             NO-UNIT NOT PAYABLE                          
         MVI   BYTE,C'T'           LOOK FOR PREVIOUS PAYMENT                    
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0            TEST IF ELEMENT FOUND                        
         BE    GETPAY6             YES-UNIT IS NOT PAYABLE                      
         SPACE 1                                                                
GETPAY5  BAS   RE,FINDNET                                                       
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+8                                                              
         LNR   R2,R2                                                            
         LNR   R3,R3                                                            
         S     R2,NBPAYTGR         DERIVE PAYABLE BALANCE                       
         S     R3,NBPAYTNT                                                      
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         OI    PAYSTAT,PAYABLE+TPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY6                                                          
         MVI   BYTE,C'T'                                                        
         BAS   RE,GENELS                                                        
         SPACE                                                                  
GETPAY6  CLI   PAYTYPE,C'T'        TEST FOR TIME ONLY                           
         BE    GETPAYX             YES-ALL DONE                                 
         L     R2,NBINTEG          GET INTEGRATION                              
         LR    R3,R2               SET NET=GROSS                                
         TM    NBPACKST,X'04'      TEST FOR NON-COMMISSIONABLE INT              
         BO    *+8                 YES                                          
         BAS   RE,FINDNET                                                       
         TM    NBUNITST,X'80'                                                   
         BZ    *+8                                                              
         LNR   R2,R2                                                            
         LNR   R3,R3                                                            
         S     R2,NBPAYIGR         DERIVE PAYABLE INTEGRATION                   
         BZ    GETPAYX             ZERO-ALL DONE                                
         S     R3,NBPAYINT                                                      
         TM    NBPACKST,X'04'      TEST FOR NON-COMMISSIONABLE INT              
         BZ    *+6                                                              
         LR    R3,R2               IF ON USE GROSS FIGURE                       
         STM   R2,R3,DUB                                                        
         CLI   PAYTYPE,C'I'        TEST FOR INT ONLY                            
         BNE   GETPAY7             NO                                           
         MVI   BYTE,C'T'           YES-NOW SEE IF TIME PAID                     
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0                                                         
         BE    GETPAY7             FOUND A TIME PAYMENT                         
         CLI   ACTION,CLEAR        FOR ACTIONS CLEAR OR TEST, THIS              
         BE    PAYRSN3             IS AN ERROR                                  
         CLI   ACTION,TEST                                                      
         BE    PAYRSN3                                                          
         SPACE                                                                  
GETPAY7  OI    PAYSTAT,PAYABLE+IPAY                                             
         BAS   RE,UPPAY                                                         
         MVI   BYTE,C'I'                                                        
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAYX                                                          
         BAS   RE,GENELS                                                        
         SPACE                                                                  
GETPAYX  XMOD1 1                                                                
         SPACE 2                                                                
PAYRSN1  MVI   PAYRSN,UNALLOC                                                   
         B     GETPAYX                                                          
         SPACE 1                                                                
PAYRSN2  MVI   PAYERROR,ACTERR                                                  
         MVI   PAYRSN,NOACTUAL                                                  
         B     GETPAYX                                                          
         SPACE 1                                                                
PAYRSN3  MVI   PAYERROR,TFRSTERR                                                
         MVI   PAYRSN,TIMEUNPD                                                  
         B     GETPAYX                                                          
         EJECT                                                                  
* SUB-ROUTINE TO UPDATE PAY BUCKETS - AT ENTRY DUB CONTAINS VALUES              
*                                                                               
UPPAY    LM    R0,R1,DUB                                                        
         A     R0,PAYGROSS                                                      
         A     R1,PAYNT                                                         
         STM   R0,R1,PAYGROSS                                                   
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO CALCULATE NET - AT ENTRY R2 CONTAINS GROSS, ON EXIT            
* R3 CONTAINS RESULT                                                            
*                                                                               
FINDNET  LR    R1,R2                                                            
         M     R0,=F'8500'                                                      
         SLDA  R0,1                DIVIDEND X 2                                 
         D     R0,=F'10000'                                                     
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                COMPLETE ROUNDING                            
         LR    R3,R1                                                            
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO GENERATE A PAY ELEMENT - AT ENTRY DUB CONTAINS AMOUNTS         
*                                                                               
GENELS   ST    RE,FULL             SAVE RETURN POINT                            
         ICM   RE,15,PAYAELS       GET POINTER TO ELEMENT AREA                  
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP - USER DID NOT SUPPLY ONE            
         ZIC   R1,PAYNELS          INCREMENT ELEMENT COUNT                      
         LA    R1,1(R1)                                                         
         STC   R1,PAYNELS                                                       
         BCTR  R1,0                                                             
         MH    R1,=Y(NUPAYELN)     INDEX INTO ELEMENT AREA                      
         LA    RE,0(R1,RE)         POINT TO NEXT ELEMENT POSITION               
         USING NUPAYD,RE                                                        
         XC    NUPAYD(NUPAYELN),NUPAYD                                          
         MVI   NUPAYEL,X'12'                                                    
         MVI   NUPAYLEN,NUPAYELN                                                
         MVC   NUPAYTYP,BYTE                                                    
         MVC   NUPAYDAT,TODAYC                                                  
         MVC   NUPAYRTY,REPTYPE                                                 
         MVC   NUPAYREP,REP                                                     
         MVC   NUPAYGRS,DUB                                                     
         MVC   NUPAYNET,DUB+4                                                   
         L     RE,FULL                                                          
         BR    RE                                                               
         DROP  RE                                                               
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE NEPAYWRK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004NEGETPAYT 05/01/02'                                      
         END                                                                    
