*          DATA SET DEGETDPT   AT LEVEL 041 AS OF 11/12/18                      
*PROCESS USING(WARN(15))                                                        
*CATALP DEGETDPT                                                                
         TITLE 'DEGETDPT - DAYPART FILE READ CONTROLLER'                        
GETDPT   RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**GETDPT,RA                                                    
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
         USING COMFACSD,R8         R8=A(COMFACS)                                
*                                                                               
         MVC   DBFACTOR,=X'0001'                                                
         CLI   0(R1),0             TEST FIRST/NEXT TIME                         
         BNE   GETDPT1                                                          
*                                                                               
         ZIC   R9,DBFUNCT          SAVE ORIGINAL REQUESTED FUNC                 
         MVI   DBFUNCT,DBTSTACS    SET FOR VALIDATE                             
         GOTO1 ATSTACS             CALL TO VALIDATE ACCESS                      
         STC   R9,DBFUNCT          RESTORE ORIGINAL REQUEST                     
         CLI   DBERROR,0           EXIT IF NOT VALID                            
         BNE   GETX                                                             
         OC    ACONHDR,ACONHDR                                                  
         BZ    GETX                                                             
         L     R2,ACONHDR          R2=A(CONTROL TABLE)                          
         USING CONHDRD,R2                                                       
         LA    R3,CONHDRD+CONHDRLN                                              
         USING CONDTAD,R3                                                       
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALET                                                     
         CPYA  AR3,AR2                                                          
         SAC   512                                                              
         TM    CONCTRLS+1,X'08'                                                 
         BO    *+8                                                              
         MVI   DBERROR,INVFILE                                                  
         SAC   0                                                                
         LAM   AR2,AR3,ARZERO                                                   
         CLI   DBERROR,0                                                        
         BNE   GETX                                                             
*                                                                               
* GET DAYPART DEMOS                                                             
*                                                                               
GETDPT1  B     GETDPT1B            SKIP THE DAY/QTR STUFF                       
*                                  ----------------------                       
         SR    R1,R1                                                            
         CLI   DBDAYOPT,C'Y'                                                    
         BE    GETDPT1A                                                         
         CLI   DBSELSRC,C'S'                                                    
         BNE   *+8                                                              
         CLI   DBBTYPE,C'S'                                                     
         BNE   *+8                                                              
GETDPT1A LA    R1,1(R1)                                                         
         GOTO1 AGETDQ                                                           
         BNE   GETX                                                             
         LA    R1,DBDQTAB                                                       
*                                                                               
GETDPT1B DS    0H                                                               
         LA    R2,DBKEY            BUILD DIRECTORY KEY                          
         USING DRKEY,R2            R2=A(KEY)                                    
         XC    DRKMAJOR,DRKMAJOR                                                
         XC    0(L'DBLSTDIR,R2),0(R2)                                           
         MVC   DRCODE,RECCODE                                                   
         MVC   DRMEDIA,DBINTMED                                                 
         MVC   DRSRC,DBACTSRC                                                   
         MVC   DRSTAT,DBSELSTA                                                  
         MVC   DRBOOK,DBSELBK                                                   
         MVC   DRKMKT,DBSELMK                                                   
         XC    DRBOOK,=X'FFFF'     DESCENDING ORDER                             
         MVC   DRBTYP,DBBTYPE                                                   
*                                                                               
*                                                                               
GETDPT2  MVI   IOFLAG,DIR+READ+DEM                                              
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BNE   GETDPT3                                                          
*****    OC    DBSELDPT,DBSELDPT                                                
*****    BZ    GETDPT40                                                         
         MVI   IOFLAG,FILE+HIGH+DEM   READ HIGH                                 
*****    BNZ   GETDPT42                                                         
* BEN SAYS (OCT/2017):                                                          
* WE SHOULD ALWAYS READ HIGH.  I DONT UNDERSTAND WHY WE WOULD NEED              
* TO READ SEQUENTIAL IF DBSELDPT IS NOT FILLED IN.                              
         B     GETDPT42                                                         
*                                                                               
GETDPT3  MVI   DBERROR,NOTFOUND    SET NOT FOUND                                
         B     GETX                                                             
*                                  SEQUENTIAL MODE (FILE)                       
                                                                                
GETDPT40 MVI   IOFLAG,FILE+SEQ+DEM        READ SEQUENTIAL/FILE                  
GETDPT42 GOTO1 AIO,IOFLAG                                                       
         BNE   GETX                                                             
         GOTO1 ATSTMRKT            CHECK FOR VALID MARKET                       
         BNE   GETDPT80                                                         
         L     R1,DBAREC           SET DATA START FOR ROUTINES                  
         AH    R1,DBDTADSP                                                      
         ST    R1,DBAQUART                                                      
GETDPT50 L     RE,DBAQUART         SET TO PREV DAYPART                          
GETDPT52 ZIC   R1,1(RE)            SCAN FOR NEXT IN THIS REC                    
         AR    RE,R1                                                            
         CLI   0(RE),0             END - READ NEXT RECORD                       
         BE    GETDPT40                                                         
         CLI   0(RE),X'20'         QH ELEM - FOUND NEXT DAYPART                 
         BNE   GETDPT52                                                         
         ST    RE,DBAQUART                                                      
*                                  RETURN TO CALLER                             
         OC    DBSELDPT,DBSELDPT                                                
         BZ    GETDPT60                                                         
         CLC   DBSELDPT,2(RE)                                                   
         BNE   GETDPT50                                                         
         MVI   DBRECTYP,DBRECDEM                                                
         MVI   DBMODE,DBMNEXT      NO - SET SEQUENTIAL MODE                     
         BAS   RE,GETX                                                          
         MVI   DBERROR,X'80'                                                    
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         XC    DBMINKEY,DBMINKEY                                                
         XC    DBNDXDA,DBNDXDA                                                  
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         B     GETX                                                             
*                                                                               
GETDPT60 MVI   DBRECTYP,DBRECDEM                                                
         MVI   DBMODE,DBMNEXT      NO - SET SEQUENTIAL MODE                     
         BAS   RE,GETX                                                          
         B     GETDPT50            GET NEXT DAYPART                             
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*                                  ERROR EXIT                                   
GETDPT80 MVI   DBERROR,INVMRKT                                                  
         B     GETX                                                             
*                                                                               
*                                                                               
*                                  EXIT FROM MODULE                             
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                                                               
         NTR1  ,                   SAVE REGS & GO TO USER HOOK                  
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
*                                                                               
GETXX    XIT1  ,                   EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
         LTORG                                                                  
ARZERO   DC    16F'0'                                                           
         EJECT                                                                  
       ++INCLUDE DEGETD                                                         
         EJECT                                                                  
       ++INCLUDE DEDBEXTRAD                                                     
         EJECT                                                                  
* INCLUDE DDMONYREQU                                                            
         PRINT OFF                                                              
       ++INCLUDE DDMONYREQU                                                     
         PRINT ON                                                               
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'041DEGETDPT  11/12/18'                                      
         END                                                                    
