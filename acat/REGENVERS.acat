*          DATA SET REGENVERS  AT LEVEL 065 AS OF 05/01/02                      
*CATALP REGENVER                                                                
***********************************************************************         
*                                                                     *         
*         REGENVER --- BUMP UP REP/STA VERSION AND MARK UNCONFIRMED   *         
*                  --- SAVES LAST 3 VERSION DATES FOR REP AND STA     *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 09NOV94 (SKU) --- ORIGINATION DATE                                  *         
*                                                                     *         
* 12JAN95 (SKU) --- BUMP REP AS WELL AS STATION VERSION NUMBERS HERE  *         
*                                                                     *         
* 02MAR95 (SKU) --- SUPPORT FOR NEW STA MULTI-SEND IN X'20' ELEMENT   *         
*                                                                     *         
* 06MAR96 (RHV) --- DELETE REP/STA ORD COMMENT WHEN CHANGE VERSION #  *         
*                                                                     *         
* 20MAY97 (RHV) --- DELETE CFC COMMENT ON VERSION # CHANGE            *         
*                                                                     *         
* 26AUG97 (SKU) --- SUPPORT FOR DARE AGENCY CHANGES                   *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENVER - CONTRACT VERSION DATES'                              
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 =       A(CONREC)                                    
*                                      BYTE 1 C'R' = PROCESS FOR REP            
*                                             C'S' = PROCESS FOR STA            
*                        2 =       A(ROUTINE ADDRESS BLOCK)                     
*                                      WORD 1 = A(HELLO)                        
*                                           2 = A(DATCON)                       
*                                  BYTE 1 X'80' = SKIP ORD COMMENT DEL          
*                                                                               
*  OUTPUT:                                                                      
*              ON ERROR SET ^0 CC AND/OR ERROR EXIT                             
*              R3 HAS ERROR MESSAGE ON EXIT                                     
*                                                                               
*              NOTE!! BEWARE THAT THE RECORD SIZE CAN CHANGE AFTER              
*               CALLING REGENVER. THEREFORE, ANY ELEMENT POINTERS IN            
*               THE CALLER'S MODULE MIGHT NEED TO BE RESET AFTER                
*               RETURNING FROM THIS MODULE!!                                    
***********************************************************************         
REGENVER CSECT                                                                  
         PRINT GEN                                                              
         NMOD1 WORKX-WORKD,REGENVER                                             
         USING WORKD,RC                                                         
         MVC   REPORSTA,0(R1)                                                   
         MVC   ACONREC,0(R1)                                                    
         MVC   SKIPCOMD,4(R1)                                                   
         L     R1,4(R1)                                                         
         MVC   VHELLO,0(R1)                                                     
         MVC   VDATCON,4(R1)                                                    
         MVC   DATADISP,=H'34'                                                  
*                                                                               
         LR    RF,RA                                                            
         AH    RF,=Y(TWAWORKQ)     4K                                           
         USING TWAWORK,RF                                                       
         OI    TWACFCFL,X'80'      DELETE CFC COMMENT                           
         DROP  RF                                                               
*                                                                               
REGENV10 DS    0H                                                               
         BAS   RE,COMD                                                          
         B     VERSDATE            PROCESS VERSION DATES                        
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC               SET CONDITION CODE AT EXIT                   
         XIT1  REGS=(R3)                                                        
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* BUMP UP REP/STA VERSION NUMBER AND MARK CONTRACT UNCONFIRMED                  
*********************************************************************           
VERSDATE DS    0H                                                               
         L     R6,ACONREC                                                       
         USING RCONSEND,R6                                                      
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         CLI   REPORSTA,C'S'       PROCESS FOR REP/STA?                         
         BE    VERS10                                                           
                                                                                
* PROCESS FOR REP                                                               
* SET REP VERSION ADVANCED                                                      
         NI    RCONSENF,X'FF'-X'20'                                             
                                                                                
         ZIC   R1,RCONSRV          NEXT REP VERSION NUMBER                      
         LA    R1,2(R1)            SHOULD BE THE GREATER OF                     
         STC   R1,RCONSRV          REP VERSION NUMBER + 2                       
         CLC   RCONSRV,RCONSSV                                                  
         BH    VERS30              OR                                           
         ZIC   R1,RCONSSV                                                       
         LA    R1,1(R1)            STA VERSION NUMBER + 1                       
         STC   R1,RCONSRV                                                       
         B     VERS30                                                           
                                                                                
* PROCESS FOR STATION                                                           
VERS10   DS    0H                                                               
* SET STATION VERSION ADVANCED                                                  
         NI    RCONSENF,X'FF'-X'10'                                             
                                                                                
         ZIC   R1,RCONSSV          NEXT STATION VERSION NUMBER                  
         LA    R1,2(R1)            SHOULD BE THE GREATER OF                     
         STC   R1,RCONSSV          STATION VERSION NUMBER + 2                   
         CLC   RCONSSV,RCONSRV                                                  
         BH    VERS30              OR                                           
         ZIC   R1,RCONSRV                                                       
         LA    R1,1(R1)            REP VERSION NUMBER + 1                       
         STC   R1,RCONSSV                                                       
         DROP  R6                                                               
         EJECT                                                                  
*********************************************************************           
* SAVE OFF LAST 3 VERSION DATES. IF OLD ELEMENT, CHANGE TO NEW                  
*********************************************************************           
VERS30   DS    0H                                                               
         L     R6,ACONREC                                                       
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING RCONSEND,R6                                                      
         CLI   RCONSNLN,RCONSN2Q   ELEMENT WITH MULTI-STA SEND INFO?            
         BE    VERS50                                                           
                                                                                
         XC    ELEM,ELEM           UPDATE OLD ELEMENT WITH NEW LENGTH           
         ZIC   RE,RCONSNLN                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),RCONSEND    SAVE OFF OLD ELEMENT                         
                                                                                
         CLI   RCONSNLN,RCONSNLQ   ELEMENT WITH INITIAL RELEASE OF              
         BNE   VERS35              SAVED VERSION INFO HAS THIS LENGTH           
                                                                                
VER      USING RCONSEND,ELEM       NEED TO RE-ARRANGE INTO NEW DSECT            
         XC    VER.RCONSRV1(3),VER.RCONSRV1                                     
                                                                                
         ZIC   R4,RCONSRV          CALCULATE PREVIOUS REP VERSIONS              
         CLI   REPORSTA,C'S'       PREVIOUS REP VERSIONS ARE:                   
         BE    *+8                 DECREMENTS OF 2 OF CURRENT                   
         SH    R4,=H'2'            REP VERSION # - 2                            
         OC    RCONSRD3,RCONSRD3                                                
         BZ    *+12                                                             
         STC   R4,VER.RCONSRV3                                                  
         SH    R4,=H'2'                                                         
         OC    RCONSRD2,RCONSRD2                                                
         BZ    *+12                                                             
         STC   R4,VER.RCONSRV2                                                  
         SH    R4,=H'2'                                                         
         OC    RCONSRD1,RCONSRD1                                                
         BZ    *+8                                                              
         STC   R4,VER.RCONSRV1                                                  
                                                                                
         MVC   VER.RCONS2D1,RCONSSD1                                            
         MVC   VER.RCONS2D2,RCONSSD2                                            
         MVC   VER.RCONS2D3,RCONSSD3                                            
         MVC   VER.RCONS2V1,RCONSSV1                                            
         MVC   VER.RCONS2V2,RCONSSV2                                            
         MVC   VER.RCONS2V3,RCONSSV3                                            
         DROP  VER,R6                                                           
                                                                                
* DELETE CURRENT X'20' ELEMENT                                                  
VERS35   DS    0H                                                               
         GOTO1 VHELLO,DMCB,(C'D',=C'REPFILE'),(X'20',ACONREC),0                 
                                                                                
* ADD X'20' ELEMENT BACK WITH NEW LENGTH                                        
         MVI   ELEM+1,RCONSN2Q                                                  
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),ACONREC,ELEM,=C'ADD=CODE'         
         TM    DMCB+12,X'05'       REC TOO LONG                                 
         BZ    VERS40                                                           
         LA    R3,339              RECORD FULL - CHANGE NOT PROCESSED           
         B     NO                                                               
                                                                                
VERS40   DS    0H                                                               
         L     R6,ACONREC                                                       
         USING RCONSEND,R6                                                      
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* SAVE REP VERSION/DATE                                                         
*                                                                               
VERS50   DS    0H                                                               
         CLI   REPORSTA,C'S'                                                    
         BE    VERS80                                                           
                                                                                
         XC    WORK,WORK                                                        
         MVC   WORK(6),RCONSRD1                                                 
         MVC   WORK+8(3),RCONSRV1  LEAVE 2 BYTES BEFORE VERSION#'S              
*                                  INCASE OF OVERFLOW                           
         LA    R5,WORK             FIND EMPTY DATE SLOT                         
         LA    R4,WORK+8           AND CORRESPONDING EMPTY VERSION SLOT         
                                                                                
VERS60   OC    0(2,R5),0(R5)                                                    
         BZ    VERS70                                                           
         LA    R5,2(R5)                                                         
         LA    R4,1(R4)                                                         
         B     VERS60                                                           
                                                                                
VERS70   DS    0H                  SAVE OFF REP DATE AND VERSION#               
         GOTO1 VDATCON,DMCB,(5,0),(2,0(R5))                                     
         MVC   0(1,R4),RCONSRV                                                  
                                                                                
         LA    R5,WORK             CHECK FOR OVERFLOW                           
         LA    R4,WORK+8                                                        
         CLI   WORK+6,0            IF 4 DATES ARE SAVED                         
         BE    VERS75              KEEP ONLY THE 3 MOST CURRENT                 
         LA    R5,2(R5)                                                         
         LA    R4,1(R4)                                                         
*                                                                               
VERS75   DS    0H                                                               
         MVC   RCONSRD1(6),0(R5)                                                
         MVC   RCONSRV1(3),0(R4)                                                
*                                                                               
         BAS   RE,DAREMAN          UPDATE DARE ELEM FOR AGENCY CHANGES          
*                                  USER HAS STARTED MANUAL CHANGES              
         B     VERSX                                                            
*                                                                               
* SAVE STATION VERSION/DATE                                                     
*                                                                               
VERS80   DS    0H                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(6),RCONS2D1                                                 
         MVC   WORK+8(3),RCONS2V1  LEAVE 2 BYTES BEFORE VERSION#'S              
*                                  INCASE OF OVERFLOW                           
         LA    R5,WORK             FIND EMPTY DATE SLOT                         
         LA    R4,WORK+8           AND CORRESPONDING EMPTY VERSION SLOT         
                                                                                
VERS90   OC    0(2,R5),0(R5)                                                    
         BZ    VERS100                                                          
         LA    R5,2(R5)                                                         
         LA    R4,1(R4)                                                         
         B     VERS90                                                           
                                                                                
VERS100  DS    0H                  SAVE OFF STA DATE AND VERSION#               
         GOTO1 VDATCON,DMCB,(5,0),(2,0(R5))                                     
         MVC   0(1,R4),RCONSSV                                                  
                                                                                
         LA    R5,WORK             CHECK FOR OVERFLOW                           
         LA    R4,WORK+8                                                        
         CLI   WORK+6,0            IF 4 DATES ARE SAVED                         
         BE    VERS110             KEEP ONLY THE 3 MOST CURRENT                 
         LA    R5,2(R5)                                                         
         LA    R4,1(R4)                                                         
                                                                                
VERS110  DS    0H                                                               
         MVC   RCONS2D1(6),0(R5)                                                
         MVC   RCONS2V1(3),0(R4)                                                
                                                                                
VERSX    DS    0H                                                               
         B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
*****************************************************************               
*   CLEAR REP OR STA ORD COMMENT WHEN VERSION NUM INCREMENTED   *               
*****************************************************************               
COMD     NTR1                                                                   
         TM    SKIPCOMD,X'80'                                                   
         BO    COMDX                                                            
         L     R6,ACONREC                                                       
         USING RCONSEND,R6                                                      
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   REPORSTA,C'S'                                                    
         BE    COMD10                                                           
*                                                                               
* CASE OF REP SIGNED ON - DELETE REP ORD COMMENT                                
         TM    RCONSENF,X'80'                                                   
         BZ    COMDX                                                            
         GOTO1 VHELLO,DMCB,(C'D',=C'REPFILE'),(X'82',ACONREC),0                 
         B     COMDX                                                            
*                                                                               
* CASE OF STA SIGNED ON - DELETE STA ORD COMMENT                                
COMD10   DS    0H                                                               
         TM    RCONSENF,X'40'                                                   
         BZ    COMDX                                                            
         GOTO1 VHELLO,DMCB,(C'D',=C'REPFILE'),(X'92',ACONREC),0                 
         DROP  R6                                                               
COMDX    B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* UPDATE DARE X'1D' ELEMENT TO FLAG USER HAS STARTED MANUAL CHANGES *           
* THIS IS TO SUPPORT DARE AGENCY REVISIONS. THE CONTRACT MUST HAVE  *           
* BEEN CONFIRMED AT LEAST ONCE.                                     *           
*********************************************************************           
DAREMAN  NTR1                                                                   
         L     R6,ACONREC                                                       
         USING RCONREC,R6                                                       
         CLI   RCONMOD,X'FF'       MUST BE CONFIRMED AT LEAST ONCE              
         BE    DAREX                                                            
         DROP  R6                                                               
*                                                                               
         MVI   ELCODE,X'1D'                                                     
         BAS   RE,GETEL                                                         
         BNE   DAREX                                                            
*                                                                               
         USING RCONDREL,R6                                                      
         CLI   RCONDRLN,RCONDL2Q                                                
         BL    DARE10                                                           
*                                                                               
         OI    RCONDRF2,X'04'      FLAG MANUAL CHANGES STARTED                  
         B     DAREX                                                            
*                                                                               
* DARE ELEMENT HAS OLD LENGTH, UPDATE WITH NEW SIZE                             
*                                                                               
DARE10   DS    0H                                                               
         XC    ELEM,ELEM                                                        
         ZIC   RE,RCONDRLN                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),RCONDREL    SAVE OFF OLD ELEMENT                         
         DROP  R6                                                               
*                                                                               
* ADD X'1D' ELEMENT BACK WITH NEW LENGTH                                        
*                                                                               
         LA    R6,ELEM                                                          
         USING RCONDREL,R6                                                      
         MVI   RCONDRLN,RCONDL2Q                                                
         OI    RCONDRF2,X'04'      FLAG MANUAL CHANGES STARTED                  
         DROP  R6                                                               
*                                                                               
         GOTO1 VHELLO,DMCB,(C'D',=C'REPFILE'),(X'1D',ACONREC),0                 
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),ACONREC,ELEM,=C'ADD=CODE'         
         TM    DMCB+12,X'05'       REC TOO LONG                                 
         BZ    DAREX                                                            
         LA    R3,339              RECORD FULL - CHANGE NOT PROCESSED           
         B     NO                                                               
*                                                                               
DAREX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
REPORSTA DS    C                   PROCESS (R)EP OR (S)TATION?                  
ACONREC  DS    A                                                                
VHELLO   DS    F                                                                
VDATCON  DS    F                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
SKIPCOMD DS    X                                                                
WORK     DS    CL17                FOR EDIT                                     
ELEM     DS    XL256                                                            
WORKX    EQU   *                                                                
*                                                                               
*                                                                               
RCOND    DSECT                                                                  
       ++INCLUDE RECNTWA                                                        
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'065REGENVERS 05/01/02'                                      
         END                                                                    
