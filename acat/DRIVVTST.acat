*          DATA SET DRIVVTST   AT LEVEL 045 AS OF 05/01/02                      
*CATALP DRIVVTST                                                                
         TITLE 'DRIVVTST - TESTS FOR MIN/MAX VALUES'                            
DRIVVTST CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYDEND-MYD,**DRITST                                              
         USING MYD,RC                                                           
         CLI   0(R1),X'FF'         P1 BYTE 1 X'FF'=RESET                        
         BNE   DRIVTST2            RESET FOR EACH REQUEST                       
         MVI   TESTSTAT,0                                                       
         MVI   RECNUM,0                                                         
         XC    THISIN,THISIN                                                    
         XC    THISOUT,THISOUT                                                  
         B     XIT                                                              
         SPACE 1                                                                
DRIVTST2 L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
*                                  R5=A(RECORD TO BE TESTED)                    
         CLC   RECNUM,0(R5)        IF THIS IS A NEW RECORD TYPE                 
         BE    *+8                                                              
         MVI   TESTSTAT,0          RESET TEST STATUS                            
         MVC   RECNUM,0(R5)                                                     
         CLI   TESTSTAT,1          NONE FOUND PREVIOUSLY                        
         BE    ITSFINE                                                          
         MVI   TESTSTAT,0                                                       
         BAS   RE,TESTREC                                                       
         CLI   TESTSTAT,0          NONE FOUND THIS TIME                         
         BNE   TEST2                                                            
         MVI   TESTSTAT,1                                                       
         B     ITSFINE                                                          
         SPACE 1                                                                
TEST2    CLI   TESTSTAT,3          ONE OF THE TESTS FAILED                      
         BE    NOGOOD                                                           
         B     ITSFINE                                                          
         EJECT                                                                  
*              ROUTINE CONTROLS TESTS ON RECORD                                 
         SPACE 3                                                                
*              INPUT               R5=A(RECORD TO BE TESTED)                    
         SPACE 1                                                                
TESTREC  NTR1                                                                   
         ZIC   R2,0(R5)            RECORD NUMBER                                
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         L     R3,GLAFIN                                                        
         XC    GLAIFLD,GLAIFLD                                                  
         MVI   PREVTEST,0                                                       
         DROP  R2                                                               
         SPACE 1                                                                
         USING DRIND,R3                                                         
TEST20   CLI   DRINTYPE+1,C'+'     MUST BE ADDITIVE                             
         BNE   TESTNEXT                                                         
         ST    R3,THISIN                                                        
         LH    R1,DRINDISP         INPUT FIELD                                  
         AR    R1,R5                                                            
         ST    R1,GLAIFLD          A(INPUT FIELD)                               
         B     TESTNEXT                                                         
         SPACE 1                                                                
         USING DROD,R3                                                          
*                                  IN ORDER TO QUALIFY FIELD                    
*                                  MUST HAVE AN OUTPUT ROUTINE                  
TEST30   OC    DROMNVAL,DROMNVAL   AND .....                                    
         BNZ   TEST302             MUST HAVE A MINIMUM VALUE                    
         OC    DROMXVAL,DROMXVAL          OR A MAXIMUM VALUE                    
         BZ    TEST30X                                                          
         SPACE 1                                                                
TEST302  OC    GLAIFLD,GLAIFLD     AND ALSO A PREVIOUS 'IN'                     
         BZ    TEST30X                                                          
         MVI   TESTSTAT,2          NOTE THERE IS A TEST.PRESET TO OK            
         ST    R3,THISOUT                                                       
         BAS   RE,TESTTHIS         TEST THIS EXPRESSION                         
         BNE   TESTFAIL                                                         
         SPACE 1                                                                
*                                  THIS TEST PASSED                             
         TM    GLINDS2,GLORTEST    IF TESTS ARE 'OR'                            
         BO    RECPASS                THEN WHOLE RECORD PASSED                  
         TM    GLINDS2,GLXCTEST    IF TESTS ARE 'XC'                            
         BNO   TEST30X                                                          
         CLI   PREVTEST,0             IF FIRST TEST                             
         BNE   *+12                                                             
         MVI   PREVTEST,C'P'             SET TO PASS                            
         B     TEST30X                                                          
         CLI   PREVTEST,C'F'          IF FIRST TEST FAIL                        
         BE    RECPASS                   THIS IS A PASS                         
         B     RECFAIL                ELSE BOTH PASSED SO FAIL!                 
         SPACE 1                                                                
*                                  THIS TEST FAILED                             
TESTFAIL TM    GLINDS2,GLORTEST    IF TESTS ARE 'OR'                            
         BO    TEST30X                GO AND TRY FOR ANOTHER TEST               
         TM    GLINDS2,GLXCTEST    IF TESTS ARE 'XC'                            
         BNO   RECFAIL                                                          
         CLI   PREVTEST,0             IF FIRST TEST                             
         BNE   *+12                                                             
         MVI   PREVTEST,C'F'             SET TO FAIL                            
         B     TEST30X                                                          
         CLI   PREVTEST,C'P'          IF FIRST TEST PASS                        
         BE    RECPASS                   THIS IS A PASS                         
         B     RECFAIL                ELSE BOTH FAILED SO FAIL!                 
         SPACE 1                                                                
TEST30X  XC    GLAIFLD,GLAIFLD                                                  
         SPACE 1                                                                
TESTNEXT ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'20'                                                      
         BE    TEST20                                                           
         CLI   0(R3),X'30'                                                      
         BE    TEST30                                                           
         CLI   0(R3),X'10'                                                      
         BE    XIT                                                              
         CLI   0(R3),0                                                          
         BNE   TESTNEXT                                                         
         SPACE 1                                                                
*                                  END OF TESTS                                 
         TM    GLINDS2,GLORTEST    IF TESTS ARE 'OR'                            
         BO    RECFAIL                THIS IS A FAIL                            
         TM    GLINDS2,GLXCTEST    IF TESTS ARE 'XC'                            
         BO    RECFAIL                THIS IS A FAIL                            
         DROP  R3                                                               
         SPACE 1                                                                
RECPASS  B     ITSFINE             SET THIS RECORD PASSED                       
         SPACE 1                                                                
RECFAIL  MVI   TESTSTAT,3          SET THIS RECORD FAILED                       
         B     NOGOOD                                                           
         EJECT                                                                  
*              ROUTINE TO TEST A SINGLE EXPRESSION                              
         SPACE 3                                                                
TESTTHIS NTR1                                                                   
         USING DROD,R3                                                          
         BAS   RE,GETVALUE         GETVALUE INTO DUB                            
         OC    DROMNVAL,DROMNVAL   MINIMUM VALUE TESTING                        
         BZ    THISMAX                                                          
         CP    DUB,DROMNVAL+1(7)                                                
         BNL   THISMAX                                                          
         TM    GLINDS2,GLEXCLUD    IF WE'RE TESTING FOR EXCLUDES                
         BO    ITSFINE                THIS IS A PASS                            
         CLI   DROMNVAL,1             (ALSO MEANS EXCLUDE)                      
         BE    ITSFINE                THIS IS A PASS                            
         B     NOGOOD              ELSE THIS ONE FAILED                         
         SPACE 1                                                                
THISMAX  OC    DROMXVAL,DROMXVAL   MAXIMUM VALUE TESTING                        
         BZ    THISEND                                                          
         CP    DUB,DROMXVAL+1(7)                                                
         BNH   THISEND                                                          
         TM    GLINDS2,GLEXCLUD    IF WE'RE TESTING FOR EXCLUDES                
         BO    ITSFINE                THIS IS A PASS                            
         CLI   DROMXVAL,1             (ALSO MEANS EXCLUDE)                      
         BE    ITSFINE                THIS IS A PASS                            
         B     NOGOOD              ELSE THIS ONE FAILED                         
         SPACE 1                                                                
THISEND  TM    GLINDS2,GLEXCLUD    IF WE'RE TESTING FOR EXCLUDES                
         BO    NOGOOD                 IT'S A FAIL                               
         CLI   DROMNVAL,1                                                       
         BE    NOGOOD                                                           
         CLI   DROMXVAL,1                                                       
         BE    NOGOOD                                                           
         B     ITSFINE             ELSE IT'S A PASS                             
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE TO GET THIS VALUE INTO DUB                               
         SPACE 3                                                                
*              INPUT               THISIN   A(DRIND)                            
*                                  THISOUT  A(DROUTD)                           
         SPACE 1                                                                
GETVALUE NTR1                                                                   
         L     R3,THISIN                                                        
         L     R4,GLAIFLD                                                       
         USING DRIND,R3                                                         
         ZAP   DUB,=P'0'                                                        
         ZIC   R1,DRINFLEN                                                      
         CLI   DRINTYPE,C'B'       CHECK TYPE                                   
         BE    GETBIN                                                           
         CLI   DRINTYPE,C'P'                                                    
         BE    GETPACK                                                          
         CLI   DRINTYPE,C'E'                                                    
         BE    GETEBCD                                                          
         LA    R4,1(R4)                                                         
         BCTR  R1,0                                                             
         CLI   DRINTYPE,C'M'                                                    
         BE    GETPACK                                                          
         B     XIT                                                              
         SPACE 1                                                                
GETBIN   ZIC   R1,0(R4)                                                         
         CLI   DRINFLEN,1                                                       
         BE    GETBIN2                                                          
         LH    R1,0(R4)                                                         
         CLI   DRINFLEN,2                                                       
         BE    GETBIN2                                                          
         L     R1,0(R4)                                                         
         SRA   R1,8                                                             
         CLI   DRINFLEN,3                                                       
         BE    GETBIN2                                                          
         L     R1,0(R4)                                                         
         SPACE 1                                                                
GETBIN2  CVD   R1,DUB                                                           
         B     XIT                                                              
         SPACE 1                                                                
GETPACK  BCTR  R1,0                                                             
         EX    R1,VARZAP                                                        
         B     XIT                                                              
VARZAP   ZAP   DUB,0(0,R4)                                                      
         SPACE 1                                                                
GETEBCD  BCTR  R1,0                                                             
         EX    R1,VARPACK                                                       
         B     XIT                                                              
VARPACK  PACK  DUB,0(0,R4)                                                      
         SPACE 1                                                                
THISIN   DS    A                                                                
THISOUT  DS    A                                                                
TESTSTAT DC    X'00'               CHANGED TO 1 IF NO TESTS                     
*                                  CHANGED TO 2 IF TESTS OK                     
*                                  CHANGED TO 3 IF TEST FAILS                   
RECNUM   DC    X'00'               RECORD NUMBER                                
PREVTEST DC    X'00'                                                            
         SPACE 1                                                                
ITSFINE  SR    R0,R0                                                            
         B     XIT                                                              
         SPACE 1                                                                
NOGOOD   LA    R0,1                                                             
         LTR   R0,R0                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
MYD      DSECT                                                                  
DUB      DS    D                                                                
         SPACE 1                                                                
MYDEND   EQU   *                                                                
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'045DRIVVTST  05/01/02'                                      
         END                                                                    
